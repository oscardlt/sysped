      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSL12 MODULE(*LIBL/STSL12
CRTPGM*        *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FLLMRF     IF   E           K DISK    USROPN
     FLLVARE01  UF   E           K DISK    USROPN
     FLLGRPK05  IF   E           K DISK    USROPN
     FLLAGJL98  IF   E           K DISK    usropn
     FLLAGJL07  UF   E           K DISK    usropn
     F                                     RENAME(LLAGJR:LLAGJR07)
     FLLSERL03  IF   E           K DISK    usropn
     FLLSERL05  UF   E           K DISK    usropn
     F                                     RENAME(LLSERR:LLSERR05)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     Dwssninn          s             35
     DWSUSER           s             10
     DWSLENGDE         s              2
     DWSMN             s              7
     DXWSMN            s              7  0
     DXXANT            s              5  0
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Feiltxt         s             50
     D FeilKode        s              1
     D MsgId           s              7
     D State           ds                  based(StateP)
     D  XMR                 1001   1200  0 DIM(40)
     D  XKN1                1201   1520  0 DIM(40)
     D  XKN2                1521   1840  0 DIM(40)
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
     D XTXT01          C                   CONST('Du må skanne serienr.')
     D XTXT02          C                   CONST('SKANNET SERIENR FINNES IKKE E-
     D                                     LLER ER UTE AV LAGER.')
     D XTXT03          C                   CONST('SERIENR ER SKANNET FØR.')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      ***
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * User changed?? - Set libl ..
     C                   exsr      Init
      *
     C                   SELECT
     C                   WHEN      WSSNINN = '*TILBAKE*'
     C                   EVAL      FEILKODE = *BLANKS
     C                   EVAL      FEILTXT  = *BLANKS
     C                   MOVE      XWSMN         SNMNI
     C     LLAGJL98K     CHAIN     LLAGJL98                           33
     C     1             DO        40            XN                3 0
     C                   IF        XMR(XN) = 0
     C                   MOVE      VAMRNR        XMR(XN)
     C                   MOVE      VAGRUP        XKN1(XN)
     C                   MOVE      VAKJED        XKN2(XN)
     C                   LEAVE
     C                   ELSE
     C                   IF        ((XMR(XN) = VAMRNR) AND (XKN2(XN) = VAKJED))
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
     C                   WHEN      WSSNINN = *BLANKS
      * Ingen serienr er skannet.
     C                   EVAL      FEILKODE = '1'
     C                   EVAL      FEILTXT  = XTXT01
     C                   OTHER
     C     LLSERL05K     CHAIN     LLSERL05                           33
     C                   IF        *IN33
      * Skannet serienr finnes ikke (kanskje tilhører en annen kunde).
     C                   EVAL      FEILKODE = '2'
     C                   EVAL      FEILTXT  = XTXT02
     C                   ELSE
     C                   IF        SNST = ' '
      * Skannet serienr er ikke skannet før. Oppdater status.
     C                   MOVE      'I'           SNST
     C                   ELSE
     C                   EVAL      FEILKODE = '3'
     C                   EVAL      FEILTXT  = XTXT03
     C                   END
     C                   UPDATE    LLSERR05
      * Oppdater mottaksrapportnr for å vise på PDA.
     C     LLAGJL98K     CHAIN     LLAGJL98                           33
     C     1             DO        40            XN                3 0
     C                   IF        XMR(XN) = 0
     C                   MOVE      VAMRNR        XMR(XN)
     C                   MOVE      VAGRUP        XKN1(XN)
     C                   MOVE      VAKJED        XKN2(XN)
     C                   LEAVE
     C                   ELSE
     C                   IF        ((XMR(XN) = VAMRNR) AND (XKN2(XN) = VAKJED))
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
     C                   END
     C                   ENDSL
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      *
     C     1             DO        40            XN
     C                   IF        XMR(XN) = 0
     C                   LEAVE
     C                   END
     C                   MOVE      XMR(XN)       VAMRNR
     C                   MOVE      XKN1(XN)      VAGRUP
     C                   MOVE      XKN2(XN)      VAKJED
     C     LLAGJL07K     SETLL     LLAGJL07
     C     LLAGJL07K     READE     LLAGJL07                               50
     C                   DOW       *IN50 = '0'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C                   UPDATE    LLAGJR07
     C     LLAGJL07K     READE     LLAGJL07                               50
     C  N50              ENDDO                                                  *hival
     C                   ENDDO
      *
     C                   callp     wrtsection('tableend')
      *
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   CLOSE     LLMRF
     C                   CLOSE     LLVARE01
     C                   CLOSE     LLGRPK05
     C                   CLOSE     LLAGJL98
     C                   CLOSE     LLAGJL07
     C                   CLOSE     LLSERL03
     C                   CLOSE     LLSERL05
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER   = zhbgetvar('USER')
     C                   eval      WSSNINN  = zhbgetvar('WSSNINN')
     C                   MOVE      WSSNINN       WSSN34           34
     C                   EVAL      WSSNINN  = WSSN34
     C                   eval      WSLENGDE = zhbgetvar('XLENGDE')
     C                   eval      WSMN     = zhbgetvar('VAMN')
     C                   eval      XWSMN    = c2n2(WSMN)
      *
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      StateP = RtvUsrSpcPtr(UsrSpcName:
     C                             UsrSpcLib)
      *
     C                   endsr
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
      * Repeat UserId for the next invocation
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('XLENGDE':WSLENGDE)
     C                   callp     updhtmlvar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('FEILTXT':FEILTXT)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   open      BRIDF
     C     WSUSER        CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      LLMRF
     C                   open      LLVARE01
     C                   open      LLGRPK05
     C                   open      LLAGJL98
     C                   open      LLAGJL07
     C                   open      LLSERL03
     C                   open      LLSERL05
      *
     C     LLMRFK        KLIST
     C                   KFLD                    SNGRP
     C                   KFLD                    SNPNR
     C                   KFLD                    VAMRNR
     C     LLVARE01K     KLIST
     C                   KFLD                    SNGRP
     C                   KFLD                    SNPNR
     C                   KFLD                    VAANR
     C     LLAGJL98K     KLIST
     C                   KFLD                    SNMNI
     C     LLAGJL07K     KLIST
     C                   KFLD                    VAGRUP
     C                   KFLD                    VAKJED
     C                   KFLD                    VAMRNR
     C     LLSERL05K     KLIST
     C                   KFLD                    WSSNINN
      *
     C     BIKUNR        chain     LLGRPK05                           33
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSL12'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
      *
     C                   callp     updhtmlvar('FEILKOD':FeilKode)
     C                   callp     updhtmlvar('FEILTXT':Feiltxt)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C     VAMN          SETLL     LLSERL03
     C     VAMN          READE     LLSERL03                               33
     C                   Z-ADD     0             XXANT
     C                   DOW       *IN33 = *OFF
     C                   IF        SNST <> ' '
     C                   ADD       1             XXANT
     C                   END
     C     VAMN          READE     LLSERL03                               33
     C                   ENDDO
      *
     C     LLMRFK        CHAIN     LLMRF                              33
     C                   IF        ((XXANT = VAOANT) AND (VADATO = 99999999))
     C                   MOVE      MRDT          VADATO
     C     LLVARE01K     CHAIN     LLVARE01                           33
     C                   ADD       VASALD        LVSALD
     C                   UPDATE    LLVARER
     C                   END
      *
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('VAKJED':
     C                             %trim(%editc(VAKJED:'Z')))
     C                   callp     updhtmlvar('VAMRNR':
     C                             %trim(%editc(VAMRNR:'Z')))
     C                   callp     updhtmlvar('VAANR':VAANR)
     C                   callp     updhtmlvar('VAMN':
     C                             %trim(%editc(VAMN:'Z')))
     C                   callp     updhtmlvar('VAOANT':
     C                             %trim(%editc(VAOANT:'Z')))
     C                   callp     updhtmlvar('XXANT':
     C                             %trim(%editc(XXANT:'Z')))
     C                   callp     updhtmlvar('MRLREF':MRLREF)
      *
     C                   endsr
