      *********************************************************************
      * ARBEID MED ISO TILLEGGSINFO.
      * PROGRAM BEHANDLER INPUT FRA CCOS01 OG VISER UTVALGT LINJER
      * OBS! LAG KUN MODUL AV CCOS01B IKKE PROGRAM.
      *     (INNGÅR SOM MODUL I PGM CCOS01, CCOS02)
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FCCBLV1    UF   E           K DISK    USROPN
     FCCBLV1LF  IF   E           K DISK    USROPN
     F                                     RENAME(CCBLV1R:CCBLV1R1)
     FHEADF     IF   E           K DISK    USROPN
     FCDCON4    IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
      * Variables used to load the external html source member
     D Lib             S             10
     D Fn              S             10
     D Mbr             S             10
     D WSLEDET1        S             15
     D WSLEDET2        S             15
      *
     D Spaces          S             12a   inz('&nbsp;&nbsp;')
     D FEILMELDING     S             80

     D BSAVD           S              4  0
     D BSOPD           S              7  0
     D BSLI            S              3  0
     D RecordCnt       S              5S 0
     D lstbrd          S              1
     D                 DS
     D XXREF                   1     24
     D XXAVD                   1      4
     D XXOPD                   5     11
     D XXLI                   12     14
     D XXUSER                 15     24
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      */copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Initier
     C                   EXSR      INIT
      * Set output skeleton html member name
     C                   EXSR      VISLINJE
      * Send output buffer and quit
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     CCBLV1
     C                   close     CCBLV1LF
     C                   close     HEADF
     C                   close     CDCON4
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * VIS LINJER
      *=====================================================================
     C     VISLINJE      begsr
      *
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   SELECT
     C                   WHEN      OSCHG <> 'J'
     C                   callp     wrtsection('VisCN')
     C                   when      BSLI = 0
     C                   callp     wrtsection('lagny')
     C                   callp     wrtsection('top2')
     C                   OTHER
     C                   callp     wrtsection('endre')
     C                   callp     wrtsection('top2')
     C                   ENDSL
      *
      *------------------                                                   ******
      * Find number of Recds matching selection criteria
     C                   exsr      Recdsnumb
      *------------------                                                   ******
      * Write HTML sections
      *
      * 2-Section /$howmany
     C                   exsr      SetHowMany
     C                   callp     wrtsection('howmany')
      * 2a-Section /$0found
     C     RecordCnt     ifeq      0
     C                   MOVE      *ZEROS        XXLI
     C                   MOVE      BSAVD         XXAVD
     C                   MOVE      BSOPD         XXOPD
     C                   MOVE      WSUSER        XXUSER
     C                   callp     updhtmlvar('XXREF':XXREF)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     wrtsection('0found')
     C                   goto      endhtml
     C                   endif
      * 3-Section /$tablestr
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
     C                   IF        OSCHG = 'J'
     C                   callp     wrtsection('ENDRING11')
     C                   END
     C                   callp     wrtsection('ENDRING12')
     C     CCBLV1K2      SETLL     CCBLV1LF
     C     CCBLV1K2      READE     CCBLV1LF                               50
     C     *in50         doweq     '0'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C                   IF        OSCHG = 'J'
     C                   callp     wrtsection('ENDRING21')
     C                   END
     C                   callp     wrtsection('ENDRING22')
     C     CCBLV1K2      READE     CCBLV1LF                               50
     C  N50              ENDDO                                                  *hival
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      * 5a-Section /$foundsome
     C     RecordCnt     ifgt      0
     C                   exsr      SetFndSome
     C                   callp     wrtsection('foundsome')
     C                   IF        OSCHG = 'J'
     C                   callp     wrtsection('ENDRING31')
     C                   IF        XPRTBL = 'J'
     C                   callp     wrtsection('CCTag01')
     C                   END
     C                   callp     wrtsection('CCTag02')
     C                   END
     C                   callp     wrtsection('ENDRING32')
     C                   IF        OSCHG = 'J'
     C                   callp     wrtsection('ENDRING33')
     C                   callp     wrtsection('ENDRING34')
     C                   ELSE
     C                   callp     wrtsection('ENDRING34')
     C                   END
     C                   callp     wrtsection('ENDRING35')
     C                   endif
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton TIL LINJE
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'CCOS01'
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('XXNY':'N')
      *
     C                   callp     updhtmlvar('WSAVD':
     C                             %trim(%editc(BSAVD:'Z')))
     C                   callp     updhtmlvar('WSOPD':
     C                             %trim(%editc(BSOPD:'Z')))
     C                   callp     updhtmlvar('WSLI':
     C                             %trim(%editc(BSLI:'Z')))
      *
     C                   MOVE      BSLI          XXLI
     C                   MOVE      BSAVD         XXAVD
     C                   MOVE      BSOPD         XXOPD
     C                   MOVE      BSLI          XXLI
     C                   MOVE      WSUSER        XXUSER
     C                   callp     updhtmlvar('XXREF':XXREF)
      *
     C                   callp     updHTMLvar('WSCN':WSCN)
     C     LC:UC         XLATE     WSCN          WSCN
     C                   callp     updHTMLvar('WSSL':WSSL)
     C                   callp     updHTMLvar('WSVS35':WSVS35)
     C                   callp     updHTMLvar('WSCT':WSCT)
     C                   callp     updhtmlvar('WSNT':
     C                             %trim(%editc(WSNT:'Z')))
     C                   callp     updhtmlvar('WSVKT':
     C                             %trim(%editc(WSVKT:'4')))
     C                   callp     updhtmlvar('WSM3':
     C                             %trim(%editc(WSM3:'M')))
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     updhtmlvar('FEILMELDING':FEILMELDING)
      *
     C                   endsr
      *
      *=====================================================================******
     C*  Find number of Recds matching search criteria
      *=====================================================================******
     C     Recdsnumb     begsr
     C                   eval      RecordCnt = 0
     C     CCBLV1K2      SETLL     CCBLV1LF
     C     CCBLV1K2      READE     CCBLV1LF                               50
     C     *in50         DoWeq     '0'
     C                   eval      RecordCnt = RecordCnt + 1
     C     CCBLV1K2      READE     CCBLV1LF                               50
     C                   enddo                                                  *hival
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$howmany
      *=====================================================================******
     C     SetHowMany    begsr
     C* Overall Recds count
     C     RecordCnt     ifgt      0
     C                   callp     updhtmlvar('RecordCnt':
     C                             %trim(%editw(RecordCnt:'   0 ')))
     C                   else
     C                   callp     updhtmlvar('RecordCnt':'Ingen')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
     C                   MOVE      BVLI          XXLI
     C                   MOVE      BSAVD         XXAVD
     C                   MOVE      BSOPD         XXOPD
     C                   MOVE      WSUSER        XXUSER
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('XXREF':XXREF)
     C                   callp     updhtmlvar('BVLI':
     C                             %trim(%editC(BVLI:'Z')))
     C                   IF        BVMN = *BLANKS
     C                   callp     updhtmlvar('BVMN':'__')
     C                   ELSE
     C                   callp     updhtmlvar('BVMN':BVMN)
     C                   END
     C     CCBLV1K       CHAIN     CCBLV1                             33
     C   33              MOVE      *BLANKS       BVSEAL
     C   33              MOVE      *BLANKS       BVVS35
     C                   callp     updhtmlvar('BVSEAL':BVSEAL)
     C                   IF        BVVS35 = *BLANKS AND BVLI = 1
     C     HEADFK        CHAIN     HEADF                              34
     C                   EVAL      BVVS35 = %TRIM(HEVS1) + ' ' + %TRIM(HEVS2)
     C                   END
     C                   callp     updhtmlvar('BVVS35':BVVS35)
     C  N33              UPDATE    CCBLV1R
     C                   callp     updhtmlvar('BVKDC':BVKDC)
     C                   callp     updhtmlvar('WSSORT':
     C                             %trim(%editc(BVSORT:'Z')))
     C                   callp     updhtmlvar('BVNT':
     C                             %trim(%editc(BVNT:'Z')))
     C                   callp     updhtmlvar('BVVKT':
     C                             %trim(%editc(BVVKT:'4')))
     C                   callp     updhtmlvar('BVM3':
     C                             %trim(%editc(BVM3:'M')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome    begsr
      * Repeat background color
     C                   MOVE      *ZEROS        XXLI
     C                   MOVE      BSAVD         XXAVD
     C                   MOVE      BSOPD         XXOPD
     C                   MOVE      WSUSER        XXUSER
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('XXREF':XXREF)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      *
     C                   endsr
      *=====================================================================
      *  Retrieve environment variables
      *=====================================================================
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
     C     *ENTRY        PLIST
     C                   PARM                    BSAVD
     C                   PARM                    BSOPD
     C                   PARM                    BSLI
     C                   PARM                    WSUSER           10
     C                   PARM                    UsrSpcName       10
     C                   PARM                    BIFARG           10
     C                   PARM                    WSCN             17
     C                   PARM                    WSSL             17
     C                   PARM                    WSVS35           35
     C                   PARM                    WSCT              2
     C                   PARM                    WSNT              5 0
     C                   PARM                    WSVKT             9 2
     C                   PARM                    WSM3              9 3
     C                   PARM                    FMELDNR           2
     C                   PARM                    FEILMELDING      80
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   open      CCBLV1LF                             50
     C                   open      CCBLV1                               50
     C                   open      HEADF                                50
     C                   open      CDCON4                               50
     C                   open      BRPARF                               50
     C     CCBLV1KMAX    klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C                   kfld                    XLIMAX
     C                   MOVE      999           XLIMAX            3 0
     C     CCBLV1K2      klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C     CCBLV1K       klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C                   kfld                    BVLI
     C     HEADFK        klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C     BRPARFK       klist
     C                   kfld                    WSUSER
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVEL     'Z0001'       PAID
     C     BRPARFK       Chain     BRPARF                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   MOVE      'N'           XPRTBL            1
     C                   WHEN      PAVRDA = 'J' OR PAVRDA = 'j'
     C                   MOVE      'J'           XPRTBL
     C                   OTHER
     C                   MOVE      'N'           XPRTBL
     C                   ENDSL
      * HENT RETTIGHET FOR ENDRING
     C                   MOVEL     'OSCHG'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      'N'           OSCHG
     C                   ELSE
     C                   MOVEL     PAVRDA        OSCHG             1
     C                   END
      *
     C                   endsr
      *
