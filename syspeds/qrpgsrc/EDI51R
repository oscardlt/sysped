     FEDI51D    CF   E             WORKSTN
     F                                     SFILE(EDI51DA:ZRECNO)
     FEDIJ      UF A E           K DISK
     FEDIJ1     IF   E           K DISK
     F                                     RENAME(EDIJR:EDIJR1)
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    J0004         W0004
     C     *LIKE         DEFINE    J0010         W0010
     C     *LIKE         DEFINE    JCLFE         WCLFE
     C     *LIKE         DEFINE    JLI5          WLI5
     C     *LIKE         DEFINE    *IN03         WIN03
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    W0004
     C                   KFLD                    W0010
     C                   KFLD                    WLI5
     C                   KFLD                    WCLFE
     C     KEYB          KLIST
     C                   KFLD                    W0004
     C                   KFLD                    W0010
     C                   KFLD                    WCLFE
     C                   KFLD                    WLI5
      *_________________
      * Input Parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     W0004         PARM                    W0004
     C     W0010         PARM                    W0010
     C                   PARM      *IN03         WIN03
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     EDI51DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
     C                   Z-ADD     *ZERO         WLI5
     C                   MOVE      *BLANKS       WCLFE
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EDIJ1                              9494
     C  N94W0004         READE     EDIJ1                                  94
     C  N94J0010         COMP      W0010                              9494
     C     *IN94         CABEQ     '1'           SRB4
      *
     C     SRB2          TAG
     C                   Z-ADD     WRECNO        ZRECNO
      *
     C  N94              DO        6
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     EDI51DA
     C     W0004         READE     EDIJ1                                  94
     C  N94J0010         COMP      W0010                              9494
     C  N94              END
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI51DØ
     C                   WRITE     EDI51DC
     C                   EXFMT     EDI51DB
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_______
      * ROLLUP
      *"""""""
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      SRB2
     C                   END
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C     SRB4          TAG
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     EDI51DA                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   END
     C                   END
      *
     C  N52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3
      *
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      *____________
      * Blanke felt
      *""""""""""""
     C                   MOVE      '0'           *IN41
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      *BLANKS       JCLC
     C                   MOVE      *BLANKS       JCLFE
     C                   Z-ADD     0             JLI5
      *
     C     SRBA1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI51DØ
     C                   EXFMT     EDI51DD
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
      * Post må ikke finnes fra før.
     C                   Z-ADD     ÅLI5          WLI5
     C                   MOVE      ÅCLFE         WCLFE
     C     KEYB          CHAIN     EDIJ                               53
     C     *IN53         IFEQ      '0'
     C                   MOVE      'SY00100'     ZMSGNO
     C                   MOVEA     '1110'        *IN(30)
     C                   GOTO      SRBA1
     C                   END
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   MOVE      ÅCLC          JCLC
     C                   MOVE      W0004         J0004
     C                   MOVE      W0010         J0010
     C                   MOVE      ÅCLFE         JCLFE
     C                   Z-ADD     ÅLI5          JLI5
     C                   WRITE     EDIJR
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   WRITE     EDI51DA
      *
     C                   END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *____________
      * Flytte felt
      *""""""""""""
     C                   MOVE      '1'           *IN41
     C                   MOVEA     '0000'        *IN(30)
     C                   Z-ADD     JLI5          ÅLI5
     C                   MOVE      JCLFE         ÅCLFE
     C                   MOVE      JCLC          ÅCLC
      *
     C     SRBB1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI51DØ
     C                   EXFMT     EDI51DD
     C                   MOVEA     '0000'        *IN(30)
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
     C   30              GOTO      SRBB1
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C                   Z-ADD     ÅLI5          WLI5
     C                   MOVE      ÅCLFE         WCLFE
     C     KEYB          CHAIN     EDIJ                               53
     C                   MOVE      ÅCLC          JCLC
     C                   UPDATE    EDIJR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI51DA
      *
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C                   Z-ADD     JLI5          WLI5
     C                   MOVE      JCLFE         WCLFE
     C     KEYB          CHAIN     EDIJ                               53
     C                   DELETE    EDIJR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *BLANKS       JCLC
     C                   MOVE      *BLANKS       J0004
     C                   MOVE      *BLANKS       J0010
     C                   MOVE      *BLANKS       JCLFE
     C                   Z-ADD     0             JLI5
     C                   UPDATE    EDI51DA
     C                   MOVE      '1'           *IN95
      *
     C                   ENDSR
