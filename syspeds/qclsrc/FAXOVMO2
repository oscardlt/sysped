             PGM
             DCL        VAR(&DTAQ)    TYPE(*CHAR) LEN(10) +
                          VALUE('INDUSRQ   ')
             DCL        VAR(&DTAQLIB) TYPE(*CHAR) LEN(10) +
                          VALUE('*LIBL     ')
             DCL        VAR(&LEN)     TYPE(*DEC)  LEN(5 0) VALUE(163)
             DCL        VAR(&DATA)    TYPE(*CHAR) LEN(163)
             DCL        VAR(&BPARM)    TYPE(*CHAR) LEN(35)
             DCL        VAR(&PDUM) TYPE(*CHAR) LEN(1) VALUE('E')
             DCL        VAR(&WAIT)    TYPE(*DEC)  LEN(5 0) VALUE(-1)
             DCL        VAR(&FAX)     TYPE(*LGL)
             DCL        VAR(&DISTID)  TYPE(*CHAR) LEN(20)
             DCL        VAR(&USRRUN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&BRUKER)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&UNIK)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&NAVN)    TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRPRF)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&USERID)  TYPE(*CHAR) LEN(8)
             DCL        VAR(&ADDRESS) TYPE(*CHAR) LEN(8)
             DCL        VAR(&FAXCMD)  TYPE(*CHAR) LEN(250)
             DCL        VAR(&EXT)     TYPE(*CHAR) LEN(2)
             DCL        VAR(&LDISTID) TYPE(*CHAR) LEN(20)
             DCL        VAR(&LEXT)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&PRINTER) TYPE(*CHAR) LEN(10)
             DCLF       FILE(FAXUSRS)

/* WE'LL GET UP A GLOBAL MONITOR SO THAT THIS PROGRAM WILL KEEP               */
/* GOING NO MATTER WHAT.                                                      */

             MONMSG     MSGID(CPF9999) EXEC(GOTO CMDLBL(LOSTDIST))

/* THIS PROGRAM IS THE CORE OF THE OFFICE FAX SUPPORT.  IT MUST BE            */
/* STARTED BEFORE ANYTHING WILL BE FAXED.  TO START THE PROGRAM,              */
/* DO A SBMJOB WITH THE REQUEST DATA (CALL INDUSR).  THE                      */
/* PERSON DOING THE SBMJOB MUST BE A SECURITY OFFICER THAT IS                 */
/* ENROLLED IN OFFICE.                                                        */

             RTVJOBA    USER(&USRRUN)

/* THE PROGRAM QRCVDTAQ WILL WAIT UNTIL AN ENTRY IS PLACED ON THE             */
/* DATA QUEUE.  THIS DATA QUEUE IS THE ONE DEFINED FOR THE SYSTEM             */
/* TO LET US KNOW THAT AN INDIRECT USER HAS RECEIVED MAIL.  WE WILL           */
/* ONLY GET CONTROL AGAIN WHEN THERE IS A MAIL ITEM TO PROCESS.               */
/* THE PROGRAM QDIAINDUSR RUNNING IN THE QSNADS SUBSYSTEM SHOULD BE           */
/* STOPPED.                                                                   */

 WAIT:       CALL       PGM(QRCVDTAQ) PARM(&DTAQ &DTAQLIB &LEN &DATA +
                          &WAIT)
             IF         COND(&LEN = 0) THEN(DO)
             CHGVAR     VAR(&WAIT) VALUE(-1)
             GOTO       CMDLBL(WAIT)
             ENDDO

/* OBTAIN THE USER ID/ADDRESS RECEIVING THE MAIL ITEM.  WE WILL ALSO          */
/* OBTAIN THE DISTRIBUTION ID AND EXTENSION.                                  */

             CHGVAR     VAR(&DISTID) VALUE(%SST(&DATA 1 20))
             CHGVAR     VAR(&USERID) VALUE(%SST(&DATA 21 8))
             CHGVAR     VAR(&ADDRESS) VALUE(%SST(&DATA 29 8))
             CHGVAR     VAR(&EXT) VALUE(%SST(&DATA 138 2))
             CHGVAR     VAR(&BRUKER) VALUE(%SST(&DISTID 9 8))

/* IF NO EXTENSION IS GIVEN, THEN SET THE EXTENSION TO SPECIAL                */
/* VALUE *NONE.                                                               */

             IF         COND(&EXT = ' ') THEN(CHGVAR VAR(&EXT) +
                          VALUE(*NONE))

/* GET THE USER PROFILE NAME ASSOCIATED WITH THIS USER ID/ADDRESS             */

             CALL       PGM(FAXGETUSP) PARM(&USERID &ADDRESS &USRPRF)

/* IF THIS DISTRIBUTION IS DIFFERENT FROM THE LAST ONE (WE COULD GET          */
/* THE SAME ONE IF A DISTRIBUTION WAS MAILED TO TWO INDIRECT FAX USERS),      */
/* THEN WE NEED TO RECEIVE THE DISTRIBUTION.                                  */

             IF         COND(&DISTID *NE &LDISTID *OR &EXT *NE +
                          &LEXT) THEN(DO)
/* GETS  UNIK NUMBER FOR DOCUMENT NAME   */
             CALL       PGM(FAXUNI) PARM(&UNIK)

/* WE MUST GRANT OURSELVES AUTHORITY TO RECEIVE THE DISTRIBUTION ON           */
/* BEHALF OF THE USER THAT WAS INTENDED TO RECEIVE IT.                        */
/* THIS IS WHY THIS PROGRAM MUST RUN UNDER A SECURITY OFFICER                 */
/* USER PROFILE ENROLLED IN OFFICE.                                           */

             ADDDLOAUT  DLO(&BRUKER) USRAUT((&USRRUN *ALL))
             MONMSG     MSGID(CPF0000)
             GRTUSRPMN  TOUSER(&USRRUN) FORUSER(&USRPRF &BRUKER)
             MONMSG     MSGID(CPF0000)
/* TRY TO RECEIVE THE DISTRIBUTION.  IF THE DISTRIBUTION IS A NOTE            */
/* OR DOCUMENT, IT WILL BE RECEIVED INTO THE DOCUMENT INDDOC IN               */
/* FOLDER INDFLR.  IF THE DISTRIBUTION IS A MESSAGE, THEN IT WILL BE          */
/* RECEIVED INTO THE DATA BASEFILE QTEMP/FAXTEMP.                             */

             RCVDST     DSTID(&DISTID) USRID(&USERID &ADDRESS) +
                          DOC(&UNIK) FLR(INDFLR) +
                          OUTFILE(QTEMP/FAXTEMP) OUTDTATYP(*MSG) +
                          DSTIDEXN(&EXT)
             MONMSG     MSGID(CPI9083) EXEC(GOTO CMDLBL(GOODEXT))
             CPYDOC     FROMDOC(&UNIK) FROMFLR(INDFLR) TODOC(&UNIK) +
                          TOFLR(&BRUKER)
             MONMSG     MSGID(CPF8A12) EXEC(GOTO CMDLBL(GOODEXT))
             DLTDLO     DLO(&UNIK) FLR(INDFLR)
/* REVOKE THE AUTHORITY TO WORK ON BEHALF OF THE USER.                        */

 GOODEXT:    RVKUSRPMN  FROMUSER(&USRRUN) FORUSER(&USRPRF)

/* REMEMBER THE LAST DISTRIBUTION THAT WE RECEIVED.                           */

             CHGVAR     VAR(&LDISTID) VALUE(&DISTID)
             CHGVAR     VAR(&LEXT) VALUE(&EXT)
             CHGVAR     VAR(&LEXT) VALUE(&EXT)
             ENDDO

/* CHECK IF THE USER RECEIVING THE DISTRIBUTION IS A FAX USER.                */

             CHGVAR     VAR(&FUSERI) VALUE(&USRPRF)
             CALL       PGM(FAXGRE) PARM(&FUSERI &FFAXNB)
             IF         COND(%SST(&FUSERI 1 3) = '*NF') THEN(GOTO +
                          CMDLBL(NOTFAX))

 FAX:        CHGVAR     VAR(&FAX) VALUE('1')
             CHGVAR     VAR(&PRINTER) VALUE('FAX3812   ')
             GOTO       CMDLBL(PRTDOC)

/* THIS CASE IS WHERE THE USER IS NOT A FAX USER, JUST A LOCAL USER           */
/* WHO WANTS HIS MAIL AUTOMATICALLY PRINTED.  IN THIS CASE, WE WILL           */
/* JUST PRINT THE ITEM TO THE PRINTER THAT THE USER SPECIFIED.                */

 NOTFAX:     CHGVAR     VAR(&FAX) VALUE('0')
             RTVUSRPRF  USRPRF(&USRPRF) PRTDEV(&PRINTER)
             CHKDLO     DLO(&UNIK) FLR(&BRUKER)
             MONMSG     MSGID(CPF8A82) EXEC(GOTO CMDLBL(PRT0))
             MRGDOC     FROMDOC(MSGSHL) FROMFLR(INDFLR) TODOC(&UNIK) +
                          TOFLR(&BRUKER)
 PRT0:       PRTDOC     DOC(&UNIK) FLR(&BRUKER) OPTIONS(*NO) +
                          OUTPUT(*PRINT) DEV(&PRINTER) OUTQ(*DEV)
                        GOTO LOSTDIST

/* NOW WE NEED TO PRINT THE DOCUMENT.  IF THE DISTRIBUTION WAS A              */
/* MESSAGE, THEN WE WILL MERGE THE CONTENTS OF THE MESSAGE INTO A             */
/* SHELL DOCUMENT, THEN PRINT THE SHELL DOCUMENT.  WE WILL MERGE              */
/* INTO THE SAME DOCUMENT THAT WE USED FOR THE RTVDST, SO WE DON'T            */
/* HAVE TO WORRY ABOUT WHICH DOCUMENT TO PRINT.                               */

 PRTDOC:     CHKDLO     DLO(&UNIK) FLR(&BRUKER)
             MONMSG     MSGID(CPF8A82) EXEC(GOTO CMDLBL(MRG))
             GOTO       CMDLBL(SND)
 MRG:        RTVUSRPRF  USRPRF(&BRUKER) RTNUSRPRF(&NAVN)
             MRGDOC     FROMDOC(MSGSHL) FROMFLR(INDFLR) TODOC(&UNIK) +
                          TOFLR(&BRUKER)
 SND:        CHGVAR     VAR(&BPARM) VALUE(&UNIK *CAT &BRUKER *CAT +
                          &NAVN *CAT &FUSERI *CAT &PDUM)
             SBMJOB     CMD(CALL PGM(FAX015CLJ) PARM(&BPARM)) +
                          JOB(FAX015CLJ) OUTQ(QFAX/QFAXOUTQ)

/* IF THE USER IS A FAX USER, NOW WE WILL FAX THE SPOOL FILE TO THE           */
/* USER AND LEAVE A MESSAGE IN THE JOB LOG.  IF THE USER IS NOT A             */
/* FAX USER, THEN WE ARE DONE.                                                */

 DOC:        IF         COND(&FAX) THEN(DO)
             CHGVAR     VAR(&FAXCMD) VALUE('SNDFAX to ' *CAT &FUSERI +
                          *CAT ' at number ' *CAT &FFAXNB)
             SNDPGMMSG  MSG(&FAXCMD)
             ENDDO

/* NOW GO BACK UP AND WAIT FOR THE NEXT PIECE OF MAIL.                        */

 LOSTDIST:   CHGVAR     VAR(&WAIT) VALUE(0)
             GOTO       CMDLBL(WAIT)
             ENDPGM
