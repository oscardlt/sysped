     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYSJ20PNL)
     A                                      HLPTITLE('HURTIGREGISTRERING AV OPP-
     A                                      DRAG')
     A                                      ALTHELP
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            HEOPD          7Y 0O  6  5EDTCDE(4)
     A*           HERFA         15A  O  6 13
     A            HERFA15       15A  O  6 13
     A            HEFR           3A  O  6 29
     A            HEOT           2A  O  6 33
     A            HEGN          15A  H
     A            HEPRO          8  0H
     A            XLCL           1A  H
     A            HEDTOP         8S 0H
     A            W2DT           6Y 0O  6 36EDTCDE(4)
     A            HEPOS1         7A  O  6 43
     A            HEPOS2         4S 0H
     A            W2POS2         1Y 0O  6 51EDTCDE(4)
     A            WHEGN         11A  O  6 53
     A            HELB           5A  O  6 65
     A            HEKNT          8Y 0O  6 71EDTCDE(4)
     A            HETRM          2Y 0O  7  7DSPATR(UL)
     A                                      EDTCDE(4)
     A            HETRI          2A  O  7 13DSPATR(UL)
     A            HETRC          1S 0O  7 18DSPATR(UL)
     A            HETRCN        17A  O  7 22DSPATR(UL)
     A            HENTF          4Y 0O  7 40DSPATR(UL)
     A                                      EDTCDE(4)
     A            HESDL         20A  O  7 47DSPATR(UL)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ20'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 19MSGCON(40 DIV3223 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(9 DIV0050 SYMSGF)
     A            UAVD           4Y 0O  2 12DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(15 DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3224 SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(78 DIV3226 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 YBC1923 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(003 DIV3227 *LIBL/SYMSGF)
     A            HEOPD          7Y 0O  1  6DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 14MSGCON(004 DIV3228 *LIBL/SYMSGF)
     A            WSRFA         15A  B  1 19DSPATR(UL)
     A                                  1 35MSGCON(003 DIV3491 *LIBL/SYMSGF)
     A            WSFR           3A  B  1 39DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                  1 43MSGCON(002 DIV3492 *LIBL/SYMSGF)
     A            WSOT           2A  B  1 46DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                  1 49MSGCON(006 DIV3493 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  1 56DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  1 63MSGCON(003 DIV3494 *LIBL/SYMSGF)
     A            WSPOS1         7A  B  1 67DSPATR(UL)
     A            WSPOS2         4Y 0B  1 75DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  2 10MSGCON(006 DIV3495 *LIBL/SYMSGF)
     A            WSGN          15A  B  2 17DSPATR(UL)
     A                                  2 33MSGCON(006 DIV3496 *LIBL/SYMSGF)
     A            WSLB           5A  B  2 40DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                  2 46MSGCON(005 DIV3497 *LIBL/SYMSGF)
     A            WSSDL         20A  B  2 52DSPATR(UL)
     A                                  2 73MSGCON(003 DIV3498 *LIBL/SYMSGF)
     A            WSTRM          2Y 0B  2 77DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3 14MSGCON(002 DIV3499 *LIBL/SYMSGF)
     A            WSTRI          2A  B  3 17DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                  3 20MSGCON(004 DIV3500 *LIBL/SYMSGF)
     A            WSTRC          1Y 0B  3 25DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  3 27MSGCON(006 DIV3501 *LIBL/SYMSGF)
     A            WSTRCN        17A  B  3 34DSPATR(UL)
     A                                  3 52MSGCON(005 DIV3502 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  3 58DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3 67MSGCON(005 DIV3503 *LIBL/SYMSGF)
     A            WSNTF          4Y 0B  3 73DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(27 DIV3504 SYMSGF)
     A            W2OPD          7  0I  2 30DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'BYTT AVDELINGER')
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ20'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV3223 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(9 DIV0050 SYMSGF)
     A            UAVD           4Y 0O  2 12DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(15 DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3224 SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(78 DIV3226 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 YBC1923 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 32MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
