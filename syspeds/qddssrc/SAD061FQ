     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/SADBIL SADBILR)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 58 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '4')
     A            SBPACB        15A  O  5  4
      * Videre start-pos økt med 8
     A            SBTX1C        10A  O  5 20
     A            SBTX2C        10A  O  5 31
     A            SBTX1A         6Y 1O  5 42EDTWRD('     , ')
     A            SBTX2A         6Y 2O  5 50EDTWRD('    ,  ')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD061Q'
     A                                      COLOR(BLU)
     A                                  1 10'Søking merke/modell (avgifts-mal)'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0B  1 42SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Velg option:  1 = Plukke   4= Slet-
     A                                      te'
     A                                      COLOR(BLU)
     A                                  3  2'Op Merke  Modell 1   Modell 2    V-
     A                                      ekt   Effekt'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1' '4')
     A                                      DSPATR(UL)
     A            W2MERK        15A  I  4  4DSPATR(UL)
     A            W2MOD1        10A  I  4 20DSPATR(UL)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1  2'F5=Forny    F3/F12=Avslutt'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD061Q'
     A                                      COLOR(BLU)
     A                                  1 10'Søking merke/modell (avgifts-mal)'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plukke   4= Slet-
     A                                      te'
     A                                      COLOR(BLU)
     A                                  3  2'Op Merke  Modell 1   Modell 2    V-
     A                                      ekt   Effekt'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1' '4')
     A                                      DSPATR(UL)
     A            W2MERK        15A  I  4  4DSPATR(UL)
     A            W2MOD1        10A  I  4 20DSPATR(UL)
     A                                 10  2'F5=Forny    F3/F12=Avslutt'
     A                                      COLOR(BLU)
