********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXX * 01 YBC PTF7 IKKE LOV Å ENDRE MOTTAKSDATO VED EDI-PLUKKLISTE
P7113 * 02 YBC PTF7 REFERANSEFELTER ER ØKT TIL 17A
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'søking')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL060R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1647 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  5  2MSGCON(27 YBC1648 SYMSGF)
     A            WSGRUP         8Y 0B  5 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  5 41MSGCON(35 YBC1649 SYMSGF)
     A                                  7  2MSGCON(27 YBC1650 SYMSGF)
     A            WSPNR          8Y 0B  7 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A  30                                  COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '2')
N01  A            LHOPR          1A  H
     A            LHMN           7Y 0H
     A            LHMDT          8Y 0H
S02  A*           LHPAK         10A  O  6  5
N02  A            LHPAK         17A  O  6  5
     A  51                                  DSPATR(HI)
E02  A            WHMDT          6Y 0O  6 21
     A  52                                  DSPATR(HI)
     A            LHBNA         30A  H
S02  A*           WHBNA         15A  O  6 23
N02  A            WHBNA         13A  O  6 30
E02  A            LHBPN          4Y 0O  6 44
E02  A            LHTN           8Y 0O  6 49EDTCDE(4)
E02  A            LHWKUB         7Y 4O  6 58EDTCDE(4)
     A            LHODT          8Y 0H
E02  A            WHODT          6Y 0O  6 67
     A  53                                  DSPATR(HI)
     A            LHTDT          8Y 0H
E02  A            WHTDT          6Y 0O  6 74
     A  54                                  DSPATR(HI)
     A            LHMN01        30A  H
     A            LHMN02        30A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt program')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Avslutt program')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL060R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1647 SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 44SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(08 YBC1651 SYMSGF)
     A            WSPNR          8Y 0O  2 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(40 YBC1652 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1653 SYMSGF)
     A                                      COLOR(WHT)
S02  A*           W2PAK         10A  B  5  5
N02  A            W2PAK         17A  B  5  5
E02  A            W2MDT          6Y 0B  5 23EDTCDE(4)
E02  A            W2TN           8Y 0B  5 49EDTCDE(4)
E02  A            W2ODT          6Y 0B  5 67EDTCDE(4)
E02  A            W2TDT          6Y 0B  5 74EDTCDE(4)
     A          R BUNN
     A                                      OVERLAY
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A  30                                  COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CA03(03 'Avslutt program')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Avslutt program')
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL060R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1647 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(08 YBC1651 SYMSGF)
     A            WSPNR          8Y 0O  2 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(40 YBC1652 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1653 SYMSGF)
     A                                      COLOR(WHT)
S02  A*           W2PAK         10A  B  5  5
N02  A            W2PAK         17A  B  5  5
E02  A            W2MDT          6Y 0B  5 23EDTCDE(4)
E02  A            W2TN           8Y 0B  5 49EDTCDE(4)
E02  A            W2ODT          6Y 0B  5 67EDTCDE(4)
E02  A            W2TDT          6Y 0B  5 74EDTCDE(4)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A  30                                  COLOR(WHT)
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 8 60 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF04(04 'søking')
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL060R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(30 YBC1654 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(11 YBC1655 SYMSGF)
S02  A*           LHPAK         10A  O  2 14DSPATR(UL)
N02  A            LHPAK         17A  O  2 14DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(11 YBC1656 SYMSGF)
     A            LHBNA         30A  O  3 14DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4  2MSGCON(11 YBC1657 SYMSGF)
     A            WSTN           8Y 0B  4 14DSPATR(UL)
     A  90                                  DSPATR(HI)
     A  90                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(11 YBC1658 SYMSGF)
     A            WSODT          6Y 0B  5 14DSPATR(UL)
     A  89                                  DSPATR(HI)
     A  89                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  5 22MSGCON(11 YBC1659 SYMSGF)
     A            WSTDT          6Y 0B  5 34DSPATR(UL)
     A  88                                  DSPATR(HI)
     A  88                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  5 42MSGCON(11 YBC1660 SYMSGF)
     A            WSMDT          6Y 0B  5 54DSPATR(UL)
N01  A  50                                  DSPATR(PR)
     A  87                                  DSPATR(HI)
     A  87                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(11 YBC1661 SYMSGF)
     A            WSMN01        30A  B  6 14DSPATR(UL)
     A            WSMN02        30A  B  7 14DSPATR(UL)
     A N30                              8  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A N30                              8 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSG58        58A  O  8  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
