      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI012R MODULE(*LIBL/TNOI012R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
      * I WEB varianten av SAD Import brukes kun fullskjermmodus for vareposter.
      * D.v.s. linjenr fra web er ALLTID hovedlinjenummer og så må jeg returnere
      * alle tillegslinjer som felt med repetisjoner.
      * WA1 - WA7  =  7 Repetisjoner av SVFT
      * WB1 - WB7  =  7 Repetisjoner av SVNT
      * WC1 - WC7  =  7 Repetisjoner av SVEH
      * WD1 - WD5  =  5 Repetisjoner av SVVT
      * WE1 - WE10 = 10 Repetisjoner av SVCREF
      * WF1 - WF10 = 10 Repetisjoner av SVTOA
      * WG1 - WG8  =  8 Repetisjoner av SVKDAÆ
      * WH1 - WH8  =  8 Repetisjoner av SVKDSÆ
      * WI1 - WI8  =  8 Repetisjoner av SVBLÆ
      * WJ1 - WJ8  =  8 Repetisjoner av SVBLGÆ
      * WK1 - WK8  =  8 Repetisjoner av SVBLSÆ
     FBRIDF     IF   E           K DISK    USROPN
     FSADV      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FSADV4     UF   E           K DISK    USROPN
     F                                     RENAME(SADVR:SADVR4)
     FSADH      IF   E           K DISK    USROPN
     FSADVARE   UF A E           K DISK    PREFIX(SV_)
     F                                     USROPN
     FNAVAUTK   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WN              S             15
     D SVERR           S              1
     D SVLIGH          S              1
      * Array for SVFT
     D                 DS
     D  WA1                    1     28
     D  WA2                   29     56
     D  WA3                   57     84
     D  WA4                   85    112
     D  WA5                  113    140
     D  WA6                  141    168
     D  WA7                  169    196
     D  WA                     1    280    DIM(10)
     D  XSVFT01                1     28
     D  XSVFT02               29     56
     D  XSVFT03               57     84
     D  XSVFT04               85    112
     D  XSVFT05              113    140
     D  XSVFT06              141    168
     D  XSVFT07              169    196

      * Array for SVNT
     D                 DS
     D  WB1                    1      6S 0
     D  WB2                    7     12S 0
     D  WB3                   13     18S 0
     D  WB4                   19     24S 0
     D  WB5                   25     30S 0
     D  WB6                   31     36S 0
     D  WB7                   37     42S 0
     D  WB                     1     60S 0 DIM(10)
     D  XSVNT01                1      6S 0
     D  XSVNT02                7     12S 0
     D  XSVNT03               13     18S 0
     D  XSVNT04               19     24S 0
     D  XSVNT05               25     30S 0
     D  XSVNT06               31     36S 0
     D  XSVNT07               37     42S 0

      * Array for SVEH
     D                 DS
     D  WC1                    1      4
     D  WC2                    5      8
     D  WC3                    9     12
     D  WC4                   13     16
     D  WC5                   17     20
     D  WC6                   21     24
     D  WC7                   25     28
     D  WC                     1     40    DIM(10)
     D  XSVEH01                1      4
     D  XSVEH02                5      8
     D  XSVEH03                9     12
     D  XSVEH04               13     16
     D  XSVEH05               17     20
     D  XSVEH06               21     24
     D  XSVEH07               25     28

      * Array for SVVT
     D                 DS
     D  WD1                    1     30
     D  WD2                   31     60
     D  WD3                   61     90
     D  WD4                   91    120
     D  WD5                  121    150
     D  WD                     1    300    DIM(10)
     D  XSVVT01                1     30
     D  XSVVT02               31     60
     D  XSVVT03               61     90
     D  XSVVT04               91    120
     D  XSVVT05              121    150

      * Array for SVCREF
     D                 DS
     D  WE1                    1      3
     D  WE2                    4      6
     D  WE3                    7      9
     D  WE4                   10     12
     D  WE5                   13     15
     D  WE6                   16     18
     D  WE7                   19     21
     D  WE8                   22     24
     D  WE9                   25     27
     D  WE10                  28     30
     D  WE                     1     30    DIM(10)
     D  XSVCRE1                1      3
     D  XSVCRE2                4      6
     D  XSVCRE3                7      9
     D  XSVCRE4               10     12
     D  XSVCRE5               13     15
     D  XSVCRE6               16     18
     D  XSVCRE7               19     21
     D  XSVCRE8               22     24
     D  XSVCRE9               25     27
     D  XSVCRE10              28     30

      * Array for SVTOA
     D                 DS
     D  WF1                    1     17
     D  WF2                   18     34
     D  WF3                   35     51
     D  WF4                   52     68
     D  WF5                   69     85
     D  WF6                   86    102
     D  WF7                  103    119
     D  WF8                  120    136
     D  WF9                  137    153
     D  WF10                 154    170
     D  WF                     1    170    DIM(10)
     D  XSVTOP1                1     17
     D  XSVTOP2               18     34
     D  XSVTOP3               35     51
     D  XSVTOP4               52     68
     D  XSVTOP5               69     85
     D  XSVTOP6               86    102
     D  XSVTOP7              103    119
     D  XSVTOP8              120    136
     D  XSVTOP9              137    153
     D  XSVTOP10             154    170

      * Array for SVKDAÆ
     D                 DS
     D  WG1                    1      2
     D  WG2                    3      4
     D  WG3                    5      6
     D  WG4                    7      8
     D  WG5                    9     10
     D  WG6                   11     12
     D  WG7                   13     14
     D  WG8                   15     16
     D  WG                     1     20    DIM(10)
     D  XSVAKD1                1      2
     D  XSVAKD2                3      4
     D  XSVAKD3                5      6
     D  XSVAKD4                7      8
     D  XSVAKD5                9     10
     D  XSVAKD6               11     12
     D  XSVAKD7               13     14
     D  XSVAKD8               15     16

      * Array for SVKDSÆ
     D                 DS
     D  WH1                    1      3
     D  WH2                    4      6
     D  WH3                    7      9
     D  WH4                   10     12
     D  WH5                   13     15
     D  WH6                   16     18
     D  WH7                   19     21
     D  WH8                   22     24
     D  WH                     1     30    DIM(10)
     D  XSVASV1                1      3
     D  XSVASV2                4      6
     D  XSVASV3                7      9
     D  XSVASV4               10     12
     D  XSVASV5               13     15
     D  XSVASV6               16     18
     D  XSVASV7               19     21
     D  XSVASV8               22     24

      * Array for SVBLÆ
     D                 DS
     D  WI1                    1     11S 2
     D  WI2                   12     22S 2
     D  WI3                   23     33S 2
     D  WI4                   34     44S 2
     D  WI5                   45     55S 2
     D  WI6                   56     66S 2
     D  WI7                   67     77S 2
     D  WI8                   78     88S 2
     D  WI                     1    110S 2 DIM(10)
     D  XSVABL1                1     11S 2
     D  XSVABL2               12     22S 2
     D  XSVABL3               23     33S 2
     D  XSVABL4               34     44S 2
     D  XSVABL5               45     55S 2
     D  XSVABL6               56     66S 2
     D  XSVABL7               67     77S 2
     D  XSVABL8               78     88S 2

      * Array for SVBLGÆ
     D                 DS
     D  WJ1                    1     11S 2
     D  WJ2                   12     22S 2
     D  WJ3                   23     33S 2
     D  WJ4                   34     44S 2
     D  WJ5                   45     55S 2
     D  WJ6                   56     66S 2
     D  WJ7                   67     77S 2
     D  WJ8                   78     88S 2
     D  WJ                     1    110S 2 DIM(10)
     D  XSVAGR1                1     11S 2
     D  XSVAGR2               12     22S 2
     D  XSVAGR3               23     33S 2
     D  XSVAGR4               34     44S 2
     D  XSVAGR5               45     55S 2
     D  XSVAGR6               56     66S 2
     D  XSVAGR7               67     77S 2
     D  XSVAGR8               78     88S 2

      * Array for SVBLSÆ
     D                 DS
     D  WK1                    1      7S 2
     D  WK2                    8     14S 2
     D  WK3                   15     21S 2
     D  WK4                   22     28S 2
     D  WK5                   29     35S 2
     D  WK6                   36     42S 2
     D  WK7                   43     49S 2
     D  WK8                   50     56S 2
     D  WK                     1     70S 2 DIM(10)
     D  XSVASA1                1      7S 2
     D  XSVASA2                8     14S 2
     D  XSVASA3               15     21S 2
     D  XSVASA4               22     28S 2
     D  XSVASA5               29     35S 2
     D  XSVASA6               36     42S 2
     D  XSVASA7               43     49S 2
     D  XSVASA8               50     56S 2

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SVAVD:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SVTDN:'Z'))+'¬, '
        +'¬lin¬ : ¬'+%trim(%editc(SVLI:'Z'))+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"lineupdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SADV
     C  N50              CLOSE     SADV4
     C  N50              CLOSE     SADH
     C  N50              CLOSE     SADVARE
     C  N50              CLOSE     NAVAUTK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
     C                   MOVEA     *ZEROS        WB
     C                   MOVEA     *ZEROS        WI
     C                   MOVEA     *ZEROS        WJ
     C                   MOVEA     *ZEROS        WK
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WN         = zhbgetvar('AVD')
     C                   EVAL      SVAVD      = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('OPD')
     C                   EVAL      SVTDN      = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('LIN')
     C                   EVAL      SVLI       = c2n2(WN)
     C                   EVAL      SVLIORG    = c2n2(WN)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      SV01  = zhbgetvar('SV01')
     C                   EVAL      SVLK  = zhbgetvar('SVLK')
     C                   EVAL      WN         = zhbgetvar('SVVNT')
     C                   EVAL      SVVNT = c2n2(WN)
     C                   EVAL      SVTN  = zhbgetvar('SVTN')
     C                   EVAL      SVPRE = zhbgetvar('SVPRE')
     C                   EVAL      WN         = zhbgetvar('SVAS')
     C                   EVAL      SVAS  = c2n2(WN)
     C                   EVAL      SVPVA = zhbgetvar('SVPVA')
     C                   EVAL      SVMFR = zhbgetvar('SVMFR')
     C                   EVAL      SVVF  = zhbgetvar('SVVF')
     C                   EVAL      WN         = zhbgetvar('SVVKTB')
     C                   EVAL      SVVKTB= c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('SVNTM')
     C                   EVAL      SVNTM = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('SVBELT')
     C                   EVAL      SVBELT= c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('SVBELS')
     C                   EVAL      SVBELS= c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('SVVKTN')
     C                   EVAL      SVVKTN= c2n2(WN)
     C                   EVAL      SVERR      = zhbgetvar('SVERR')
     C                   EVAL      SVLIGH  = zhbgetvar('SVLIGH')
     C                   EVAL      WA1   = zhbgetvar('WA1')
     C                   EVAL      WA2   = zhbgetvar('WA2')
     C                   EVAL      WA3   = zhbgetvar('WA3')
     C                   EVAL      WA4   = zhbgetvar('WA4')
     C                   EVAL      WA5   = zhbgetvar('WA5')
     C                   EVAL      WA6   = zhbgetvar('WA6')
     C                   EVAL      WA7   = zhbgetvar('WA7')
     C                   EVAL      WN         = zhbgetvar('WB1')
     C                   EVAL      WB1   = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WB2')
     C                   EVAL      WB2   = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WB3')
     C                   EVAL      WB3   = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WB4')
     C                   EVAL      WB4   = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WB5')
     C                   EVAL      WB5   = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WB6')
     C                   EVAL      WB6   = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WB7')
     C                   EVAL      WB7   = c2n2(WN)
     C                   EVAL      WC1   = zhbgetvar('WC1')
     C                   EVAL      WC2   = zhbgetvar('WC2')
     C                   EVAL      WC3   = zhbgetvar('WC3')
     C                   EVAL      WC4   = zhbgetvar('WC4')
     C                   EVAL      WC5   = zhbgetvar('WC5')
     C                   EVAL      WC6   = zhbgetvar('WC6')
     C                   EVAL      WC7   = zhbgetvar('WC7')
     C                   EVAL      WD1   = zhbgetvar('WD1')
     C                   EVAL      WD2   = zhbgetvar('WD2')
     C                   EVAL      WD3   = zhbgetvar('WD3')
     C                   EVAL      WD4   = zhbgetvar('WD4')
     C                   EVAL      WD5   = zhbgetvar('WD5')
     C                   EVAL      WE1   = zhbgetvar('WE1')
     C                   EVAL      WE2   = zhbgetvar('WE2')
     C                   EVAL      WE3   = zhbgetvar('WE3')
     C                   EVAL      WE4   = zhbgetvar('WE4')
     C                   EVAL      WE5   = zhbgetvar('WE5')
     C                   EVAL      WE6   = zhbgetvar('WE6')
     C                   EVAL      WE7   = zhbgetvar('WE7')
     C                   EVAL      WE8   = zhbgetvar('WE8')
     C                   EVAL      WE9   = zhbgetvar('WE9')
     C                   EVAL      WE10  = zhbgetvar('WE10')
     C                   EVAL      WF1   = zhbgetvar('WF1')
     C                   EVAL      WF2   = zhbgetvar('WF2')
     C                   EVAL      WF3   = zhbgetvar('WF3')
     C                   EVAL      WF4   = zhbgetvar('WF4')
     C                   EVAL      WF5   = zhbgetvar('WF5')
     C                   EVAL      WF6   = zhbgetvar('WF6')
     C                   EVAL      WF7   = zhbgetvar('WF7')
     C                   EVAL      WF8   = zhbgetvar('WF8')
     C                   EVAL      WF9   = zhbgetvar('WF9')
     C                   EVAL      WF10  = zhbgetvar('WF10')
     C                   EVAL      SVTOB = zhbgetvar('SVTOB')
     C                   EVAL      WG1   = zhbgetvar('WG1')
     C                   EVAL      WG2   = zhbgetvar('WG2')
     C                   EVAL      WG3   = zhbgetvar('WG3')
     C                   EVAL      WG4   = zhbgetvar('WG4')
     C                   EVAL      WG5   = zhbgetvar('WG5')
     C                   EVAL      WG6   = zhbgetvar('WG6')
     C                   EVAL      WG7   = zhbgetvar('WG7')
     C                   EVAL      WG8   = zhbgetvar('WG8')
     C                   EVAL      WH1    = zhbgetvar('WH1')
     C                   EVAL      WH2    = zhbgetvar('WH2')
     C                   EVAL      WH3    = zhbgetvar('WH3')
     C                   EVAL      WH4    = zhbgetvar('WH4')
     C                   EVAL      WH5    = zhbgetvar('WH5')
     C                   EVAL      WH6    = zhbgetvar('WH6')
     C                   EVAL      WH7    = zhbgetvar('WH7')
     C                   EVAL      WH8    = zhbgetvar('WH8')
     C                   EVAL      WN         = zhbgetvar('WI1')
     C                   EVAL      WI1    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WI2')
     C                   EVAL      WI2    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WI3')
     C                   EVAL      WI3    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WI4')
     C                   EVAL      WI4    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WI5')
     C                   EVAL      WI5    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WI6')
     C                   EVAL      WI6    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WI7')
     C                   EVAL      WI7    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WI8')
     C                   EVAL      WI8    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WJ1')
     C                   EVAL      WJ1    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WJ2')
     C                   EVAL      WJ2    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WJ3')
     C                   EVAL      WJ3    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WJ4')
     C                   EVAL      WJ4    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WJ5')
     C                   EVAL      WJ5    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WJ6')
     C                   EVAL      WJ6    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WJ7')
     C                   EVAL      WJ7    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WJ8')
     C                   EVAL      WJ8    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WK1')
     C                   EVAL      WK1    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WK2')
     C                   EVAL      WK2    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WK3')
     C                   EVAL      WK3    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WK4')
     C                   EVAL      WK4    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WK5')
     C                   EVAL      WK5    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WK6')
     C                   EVAL      WK6    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WK7')
     C                   EVAL      WK7    = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('WK8')
     C                   EVAL      WK8    = c2n2(WN)
     C                   EVAL      SVEH21 = zhbgetvar('SVEH2')
     C                   EVAL      SVPREÆ = zhbgetvar('SVPREÆ')
     C                   EVAL      SVEH2Æ = zhbgetvar('SVEH2Æ')
     C                   EVAL      SVEXR01= zhbgetvar('SVEXR01')
     C                   EVAL      SVEXR02= zhbgetvar('SVEXR02')
     C                   EVAL      SVEXR03= zhbgetvar('SVEXR03')
     C                   EVAL      WN         = zhbgetvar('SVEXR04')
     C                   EVAL      SVEXR04= c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('SVEXR05')
     C                   EVAL      SVEXR05= c2n2(WN)
     C                   EVAL      SVEXR06= zhbgetvar('SVEXR06')
     C                   EVAL      WN         = zhbgetvar('SVEXR07')
     C                   EVAL      SVEXR07= c2n2(WN)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SADVKEY       KLIST
     C                   KFLD                    SVAVD
     C                   KFLD                    SVTDN
     C                   KFLD                    SVLI
     C     SADVKEY2      KLIST
     C                   KFLD                    SVAVD
     C                   KFLD                    SVTDN
     C                   KFLD                    WSVLI
     C     SADVKEY4      klist
     C                   kfld                    SVAVD
     C                   kfld                    SVTDN
     C                   kfld                    SVLN
     C     SADHKEY       KLIST
     C                   KFLD                    SVAVD
     C                   KFLD                    SVTDN
     C     SADVAREK      KLIST
     C                   KFLD                    NAVKUNA
     C                   KFLD                    SV_VARENR
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SADV
     C  N50              OPEN      SADV4
     C  N50              OPEN      SADH
     C  N50              OPEN      SADVARE
     C  N50              OPEN      NAVAUTK
      * Dummy chain for feltbeskrivelse
     C   50*IN50         IFEQ      *OFF
     C                   READ      SADV4
     C                   ENDIF
     C     *LIKE         DEFINE    SVLI          WSVLI2
     C     *LIKE         DEFINE    SVLI          WSVLI
     C     *LIKE         DEFINE    SVLN          WSVLN
     C     *LIKE         DEFINE    SVLI          SVLIORG
     C                   EVAL      WSVLI=99999
     C                   Z-ADD     0             WX                2 0
     C                   Z-ADD     0             WY                2 0
     C                   IF        *IN50=*OFF
     C     SADHKEY       CHAIN     SADH
     C                   ENDIF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     WMODE         IFNE      'A'
     C     SADVKEY       CHAIN     SADV                               55
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   EXSR      SRA
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
      * Finn høyeste linjenr
     C     SADVKEY2      SETLL     SADV
     C     SADHKEY       READPE    SADV                                   56
     C                   EVAL      WSVLI=XSVLI
     C                   ADD       1             WSVLI
     C                   CLEAR                   SADVR
     C                   EVAL      XSVAVD=SVAVD
     C                   EVAL      XSVTDN=SVTDN
     C                   EVAL      XSVLI=WSVLI
     C                   EVAL      XSVLN=WSVLI
     C                   EVAL      SVLI=WSVLI
     C                   EVAL      XSVREFL=*ZEROS
     C                   WRITE     SADVR
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   UPDATE    SADVR
     C                   EVAL      SVLN=XSVLN
     C     SADVKEY4      SETLL     SADV4
     C     SADVKEY4      READE     SADV4                                  56
     C     *IN56         DOWEQ     *OFF
     C                   DELETE    SADVR4
     C     SADVKEY4      READE     SADV4                                  56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XSVPRE=SVPRE
     C                   EVAL      XSVPVA=SVPVA
     C                   IF        XSVPVA=*BLANKS
     C                   IF        SVAS=*ZEROS
     C*                  EVAL      XSVPVA='F'
     C                   ENDIF
     C                   ENDIF
     C                   EVAL      XSVVKTN=SVVKTN
     C                   EVAL      XSVREFL=*ZEROS

     C                   IF        SVLIGH=*BLANKS
     C                   EVAL      XSV01=SV01
     C                   EVAL      XSVLK=SVLK
     C                   EVAL      XSVVNT=SVVNT
     C                   EVAL      XSVTN=SVTN
     C                   EVAL      XSVAS=SVAS
     C                   EVAL      XSVMFR=SVMFR
     C                   EVAL      XSVVF=SVVF
     C                   EVAL      XSVVKTB=SVVKTB
     C                   EVAL      XSVNTM=SVNTM
     C                   EVAL      XSVBELT=SVBELT
     C                   EVAL      XSVBELS=SVBELS
     C                   EVAL      XSVEH21=SVEH21
     C                   EVAL      XSVPREÆ=SVPREÆ
     C                   EVAL      XSVEH2Æ=SVEH2Æ
     C                   EVAL      XSVERR=SVERR
     C*                  EVAL      XSVEXR01=SVEXR01
     C*                  EVAL      XSVEXR02=SVEXR02
     C*                  EVAL      XSVEXR03=SVEXR03
     C*                  EVAL      XSVEXR04=SVEXR04
     C*                  EVAL      XSVEXR05=SVEXR05
     C*                  EVAL      XSVEXR06=SVEXR06
     C*                  EVAL      XSVEXR07=SVEXR07
     C                   ENDIF
     C                   UPDATE    SADVR
      * Skal kunders varergister oppdateres ?
     C                   IF        XSVEXR03<>*BLANKS
     C     SIKNK         CHAIN     NAVAUTK                            34
     C     SIKNS         IFNE      0
     C   34SIKNS         CHAIN     NAVAUTK                            34
     C                   ENDIF

     C                   IF        *IN34 = *OFF
     C                   EVAL      SV_VARENR = XSVEXR03
     C     NAVLKA        IFEQ      'J'
     C                   MOVE      XSVLK         SV_VARENR
     C                   ENDIF
     C     SADVAREK      CHAIN     SADVARE                            34

     C                   IF        *IN34 = *OFF
     C                   EVAL      SV_W2LK   = XSVLK
     C                   MOVE      XSVVNT        SV_W2VNTI
     C                   EVAL      SV_W2AKD1 = XSVAKD1
     C                   EVAL      SV_W2ASV1 = XSVASV1
     C                   UPDATE    SADVARER

     C                   ELSE

     C                   CLEAR                   SADVARER

     C                   EVAL      SV_VARENR = XSVEXR03
     C                   EVAL      SV_VAREBE = XSVVT01
     C                   EVAL      SV_LEVENR = NAVKUNA
     C                   EVAL      SV_W2VF   = XSVVF
     C                   EVAL      SV_W2LK   = XSVLK
     C                   MOVE      XSVVNT        SV_W2VNTI
     C                   EVAL      SV_W2TN   = XSVTN
     C                   EVAL      SV_W2PRE  = XSVPRE
     C                   EVAL      SV_W2PVA  = XSVPVA
     C                   EVAL      SV_W2MFR  = XSVMFR
     C                   EVAL      SV_W2AKD1 = XSVAKD1
     C                   EVAL      SV_W2ASV1 = XSVASV1
     C                   EVAL      SV_W2FT01 = XSVFT01
     C                   EVAL      SV_W2EH01 = XSVEH01
     C                   EVAL      SV_W2VT01 = XSVVT01
     C                   EVAL      SV_W2TOP1 = XSVTOP1
     C                   EVAL      SV_W2CRE1 = XSVCRE1

     C                   WRITE     SADVARER

     C                   ENDIF

     C                   ENDIF

     C                   ENDIF
     C                   ENDSR
