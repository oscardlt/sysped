*****q**************************************************************
      * Sjekke om det er farlig gods som ikke kan samlastes
******* KUN HOVEDREGEL :
      * Det er IKKE tillatt å samlaste Eksplosiver=Faresedler 1, 1.4  (unntatt 1.4S), 1.5 og 1.6
      *                                med IKKE eksplosiver  (= andre faresedler ...)
      *+Det er ikke lov samlaste gods som har både  4.1 og 1 (i fareseddel) med annet gods
      *+Det er ikke lov samlaste gods som har både  5.2 og 1 (i fareseddel) med annet gods
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF11 Et oppdrag kan være unntatt for ADRsjekk
10XXX * 02 JOV PTF11 endret håndtering av 1.4S
********************************************************************
     FHEAD11    IF   E           K DISK
     FDOKUFVF   IF   E           K DISK
      *
     c     *ENTRY        PLIST
     c                   parm                    Uavd              4 0
     c                   parm                    UOpd              7 0
     c                   parm                    UPRO              8 0
     c                   parm                    usvar             1
     c                   move      '0'           usvar
     c     HeaKey        KLIST
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
      *
      * kun nødvendig å teste dersom oppdraget har ADR
     c                   move      Uavd          HEAVD
     c                   move      UOpd          HEOPD
     C     Heakey        chain     DOKUFVF                            35
      * kun nødvendig å teste dersom oppdrag har ADR
     c     *in35         cabeq     '1'           Slutt
     c                   movel     FFKLAS        OpdKlasse         4
     c                   movel     FFSEDD        OpdSeddel        10
      * kun nødvendig å teste dersom Turen har ADR
     c                   exsr      GetAdrT
     c     *in35         cabeq     '1'           Slutt
     c                   movel     FFKLAS        TurKlasse         4
     c                   movel     FFSEDD        TurSeddel        10
     c                   exsr      TstAdr
      *
     c     Slutt         tag
     c                   seton                                        lr
     c                   return
     C***********************************************
     c     GetAdrT       BEGSR
     C***********************************************
     c* finnes det på turen minst ett oppdrag med farlig gods
      * (første/beste - har det restriksjon skal resten ha samme / IKKE ->resten samme)
     c                   move      *blanks       FFKLAS
     c                   move      *blanks       FFSEDD
     C     UPRO          SETLL     HEAD11
     C     UPRO          READE     HEAD11                                 35
     C     *IN35         doweq     '0'
N01  C     HESTN8        IFNE      'X'
     C     Heakey        chain     DOKUFVF                            35
     c     *in35         ifeq      '0'
     c                   leave
     c                   endif
N01  c                   endif
     C     UPRO          READE     HEAD11                                 35
     C                   ENDDO
      * 35 ON = leste gjennom hele turen (evt tom tur) uten å finne Fralig gods
     C                   ENDSR
     C*
     C***********************************************
     c     TstAdr        BEGSR
     C***********************************************
      * 1. Hovedregel:
      * Det er IKKE tillatt å samlaste Eksplosiver=Faresedler 1, 1.4  (unntatt 1.4S), 1.5 og 1.6
      *                                med IKKE eksplosiver
      * (Slik det ser ut i ADR-tabellene er alle hvir fareklasse begynner på 1..(og unntatt 1.4S)
      *  i de aktuelle fareseddel-gruppene, derfor tiltrekkelig å teste mot klasse= 1... )
      * (mange har klass=blank men seddel<>blank (det betyr jo et de definitivt IKKE er ekplosiver))
      *
N02   * 1.4S er i særstilling - kan blandes med det meste, men ikke 4.1 ( 1 ) / 5.2 ( 1 )
N02  c                   if        OpdKlasse='1.4S' or TurKlasse='1.4S'
N02  c                   goto      Regel2
N02  c                   endif
      * Oppdraget Eksplosjonsfare ??
     c                   If        %subst(OpdKlasse:1:1)='1'
S02  c*                            and OpdKlasse<>'1.4S'                        (1.4S= ikke ...)
     c                   move      'E'           OpdEksplo         1            Eksplosiv
     c                   else
     c                   move      'I'           OpdEksplo                      IKKE Eksplosiv
     C                   endif
      * Turen     Eksplosjonsfare ??
     c                   If        %subst(TurKlasse:1:1)='1'
     c                             and TurKlasse<>'1.4S'                        (1.4S= ikke ...)
     c                   move      'E'           TurEksplo         1            Eksplosiv
     c                   else
     c                   move      'I'           TurEksplo                      IKKE Eksplosiv
     C                   endif
      *
      * Dersom oppdrag/tur ulike m.h.t eksplosjonsfare så kan de ikke blandes
     C                   if        OpdEksplo <> TurEksplo
     C                   move      '1'           Usvar
     c                   goto      UtTst
     C                   endif
      *
      * 2. Hovedregel:
      * Det er ikke lov samlaste gods som har både  4.1 og 1 (i fareseddel) med annet gods
N02  C     Regel2        tag
     C                   if        OpdSeddel = '4.1 ( 1 )' and
     C                             TurSeddel <>'4.1 ( 1 )'
     C                   move      '2'           Usvar
     c                   goto      UtTst
     C                   endif
     C                   if        OpdSeddel <>'4.1 ( 1 )' and
     C                             TurSeddel = '4.1 ( 1 )'
     C                   move      '2'           Usvar
     c                   goto      UtTst
     C                   endif
      * 3. Hovedregel:
      * Det er ikke lov samlaste gods som har både  5.2 og 1 (i fareseddel) med annet gods
     C                   if        OpdSeddel = '5.2 ( 1 )' and
     C                             TurSeddel <>'5.2 ( 1 )'
     C                   move      '3'           Usvar
     c                   goto      UtTst
     C                   endif
     C                   if        OpdSeddel <>'5.2 ( 1 )' and
     C                             TurSeddel = '5.2 ( 1 )'
     C                   move      '3'           Usvar
     c                   goto      UtTst
     C                   endif
      *
     C     UtTst         ENDSR
