********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10244 * 01 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A
      *              MYE ENDRING. INGEN MERKING
********************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 85 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            VARENR        28A  O  5  4DSPATR(HI)
     A            VAREBE        50A  O  5 33
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD001B'
     A                                      COLOR(BLU)
     A                                  1 11MSGCON(030 SHU0803 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 34MSGCON(006 SHU0815 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSKUND         8Y 0B  2 41EDTCDE(4)
     A                                  2 51'Frisøk J/N'
     A                                      COLOR(WHT)
     A            FRISOK         1   B  2 63
     A                                  3  1MSGCON(040 SHU0799 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 46SFLRCDNBR
     A                                      DSPATR(ND)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2VANR        28A  I  4  4DSPATR(UL)
     A            W2VABE        50A  I  4 33DSPATR(UL)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD001B'
     A                                      COLOR(BLU)
     A N30                              1  2MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              1 10MSGCON(020 YBC0235 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 10MSGCON(030 SHU0803 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 34MSGCON(006 SHU0815 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSKUND         8Y 0B  2 41EDTCDE(4)
     A                                  2 51'Frisøk J/N'
     A                                      COLOR(WHT)
     A            FRISOK         1   B  2 63
     A                                  3  1MSGCON(030 SHU0799 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2VANR        28A  I  4  4DSPATR(UL)
     A            W2VABE        50A  I  4 33DSPATR(UL)
