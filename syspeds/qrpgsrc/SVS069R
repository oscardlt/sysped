      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED NY FORSEGLINGS-ID          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FTRIHFS    UF A E           K DISK
     FTRKODL01  IF   E           K DISK
     FTR069D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UTDFKD        IFNE      *BLANKS
     C     UNDFKD        ORNE      *BLANKS
     C                   MOVE      UNDFKD        NIDFKD
     C                   MOVE      UNDFSK        NIDFSK
     C                   MOVE      UTDFKD        TIDFKD
     C                   MOVE      UTDFSK        TIDFSK
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     TRHFSK        SETLL     TRIHFS
     C     TRHFSK        READE(N)  TRIHFS                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     TRHFSK        READE(N)  TRIHFS                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C   03
     COR 12              GOTO      SLUTT
      *
     C   04              DO
     C                   MOVEL     W2DFSK        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         W2DFSK
     C                   GOTO      START3
     C                   END
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C  N27W2NR          IFEQ      '1'
     C                   EXSR      CRTFS
     C                   GOTO      START3
     C                   END
      *
     C  N27ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     TRHFSK        SETLL     TRIHFS
     C     TRHFSK        READE     TRIHFS                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    TRIHFSR
     C     TRHFSK        READE     TRIHFS                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UTDFKD
     C                   MOVE      *BLANKS       UTDFSK
     C                   MOVE      *BLANKS       UNDFKD
     C                   MOVE      *BLANKS       UNDFSK
     C                   MOVE      *ZEROS        XANTL             3 0
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     NIDFKD        IFNE      '*DELETE'
     C                   MOVE      NIDFKD        UNDFKD
     C                   MOVE      NIDFSK        UNDFSK
     C                   MOVE      TIDFKD        UTDFKD
     C                   MOVE      TIDFSK        UTDFSK
     C                   ADD       1             XANTL
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     NIDFKD        IFNE      '*DELETE'
     C     XANTL         IFEQ      0
     C                   MOVE      TIDFKD        UTDFKD
     C                   MOVE      TIDFSK        UTDFSK
     C                   MOVE      NIDFKD        UNDFKD
     C                   MOVE      NIDFSK        UNDFSK
     C                   ADD       1             XANTL
     C                   ELSE
     C                   ADD       1             XANTL
     C                   WRITE     TRIHFSR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     NIDFKD        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGFS
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     NIDFKD        COMP      '*DELETE'                              98
     C  N98TIDFKD        COMP      *BLANKS                            9898
     C   98              GOTO      SLHENT
     C                   EXSR      DLTFS
     C                   END
     C  N98WSNR          IFEQ      '9'
     C     NIDFKD        COMP      '*DELETE'                              98
     C  N98TIDFKD        COMP      *BLANKS                                98
     C   98              GOTO      SLHENT
     C                   EXSR      ANGFS
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTFS         BEGSR
     C***********************************************
     C     W2DFKD        IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'YBC0821'     MSGNR
     C                   GOTO      SLCRT
     C                   END
      *
     C     W2DFSK        IFNE      *BLANKS
     C                   MOVEL     W2DFSK        UKODE
     C     TRK01K        CHAIN     TRKODL01                           89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'YBC0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLCRT
     C                   END
     C                   END
      *
     C                   MOVE      W2DFKD        NIDFKD
     C                   MOVE      W2DFSK        NIDFSK
     C                   MOVE      *BLANKS       TIDFKD
     C                   MOVE      *BLANKS       TIDFSK
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2DFKD
     C                   MOVE      *BLANKS       W2DFSK
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGFS         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      NIDFKD        WSDFKD
     C                   MOVE      NIDFSK        WSDFSK
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   MOVEL     W2DFSK        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         W2DFSK
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C     WSDFKD        IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'YBC0821'     MSGNR
     C                   GOTO      STCHG
     C                   END
      *
     C     WSDFSK        IFNE      *BLANKS
     C                   MOVEL     WSDFSK        UKODE
     C     TRK01K        CHAIN     TRKODL01                           89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'YBC0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
     C                   END
      *
     C                   MOVE      WSDFKD        NIDFKD
     C                   MOVE      WSDFSK        NIDFSK
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTFS         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       NIDFKD
     C                   MOVEL     '*DELETE'     NIDFKD
     C                   MOVE      *BLANKS       NIDFSK
     C                   MOVE      *BLANKS       TIDFKD
     C                   MOVE      *BLANKS       TIDFSK
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     ANGFS         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
      * KUN SLETTE NY LINJE
     C                   MOVE      TIDFKD        NIDFKD
     C                   MOVE      TIDFSK        NIDFSK
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TRHFSK        KLIST
     C                   KFLD                    TIAVD
     C                   KFLD                    TITDN
     C     TRK01K        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    UKODE
      *
     C     *ENTRY        PLIST
     C                   PARM                    TIAVD
     C                   PARM                    TITDN
     C                   PARM                    UTDFKD           20
     C                   PARM                    UTDFSK            2
     C                   PARM                    UNDFKD           20
     C                   PARM                    UNDFSK            2
     C                   PARM                    UTYPE             3
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      '012'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C     UTYPE         COMP      'DSP'                                  27
      *
     C                   ENDSR
      *
