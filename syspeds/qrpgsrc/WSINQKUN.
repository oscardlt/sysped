      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/WSINQKUN MODULE(*LIBL/WSINQKUN *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FCUNDL02   IF   E           K DISK    USROPN
     F                                     RENAME(KUNDR:KUNDR2)
     FVADR      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WSONAVN         S             10
     D WKNR            S              8
     D WKNRN           S              8  0
     D STOPP           S             10
     D string          s          32000
     D Error           S            150
     D AntLest         S              9  0
     D ZB              S              1    DIM(10)
      * Prototype
     D XMLesc          PR                  ExtPgm('XMLESCAPE')
     D   XMLStr                   32000A

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      *
      /free
       // Innparametre
        WSUSER  = zhbgetvar('USER');
        WSONAVN = zhbgetvar('SONAVN');
        STOPP = %trim(WSONAVN)+'9999999999';
        WKNR = zhbgetvar('KNR');
        WKNRN = c2n2(WKNR);
       // Open files etc
        EXSR INIT;

       // hent html-mal
        gethtmlifs('/syspeddev/html/STDLIN.html':'/CGITAG/');
       // skriv toppen
        wrtsection('top');

        string=
        '<?xml version="1.0" encoding="ISO-8859-1" standalone="yes"?>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');

        string=
        '<GetCustomerResponse>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');

        string=
            '<CustomerList>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');
      /end-free
      *
      *
      * Hente kund/kunder
      *
     c     Error         ifeq      *blanks
     C     KEY1          SETLL     CUNDL01
     C     KEY2          SETLL     CUNDL02
     C                   IF        WKNRN > 0
     C     KEY1          READE     CUNDL01                                55
     C                   ELSE
     C     BIFIRM        READE     CUNDL02                                55
N01  C  N55SONAVN        CABGT     STOPP         UTLES
     C                   ENDIF
     C     *IN55         DOWEQ     *OFF
     C                   Add       1             AntLest
     c                   move      *blanks       PONR              9
     c                   if        SYPOGE <> *blanks
     c                   movel     SYPOGE        PONR
     c                   else
     c                   if        POSTNR <> *zeros
     c                   movel     POSTNR        PONR
     c                   endif
     c                   endif
      * hent evt vadr
     c     VadrKey       chain     VADR                               33
     c                   if        *in33='1'
     C                   move      *blanks       XVNAVN           30
     C                   move      *blanks       XVADR1           30
     C                   move      *blanks       XVADR2           30
     c                   move      *blanks       XVPONR            9
     c                   move      *blanks       XVSTED           30
     c                   else
     C                   MOVE      VADRNA        XVNAVN
     C                   MOVE      VADRN1        XVADR1
     C                   MOVE      VADRN2        XVADR2
     c                   move      *blanks       XVPONR
     c                   move      *blanks       XVSTED
     C     ' '           SCAN      VADRN3:4      Y                 3 0    33
     c                   if        Y>4 and Y<10
     c                   eval      XVPONR = %subst(VADRN3:1:Y)
     c                   eval      XVSTED = %subst(VADRN3:y+1:30-y)
     c                   else
     c                   eval      XVSTED = VADRN3
     c                   endif
     c
     c                   endif
      /free
        string=
                '<Customer>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');

        string=
                    '<Number>'   +%trim(%editc(KUNDNR:'Z'))      +'</Number>'
              +     '<ActiveCde>'+    AKTKOD                     +'</ActiveCde>'
              +     '<Name>'     +%trim(XMLesc(KNAVN))           +'</Name>'
              +     '<Address1>' +%trim(ADR1)                    +'</Address1>'
              +     '<Address2>' +%trim(ADR2)                    +'</Address2>'
              +     '<PostalNo>' +%trim(PONR)                    +'</PostalNo>'
              +     '<Place>'    +%trim(ADR3)                    +'</Place>'
              +     '<Country>'  +%trim(SYLAND)                  +'</Country>'
              +     '<Contact>'  +%trim(KPERS)                   +'</Contact>'
              +     '<Telephone>'+%trim(TLF)                     +'</Telephone>'
              +     '<SearchName>'+%trim(SONAVN)                +'</SearchName>'
              +     '<DName>'    +%trim(XVNAVN)                  +'</DName>'
              +     '<DAddress1>'+%trim(XVADR1)                  +'</DAddress1>'
              +     '<DAddress2>'+%trim(XVADR2)                  +'</DAddress2>'
              +     '<DPostalNo>'+%trim(XVPONR)                  +'</DPostalNo>'
              +     '<DPlace>'   +%trim(XVSTED)                  +'</DPlace>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');

        string=
                '</Customer>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');
      /end-free
      *
     C                   IF        WKNRN > 0
     C     KEY1          READE     CUNDL01                                55
     C                   ELSE
     C     BIFIRM        READE     CUNDL02                                55
N01  C  N55SONAVN        CABGT     STOPP         UTLES
     C                   ENDIF
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      *
      *
      /free
        string=
            '</CustomerList>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');

        string=
        '</GetCustomerResponse>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');
      /end-free
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     CUNDL01
     C  N50              CLOSE     CUNDL02
     C  N50              CLOSE     VADR

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSONAVN = zhbgetvar('SONAVN')
     c                   eval      STOPP = %trim(WSONAVN)+'9999999999'
     C                   EVAL      WKNR = zhbgetvar('KNR')
     C                   EVAL      WKNRN = c2n2(WKNR)
     C
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     KEY1          KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WKNRN
     C     KEY2          KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WSONAVN
     C     VADRKEY       KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C                   KFLD                    VADRNR
     C                   Z-ADD     1             VADRNR
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      CUNDL01
     C  N50              OPEN      CUNDL02
     C  N50              OPEN      VADR

     c                   endsr
      *
