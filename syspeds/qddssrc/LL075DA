     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 65 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            VARENR        15A  O  4  2
     A            VARTE1        30A  O  4 18
     A            LEVERT        11Y 3O  4 49EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 40SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/LL075RA'
     A                                      COLOR(BLU)
     A                                  1 19'Vis pakkseddel'
     A                                      COLOR(WHT)
     A                                  2  2'Pakksdl:'
     A            XPSDN          7Y 0O  2 11COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 22'År:'
     A            XAAR           4Y 0O  2 26COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'Artikkelnr      Beskrivels        -
     A                                                   Antall stykk'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A                                      CF12(12 'Gå til bilde02')
     A                                      SLNO(12)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3/F12=Avbryt   F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF12(12 'Gå til bilde02')
     A                                  1  2'SYSPED/LL075RA'
     A                                      COLOR(BLU)
     A                                  1 19'Vis pakkseddel'
     A                                      COLOR(WHT)
     A                                  2  2'Pakksdl:'
     A            XPSDN          7Y 0O  2 11COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 22'År:'
     A            XAAR           4Y 0O  2 26COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'Artikkelnr      Beskrivels        -
     A                                                   Antall stykk'
     A                                      COLOR(WHT)
     A                                 12  2'F3/F12=Avbryt'
     A                                      COLOR(BLU)
