********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 05 TH  PTF8 NY HELPFUNKSJON  KUN DDS
P80   * 06 TH  PTF9 VARNING / OVERSTYR DUPLIKAT KODE
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S05  A*                                     HLPDOC('SYFA3401' 'SYFA34' 'SY400LO-
S05  A*                                     /MAINT')
N05  A                                      HLPPNLGRP(HLPSCR SYFA34PNL)
N05  A                                      HLPTITLE('VEDLIKEHOLD AV STEDSKODER-
N05  A                                      ')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            ST2SOI         3S 0H
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A            ST2KOD         5A  O  5  8TEXT('STEDSKODE')
     A                                      COLOR(WHT)
     A            ST2NVN        30A  O  5 18TEXT('STEDSNAVN')
     A            ST2LK          2A  O  5 49TEXT('LANDKODE')
     A            ST2SOL         3A  O  5 52
     A            ST2PNR         5S 0O  5 56
     A            ST2SON         1Y 0O  5 62TEXT('SONE')
     A                                      EDTCDE(4)
     A            ST2UNI         9   O  5 65COLOR(WHT)
     A            ST2KO2         5A  O  5 75TEXT('stedskode 2')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA34'
     A                                      COLOR(BLU)
     A            MSGF01        30A  O  1 19MSGID(DIV 1269 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 53DATE
     A                                      EDTCDE(Y)
     A                                  1 63TIME
     A            MSGF03        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF04        30A  O  3 34MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV1270 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGB01        18A  O  1  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB02        18A  O  1 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB03        18A  O  1 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB04        18A  O  1 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF23(23 'OVERSTYR DUPLIKAT')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  6MSGCON(075 DIV1271 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSKOD          5A  B  2  9TEXT('STEDSKODE')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WSNVN         30A  B  2 18TEXT('STEDSNAVN')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSLK           2A  B  2 50TEXT('LANDKODE')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSSOL2         3A  B  2 54
     A            WSPNR          5Y 0B  2 58EDTCDE(4)
     A            WSSON          1Y 0B  2 65TEXT('SONE')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A            WSKOD2         5A  B  2 67TEXT('Landkode 2')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A            WSSONI         3Y 0B  2 75EDTCDE(4)
     A            MSG102        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22'Zip-code:'
     A            WSUNI          9   B  3 33
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG201        25A  O  2  2MSGID(DIV 1040 SYMSGF)
     A            W2LK           2A  I  2 30DSPATR(PC)
     A            W2NVN         30A  I  2 36DSPATR(UL)
     A            MSG202        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            W2SORT         1Y 0B  3 65EDTCDE(4)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3 30MSGCON(030 SHU0403 *LIBL/SYMSGF)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG301        75A  O  1  6MSGID(DIV 1271 SYMSGF)
     A                                      DSPATR(HI)
     A            ST2KOD         5A  O  2  9TEXT('STEDSKODE')
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            ST2NVN        30A  O  2 18TEXT('STEDSNAVN')
     A                                      DSPATR(HI)
     A            ST2LK          2A  O  2 50TEXT('LANDKODE')
     A                                      DSPATR(HI)
     A            ST2SOL         3A  O  2 54COLOR(WHT)
     A            ST2SON         1Y 0O  2 62TEXT('SONE')
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            ST2KO2         5A  O  2 67DSPATR(HI)
     A            ST2SOI         3Y 0O  2 75EDTCDE(4)
     A                                      DSPATR(HI)
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA34'
     A                                      COLOR(BLU)
     A            MSGF01        30A  O  1 19MSGID(DIV 1269 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 53DATE
     A                                      EDTCDE(Y)
     A                                  1 66TIME
     A            MSGF03        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF04        30A  O  3 34MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF05        78A  O  4  2MSGID(DIV 1270 SYMSGF)
     A                                      DSPATR(HI)
     A            MSGF07        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF08        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF09        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGF10        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
