OBS!  * Siden basis-prog kjører på aspweb.systema.no  så er kundens ip foreløpig hardkodet.
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK15 MODULE(*LIBL/ESTK15 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 store omskrivinger / testet på lat/lon avd mm - ingen merking
10XXX * 02 JOV PTF10 Lat/lon ligger i HEADF - ikke behov for egen fil
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FDISPL04   IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     F*HEBRELE   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D WSSAVD          S              4
     D WBSAVD          S              4  0
     D XAVD            S              4  0
     D sAv             S              4  0 DIM(99)
      *
     D CallBack        s           1000
     D Title           s            500
      *
     D Lat             s             15
     D Lon             s             15
     D Post1           s              1
     D LinTxt          s           5000
     D SeOpd           s            500
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  AvdOpd                 1     11
     D   Avd                   1      4
     D   Opd                   5     11
      *
      * Definere  hex appostrof
     D Ap              c                   x'7D'
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user = zhbgetvar('user')
     C                   eval      WSSAVD   = zhbgetvar('departmentNr')
     C                   EVAL      WBSAVD   = c2n2(WSSAVD)
     C                   eval      CallBack = zhbgetvar('CallBack')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     DISPL04
     C                   CLOSE     HEADF
S02  C*                  CLOSE     HEBRELE
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESTK11G':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
     C                   callp     wrtsection('top')
      *
     C                   move      'J'           Post1
      * cross domain oppkall ??
     c     CallBack      ifne      *blanks
     c                   eval      LinTxt=%trim(CallBack)+'('
     C                   callp     updHTMLvar('LINTXT':Lintxt)
     C                   callp     wrtsection('lin')
     C                   end
      * LES ÅPNE OPPDRAG PÅ EN AVDELING
     C     nextAvd       tag
     c     Disp04Key     setll     DISPL04
     c     Disp04Key     reade     DISPL04                                33
     c     *in33         doweq     '0'
     c     HeaKey        chain     HEADF
      * Filter
     c                   exsr      TstData
     c     SkippOppd     cabeq     'Y'           NextOpd

      * Oppdrags som skal vises er valgt...
     C     Post1         Ifeq      'J'
     C                   eval      Lintxt = '{"points": [ '
     C                   move      'N'           Post1
     C                   else
     c                   eval      Lintxt = ','                                 komma mellom hver
     C                   end
      * bearbeide/innhente visse felt før output
     c                   exsr      FixData
      /free
       // Ap=Hexavedi for single Appostrof for å kunne bruke inne i tekstkonst.(mellom '       ')

       // Definer url i hjelpevariabel som skal inn i Infobobla
        SeOpd  = '<a href='+Ap+'#'+Ap+' OnClick=\"window.open('+Ap
       //OBS !!  +'../../sycgip/syso02.pgm?user='+%trim(USER)+'&UsrSpcName=InT5WXz4BJ'
        +'http://gw.systema.no'
        +'/sycgip/syso02.pgm?user='+%trim(USER)+'&UsrSpcName=InT5WXz4BJ'
        +'&heavd='+%trim(%editc(DIAVD:'Z'))+'&heopd='+%trim(%editc(DIOPD:'Z'))
        +Ap+','+Ap+Ap+','+Ap+'width=1245,height=800,left=15,top=30,'
        +'scrollbars,resizable'+Ap+')\">Se Oppdrag</A>';

        //Title=Hoover-tekst:
          Title='Fra: '+%trim(%subst(HENAS:1:12))+', '
                       +%trim(%subst(HEADS1:1:12))+', '
                       +%trim(%subst(HEADS3:1:12))+' \n'
               +'Til: '+%trim(%subst(HENAK:1:12))+', '
                       +%trim(%subst(HEADK1:1:12))+', '
                       +%trim(%subst(HEADK3:1:12))+' \n'
               +'Kll: '+%trim(%editc(HENT:'Z'))
                       +'  Vkt: '+%trim(%editc(HEVKT:'Z'))+' kg'
                       +'  M3: '+%trim(%editc(HEM3:'M'))
                       +'  LM: '+%trim(%editc(HELM:'M'));
        //Definer ETT punkt / en boble
        LinTxt = %trim(LinTxt)+'{'
        +' "timestamp": "' + %trim(%editw(TRSDFD:'    .  .  ')) + '",'
        +' "address":{"address": "'+ %trim(HEADS1)+' '+ %trim(HEADS3)+'",'
                   +'"contrycode": "'+LK+'"},'
        +' "pos":{"lat": "'+%trim(LAT)+'", "lng": "'+%trim(LON)+'"},'
        +' "collies":"'+%trim(%editc(HENT:'3'))+'",'
        +' "weight":"'+%trim(%editc(HEVKT:'3'))+'",'
        +' "M3":"'+%trim(%editc(HEM3:'3'))+'",'
        +' "LM":"'+%trim(%editc(HELM:'3'))+'",'
        +' "icon": {"url":"images/parcel.gif", '
                   +'"offset": { "X":"0", "Y":"15"},'
                   +'"labeloffset": { "X":"0", "Y":"0"},'
                   +'"title": "'+%trim(Title)+'", '
                   +'"size":{"width":"15", "height":"15"}},'
        +' "info":"'
        +'<h1>'+%trim(%editc(DIAVD:'Z'))+'/'+%trim(%editc(DIOPD:'Z'))+'</h1>'
        +'Hentes: '+ %trim(HENAS)+'<br>'
        +'Dto/tid: '+   %trim(%editw(TRSDFD:'    .  .  '))  +' '
        + %trim(%editw(TRSDFK:'  :  :  ')) +'<br>'
         +%trim(SeOpd) +'" ,'
         +' "unit": "'+%trim(AvdOpd)+'", '
         +' "type":"parcel", '
         +' "group":""'
         +'}';
      /end-free
     C                   callp     updHTMLvar('LINTXT':Lintxt)
     C                   callp     wrtsection('lin')
     c     NextOpd       tag
     c     Disp04Key     reade     DISPL04                                33
     c                   enddo
      * flere avdelinger valgt?
     c     WBSAVD        ifeq      *zeros
     c                   add       1             aNr
     C                   eval      XAVD = sAv(aNr)
     c     XAVD          cabne     *zeros        NextAvd
     c                   endif
     C     Post1         ifeq      'N'                                          minst en skrevet
     C                   eval      LinTxt = ']}'
     C                   callp     updHTMLvar('LINTXT':Lintxt)
     C                   callp     wrtsection('lin')
     c                   endif
      * cross domain oppkall ??
     c     CallBack      ifne      *blanks
     c                   eval      LinTxt = ')'
     C                   callp     updHTMLvar('LINTXT':Lintxt)
     C                   callp     wrtsection('lin')
     C                   end
     C
     C                   endsr
      *

     C***************************************************************
     C     TSTDATA       begsr
     C***************************************************************
     c                   Move      'Y'           SkippOppd         1
S02  C**                 move      *zeros        TRHBRE
S02  C**                 move      *zeros        TRHLEN
     c                   eval      Lat=*blanks
     c                   eval      Lon=*blanks
s02  c*    HeaKey        chain     HEBRELE
e02  c     HEHBRE        ifne      *zeros
e02  c     HEHLEN        andne     *zeros
e02  c     HEHBRE        iflt      *zeros
e02  c                   eval      Lat='-' + %triml(%editw(HEHBRE:'  .      '))
     c                   else
e02  c                   eval      Lat='+' + %triml(%editw(HEHBRE:'  .      '))
     c                   endif
e02  c     HEHBRE        iflt      *zeros
e02  c                   eval      Lon='-' + %triml(%editw(HEHLEN:'   .      '))
     c                   else
e02  c                   eval      Lon='+' + %triml(%editw(HEHLEN:'   .      '))
     c                   endif
     c                   endif
      * landkode foreløpig hardkodet
     c                   movel     '.no'         LK                3
      *
     C     Lat           cabeq     *blanks       UtTst
     C     Lon           cabeq     *blanks       UtTst
      *
      * Kommet gjennom alle tester - OK - ikke skipp
     c                   Move      'N'           SkippOppd         1
     C     UtTst         endsr
     C
     C***************************************************************
     C     FIXDATA       begsr
     C***************************************************************
      * hent Lat/Lon flyttet til TSTdata  (uten merking)
     c                   move      DIAVD         AVD
     c                   move      DIOPD         OPD
     C
     C                   endsr
     C
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * Ugyldig userID
     c     *in50         ifeq      '1'
     C                   callp     gethtml('HTMLSRC':'*LIBL':'ERRUSR':
     c                             '/CGITAG/')
     C                   callp     updhtmlvar('User':USER)
     C                   callp     wrtsection('all')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   eval      *inlr = *on
     C                   return
     c                   endif
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      BRPARF
     C                   OPEN      DISPL04
     C                   OPEN      HEADF
s02  C*                  OPEN      HEBRELE
      *
     C     Disp04Key     klist
     C                   kfld                    XXST              1
     C                   kfld                    NullTur           8 0
     C                   kfld                    XAVD
     C     HeaKey        klist
     C                   kfld                    DIAVD
     C                   kfld                    DIOPD
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVE      'LTAVD'       PAID
      *
      * Klienten tillater nå kall kun dersom USER har definer minst en avdeling i LTAVD
      * oppdrag for EN avdeling
     C                   IF        WBSAVD <> 0
     C                   eval      XAVD = WBSAVD
     C                   else
      * 0 i avd=vis alle (for aktuell user) - legg i array for selectAvd
     c                   move      *zeros        aNr               2 0
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 33
     c     *in33         doweq     '0'
     c                   add       1             aNr
     C                   Z-ADD     PAVRDN        sAv(aNr)
     C     BrParKey      reade     BRPARF                                 33
     c                   enddo
      * start med første avd ...
     c                   z-add     1             aNr
     C                   eval      XAVD = sAv(aNr)
     C                   endif
     C     UtInit        endsr
