     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/PAPA)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            PPSADT    R        H
     A            PPAVRE    R        H
     A            PPALFA    R        H
     A            WSNR           1A  B  6  2VALUES(' ' '5' '6' '7' '8')
     A            PPNR      R        O  6  4EDTCDE(4)
     A            PPKUNR    R        O  6 13EDTCDE(4)
     A            PPNAVN    R        O  6 22
     A            PPKATE    R        O  6 53REFFLD(PAPAR/PPKATE *LIBL/PAPA)
     A            PPSPDT    R        H      REFFLD(PAPAR/PPSPDT *LIBL/PAPA)
     A            WPSPDT         6Y 0O  6 55EDTWRD('  .  .  ')
     A            PPSALP    R        O  6 64EDTCDE(L)
     A            PPSALT    R        O  6 73EDTCDE(L)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      OVERLAY
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'SØK')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF16(16 'DET.LIST ALLE (I UTVALG)')
     A                                      CF17(17 'PURRE ALLE (I UTVALG)')
     A                                      CF18(18 'AVR.OPPD ALLE (I UTVALG)')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYPA02'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 JOV0645 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(078 JOV0959 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(079 JOV0960 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2PPNR    R        B  5  4REFFLD(PAPAR/PPNR *LIBL/PAPA)
     A                                      EDTCDE(4)
     A            W2KUNR    R        B  5 13REFFLD(PAPAR/PPKUNR *LIBL/PAPA)
     A                                      EDTCDE(4)
     A            W2ALFA         1A  B  5 22VALUES(' ' 'X')
     A            W2KATE    R        B  5 53REFFLD(PAPAR/PPKATE *LIBL/PAPA)
     A            W2SPPM         1A  B  5 70VALUES(' ' '+' '-' '0')
     A            W2STPM         1A  B  5 79VALUES(' ' '+' '-' '0')
      *
     A          R BUNN
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(078 JOV0961 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98N30                           4  2MSGCON(079 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                  1  2'SYSPED/SYPA04'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 JOV0645 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(078 JOV0959 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(079 JOV0960 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2PPNR    R        B  5  4REFFLD(PAPAR/PPNR *LIBL/PAPA)
     A                                      EDTCDE(4)
     A            W2KUNR    R        B  5 13REFFLD(PAPAR/PPKUNR *LIBL/PAPA)
     A                                      EDTCDE(4)
     A            W2ALFA         1A  B  5 22VALUES(' ' 'X')
     A            W2KATE    R        B  5 53REFFLD(PAPAR/PPKATE *LIBL/PAPA)
     A            W2SPPM         1A  B  5 70VALUES(' ' '+' '-' '0')
     A            W2STPM         1A  B  5 79VALUES(' ' '+' '-' '0')
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 42MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(078 JOV0961 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VALG6
     A                                      WINDOW(*DFT 12 70)
     A                                      CF12(12 'Returner uten oppdatering')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                  1  2'SYSPED/SYPA02'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 JOV0977 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(065 JOV0985 *LIBL/SYMSGF)
     A                                  4  3MSGCON(018 JOV0978 *LIBL/SYMSGF)
     A            WSFDAT         6Y 0B  4 24
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6  3MSGCON(018 JOV0647 *LIBL/SYMSGF)
     A            WSDATO         6Y 0B  6 24
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8  3MSGCON(065 JOV0979 *LIBL/SYMSGF)
     A                                  9  3MSGCON(065 JOV0980 *LIBL/SYMSGF)
     A                                 10  3MSGCON(065 JOV0981 *LIBL/SYMSGF)
     A                                 11  2MSGCON(012 BBA0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VALG7
     A                                      WINDOW(*DFT 12 70)
     A                                      CF12(12 'Returner uten oppdatering')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                  1  2'SYSPED/SYPA02'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 JOV0982 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(065 JOV0984 *LIBL/SYMSGF)
     A                                  6  3MSGCON(018 JOV0647 *LIBL/SYMSGF)
     A            WSDATO         6Y 0B  6 24
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8  3MSGCON(065 JOV0979 *LIBL/SYMSGF)
     A                                  9  3MSGCON(065 JOV0983 *LIBL/SYMSGF)
     A                                 11  2MSGCON(012 BBA0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VALG8
     A                                      WINDOW(*DFT 12 70)
     A                                      CF12(12 'Returner uten oppdatering')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                  1  2'SYSPED/SYPA02'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 JOV0986 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(065 JOV0984 *LIBL/SYMSGF)
     A                                  6  3MSGCON(018 JOV0647 *LIBL/SYMSGF)
     A            WSDATO         6Y 0B  6 24
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8  3MSGCON(065 JOV0979 *LIBL/SYMSGF)
     A                                  9  3MSGCON(065 JOV0987 *LIBL/SYMSGF)
     A                                 10  3MSGCON(065 JOV0988 *LIBL/SYMSGF)
     A                                 11  2MSGCON(012 BBA0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R ASSUME
     A                                      ASSUME
     A                                  1  4' '
