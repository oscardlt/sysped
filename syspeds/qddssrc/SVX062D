     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SVX062PNL)
     A                                      HLPTITLE('REGISTRERING AV HENDELSER-
     A                                       TET IMPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5')
     A            TVLI           5Y 0O  4  5EDTCDE(4)
     A            TVST          35A  O  4 11
     A            TVCTL          1  0O  4 47
     A            TVFLG          1  0O  4 49
     A            TVTAID        27A  O  4 51
     A            TV01           1   H
     A            TVSTSK         2A  H
     A            TVSTLK         2A  H
     A            TVINF1        70A  H
     A            TVINF2        70A  H
     A            TVINF3        70A  H
     A            TVINF4        70A  H
     A            TVINSK         2A  H
     A            TVGDT          8S 0H
     A            TVGM          35A  H
     A            TVGMSK         2A  H
     A            TVGMST        35A  H
     A            TVGMSS         2A  H
     A            TVGMLK         2A  H
     A            TVDANT         3S 0H
     A            TVDFKD        20A  H
     A            TVDFSK         2A  H
     A            TVTASK         2A  H
     A            TVTALK         2A  H
     A            TVGODT         8S 0H
     A            TVOM          35A  H
     A            TVOMSK         2A  H
     A            TVOMST        35A  H
     A            TVOMSS         2A  H
     A            TVOMLK         2A  H
     A            TVCNR         17A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SVX062R'
     A                                      COLOR(BLU)
     A                                  1 18'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 40'Ærende '
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Vælj option, 2=Ændra 4=Radera 5=Vi-
     A                                      sa'
     A                                      COLOR(BLU)
     A  27                              2  2'Vælj option, 5=Visa'
     A                                      COLOR(BLU)
     A                                  3  2'Op Linje Hændelseplats            -
     A                                                 K F Omlasting           -
     A                                             '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Send til TET')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til bilde02')
     A N27                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              4  2'F3=Avsluta   F5=Uppdatera   F6=Ska-
     A                                      pa ny   F7=Visa uppdrag   F8=Hoppa -
     A                                      till'
     A                                      COLOR(BLU)
     A  27                              4  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A N27                              5  2'F12=Avbryt   F17=Kopiera detta ære-
     A                                      nde till nytt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CF12(12 'Forrige')
     A          H                           HLPPNLGRP(SVX062006A SVX062PNL)
     A                                      HLPARA(04 001 04 046)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(04 052 04 077)
     A          H                           HLPPNLGRP(SVX062006B SVX062PNL)
     A                                      HLPARA(05 001 05 044)
     A          H                           HLPPNLGRP(SVX062006 SVX062PNL)
     A                                      HLPARA(06 001 06 042)
     A          H                           HLPPNLGRP(SVX062007 SVX062PNL)
     A                                      HLPARA(07 001 10 080)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(11 001 11 013)
     A          H                           HLPPNLGRP(SVX062008 SVX062PNL)
     A                                      HLPARA(12 001 12 017)
     A          H                           HLPPNLGRP(SVX062010 SVX062PNL)
     A                                      HLPARA(12 020 12 064)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(12 067 12 080)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(13 001 13 013)
     A          H                           HLPPNLGRP(SVX062011 SVX062PNL)
     A                                      HLPARA(13 020 13 064)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(13 067 13 080)
     A          H                           HLPPNLGRP(SVX062012 SVX062PNL)
     A                                      HLPARA(15 001 15 014)
     A          H                           HLPPNLGRP(SVX062013 SVX062PNL)
     A                                      HLPARA(15 020 15 049)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(15 062 15 073)
     A          H                           HLPPNLGRP(SVX062014 SVX062PNL)
     A                                      HLPARA(17 002 17 038)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(17 042 17 053)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(17 062 17 073)
     A          H                           HLPPNLGRP(SVX062015 SVX062PNL)
     A                                      HLPARA(18 002 18 017)
     A          H                           HLPPNLGRP(SVX062016 SVX062PNL)
     A                                      HLPARA(18 020 18 064)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(18 067 18 078)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(19 001 19 013)
     A          H                           HLPPNLGRP(SVX062017 SVX062PNL)
     A                                      HLPARA(19 020 19 064)
     A          H                           HLPPNLGRP(SVX062009 SVX062PNL)
     A                                      HLPARA(19 067 19 078)
     A          H                           HLPPNLGRP(SVX062018 SVX062PNL)
     A                                      HLPARA(20 002 20 022)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSLI           5S 0H
     A                                  1  2'SYSPED/SVX062R'
     A                                      COLOR(BLU)
     A                                  1 17'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 40'Ærende '
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'HÆNDELSE UNDER VÆGEN:'
     A                                      COLOR(WHT)
     A                                  4  2'Plats   :'
     A            WSST          35A  B  4 12TEXT('Sted')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 52'Landkod: '
     A            WSSTLK         2A  B  4 62TEXT('Landkode')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 66'Språkkod:'
     A            WSSTSK         2A  B  4 76TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5  2'Kontroll:'
     A            WSCTL          1Y 0B  5 12TEXT('Kontroll')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1)
     A                                  5 16'(Redan i NCTS ?  0=Nej, 1=Ja)'
     A                                  7  2'Hændelse:'
     A                                      COLOR(WHT)
     A                                  7 22'Språkkod:'
     A            WSINSK         2A  B  7 32TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSINF1        70A  B  8 10TEXT('Informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSINF2        70A  B  9 10TEXT('Informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSINF3        70A  B 10 10TEXT('Informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSINF4        70A  B 11 10TEXT('Informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'Godk.DT.:'
     A            WSGDT          8Y 0B 12 12TEXT('Godkjenningsdato')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 21'Myndighe:'
     A            WSGM          35A  B 12 31TEXT('Godkj.myndighet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 67'Språkkod:'
     A            WSGMSK         2A  B 12 77TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'Godk.LK.:'
     A            WSGMLK         2A  B 13 12TEXT('Godkjenningsland')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13 20'Godk.pl.:'
     A            WSGMST        35A  B 13 30TEXT('Godkjenningssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 67'Språkkod:'
     A            WSGMSS         2A  B 13 77TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14  2'Plombering:'
     A                                      COLOR(WHT)
     A                                 15  2'Antal   :'
     A            WSDANT         3Y 0B 15 12TEXT('Antall forsigling')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                 15 20'Kod     :'
     A            WSDFKD        20A  B 15 30TEXT('Forseglingskode')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 15 62'Språkkod:'
     A            WSDFSK         2A  B 15 72TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 16  2'Omlastning:'
     A                                      COLOR(WHT)
     A                                 17  2'Tr.midel:'
     A            WSTAID        27A  B 17 12TEXT('Transportmiddels-id')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 42'Landkod.:'
     A            WSTALK         2A  B 17 52TEXT('Landkode')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17 62'Språkkod:'
     A            WSTASK         2A  B 17 72TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18  2'Godk.DT.:'
     A            WSGODT         8Y 0B 18 12TEXT('Godkjenningsdato')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18 21'Myndighe:'
     A            WSOM          35A  B 18 31TEXT('Godkj.myndighet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 67'Språkkod:'
     A            WSOMSK         2A  B 18 77TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19  2'Godk.LK.:'
     A            WSOMLK         2A  B 19 12TEXT('Godkjenningsland')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19 20'Godk.pl.:'
     A            WSOMST        35A  B 19 30TEXT('Godkjenningssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19 67'Språkkod:'
     A            WSOMSS         2A  B 19 77TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 20  2'Containe:'
     A            WSCNR         17A  B 20 12TEXT('Container')
     A                                      DSPATR(UL)
     A N28                                  COLOR(YLW)
     A  28                                  COLOR(RED)
     A                                 23  2'F4=Søk   F12=Avbryt   F15=Tillæggs-
     A                                      upplysningar'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                  1  2'SYSPED/SVX062R'
     A                                      COLOR(BLU)
     A                                  1 21'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'HÆNDELSER På VÆGEN:'
     A                                      COLOR(WHT)
     A                                  4  2'Plats   :'
     A            TVST          35A  O  4 12TEXT('Sted')
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                  4 52'Språkkod:'
     A            TVSTSK         2A  O  4 62TEXT('Språkkode')
     A                                      COLOR(WHT)
     A                                  4 66'Landkod: '
     A            TVSTLK         2A  O  4 76TEXT('Landkode')
     A                                      COLOR(WHT)
     A                                  5  2'Kontroll:'
     A            TVCTL          1Y 0O  5 12TEXT('Kontroll')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 16'(Redan i NCTS ?  0=Nej, 1=Ja)'
     A                                  7  2'Hendelse:'
     A                                      COLOR(WHT)
     A                                  7 22'Språkkod:'
     A            TVINSK         2A  O  7 32TEXT('Språkkode')
     A                                      COLOR(WHT)
     A            TVINF1        70A  O  8 10TEXT('Informasjon')
     A                                      COLOR(WHT)
     A            TVINF2        70A  O  9 10TEXT('Informasjon')
     A                                      COLOR(WHT)
     A            TVINF3        70A  O 10 10TEXT('Informasjon')
     A                                      COLOR(WHT)
     A            TVINF4        70A  O 11 10TEXT('Informasjon')
     A                                      COLOR(WHT)
     A                                 12  2'Godk.DT.:'
     A            TVGDT          8Y 0O 12 12TEXT('Godkjenningsdato')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12 21'Myndighe:'
     A            TVGM          35A  O 12 31TEXT('Godkj.myndighet')
     A                                      COLOR(WHT)
     A                                 12 67'Språkkod:'
     A            TVGMSK         2A  O 12 77TEXT('Språkkode')
     A                                      COLOR(WHT)
     A                                 13  2'Godk.LK.:'
     A            TVGMLK         2A  O 13 12TEXT('Godkjenningsland')
     A                                      COLOR(WHT)
     A                                 13 20'Godk.pl.:'
     A            TVGMST        35A  O 13 30TEXT('Godkjenningssted')
     A                                      COLOR(WHT)
     A                                 13 67'Språkkod:'
     A            TVGMSS         2A  O 13 77TEXT('Språkkode')
     A                                      COLOR(WHT)
     A                                 14  2'Forsegling:'
     A                                      COLOR(WHT)
     A                                 15  2'Antal   :'
     A            TVDANT         3Y 0O 15 12TEXT('Antall forsigling')
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                 15 20'Kod     :'
     A            TVDFKD        20A  O 15 30TEXT('Forseglingskode')
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 15 62'Språkkod:'
     A            TVDFSK         2A  O 15 72TEXT('Språkkode')
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 16  2'Omlasting:'
     A                                      COLOR(WHT)
     A                                 17  2'Tr.midle:'
     A            TVTAID        27A  O 17 12TEXT('Transportmiddels-id')
     A                                      COLOR(WHT)
     A                                 17 42'Språkod.:'
     A            TVTASK         2A  O 17 52TEXT('Spårkkode')
     A                                      COLOR(WHT)
     A                                 17 62'Landkod.:'
     A            TVTALK         2A  O 17 72TEXT('Landkode')
     A                                      COLOR(WHT)
     A                                 18  2'Godk.DT.:'
     A            TVGODT         8Y 0O 18 12TEXT('Godkjenningsdato')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18 21'Myndighe:'
     A            TVOM          35A  O 18 31TEXT('Godkj.myndighet')
     A                                      COLOR(WHT)
     A                                 18 67'Språkkod:'
     A            TVOMSK         2A  O 18 77TEXT('Språkkode')
     A                                      COLOR(WHT)
     A                                 19  2'Godk.LK.:'
     A            TVOMLK         2A  O 19 12TEXT('Godkjenningsland')
     A                                      COLOR(WHT)
     A                                 19 20'Godk.pl.:'
     A            TVOMST        35A  O 19 30TEXT('Godkjenningssted')
     A                                      COLOR(WHT)
     A                                 19 67'Språkkod:'
     A            TVOMSS         2A  O 19 77TEXT('Språkkode')
     A                                      COLOR(WHT)
     A                                 20  2'Containe:'
     A            TVCNR         17A  O 20 12TEXT('Container')
     A N28                                  COLOR(WHT)
     A  28                                  COLOR(RED)
     A                                 23  2'F12=Avbryt    F15=Tillæggsupplysni-
     A                                      ngar'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til bilde02')
     A N27                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SVX062R'
     A                                      COLOR(BLU)
     A                                  1 18'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 40'Ærende '
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Vælj option, 2=Ændra 4=Radera 5=Vi-
     A                                      sa'
     A                                      COLOR(BLU)
     A  27                              2  2'Vælj option, 5=Visa'
     A                                      COLOR(BLU)
     A                                  3  2'Op Linje Plats                    -
     A                                                 K F Omlastning'
     A                                      COLOR(WHT)
     A N27                             22  2'F3=Avsluta   F5=Uppdatera   F6=Ska-
     A                                      pa ny   F8=Hoppa till   F7=Se uppdr-
     A                                      ag'
     A                                      COLOR(BLU)
     A  27                             22  2'F3=Avsluta   F5=Uppdatera   F8=Hop-
     A                                      pa till   F12=Avbryt'
     A                                      COLOR(BLU)
     A N27                             23  2'F12=Avbryt   F17=Kopiera detta ære-
     A                                      nde till ett nytt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  3  2'Hoppa till linje--->'
     A            W3LI           5S 0I  3 24DSPATR(UL)
     A                                  5  2'F12=Avbryt '
     A                                      COLOR(BLU)
      *
     A          R SUBFIL2                   SFL
     A            WSNR           1A  O  5  2
     A            TVLI           5Y 0H
     A            TVLN           5Y 0O  5  5EDTCDE(4)
     A            TVST          35A  O  5 11
     A            TVCTL          1  0O  5 47
     A            TVFLG          1  0O  5 49
     A            TVTAID        27A  O  5 51
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N93N94                            ROLLUP(25)
     A            ZRECN2         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SVX062R'
     A                                      COLOR(BLU)
     A                                  1 17'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 40'Ærende '
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Tryck Enter før att bekræfta rader-
     A                                      ing   4=Radera.'
     A                                      COLOR(BLU)
     A                                  3  2'Tryck F12 før att ångra'
     A                                      COLOR(BLU)
     A                                  4  2'Op Linje Plats                    -
     A                                                 K F Omlastning'
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'Gå tilbake')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F12=Avbryt'
     A                                      COLOR(BLU)
