     FSAD061FS  CF   E             WORKSTN
     F                                     SFILE(SAD061S1:ÆRECNO)
     FSADH      UF   E           K DISK
     FTVINRMD   UF   E           K DISK
     FTVINF     IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C  N33              EXSR      SRB
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    TR068A        W0068A
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     TRMDK         KLIST
     C                   KFLD                    TRST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W0068A
     C     TRMDK2        KLIST
     C                   KFLD                    TRST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     TRMDK3        KLIST
     C                   KFLD                    TRST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    TR068A
     C                   KFLD                    TR068B
     C                   KFLD                    TR068C
     C     SADHK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UFLAGG            1 0
      *
     C                   Z-ADD     1             UFLAGG
     C                   MOVE      99999999      W0068A
     C     TRMDK         SETLL     TVINRMD
     C     TRMDK2        READPE(N) TVINRMD                                33
     C  N33TRMTY         DOWNE     '1'
     C     TRMDK2        READPE(N) TVINRMD                                33
     C  N33              ENDDO
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Vise hele meldingsloggen.                            SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C                   MOVE      '1'           *IN30
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN83
     C                   WRITE     SAD061S2
     C                   MOVEA     '000'         *IN(83)
     C                   Z-ADD     *ZERO         ÆRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     TRMN          SETLL     TVINF
     C     TRMN          READE     TVINF                                  84
      *
     C     *IN84         DOWEQ     '0'
     C                   MOVE      '0'           *IN30
     C                   ADD       1             ÆRECNO
     C                   WRITE     SAD061S1
     C     TRMN          READE     TVINF                                  84
     C  N84              END
     C                   Z-ADD     1             ÆRECNO
      *_______________
      * Ingen poster ?
      *"""""""""""""""
     C     *IN30         IFEQ      '1'
     C                   GOTO      SRBF9
     C                   END
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRBF1         TAG
     C                   MOVEA     '11'          *IN(81)
     C                   WRITE     SAD061S3
     C                   EXFMT     SAD061S2
     C  N03
     CANN05
     CANN12              GOTO      SRBF1
     C     *IN05         IFEQ      '1'
     C     SADHK         CHAIN     SADH                               33
     C                   SELECT
     C     SI0035        WHENEQ    '6'                                          DOK.KONTROLL
     C                   MOVE      'D'           SIST
     C     SI0035        WHENEQ    '5'                                          VAREKONTROLL
     C                   MOVE      'V'           SIST
     C     SI0035        WHENEQ    '9'                                          TVINN INFO.
     C                   MOVE      'T'           SIST
     C                   OTHER
     C                   MOVE      'M'           SIST
     C                   Z-ADD     0             UFLAGG
     C                   ENDSL
     C                   MOVE      ' '           SI0035
     C                   UPDATE    SADHR
     C     TRMDK3        CHAIN     TVINRMD                            33
     C                   MOVE      'P'           TRST
     C                   UPDATE    TVINRMDR
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBF9         ENDSR
