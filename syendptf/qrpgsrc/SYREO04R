     H DFTACTGRP(*NO)
     FEDIM      UF   E           K DISK
     FEDIS      UF   E           K DISK
     FEDISE     UF   E           K DISK
     FEDIRE     UF   E           K DISK
     FEDISE1K   UF   E           K DISK    RENAME(EDISER:EDISER1K)
     F                                     PREFIX(K_)
     FEDIRE1K   UF   E           K DISK    RENAME(EDIRER:EDIRER1K)
     F                                     PREFIX(K_)
     FEDISX     UF   E           K DISK
     FXMLRCVF   UF   E           K DISK
     FXMLRCAF   UF   E           K DISK
     FXMLRCLF   UF   E           K DISK
     FTRIH      UF   E           K DISK
     FTRIHFS    UF   E           K DISK
     FTRIHPK    UF   E           K DISK
     FTRIV      UF   E           K DISK
     FTRIVCN    UF   E           K DISK
     FTRIVFS    UF   E           K DISK
     FTRIL      UF   E           K DISK
     FTRILCN    UF   E           K DISK
     FTRILDK    UF   E           K DISK
     FTRILKL    UF   E           K DISK
     FTRILMN    UF   E           K DISK
     FTRILPK    UF   E           K DISK
     FTRILVT    UF   E           K DISK
     FGODSJF    UF   E           K DISK
     FTVINF     UF   E           K DISK
     FFRISOK    UF   E           K DISK
     FTRACKF    UF   E           K DISK
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FARKIVL17  UF   E           K DISK
     d                 ds
     d  smbr                   1     10A
     d  smbr_1                 1      1A
     d  smbr_2                 2      8A
     d  smbr_3                 9     10A
     d  smbr_4                 4     10A
     d                 ds
     d  uavd                   1      4A
     d  xavd                   1      4  0
     d                 ds
     d  utdn                   1      7A
     d  xtdn                   1      7  0

     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows0         s             10i 0
     d myrows          s             10i 0
     d myrows2         s             10i 0
     D XDTF            S              8  0
     D XDTP            S              8  0
     D XPOS            S              3  0
     d XAPS            C                   const(X'7D')
     D XMBR            S             10A
     D XGN15           S             15A
     D XTRNR           S             20A
     D Error           S            150
     d X               S             10  0
     d X2              S             10  0
     D ufil            S            256A
     D utyp            S              5A
     D uok             S              1A
     D XSLUTT          S              1
     D XSLUTT2         S              1
      *
      *  Prototype for 'SYREO01C3'
     d SYREO01C3       pr                  extpgm('SYREO01C3')
     d ufil_                               like(ufil)
      *  Prototype for 'SYREO01R3'
     d SYREO01R3       pr                  extpgm('SYREO01R3')
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d utyp_                               like(utyp)
     d uok_                                like(uok)
      *  Prototype for syreo04r
     d syreo04r        pr
     d udtf_                               like(udtf)
     d udtp_                               like(udtp)
      *  *ENTRY Interface for Main Procedure
     d syreo04r        pi
     d udtf                           8
     d udtp                           8
      *
     d MyPoDS0         ds                  Qualified  dim(1)
     d  FIFIRM                        2A
     d MyPoDS          ds                  Qualified  dim(10000)
     d  TIAVD                         4S 0
     d  TITDN                         7S 0
     d  TISG                          3A
     d  TIDT                          8S 0
     d  TIGN                         35A
     d  TITRNR                       18A
     d MyPoDS2         ds                  Qualified  dim(1000)
     d  MAVD                          4S 0
     d  MTDN                          7S 0
     d  MSR                           1A
     d  MMN                           9S 0
     d  MSN                           9S 0
      /free

         exsr init;

         dow xslutt <> 'J';
           exsr srhoved;
         enddo;

       *inlr = *on;
       return;

       //=====================================================================
       // Hoved logikk
       //=====================================================================
       begsr srhoved;

       clear MyPoDS;
       SqlStm = 'Select TIAVD, TITDN, TISG, TIDT, TIGN, TITRNR ' +
                'from TRIH ' +
                'Where TIDT <= ' +
                %trim(udtf);
       Exec SQL PREPARE DynSel from :SqlStm;
       EXEC SQL DECLARE  A33  cursor for DynSel;
       Exec SQL OPEN A33;
       Myrows = %elem(MyPoDS);
       exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       Exec SQL CLOSE A33;

       if myrows > 0;
         for x = 1 to myrows;
           tiavd  = mypods(x).tiavd;
           tisg   = mypods(x).tisg;
           if tisg = *blanks and tiavd = 0;
             xslutt = 'J';
             leave;
           endif;

             titdn  = mypods(x).titdn;
             tign   = mypods(x).tign;
             titrnr = mypods(x).titrnr;

             exsr sra;
             exsr srb;
             xslutt2 = 'N';
             dow xslutt2 <> 'J';
               exsr src;
             enddo;
             exsr sre;
             exsr srf;

             exec sql
             delete from TRIHFS
             where tiavd = :tiavd and titdn = :titdn
             with NC;
             exec sql
             delete from TRIHPK
             where tiavd = :tiavd and titdn = :titdn
             with NC;

           // slett hovedpost
             exec sql
             delete from TRIH
             where tiavd = :tiavd and titdn = :titdn
             with NC;

         endfor;
       endif;

       endsr;
      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM TRIV
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sra;

       exec sql
       delete from triv
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       exec sql
       delete from trivcn
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       exec sql
       delete from trivfs
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM TRIL
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srb;

       exec sql
       delete from tril
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       exec sql
       delete from trilcn
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       exec sql
       delete from trildk
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       exec sql
       delete from trilkl
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       exec sql
       delete from trilmn
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       exec sql
       delete from trilpk
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       exec sql
       delete from trilvt
       where tvavd=:tiavd and tvtdn=:titdn
       with NC;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIM/EDIS/TVINF
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src;

       clear MyPoDS2;
       SqlStm = 'Select MAVD, MTDN, MSR, MMN, MSN ' +
                'from EDIM ' +
                'Where MAVD = ' + %trim(%editc(tiavd:'Z')) +
                ' and MTDN = ' + %trim(%editc(titdn:'Z')) +
                ' and M1001 <> ' + xaps + '929' + xaps +
                ' and M1001 <> ' + xaps + '830' + xaps +
                ' and M1001 <> ' + xaps + 'TET' + xaps +
                ' and M1001 <> ' + xaps + 'SVI' + xaps +
                ' and M1001 <> ' + xaps + 'SVE' + xaps +
                ' and M1001 <> ' + xaps + 'STI' + xaps +
                ' and M1001 <> ' + xaps + 'STE' + xaps +
                ' and M1001 <> ' + xaps + 'DKI' + xaps +
                ' and M1001 <> ' + xaps + 'DKE' + xaps;
       Exec SQL PREPARE DynSel2 from :SqlStm;
       EXEC SQL DECLARE  A34  cursor for DynSel2;
       Exec SQL OPEN A34;
       Myrows2 = %elem(MyPoDS2);
       exec sql FETCH A34 for :MyRows2 rows
                  INTO :MyPoDs2;
       Exec SQL CLOSE A34;

       if myrows2 > 0;
         for x2 = 1 to myrows2;
           if mypods2(x2).msr = *blanks;
             xslutt2 = 'J';
             leave;
           endif;

           mmn = mypods2(x2).mmn;
           msn = mypods2(x2).msn;

           exec sql
           delete from TVINF
           where fmn = :mmn
           with NC;
           exec sql
           delete from XMLRCVF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCAF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCLF
           where xrsn = :msn
           with NC;

           chain (msn) edis;
             if %found;
              // slett komponent
               exsr src2;
              // slett fil i IFS
               if sifs <> *blanks;
                 if %subst(sifs:1:1) <> '/';
                   sifs = '/' + %trim(sifs);
                 endif;
                 ufil = sifs;
                 syreo01c3 (ufil);
               endif;
               delete edisr;
             endif;

         // slett hovedpost
           exec sql
           delete from EDIM
           where mmn = :mmn
           with NC;

         endfor;
       endif;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIRE/EDISE/EDIRE1K/EDISE1K/XMLRCVF/AF/LF/EDISX
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src2;

       exec sql
       delete from edire
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise
       where sembr = :smbr
       with NC;

       exec sql
       delete from edire1k
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise1k
       where sembr = :smbr
       with NC;

       chain smbr edisx;
       if %found;
         if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
           sxifsobj = '/' + %trim(sxifsobj);
         endif;
         syreo01c3 (sxifsobj);
         delete edisxr;
       else;
         if smbr_3 = ' ';
         // MBR = R1234567 -> PRØV R001234567
           xmbr = smbr_1 + '00' + smbr_2;
         else;
         // MBR = R001234567 -> PRØV R1234567
           xmbr = smbr_1 + smbr_4 + '  ';
         endif;
         chain xmbr edisx;
         if %found;
           if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
             sxifsobj = '/' + %trim(sxifsobj);
           endif;
           syreo01c3 (sxifsobj);
           delete edisxr;
         endif;
       endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM GODSJF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sre;

       if tign <> *blanks;
         xgn15 = %subst(tign:1:15);
         xtrnr = titrnr;
         exec sql
         delete from godsjf
         where gogn = :xgn15 and gotrnr = :xtrnr
         with NC;
       endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM FRISOK/TRACKF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srf;

       exec sql
       delete from frisok
       where fsavd = :tiavd and fsopd = :titdn
       with NC;

       exec sql
       delete from trackf
       where ttavd = :tiavd and ttopd = :titdn
       with NC;

       // Sjekk om oppdrag ligger i andre modul. Ved N sletter KUN TET-import.
       xavd = tiavd;
       xtdn = titdn;
       syreo01r3 (uavd: utdn: utyp: uok);
       setll (tiavd:titdn) arkivl17;
       reade (tiavd:titdn) arkivl17;
       dow not %eof;
         if uok = 'J';
           chain (fifirm) firmarc;
           chain (artype) arktxt;
           if %found and arklag <> *blanks;
             chain (fifirm:arklag) arkext;
           endif;
           if arbhis <> *blanks;
           // Bane for historikk
             arcane = arbhis;
           endif;
           xpos = %scan ('.' : arlink : 1);
           if xpos = 0;
             ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK) + '.pdf';
           else;
             ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK);
           endif;
           syreo01c3 (ufil);

           delete arkivr;
         else;
           update arkivr;
         endif;
         reade (tiavd:titdn) arkivl17;
       enddo;

       endsr;

      //***********************************************
       begsr init;
      //***********************************************
       xdtf = %float(udtf);
       xdtp = %float(udtp);
       utyp = 'TET-I';

       //exec sql
       //select *
       //from firm;
       SqlStm = 'Select * ' +
                'from FIRM ' +
                'FOR READ ONLY ';
       Exec SQL PREPARE DynSel0 from :SqlStm;
       EXEC SQL DECLARE  A30  cursor for DynSel0;
       Exec SQL OPEN A30;
       Myrows0 = %elem(MyPoDS0);
       exec sql FETCH A30   for :MyRows0 rows
                  INTO :MyPoDs0;
       Exec SQL CLOSE A30;
       FIFIRM = mypods0(1).fifirm;

       endsr;

      /end-free
