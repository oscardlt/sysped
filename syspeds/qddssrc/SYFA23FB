     A*%%TS  SD  20220321  113148  SVEIN       REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 03 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA23BPNL)
     A                                      HLPTITLE('VEDLIKEHOLD TILLEGG TIL F-
     A                                      RAKTBELØP')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A*%%TS  SD  20000516  112344  JOVO        REL-V4R4M0  5769-PW1
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde')
     A                                      CF12(12 'Gå til forrige bilde')
     A                                  1  2'SYSPED/SYFA23B'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                 11  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0B 11 17DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 20MSGCON(040 JOV1023 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 11 30MSGCON(050 JOV1024 *LIBL/SYMSGF)
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20000518  112654  JOVO        REL-V4R4M0  5769-PW1
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
N02  A  88                                  DSPATR(PR ND)
     A            FRTDEL         1A  O  5  8
     A            FRTAVD         4  0O  5 15EDTCDE(4)
     A            FRTKUN         8  0O  5 21EDTCDE(4)
     A            KNAVN         30   O  5 30
     A            FRTGEB         3   O  5 61
     A            FRTSAT         3Y 1O  5 70EDTCDE(J)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20000516  112344  JOVO        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91N93N94                            ROLLUP(25)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYFA23B'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 JOV1023 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSKNAV        30A  O  2 25TEXT('Kundenavn')
     A                                      COLOR(WHT)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 74SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(075 JOV1025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A*%%TS  SD  20130408  080654  SVEIN       REL-V7R1M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      CF10(10 'prisjuster')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                  1  2MSGCON(078 JOV1029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(078 JOV1030 *LIBL/SYMSGF)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              5  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3 22MSGCON(018 SHU1375 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VARELI
     A*%%TS  SD  20000522  111518  JOVO        REL-V4R4M0  5769-PW1
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'Søk')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(074 JOV1025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTDEL         1A  B  2  8
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WSTAVD         4Y 0B  2 15EDTCDE(4)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A            WSTKUN         8Y 0B  2 21EDTCDE(4)
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A            WSTGEB         3A  B  2 61
     A            WSTSAT         3Y 1B  2 70EDTCDE(J)
     A                                  5 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 44'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL2
     A*%%TS  SD  20000518  112654  JOVO        REL-V4R4M0  5769-PW1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2ODEL         1A  I  2 16
     A            W2OAVD         4S 0I  2 21
     A            W2OKUN         8S 0I  2 28
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A*%%TS  SD  20000519  121507  JOVO        REL-V4R4M0  5769-PW1
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2MSGCON(074 JOV1025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            FRTDEL         1A  O  2  8COLOR(WHT)
     A            FRTAVD         4S 0O  2 15COLOR(WHT)
     A            FRTKUN         8S 0B  2 21COLOR(WHT)
     A            FRTGEB         3A  B  2 61COLOR(WHT)
     A            FRTSAT         3Y 1B  2 70COLOR(WHT)
     A                                      EDTCDE(J)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A*%%TS  SD  20000519  235651  JOVO        REL-V4R4M0  5769-PW1
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SYFA23B'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 JOV1023 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8  0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSKNAV        30   O  2 25TEXT('Kundenavn')
     A                                      COLOR(WHT)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(075 JOV1025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 20  2MSGCON(078 JOV1029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 21  2MSGCON(078 JOV1030 *LIBL/SYMSGF)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R PRISJU
     A*%%TS  SD  20130408  080654  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'Søk')
     A                                      CF02(02 'start justering')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSTDEL         1A  B  2  8
     A            WSTAVD         4Y 0B  2 15EDTCDE(4)
     A            WSTKUN         8Y 0B  2 21EDTCDE(4)
     A            WSTGEB         3A  B  2 61
     A            WSTSAT         3Y 1B  2 70EDTCDE(J)
     A                                  4 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1  2MSGCON(077 SHU1376 *LIBL/SYMSGF)
     A                                  3 14MSGCON(030 SHU1377 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  4 40MSGCON(040 SHU1382 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R PRISJU2
     A*%%TS  SD  20220321  113148  SVEIN       REL-V7R4M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'Søk')
     A                                      CF02(02 'start justering')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSTDEL         1A  B  2  8
     A            WSTAVD         4Y 0B  2 15EDTCDE(4)
     A            WSTKUN         8Y 0B  2 21EDTCDE(4)
     A            WSTGEB         3A  B  2 61
     A            WSTSAT         3Y 1B  2 70EDTCDE(J)
     A                                  4 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1  2MSGCON(077 SHU1376 *LIBL/SYMSGF)
     A                                  3 14MSGCON(030 SHU1377 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  4 40MSGCON(040 SHU1382 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
