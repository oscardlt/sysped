     FEDIRE     IF   E           K DISK
     FEDISX     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E             DISK
     FEDIM      O  A E             DISK
     FSVUH      O  A E             DISK
     D NU              S              1    DIM(15)
     D KD              S              5    DIM(10)
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D  LDADTA         DS          1024
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D  AA                   301    380
     D                                     DIM(80)
     D  AADS                 301    380
     D  AB                   401    480
     D                                     DIM(80)
     D  ABDS                 401    480
     D  U0065                501    506
     D  SEGNAM               507    509
     D  TN                   601    602  0
     D  EOF                  603    605
     D  TEGN                 606    606
     D  LOKAHE               607    607
     D  UMN                  608    616  0
     D  UMBR                 617    626
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C     SEGSTA        TAG
     C  N91              EXSR      GETSEG
     C   91              GOTO      SLUTT
     C     SEGNAM        CASEQ     'UNB'         UNB
     C     SEGNAM        CASEQ     'UNH'         UNH
     C     SEGNAM        CASEQ     'DOC'         DOC
     C     SEGNAM        CASEQ     'PAC'         PAC
     C     SEGNAM        CASEQ     'RFF'         RFF
     C     SEGNAM        CASEQ     'PCI'         PCI
     C     SEGNAM        CASEQ     'FTX'         FTX
     C     SEGNAM        CASEQ     'LOC'         LOC
     C     SEGNAM        CASEQ     'DTM'         DTM
     C     SEGNAM        CASEQ     'MEA'         MEA
     C     SEGNAM        CASEQ     'NAD'         NAD
     C     SEGNAM        CASEQ     'CTA'         CTA
     C     SEGNAM        CASEQ     'COM'         COM
     C     SEGNAM        CASEQ     'CNT'         CNT
     C     SEGNAM        CASEQ     'UNT'         UNT
     C     SEGNAM        CASEQ     'UNZ'         UNZ
     C                   ENDCS
     C                   GOTO      SEGSTA
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WLIB             10
     C                   PARM                    WFIL             10
     C                   PARM                    WMBR             10
     C                   PARM                    WNUMR            10 0
      * Open input file
     C   91              GOTO      ENDINI
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C                   MOVE      *ZEROS        NUMVRD           15 0
     C                   MOVE      *ZEROS        NUTEGN            1 0
     C                   MOVE      *BLANKS       SEGNAM
     C                   MOVE      *BLANKS       AADS
     C                   MOVE      *BLANKS       ABDS
     C                   MOVEA     *BLANKS       FE
     C                   MOVEA     *BLANKS       NU
     C                   MOVEA     *BLANKS       KD
     C                   MOVE      *ZEROS        XN                3 0
     C     *LIKE         DEFINE    *IN91         WIN91
     C     *LIKE         DEFINE    *IN99         WIN99

      * Datafelt AB i EDIRE (80 lang) går rett inn i array AB ved READ
      * TN=81 soerger for automatisk overflyt array AB->AA ved kall av GETCH
     C                   MOVE      '0'           *IN91
     C                   MOVE      '0'           *IN98
     C                   MOVE      '0'           *IN99
     C                   EVAL      UMBR=WMBR
     C     WMBR          SETLL     EDIRE
     C     WMBR          READE     EDIRE                                  91
     C     *IN91         DOWEQ     '0'
     C     'UNB+'        SCAN      RDATA         TN                       20
     C  N20WMBR          READE     EDIRE                                  91
     C   91              GOTO      ENDINI
     C  N20              ENDDO

     C                   MOVE      RDATA         ABDS
     C     TN            IFGT      1
     C                   SUB       1             TN
     C                   ELSE
     C                   Z-ADD     81            TN
     C                   ENDIF
     C                   BITON     '17'          TEGN
     C                   BITOFF    '023456'      TEGN
     C                   MOVE      'NO '         EOF
     C                   MOVE      ' '           LOKAHE

      * Spesielt reserverte tegn defineres her med både grafisk verdi og
      * med en logisk verdi. Den grafiske verdien testes det mot i rutinen
      * som henter tegn. Finner denne rutinen at at tegnet er ment som en
      * spesialverdi (ikke ? foran), byttes tegnet med tilsvarende LOGISK VERDI
      * (De logiske verdiene er ikke lesbre tegn,og vil aldri forekomme i den
      * innkommende datastroemmen). '?' når brukt som opphevelse foran annet
      * spesialtegn vil bli "spist" av henterutinen (GETCH) og en vil derfor
      * kunne betrakte spoersmålstegn som grafisk tegn i resten av programmet.

      * Ved andre standarder for spesialtegn er det bare å bytte verdiene til
      * de GRAFISKE tegnene her. (Programmet arbeider da med riktige verdier).
     C                   MOVE      '?'           GRASPØ            1
     C                   MOVE      '+'           GRAPLU            1
     C                   MOVE      ':'           GRAKOL            1
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      * Logiske verdier for spesialtegn
      * LOGAPP = slutt på segmentet. I NODI er tegnet "'".
     C                   BITON     '17'          LOGAPP            1
     C                   BITOFF    '023456'      LOGAPP
      * HVIS KOMMUNEDATA, INITIERE MED LOGAPP (GÅ RETT PÅ UNB)
     C                   MOVE      LOGAPP        TEGN
      * LOGPLU = sparasjonstegn for Tag/dataelement. I NODI er tegnet "+".
     C                   MOVE      X'38'         LOGPLU            1
     C*                  BITON     '16'          LOGPLU            1
     C*                  BITOFF    '023457'      LOGPLU
      * LOGKOL = sparasjonstegn for delelement. I NODI er tegnet ":".
     C                   MOVE      X'39'         LOGKOL            1
     C*                  BITON     '167'         LOGKOL            1
     C*                  BITOFF    '02345'       LOGKOL
      * LOGBLA = utenlandske måten å vise blank.
     C                   BITON     '01234567'    LOGBLA            1

     C     ENDINI        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å hente neste tegn i datastroem             GETCH  /
      *////////////////////////////////////////////////////////////
     C     GETCH         BEGSR
      * Rutinen henter "TEGN" fra array AA. Ved slutten av AA "toemmes" AB inn
      * i AA. (AB benyttes kun for alltid å kunne ha "lookahead-tegn" - LOKAHE)
      * 91 settes når en ved "Toemming" finner at AB-arrayet er tomt.

      * "?" spises av SR når "?" kun varsler grafisk betydning av spesialtegn.
      *Når tegn ved utgang av SR er ',+ eller :  er betydningen av ind. 98 :
      *    98 ON -> kun grafisk betydning (uforandret)
      *    98 OFF-> spesialtegn (Bytt tegn mot ikke lesbar logisk verdi LOG???)
     C                   MOVE      '0'           *IN98
     C     STACH         TAG
      * Må ny record hentes inn i array AA (fra AB)?????
     C     TN            IFEQ      81
     C     EOF           IFEQ      'YES'
     C                   SETON                                            91
     C                   GOTO      ENDCH
     C                   ENDIF
     C                   MOVE      ABDS          AADS
     C  N99WMBR          READE     EDIRE                                  99
     C  N99              MOVE      RDATA         ABDS
     C   99              MOVE      *BLANKS       ABDS
     C   99              MOVE      'YES'         EOF
     C                   Z-ADD     1             TN
     C                   ENDIF
      * Hent det tegn som TN peker på - Tegnet etter er "Lookahead"
     C                   MOVE      AA(TN)        TEGN
     C                   ADD       1             TN
     C     TN            IFLE      80
     C                   MOVE      AA(TN)        LOKAHE            1
     C                   ELSE
     C                   MOVE      AB(1)         LOKAHE
     C                   ENDIF
      * Dersom TEGN er "?" og LOKAHE er spes.tegn - "spis" spoersmålstegnet
      * Når 98 er on her betyr det at "?" foran spesialtegn alt er "spist"
      *        evt. nytt "?" er da grafisk tegn - LOKAHE skal da IKKE testes.
     C  N98TEGN          IFEQ      GRASPØ
     C     LOKAHE        COMP      GRAAPP                                 98
     C  N98LOKAHE        COMP      GRAPLU                                 98
     C  N98LOKAHE        COMP      GRAKOL                                 98
     C  N98LOKAHE        COMP      GRASPØ                                 98
     C   98              GOTO      STACH
     C                   END
      * Ved N98 og ',+ eller : i TEGN betyr det at det IKKE var opphevelsestegn
      * foran. Vi har med andre ord et LOGISK SPESIALTEN INNE
     C  N98TEGN          IFEQ      GRAAPP
     C                   MOVE      LOGAPP        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAPLU
     C                   MOVE      LOGPLU        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAKOL
     C                   MOVE      LOGKOL        TEGN
     C                   ELSE
     C     TEGN          IFEQ      LOGBLA
     C                   MOVE      ' '           TEGN
     C                   END
     C                   END
     C                   END
     C                   END

     C     ENDCH         ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å hente neste Segment                       GETSEG /
      *////////////////////////////////////////////////////////////
     C     GETSEG        BEGSR
      * "Spis tegn" til appostrof er lest
     C     TEGN          DOWNE     LOGAPP
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   END
     C* "Spis tegn" til første IKKE blanke
     C                   MOVEA     *BLANKS       FE
     C                   EXSR      GETCH
     C     TEGN          DOWEQ     ' '
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   ENDDO
     C                   MOVEA     TEGN          FE(1)
      * Har nå lest appostrof - de 3 neste vil være SEGMENTNAVN
     C     2             DO        3             NR                3 0
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   MOVEA     TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          SEGNAM
      * La TEGN være '+' etter segmentnavnet ved utgang av SR
     C                   EXSR      GETCH
      * Segmentnavn returneres nå i variabel 'SEGNAM'
     C     ENDSEG        ENDSR
      *////////////////////////////////////////////////////////////
      * Get Data Element                                   GETDED /
      *////////////////////////////////////////////////////////////
     C     GETDED        BEGSR
     C                   MOVEA     *BLANKS       FE
     C     1             DO        256           NR                3 0
     C                   EXSR      GETCH
     C     TEGN          IFEQ      LOGAPP
     C     TEGN          OREQ      LOGPLU
     C     TEGN          OREQ      LOGKOL
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   MOVEA     TEGN          FE(NR)
     C                   ENDDO
     C     ENDDED        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle COM-segmentet                      COM  /
      *////////////////////////////////////////////////////////////
     C     COM           BEGSR
      *_____________
      * C076 CONTACT
      *"""""""""""""
      * 3148 Telefonnummer
     C                   EXSR      GETDED
     C                   SELECT
     C     NAD_3035      WHENEQ    'AE'
     C                   MOVEL     FELT          COM_3148A
     C     NAD_3035      WHENEQ    'DT'
     C                   MOVEL     FELT          COM_3148B
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle CTA-segmentet                      CTA  /
      *////////////////////////////////////////////////////////////
     C     CTA           BEGSR
      *__________
      * 3139 CODE
      *""""""""""
     C                   EXSR      GETDED
      *_____________
      * C056 DETAILS
      *"""""""""""""
      * 3413 ID
     C                   EXSR      GETDED
      * 3412 Handlæggare
     C                   EXSR      GETDED
     C                   SELECT
     C     NAD_3035      WHENEQ    'AE'
     C                   MOVEL     FELT          CTA_3412A
     C     NAD_3035      WHENEQ    'DT'
     C                   MOVEL     FELT          CTA_3412B
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle DOC-segmentet                      DOC  /
      *////////////////////////////////////////////////////////////
     C     DOC           BEGSR
      *___________________________
      * C002 DOCUMENT/MESSAGE NAME
      *"""""""""""""""""""""""""""
      * 1001 Referans
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DOC_1001
      *______________________________
      * C503 DOCUMENT/MESSAGE DETAILS
      *""""""""""""""""""""""""""""""
      * 1004 Tullid
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DOC_1004
     C     TEGN          CABEQ     LOGAPP        DOCEND
     C     DOCEND        TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle DTM-segmentet                      DTM  /
      *////////////////////////////////////////////////////////////
     C     DTM           BEGSR
      *______________________
      * C507 DATE/TIME/PERIOD
      *""""""""""""""""""""""
      * 2005 Qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DTM_2005          3
      * 2380 Date
     C                   EXSR      GETDED
     C                   SELECT
     C     DTM_2005      WHENEQ    '131'
     C                   MOVEL     FELT          DTM_2380A
     C     DTM_2005      WHENEQ    '161'
     C                   MOVEL     FELT          DTM_2380B
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle FTX-segmentet                      FTX  /
      *////////////////////////////////////////////////////////////
     C     FTX           BEGSR
      *_______________
      * 4451 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
      *__________
      * 4453 CODE
      *""""""""""
     C                   EXSR      GETDED
      *_______________
      * C107 REFERENCE
      *"""""""""""""""
      * 4440 Varuslag rad 1
     C                   EXSR      GETDED
     C                   EXSR      FTX2
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Varuslag rad 2
     C                   EXSR      GETDED
     C                   EXSR      FTX2
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Varuslag rad 3
     C                   EXSR      GETDED
     C                   EXSR      FTX2
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Varuslag rad 4
     C                   EXSR      GETDED
     C                   EXSR      FTX2
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Varuslag rad 5
     C                   EXSR      GETDED
     C                   EXSR      FTX2
     C     FTXEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle FTX-segmentet - 2                 FTX2  /
      *////////////////////////////////////////////////////////////
     C     FTX2          BEGSR
     C                   SELECT
     C     FTX_4440A     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440A
     C     FTX_4440B     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440B
     C     FTX_4440C     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440C
     C     FTX_4440D     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440D
     C     FTX_4440E     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440E
     C     FTX_4440F     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440F
     C     FTX_4440G     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440G
     C     FTX_4440H     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440H
     C     FTX_4440I     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440I
     C     FTX_4440J     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440J
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle LOC-segmentet                      LOC  /
      *////////////////////////////////////////////////////////////
     C     LOC           BEGSR
      *_______________
      * 3227 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          LOC_3227          3
      *________
      * C517 ID
      *""""""""
      * 3225 ID
     C                   EXSR      GETDED
     C                   SELECT
     C     LOC_3227      WHENEQ    '18'
     C                   MOVEL     FELT          LOC_3225A
     C     LOC_3227      WHENEQ    '122'
     C                   MOVEL     FELT          LOC_3225B
     C     LOC_3227      WHENEQ    'ZIL'
     C                   MOVEL     FELT          LOC_3225C
      * 1131 Lagertyp
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        LOCEND
     C                   MOVEL     FELT          LOC_1131
      * 3224 Landkod
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   MOVEL     FELT          LOC_3224
     C                   ENDSL
     C     LOCEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle MEA-segmentet                      MEA  /
      *////////////////////////////////////////////////////////////
     C     MEA           BEGSR
      *_______________
      * 6311 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
      *_____________
      * C502 DETAILS
      *"""""""""""""
     C                   EXSR      GETDED
      *____________
      * C174 VALUES
      *""""""""""""
      * 6411 qualifier
     C                   EXSR      GETDED
      * 6314 value
     C                   EXSR      GETDED
     C                   SELECT
     C     MEA_6314      WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314
     C     MEA_6314B     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314B
     C     MEA_6314C     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314C
     C     MEA_6314D     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314D
     C     MEA_6314E     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314E
     C     MEA_6314F     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314F
     C     MEA_6314G     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314G
     C     MEA_6314H     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314H
     C     MEA_6314I     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314I
     C     MEA_6314J     WHENEQ    *BLANKS
     C                   MOVEL     FELT          MEA_6314J
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle CNT-segmentet                      CNT  /
      *////////////////////////////////////////////////////////////
     C     CNT           BEGSR
      *_______________
      * 6069 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
      *___________________
      * 6066 ANTALL KOLLIN
      *"""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          CNT_6066
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle NAD-segmentet                      NAD  /
      *////////////////////////////////////////////////////////////
     C     NAD           BEGSR
     C                   EVAL      NAD_3039=*BLANKS
     C                   EVAL      NAD_1131=*BLANKS
     C                   EVAL      NAD_3036=*BLANKS
     C                   EVAL      NAD_3042A=*BLANKS
     C                   EVAL      NAD_3042B=*BLANKS
     C                   EVAL      NAD_3164=*BLANKS
     C                   EVAL      NAD_3251=*BLANKS
     C                   EVAL      NAD_3207=*BLANKS
      *_______________
      * 3035 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3035          2
      *__________________
      * C082 PARTY ID DET
      *""""""""""""""""""
      * 3039 Org.nr
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3039         35
     C     TEGN          CABEQ     LOGAPP        NADEND
     C     TEGN          CABEQ     LOGPLU        NAD001
      * 1131 Landkod
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_1131          2
     C     TEGN          CABEQ     LOGAPP        NADEND
      *______________________
      * C058 NAME AND ADDRESS
      *""""""""""""""""""""""
     C     NAD001        TAG
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        NADEND
      *________________
      * C080 PARTY NAME
      *""""""""""""""""
      * 3036 Namn
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3036         35
     C     TEGN          CABEQ     LOGAPP        NADEND
      *____________
      * C059 STREET
      *""""""""""""
      * 3042 Gatuadress 1
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3042A        35
     C     TEGN          CABEQ     LOGAPP        NADEND
     C     TEGN          CABEQ     LOGPLU        NAD002
      * 3042 Gatuadress 2
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3042B        35
     C     TEGN          CABEQ     LOGAPP        NADEND
      *_______________________
      * 3164 CITY (Postadress)
      *"""""""""""""""""""""""
     C     NAD002        TAG
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3164         35
     C     TEGN          CABEQ     LOGAPP        NADEND
      *____________________
      * 3229 COUNTRY SUB ID
      *""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        NADEND
      *________________
      * 3251 POSTNUMMER
      *""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3251         15
     C     TEGN          CABEQ     LOGAPP        NADEND
      *_____________
      * 3207 LANDKOD
      *"""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3207          2

     C     NADEND        TAG
     C                   SELECT
     C     NAD_3035      WHENEQ    'AE'
     C                   MOVEL     NAD_3039      NAD_3039A        12
     C                   MOVEL     NAD_1131      NAD_1131A         2
     C                   MOVEL     NAD_3036      NAD_3036A        35
     C                   MOVEL     NAD_3042A     NAD_3042AA       35
     C                   MOVEL     NAD_3042B     NAD_3042BA       35
     C                   MOVEL     NAD_3164      NAD_3164A        35
     C                   MOVEL     NAD_3251      NAD_3251A        15
     C                   MOVEL     NAD_3207      NAD_3207A         2
     C     NAD_3035      WHENEQ    'DT'
     C                   MOVEL     NAD_3039      NAD_3039B        12
     C                   MOVEL     NAD_1131      NAD_1131B         2
     C                   MOVEL     NAD_3036      NAD_3036B        35
     C                   MOVEL     NAD_3042A     NAD_3042AB       35
     C                   MOVEL     NAD_3042B     NAD_3042BB       35
     C                   MOVEL     NAD_3164      NAD_3164B        35
     C                   MOVEL     NAD_3251      NAD_3251B        15
     C                   MOVEL     NAD_3207      NAD_3207B         2
     C     NAD_3035      WHENEQ    'CZ'
     C                   MOVEL     NAD_3039      NAD_3039D        12
     C                   MOVEL     NAD_1131      NAD_1131D         2
     C                   MOVEL     NAD_3036      NAD_3036D        35
     C                   MOVEL     NAD_3042A     NAD_3042AD       35
     C                   MOVEL     NAD_3042B     NAD_3042BD       35
     C                   MOVEL     NAD_3164      NAD_3164D        35
     C                   MOVEL     NAD_3251      NAD_3251D        15
     C                   MOVEL     NAD_3207      NAD_3207D         2
     C     NAD_3035      WHENEQ    'CN'
     C                   MOVEL     NAD_3039      NAD_3039E        12
     C                   MOVEL     NAD_1131      NAD_1131E         2
     C                   MOVEL     NAD_3036      NAD_3036E        35
     C                   MOVEL     NAD_3042A     NAD_3042AE       35
     C                   MOVEL     NAD_3042B     NAD_3042BE       35
     C                   MOVEL     NAD_3164      NAD_3164E        35
     C                   MOVEL     NAD_3251      NAD_3251E        15
     C                   MOVEL     NAD_3207      NAD_3207E         2
     C                   ENDSL

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle PAC-segmentet                      PAC  /
      *////////////////////////////////////////////////////////////
     C     PAC           BEGSR
      * 7077 Kollislag
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   SELECT
     C     PAC_7077A     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077A
     C     PAC_7077B     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077B
     C     PAC_7077C     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077C
     C     PAC_7077D     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077D
     C     PAC_7077E     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077E
     C     PAC_7077F     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077F
     C     PAC_7077G     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077G
     C     PAC_7077H     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077H
     C     PAC_7077I     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PAC_7077I
     C                   ENDSL
      * 7064 Antall kolli
     C                   EXSR      GETDED
     C                   SELECT
     C     PAC_7064A     WHENEQ    *BLANKS
     C     PAC_70641     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064A
     C     PAC_7064B     WHENEQ    *BLANKS
     C     PAC_70642     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064B
     C     PAC_7064C     WHENEQ    *BLANKS
     C     PAC_70643     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064C
     C     PAC_7064D     WHENEQ    *BLANKS
     C     PAC_70644     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064D
     C     PAC_7064E     WHENEQ    *BLANKS
     C     PAC_70645     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064E
     C     PAC_7064F     WHENEQ    *BLANKS
     C     PAC_70646     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064F
     C     PAC_7064G     WHENEQ    *BLANKS
     C     PAC_70647     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064G
     C     PAC_7064H     WHENEQ    *BLANKS
     C     PAC_70648     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064H
     C     PAC_7064I     WHENEQ    *BLANKS
     C     PAC_70649     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_7064I
     C                   ENDSL
     C     TEGN          CABEQ     LOGAPP        PACEND
      * 7064 Antall delar
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   SELECT
     C     PAC_70641     WHENEQ    *BLANKS
     C     PAC_7064A     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70641
     C     PAC_70642     WHENEQ    *BLANKS
     C     PAC_7064B     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70642
     C     PAC_70643     WHENEQ    *BLANKS
     C     PAC_7064C     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70643
     C     PAC_70644     WHENEQ    *BLANKS
     C     PAC_7064D     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70644
     C     PAC_70645     WHENEQ    *BLANKS
     C     PAC_7064E     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70645
     C     PAC_70646     WHENEQ    *BLANKS
     C     PAC_7064F     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70646
     C     PAC_70647     WHENEQ    *BLANKS
     C     PAC_7064G     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70647
     C     PAC_70648     WHENEQ    *BLANKS
     C     PAC_7064H     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70648
     C     PAC_70649     WHENEQ    *BLANKS
     C     PAC_7064I     ANDEQ     *BLANKS
     C                   MOVEL     FELT          PAC_70649
     C                   ENDSL
     C     PACEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle PCI-segmentet                      PCI  /
      *////////////////////////////////////////////////////////////
     C     PCI           BEGSR
      *__________________________
      * 4233 MARKING INTSRUCTIONS
      *""""""""""""""""""""""""""
     C                   EXSR      GETDED
      *__________________
      * C210 MARKS&LABELS
      *""""""""""""""""""
      * 7102 Godsmærkning
     C                   EXSR      GETDED
     C                   SELECT
     C     PCI_7102A     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PCI_7102A
     C     PCI_7102C     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PCI_7102C
     C     PCI_7102E     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PCI_7102E
     C     PCI_7102G     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PCI_7102G
     C                   ENDSL
     C     TEGN          CABEQ     LOGAPP        PCIEND
      * 7102 Godsmærkning
     C                   EXSR      GETDED
     C                   SELECT
     C     PCI_7102B     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PCI_7102B
     C     PCI_7102D     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PCI_7102D
     C     PCI_7102F     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PCI_7102F
     C     PCI_7102H     WHENEQ    *BLANKS
     C                   MOVEL     FELT          PCI_7102H
     C                   ENDSL
     C     PCIEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle RFF-segmentet                      RFF  /
      *////////////////////////////////////////////////////////////
     C     RFF           BEGSR
      *_______________
      * C506 REFERENCE
      *"""""""""""""""
      * 1153 Qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          RFF_1153          3
      * 1154 Number
     C                   EXSR      GETDED
     C                   SELECT
     C     RFF_1153      WHENEQ    'ZTI'
     C                   MOVEL     FELT          DOC_1004         10
     C     RFF_1153      WHENEQ    'ZDN'
     C                   MOVEL     FELT          DOC_1001          3
     C     RFF_1153      WHENEQ    'ABE'
     C                   MOVEL     FELT          RFF_1154A        35
     C     RFF_1153      WHENEQ    'ACD'
     C                   SELECT
     C     RFF_1154B     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154B        35
     C     RFF_1154C     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154C        35
     C     RFF_1154D     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154D        35
     C     RFF_1154E     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154E        35
     C     RFF_1154F     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154F        35
     C     RFF_1154G     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154G        35
     C     RFF_1154H     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154H        35
     C     RFF_1154I     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154I        35
     C     RFF_1154J     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154J        35
     C     RFF_1154K     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154K        35
     C     RFF_1154L     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154L        35
     C     RFF_1154M     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154M        35
     C     RFF_1154N     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154N        35
     C     RFF_1154O     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154O        35
     C     RFF_1154P     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154P        35
     C     RFF_1154Q     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_1154Q        35
     C                   ENDSL
     C     RFF_1153      WHENEQ    'AAQ'
     C                   SELECT
     C     RFF_11541     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_11541
     C     RFF_11542     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_11542
     C     RFF_11543     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_11543
     C     RFF_11544     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_11544
     C     RFF_11545     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_11545
     C     RFF_11546     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_11546
     C     RFF_11547     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_11547
     C     RFF_11548     WHENEQ    *BLANKS
     C                   MOVEL     FELT          RFF_11548
     C                   ENDSL
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for UNB                                           UNB  /
      *////////////////////////////////////////////////////////////
     C     UNB           BEGSR
      *___________________
      * Clear recordformat
      *"""""""""""""""""""
     C                   CLEAR                   EDISR
     C                   EVAL      XMBR=WMBR
     C                   EVAL      SSN=XMBRB
     C                   MOVE      'C'           SST
     C                   MOVE      'R'           SSR
S01  C*                  MOVE      ' '           SST
     C                   MOVEL     WDATO         SDT
     C                   MOVEL     WTID          STM
     C                   MOVEL     WLIB          SLIB
     C                   MOVEL     WFIL          SFILE
     C                   MOVEL     WMBR          SMBR
     C                   EVAL      S0036=1
      *_______________________
      * S001 SYNTAX IDENTIFIER
      *"""""""""""""""""""""""
      * 0001 Syntax identifier
     C                   EXSR      GETDED
      * 0002 Syntax version number
     C                   EXSR      GETDED
      *________________________
      * S002 INTERCHANGE SENDER
      *""""""""""""""""""""""""
      * 0004 Sender identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0004
     C     TEGN          CABEQ     LOGPLU        UNB001
      * 0007 Partner identification code qualifier
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGPLU        UNB001
      * 0008 Address for reverse routing
     C                   EXSR      GETDED
      *___________________________
      * S003 INTERCHANGE RECIPIENT
      *"""""""""""""""""""""""""""
     C     UNB001        TAG
      * 0010 Recipient identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0010
     C     TEGN          CABEQ     LOGPLU        UNB002
      * 0007 Partner identification code qualifier
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGPLU        UNB002
      * 0014 Routing address
     C                   EXSR      GETDED
      *______________________________
      * S004 DATE/TIME OF PREPARATION
      *""""""""""""""""""""""""""""""
     C     UNB002        TAG
      * 0017 Date of preparation
     C                   EXSR      GETDED
      * 0019 Time of preparation
     C                   EXSR      GETDED
      *___________________________________
      * 0020 INTERCHANGE CONTROL REFERENCE
      *"""""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0020
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *___________________________________
      * S005 RECIPIENTS REFERENCE PASSWORD
      *"""""""""""""""""""""""""""""""""""
      * 0022 Recipients reference/password
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
     C     TEGN          CABEQ     LOGPLU        UNB003
      * 0025 Recipients reference/password qualifier
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *___________________________
      * 0026 APPLICATION REFERENCE
      *"""""""""""""""""""""""""""
     C     UNB003        TAG
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0026
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *______________________________
      * 0029 PROCESSING PRIORITY CODE
      *""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *_____________________________
      * 0031 ACKNOWLEDGEMENT REQUEST
      *"""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *________________________________
      * 0032 COMMUNICATION AGREEMENT ID
      *""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *____________________
      * 0035 TEST INDICATOR
      *""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0035
     C     UNBEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for UNZ                                           UNZ  /
      *////////////////////////////////////////////////////////////
     C     UNZ           BEGSR
      *_______________________________
      * 0036 INTERCHANGE CONTROL COUNT
      *"""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEA     FELT          NU
     C                   MOVE      *ZEROS        NUMVRD
     C     1             DO        6             NR                                         '+'
     C     NU(NR)        IFLT      '0'
     C     NU(NR)        ORGT      '9'
     C                   GOTO      UNZ001
     C                   END
     C                   MULT      10            NUMVRD
     C                   MOVE      NU(NR)        NUTEGN
     C                   ADD       NUTEGN        NUMVRD
     C                   END
     C     UNZ001        TAG
     C                   Z-ADD     NUMVRD        S0036
      *_______________
      * Skriv til EDIS
      *"""""""""""""""
     C     WNUMR         MULT      80            SMBRS
     C                   WRITE     EDISR
      *____________________________________________
      * Skicka positiv eller negativ respons til TV
      *""""""""""""""""""""""""""""""""""""""""""""
     C     WMBR          CHAIN     EDISX                              51
     C     *IN51         CABEQ     *ON           UNZEND
      * Kontroller sikkerhet i innkommende edifact
     C                   CALL      'SVG006R'
     C                   PARM                    SXIFSOBJ
     C                   PARM                    RESULT           20
     C                   MOVEL     RESULT        RES11            11
      * Skicka CONTRL i retur till Tullverket
     C     RES11         IFEQ      'Verified OK'
     C                   CALL      'SVG007R'                                    Positiv
     C                   PARM                    S0020
     C                   CALL      'SVG008R2'                                   Positiv
     C                   PARM                    S0020
     C                   PARM                    W0062
     C                   ELSE
     C                   CALL      'SVG008R'                                    Negativ
     C                   PARM                    S0020
     C                   ENDIF

     C     UNZEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle UNH-segmentet                      UNH  /
      *////////////////////////////////////////////////////////////
     C     UNH           BEGSR
      *___________________
      * Clear recordformat
      *"""""""""""""""""""
     C                   CLEAR                   EDIMR
     C                   CLEAR                   SVUHR
     C     1             CHAIN     EDIC
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   EVAL      MSR='R'
     C                   EVAL      MST='C'
     C                   EVAL      MSN=SSN
     C                   EVAL      MMN=CMN
     C                   EVAL      M0004=S0004
     C                   EVAL      M0010=S0010
     C                   EVAL      M0035=S0035
     C                   EVAL      MDT=SDT
     C                   EVAL      MTM=STM
      *______________________________
      * 0062 MESSAGE REFERENCE NUMBER
      *""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0062
     C                   IF        W0062=*BLANKS
     C                   MOVEL     FELT          W0062            14
     C                   ENDIF
      *________________________
      * S009 MESSAGE IDENTIFIER
      *""""""""""""""""""""""""
      * 0065 Message identifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0065
      * 0052 Message type version number
     C                   EXSR      GETDED
      * 0054 Message type release number
     C                   EXSR      GETDED
      * 0051 Controlling agency
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNHEND
     C     TEGN          CABEQ     LOGPLU        UNH001
      * 0057 Association assigned code
     C                   EXSR      GETDED
     C                   MOVEL     FELT          X0057            10
     C     TEGN          CABEQ     LOGAPP        UNHEND
      *_____________________________
      * 0068 COMMON ACCESS REFERENCE
      *"""""""""""""""""""""""""""""
     C     UNH001        TAG
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0068
     C     TEGN          CABEQ     LOGAPP        UNHEND
      *____________________________
      * S010 STATUS OF THE TRANSFER
      *""""""""""""""""""""""""""""
      * 0070 Sequence message transfer number
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNHEND
      * 0073 First/last sequence message transfer indication
     C                   EXSR      GETDED
     C     UNHEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for UNT                                           UNT  /
      *////////////////////////////////////////////////////////////
     C     UNT           BEGSR
     C                   IF        WUNT=*BLANKS
     C                   MOVE      'X'           WUNT              1
      *_____________________________________
      * 0074 NUMBER OF SEGMENTS IN A MESSAGE
      *"""""""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
      *______________________________
      * 0062 MESSAGE REFERENCE NUMBER
      *""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   WRITE     EDIMR
     C                   WRITE     SVUHR
     C                   CALL      'SVU002R'
     C                   PARM                    DOC_1004
     C                   PARM      'C'           CALLTYP           1
     C                   ENDIF

     C                   ENDSR
