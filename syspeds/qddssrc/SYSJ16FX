********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 F12 = AVBRYT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'FAX MED COVERPG.')
N01  A                                      CF12(12 'AVBRYT')
     A  27                                  CF17(17 'FAX UTEN COVERPG')
     A                                  1  3'SYSPED/SYSJ16X'
     A                                      COLOR(BLU)
     A                                  1 21'SYSPED      FAX-UTILITY'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 73TIME
     A                                      COLOR(GRN)
     A                                  2 21MSGCON(040 SHU0203 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              3 21MSGCON(040 SHU0344 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  3MSGCON(025 YBC0438 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0B  5 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            KNAVN         30A  O  5 40
     A                                  6  3MSGCON(025 SHU0167 *LIBL/SYMSGF)
     A            WSFAXN        15A  B  6 30DSPATR(UL)
     A  99                                  DSPATR(PC)
     A                                  6 46MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B  6 59
     A                                  6 62'='
     A            FAXRET         6A  O  6 64
     A                                  7  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B  7 11CHECK(LC)
     A  27                              8  3MSGCON(010 SHU0345 *LIBL/SYMSGF)
     A  27        WSCVP1        40A  B  8 17CHECK(LC)
     A  27        WSCVP2        40A  B 10 17CHECK(LC)
     A  27                             10  3MSGCON(010 SHU0346 *LIBL/SYMSGF)
     A  27        WSCVP3        40A  B 11 17CHECK(LC)
     A  27        WSCVP4        40A  B 12 17CHECK(LC)
     A  27                             14  3MSGCON(010 SHU0347 *LIBL/SYMSGF)
     A  27        WSCVP5        40A  B 14 17CHECK(LC)
     A  27        WSCVP6        40A  B 15 17CHECK(LC)
     A  27        WSCVP7        40A  B 16 17CHECK(LC)
     A  27                             18  3MSGCON(010 SHU0348 *LIBL/SYMSGF)
     A  27        WSCVP8        40A  B 18 17CHECK(LC)
     A  27                             23  2MSGCON(060 YBC0339 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23  2MSGCON(018 YBC0436 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N01  A N27                             23 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
N01  A                                      COLOR(BLU)
     A  99                             24  2MSGCON(078 SHU0169 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE02
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'FAX MED COVERPG.')
N01  A                                      CF12(12 'AVBRYT')
     A                                      CF17(17 'FAX UTEN COVERPG')
     A                                  1  3'SYSPED/SYSJ16X'
     A                                      COLOR(BLU)
     A                                  1 21'SYSPED      FAX-UTILITY'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 73TIME
     A                                      COLOR(GRN)
     A                                  1 21MSGCON(040 SHU0203 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 21MSGCON(040 SHU0344 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(025 YBC0438 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0B  3 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            KNAVN         30A  O  3 40
     A                                  4  3MSGCON(025 SHU0167 *LIBL/SYMSGF)
     A            WSFAXN        15A  B  4 30DSPATR(UL)
     A  99                                  DSPATR(PC)
     A                                  4 46MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B  4 59
     A                                  4 62'='
     A            FAXRET         6A  O  4 64
     A                                  5  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B  5 11CHECK(LC)
     A                                  6  3'Til:'
     A            WSCNV         30A  B  6 17DSPATR(UL)
     A            WSCAD1        30A  B  7 17DSPATR(UL)
     A            WSCAD2        30A  B  8 17DSPATR(UL)
     A            WSCAD3        30A  B  9 17DSPATR(UL)
     A                                 10  3'Attn:'
     A            WSCATT        30A  B 10 17DSPATR(UL)
     A                                 11  3'Besjed til mottager:'
     A            WSCF01        75A  B 12  3DSPATR(UL)
     A            WSCF02        75A  B 13  3DSPATR(UL)
     A            WSCF03        75A  B 14  3DSPATR(UL)
     A            WSCF04        75A  B 15  3DSPATR(UL)
     A            WSCF05        75A  B 16  3DSPATR(UL)
     A            WSCF06        75A  B 17  3DSPATR(UL)
     A            WSCF07        75A  B 18  3DSPATR(UL)
     A            WSCF08        75A  B 19  3DSPATR(UL)
     A            WSCF09        75A  B 20  3DSPATR(UL)
     A            WSCF10        75A  B 21  3DSPATR(UL)
     A            WSCF11        75A  B 22  3DSPATR(UL)
S01  A*                                23  2MSGCON(060 YBC0339 *LIBL/SYMSGF)
N01  A  27                             23  2MSGCON(060 YBC0339 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S01  A*                                23  2MSGCON(018 YBC0436 *LIBL/SYMSGF)
N01  A N27                             23  2MSGCON(018 YBC0436 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(078 SHU0169 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
