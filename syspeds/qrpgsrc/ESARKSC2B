      *********************************************************************
CRTPG+*  CRTPGM SYSPED/ESARKSC2B MODULE(*LIBL/ESARKSC2B *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd

     FBRIDF     IF   E           K DISK    USROPN
     FARKTXT    IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FKOSTA     IF   E           K DISK    USROPN
     FKOSTA14   IF   E           K DISK    USROPN
     F                                     RENAME(KOSTAR:KOSTAR1)
     FHEAD14    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADR14)
     FTURER14   IF   E           K DISK    USROPN
     FGSSORF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FLEVEL01   IF   E           K DISK    USROPN
      * ikke alle har HPOHL02 (L1), men med conditional open ved valg av L1 mapping så går det bra.
     FHPOHL02   IF   E           K DISK    USROPN
      *_____________________________
      * Variables common to all CGIs
      *"""""""""""""""""""""""""""""
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *_______________________
      * Client input variables
      *"""""""""""""""""""""""
     D WUSER           S             10
     D KOBL            S            100
     D DOC             S            100
     D KAT             S             50
     D AVD             S              4
     D OPD             S              7
     D UNDN7           S              7  0
     D TYPE            S              2
     D TYPETXT         S             70
     D FEILTEXT        S             70
     D AVSLUTT         S              1
      *________________
      * Andre variabler
      *""""""""""""""""
     D NULL            S              1A   INZ(X'00')
     D CMDLINE         S            512
     D CMDLEN          S             15  5
     D NEWDIR          S            100
     D NEWFILE         S            100
     D NEWOBJ          S            200
     D NEWEND          S              4
     D NEWRAN          S             10
     D                 DS
     D  AVDOPD                 1     11
     D  AVDN                   1      4  0
     D  OPDN                   5     11  0
     D                 DS
     D UND                     1     20
     D UND1_2                  1      2
     D UND3_9                  3      9
     D UND1_4                  1      4
     D UND5_11                 5     11
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI

      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input / cvt to numeric
     C                   EXSR      PARSE
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   EXSR      LOADHTML
      * Set section variables
     C                   EXSR      SETTOP
     C                   CALLP     wrtsection('top')
      * Skriv
     C                   EXSR      SETROW

     C                   CALLP     wrtsection('bot')

     C                   EXSR      EXIT

     C                   EVAL      *inlr = *on
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Hente variabler                                    PARSE /
      *////////////////////////////////////////////////////////////
     C     PARSE         BEGSR
      * Parse variables from QUERY_STRING environment variable
     C                   EVAL      KOBL     = zhbgetvar('KOBL')
     C                   EVAL      KAT      = zhbgetvar('KAT')
     C                   EVAL      AVD      = zhbgetvar('AVD')
     C                   EVAL      AVDN     = c2n2(AVD)
     C                   EVAL      OPD      = zhbgetvar('OPD')
     C                   EVAL      OPDN     = c2n2(OPD)
     C                   EVAL      UND      = zhbgetvar('UND')
     C                   EVAL      TYPE     = zhbgetvar('TYPE')
      * ved kall fra liste er type blank.
     C                   EVAL      AVSLUTT  = zhbgetvar('AVSLUTT')
     c     AVSLUTT       ifeq      ' '
     C     LC:UC         XLATE     TYPE          TYPE
     c                   end
      * Userid.....
     C                   EVAL      WUSER = zhbgetvar('USER')
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Close output html and quit                          EXIT /
      *////////////////////////////////////////////////////////////
     C     EXIT          BEGSR
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     ARKTXT
     C                   CLOSE     HEADF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Read output skeleton html member into memory    LOADHTML /
      *////////////////////////////////////////////////////////////
     C     LOADHTML      BEGSR
     C                   CALLP     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESARKSC2B':'/CGITAG/')
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Set variable data for section /$top               SETTOP /
      *////////////////////////////////////////////////////////////
     C     SETTOP        BEGSR
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   CALLP     updHTMLvar('USER':WUSER)
     C                   CALLP     updHTMLvar('BESK':BIBESK)
     C                   CALLP     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   CALLP     updHTMLvar('KOBL':KOBL)
     C                   CALLP     updHTMLvar('KAT':KAT)
     C                   CALLP     updHTMLvar('AVD':AVD)
     C                   CALLP     updHTMLvar('OPD':OPD)
     C                   CALLP     updHTMLvar('UND':UND)
      * Nullstill en del verdier
     C                   EVAL      *IN30=*OFF
     C                   EVAL      FEILTEXT=*BLANKS
     C                   EVAL      AVSLUTT='N'
      * Test Input
     c                   EXSR      TstInput
     c     FeilText      cabne     *blanks       NoVar
      *
      * Ingen feil, MOV fil og skriv til ARKIVP
     C     AVDN          IFGT      0
     C     OPDN          ORGT      0
     C     UND           ORNE      *BLANKS
     C                   EXSR      MOV
     C                   ENDIF
      * Oppdater variabel for selfclose/refresh liste
     C                   EVAL      AVSLUTT='Y'
      *
     C     NOVAR         TAG
     C                   CALLP     updHTMLvar('FEILTEXT':FEILTEXT)
     C                   CALLP     updHTMLvar('AVSLUTT':AVSLUTT)
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Set variable data for section /$row              SETROW  /
      *////////////////////////////////////////////////////////////
     C     SETROW        BEGSR
      * Når Type alt valgt (inp.param.) vis posten først i pulldown med 'SELECTED'
      * Deretter alle andre
     c     '  '          setll     ARKTXT
     C                   READ      ARKTXT                                 51
     C     *IN51         DOWEQ     *OFF
     c     ARSBAN        IFne      *blanks
     c                   exsr      GetUlFlr
     c                   eval      TypeTxt=%trim(ARTXT)+' Mappe:'+UlFlr
     c     TYPE          ifne      *blanks
     c     TYPE          andeq     ARTYPE
     C                   callp     updhtmlvar('RSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('RSEL':spaces)
     c                   end
     C                   CALLP     updHTMLvar('TYPE':ARTYPE)
     C                   CALLP     updHTMLvar('TYPETXT':TYPETXT)
     C                   CALLP     wrtsection('row')
     c                   end
     C                   READ      ARKTXT                                 51
     C                   ENDDO
     C                   ENDSR
      *
      *
      *********************************************************************
     c     TstInput      Begsr
      *********************************************************************
      *
     C     TYPE          CHAIN     ARKTXT                             30
     C                   IF        *IN30
     C     Type          IFEQ      *BLANKS
     c                   movel     '( blank )'   typblan          10
     c                   else
     c                   eval      typblan='( '+TYPE+' )'
     c                   end
     C                   EVAL      FEILTEXT='Oppgitt type '+%trim(typblan)
     C                             +' finnes ikke, Velg fra lista.'
     C                   GOTO      UtFeil
     C                   ENDIF
      * Er noe utfyllt ?
     C     AVD           IFEQ      *BLANKS
     C     OPD           ANDEQ     *BLANKS
     C     UND           ANDEQ     *BLANKS
     C                   EVAL      FEILTEXT='Avd/Oppdrag eller Annet '
     C                             +' må angis!'
     C                   GOTO      UtFeil
     C                   ENDIF
      *
      * NOE er utfylt - videre testing avhenig av UpLoadFolder
     c                   exsr      GetUlFlr
     c                   movel     UlFlr         TstFlr6           6
      *
      *  AVD OPPD er utfylt - Kontroller..
     C     UND           IFEQ      *BLANKS
      * tillatt til oppdrag på denne type ???
     c     TstFlr6       ifne      'OPPDRA'
     c     TstFlr6       andne     'FRBREV'
     C                   EVAL      FEILTEXT='Oppdragsmatch ikke tillatt på '
     c                             + 'type ' + ARTXT
     C                   GOTO      UtFeil
     c                   endif
     C     HEADKEY       CHAIN     HEADF                              30
     C                   IF        *IN30
     C                   EVAL      FEILTEXT='Angitt Avd/Oppdrag finnes ikke !'
     C                   GOTO      UtFeil
     C                   ENDIF
     C                   ENDIF
      *
      *  ANNET  er utfylt - Kontroller.. avhengig av TYPE
      *
     C     UND           IFNE      *BLANKS
      *
     c                   select
      *
     c     UlFlr         wheneq    'OPPDRAG'
     C                   EVAL      FEILTEXT='Denne type MÅ matches via avd/opd!'
     C                   GOTO      UtFeil
      *
     c     UlFlr         wheneq    'KBILAG'
     C                   OPEN      KOSTA
     C                   OPEN      KOSTA14
     C                   EVAL      UNDN7    = c2n2(UND)
     c                   movel     UNDN7         UND
     C     UNDN7         CHAIN     KOSTA14                            30
     C   30UNDN7         CHAIN     KOSTA                              30
     C                   CLOSE     KOSTA
     C                   CLOSE     KOSTA14
     C                   IF        *IN30
     C                   EVAL      FEILTEXT='Angitt bilag finnes ikke i '
     C                             + 'SYSPED kostnadsføring.!'
     C                   GOTO      UtFeil
     C                   ENDIF
      *
     c     UlFlr         wheneq    'L1BILAG'
     C                   OPEN      HPOHL02
     C                   EVAL      PERAAR   = c2n2(UND1_2)
     C                   add       2000          PERAAR
     C                   EVAL      BILNR    = c2n2(UND3_9)
     c                   move      BILNR         UND3_9
     C     L1Key         CHAIN     HPOHL02                            30
     C                   CLOSE     HPOHL02
     C                   IF        *IN30
     C                   EVAL      FEILTEXT='Angitt bilag finnes ikke i L1!'
     C                             + ' (HUSK 2 første = ÅR !!!)'
     C                   GOTO      UtFeil
     C                   ENDIF
      *
     c     UlFlr         wheneq    'TUR'
     C                   OPEN      TURER14
     C                   OPEN      HEAD14
     C                   EVAL      TUPRO    = c2n2(UND)
     c                   movel     TUPRO         UND
     C     TUPRO         CHAIN     TURER14                            30
     C   30TUPRO         CHAIN     HEAD14                             30
     C                   CLOSE     TURER14
     C                   CLOSE     HEAD14
     C                   IF        *IN30
     C                   EVAL      FEILTEXT='Angitt TURNUMMER finnes ikke!'
     C                   GOTO      UtFeil
     C                   ENDIF
      *
     c     UlFlr         wheneq    'SS'
     C                   OPEN      GSSORF
     C                   EVAL      ORONO    = c2n2(UND)
     c                   movel     ORONO         UND
     C     ORONO         CHAIN     GSSORF                             30
     C                   CLOSE     GSSORF
     C                   IF        *IN30
     C                   EVAL      FEILTEXT='Angitt SHIPsordre finnes ikke!'
     C                   GOTO      UtFeil
     C                   ENDIF
      *
     c     UlFlr         wheneq    'KUNDE'
     C                   OPEN      CUNDL01
     C                   EVAL      KUNDNR   = c2n2(UND)
     c                   movel     KUNDNR        UND
     C     KunKey        CHAIN     CUNDL01                            30
     C                   CLOSE     CUNDL01
     C                   IF        *IN30
     C                   EVAL      FEILTEXT='Angitt kunde finnes ikke!'
     C                   GOTO      UtFeil
     C                   ENDIF
      *
     c     UlFlr         wheneq    'LEVE'
     C                   OPEN      LEVEL01
     C                   EVAL      KUNDNR   = c2n2(UND)
     c                   movel     KUNDNR        UND
     C     KunKey        CHAIN     LEVEL01                            30
     C                   CLOSE     LEVEL01
     C                   IF        *IN30
     C                   EVAL      FEILTEXT='Angitt leverandør finnes ikke!'
     C                   GOTO      UtFeil
     C                   ENDIF
      *
     c                   endsl
     C                   ENDIF
      *
     C     UtFeil        ENDSR
      *
      **********************************************
     c     GetUlFlr      Begsr
      **********************************************
      * finn Upload-mappe (bak siste /) i skann/upload-path
     c                   move      *blanks       UlFlr            30
     c                   z-add     1             St                3 0
     c                   z-add     1             Sl                3 0
     C                   seton                                        33
     c     *in33         doweq     '1'
     c                   eval      St=Sl+1
     C     '/'           SCAN      ARSBAN:St     Sl                       33
     c                   end
     c     St            ifgt      2
     c                   eval      UlFlr = %subst(arsban:st)
     c                   endif
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Init                                                INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      * Definiere variabler
     C                   OPEN      BRIDF
     C     WUSER         CHAIN     BRIDF                              50
      * Definiere parameterliste
     C     QCMDEXC       PLIST
     C                   PARM                    CMDLINE
     C                   PARM                    CMDLEN
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C                   OPEN      ARKTXT
     C                   OPEN      HEADF

     C     HEADKEY       KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C     L1Key         KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    PERAAR
     C                   KFLD                    BILNR
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    KUNDNR

      *
     C                   ENDSR
      *
      *********************************************
     c     MOV           begsr
      *********************************************
      * DOC = TYPE/FILNAVN (UTEN BANE og extention)
     c     UND           ifne      *blanks
     c                   eval      DOC = Type + UND
     c                   else
     c                   eval      DOC = Type + AvdOpd
     c                   end
      * finn extention name Ye=startpkt max 5 lang inkl punktum
     C     '.'           SCAN      KOBL          X                 3 0
     C                   EVAL      NEWEND = %SUBST(KOBL:X:4)
     c                   move      *blanks       Extent            5
     c                   move      *zeros        Xe                3 0
     c                   move      *zeros        Ye                3 0
     C     '.'           Scan      KOBL          Xe                       33
     c     *IN33         doweq     '1'
     c                   move      Xe            Ye
     c                   add       1             Xe
     C     '.'           SCAN      KOBL:Xe       Xe                       33
     c                   end
     c     Ye            ifne      *zeros
     C                   EVAL      EXTENT = %SUBST(KOBL:Ye:5)
     c                   end
      * Bane + navn på utfil (inkl. extention) og timestamp(for å tillate flere mot samme)
     c                   time                    Timedata1
     c                   movel     timedata1     TS26             26
     c                   movel     TS26          TS               22
     C                   eval      NewOBJ = %trim(ARSBAN)+'/'
     C                             +%trim(DOC)+%trim(EXTENT)
     c                             +'_'+%trim(TS)
      * Flytte objekt
     C                   CALL      'ESARKS2C'
     C                   PARM                    KOBL
     C                   PARM                    NEWOBJ          200
     C                   ENDSR
      *
