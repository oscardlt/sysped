     h debug(*yes)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 KODE FOR ØREBEREGNING
********************************************************************
     FSYTIRCV   IP   F  256        DISK
     FKODTS8NY  UF A E           K DISK
     FSADSDNY   UF A E           K DISK
     FKODTS8    IF   E           K DISK    RENAME(KOS8R:KOS8RGM)
     F                                     PREFIX(GM_)
     D                 DS
     D  SDDTFÅ                 1      4  0
     D  SDDTFM                 5      6  0
     D  SDDTFD                 7      8  0
     D  SDDTF                  1      8  0
     D                 DS
     D  SDDTTÅ                 1      4  0
     D  SDDTTM                 5      6  0
     D  SDDTTD                 7      8  0
     D  SDDTT                  1      8  0
     D                 DS
     D  SATS                   1      8  2
     D  SATS1                  1      6  0
     D  SATS2                  7      8  0
     D                 DS
     D  KS8FTX                 1     52
     D  KS8MIL                52     52
     D  KS8ØRE                51     51
     ISYTIRCV   NC  01
     I                                  1    8  TARNR
     I                                 11   12  AVGKOD
     I                                 15   17  AVGSKV
     I                                 17   17  hoyven
     I                                 20   25 0SATS1
     I                                 27   28 0SATS2
     I                                 31   31  ENHET
     I                                 34   37  DTFÅ
     I                                 39   40  DTFM
     I                                 42   43  DTFD
     I                                 46   49  DTTÅ
     I                                 51   52  DTTM
     I                                 54   55  DTTD
     I                                 57   57  ENHET2
     I                                 58   58  ENHET3
      *
     C     *ENTRY        PLIST
     C                   PARM                    INST              1
      * ER MODUL INSTALLERT ?????
     C     MODOK         IFNE      'J'
     C                   MOVEL     'SYTADINT'    UMODUL           15
     C                   MOVE      '       '     UMODUL
     C                   CALL      'SYPRODT'
     C                   PARM                    UMODUL
     C                   PARM                    UMFLAG            1 0
     C     UMFLAG        IFEQ      1
     C                   MOVE      UMFLAG        INST
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C                   MOVE      'J'           MODOK             1
     C                   MOVE      *BLANKS       KS8FTX
     C                   END
     C*
     C*
     C     SADSDK        KLIST
     C                   KFLD                    SDTNRF
     C                   KFLD                    SDKDAÆ
     C                   KFLD                    SDKDSÆ
     C                   KFLD                    SDDTF
     C*
     C     KODS8H        KLIST
     C                   KFLD                    XS8AVG
     C                   KFLD                    XS8SKH            3
     C     KODS8K        KLIST
     C                   KFLD                    XS8AVG
     C                   KFLD                    XS8SKV
      *
      *
      ***********
      * KODTS8
      ***********
     C                   MOVE      AVGKOD        XS8AVG            2
     C     AVGKOD        IFEQ      'MV'
     c     hoyven        andne     ' '
     C                   MOVE      *BLANKS       XS8SKV
     C                   MOVE      AVGSKV        XXXSKV            1
     C                   MOVEL     XXXSKV        XS8SKV
     C                   ELSE
     C                   MOVE      AVGSKV        XS8SKV            3
     C                   END
     C                   SELECT                                                   3<-B
     C     ENHET         WHENEQ    'L'
     C                   MOVE      'L'           XS8STY
     C     ENHET         WHENEQ    'S'
     C                   MOVE      *BLANKS       XS8STY
     C     ENHET         WHENEQ    'B'
     C                   MOVE      'B'           XS8STY
     C     ENHET         WHENEQ    'A'
     C                   MOVE      'A'           XS8STY
     C     ENHET         WHENEQ    'M'
     C                   MOVE      *BLANKS       XS8STY            1
     C     ENHET         WHENEQ    'P'
     C                   MOVE      '%'           XS8STY
     C     ENHET         WHENEQ    'R'
     C                   MOVE      'P'           XS8STY
     C     ENHET         WHENEQ    'H'
     C                   MOVE      'H'           XS8STY
     C     ENHET         WHENEQ    'K'
     C                   MOVE      'K'           XS8STY
     C     ENHET         WHENEQ    'D'
     C                   MOVE      'D'           XS8STY
     C     ENHET         WHENEQ    'T'
     C                   MOVE      'T'           XS8STY
     C     ENHET         WHENEQ    'G'
     C                   MOVE      'G'           XS8STY
     C                   OTHER
     C                   MOVE      ENHET         XS8STY
     C                   ENDSL                                                    3<-E
     C                   MOVE      ENHET2        XS8ØRE            1
     c     enhet3        ifeq      '*'
     c                   move      enhet3        XS8ØRE
     c                   end
     C     XS8AVG        CHAIN     KODTS8                             33
     C   33              EVAL      GM_KS8FTX = *BLANKS
      *
      * HEADER/GRUNN-DEFINISJON MED BLANK I SEKVENS...
     C     KODS8H        CHAIN     KODTS8NY                           33
     C     *IN33         IFEQ      '1'                                            3<-B
     C                   MOVE      XS8AVG        KS8AVG
     C                   MOVE      *BLANKS       KS8SKV
     C                   MOVE      XS8STY        KS8STY
     C                   MOVE      *ZEROS        KS8SAT
S01  C*                  MOVE      XS8ØRE        KS8ØRE
     C                   MOVE      GM_KS8FTX     KS8FTX
N01  C                   MOVE      XS8ØRE        KS8ØRE
     C                   WRITE     KOS8R
     C                   END                                                      3<-E
      * AVGIFT/SEKVENS (MED SATS.. FRA FØRSTE FOREKOMST.)
     C     KODS8K        CHAIN     KODTS8NY                           33
     C     *IN33         IFEQ      '1'                                            3<-B
     C                   MOVE      XS8SKV        KS8SKV
     C                   MOVE      XS8STY        KS8STY
     C                   Z-ADD     SATS          KS8SAT
S01  C*                  MOVE      XS8ØRE        KS8ØRE
     C                   MOVE      GM_KS8FTX     KS8FTX
N01  C                   MOVE      XS8ØRE        KS8ØRE
     C                   WRITE     KOS8R
     C                   END                                                      3<-E
      *
      ***********
      * SADSD
      ***********
     C                   MOVE      TARNR         SDTNRF
     C                   MOVE      AVGKOD        SDKDAÆ
     C     AVGKOD        IFEQ      'MV'
     c     hoyven        andne     ' '
     C                   MOVE      *BLANKS       SDKDSÆ
     C                   MOVE      AVGSKV        XXXSKV
     C                   MOVEL     XXXSKV        SDKDSÆ
     C                   ELSE
     C                   MOVE      AVGSKV        SDKDSÆ
     C                   END
     C                   MOVE      DTFÅ          SDDTFÅ
     C                   MOVE      DTFM          SDDTFM
     C                   MOVE      DTFD          SDDTFD
     C                   MOVE      DTTÅ          SDDTTÅ
     C                   MOVE      DTTM          SDDTTM
     C                   MOVE      DTTD          SDDTTD
     C     SADSDK        CHAIN     SADSDNY                            33
     C                   Z-ADD     SATS          SDBLSÆ
     C   33              WRITE     SADSDR
     C  N33              UPDATE    SADSDR
      *
     C                   MOVE      '0'           INST
      *
