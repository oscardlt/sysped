/* *********************************************************************   */
/*  TOLKE "MELDINGSTYPE" I FLATFIL - LOGGE                                 */
/*SYSTEMA*/
/*  STATEMENTS MERKET SYSTEMA MÅ NORMALT TILPASSES DEN ENKELTE KUNDE       */
/* 11XXX * 01 YBC PTF11 KONVERTER XML (KVITTERING TIL EHF-FAKTURA)         */
/* *********************************************************************   */
             PGM        PARM(&FILE &LIBRARY &MEMBER)
             DCL        VAR(&LIBRARY) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TYP) TYPE(*CHAR) LEN(3)
/*SYSTEMA*/  DCL        VAR(&FROM) TYPE(*CHAR) LEN(35) +
                          VALUE('NODNB101')
/*SYSTEMA*/  DCL        VAR(&FROCR) TYPE(*CHAR) LEN(35) +
                          VALUE('BBS/OCR ')
/*SYSTEMA*/  DCL        VAR(&TO) TYPE(*CHAR) LEN(35) +
                          VALUE('NO005405')
             DCL        VAR(&STRPOS) TYPE(*DEC) LEN(3) VALUE(2) /* +
                          Startposisjon for å lese datarea: EDIDTA */
             DCL        VAR(&USRID) TYPE(*CHAR) LEN(10) /* USERID +
                          FOR MSG*/
             DCL        VAR(&MSGTXT) TYPE(*CHAR) LEN(256)

     /*________________________________*/
     /* SJEKK OM INN- ELLER UTBETALINGER */
     /*""""""""""""""""""""""""""""""""*/
             CALL       PGM(EDIOCCR) PARM(&TYP &FROM &FROCR &TO +
                          &LIBRARY &FILE &MEMBER)
     /*_______________*/
     /* INNBETALINGER */
     /*"""""""""""""""*/
             IF         COND(&TYP *EQ 'OCR') THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('OCR - innbetalinger er +
                          ankommet, hentes inn til Løsning-1 via +
                          EDIMNU4 pkt 1')
             ENDDO
     /*_______________*/
     /* REMMITERING */
     /*"""""""""""""""*/
             IF         COND(&TYP *EQ 'REM') THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('Returdata fra +
                          remitering/utbetalinger er ankommet, +
                          hentes inn til Løsning-1 via +
                          EDIMNU4 pkt 1')
             ENDDO
     /*_______________*/
     /* BANKAVSTEMNING */
     /*"""""""""""""""*/
             IF         COND(&TYP *EQ 'AVR') THEN(DO) /*N01*/
             CHGVAR     VAR(&MSGTXT) VALUE('Bankavstemning er +
                          ankommet, hentes inn til PC-fil via +
                          EDIMNU4 pkt 1')
             ENDDO
     /*_______________________________________*/
     /* INTERN SYSPED-FAKTURA ("INVOICE")..*/
     /*"""""""""""""""""""""""""""""""""""""""*/
             IF         COND(&TYP *EQ 'IFA') THEN(DO)
             CALL       PGM(SYKS39CL) PARM(&MEMBER)
             GOTO       CMDLBL(END)
             ENDDO

     /*_______________________________________*/
     /* KUNDESPESIFIKE FLATFIL MELDINGSTYPER..*/
     /*"""""""""""""""""""""""""""""""""""""""*/
      /* FJERN STJERNINGEN/ENDRE DE STATEMENTS EN VIL AKTIVERE       */
      /* LAG  EN NY SEKVENS FOR HVER TYPE SOM SKAL BEARBEIDES....    */
      /*     IF         COND(&TYP *EQ 'XXX') THEN(DO)                */
      /*     CHGVAR     VAR(&MSGTXT) VALUE('Data fra XXXXXXX er +    */
      /*                  ankommet......')                           */
                /* KALLE SPESIFIKT PROGRAM FOR BEARBEIDINGEN....*/
      /*     CALL XXXXXXXX                                           */
                /* MELDING TIL ANDRE PERSONER/KØER ENN EDIDTA?*/
      /*    SNDMSG     MSG(' ' *BCAT &MSGTXT *BCAT ' ') TOUSR(&USRID)*/
      /*    MONMSG     MSGID(CPF0000)                                */
                /* HOPPE OVER MELDING TIL DE I EDIDTA?????    */
      /*     GOTO       CMDLBL(END)                                  */
      /*     ENDDO                                                   */
/*N01  START */
     /*_______________________________*/
     /* KVITTERING XML-FILER          */
     /*"""""""""""""""""""""""""""""""*/
             IF         COND(&TYP *EQ 'XML') THEN(DO)
             CALL       PGM(XMLRCVC) PARM(&LIBRARY &FILE &MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
/*N01  SLUTT */

     /*_______________________________________*/
     /* VARSEL TIL DE BRUKERE SOM ER DEFINERT */
     /*"""""""""""""""""""""""""""""""""""""""*/
GETUSR:      RTVDTAARA  DTAARA(EDIDTA (&STRPOS 10)) RTNVAR(&USRID)
             IF         COND(&USRID *NE ' ') THEN(DO)
             SNDMSG     MSG(' ' *BCAT &MSGTXT *BCAT ' ') TOUSR(&USRID)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(&STRPOS) VALUE(&STRPOS + 10)
             GOTO GETUSR
             ENDDO

 END:        ENDPGM
