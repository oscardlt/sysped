      *
      ***********************************************************
      *** KUNDE SPESIFIKE OPPDRAGSTILLEG (VIA FRIE SØKEVEIER) ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FCUOTI     UF A E           K DISK
     FCUOTIG    UF   E           K DISK
     FKODFRI    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FVCUNDOD   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KFSOTX                 1     30
     D  XDLT                   1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2AVD         YSAVD
     C                   MOVE      W2KODE        YSKODE
     C                   MOVE      *ZEROS        W2AVD
     C                   MOVE      *BLANKS       W2KODE
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     COKEYL        SETLL     CUOTI
     C     START1        TAG
     C     COKEY         READE(N)  CUOTI                                  94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        10
     C                   ADD       1             ZRECNO
     C                   MOVE      COKRAV        WSKRAV
     C     COKODE        CHAIN     KODFRI                             33
     C     COKDLT        CHAIN(N)  CUOTIG                             33
     c   33              eval      SFGyld = ' '
     c  N33              eval      SFGyld = 'Gyldighetskrav!'
     C                   WRITE     SUBFIL
     C     COKEY         READE(N)  CUOTI                                  94
     C  N94              MOVE      COAVD         YSAVD
     C  N94              MOVE      COKODE        YSKODE
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C                   SETOFF                                       9830
     C                   MOVEA     '000'         *IN(88)
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C*** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
     C*** SØKING
     C   04              DO
     C                   CALL      'SYFA65A'
     C                   PARM                    W2KODE
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      START3
     C                   END
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94COKEYL        SETLL     CUOTI
     C  N94COKEY         READE(N)  CUOTI                                  94
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C                   EXSR      TESTB
     C                   GOTO      START3
     C                   END
     C     W2AVD         IFNE      *ZEROS
     C                   GOTO      START
     C                   END
     C     W2KODE        IFNE      *BLANKS
     C                   GOTO      START
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     W2AVD         IFNE      *ZEROS
     C     AVDKEY        CHAIN     KODTA                              88
     C     *IN88         IFEQ      '1'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        30
     C                   MOVE      'SPA0001'     MSGNR
     C                   END
     C                   END
      *
     C     W2KODE        IFEQ      *BLANKS
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   SETON                                        9030
     C                   MOVE      'YBC0227'     MSGNR
     C                   ELSE
     C     W2KODE        CHAIN     KODFRI                             89
     C     *IN89         IFEQ      '1'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   SETON                                        8930
     C                   MOVE      'YBC0230'     MSGNR
     C                   END
     C                   END
      *
     C     W2KRAV        IFLT      '0'
     C     W2KRAV        ORGT      '2'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     45            POSNBR
     C                   SETON                                        8930
     C                   MOVE      'JOV0871'     MSGNR
     C                   END
      *
     C     COKUPD        CHAIN(N)  CUOTI                              33
     C     *IN33         IFEQ      '0'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        888930
     C                   MOVE      'YBC0229'     MSGNR
     C                   END
      *
     C   30              GOTO      SLTB
      *
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C                   MOVE      W2AVD         COAVD
     C                   MOVE      W2KODE        COKODE
     C                   MOVE      W2KRAV        COKRAV
     C                   WRITE     CUOTIR
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      COKRAV        WSKRAV
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2AVD
     C                   MOVE      *BLANKS       W2KODE
     C                   MOVE      *BLANKS       W2KRAV
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       5             LINNBR
     C     LINNBR        IFEQ      0
     C                   Z-ADD     5             LINNBR
     C                   END
      *
      *SLETTING
      *
     C     WSNR          IFEQ      '4'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     COKDLT        CHAIN     CUOTI                              33
     C     *IN33         IFEQ      '0'
     C                   DELETE    CUOTIR
      * slett også under-poster (gyldige på denne kunde/kode/avd)
     C     COKDLT        setll     CUOTIG
     C     COKDLT        reade     CUOTIG                                 33
     C     *in33         doweq     '0'
     C                   DELETE    CUOTIGR
     C     COKDLT        reade     CUOTIG                                 33
     c                   end
     c                   eval      SFGyld = ' '
     C                   MOVE      *ZEROS        COAVD
     C                   MOVE      *BLANKS       COKODE
     C                   MOVE      *BLANKS       COKRAV
     C                   MOVE      *BLANKS       KFSOTX
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      '1'           *IN99
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
      *ENDRING
      *
     C     WSNR          IFEQ      ' '
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C     COKDLT        CHAIN     CUOTI                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      WSKRAV        COKRAV
     C                   UPDATE    CUOTIR
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      '1'           *IN99
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
      *9= GYLDIGE
      *
     C     WSNR          IFEQ      '9'
     C                   MOVE      'V'           UMODE             1
     C                   CALL      'VCUNDO2R'
     C                   PARM                    COFIRM
     C                   PARM                    COKUNR
     C                   PARM                    COAVD
     C                   PARM                    COKODE
     C                   PARM                    DUM              15
     C                   PARM                    UMODE
     C                   MOVE      *BLANKS       WSNR
     C     COKDLT        CHAIN(N)  CUOTIG                             33
     c   33              eval      SFGyld = ' '
     c  N33              eval      SFGyld = 'Gyldighetskrav!'
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KUNKEY        KLIST
     C                   KFLD                    COFIRM
     C                   KFLD                    COKUNR
     C     AVDKEY        KLIST
     C                   KFLD                    WSAUNI
     C                   KFLD                    W2AVD
     C                   MOVE      'A'           WSAUNI            1
     C     COKEY         KLIST
     C                   KFLD                    COFIRM
     C                   KFLD                    COKUNR
     C     COKEYL        KLIST
     C                   KFLD                    COFIRM
     C                   KFLD                    COKUNR
     C                   KFLD                    YSAVD             4 0
     C                   KFLD                    YSKODE            3
     C     COKUPD        KLIST
     C                   KFLD                    COFIRM
     C                   KFLD                    COKUNR
     C                   KFLD                    W2AVD
     C                   KFLD                    W2KODE
     C     COKDLT        KLIST
     C                   KFLD                    COFIRM
     C                   KFLD                    COKUNR
     C                   KFLD                    COAVD
     C                   KFLD                    COKODE
      *
     C     *ENTRY        PLIST
     C                   PARM                    COFIRM
     C                   PARM                    COKUNR
      *
     C     KUNKEY        CHAIN     CUNDL01                            33
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
     C*
