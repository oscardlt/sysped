     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF04(04 'Spørr')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL045R'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 YBC1518 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  3MSGCON(015 YBC1519 *LIBL/SYMSGF)
     A            WSGRUP         8Y 0B  3 20DSPATR(HI)
     A                                      EDTCDE(4)
     A N49        NVNGRU        30A  O  3 29COLOR(WHT)
     A  49                              3 29MSGCON(045 YBC1520 *LIBL/SYMSGF)
     A                                  4  3MSGCON(015 YBC1521 *LIBL/SYMSGF)
     A            WSKJED         8Y 0B  4 20DSPATR(HI)
     A                                      EDTCDE(4)
     A            NVNKJE        30A  O  4 29COLOR(WHT)
     A                                  5  3MSGCON(015 YBC1522 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  5 20DSPATR(HI)
     A                                      EDTWRD('0      ')
     A                                  6  3MSGCON(040 YBC1524 *LIBL/SYMSGF)
     A                                  7  3MSGCON(030 YBC1525 *LIBL/SYMSGF)
     A            WSARTF        15A  B  7 34
     A                                  7 60MSGCON(015 YBC1526 *LIBL/SYMSGF)
     A                                  8  6MSGCON(027 YBC1527 *LIBL/SYMSGF)
     A            WSARTT        15A  B  8 34
     A                                  8 60MSGCON(025 YBC1528 *LIBL/SYMSGF)
     A                                  9  3MSGCON(020 YBC1529 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  3MSGCON(030 YBC1530 *LIBL/SYMSGF)
     A            WSLF1          5A  B 10 34
     A            WSLF2         20A  B 10 40
     A                                 10 60MSGCON(015 YBC1526 *LIBL/SYMSGF)
     A                                 11  6MSGCON(027 YBC1531 *LIBL/SYMSGF)
     A            WSLT1          5A  B 11 34
     A            WSLT2         20A  B 11 40
     A                                 11 60MSGCON(025 YBC1528 *LIBL/SYMSGF)
     A                                 12  3MSGCON(020 YBC1529 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 13  3MSGCON(030 YBC1322 *LIBL/SYMSGF)
     A            WSDATF         6Y 0B 13 34EDTCDE(4)
     A                                 13 41'/'
     A            WSDATT         6Y 0B 13 43EDTCDE(4)
     A                                 13 50MSGCON(025 YBC1528 *LIBL/SYMSGF)
     A                                 14 50MSGCON(020 YBC1523 *LIBL/SYMSGF)
     A                                 20  3MSGCON(030 YBC1533 *LIBL/SYMSGF)
     A            WSSORT         1A  B 20 34VALUES(' ' '1' '2' '3')
     A                                 20 38MSGCON(040 YBC1534 *LIBL/SYMSGF)
     A                                 21  3MSGCON(030 JOV0490 *LIBL/SYMSGF)
     A            WSPRTL        10A  B 21 34
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 29MSGCON(015 YBC1523 *LIBL/SYMSGF)
     A                                  5 45MSGCON(035 JOV1250 *LIBL/SYMSGF)
     A                                      COLOR(PNK)
      *
      * BILDE2 BRUKES IKKE
     A          R BILDE2
     A                                      WINDOW(*DFT 5 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'LL045R'
     A                                      COLOR(BLU)
     A                                  1  9'Utskrift av lager-telleliste'
     A                                      DSPATR(HI)
     A                                  2  2'Det finnes lager-telleliste fra '
     A                                      COLOR(YLW)
     A            LTLTDT         8  0O  3  2EDTWRD('    .  .  ')
     A                                      COLOR(RED)
     A                                  3 13'som ikke er oppdatert på '
     A                                      COLOR(YLW)
     A                                  4  2'lager.'
     A                                      COLOR(YLW)
     A                                  5  2'Vil du slette den?'
     A            WSSLET         1A  B  5 22DSPATR(UL)
     A                                      VALUES('N' 'J')
     A                                  5 25'(J/N)'
