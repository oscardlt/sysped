     H DFTACTGRP(*NO)
     fsaeh      uf a e           k disk
     fsaehko    uf a e           k disk
     fsaef      uf a e           k disk
     fsaefko    uf a e           k disk
     fsaev      uf a e           k disk
     fsaevcn    uf a e           k disk
     fsaevko    uf a e           k disk
     fsaevcnko  uf a e           k disk

     d xst             s              1a
     ‚*  Prototype for sad052x
     d sad052x         pr
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d umod_                               like(umod)
     ‚*  *ENTRY Interface for Main Procedure
     d sad052x         pi
     d uavd                           4  0
     d utdn                           7  0
     d umod                           1

      /free

       select;
       when umod = '1';    // Bevar opprinnelig SAD
         // Hode
         chain(n) (uavd: utdn) saehko;
         if not %found;
           chain (uavd: utdn) saeh;
           if %found;
             write saehkor;
           endif;
         endif;

         // Faktura
         chain(n) (uavd: utdn) saefko;
         if not %found;
           setll (uavd: utdn) saef;
           reade (uavd: utdn) saef;
           dow not %eof;
             write sadfkor;
             reade (uavd: utdn) saef;
           enddo;
         endif;

         // linje
         chain(n) (uavd: utdn) saevko;
         if not %found;
           setll (uavd: utdn) saev;
           reade (uavd: utdn) saev;
           dow not %eof;
             write saevkor;
             reade (uavd: utdn) saev;
           enddo;
         endif;

         // linje container
         chain(n) (uavd: utdn) saevcnko;
         if not %found;
           setll (uavd: utdn) saevcn;
           reade (uavd: utdn) saevcn;
           dow not %eof;
             write saevcnkor;
             reade (uavd: utdn) saevcn;
           enddo;
         endif;

       when umod = '2';    // Slett bevarte SAD
         // Hode
         chain (uavd: utdn) saehko;
         if %found;
           delete saehkor;
         endif;

         // Faktura
         setll (uavd: utdn) saefko;
         reade (uavd: utdn) saefko;
         dow not %eof;
           delete sadfkor;
           reade (uavd: utdn) saefko;
         enddo;

         // linje
         setll (uavd: utdn) saevko;
         reade (uavd: utdn) saevko;
         dow not %eof;
           delete saevkor;
           reade (uavd: utdn) saevko;
         enddo;

         // linje container
         setll (uavd: utdn) saevcnko;
         reade (uavd: utdn) saevcnko;
         dow not %eof;
           delete saevcnkor;
           reade (uavd: utdn) saevcnko;
         enddo;

       when umod = '3';    // Flytt bevarte SAD tilbake
         chain(n) (uavd: utdn) saehko;
         if %found;

           // Hode
           chain (uavd: utdn) saeh;
           if %found;
             xst = sest;
             delete saehr;
           endif;
           chain (uavd: utdn) saehko;
           sest = xst;
           se0035 = '7';
           write saehr;
           delete saehkor;

           // Faktura
           setll (uavd: utdn) saef;
           reade (uavd: utdn) saef;
           dow not %eof;
             delete sadfr;
             reade (uavd: utdn) saef;
           enddo;
           setll (uavd: utdn) saefko;
           reade (uavd: utdn) saefko;
           dow not %eof;
             write sadfr;
             delete sadfkor;
             reade (uavd: utdn) saefko;
           enddo;

           // linje
           setll (uavd: utdn) saev;
           reade (uavd: utdn) saev;
           dow not %eof;
             delete saevr;
             reade (uavd: utdn) saev;
           enddo;
           setll (uavd: utdn) saevko;
           reade (uavd: utdn) saevko;
           dow not %eof;
             write saevr;
             delete saevkor;
             reade (uavd: utdn) saevko;
           enddo;

           // linje container
           setll (uavd: utdn) saevcn;
           reade (uavd: utdn) saevcn;
           dow not %eof;
             delete saevcnr;
             reade (uavd: utdn) saevcn;
           enddo;
           setll (uavd: utdn) saevcnko;
           reade (uavd: utdn) saevcnko;
           dow not %eof;
             write saevcnr;
             delete saevcnkor;
             reade (uavd: utdn) saevcnko;
           enddo;

         endif;
       endsl;

       *inlr = *on;
       return;

      /end-free
