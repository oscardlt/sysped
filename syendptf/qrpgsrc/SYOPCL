      *********************************************************************
CRTPG+*  CRTPGM SYSPED/SYOPCL MODULE(*LIBL/SYOPCL *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 sumtekst ved mix av stykkgods / LM-gods
11XXX * 02 SH  PTF12 parametbr bhrurlcl
      * 03 JOV PTF12 Dersom kolli-ID innom flere terminaler - hent fra første
      * 04 JOV PTF12 blanke avvik og bilde på sumlinje (flytte bilde01 sammen med bilde2)
      * 05 JOV PTF12 usropn på BRPARF
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    usropn
     FDOKUFM    IF   E           K DISK    usropn
     FCARGOF    IF   E           K DISK    usropn
S05  F*RPARF    IF   E           K DISK
N05  FBRPARF    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
N03   * lookup-table for avviks-koder - max 100 a 5 tegn
N03  D                 DS
N03  D  AK                     1    500
N03  D                                     DIM(100)
N03   * lookup-table for avviks-tekst - max 100 a 25 tegn
N03  D                 DS
N03  D  AT                     1   2500
N03  D                                     DIM(100)
N03  D                 DS
N03  D  FMLEDI                 1     10
N03  D   AVVK_IN               1      5
N03  D   AVVK_OU               6     10
N03   *
N03  D AvvInn          s             25
N03  D AvvOut          s             25
      *
     D OddEven         s             10
      * Client input variables
     D user            s             10
     D AVD             s              4
     D OPD             s              7
     D AVDN            s              4  0
     D OPDN            s              7  0
N01  D SumTxt          s              4
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D DsDat                   1      8  0
     D  DsDatÅÅ                1      4  0
     D  DsDatMM                5      6  0
     D  DsDatDD                7      8  0
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
      * A KOLLI-IDS  PÅ LINJE NR 00
      *   1. Vis liste over stykkgods (IKKE LMindikator = 1)
     C                   MOVE      *ZEROS        XXLINR
     C     DokKey        SETLL     DOKUFM
     C     DokKey        READE     DOKUFM                                 35
     c     *IN35         doweq     '0'

     c                   move      ' '           KOLMIN
     C                   eval      KOID = '00'+FMMRK1
     C     CARGOFKey     Chain     CARGOF                             34
N03  C   34KOID          Chain     CARGOF                             34
      *
     c                   if        KOLMIN = '1'
     c                   add       1             AntLM1            5 0
     c                   else
      *   i denne syklus - kun de uten LM = KOLMIN=0
     c                   add       1             AntLM0            5 0
     c  n34              move      KOINDT        FMITDT
     c  n34              move      KOINTI        FMITTM
     c   34              exsr      clearCF
N03  C                   exsr      GetAvvik
     C                   exsr      Setrow
     c                   add       KOMVKT        SUMVKT           11 3
     c                   add       KOMM3         SUMM3            11 3
     c                   add       KOMLM         SUMLM            11 3
     C                   callp     wrtsection('row')
     c                   endif
      *
     C     DokKey        READE     DOKUFM                                 35
     c                   end
      *
     c     ANTLM0        Ifgt      1
     c     BIKUNR        orne      *zeros
N01   * Skriv stykkgodssum../avvikende tekst dersom mix finnes
N01  c                   eval      SumTxt = 'SUM :'
N01  c     ANTLM1        Ifgt      0
N01  c                   eval      SumTxt = 'SUM (stykkgods) :'
N01  c                   endif
     C                   exsr      SetrowSum
     C                   callp     wrtsection('row')
     c                   endif

     c     ANTLM1        Ifgt      0
      *   2. Vis liste over LM-gods  (LMindikator = 1)
     C     DokKey        SETLL     DOKUFM
     C     DokKey        READE     DOKUFM                                 35
     c     *IN35         doweq     '0'
     c                   move      ' '           KOLMIN
     C                   eval      KOID = '00'+FMMRK1
     C     CARGOFKey     Chain     CARGOF                             34
N03  C   34KOID          Chain     CARGOF                             34
      *
      * i denne syklus - kun de med LM = KOLMIN=1
     c     KOLMIN        ifeq      '1'
     c  n34              move      KOINDT        FMITDT
     c  n34              move      KOINTI        FMITTM
     c   34              exsr      clearCF
     c                   MOVE      'J'           LMsyklus          1
N03  C                   exsr      GetAvvik
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   endif
      *
     C     DokKey        READE     DOKUFM                                 35
     c                   end
      *
N01   * Skriv grand total ../avvikende tekst dersom mix finnes
N01  c                   eval      SumTxt = 'SUM :'
N01  c     ANTLM0        Ifgt      0
N01  c                   eval      SumTxt = 'SUM (stykk+LM-gods) :'
N01  c                   endif
     C                   exsr      SetrowSum
     C                   callp     wrtsection('row')
      *
     c                   endif
      ***
      ***
      ***
      * B VARELINJER/GODSMERKING  PÅ LINJE NR 01 ->
      ***  (bør utvides med 'ytre leseloop' over DOKUFV = Varelinjer...)
     c                   exsr      clearCF
     C                   Z-ADD     1             XXLINR
     C     DokKey        SETLL     DOKUFM
     C     DokKeyE       READE     DOKUFM                                 35
     c     *IN35         doweq     '0'
     c                   add       1             AntAnnen          5 0
     c     AntAnnen      ifeq      1
     c                   eval      KOID = '* Annen godsmerking *'
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   endif
     c                   eval      KOID = FMMRK1
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C     DokKeyE       READE     DOKUFM                                 35
     C                   ENDDO
      *
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
N03   *********************************************
N03  C     GetAvvik      begsr
N03   *********************************************
N03  c                   if        AVVK_IN <> *blanks
N03  C                   Z-ADD     1             XN
N03  C     AVVK_IN       LOOKUP    AK(XN)                                 75
N03  C   75              eval      AvvInn  = AT(XN)
N03  c                   endif
N03  c                   if        AVVK_OU <> *blanks
N03  C                   Z-ADD     1             XN
N03  C     AVVK_OU       LOOKUP    AK(XN)                                 75
N03  C   75              eval      AvvOut  = AT(XN)
N03  c                   endif
N03  C                   endsr
N03   *
      *********************************************
     C     ClearCF       begsr
      *********************************************
     c                   clear                   KOMVKT
     c                   clear                   KOMLEN
     c                   clear                   KOMBRE
     c                   clear                   KOMHØY
     c                   clear                   KOMM3
     c                   clear                   KOMLM
     c                   clear                   KOLMIN
     c                   clear                   KOPATH
     C                   endsr
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
      * Userid.....
     C                   eval      AVD   = zhbgetvar('AVD')
     C                   eval      AVDN  = c2n2(AVD)
     C                   eval      OPD   = zhbgetvar('OPD')
     C                   eval      OPDN  = c2n2(OPD)
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
      *
     C                   CLOSE     BRIDF
     C                   CLOSE     HEADF
     C                   CLOSE     DOKUFM
     C                   CLOSE     CARGOF
N05  C                   CLOSE     BRPARF
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYOPCL':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     c     Heakey        chain     Headf
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHTMLvar('SNDN':HXSNDN)
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(AVDN:'Z')))
     C                   callp     updHTMLvar('OPD':
     C                             %trim(%editc(OPDN:'Z')))
      * Søkeparam
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     C                   callp     updHTMLvar('KOID':KOID)
      * Forkort inndato/tid
n02  C                   EVAL      DSdat  =FMITDT
N02  C                   MOVEL     DsDatDD       WKOINDT4          4 0
N02  C                   MOVE      DsDatMM       WKOINDT4
N02  C                   MOVEL     FMITTM        WKOINTI4          4 0
     C                   callp     updHTMLvar('INNDT':
     C                             %trim(%editw(WKOINDT4:'  /  ')))
     C                   callp     updHTMLvar('INNTI':
     C                             %trim(%editw(WKOINTI4:'  :  ')))
      * Forkort Utdato/tid
n02  C                   EVAL      DSdat  =FMUTDT
N02  c                   MOVEL     DsDatDD       WKOUTDT4          4 0
N02  C                   MOVE      DsDatMM       WKOUTDT4
N02  C                   MOVEL     FMUTTM        WKOUTTI4          4 0
     C                   callp     updHTMLvar('UTDT':
     C                             %trim(%editw(WKOUTDT4:'  /  ')))
     C                   callp     updHTMLvar('UTTI':
     C                             %trim(%editw(WKOUTTI4:'  :  ')))
      *
     C                   callp     updHTMLvar('KOMLEN':
     C                             %trim(%editc(KOMLEN:'Z')))
     C                   callp     updHTMLvar('KOMBRE':
     C                             %trim(%editc(KOMBRE:'Z')))
     C                   callp     updHTMLvar('KOMHØY':
     C                             %trim(%editc(KOMHØY:'Z')))
      *
     C                   callp     updHTMLvar('KOMVKT':
     C                             %trim(%editc(KOMVKT:'4')))
     C                   callp     updHTMLvar('KOMM3':
     C                             %trim(%editc(KOMM3:'4')))
     C                   callp     updHTMLvar('KOMLM':
     C                             %trim(%editc(KOMLM:'4')))
      *
     c                   move      *zeros        FVEKT             9 0
     c                   if        LMsyklus = 'J'
     c                   move      *zeros        FVLM              9 0
      * dersom KUNDE pålogget (annet enn NN mm / passord blank) beregn Fvekt pr kolli ??
     C                   if        BIKUNR<>0
     c                   eval      FVLM = KOMLM * 2000
     c                   eval      Fvekt= KOMVKT
     C                   if        FVLM > Fvekt
     c                   eval      Fvekt= FVLM
     c                   endif
     c                   add       Fvekt         SumFvekt         11 3
     c                   endif
     c                   endif
      * dersom KUNDE pålogget (annet enn NN mm / passord blank) beregn Fvekt pr kolli ??
     C                   callp     updHTMLvar('FVEKT':
     C                             %trim(%editc(FVEKT:'Z')))
      *
      * finnes Bilde 1
     C                   eval      ubane='/syspeddev/clid/'+KOID
     c                             +'-0.jpg'
     c                   CALL      'BHRURLCL'
s07  c*                  Parm                    ubane            70
n07  c                   Parm                    ubane           256
     c                   Parm                    Bilde1            1
     c                   move      *blanks       Bilde1U         256
     c                   if        Bilde1 = 'J'
     c                   eval      Bilde1U='<a href="'+%trim(ubane)+'" '
     c                             +'Target="ImgWindow">Bilde1</A>'
Nxx  c                   else
Nxx   * finnes TEI_bilde (kun hvis Bilde en mangler
Nxx  C                   eval      ubane='/syspeddev/clid/TEI_'+KOID
Nxx  c                             +'.jpg'
Nxx  c                   CALL      'BHRURLCL'
Nxx  c                   Parm                    ubane           256
Nxx  c                   Parm                    Bilde1            1
Nxx  c                   move      *blanks       Bilde1U         256
Nxx  c                   if        Bilde1 = 'J'
Nxx  c                   eval      Bilde1U='<a href="'+%trim(ubane)+'" '
Nxx  c                             +'Target="ImgWindow">BildeTEI</A>'
Nxx  c                   endif
     c                   endif
S04  C*                  callp     updHTMLvar('BILDE1U':BILDE1U)
      * finnes Bilde 2
     C                   eval      ubane='/syspeddev/clid/'+KOID
     c                             +'-1.jpg'
     c                   CALL      'BHRURLCL'
s02  c*                  Parm                    ubane            70
n02  c                   Parm                    ubane
     c                   Parm                    Bilde2            1
     c                   move      *blanks       Bilde2U         256
     c                   if        Bilde2 = 'J'
     c                   eval      Bilde2U='<a href="'+%trim(ubane)+'" '
     c                             +'Target="ImgWindow">Bilde2</A>'
Nxx  c                   else
Nxx   * finnes TEI_bilde (kun hvis Bilde en mangler
Nxx  C                   eval      ubane='/syspeddev/clid/TEU_'+KOID
Nxx  c                             +'.jpg'
Nxx  c                   CALL      'BHRURLCL'
Nxx  c                   Parm                    ubane           256
Nxx  c                   Parm                    Bilde2            1
Nxx  c                   move      *blanks       Bilde2U         256
Nxx  c                   if        Bilde2 = 'J'
Nxx  c                   eval      Bilde2U='<a href="'+%trim(ubane)+'" '
Nxx  c                             +'Target="ImgWindow">BildeTEU</A>'
Nxx  c                   endif
     c                   endif
n04  C                   if        FMLINR=*ZEROS
N04  C                   callp     updHTMLvar('BILDE1U':BILDE1U)
     C                   callp     updHTMLvar('BILDE2U':BILDE2U)
N03  C                   callp     updHTMLvar('AVVIK1': AvvInn)
N03  C                   callp     updHTMLvar('AVVIK2': AvvOut)
n04  C                   else
N04  C                   callp     updHTMLvar('BILDE1U':' ')
N04  C                   callp     updHTMLvar('BILDE2U':' ')
N04  C                   callp     updHTMLvar('AVVIK1':' ')
N04  C                   callp     updHTMLvar('AVVIK2':' ')
n04  C                   endif
N03  c                   move      *blanks       AvvInn
N03  c                   move      *blanks       AvvOut
      *
      *
     C                   endsr
      ******************************************
     C     SetrowSum     begsr
      ******************************************
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
S01  C*                  callp     updHTMLvar('KOID':'SUM:')
N01  C                   callp     updHTMLvar('KOID':SumTxt)
     C                   callp     updHTMLvar('INNDT':' ')
     C                   callp     updHTMLvar('INNTI':' ')
     C                   callp     updHTMLvar('UTDT':' ')
     C                   callp     updHTMLvar('UTTI':' ')
      *
     C                   callp     updHTMLvar('KOMLEN':' ')
     C                   callp     updHTMLvar('KOMBRE':' ')
     C                   callp     updHTMLvar('KOMHØY':' ')
      *
     C                   callp     updHTMLvar('KOMVKT':
     C                             %trim(%editc(SUMVKT:'4')))
     C                   callp     updHTMLvar('KOMM3':
     C                             %trim(%editc(SUMM3:'4')))
     C                   callp     updHTMLvar('KOMLM':
     C                             %trim(%editc(SUMLM:'4')))
      *
     c                   move      *zeros        FVEKT             9 0
     c                   move      *zeros        FVM3              9 0
     c                   move      *zeros        FVLM              9 0
      * dersom KUNDE pålogget (annet enn NN mm / passord blank) beregn Fvekt pr kolli ??
     c                   if        LMsyklus = 'J'
     c                   eval      FVEKT = SumFvekt
     c                   else
     C                   if        BIKUNR<>0
     c                   eval      FVM3 = SUMM3 * 286
     c                   eval      FVLM = SUMLM * 2000
     c                   eval      Fvekt= SUMVKT
     C                   if        FVM3 > Fvekt
     c                   eval      Fvekt= FVM3
     c                   endif
     C                   if        FVLM > Fvekt
     c                   eval      Fvekt= FVLM
     c                   endif
     c                   eval      SumFvekt = FVEKT
     c                   endif
     c                   endif
     C                   callp     updHTMLvar('FVEKT':
     C                             %trim(%editc(FVEKT:'Z')))
      *
     C                   callp     updHTMLvar('BILDE1U':' ')
     C                   callp     updHTMLvar('BILDE2U':' ')
N04  C                   callp     updHTMLvar('AVVIK1':' ')
N04  C                   callp     updHTMLvar('AVVIK2':' ')
      *
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      HEADF
     C                   OPEN      DOKUFM
     C                   OPEN      CARGOF
N05  C                   OPEN      BRPARF
      *
     C     DOKKEY        KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    XXFBNR            3 0
     C                   KFLD                    XXLINR            2 0
     C                   MOVE      'F'           FMRI
     C                   MOVE      001           XXFBNR
     C     DOKKEYE       KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    XXFBNR
     C     CARGOFKey     KLIST
     C                   KFLD                    KOID
     C                   KFLD                    KOTERM
     c                   if        AVDN >8000
     c                   eval      KOTERM = 'OSL'
     c                   else
     c                   eval      KOTERM = 'DRA'
     c                   endif
     C     HeaKey        KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
      * HENT OVER EVT BILDER FRA CARGOSCAN BILDESERVER
      * vil måttet hardkodes fra case til case (i første omgangs:=) se hos Ramberg
     C                   move      AVDN          AVD
     C                   move      OPDN          OPD
OBS  c*                  call      'CARGOIMGC'
OBS  C*                  PARM                    AVD
OBS  C*                  PARM                    OPD
      *
N03  C     BrParKey      KLIST
N03  C                   kfld                    PABRID
N03  C                   kfld                    PAKUNR
N03  C                   kfld                    PAID
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
N03  C                   MOVE      *zeros        PAKUNR
N03  c* Avvikskoder / tekster
N03  C                   MOVEL     'TKTKA'       PAID
N03  C                   move      *zeros        XN                3 0
N03  c     BrParKey      setll     BRPARF
N03  c     BrParKey      reade     BRPARF                                 33
N03  c     *in33         doweq     '0'
N03  C                   add       1             XN
N03  c                   eval      AK(XN) = %subst(Pavrda:1:5)
N03  c                   eval      AT(XN) = %subst(Pavrda:7)
N03  c     XN            ifeq      100
N03  C                   leave
N03  c                   end
N03  c     BrParKey      reade     BRPARF                                 33
N03  c                   enddo
N03  c
     C                   endsr
      *
