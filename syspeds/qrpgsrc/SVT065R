     FSVT065D   CF   E             WORKSTN
     FSVTHA2    UF A E           K DISK
     D                SDS
     D  WUSER                254    263
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *____________
      * Flytte felt
      *""""""""""""
     C     WUSER         CHAIN(N)  SVTHA2                             53
     C                   EVAL      ZPWDN = *BLANKS
     C                   EVAL      ZPWDNR = *BLANKS

     C     SRBB1         TAG

     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVT065DØ
     C                   EXFMT     SVT065DA
     C                   MOVEA     '0000'        *IN(30)
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
      * Gammalt løsenord saknas
     C     ZPWDG         IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'SVT0010'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Gammalt løsenord fel !
     C     ZPWDG         IFNE      SVTH_PWD
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'SVT0031'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Nytt løsenord saknas
     C     ZPWDN         IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'SVT0010'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Nytt løsenord, repetition  saknas
     C     ZPWDNR        IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0010'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Bægge nya løsenorden måste vara lika
     C     ZPWDN         IFNE      ZPWDNR
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0032'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Kontrollera nytt løsenord
     C                   CALL      'SVT063R'
     C                   PARM                    SVTH_PWD
     C                   PARM                    ZPWDN
     C                   PARM                    UERROR            7
     C     UERROR        IFNE      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      UERROR        ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     WUSER         CHAIN     SVTHA2                             53
     C                   EVAL      SVTH_PWD=ZPWDN
     C                   MOVE      WDT           SVTH_PWDD
     C                   UPDATE    SVTHAR

     C                   ENDIF

     C                   ENDSR
