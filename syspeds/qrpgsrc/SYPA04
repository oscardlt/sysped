      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE PALLEBYTTE-PARTER            ***
      *** FUNKSJON FOR SØKING ER OGSÅ LAGT INN                   ***
      **************************************************************
      *
     FPAPA      UF A E           K DISK
     FPAPAL01   IF   E           K DISK
     F                                     RENAME(PAPAR:PAPAR2)
     FPALLDL06  IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FTRAN      IF   E           K DISK
     FSYPA04FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  PPSPDT                 1      8  0
     D  PPSPA1                 3      4  0
     D  PPSPM1                 5      6  0
     D  PPSPD1                 7      8  0
     D                 DS
     D  WPSPDT                 1      6  0
     D  PPSPD2                 1      2  0
     D  PPSPM2                 3      4  0
     D  PPSPA2                 5      6  0
     D                 DS
     D  PPSTDT                 1      8  0
     D  PPSTA1                 3      4  0
     D  PPSTM1                 5      6  0
     D  PPSTD1                 7      8  0
     D                 DS
     D  WPSTDT                 1      6  0
     D  PPSTD2                 1      2  0
     D  PPSTM2                 3      4  0
     D  PPSTA2                 5      6  0
     D                 DS
     D  PPSADT                 1      8  0
     D  PPSAA1                 3      4  0
     D  PPSAM1                 5      6  0
     D  PPSAD1                 7      8  0
     D                 DS
     D  WPSADT                 1      6  0
     D  PPSAD2                 1      2  0
     D  PPSAM2                 3      4  0
     D  PPSAA2                 5      6  0
     D                 DS
     D  PPNAVN                 1     30
     D  XDLT                   1      5
     C*
     C                   EXSR      INIT
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2PPNR        YSPPNR
     C* 51 ON ALFA SORTERING/OFF KUNDENR SORTERING
     C     W2PPNR        COMP      *ZEROS                                 51
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N51KODSI1        SETLL     PAPA
     C   51ALFKEY        SETLL     PAPAL01
     C     START1        TAG
     C  N51              READ(N)   PAPA                                   94
     C   51              READ      PAPAL01                                94
     C  N51
     CANN94              MOVE      WSPPNR        YSPPNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   exsr      datosj
     C                   WRITE     SUBFIL
     C  N51              READ(N)   PAPA                                   94
     C   51              READ      PAPAL01                                94
     C  N94
     CANN51              MOVE      WSPPNR        YSPPNR
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     21            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94
     CANN51              MOVE      PPNR          YSPPNR
     C  N94
     CANN51KODSI3        SETLL     PAPA
     C  N94
     CANN51              READ(N)   PAPA                                   94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C                   MOVEA     '000000'      *IN(70)
     C                   SETOFF                                       30
     C   12              EXSR      MOVB2
     C**** SØKE ALFA
     C   04              DO
     C                   EXSR      SØKKUN
     C                   GOTO      START5
     C                   END
     C*
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C*
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   MOVEA     '0000'        *IN(70)
     C                   MOVE      '0'           *IN30
     C* HENT EVT TRANSPORTØR INFO
     C     WSTRNR        IFNE      *ZEROS
     C     TRANKE        CHAIN     TRAN                               73
     C     *IN73         IFEQ      '0'
     C     WSPPNR        IFEQ      *ZEROS
     C                   MOVE      VMTRAN        WSPPNR
     C                   END
     C     WSKUNR        IFEQ      *ZEROS
     C                   MOVE      VMTRKU        WSKUNR
     C                   END
     C                   END
     C                   END
     C* N40 = NY
     C  N40WSPPNR        IFEQ      *ZEROS
     C                   SETON                                        7030
     C                   MOVE      'SPT0104'     MSGNR
     C                   ELSE
     C     KODSIK        CHAIN     PAPA                               33
     C  N33              DO
     C                   UPDATE    PAPAR
     C                   SETON                                        3070
     C                   MOVE      'SPT0104'     MSGNR
     C                   END
     C                   END
     C*
     C   30              Z-ADD     3             LINNBR
     C   30              Z-ADD     21            POSNBR
     C   30              GOTO      SLTB
     C* TEST CUSTOMERS
     C                   MOVE      *BLANKS       KNAVN
     C     KUNKEY        CHAIN     CUNDL01                            71
     C  N71              MOVE      KNAVN         WSNAVN
     C  N71WSALF         IFEQ      *BLANKS
     C     *IN40         OREQ      '0'                                            *NY
     C                   MOVE      SONAVN        WSALF
     C                   END
     C   71              DO
     C                   MOVE      'SPK0001'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     21            POSNBR
     C                   SETON                                        30
     C                   GOTO      SLTB
     C                   END
     C* FEIL I TRANSPORTØR FELTET
     C     *IN73         IFEQ      '1'
     C                   MOVE      'SPT0133'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     21            POSNBR
     C                   SETON                                        30
     C                   GOTO      SLTB
     C                   END
      * KATEGORI KAN IKKE VÆRE BLANK
     C     WSKATE        COMP      *BLANKS                                72
     C     *IN72         IFEQ      '1'
     C                   MOVE      'JOV0626'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     21            POSNBR
     C                   SETON                                        30
     C                   GOTO      SLTB
     C                   END
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSIK        CHAIN     PAPA                               33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    PAPAR
     C   33              WRITE     PAPAR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C     PPNR          CHAIN(N)  PAPA                               33
     C                   MOVE      PPNR          WSPPNR
     C                   MOVE      PPKUNR        WSKUNR
     C     STRDLT        TAG
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSIK        CHAIN     PAPA                               33
     C     PPNR          CHAIN     PALLDL06                           35
     C     *IN35         IFEQ      '0'
      * FINNES TRANSER- MERK INAKTIV!
     C  N33              MOVE      'I'           PPKATE
     C  N33              UPDATE    PAPAR
     C  N33              MOVEL     '*INACT* '    PPNAVN
     C                   ELSE
      * FINNES INGEN TRANSER- FYSISK DELETE!
     C  N33              DELETE    PAPAR
     C                   EXSR      MOVB1
     C                   END
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C     PPNR          CHAIN(N)  PAPA                               33
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C                   MOVEA     '000000'      *IN(70)
     C                   SETOFF                                       30
     C*
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C**** SØKE ALFA
     C   04              DO
     C                   EXSR      SØKKUN
     C                   GOTO      STCHG
     C                   END
     C*
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        PPNR
     C                   MOVE      *ZEROS        PPKUNR
     C                   MOVE      *BLANKS       PPNAVN
     C                   MOVE      *BLANKS       PPALFA
     C*
     C                   MOVEL     '*DEL*'       XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSPPNR
     C                   MOVE      *ZEROS        WSKUNR
     C                   MOVE      *ZEROS        WSTRNR
     C                   MOVE      *BLANKS       WSNAVN
     C                   MOVE      *BLANKS       WSALF            10
     C                   MOVE      *BLANKS       WSKATE
     C                   MOVE      *ZEROS        PPSPDT
     C                   MOVE      *ZEROS        PPSTDT
     C                   MOVE      *ZEROS        PPSALP
     C                   MOVE      *ZEROS        PPSALT
     C                   MOVE      *ZEROS        PPSADT
     C                   MOVE      *ZEROS        PPAVRE
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C                   MOVE      PPNR          WSPPNR
     C                   MOVE      PPKUNR        WSKUNR
     C                   MOVE      PPNAVN        WSNAVN
     C                   MOVE      PPTRNR        WSTRNR
     C                   MOVE      PPALFA        WSALF
     C                   MOVE      PPKATE        WSKATE
     C                   EXSR      DATOSJ
     C                   SETON                                        40
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     21            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C* FIX KEY IF NOT FOUND
     C   33              DO
     C                   MOVE      WSPPNR        PPNR
     C                   END
     C*
     C* FIXES VARIABLES
     C                   MOVE      WSKUNR        PPKUNR
     C                   MOVE      WSNAVN        PPNAVN
     C                   MOVE      WSTRNR        PPTRNR
     C                   MOVE      WSALF         PPALFA
     C                   MOVE      WSKATE        PPKATE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    WSALF
     C     *LIKE         DEFINE    PPNR          YSPPNR
     C     ALFKEY        KLIST
     C                   KFLD                    WSALFA
     C     KODSIK        KLIST
     C                   KFLD                    WSPPNR
     C     KODSI1        KLIST
     C                   KFLD                    W2PPNR
     C     KODSI3        KLIST
     C                   KFLD                    YSPPNR
     C*
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSTRNR
     C                   READ      FIRM                                   33
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C* TRANSPORTØRNR
     C     LINNBR        IFLE      4
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     21            POSNBR
     C                   ELSE
     C* TRANSPORTØRNR
     C                   CALL      'SYTR04A'
     C                   PARM                    WSTRNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     21            POSNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DATOSJ        BEGSR
     C***********************************************
     C*
     C                   move      ppspa1        ppspa2
     C                   move      ppspm1        ppspm2
     C                   move      ppspd1        ppspd2
     C                   move      ppsta1        ppsta2
     C                   move      ppstm1        ppstm2
     C                   move      ppstd1        ppstd2
     C                   move      ppsaa1        ppsaa2
     C                   move      ppsam1        ppsam2
     C                   move      ppsad1        ppsad2
     C                   ENDSR
