      *
      ***********************************************************
      *** PROGRAM SKAL PLUKK OPPDRAG TIL E.F. MANIFEST        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     FSADH      IF   E           K DISK
     FSADEFL03  IF   E           K DISK
     FSADEFCL2  UF   E           K DISK
     FSADEFCL3  IF   E           K DISK    RENAME(SADEFCFR:SADEFCFR3)
     FSADEFCL4  IF   E           K DISK    RENAME(SADEFCFR:SADEFCFR4)
     FSADEFCL5  IF   E           K DISK    RENAME(SADEFCFR:SADEFCFR5)
     FTRACKF    O  A E             DISK
     FSAD133D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
      *
     C                   EXSR      INIT
     C                   WRITE     NYVARE
     C                   GOTO      START6
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C                   EXSR      LESPOS
      *
     C     START2        TAG
      *
     C                   DOW       *IN94 = *OFF
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   EXSR      LESPOS
     C                   ENDDO
      *
     C                   SELECT
     C                   WHEN      ZRECNO = 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   WHEN      ZRECNO > 19
     C                   EVAL      ZRECNO = 1
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   OTHER
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   ENDSL
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   EXFMT     SUBCTL
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
     C   05              GOTO      START
     C   08              DO
     C                   GOTO      START6
     C                   ENDDO
     C   12              GOTO      SLUTT
     C   20              DO
     C                   EXSR      PLKALLE
     C                   GOTO      SLUTT
     C                   ENDDO
      *
     C                   IF        W2AVD <> 0 AND W2TDN <> 0                    1<-B
     C                             OR W23039E <> 0 OR W2TRID <> *BLANKS
     C                   IF        W2NR = 0                                      2<-B
     C                   EXSR      NYSØK
     C                   GOTO      START
     C                   ELSE                                                    2
     C                   IF        W2TDN = 0                                      3<-B
     C                   SETON                                        9830
     C                   MOVE      'YBC2098'     MSGNR
     C                   ELSE                                                     3
     C                   EXSR      NYOPD
     C                   END                                                      3<-E
     C                   MOVE      *ZEROS        W2TDN
     C                   MOVE      *ZEROS        W23039E
     C                   MOVE      *BLANKS       W2TRID
     C                   GOTO      START3
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     START6        TAG
     C                   MOVE      *BLANKS       W3TRID
     C                   MOVE      *ZEROS        W33039E
     C                   MOVE      *ZEROS        W3AVD
     C                   MOVE      *ZEROS        W3TDN
     C                   EXFMT     VAREL2
      *
     C   12              GOTO      START3
     C                   SELECT
     C                   WHEN      W3TRID <> *BLANKS
     C                   MOVEA     '100'         *IN(41)
     C                   WHEN      W33039E <> 0
     C                   MOVEA     '010'         *IN(41)
     C                   WHEN      W3TDN <> 0
     C                   MOVEA     '001'         *IN(41)
     C                   OTHER
     C                   GOTO      START6
     C                   ENDSL
     C                   GOTO      START
      *
     C     SLUTT         TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     NYSØK         BEGSR
      ***********************************************
     C                   CLEAR                   VAREL2
      *
     C                   IF        W2AVD = 0
     C                   MOVE      EFAVD         W3AVD
     C                   ELSE
     C                   MOVE      W2AVD         W3AVD
     C                   END
     C                   IF        W2TDN <> 0                                   1<-B
     C                   MOVE      W2TDN         W3TDN
     C                   ELSE                                                   1
     C                   IF        W23039E <> 0                                  2<-B
     C                   MOVE      W23039E       W33039E
     C                   ELSE                                                    2
     C                   IF        W2TRID <> *BLANKS                              3<-B
     C                   MOVE      W2TRID        W3TRID
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   MOVE      *ZEROS        W2NR
     C                   MOVE      *BLANKS       W2TRID
     C                   MOVE      *ZEROS        W23039E
     C                   MOVE      *ZEROS        W2AVD
     C                   MOVE      *ZEROS        W2TDN
      *
     C                   MOVEA     '00'          *IN(98)
      *
     C                   ENDSR
      *
      ***********************************************
     C     NYOPD         BEGSR
      ***********************************************
     C     W2AVD         IFEQ      0
     C                   MOVE      EFAVD         W2AVD
     C                   END
     C     SADEFCL3K1    CHAIN     SADEFCL3                           98
     C   98              DO
     C                   MOVE      'YBC2099'     MSGNR
     C                   GOTO      SLOPD
     C                   END
     C                   MOVE      '1'           *IN90
      *
     C                   SELECT
     C     W2NR          WHENEQ    2
     C*                  EXSR      PLKOPD
     C     W2NR          WHENEQ    4
     C                   EXSR      RMVOPD
     C     W2NR          WHENEQ    5
     C                   EXSR      DSPOPD
     C     W2NR          WHENEQ    15
     C                   EXSR      DSPSAD
     C                   ENDSL
      *
     C     SLOPD         TAG
     C   98              MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN90
     C                   MOVE      *ZEROS        W2NR
     C  N98              MOVE      *ZEROS        W2AVD
     C  N98              MOVE      *ZEROS        W2TDN
      *
     C                   ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C                   IF        *IN98 = *OFF                                  2<-B
     C                             AND SINAK <> '*PLUKKET*'
     C                             AND SINAK <> '*SLETTET*'
     C                   SELECT                                                   3<-B
     C     WSNR          WHENEQ    1                                              3
     C                   MOVE      '1'           *IN99
     C                   EXSR      PLKOPD
     C     WSNR          WHENEQ    4                                              3
     C                   EXSR      RMVOPD
     C     WSNR          WHENEQ    5                                              3
     C                   EXSR      DSPOPD
     C     WSNR          WHENEQ    15                                             3
     C                   EXSR      DSPSAD
     C                   ENDSL                                                    3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
      ***********************************************
     C     PLKOPD        BEGSR
      ***********************************************
     C                   MOVE      'PLK'         XTYPE                          SPØRRING
     C                   CALL      'SAD134R'
     C                   PARM                    UPRO
     C                   PARM      CLAVD         U_AVD             4 0
     C                   PARM      CLTDN         U_TDN             7 0
     C                   PARM                    XTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C                   IF        *IN99
     C                   IF        UFLAGG = 0
     C                   EVAL      SINAK = '*PLUKKET*'
     C                   END
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     RMVOPD        BEGSR
      ***********************************************
     C     SADHK         CHAIN     SADEFCL2                           33
     C                   EVAL      CLPRO = -1
     C                   EVAL      CLST  = 'S'
     C                   UPDATE    SADEFCFR
     C                   EVAL      SINAK = '*SLETTET*'
     C                   EXSR      TTSR
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     DSPOPD        BEGSR
      ***********************************************
     C                   MOVE      'DSP'         XTYPE                          SPØRRING
     C                   CALL      'SAD134R'
     C                   PARM                    UPRO
     C                   PARM      CLAVD         U_AVD             4 0
     C                   PARM      CLTDN         U_TDN             7 0
     C                   PARM                    XTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     DSPSAD        BEGSR
      ***********************************************
     C                   CALL      'SAD005A'
     C                   PARM      CLAVD         U_AVD             4 0
     C                   PARM      CLTDN         U_TDN             7 0
     C                   PARM                    UFLAGG            1 0
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     PLKALLE       BEGSR
      ***********************************************
     C                   MOVE      'PLA'         XTYPE
     C                   Z-ADD     1             XRECNO            4 0
     C     XRECNO        CHAIN     SUBFIL                             33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   CALL      'SAD134R'
     C                   PARM                    UPRO
     C                   PARM      CLAVD         U_AVD             4 0
     C                   PARM      CLTDN         U_TDN             7 0
     C                   PARM                    XTYPE             3
     C                   PARM                    UFLAGG            1 0
     C                   ADD       1             XRECNO
     C     XRECNO        CHAIN     SUBFIL                             33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     SETKEY        BEGSR
      ***********************************************
     C   41W3TRID        SETLL     SADEFCL4
     C   42W33039E       SETLL     SADEFCL5
     C   43SADEFCL3K2    SETLL     SADEFCL3
      *
     C                   ENDSR
      *
      ***********************************************
     C     LESPOS        BEGSR
      ***********************************************
     C     STLES         TAG
     C   41W3TRID        READE     SADEFCL4                               94
     C   42W33039E       READE     SADEFCL5                               94
     C   43W3AVD         READE     SADEFCL3                               94
      *
     C                   IF        *IN94 = *OFF                                 1<-B
     C     SADHK         CHAIN     SADH                               33
     C                   END                                                    1<-E
      *
     C     SLLES         ENDSR
      *
      ***********************************************
     C     TTSR          BEGSR
      ***********************************************
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   EVAL      TTAVD  = CLAVD
     C                   EVAL      TTOPD  = CLTDN
     C                   EVAL      TTTUR  = 0
     C                   EVAL      TTACTI = 'EFC'
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      UUSR          TTUSER
     C                   EVAL      TTTEXT = 'Oppdrag er slettet fra E.F. '
     C                             + 'manifest.'
     C                   EVAL      TTTEXL = 'Oppdrag er slettet til E.F. '
     C                             + 'manifest.'
     C                   EVAL      TTEDEV = 'DLT'
      *
     C                   WRITE     TRACKFR
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SADHK         KLIST
     C                   KFLD                    CLAVD
     C                   KFLD                    CLTDN
     C     SADEFCL3K1    KLIST
     C                   KFLD                    W2AVD
     C                   KFLD                    W2TDN
     C     SADEFCL3K2    KLIST
     C                   KFLD                    W3AVD
     C                   KFLD                    W3TDN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    UUUID            36
     C                   PARM                    UFLAGG            1 0
      *
     C     UUUID         CHAIN     SADEFL03                           33
      *
     C                   ENDSR
      *
