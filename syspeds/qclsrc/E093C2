             PGM        PARM(&LIB &IID)
             /*_____________________*/
             /* DEKLARERE VARIABLER */
             /*"""""""""""""""""""""*/
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JUL) TYPE(*CHAR) LEN(3)
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&NBRCURRCD) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IID) TYPE(*CHAR) LEN(35)
             /*_____________________________________________*/
             /* GENERERE FIL FOR TCPIP KOMMANDOER OG OUTPUT */
             /*"""""""""""""""""""""""""""""""""""""""""""""*/
             CLRPFM     FILE(E093C2) MBR(FIL1)
             MONMSG CPF0000
             CLRPFM     FILE(E093C)
             MONMSG     MSGID(CPF0000)
             CALL       PGM(E093R2) PARM(&LIB &IID)
             /*_______________*/
             /* HENT OVER FIL */
             /*"""""""""""""""*/
              OVRDBF     FILE(INPUT) TOFILE(E093C)
              STRTCPFTP  RMTSYS(IBMFTP) TBLFTPOUT(QTCPASCNO) +
                           TBLFTPIN(QTCPEBCNO)
              RTVMBRD    FILE(E093C2) MBR(FIL1) NBRCURRCD(&NBRCURRCD)
              IF         COND(&NBRCURRCD *EQ 0) THEN(GOTO CMDLBL(EXIT))

              CHGJOB     DATFMT(*JUL)
              RTVJOBA    DATE(&DATE)
              CHGJOB     DATFMT(*SYSVAL)
              RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
              CHGVAR     VAR(&JUL) VALUE(%SST(&DATE 3 3))
              CHGVAR     VAR(&MEMBER) VALUE('E' *CAT &JUL *CAT &TIME)
              CPYF       FROMFILE(E093C2) TOFILE(EDI79F) +
                           FROMMBR(FIL1) TOMBR(&MEMBER) MBROPT(*ADD) +
                           FMTOPT(*NOCHK)

 EXIT:        DLTSPLF    FILE(QPRINT) SPLNBR(*LAST)
              MONMSG     MSGID(CPF0000)

              ENDPGM
