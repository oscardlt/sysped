             PGM
 /*                                                                */
 /*  Deklarer parametre/cl-variabler                               */
 /*                                                                */
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in01 på, betyr program kalles fra +
                          SAD400 */
             DCL        VAR(&UVALK) TYPE(*CHAR) LEN(1) VALUE(' ') /* +
                          Hvor vedlikeholdes valutakurser    */
                          /* "S" i spedisjons databibliotek  */
                          /* "Ø" i økonomi    databibliotek  */
             DCL        VAR(&FINNES) TYPE(*CHAR) LEN(1) VALUE('J') /* +
                          Finnes VALUF i spedisjons databibliotek? */
             DCL        VAR(&USRLIBL) TYPE(*CHAR) LEN(275)
             DCL        VAR(&USRLIB1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRLIB2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRLIB3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRLIB4) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTFMT) TYPE(*CHAR) LEN(3)
/*______________*/
/* LES FIRMAFIL */
/*""""""""""""""*/
             DCLF       FILE(FIRML1) RCDFMT(*ALL)
             RCVF       DEV(*FILE) RCDFMT(*FILE)
/*                      */
 /*                                                                */
 /* HENT DATOFORMAT AV MASKIN                                      */
             RTVSYSVAL  SYSVAL(QDATFMT) RTNVAR(&DTFMT)
 /*                                                                */
 /* LÅS FIRM FOR ANNEN BRUK - SLIK AT INGEN F.EKS. SKRIVER FAKTURA */
 /* SF7      ALCOBJ     OBJ((FIRM *FILE *EXCLRD))                  */
 /*  Kall vedlikeholdsprogram - returnerer parameter for valuta-   */
 /*  vedlikehold                                                   */
 /*                                                                */
             CALL       PGM(SYFA30) PARM(&IN01 &UVALK &DTFMT)
 /* LÅS FIRM FOR ANNEN BRUK - SLIK AT INGEN F.EKS. SKRIVER FAKTURA */
 /* SF7      DLCOBJ     OBJ((FIRM *FILE *EXCLRD))   */
             IF         COND(&UVALK = ' ') THEN(GOTO CMDLBL(SLUTT))
                        /* Har avsluttet med F3                    */
 /*                                                                */
 /*                                                                */
 /*                                                                */
 /*  Hente navn på databibliotek for spedisjon og økonomi          */
 /*  Forventer rekkefølge på brukerbibliotek i bibliotekslista:    */
 /*  1=SYENDRE 2=SYSPED 3=Spedisjonsdata(SYSPEDF)                  */
 /*  4=Økonomidata (DATALIB)                                       */
 /*                                                                */
             RTVJOBA    USRLIBL(&USRLIBL)
/*____________________________________________*/
/* Bruk %SUBSTRING for å dele opp parametren. */
/*""""""""""""""""""""""""""""""""""""""""""""*/
             CHGVAR     VAR(&USRLIB3) VALUE(%SUBSTRING(&USRLIBL 23 10))
             CHGVAR     VAR(&USRLIB4) VALUE(%SUBSTRING(&USRLIBL 34 10))
 /*                                                                */
 /*  Finnes fila VALUF i spedisjons databiblioteket ????           */
 /*                                                                */
             CHKOBJ     OBJ(SYENDRE/VALUF) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&FINNES) +
                          VALUE('N'))
 /*                                                                */
 /*  "S" for vedlikehold av valuta                                 */
 /*  Dersom VALUF IKKE finnes i spedisjon, kopier fra økonomi      */
 /*                                                                */
             IF         COND(&UVALK = 'S') THEN(DO)
             IF         COND(&FINNES = 'N') THEN(DO)
             CPYF       FROMFILE(&L1DATN/VALUF) +
                          TOFILE(SYENDRE/VALUF) CRTFILE(*YES)
             CRTLF      FILE(SYENDRE/VALUL01) +
                          SRCFILE(SYSPEDS/QDDSSRC) LVLCHK(*NO)
                        ENDDO
             GOTO       CMDLBL(SLUTT)
                        ENDDO
 /*                                                                */
 /*  "Ø" for vedlikehold av valuta                                 */
 /*  Dersom VALUF finnes i spedisjon, SLETT                        */
 /*                                                                */
             IF         COND(&FINNES = 'J') THEN(DO)
             DLTF       FILE(SYENDRE/VALUL01)
             MONMSG     MSGID(CPF2105)
             DLTF       FILE(SYENDRE/VALUF)
             MONMSG     MSGID(CPF2105)
                        ENDDO
 SLUTT:      ENDPGM
