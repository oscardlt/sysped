/*  an = ny type  arkivdokument  */
/*  KODE D = DUPLEX TOSIDIG UTSKRIFT                      */
/*  BYTTET GETPR2 MED GETPRN FOR DRAWER OG ANTAL.L KOPIER */
             PGM        PARM(&KOWFET &KODE &AVD &SAML1 &SAML2 &LAPP &DATO)
             DCL        VAR(&DATO) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FLYF) TYPE(*DEC) LEN(3 0) VALUE(017)
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('am')
             DCL        VAR(&ATYPE2) TYPE(*CHAR) LEN(2) VALUE('al')
/*N01*/      DCL        VAR(&ATYPE3) TYPE(*CHAR) LEN(2) VALUE('an')
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&AUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til merkelapp-1. */
             DCL        VAR(&PT2) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til merkelapp-1. */
             DCL        VAR(&PRT3) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til merkelapp-2. */
             DCL        VAR(&PT3) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til merkelapp-2. */
             DCL        VAR(&PRT17) TYPE(*CHAR) LEN(10) /* +
                          Printernavn til flyfraktbrev-eksport. */
             DCL        VAR(&PT17) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til flyflaktbrev-eksport. */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&KOWFET) TYPE(*CHAR) LEN(1)
             DCL        VAR(&KODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAML1) TYPE(*CHAR) LEN(3)
             DCL        VAR(&SAML2) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LAPP) TYPE(*CHAR) LEN(3)
    /* */
 RTVTIME:    RTVJOBA    USER(&AUSER)
             RTVDTAARA  DTAARA(*LDA (601 10)) RTNVAR(&PRT2)
             RTVDTAARA  DTAARA(*LDA (611 10)) RTNVAR(&PT2)
             RTVDTAARA  DTAARA(*LDA (621 10)) RTNVAR(&PRT3)
             RTVDTAARA  DTAARA(*LDA (631 10)) RTNVAR(&PT3)
             RTVDTAARA  DTAARA(*LDA (661 10)) RTNVAR(&PRT17)
             RTVDTAARA  DTAARA(*LDA (671 10)) RTNVAR(&PT17)
    /* */
    /* N02 TESTER LASER OG LPI                   */
    /* */
             CALL       PGM(GETPRN) PARM(&AVD &FLYF &PNAVN &FORM +
                        &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
    /* */
    /* */
    /* SKRIVER UT INNENLANDS FLYFRAKTBREV */
             OVRPRTF    FILE(MERKL1) OUTQ(&PRT2) PAGESIZE(18 50) +
                          CPI(10) OVRFLW(18) FORMTYPE(&PT2)
             OVRPRTF    FILE(MERKL2) OUTQ(&PRT3) PAGESIZE(24 50) +
                          CPI(10) OVRFLW(24) FORMTYPE(&PT3)
             IF         COND(&LASER *NE 'P') THEN(DO)
             IF         COND(&KOWFET = 'J') THEN(DO)
             OVRPRTF    FILE(SYEF44P) DEVTYPE(*IPDS) PAGESIZE(96 80) +
                          LPI(8) CPI(10) OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT17) +
                          FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
             ELSE       CMD(DO)
             OVRPRTF    FILE(SYEF44P) DEVTYPE(*SCS) PAGESIZE(96 80) +
                          LPI(8) CPI(10) OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT17) +
                          FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
             ENDDO
             IF         COND(&LASER *EQ 'P') THEN(DO)
             OVRPRTF    FILE(SYEF44P) DEVTYPE(*AFPDS) PAGESIZE(96 +
                          80) LPI(8) CPI(10) OVRFLW(96) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) +
                          PRTQLTY(*DRAFT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT17) FORMTYPE(&PT17) +
                          COPIES(&COPIES) /*N01*/
             ENDDO
             IF         COND(&LASER *EQ 'F') THEN(DO)
             OVRPRTF    FILE(SYEF44P) DEVTYPE(*AFPDS) PAGESIZE(96 +
                          80) LPI(8) CPI(10) FRONTMGN(0 0.1) +
                          OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT17) +
                          FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
 /*N01 - EGEN AFP-STYRING DUPLEX */
             IF         COND(&LASER *EQ 'D') THEN(DO)
             OVRPRTF    FILE(SYEF44P) DEVTYPE(*AFPDS) PAGESIZE(96 +
                          80) LPI(8) CPI(10) FRONTMGN(0 0.1) +
                          OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          DUPLEX(*YES) FRONTOVL(&LOGO) +
                          BACKOVL(AWBBAK 0 0 *CONSTANT) +
                          OUTQ(&PRT17) FORMTYPE(&PT17) COPIES(&COPIES)
             ENDDO
             CALL       PGM(SYEF44) PARM(&PRANT &KODE &AVD &SAML1 +
                          &SAML2 &LAPP &LASER &DATO)
             SNDPGMMSG  MSG(' ' *BCAT &PRANT *BCAT +
                          'flyfraktbrev-eksport er klare til utskrift +
                          på skriver ' *BCAT &PRT17 *BCAT '.') +
                          MSGTYPE(*INFO)
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE2 &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
/*N01*/      SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE3 &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
    /* */
 SLUTT:      ENDPGM
