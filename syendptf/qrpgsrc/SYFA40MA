********************************************************************
      *  LEGGE TIL ANK.MELDINGER SOM POSTER I PURREFIL / status A
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Les IKKE kun dagens dato men xx dager / param ANKMF MÅ finnes
      * 02 JOV PTF12 Post som er Slettemerkety AUTO (=stat- S) kan bli RE-ÅPNET til A..
      * 03 JOV PTF12 Ved ant dg ank.melding større enn purredagert ble Dgrminus satt til 0 /uheldig
      * 04 JOV PTF12 Skipp hvis HEOT = blanks
********************************************************************
     FHEADDT    IF   E           K DISK
     FRUCKDFT   IF   E             DISK
N01  FSYPARL1   IF   E           K DISK
S02  F*UCKP     IF A E           K DISK
N02  FRUCKP     UF A E           K DISK
     D                 DS
     D  HEGN                   1     15
     D  HEGN5_9                5      9
     D                 DS
     D  BV                     1     45
     D                                     DIM(9)
     D  RDBEV1                 1      5
     D  RDBEV2                 6     10
     D  RDBEV3                11     15
     D  RDBEV4                16     20
     D  RDBEV5                21     25
     D  RDBEV6                26     30
     D  RDBEV7                31     35
     D  RDBEV8                36     40
     D  RDBEV9                41     45
     D                 DS
     D  OT                     1     40
     D                                     DIM(20)
     D  RDOT                   1     40
      *
     C                   exsr      INIT
      *
N01   * Les IKKE kun dagens dato men n dager tilbake  ( n KAN være 0)
N01   * Dvs En kan ombestemme seg og gjøre om fra egenfortolling til ank-melding ETTER oppdr.dato!!
S01  C*    Idag          setll     HEADDT
S01  C*    Idag          READE     HEADDT                                 35
N01  C     FraDato       setll     HEADDT
N01  C                   READ      HEADDT                                 35
     C     *in35         DOWEQ     '0'
N01  C                   if        HEDTOP > Idag
N01  c                   leave
N01  c                   endif
     C                   exsr      TstPost
     C  N30              exsr      SkrivPost
S01  C*    Idag          READE     HEADDT                                 35
N01  C                   READ      HEADDT                                 35
     C                   END
      *
     C     Slutt         Tag
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *
      ******************************************************************************
     C     TstPost       BEGSR
      ******************************************************************************
     C                   move      *ON           *IN30
      *
      * Oppdraget har alt fått løpenr eller nytt godsnr
     c     HETLL         ifne      *blanks
     c     HEGNN         orne      *blanks
     c                   goto      UtTst
     c                   end
      * Oppdraget er slettemerket
     C     HEST          cabeq     'S'           UtTst
      *
      * Oppdraget er alt i oppfølgingsfila?
S02  c*    HeaKey        chain     RUCKP                              33
S02  C*    *IN33         cabeq     '0'           UtTst
      *
      * Godsnr
      *   utfylt...
     C     HEGN5_9       cabeq     *blanks       UtTst
      *  /innen aktuell bevillingskode ...
      * Begrepet MÅ finnes i -arrayet   (treff ved lookup = 80 ON)
     C     HEGN5_9       LOOKUP    BV                                     80
     C     *IN80         cabeq     '0'           UtTst
      *
N04  C     HEOT          CABEQ     *blanks       UtTst
      * Oppdragstype  INCLUDE / OMIT ..  (treff ved lookup = 80 ON)
     C     RDOTIO        IFEQ      'I'
      * Ved include må begrepet finnes i -arrayet
     C     HEOT          LOOKUP    OT                                     80
     C     *IN80         cabeq     '0'           UtTst
     C                   END
     C     RDOTIO        IFEQ      'O'
      * Ved omit måbegrepet IKKE finnes i-arrayet
     C     HEOT          LOOKUP    OT                                     80
     C     *IN80         cabeq     '1'           UtTst
     C                   END
      *
      *  Kommet gjennom alle tester - posten skal
      *   en sjelden gang posten finnes og skal re-åpnes - OFTE finnes udate kundenr
N02  c     HeaKey        chain     RUCKP
N02  c                   if        %found
N02  c                   if        RUST = 'S' or
N02  c                             RUST = 'A'
N02   * re-åpne en tildigere auto S-merket
N02  c                   if        RUST = 'S'
N02  c                   eval      RUST    =     'A'
N02  c                   eval      RUDTAM  =     *zeros
N02  c                   eval      RUKLAM  =     *zeros
N02  c                   endif
N02   * alltid når den finnes med A-staus - oppdater kundenr/dato - kan være endrer
N02  c                   eval      RUDTOP  =     HEDTOP
N03  c                   move      *zeros        NyKu              8 0
N02  c     HEKNKF        ifne      *zeros
N02  c                   eval      NyKu    =     HEKNKF
N02  c                   else
N02  c                   eval      NyKu    =     HEKNK
N02  c                   end
N02   * hvis kundenummer er endret - sikre 0 i sendedato = Send
N02  c                   if        RUKU    <>    NyKu
N02  c                   eval      RUKU    =     Nyku
N02  c                   eval      RUDTAM  =     *zeros
N02  c                   eval      RUKLAM  =     *zeros
N02  c                   endif
N02  c                   update    RUCKPR
N02  c                   endif
N02  c                   goto      UtTst
N02  c                   endif
      *  Kommet gjennom alle tester - posten skal medtas
     C                   move      *OFF          *IN30
      *
     C     UtTst         ENDSR
      *
      *
      ******************************************************************************
     C     SkrivPost     BEGSR
      ******************************************************************************
     c                   clear                   RUCKPR
     c                   eval      RUAVD   =     HEAVD
     c                   eval      RUOPD   =     HEOPD
     c                   eval      RUST    =     'A'                            Ank.melding sendes
     c                   eval      RUDTOP  =     HEDTOP
     c     HEKNKF        ifne      *zeros
     c                   eval      RUKU    =     HEKNKF
     c                   else
     c                   eval      RUKU    =     HEKNK
     c                   end
     c                   write     RUCKPR
     C                   ENDSR
      *
      *
      ******************************************************************************
     C     INIT          BEGSR
      ******************************************************************************
     C     *entry        PLIST
     c                   parm                    UIN01             1
      *
      * Dersom record ikke finnes eller ant dager ikke gitt - hopp ut.
     C     1             CHAIN     RUCKDFT                            33
     C  N33RDDGF         comp      *zeros                                 33
     C  N33RDDGT         comp      *zeros                                 33
N01  C  N33              eval      SYPAID = 'ANKMF'
N01  C  N33Sypar1Key     CHAIN     SYPARL1                            33
     C     *IN33         ifeq      *ON
     C                   MOVE      '1'           UIN01
     C                   MOVE      *ON           *INLR
     C                   RETURN
     c                   end
N01   * Verdi i numerisk = ant dager å lese tilbake i jakt på ankmeldingfe (0 = kun i dag)
N01   * (Hvis stor  verdi - kan gi TUNG leseloop .. - sett da 10 )
S03  c*                  if        syvrdn > RDDGT
S03  c*                  move      *zeros        syvrdn
N03  c                   if        syvrdn > 10
N03  c                   z-add     10            syvrdn
N01  c                   endif
N01  c                   z-add     syvrdn        dgMinus           5 0
      * Key i RUCKP
     C     HEAKey        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
N01  C     Sypar1Key     KLIST
N01  C                   KFLD                    SYKUNR                         0 = firma
N01  C                   KFLD                    SYPAID
      *
     c                   CALL      'SYGE15C'
     c                   parm                    WDT               8
     c                   parm                    WTM               6
     c                   move      wdt           Idag              8 0
N01  c                   if        DgMinus = 0
N01  c                   move      Idag          FraDato           8 0
N01  c                   else
N01  C                   CALL      'SYGE20'
N01  C                   PARM      'SUB'         FUNK              3
N01  C                   PARM                    IDAG
N01  C                   PARM                    FraDato
N01  C                   PARM                    DgMinus
N01  c                   endif
      *
     C                   ENDSR
