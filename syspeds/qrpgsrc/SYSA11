      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE ALFA LANDKODE             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-68 71-90    ***
      ***                    95-97 99                         ***
      ***********************************************************
      *
     FKODTLK    UF A E           K DISK
     FSYSA11FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  KLKNVN                 1     30
     D  XNVN                   1      4
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2LK          YLK               2
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODLK1        SETLL     KODTLK
     C     START1        TAG
     C                   READ      KODTLK                                 94
     C   94              MOVE      *BLANKS       KLKNVN
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      KODTLK                                 94
     C  N94              UPDATE    KODTLKR
     C  N94              MOVE      KLKLK         YLK
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   WRITE     BUNN
     C                   END
     C                   SETOFF                                       98
     C                   EXFMT     SUBCTL
     C*
     C*** AVSLUTT
     C     ST41          TAG
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY ALFA LANDKODE
     C   06              GOTO      START5
     C*** SØK FRA ALFA LANDKODE
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODLK3        SETLL     KODTLK
     C  N94              READ      KODTLK                                 94
     C                   GOTO      START2
     C                   END
     C*
     C                   EXSR      HENT
     C   98              GOTO      START3
     C*
     C*** SLETT ALFA LANDKODE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODLK2        CHAIN     KODTLK                             33
     C  N33              DELETE    KODTLKR
     C                   EXSR      MOVB1
     C                   MOVEL     '*DLT'        XNVN
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ENDRE ALFA LANDKODE
     C     START5        TAG
     C                   SETOFF                                       30
     C                   EXFMT     VARELI
     C   03              GOTO      SLUTT
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
     C*
     C*** SØK FRA UTSKRIFTTYPE
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSLK          COMP      '  '                                   70
     C   70              SETON                                        30
     C   70              GOTO      SLTB
     C     WSNVN         COMP      *BLANKS                                69
     C   69              SETON                                        30
     C   69              GOTO      SLTB
     C                   END
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODLKK        CHAIN     KODTLK                             33
     C                   EXSR      MOVWLK
     C  N33              UPDATE    KODTLKR
     C   33              WRITE     KODTLKR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     XNVN          COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVLKW
     C                   MOVE      ' '           WSNR
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     XNVN          COMP      '*DLT'                                 98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KLKLK
     C                   MOVE      *BLANKS       KLKNVN
     C                   MOVE      *BLANKS       KLKALF
     C                   MOVE      *BLANKS       KLKLK2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSLK
     C                   MOVE      *BLANKS       WSNVN
     C                   MOVE      *BLANKS       WSALF
     C                   MOVE      *BLANKS       WSLK2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVLKW        BEGSR
     C***********************************************
     C                   MOVE      KLKLK         WSLK
     C                   MOVE      KLKNVN        WSNVN
     C                   MOVE      KLKALF        WSALF
     C                   MOVE      KLKLK2        WSLK2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWLK        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      'LK '         KLKUNI
     C                   MOVE      WSLK          KLKLK
     C                   MOVE      *ZEROS        KLKMIN
     C                   MOVE      *BLANKS       KLKDIS
     C                   MOVE      *ZEROS        KLKPRO
     C                   END
     C*
     C                   MOVE      WSNVN         KLKNVN
     C                   MOVE      WSALF         KLKALF
     C                   MOVE      WSLK2         KLKLK2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODLKK        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    WSLK
     C     KODLK1        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    W2LK
     C     KODLK2        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    KLKLK
     C     KODLK3        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    YLK
     C*
     C                   MOVE      'LK '         YLKUNI            3
     C*
     C                   ENDSR
     C*
