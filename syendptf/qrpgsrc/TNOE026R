      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOE026R MODULE(*LIBL/TNOE026R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSAEVCNL1  UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WN              S             15
     D SVERR           S              1
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SVAVD:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SVTDN:'M'))+'¬, '
        +'¬lin¬ : ¬'+%trim(%editc(SVLI:'Z'))+'¬, '
        +'¬svcnr¬ : ¬'+%trim(SVCNR)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"containerlist" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SAEVCNL1

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER     = zhbgetvar('USER')
     C                   EVAL      WN         = zhbgetvar('AVD')
     C                   EVAL      SVAVD      = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('OPD')
     C                   EVAL      SVTDN      = c2n2(WN)
     C                   EVAL      WN         = zhbgetvar('LIN')
     C                   EVAL      SVLI       = c2n2(WN)
     C                   EVAL      SVCNR      = zhbgetvar('SVCNR')
     C                   EVAL      WMODE      = zhbgetvar('MODE')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SAEVKEY       KLIST
     C                   KFLD                    SVAVD
     C                   KFLD                    SVCNR
     C                   KFLD                    SVTDN
     C                   KFLD                    SVLI

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SAEVCNL1

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     WMODE         IFNE      'A'
     C     SAEVKEY       CHAIN     SAEVCNL1                           55
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   EXSR      SRA
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Container not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   WRITE     SAEVCNR
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    SAEVCNR
     C                   ENDSR
