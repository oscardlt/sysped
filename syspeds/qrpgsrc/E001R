********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10104 * 01 CB  PTF10 Ny opsjon
********************************************************************
     FE001D     CF   E             WORKSTN
     F                                     SFILE(E001DB:ZRECNO)
     FEQUA902   UF A E           K DISK
     FEQUA9021  IF   E           K DISK
     F                                     RENAME(QUAR:QUAR1)
     FEQTA902   IF   E           K DISK
      *_________________
      * DS for constants
      *"""""""""""""""""
     D                 DS
     D  WTXT01                 1     40    INZ('(No codes match the -
     D                                     subsetting criteria)')
      *//////////////
      *  Main logic /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Move blanks to PF                                  MOVEA /
      *////////////////////////////////////////////////////////////
     C     MOVEA         BEGSR
     C                   MOVE      *BLANKS       QUALTX
     C                   MOVE      *BLANKS       QUALD1
     C                   MOVE      *BLANKS       QUALD2
     C                   MOVE      *BLANKS       QUALD3
     C                   MOVE      *BLANKS       QUALD4
     C                   MOVE      *BLANKS       QUALD5
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move format to PF                                  MOVEB /
      *////////////////////////////////////////////////////////////
     C     MOVEB         BEGSR
     C                   MOVE      ÅUALTX        QUALTX
     C                   MOVE      ÅUALD1        QUALD1
     C                   MOVE      ÅUALD2        QUALD2
     C                   MOVE      ÅUALD3        QUALD3
     C                   MOVE      ÅUALD4        QUALD4
     C                   MOVE      ÅUALD5        QUALD5
     C                   MOVE      'P'           QUALTY
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move PF to format                                  MOVEC /
      *////////////////////////////////////////////////////////////
     C     MOVEC         BEGSR
     C                   MOVE      QUALTX        ÅUALTX
     C                   MOVE      QUALD1        ÅUALD1
     C                   MOVE      QUALD2        ÅUALD2
     C                   MOVE      QUALD3        ÅUALD3
     C                   MOVE      QUALD4        ÅUALD4
     C                   MOVE      QUALD5        ÅUALD5
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Move blanks to format                              MOVED /
      *////////////////////////////////////////////////////////////
     C     MOVED         BEGSR
     C                   MOVE      *BLANKS       ÅUALTX
     C                   MOVE      *BLANKS       ÅUALD1
     C                   MOVE      *BLANKS       ÅUALD2
     C                   MOVE      *BLANKS       ÅUALD3
     C                   MOVE      *BLANKS       ÅUALD4
     C                   MOVE      *BLANKS       ÅUALD5
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  INZ                                                  SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________
      * Define Workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    QUALCO        WUALCO
     C     *LIKE         DEFINE    QUALTX        WUALTX
     C                   MOVE      *BLANKS       WUALCO
      *_____________
      * Define key's
      *"""""""""""""
     C     KEYA          KLIST                                                  QUALF
     C                   KFLD                    WUALTA
     C                   KFLD                    WUALCO
     C     KEYB          KLIST                                                  QUALL1
     C                   KFLD                    WUALTA
     C                   KFLD                    WUALTX
      *________________________
      * Input/output parameters
      *""""""""""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WUALTA                         Input
     C                   PARM                    QUALCO                         Output
      *_____________________
      * Is this a ext.call ?
      *"""""""""""""""""""""
     C     WUALTA        COMP      *BLANKS                                81    =internal
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Calculate formats                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Select tag
      *"""""""""""
     C     SRB0          TAG
      *
     C     *IN81         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB9
     C                   ENDIF
      *
     C     SRB1          TAG
      *________________
      * Get name of tag
      *""""""""""""""""
     C     WUALTA        CHAIN     EQTA902                            20
     C     *IN20         IFEQ      '0'
     C                   MOVEL     QTAGTX        WTAGTX
     C                   ELSE
     C                   MOVEL     *BLANKS       WTAGTX
     C                   END
      *______________
      * Clear Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     E001DC
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
     C     XUALCO        IFEQ      '*ALL  '
     C                   MOVE      *BLANKS       WUALCO
     C                   ELSE
     C                   MOVE      XUALCO        WUALCO
     C                   END
     C     XUALTX        IFEQ      '*ALL'
     C                   MOVE      *BLANKS       WUALTX
     C                   MOVE      '0'           *IN82                          SORT BY CODE
     C                   ELSE
     C                   MOVE      '1'           *IN82                          SORT BY ALPHA
     C                   MOVEL     XUALTX        WUALTX
     C                   END
      *_____________
      * Fill Subfile
      *"""""""""""""
     C  N82KEYA          SETLL     EQUA902
     C   82KEYB          SETLL     EQUA9021
     C  N82WUALTA        READE(N)  EQUA902                                94
     C   82WUALTA        READE     EQUA9021                               94
     C     *IN94         IFEQ      '1'
     C                   MOVE      *BLANKS       QUALCO
     C                   MOVEL     WTXT01        QUALTX
     C                   MOVE      '1'           *IN41
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     E001DB
     C                   GOTO      SRB3
     C                   ENDIF
      *
     C     SRB2          TAG
     C                   Z-ADD     WRECNO        ZRECNO
      *
     C  N94              DO        12
     C                   MOVE      '0'           *IN41
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     E001DB
     C  N82WUALTA        READE(N)  EQUA902                                94
     C   82WUALTA        READE     EQUA9021                               94
     C  N94              END
      *______
      * Enter
      *""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E001DØ
     C                   WRITE     E001DD
     C                   EXFMT     E001DC
      *
      *________
      * F3=Exit
      *""""""""
     C   03              MOVE      *BLANKS       QUALCO
     C     *IN03         CABEQ     '1'           SRB9
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         IFEQ      '1'
     C     *IN81         CABEQ     '1'           SRB0
     C  N81              MOVE      *BLANKS       QUALCO
     C     *IN81         CABEQ     '0'           SRB9
     C                   ENDIF
      *
     C                   MOVEA     '00'          *IN(91)
      *_______
      * ROLLUP
      *"""""""
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      SRB2
     C                   END
      *___________
      * F5=Refresh
      *"""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *__________
      * F6=Create
      *""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBF
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   END
      *_________
      * Option ?
      *"""""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN05
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     E001DB                                 52
     C     *IN52         IFEQ      '0'
     C     ZOP           CABEQ     '1'           SRB9                           Select
     C     ZOP           CASEQ     '2'           SRBB                           Change
     C     ZOP           CASEQ     '3'           SRBC                           Copy
     C     ZOP           CASEQ     '4'           SRBD                           Delete
     C     ZOP           CASEQ     '5'           SRBE                           Display
N01  C     ZOP           CASEQ     '6'           SRBG                           X-ref
     C                   END
     C                   END
      *
     C  N52              END
      *
     C     *IN03         CABEQ     '1'           SRB9                           End
     C     *IN05         CABEQ     '1'           SRB1                           Refresh
     C     *IN05         CABEQ     '0'           SRB3                           Subfile
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Select tag                                          SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C     SRBA1         TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E001DØ
     C                   EXFMT     E001DA
     C                   MOVEA     '00'          *IN(30)
      *________
      * F3=Exit
      *""""""""
     C     *IN03         CABEQ     '1'           SRBA9
      *__________
      * F4=Prompt
      *""""""""""
     C     *IN04         IFEQ      '1'
     C                   CALL      'E002R'
     C                   PARM                    WUALTA
     C                   GOTO      SRBA1
     C                   ENDIF
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         CABEQ     '1'           SRBA9
      *______
      * Enter
      *""""""
      * Qualfier tag valid ?
     C     WUALTA        CHAIN     EQTA902                            30
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN31
     C                   MOVE      'SY00010'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
     C     SRBA9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Change                                              SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *____________
      * Move fields
      *""""""""""""
     C                   MOVE      QUALCO        WUALCO
     C     KEYA          CHAIN(N)  EQUA902                            53
     C                   MOVE      QUALCO        ÅUALCO
     C                   MOVE      '1'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEC
     C                   MOVEA     '0000'        *IN(30)
      *
     C     SRBB1         TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E001DØ
     C                   EXFMT     E001DE
     C                   MOVEA     '000'         *IN(30)
     C     *IN12         IFEQ      '0'
      *___________
      * Test input
      *"""""""""""
     C   30              GOTO      SRBB1
      *__________
      * Update PF
      *""""""""""
     C     KEYA          CHAIN     EQUA902                            53
     C                   EXSR      MOVEB
     C                   UPDATE    QUAR
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    E001DB
      *
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Copy                                                SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      *____________
      * Copy fields
      *""""""""""""
     C                   MOVE      QUALCO        WUALCO
     C     KEYA          CHAIN(N)  EQUA902                            53
     C                   MOVE      QUALCO        ÅUALCO
     C                   MOVE      '0'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEC
      *_____________
      * Blank fields
      *"""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   EXSR      MOVEA
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E001DØ
     C                   EXFMT     E001DE
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         IFEQ      '0'
      *__________
      * Add to PF
      *""""""""""
     C                   MOVE      ÅUALCO        QUALCO
     C                   MOVE      WUALTA        QUALTA
     C                   EXSR      MOVEB
     C                   WRITE     QUAR
      *_______________
      * Add to subfile
      *"""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   MOVE      '0'           *IN41
     C                   WRITE     E001DB
      *
     C                   END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Delete                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Delete record
      *""""""""""""""
     C                   MOVE      QUALCO        WUALCO
     C     KEYA          CHAIN     EQUA902                            53
     C                   DELETE    QUAR
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    E001DB
     C                   MOVE      '1'           *IN05
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Display                                             SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      QUALCO        WUALCO
     C     KEYA          CHAIN(N)  EQUA902                            53
     C                   MOVE      QUALCO        ÅUALCO
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN62
     C                   EXSR      MOVEC
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E001DØ
     C                   EXFMT     E001DE
     C                   MOVEA     '000'         *IN(30)
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Create                                              SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
      *_____________
      * Blank fields
      *"""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      '0'           *IN61
     C                   MOVE      '0'           *IN62
     C                   EXSR      MOVEA
     C                   EXSR      MOVED
      *
     C     *IN03         DOWEQ     '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVE      *BLANKS       ÅUALCO
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E001DØ
     C                   EXFMT     E001DE
     C                   MOVEA     '0000'        *IN(30)
      *______________
      * End or Cancel
      *""""""""""""""
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *__________
      * Add to PF
      *""""""""""
     C                   MOVE      ÅUALCO        QUALCO
     C                   MOVE      WUALTA        QUALTA
     C                   EXSR      MOVEB
     C                   WRITE     QUAR
      *_______________
      * Add to subfile
      *"""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   MOVE      '0'           *IN41
     C                   WRITE     E001DB
      *
     C                   ENDIF
     C                   ENDDO
      *
     C                   ENDSR
N01   *////////////////////////////////////////////////////////////
N01   *  X-ref                                               SRBG /
N01   *////////////////////////////////////////////////////////////
N01  C     SRBG          BEGSR
N01  C                   CALL      'E004R'
N01  C                   PARM                    WUALTA
N01  C                   PARM                    QUALCO
     C                   PARM      0             UXKNR             8 0
     C                   PARM                    UXKODE            6
N01  C                   ENDSR
