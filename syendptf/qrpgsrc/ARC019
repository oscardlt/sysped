********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 unngå dobbelt PDF  ....
********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8XXX * 01 sh  PTF10  bytter arkivfil ved annen ref
P8XXX * 02 sh  PTF10  tull ved visse artyper
P8XXX * 03 sh  PTF12  ARKIV LAGER
      * 04 sh  PTF12  http rettlse
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SAKSBEHANDLER(SIGNATUR)   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FARUSERF   IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     Fheadf     IF   E           K DISK
     Fgssorf    IF   E           K DISK
     Farkivl08  UF   E           K DISK
n01  Farkivl11  IF   E           K DISK
n01  F                                     RENAME(arkivr:arki11)
     FARC019FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D                SDS
     D  UUSER                254    263
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  aruser                 1     10
     D  KOSFN7                 1      7
     D                 DS
     D  WSUNDE                 1     10
     D  WSUN02                 1      2
     D  WSUN07                 3      9
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
N01  C     w2unde        ifeq      *blanks
     C     KEYF08S       SETLL     ARKIVL08
n01  c                   else
n01  c     keyf11s       setll     arkivl11
n01  c                   end
     C     START1        TAG
N01  C     w2unde        ifeq      *blanks
     C     KEYF08        READE(N)  ARKIVL08                               94
n01  c                   else
n01  c     keyf11        reade     arkivl11                               94
n01  c                   end
      *
     C     START2        TAG
      *
     c                   move      aravd         w2avd
     c                   move      aropd         w2opd
     C  N94              DO
     C                   DO        4
S01  c*    W2unde        ifne      *blanks
S01  c*    ARUNDE        downe     w2unde
S01  C*    KEYF08        READE(N)  ARKIVL08                               94
S01  c*    *IN94         cabeq     '1'           start2
s01  c*                  end
s01  c*                  end
     C                   ADD       1             ZRECNO
      *
     C                   MOVE      *BLANKS       ARTXT
     C     ARTYPE        CHAIN     ARKTXT                             44
     C   44              MOVEL     'UKJENT'      ARTXT
      *
     C                   MOVE      *BLANKS       KNAVN
     C     KEYKUN        CHAIN     CUNDL01                            44
     C   44              MOVEL     '*Udefinert*' KNAVN
      *
s02  c*    artype        ifeq      'go'
s02  c*    artype        oreq      'am'
s02  c*    artype        oreq      'fx'
s02  c*    artype        oreq      'fs'
s02  c*                  move      *zeros        aravd
s02  c*                  move      *zeros        aropd
s02  c*                  end
     c                   move      'I'           IE                1
     c                   exsr      genbane
     c                   move      'E'           IE
     c                   exsr      genbane
     c     arinek        ifeq      'E'
     C                   move      wsline        wslinw           76
     c                   else
     C                   move      wslink        wslinw
     c                   end
N01  C     w2unde        comp      *blanks                            7777
     C                   WRITE     SUBFIL
N01  C     w2unde        ifeq      *blanks
     C     KEYF08        READE(N)  ARKIVL08                               94
n01  c                   else
n01  c     keyf11        reade     arkivl11                               94
n01  c                   end
      *
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
n01  c  N94w2unde        ifeq      *blanks
     C  N94keyf08        readpe(N) arkivl08                               94
n01  c                   else
n01  c     keyf11        readpE    arkivl11                               94
n01  c                   end
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTsr
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
      *
      * tester tastet oppdragsnummer
      *
     C                   Z-ADD     22            LINNBR
     c                   movea     '000'         *in(70)
     c     wstype        chain     arktxt                             71
     c     *in(71)       ifeq      '1'
     C                   MOVE      'SHU1020'     MSGNR
     c                   seton                                        30
     C                   Z-ADD     7             POSNBR
     C   30              GOTO      SLTB
     c                   end
     C*
      *
      * tester tastet oppdragsnummer
      *
     c     wsun02        iflt      'A '
     c     wsun02        orgt      'ZZ'
     c     keyhea        chain     headf                              70
     c     *in(70)       ifeq      '1'
     C                   MOVE      'SPO0001'     MSGNR
     c                   seton                                        30
     C                   Z-ADD     31            POSNBR
     C                   GOTO      SLTB
     C                   end
     C                   else
     C                   testn                   wsun07               33
     c     *in(33)       ifeq      '0'
     C                   MOVE      'SHU1084'     MSGNR
     c                   seton                                        30
     C                   Z-ADD     37            POSNBR
     C                   GOTO      SLTB
     c                   end
     c                   move      wsun07        ssnr
     c     keyss         chain     gssorf                             33
     c     *in(33)       ifeq      '1'
     C                   MOVE      'SHU1085'     MSGNR
     c                   seton                                        30
     C                   Z-ADD     37            POSNBR
     C                   GOTO      SLTB
     c                   end
     c                   end
     C*
     C*
      *
      * tester tastet kundenummer
      *
     c     wskund        ifne      *zeros
     c     keyku2        chain     cundl01                            72
     c     *in(72)       ifeq      '1'
     C                   MOVE      'SPK0001'     MSGNR
     c                   seton                                        30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      SLTB
     c                   end
     c                   end
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KEYUNI        CHAIN     ARKIVL08                           33
     C     *IN(33)       IFEQ      '0'
     C                   EXSR      MOVWSF
     C  N33              UPDATE    ARKIVR
     C                   UPDATE    SUBFIL
     C                   END
     C                   WRITE     SUBCTL
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENTsr        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KEYUNI        CHAIN     ARKIVL08                           33
     C  N33              DELETE    ARKIVL08
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
      * KALLES VED SLETTING
     C                   MOVE      *BLANKS       ARUSER
     C                   MOVE      *BLANKS       ARUNDE
     C                   MOVE      *ZEROS        ARAVD
     C                   MOVE      *ZEROS        AROPD
     C                   MOVEL     '*DELETE'     KOSFN7
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      ARTYPE        WSTYPE
     C                   MOVE      ARAVD         WSAVD
     C                   MOVE      AROPD         WSOPD
     C                   MOVE      ARUNDE        WSUNDE
     C                   MOVE      ARKUND        WSKUND
     C                   MOVE      ARRFK         WSRFK
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     18            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C*
     C                   MOVE      WSTYPE        ARTYPE
     C                   MOVE      WSAVD         ARAVD
     C                   MOVE      WSOPD         AROPD
     C                   MOVE      WSUNDE        ARUNDE
     C                   MOVE      WSKUND        ARKUND
     C                   MOVE      WSRFK         ARRFK
      * SETTER STATUS FRA E=ERROR TIL  A=OK
     C     ARSTAT        IFEQ      'E'
     C                   MOVE      'A'           ARSTAT
     C                   END
      *
      *
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     arklagk       KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    arklag
     C*
      *
      * KEY FOR ALLE MED FEILSTATUS
      *
     C     KEYF08s       KLIST
     C                   KFLD                    UUSTAT            1
     C                   KFLD                    W2AVD
     C                   KFLD                    W2OPD
n01  C     KEYF11s       KLIST
n01  C                   KFLD                    UUSTAT            1
n01  C                   KFLD                    W2UNDE
      *
     C                   MOVE      'A'           UUSTAT
      *
      *
     C     KEYF08        KLIST
     C                   KFLD                    UUSTAT            1
      *
n01  C     KEYF11        KLIST
n01  C                   KFLD                    UUSTAT            1
      * KEY FOR TYPE DATO
      *
      *
     C     KEYKUN        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ARKUND
      *
     C     KEYKU2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    wsKUND
     C     KEYUNI        KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    ARAVD
     C                   KFLD                    AROPD
     C                   KFLD                    ARDATE
     C                   KFLD                    ARTIME
     C                   KFLD                    ARLINK
     C     KEYhea        KLIST
     C                   KFLD                    wsavd
     C                   KFLD                    wsopd
     C     KEYSS         KLIST
     C                   KFLD                    SSNR              7 0
      *
      * SLÅR PÅ 60 SOM GJØR AT FEILSTATUS LESES FØRST
      *
     C                   SETON                                        60
     C*
     C                   READ      FIRM                                   33
     C*
     c     uuser         chain     aruserf                            39
     C                   ENDSR
     C*
     C***********************************************
     C     genbane       BEGSR
     C***********************************************
     c     keyext        klist
     c                   kfld                    fifirm
     c                   kfld                    arcext
     C     ARKLAG        IFNE      *blanks
     c     arklagk       chain     arkext
     c                   else
     c     fifirm        chain     firmarc                            33
     c                   end
N03  c     arbhis        ifne      *blanks
N03  c                   eval      arcane=arbhis
N03  c                   end
     c
     C* genererer bane for lagring
     C                   move      *blanks       updf
     c                   movel     'http://'     updf             76
N04  c                   if        ARCPSI='J'
N04  c                   eval      updf = 'https://'
N04  c                   endif
     c     ie            ifeq      'I'
     C     updf          CAT       arcpad:0      updf
     c                   else
     C     updf          CAT       arcpae:0      updf
     c                   end
     C     updf          CAT       '/':0         updf
     c     arcane        ifne      *blanks
     C     updf          CAT       arcane:0      updf
     C     updf          CAT       '/':0         updf
     c                   end
     C     updf          CAT       arlink:0      updf
s01  C*    ARKJN         IFNE      'E'
s01  C*    updf          CAT       '.pdf':0      updf
s01  C*                  END
n01  C     '.pdf'        SCAN      updf                                   33
n01  C  N33'.PDF'        SCAN      updf                                   33
n01  C  N33'.'           SCAN      arlink                                 33
n01  C     arklag        IFeq      *blanks
n01  C     *in33         andeq     '0'
n01  C     updf          CAT       '.pdf':0      updf
n01  C                   END
     c     ie            ifeq      'I'
     C                   movel     updf          wslink           76
     c                   else
     C                   movel     updf          wsline           76
     c                   end
     c                   endsr
     C*
