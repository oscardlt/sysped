     A*%%TS  SD  20161205  101148  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLGRP1' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S01  A*                                     WINDOW(*DFT 15 73 *NOMSGLIN)
N01  A*                                     WINDOW(*DFT 15 75 *NOMSGLIN)
N04  A                                      WINDOW(*DFT 15 115 *NOMSGLIN)
     A                                      OVERLAY
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20161205  100653  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  5  2VALUES(' ' '1' 'S' 'P')
     A            LEVNR     R        O  5  5REFFLD(KUNDR/KUNDNR SYSPEDF/CUNDF)
     A  42                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            LNAVN     R        O  5 14REFFLD(KUNDR/KNAVN SYSPEDF/CUNDF)
     A  42                                  COLOR(WHT)
S01  A*           ADR1      R        O  5 43REFFLD(KUNDR/ADR1 SYSPEDF/CUNDF)
     A            ADR1      R        O  5 45REFFLD(KUNDR/ADR1 SYSPEDF/CUNDF)
     A  50                                  DSPATR(RI)
     A  42                                  COLOR(WHT)
     A            ADR2      R        O  6 14REFFLD(KUNDR/ADR2 SYSPEDF/CUNDF)
     A                                      DSPATR(UL)
     A  42                                  COLOR(WHT)
DD1  A*           POSTNR    R        O  6 43REFFLD(KUNDR/POSTNR SYSPEDF/CUNDF)
S01  A*           ADR3      R        O  6 49REFFLD(KUNDR/ADR3 SYSPEDF/CUNDF)
     A            ADR3TO        45A  O  6 45DSPATR(UL)
     A  42                                  COLOR(WHT)
     A            SFARKIV        1A  O  5 76COLOR(RED)
     A            FORETN         9S 0O  6 98DSPATR(UL)
     A  42                                  COLOR(WHT)
     A            BANKG         35A  O  5 79DSPATR(UL)
     A  42                                  COLOR(WHT)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20161205  100653  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0H      SFLRCDNBR
     A                                  1  2'SO1LEV'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(035 JOV0281 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*           ZRECNO         4S 0O  2 50SFLRCDNBR
     A*                                     DSPATR(ND)
     A                                  3  1MSGCON(110 JOV0283 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1' '5')
     A                                      DSPATR(UL)
     A            W2LENR         8D 0I  4  5DSPATR(UL)
     A            W2ALFA        10A  I  4 14DSPATR(UL)
     A                                  4 25MSGCON(012 DIV1698 *LIBL/SYMSGF)
     A                                  2 29MSGCON(035 JOV1434 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 75'Ar'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SO1LEV '
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(35 JOV0281 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(25 DIV1034 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  1MSGCON(70 JOV0283 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1' '5')
     A                                      DSPATR(UL)
     A            W2LENR         8D 0I  4  5DSPATR(UL)
     A            W2ALFA        10A  I  4 14DSPATR(UL)
     A                                  4 25MSGCON(12 DIV1698 SYMSGF)
     A N30                             15  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
