      * Dette programmet startes fra CLP: E115C_901 som i sin tur startes fra
      * SYEDI Standard gjenom tabellen Kunders standardmeldinger.
      *
      * Skriver til IBM L1 OIXXF filer.
     FEUNB      IF   E           K DISK
     FEUNH      IF   E           K DISK
     FEDIM6     IF   E           K DISK
     FECNT901   IF   E           K DISK
     FEDTM901   IF   E           K DISK
     FELIN901   IF   E           K DISK
     FENAD901   IF   E           K DISK
     FOI00F     O  A E             DISK
     FOI10F     O  A E             DISK
     FOI11F     O  A E             DISK
     FOI12F     O  A E             DISK
     FOI50F     O  A E             DISK
     FOI73F     O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT901                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   OI73RR

     C                   MOVEL     U0004         OITRAD
     C                   MOVEL     U0062A        OIDCRF
     C                   MOVEL     U1004         OIORDN
     C                   MOVEL     C6069A        CNTCQ1
     C                   MOVEL     C6066A        CNTCV1
     C                   MOVEL     C6069B        CNTCQ2
     C                   MOVEL     C6066B        CNTCV2

     C                   WRITE     OI73RR

     C  N51KEYMS0        READE     ECNT901                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * DTM00                                               DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM901                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   OI12RR

     C                   MOVEL     U0004         OITRAD
     C                   MOVEL     U0062A        OIDCRF
     C                   MOVEL     U1004         OIORDN
     C                   MOVEL     D2005         DTMDTQ
     C                   MOVEL     D2001         DTMDTK

     C                   WRITE     OI12RR

     C  N51KEYMS0        READE     EDTM901                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1

     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get interchange values
     C     ZMN           CHAIN     EUNB                               51

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LIN02                                               LIN02 /
      *////////////////////////////////////////////////////////////
     C     LIN02         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     2             ZGN0
     C     KEYMS0        SETLL     ELIN901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELIN901                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   OI50RR

     C                   MOVEL     U0004         OITRAD
     C                   MOVEL     U0062A        OIDCRF
     C                   MOVEL     U1004         OIORDN
     C                   MOVEL     L1082         LINLIN
     C                   MOVEL     L7020A        LINAN1
     C                   MOVEL     L7023A        LINID1
     C                   MOVEL     L6063         LINQQ
     C                   MOVEL     L6060         LINQNT
     C                   MOVEL     L5110A        LINPU
     C                   MOVEL     L5375A        LINPTC
     C                   MOVEL     L6170         LINNPU
     C                   MOVEL     L5116         LINIA

     C                   WRITE     OI50RR

     C     KEYMS0        READE     ELIN901                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD00                                               NAD00 /
      *////////////////////////////////////////////////////////////
     C     NAD00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ENAD901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD901                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   OI11RR

     C                   MOVEL     U0004         OITRAD
     C                   MOVEL     U0062A        OIDCRF
     C                   MOVEL     U1004         OIORDN
     C                   MOVEL     N3035         NADPRQ
     C                   MOVEL     N3039         NADPRK
     C                   MOVEL     N3124A        NADAD1
     C                   MOVEL     N3124B        NADAD2
     C                   MOVEL     N3124C        NADAD3
     C                   MOVEL     N3124D        NADAD4
     C                   MOVEL     N3124E        NADAD5

     C                   WRITE     OI11RR

     C     KEYMS0        READE     ENAD901                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNB00                                               UNB00 /
      *////////////////////////////////////////////////////////////
     C     UNB00         BEGSR

     C                   CLEAR                   OI00RR

     C                   MOVEL     U0004         OITRAD
     C                   MOVEL     U0001         UNBSTX
     C                   MOVEL     U0002         UNBVSN
     C                   MOVEL     U0004         UNBSID
     C                   MOVEL     U0007S        UNBSQA
     C                   MOVEL     U0010         UNBRID
     C                   MOVEL     U0007R        UNBRQA
     C                   MOVEL     U0017         UNBDAT
     C                   MOVEL     U0019         UNBTIM
     C                   MOVEL     U0020A        UNBICR
     C                   MOVEL     U0026         UNBAPP

     C                   WRITE     OI00RR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR

     C     MMN           CHAIN     EUNH                               51

     C                   CLEAR                   OI10RR

     C                   MOVEL     U0004         OITRAD
     C                   MOVEL     U0062A        OIDCRF
     C                   MOVEL     U1004         OIORDN
     C                   MOVEL     U0062A        UNHRNR
     C                   MOVEL     U0065         UNHMTY
     C                   MOVEL     U0052         UNHVER
     C                   MOVEL     U0054         UNHREL
     C                   MOVEL     U0051         UNHAGE
     C                   MOVEL     U1001         BGMDNC
     C                   MOVEL     U1004         BGMDMN
     C                   MOVEL     U2782A        BGMDAT

     C                   WRITE     OI10RR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   EXSR      UNB00

     C     ZMN           CHAIN     EDIM6                              50
     C     *IN50         DOWEQ     *OFF

     C                   EXSR      UNH00
     C                   EXSR      NAD00
     C                   EXSR      DTM00
     C                   EXSR      LIN02
     C                   EXSR      CNT00

     C     ZMN           READE     EDIM6                                  50
     C                   ENDDO

     C                   ENDSR
