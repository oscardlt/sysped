     A*%%TS  SD  20150423  090358  JOVO        REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P06XXA* 01 YBC PTF6 FETESKRIFT PÅ FLYFRAKTBREV
P0601A* 02 JOV PTF6 F4 / FRIT.KODE OGSÅ FOR F.BREV
P8068A* 11 TH  PTF8 NY HELPFUNKSJON KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/KODTP)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA28PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AVDELINGERS F-
     A                                      ASTE DATA')
     A                                      ALTHELP
     A                                      HELP
     A          R BILDE01
     A                                      CF02(02 'Kopi OPPLYN.')
     A                                      CF03(03 'Avslutt program')
     A                                  1  2'SYSPED/SYFA28'
     A                                      COLOR(BLU)
     A                                  1 56DATE
     A                                      EDTCDE(Y)
     A                                  1 66TIME
     A                                  1 19MSGCON(35 DIV1220 SYMSGF)
     A                                      DSPATR(HI)
     A                                  8  2MSGCON(15 DIV1221 SYMSGF)
     A            WSAAVD         4Y 0B  8 20DSPATR(UL)
     A  99
     AO 96                                  DSPATR(PC)
     A  99
     AO 96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A N27                             11  2MSGCON(45 DIV5097 SYMSGF)
     A                                      COLOR(BLU)
     A N27                             13  2MSGCON(15 DIV1221 SYMSGF)
     A N27        WSAVDK         4Y 0B 13 20DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A N27                             15  2MSGCON(45 DIV5098 SYMSGF)
     A N27        WSSLET         1   B 15 49DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      VALUES(' ' 'J' 'N')
     A N27                             23  2MSGCON(18 DIV1638 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(78 SPA0001 SYMSGF)
     A                                      COLOR(WHT)
     A  98                             24  2MSGCON(78 SPA0031 SYMSGF)
     A                                      COLOR(WHT)
     A  96                             24  2MSGCON(78 SPA0032 SYMSGF)
     A                                      COLOR(WHT)
     A N99N98N96                       24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R BILDE02
     A*%%TS  SD  20150423  090358  JOVO        REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Bytt avdelinger')
     A                                      CF04(04 'SØK')
     A          H                           HLPPNLGRP(SYFA280030 SYFA28PNL)
     A  *DS3                                HLPARA(04 002 04 031)
     A  *DS4                                HLPARA(04 002 04 031)
     A          H                           HLPPNLGRP(SYFA280035 SYFA28PNL)
     A  *DS3                                HLPARA(04 040 04 069)
     A  *DS4                                HLPARA(04 040 04 069)
     A          H                           HLPPNLGRP(SYFA280040 SYFA28PNL)
     A  *DS3                                HLPARA(06 002 06 037)
     A  *DS4                                HLPARA(06 002 06 037)
     A          H                           HLPPNLGRP(SYFA280050 SYFA28PNL)
     A  *DS3                                HLPARA(06 040 06 069)
     A  *DS4                                HLPARA(06 040 06 069)
     A          H                           HLPPNLGRP(SYFA280060 SYFA28PNL)
     A  *DS3                                HLPARA(07 002 07 075)
     A  *DS4                                HLPARA(07 002 07 075)
     A          H                           HLPPNLGRP(SYFA280070 SYFA28PNL)
     A  *DS3                                HLPARA(09 002 09 032)
     A  *DS4                                HLPARA(09 002 09 032)
     A          H                           HLPPNLGRP(SYFA280080 SYFA28PNL)
     A  *DS3                                HLPARA(09 040 09 067)
     A  *DS4                                HLPARA(09 040 09 067)
     A          H                           HLPPNLGRP(SYFA280090 SYFA28PNL)
     A  *DS3                                HLPARA(10 002 10 025)
     A  *DS4                                HLPARA(10 002 10 025)
     A          H                           HLPPNLGRP(SYFA280100 SYFA28PNL)
     A  *DS3                                HLPARA(12 002 15 072)
     A  *DS4                                HLPARA(12 002 15 072)
     A          H                           HLPPNLGRP(SYFA280110 SYFA28PNL)
     A  *DS3                                HLPARA(16 002 16 072)
     A  *DS4                                HLPARA(16 002 16 072)
     A          H                           HLPPNLGRP(SYFA280120 SYFA28PNL)
     A  *DS3                                HLPARA(17 002 17 056)
     A  *DS4                                HLPARA(17 002 17 056)
     A          H                           HLPPNLGRP(SYFA280130 SYFA28PNL)
     A  *DS3                                HLPARA(18 002 18 079)
     A  *DS4                                HLPARA(18 002 18 079)
     A          H                           HLPPNLGRP(SYFA280140 SYFA28PNL)
     A  *DS3                                HLPARA(19 002 19 031)
     A  *DS4                                HLPARA(19 002 19 031)
     A          H                           HLPPNLGRP(SYFA280150 SYFA28PNL)
     A  *DS3                                HLPARA(20 002 20 079)
     A  *DS4                                HLPARA(20 002 20 079)
     A          H                           HLPPNLGRP(SYFA280160 SYFA28PNL)
     A  *DS3                                HLPARA(21 002 21 074)
     A  *DS4                                HLPARA(21 002 21 074)
     A          H                           HLPPNLGRP(SYFA280170 SYFA28PNL)
     A  *DS3                                HLPARA(22 002 22 025)
     A  *DS4                                HLPARA(22 002 22 025)
     A          H                           HLPPNLGRP(SYFA280180 SYFA28PNL)
     A  *DS3                                HLPARA(22 030 22 043)
     A  *DS4                                HLPARA(22 030 22 043)
     A          H                           HLPPNLGRP(SYFA280190 SYFA28PNL)
     A  *DS3                                HLPARA(22 045 22 078)
     A  *DS4                                HLPARA(22 045 22 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA28'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(035 DIV1220 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 55DATE
     A                                      EDTCDE(Y)
     A                                  1 64TIME
     A                                  2 19MSGCON(008 DIV1222 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(015 DIV1221 *LIBL/SYMSGF)
     A            KOAAVD         4Y 0O  3 25COLOR(WHT)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  3 31COLOR(WHT)
     A                                  4  2MSGCON(018 DIV1225 *LIBL/SYMSGF)
     A            WSVPRO         1A  B  4 25DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      VALUES('J' 'N')
     A                                  4 27'(J/N)'
     A                                  4 40MSGCON(018 YBC0858 *LIBL/SYMSGF)
     A            WSWFET         1A  B  4 63DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  4 65'(J/N)'
     A                                  6  2MSGCON(022 JOV1099 *LIBL/SYMSGF)
     A            WSBELA         1A  B  6 25DSPATR(UL)
     A                                      VALUES(' ' 'S' 'M')
     A                                  6 27'(S/M/Blank)'
     A                                  6 40MSGCON(022 JOV1132 *LIBL/SYMSGF)
     A            WSFBFK         2A  B  6 63DSPATR(UL)
     A                                  6 66'/'
     A            WSTPFK         2A  B  6 68DSPATR(UL)
     A                                  7  2MSGCON(022 JOV1114 *LIBL/SYMSGF)
     A            WSFBSE         1A  B  7 25DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N' '1')
     A                                  7 27'(J/N/Blank)'
     A                                  7 40MSGCON(022 JOV1115 *LIBL/SYMSGF)
     A            WSTPSE         1A  B  7 63DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N' '1')
     A                                  7 65'(J/N/Blank)'
     A                                  8  2MSGCON(022 SHU1528 *LIBL/SYMSGF)
     A            WSWVLF         1A  B  8 25
     A                                  9  2MSGCON(018 DIV1226 *LIBL/SYMSGF)
     A            WSVLKG         5Y 0B  9 25DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  9 31MSGCON(005 DIV1227 *LIBL/SYMSGF)
     A                                  9 40MSGCON(018 DIV1228 *LIBL/SYMSGF)
     A            WSVKKG         5Y 0B  9 60DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  9 66MSGCON(005 DIV1227 *LIBL/SYMSGF)
     A                                 10  2MSGCON(018 DIV1229 *LIBL/SYMSGF)
     A            WSVAVR         1A  B 10 25DSPATR(UL)
     A                                 12  2MSGCON(070 DIV1230 *LIBL/SYMSGF)
     A                                 13  2MSGCON(070 DIV1231 *LIBL/SYMSGF)
     A                                 14  2'  1     2     3     4     5     6 -
     A                                          7     8     9     10    11    1-
     A                                      2 '
     A                                      COLOR(WHT)
     A            WSVK1          5Y 0B 15  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK2          5Y 0B 15  8DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK3          5Y 0B 15 14DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK4          5Y 0B 15 20DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK5          5Y 0B 15 26EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSVK6          5Y 0B 15 32DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK7          5Y 0B 15 38DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK8          5Y 0B 15 44DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK9          5Y 0B 15 50DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK10         5Y 0B 15 56DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVK11         5Y 0B 15 62DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 15 68'99999'
     A                                      DSPATR(UL)
     A            WSWF1          1A  B 16  4DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF2          1A  B 16 10DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF3          1A  B 16 16DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF4          1A  B 16 22DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF5          1A  B 16 28DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF6          1A  B 16 34DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF7          1A  B 16 40DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF8          1A  B 16 46DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF9          1A  B 16 52DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF10         1A  B 16 58DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF11         1A  B 16 64DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSWF12         1A  B 16 70DSPATR(UL)
     A                                      VALUES(' ' 'F')
     A            WSVOMR         3Y 0B 17  2DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 17  9MSGCON(055 DIV1232 *LIBL/SYMSGF)
     A            WSWMM          1Y 0B 18  2DSPATR(UL)
     A                                      VALUES(0 1 2)
     A                                      EDTCDE(4)
     A                                 18  9MSGCON(024 DIV1233 *LIBL/SYMSGF)
     A                                 18 35MSGCON(045 DIV1235 *LIBL/SYMSGF)
     A                                 19  2MSGCON(017 SHU0452 *LIBL/SYMSGF)
     A            WSKOMB         1A  B 19 25VALUES('J' 'N' ' ')
     A                                 19 27MSGCON(005 SHU0453 *LIBL/SYMSGF)
     A                                 19 35MSGCON(045 DIV1234 *LIBL/SYMSGF)
     A                                 20  2MSGCON(018 DIV1236 *LIBL/SYMSGF)
     A            WSWBBS         1A  B 20 25DSPATR(UL)
     A                                      VALUES(' ' 'A' 'B' 'C' 'D' 'E' 'N' -
     A                                      'F')
     A                                 20 30MSGCON(050 DIV1237 *LIBL/SYMSGF)
     A                                 21  2MSGCON(018 DIV1238 *LIBL/SYMSGF)
     A            WSWAWB         1A  B 21 25DSPATR(UL)
     A                                      VALUES(' ' 'S' 'J' 'N' 'K')
     A                                 21 30MSGCON(045 YBC0103 *LIBL/SYMSGF)
     A                                 22  2MSGCON(018 DIV1240 *LIBL/SYMSGF)
     A            WSWHOD         1A  B 22 25DSPATR(UL)
     A                                      VALUES(' ' 'J' 'T' 'B')
     A                                 22 30MSGCON(010 DIV1241 *LIBL/SYMSGF)
     A            WSWLAS         1A  B 22 43DSPATR(UL)
     A                                 22 45MSGCON(010 SHU0296 *LIBL/SYMSGF)
     A            WSUTPR         4Y 2B 22 56EDTCDE(J)
     A                                 22 64MSGCON(010 SHU0297 *LIBL/SYMSGF)
     A            WSUTMI         4Y 0B 22 75EDTCDE(4)
     A                                 23  2MSGCON(018 DIV1242 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 25MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  8 40MSGCON(022 JOV1672 *LIBL/SYMSGF)
     A            WSWSOT         2A  B  8 63DSPATR(UL)
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20110513  094416  SVEIN       REL-V7R1M0  5770-WDS
     A            KOPFOV1        9A  H
     A            KOPDRAW        7A  H
     A            KOPOUTB       11A  H
     A            KOPCOPI        6A  H
     A            KOPLPI         2S 0H
     A            W2NR           1A  I  6  2VALUES(' ' '2' '9')
     A            KOPLNR         3Y 0O  6  5EDTCDE(4)
     A                                      DSPATR(HI)
     A            KOPTY         10A  O  6 32
     A            UTPL           3Y 0O  6 43EDTCDE(4)
     A            UTPK           3Y 0O  6 47EDTCDE(4)
     A            KOPNVN        10A  O  6 51
     A            KOPHEA         1A  O  6 62
     A            KOPLAS    R        O  6 64REFFLD(KOPR/KOPLAS *LIBL/KODTP)
     A            KOPLP2         1Y 0O  6 66EDTCDE(4)
     A            KOPFM          5A  O  6 68
     A            KOPDR3         3A  O  6 74
     A            KOPCO2         2A  O  6 78
     A            UTTX22        22A  O  6  9
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20100615  110529  SVEIN       REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYFA280200 SYFA28PNL)
     A                                      HLPARA(01 002 23 079)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA28'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(035 DIV1220 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 56DATE
     A                                      EDTCDE(Y)
     A                                  1 66TIME
     A                                  2  2MSGCON(015 DIV1221 *LIBL/SYMSGF)
     A                                  2 19MSGCON(008 DIV1243 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KOAAVD         4Y 0O  2 33COLOR(WHT)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 39COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(015 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 SHU1251 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  4 21MSGCON(030 JOV0673 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF05(05 'Frisk')
     A                                      CF03(03 'Avslutt')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'Forrige')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VARELI
     A*%%TS  SD  20110513  094105  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØKE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYFA280210 SYFA28PNL)
     A                                      HLPARA(*RCD)
N11  A*                                     HLPARA(21 045 21 079)
     A            MSGNR          7A  P
     A                                  1  2MSGCON(078 YBC0028 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KOPLNR    R    3Y00O  2  6DSPATR(UL)
     A                                      EDTCDE(4)
     A            UTPTXT        30A  O  2 14DSPATR(UL)
     A            W2PTY         10A  B  2 45DSPATR(UL)
     A            W2PNVN        10A  B  2 57DSPATR(UL)
     A N90N70                               DSPATR(PC)
     A            W2PHEA         1A  B  2 68DSPATR(UL)
     A            W2PLAS         1A  B  2 70
     A  70                                  DSPATR(HI)
     A  70                                  DSPATR(PC)
     A            W2LPI          1Y 0B  2 72EDTCDE(4)
     A            W2PFM          5A  B  2 74DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(014 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 37MSGCON(009 SHU1249 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2DRAW         3A  B  3 47
     A            W2DRAU         3A  B  3 51
     A            W2COPY         2A  B  3 63
     A                                  3 66'Ovl:'
     A                                      COLOR(WHT)
     A            W2FOV1         9A  B  3 71
     A N30                              4  2MSGCON(024 DIV0000 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        77A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 55'kopier:'
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(12 DIV1216 SYMSGF)
     A            WSPLNR         3Y 0I  2 15DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
