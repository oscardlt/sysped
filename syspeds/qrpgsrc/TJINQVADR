      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQVADR MODULE(*LIBL/TJINQVADR *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 innføre AKT-kod    .....
********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FVADR      IF   E           K DISK    USROPN
     FVADR1     IF   E           K DISK    USROPN
     F                                     RENAME(VADRR:VADRR1)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D WSVARNV         S             20
     D WKUNDNR         S              8
     D WKUNDNRN        S              8S 0
     D WVADRNR         S              5
     D WVADRNRN        S              5S 0
     D WVADRNA         S             10    varying
     D WVADRNA2        S             10
     D USER            S             10
     D MaxV            S              5
     D MaxVis          S              5  0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D Error           S            150
     D JSONstring      s          32000

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬wkundnr¬ : ¬'+%trim(WKUNDNR)+'¬, '
        +'¬wvadrnr¬ : ¬'+%trim(WVADRNR)+'¬, '
        +'¬wvadrna¬ : ¬'+%trim(WVADRNA)+'¬, '
        +'¬maxv¬ : ¬'+%trim(MAXV)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"inqdeladdr" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C     ERROR         IFEQ      *blanks
     C                   EXSR      LINES
     C                   ENDIF
      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     PARSE         BEGSR
     C                   EVAL      USER    = zhbgetvar('USER')
     C                   EVAL      WKUNDNR = zhbgetvar('WKUNDNR')
     C                   EVAL      WKUNDNRN= c2n2(WKUNDNR)
     C                   EVAL      WVADRNR = zhbgetvar('WVADRNR')
     C                   EVAL      WVADRNRN= c2n2(WVADRNR)
     C                   EVAL      WVADRNA = zhbgetvar('WVADRNA')
     C     LO:UPC        XLATE     WVADRNA       WVADRNA
     C                   EVAL      MaxV    = zhbgetvar('MaxV')
     C                   EVAL      MaxVis  = c2n2(MaxV)
     C     MaxVis        IFEQ      *ZEROS
     C                   Z-ADD     50            MaxVis
     C                   ENDIF
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     VADR
     C  N50              CLOSE     VADR1
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         BEGSR
     C                   EVAL      WVADRNA2   = WVADRNA
     C                   MOVE      *ZEROS        AntVist           5 0
     C     WVADRNRN      IFEQ      *ZEROS
     C     WVADRNA       ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   ENDIF

     C     WVADRNRN      IFNE      *ZEROS
     C     VARKEY        SETLL     VADR
     C     VARKEY        READE     VADR                                   33
     C                   ELSE
     C     NVNKEY        SETLL     VADR1
     C     NVNKEY        READE     VADR1                                  33
     C     WVADRNA       SCAN      VADRNA                                 34
     C     *IN34         CABEQ     *OFF          UtLes
     C                   ENDIF

     C     *IN33         DOWEQ     *OFF

     C                   ADD       1             AntVist           5 0
      /free
       if AntVist=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
N01       +'¬aktkod¬ : ¬'+%trim(AKTKOD)+'¬, '
          +'¬vadrnr¬ : ¬'+%trim(%editc(VADRNR:'3'))+'¬, '
          +'¬vadrna¬ : ¬'+%trim(VADRNA)+'¬, '
          +'¬vadrn1¬ : ¬'+%trim(VADRN1)+'¬, '
          +'¬vadrn2¬ : ¬'+%trim(VADRN2)+'¬, '
          +'¬vadrn3¬ : ¬'+%trim(VADRN3)+'¬, '
          +'¬valand¬ : ¬'+%trim(VALAND)+'¬, '
          +'¬vakure¬ : ¬'+%trim(VAKURE)+'¬, '
          +'¬vatlf¬ : ¬'+%trim(VATLF)+'¬, '
          +'¬vafax¬ : ¬'+%trim(VAFAX)+'¬, '
          +'¬vamail¬ : ¬'+%trim(VAMAIL)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C     AntVist       CABGE     MaxVis        UtLes

     C     WVADRNRN      IFNE      *ZEROS
     C     VARKEY        READE     VADR                                   33
     C                   ELSE
     C     NVNKEY        READE     VADR1                                  33
     C     WVADRNA       SCAN      VADRNA                                 34
     C     *IN34         CABEQ     *OFF          UTLES
     C                   ENDIF
     C                   ENDDO

     C     UTLES         TAG

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på USER - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      VADR
     C  N50              OPEN      VADR1
      * Key for VADR
     C     VARKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WKUNDNRN
     C                   KFLD                    WVADRNRN
      * Key for VADR1
     C     NVNKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WKUNDNRN

     C                   ENDSR
