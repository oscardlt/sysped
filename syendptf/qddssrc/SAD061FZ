     A*%%TS  SD  20230427  132036  SVEIN       REL-V7R4M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 30 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            KS3TRT         2S 0O  5  5
     A            WS3FTX        20A  O  5  8
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20230427  132036  SVEIN       REL-V7R4M0  5770-WDS
     A  *DS3                                SFLSIZ(0006)
     A  *DS4                                SFLSIZ(0006)
     A  *DS3                                SFLPAG(0005)
     A  *DS4                                SFLPAG(0005)
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD061Z'
     A                                      COLOR(BLU)
     A                                  1 10'Søking av tr.typer'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  3  2'Op TT Beskrivelse'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A*
     A            W2KOD          2Y 0I  4  5DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R NYWRK
     A*%%TS  SD  20230427  132036  SVEIN       REL-V7R4M0  5770-WDS
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD061I'
     A                                      COLOR(BLU)
     A                                  1 10'Søking av tr.typer'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  3  2'Op TT Beskrivelse'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KOD          2Y 0I  4  5DSPATR(UL)
     A                                 10  2MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
