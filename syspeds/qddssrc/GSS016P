********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10027 * 01 YBC PTF10 VIS ORDRETYPE
********************************************************************
     A          R GSS016PA
     A                                  2  2'NEW REGISTERED ORDERS'
     A                                  2 40'DATE'
     A                                  2 45DATE EDTCDE(Y)
     A                                  2 54'TIME'
     A            XTID4          4  0   2 59EDTWRD('  .  ')
     A                                  2 66'PAGE'
     A                                  2 71PAGNBR
     A                                  4  2'Clients      :'
     A            PCPNO          8  0   4 18EDTCDE(Z)
     A            PCNM          30A     4 27
     A                                  6  2'Orderno Supplier/Goods description-
     A                                       LC/Date Col/W/V Pos.  Clients P.O.-
     A                                      N.      '
     A                                  7  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  8  2' '
      *
     A          R GSS016PB                  SPACEB(1)
     A            ORONO          7  0      2EDTCDE(Z)
     A            XSNM27        27A       10
     A            ORLC           5A       38
     A            ORCLI          5  0     47EDTCDE(Z)
     A            ORPOS1         5A       53
     A            XCONO1        20A       59SPACEA(1)
N01  A            ORTYPE         1A        5
     A            XDSC27        27A       10
     A            XDTL           6  0     38EDTCDE(Z)
     A            ORWT           7  1     44EDTCDE(3)
     A            ORPOS2         5A       53
     A            XCONO2        20A       59SPACEA(1)
     A            ORBCD          5A        2
     A            XNAB27        27A       10
     A            XDTWL          6  0     38EDTCDE(Z)
     A            ORVOL          5  2     46EDTCDE(4)
     A            ORPOS3         5A       53
     A            XCONO3        20A       59SPACEA(1)
     A  35        XCONO4        20A       59SPACEA(1)
     A  36        XCONO5        20A       59SPACEA(1)
     A  37        XCONO6        20A       59SPACEA(1)
      *
     A          R GSS016PC
     A            XCONOX        20A       59SPACEA(1)
