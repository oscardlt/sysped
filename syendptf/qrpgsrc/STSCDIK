      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSCDIK MODULE(*LIBL/STSCDIK *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P090xx* 01 JOV PTF9 parameter SEMI (kall fra TKTerm)
P090xx* 01 JOV PTF9 parameter FRASYMNU=Y (= avslutt til ved å close vindu)
      * 03 jov      Def. MÅ gis på USER-NIVÅ (foreløpig kun stjernet i fall gjeninføring av FI-nivå)
      * 04 jov ptf10 koder for avvik på kollinivå dersom det er TKterm/SEMI=Y
      * 05 jov ptf10 FRA 01.19, ANGI TURAVSL,VALG(J/N/V SAMT DFT ANT TIMER I 48, 49-50)
      *%%%%%%%%%%%%%%%%%%%%%%%%
      * 06 JOV ptf12 SEMI=J=JSON-svar - fjernet versj.tester for enklere logikk / PTF3 helt bort
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
N01  D SEMI            s              1
N02  D FRASYMNU        s              1
N04  D VERSJ           s             10
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
N05  D TermText        s             50
N06  D JSONstring      s          32000
N06  D Error           s            200
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA1_5              1      5
     D  PAVRDA7_46             7     46
N05  D  PAVRDA48_48           48     48
N05  D  PAVRDA49_50           49     50
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
      ***
     C                   exsr      Init
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
      *
     C                   callp     wrtsection('top')
      *
N06   * VED JSON - Start opp JSON-streng
N06  c                   if        semi='J'
N06   /free
N06     JSONstring='{'
N06     +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
N06     +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
N06   /end-free
N06  c                   call      'JSONFIX'
N06  c                   parm                    JSONstring
N06  C                   CALLP     updHTMLvar2('JSONstring'
N06  C                                         :%addr(JSONstring)
N06  C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   endif
      *
     C                   EXSR      DSPFK
      * Koder for kolli-skader/avvik returneres kn derosm det er TKterm/semikolon-svar
      *
N06   * VED JSON - Avslutt JSON-streng
N06  c                   if        semi='J'
N06  C                   eval      JSONstring='}'
N06  C                   CALLP     updHTMLvar2('JSONstring'
N06  C                                         :%addr(JSONstring)
N06  C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   endif
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     BRPARF
     C     SLUTT         TAG
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER   = zhbgetvar('USER')
     C                   eval      WSUSER   = uppify(WSUSER)
N01  C                   eval      SEMI     = zhbgetvar('SEMI')
N02  C                   eval      FRASYMNU = zhbgetvar('FRASYMNU')
N04  C                   eval      VERSJ = zhbgetvar('V')
      *
     C                   endsr
      *
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('User':WSUSER)
N02  C                   callp     updhtmlvar('FRASYMNU':FRASYMNU)
      *
     C                   endsr
      *
      *=====================================================================*
      *  Display end Customer Adress to select from
      *=====================================================================*
     C     DSPFK         begsr
      * Top inneholder WSKNS eller WSKNK, derfor 2 ulike format...
n01  c     SEMI          ifne      'Y'
n06  c     SEMI          andne     'J'
     C                   callp     wrtsection('FKtop')
n01  c                   end
      *
N06  c                   if        semi='J'
N06  c                   eval      JSONstring =' "termListe" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   move      'J'           Json1             1
N06  c                   endif
      *
     C     BrParKey      SETLL     BRPARF
     C     BrParKey      READE     BRPARF                                 40
     c     *in40         doweq     '0'
      * Manipulering av time/avsl-param flytte til topp.. (gjelder både semi = Y og J )
N06  C     PAVRDA48_48   ifne      'J'
N06  c     PAVRDA48_48   andne     'N'
N06  c     PAVRDA48_48   andne     'V'
N06  c                   move      'V'           PAVRDA48_48
N06  c                   endif
N06  C     PAVRDA49_50   iflt      '00'
N06  c     PAVRDA49_50   orgt      '99'
N06  c                   move      '01'          PAVRDA49_50
N06  c                   endif
      *
N06  c                   if        semi='J'
N06  c                   if        Json1 <> 'J'
N06  c                   eval      JSONstring =', '
N06  C                   CALLP     updHTMLvar2('JSONstring'
N06  C                                         :%addr(JSONstring)
N06  C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   endif
N06  c                   move      'N'           Json1
N06  c                   eval      JSONstring ='{¬termKode¬ : ¬'
N06  c                                        +%trim(PAVRDA1_5)+'¬,'
N06  c                                        +'¬termTxt¬ : ¬'
N06  c                                        +%trim(PAVRDA7_46)+'¬, '
N06  c                                        +'¬timer¬ : ¬'
N06  c                                        +%trim(PAVRDA49_50)+'¬, '
N06  c                                        +'¬turAvslKode¬ : ¬'
N06  c                                        +%trim(PAVRDA48_48)+'¬}'
N06  c                   call      'JSONFIX'
N06  c                   parm                    JSONstring
N06  C                   CALLP     updHTMLvar2('JSONstring'
N06  C                                         :%addr(JSONstring)
N06  C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   else
     C                   callp     updhtmlvar('KFKOD':PAVRDA1_5)
N05  c                   eval      TermText=%trim(PAVRDA7_46)+';'
N05  c                             +PAVRDA49_50+';'+PAVRDA48_48
N05  C                   callp     updhtmlvar('KFTXT':TermText)
     C                   callp     wrtsection('FKrow')
N06  c                   endif
     C     BrParKey      READE     BRPARF                                 40
     C                   ENDDO
N06   *
N06   * JSON - avslutt liste
N06  c                   if        semi='J'
N06  c                   eval      JSONstring ='], '
N06  C                   CALLP     updHTMLvar2('JSONstring'
N06  C                                         :%addr(JSONstring)
N06  C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   endif
      *
      * Kolli-avvik ??
n04  c     SEMI          ifeq      'Y'
n04  c     SEMI          oreq      'J'
N04  C                   EXSR      DSPKA
n04  c                   endif
      *
N06  c     SEMI          ifne      'J'
N03  c     TermDefin     ifeq      'N'
N03  c     SEMI          andne     'Y'
n06   * info om at term-koder MÅ angis på brukernivå
N03  C                   callp     wrtsection('FKBotINF')
N03  c                   else
     C                   callp     wrtsection('FKBot')
N03  c                   end
N06  c                   end
      *
     C                   endsr
N04
N04   *=====================================================================*
N04   *  Kolliavvik    (kun på firmanivå)
N04   *=====================================================================*
N04  C     DSPKA         begsr
N04   * Kolli-avviksheader..
N06  c                   if        semi='J'
N06  c                   eval      JSONstring =' "avvik" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   move      'J'           Json1             1
N06  c                   else
N04  C                   callp     wrtsection('KAhead')
N06  c                   endif
N04   *
N04  C     BrParKeKA     SETLL     BRPARF
N04  C     BrParKeKA     READE     BRPARF                                 40
N04  c     *in40         doweq     '0'
N06  c                   if        semi='J'
N06  c                   if        Json1 <> 'J'
N06  c                   eval      JSONstring =', '
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   endif
N06  c                   move      'N'           Json1
N06  c                   eval      JSONstring ='{¬avvKod¬ : ¬'
N06  c                                        +%trim(PAVRDA1_5)+'¬,'
N06  c                                        +'¬avvTxt¬ : ¬'
N06  c                                        +%trim(PAVRDA7_46)+'¬}'
N06  c                   call      'JSONFIX'
N06  c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   else
N04  C                   callp     updhtmlvar('KFKOD':PAVRDA1_5)
N04  C                   callp     updhtmlvar('KFTXT':PAVRDA7_46)
N04  C                   callp     wrtsection('FKrow')
N06  c                   endif
N04  C     BrParKeKA     READE     BRPARF                                 40
N04  C                   ENDDO
N04   *
N06  c                   if        semi='J'
N06  c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
N06  C                   callp     wrtsection('JSONlin')
N06  c                   endif
N04  C                   endsr
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              30
      *
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
      * (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      BRPARF
      *
     C     BrParKey      KLIST
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVEL     'TERMK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C   33              MOVE      'N'           TermDefin         1
N06  c                   if        TermDefin='N'
N06  c                   eval      Error='Bruker mangler definisjon av '
N06  c                             +'terminalkoder (parameter "TERMK") '
N06  c                   endif
      *
N04   * KOLLIAVVIK
N04  C     BrParKeKA     KLIST
N04  C                   kfld                    XXBRID           10
N04  C                   kfld                    XXKUNR            8 0
N04  C                   kfld                    XXID              5
N04  C                   MOVE      '*KUNDFT*  '  XXBRID
N04  C                   MOVE      BIFIRM        XXBRID
N04  C                   MOVEL     'TKTKA'       XXID
N04
      *    TKTKA
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSCDIK'
n01  c     SEMI          ifeq      'Y'
N01  C                   eval      Mbr = 'STSCDIKT'
n01  c                   end
n01  c     SEMI          ifeq      'J'
N01  C                   eval      Mbr = 'JSON'
n01  c                   end
      *------------------
     C                   endsr
      *
