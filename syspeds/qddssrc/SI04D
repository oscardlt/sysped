     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/HEADF)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'Forrige')
      *
     A          R SI04DA
     A                                      OVERLAY
     A                                  1  2'SI04D A'
     A                                      COLOR(BLU)
     A                                  1 33MSGCON(025 SSI0810 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2MSGCON(035 SSI0820 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  4MSGCON(020 SSI0830 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(003 SSI0280 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  7MSGCON(004 SSI0290 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZALT1          1A  I  7  3VALUES(' ' '1')
     A                                  7  7MSGCON(060 SSI0840 *LIBL/SYMSGF)
     A            ZALT2          1A  I  8  3VALUES(' ' '1')
     A                                  8  7MSGCON(060 SSI0850 *LIBL/SYMSGF)
     A            ZALT3          1A  I  9  3VALUES(' ' '1')
     A                                  9  7MSGCON(060 SSI0860 *LIBL/SYMSGF)
     A            ZALT4          1A  I 10  3VALUES(' ' '1')
     A                                 10  7MSGCON(060 SSI0870 *LIBL/SYMSGF)
     A            ZALT5          1A  I 11  3VALUES(' ' '1')
     A                                 11  7MSGCON(060 SSI0880 *LIBL/SYMSGF)
     A            ZALT6          1A  I 12  3VALUES(' ' '1')
     A                                 12  7MSGCON(60 SSI0930 SYMSGF)
     A                                 21 71MSGCON(010 SSI0060 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(030 SSI0600 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SI04DB
     A                                      OVERLAY
     A                                  1  2'SI04D B'
     A                                      COLOR(BLU)
     A                                  1 33MSGCON(025 SSI0810 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2MSGCON(035 SSI0640 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(037 SSI0890 *LIBL/SYMSGF)
     A            ZEFNK          7Y 0B  5 42
     A                                  5 51MSGCON(010 SSI0630 *LIBL/SYMSGF)
     A                                 21 71MSGCON(010 SSI0060 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(030 SSI0600 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SI04DC
     A                                      OVERLAY
     A                                  1  2'SI04D C'
     A                                      COLOR(BLU)
     A                                  1 33'Utførte oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Godsnummer/MAWB-export/HAWB-import-
     A                                      . :'
     A            ZEGN      R        B  5 42REFFLD(HEGN)
     A                                  5 56'Tegn'
     A                                 21 76'Slutt'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt   F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SI04DD
     A                                      OVERLAY
     A                                  1  2'SI04D D'
     A                                      COLOR(BLU)
     A                                  1 33'Utførte oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Agentreferanse/Samlastnummer/MAWB--
     A                                      import :'
     A*           ZERFA     R        B  5 47REFFLD(HERFA)
     A            ZERFA         15   B  5 47
     A                                  5 65'Tegn'
     A                                 21 76'Slutt'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt   F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SI04DE
     A                                      OVERLAY
     A                                  1  2'SI04D E'
     A                                      COLOR(BLU)
     A                                  1 33'Utførte oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Fra og med oppdragsdato . . . . . -
     A                                      . :'
     A            ZDTF           6Y 0B  5 42EDTCDE(4)
     A  31                                  COLOR(RED)
     A                                  5 51'ÅMD'
     A                                  6  4'Til og med oppdragsdato . . . . . -
     A                                      :'
     A            ZDTT           6Y 0B  6 42EDTCDE(4)
     A  32                                  DSPATR(PC)
     A  32                                  COLOR(RED)
     A                                  6 51'ÅMD'
     A                                 21 76'Slutt'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt   F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SI04DF
     A                                      CF04(04 'Søk')
     A                                      CSRLOC(ZLIN ZPOS)
     A                                      OVERLAY
     A            ZLIN           3S 0H
     A            ZPOS           3S 0H
     A                                  1  2'SI04D F'
     A                                      COLOR(BLU)
     A                                  1 33'Utførte oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Fra sted  . . . . . . . . . . . . -
     A                                      . :'
     A            ZESDF     R        B  5 42REFFLD(HESDF)
     A  31                                  COLOR(RED)
     A                                  5 50'Kode'
     A                                  6  4'Til sted  . . . . . . . . . . . . -
     A                                      :'
     A            ZESDT     R        B  6 42REFFLD(HESDT)
     A  32                                  DSPATR(PC)
     A  32                                  COLOR(RED)
     A                                  6 50'Kode'
     A                                  8  2'Fra og med oppdragsdato . . . . . -
     A                                      . :'
     A            ZDTF           6Y 0B  8 42EDTCDE(4)
     A  33                                  DSPATR(PC)
     A  33                                  COLOR(RED)
     A                                  8 51'ÅMD'
     A                                  9  4'Til og med oppdragsdato . . . . . -
     A                                      :'
     A            ZDTT           6Y 0B  9 42EDTCDE(4)
     A  34                                  DSPATR(PC)
     A  34                                  COLOR(RED)
     A                                  9 51'ÅMD'
     A                                 21 76'Slutt'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt   F4=Søk   F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SI04DK
     A                                      OVERLAY
     A                                  1  2'SI04D K'
     A                                      COLOR(BLU)
     A                                  1 33'Utførte oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Fri søkevei . . . . . . . . . . . -
     A                                      . :'
     A            ZFRI1          3A  B  5 42
     A            ZFRI2         35A  B  5 47
     A                                 21 76'Slutt'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt   F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SI04DG                    SFL
     A            HEAVD     R        H
     A            HEOPD     R        H
     A            ZOP            1A  I  7  3VALUES(' ' '5')
SV5  A*           HEDTO          6S 0O  7  7
NV5  A            WSDTOP         6S 0O  7  7
     A            HEFNS          6Y 0O  7 14EDTCDE(4)
     A            HESDF     R        O  7 21
     A            HENAS     R        O  7 27
     A            HENT      R        O  7 58EDTCDE(4)
     A            HEGN      R        O  7 66
     A            HEFNK          6Y 0O  8 14EDTCDE(4)
     A            HESDT     R        O  8 21
     A            HENAK     R        O  8 27
     A            HEVKT     R        O  8 60EDTCDE(4)
     A*           HERFA     R        O  8 66
     A            HERFA15       15   O  8 66
      *
     A          R SI04DH                    SFLCTL(SI04DG)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A  91N93N94                            CA07(25)
     A  91N93N94                            CA08(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SI04D H'
     A                                      COLOR(BLU)
     A                                  1 33'Utførte oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2'Velg alternativ og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  4  4'5=Vise oppdrag'
     A                                      COLOR(BLU)
     A            ZRECNO         4  0O  4 69SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  5 21'Fra'
     A                                      COLOR(WHT)
     A                                  5 27'Avsender'
     A                                      COLOR(WHT)
     A                                  5 59'Antall'
     A                                      COLOR(WHT)
     A                                  5 66'Godsnummer'
     A                                      COLOR(WHT)
     A                                  6  2'Alt'
     A                                      COLOR(WHT)
     A                                  6  7'Dato'
     A                                      COLOR(WHT)
     A                                  6 14'Fak.nr'
     A                                      COLOR(WHT)
     A                                  6 21'Til'
     A                                      COLOR(WHT)
     A                                  6 27'Mottager'
     A                                      COLOR(WHT)
     A                                  6 61'Vekt'
     A                                      COLOR(WHT)
     A                                  6 66'Agentref.'
     A                                      COLOR(WHT)
      *
     A          R SI04DI
     A                                      OVERLAY
     A                                 23  2'F3=Avslutt   F5=Frisk opp   F12=Fo-
     A                                      rrige'
     A                                      COLOR(BLU)
      *
     A          R SI04DJ
     A                                      OVERLAY
     A                                  1  2'SI04D J'
     A                                      COLOR(BLU)
     A                                  1 33'Utførte oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Tjenestetype . . . . . . . . . . .-
     A                                       . :'
     A            ZEURTX        13   O  5 43
     A                                  6  4'Vår referanse  . . . . . . . . . .-
     A                                       :'
     A            HEAVD     R        O  6 43EDTCDE(4)
     A                                  6 48'/'
     A            HEOPD     R        O  6 50EDTCDE(4)
     A                                  6 58'/'
     A            HESG      R        O  6 60
     A                                  7  4'Oppdragsdato . . . . . . . . . . .-
     A                                       :'
SV5  A*           HEDTÅ     R        O  7 43
SV5  A*           HEDTM     R        O  7 46
SV5  A*           HEDTD     R        O  7 49
NV5  A            WSDTOP         6Y 0O  7 43EDTCDE(Y)
     A                                  8  4'Godsnummer/MAWB-export/HAWB-import-
     A                                       :'
     A            HEGN      R        O  8 43
     A            HEGNN     R        O  8 56
     A                                  9  6'Posisjon . . . . . . . . . . . . :'
     A            HEPOS1    R        O  9 43
     A                                  9 51'/'
     A            HEPOS2    R        O  9 53EDTCDE(4)
     A                                 10  4'A.ref/Saml.nr-export/MAWB-import .-
     A                                       :'
     A*           HERFA     R        O 10 43
     A            HERFA15       15   O 10 43
     A                                 11  4'Fra sted . . . . . . . . . . . . .-
     A                                       :'
     A            ZESDFT    R        O 11 43REFFLD(ST2NVN *LIBL/STED2)
     A                                 12  6'Til sted . . . . . . . . . . . . :'
     A            ZESDTT    R        O 12 43REFFLD(ST2NVN *LIBL/STED2)
     A                                 13  4'Selger . . . . . . . . . . . . . .-
     A                                       :'
     A            HENAS     R        O 13 43
     A                                 14  6'Kjøper . . . . . . . . . . . . . :'
     A            HENAK     R        O 14 43
     A                                 15  4'Frankatur  . . . . . . . . . . . .-
     A                                       :'
     A            KFRNTX    R        O 15 43REFFLD(KFRNTX *LIBL/KODTFR)
     A                                 16  4'Antall . . . . . . . . . . . . . .-
     A                                       :'
     A            HENT      R        O 16 43EDTCDE(4)
     A                                 17  6'Enhet/Vareslag . . . . . . . . . :'
     A            HEVS1     R        O 17 43
     A                                 18  4'Bruttovekt/Lastemeter/Volym. . . .-
     A                                       :'
     A            HEVKT     R        O 18 43EDTCDE(4)
     A                                 18 49'/'
     A            HELM      R        O 18 51EDTCDE(K)
     A                                 18 56'/'
     A            HEM3      R        O 18 62EDTCDE(K)
     A                                 19  4'Fakturanummer selger . . . . . . .-
     A                                       :'
     A            HEFNS          6Y 0O 19 43EDTCDE(4)
     A                                 20  6'Fakturanummer kjøper . . . . . . :'
     A            HEFNK          6Y 0O 20 43EDTCDE(4)
     A                                 21 76'Slutt'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt   F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SI04DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
