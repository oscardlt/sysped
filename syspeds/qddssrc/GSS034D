     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 8 75 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R GSS034DA
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'SØKING')
     A                                      CF10(10 'send fax')
     A                                      CA12(12 'AVSLUTT')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS034D'
     A                                      COLOR(BLU)
     A                                  1 19'Send fax/email'
     A                                      COLOR(WHT)
     A                                  2  2'Clients No.'
     A            WSCNO2         8Y 0B  2 16EDTCDE(4)
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A            WSCNAM        30A  B  2 25
     A                                  3  2'Fax No.'
     A            WSFAXN        15A  B  3 16DSPATR(UL)
     A                                  3 32'Countrycode:'
     A            WSCOUN         2A  B  3 45
     A            WSRET          6A  O  3 48
     A                                  4  2'E:'
     A            EMAIL         70A  B  4  5CHECK(LC)
     A                                  5  2'Coverpage ?'
     A            WSCVRP         1A  B  5 16
     A                                  6  2'Send at time:'
     A            WSTIME         6Y 0B  6 16DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A                                  7  2'Send at date:'
     A            WSDATE         6Y 0B  7 16DSPATR(UL)
     A                                      EDTCDE(4)
     A  97                                  DSPATR(RI)
     A N30                              8  2'F3/F12=END   F4=QUERY  F10=SEND FA-
     A                                      X/EMAIL'
     A                                      COLOR(BLU)
     A  30        MSGTXT        52A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS034DB
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'AVSLUTT')
     A                                      CF10(10 'send fax')
     A                                      CA12(12 'AVSLUTT')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS034DB'
     A                                      COLOR(BLU)
     A                                  1 19'Send e-mail'
     A                                      COLOR(WHT)
     A                                  4  2'E:'
     A            EMAIL         70A  B  4  5CHECK(LC)
     A N30                              8  2'F3/F12=END   F10=SEND E-MAIL'
     A                                      COLOR(BLU)
     A  30        MSGTXT        52A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
