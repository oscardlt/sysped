     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' ) debug(*yes)
     FQEDIRE    O  A E             disk    RENAME(QEDIRE:QEDIRE2)
      *__________
      * Variabler
      *""""""""""
     D FILE_i          s               *
     D ErrTxt          s            128a
     D String          s             81a
     D rc              s             10i 0
     D CmdLine         S            512
     D CmdLen          S             15  5
      *___________
      * Konstanter
      *"""""""""""
     D*FilNam          c                   '/SYSPEDF/EDISE/CUSDEC'
      *_________________
      * Feildefinisjoner
      *"""""""""""""""""
     D errno           Pr            10i 0

     D strerror        Pr           128a   Varying
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfgets            Pr              *   ExtProc( '_C_IFS_fgets' )
     D                                 *   Value
     D                               10i 0 Value
     D                                 *   Value

     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
      *______
      * Input
      *""""""
     C     *ENTRY        PLIST
     C                   parm                    FilNam          256
     C     Qcmdexc       PLIST
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen
      *________________________________________
      * Åpne fil, med tegnkonvertering for jobb
      *""""""""""""""""""""""""""""""""""""""""
     C                   EVAL      FILE_i = open( %TrimR( FilNam ) : 'r' )

     C                   IF        FILE_i = *Null
     C                   EVAL      ErrTxt = %Char( Errno ) + ': ' + Strerror
     C                   ELSE

     C                   DOW       fgets( %Addr( String ) : %Size( String )
     C                             : FILE_i ) <> *Null
     C*                  DUMP
     C                   EVAL      SRCDTA=*BLANKS
     C     X'00'         SCAN      string        pos               2 0
     C                   SUB       1             pos
     C                   if        pos < 1
     C                   eval      pos=1
     C                   endif
     C                   EVAL      SRCDTA = %SUBST(string:1:pos)
     C     SRCDTA        IFNE      X'25'
     C                   WRITE     QEDIRE2
     C                   endif
     C                   ENDDO

     C                   EVAL      rc = close( FILE_i )
     C                   ENDIF
      *___________
      * Kopier fil
      *"""""""""""
     C                   EVAL      CmdLine = 'CPYF FROMFILE(QEDIRE)'
     C                             + ' TOFILE(Q2'
     C                             + ') MBROPT(*REPLACE) FMTOPT(*CVTSRC)'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'     Qcmdexc                98

     C                   EVAL      *INLR = *On
     C                   RETURN

      *_________________
      * Hente feilnummer
      *"""""""""""""""""
     P Errno           B
     D                 PI            10i 0
     D sys_errno       PR              *    ExtProc( '__errno' )

     D Error           S             10i 0  Based( pError ) NoOpt

     C                   EVAL      pError = sys_errno
     C                   RETURN    Error

     P Errno           E
      *________________
      * Hente feiltekst
      *""""""""""""""""
     P Strerror        B
     D                 PI           128a   Varying
     D sys_strerror    PR              *   ExtProc( 'strerror' )
     D                               10i 0 Value

     C                   RETURN    %Str( sys_strerror( Errno ))

     P Strerror        E
