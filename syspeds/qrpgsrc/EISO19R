********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10035 * 01 YBC PTF10 NYE PARAMETRE FOR Å SENDE E-POST
10222 * 02 YBC PTF10 EMNE SKAL AVHENGE AV POSTTYPE
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL SKRIVE UT ISO-SAK
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01 02 06-09 11-26
      *** LEDIGE INDIKATORER 44-50 53-89 93 94
      **************************************************************
      *
     FISOINFF   UF   E           K DISK
     FISOFL8    IF   E           K DISK
N01  FISOLOGF   O  A E             DISK
N02  FISOKDT    IF   E           K DISK
N02  FARKIVP    O  A E           K DISK
N02  FBRPARF    IF   E           K DISK
N02  FFIRM      IF   E           K DISK
     FEISO19P   O    E             PRINTER OFLIND(*IN27)
      *
     C                   EXSR      INIT
      *
     C                   MOVE      USAKN         IFSAKN
     C     IFSAKN        CHAIN     ISOFL8                             33
     C     IFSAKN        CHAIN     ISOINFF                            33
     C                   IF        *IN33 = *OFF
     C                   MOVE      'P'           IFST
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           IFDTS
     C                   MOVE      WTM           IFTMS
     C                   UPDATE    ISOINFFR
N02  C     ISFPFO        CHAIN     ISOKDT                             33
      *
     C                   WRITE     EISO19PA
N01   *
N01  C                   IF        USND = 'J'
S02  C*                  EVAL      UEMNE = 'ISO-sak, saksnr('
S02  C*                            + %TRIM(%EDITC(ISSAKN:'Z')) + ')'
N02  C                   EVAL      UEMNE = 'Saksnr(' + %TRIM(%EDITC(ISSAKN:'Z'))
N02  C                             + ') ' + %TRIM(TTEXT)
N01  C                   CALL      'EPOS04E'
N01  C                   PARM                    ISUSRS
N01  C                   PARM                    USERID            8
N01  C                   PARM                    ADDRESS           8
N01  C                   PARM                    UEPADR           50
N01  C                   EVAL      USENDER = ISUSRS
N03  C                   IF        UEPADR = *BLANKS
N03  C                   EVAL      USNDADR = 'BING@SYSTEMA.NO'
N03  C                   ELSE
N01  C                   EVAL      USNDADR = UEPADR
N01  C                   END
N01  C                   EVAL      UMOTADR = IFMOTT
N01  C                   EVAL      UMOTTAKER = IFMOTT
N02  C                   EVAL      UCC1ADR = IFCC01
N02  C                   EVAL      UCC2ADR = IFCC02
N02  C                   EVAL      UCC3ADR = IFCC03
N01  C                   MOVE      USAKN         ILSAKN
N01  C                   CALL      'SYGE15C'
N01  C                   PARM                    WDT               8
N01  C                   PARM                    WTM               6
N01  C                   MOVE      WDT           ILDTL
N01  C                   MOVE      WTM           ILTML
N01  C                   MOVE      ISUSRS        ILUSR
N01  C                   MOVE      'W'           ILMOD
N01  C                   MOVE      ISFPFO        ILFPFO2
N01  C                   MOVE      ISFAVA        ILFAVA2
N01  C                   MOVE      ISFANS        ILFANS2
N01  C                   MOVE      ISRTYP        ILRTYP2
N01  C                   MOVE      ISST          ILST2
N01  C                   MOVE      ISSDT         ILSDT2
N01  C                   MOVE      ISUSRS        ILUSR2
N01  C                   EVAL      ILTXT = 'E-POST ER SENDT TIL TRANSPORTØR.'
N01  C                   WRITE     ISOLOGFR
N02   *
N02  C                   EXSR      ARKEP
N02   *
N01  C                   END
N01   *
     C                   END
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
N02   ******************************************************************************
N02  C     ARKEP         BEGSR
N02   ******************************************************************************
N02  C     *LOVAL        SETLL     FIRM
N02  C                   READ      FIRM                                   33
N02  C                   MOVE      'OLBAN'       PAID
N02  C     BRPARFK       Chain     BRPARF                             33
N02  C   33              EVAL      PABRID = '*KUNDFT*' + %TRIM(FIFIRM)
N02  C   33BRPARFK       Chain     BRPARF                             33
N02  C   33              EVAL      PAVRDA = '/pdf/tmp/'
N02   *
N02  C                   CALL      'ARCRAN'
N02  C                   PARM                    NEWRAN           10
N02  C                   EVAL      USTMF = %TRIM(PAVRDA) + 'ZO' + %TRIM(USAKN)
N02  C                             + %TRIM(NEWRAN) + '.pdf'
N02   * LAG ARKIV-POST
N02  C                   EVAL      ARSTAT = 'A'
N02  C                   EVAL      ARTYPE = 'ZO'
N02  C                   EVAL      ARFIRM = FIFIRM
N02  C                   EVAL      ARUNDE = 'ISO' + %TRIM(USAKN)
N02  C                   EVAL      ARUSER = ISUSRS
N02  C                   CALL      'SYGE15C'
N02  C                   PARM                    WDT               8
N02  C                   PARM                    WTM               6
N02  C                   MOVE      WDT           ARDATE
N02  C                   MOVE      WTM           ARTIME
N02  C                   EVAL      ARLINK = 'ZO' + %TRIM(USAKN)
N02  C                             + %TRIM(NEWRAN) + '.pdf'
N02  C                   EVAL      ARRFK  = 'E-post til transportør'
N02  C                   WRITE     ARKIVR
N02   *
N02  C                   ENDSR
N02   *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    USAKN             7
N01  C                   PARM                    USND              1
N01  C                   PARM                    UEMNE            35
N01  C                   PARM                    USENDER          30
N01  C                   PARM                    USNDADR          50
N01  C                   PARM                    UMOTTAKER        30
N01  C                   PARM                    UMOTADR          50
N02  C                   PARM                    UCC1ADR          50
N02  C                   PARM                    UCC2ADR          50
N02  C                   PARM                    UCC3ADR          50
N02  C                   PARM                    USTMF           120
N02   *
N02  C     BRPARFK       klist
N02  C                   kfld                    ISUSRS
N02  C                   kfld                    PAKUNR
N02  C                   kfld                    PAID
      *
     C                   ENDSR
      *
