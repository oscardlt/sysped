     FTURER     IF   E           K DISK
     FHEAD11    IF   E           K DISK
     FFIRM      IF   E             DISK
     FCUNDL01   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FECNI96A   O  A E             DISK
     FECNT96A   O  A E             DISK
     FEDTM96A   O  A E             DISK
     FEFTX96A   O  A E             DISK
     FEGID96A   O  A E             DISK
     FELOC96A   O  A E             DISK
     FENAD96A   O  A E             DISK
     FEMEA96A   O  A E             DISK
     FEPCI96A   O  A E             DISK
     FERFF96A   O  A E             DISK
     FETDT96A   O  A E             DISK
     FETOD96A   O  A E             DISK
     FETSR96A   O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WTIDA                  1      4
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  W1004                  1     12
     D  WUAVD                  1      4  0
     D  WUPRO                  5     12  0
     D  WUKNA                 13     20  0
     D  WUMN                  21     29  0
     D                 DS
     D  W1004B                 1     11
     D  HEAVD                  1      4  0
     D  HEOPD                  5     11  0
     D                 DS
     D  WKUNDN                 1      8  0
     D                 DS
     D  WADR                   1     30
     D  W3251                  1      5
     D  W3164A                 6     10
     D  W3164B                11     30
     D  W3164                  6     30
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)                              Num edit
     D                 DS
     D  HEVS1                  1     25
     D  HEVS1A                 1      3
     D  HEVS1B                 5     25
     D                 DS
     D  HEVS2                  1     25
     D  HEVS2A                 1      3
     D  HEVS2B                 5     25
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA                                          CONSIGNMENT
     C                   EXSR      SRB                                          TRIP
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WAVD              4
     C                   PARM                    WPRO              8
     C                   PARM                    WKNA              8
     C                   PARM                    WMN               9
     C                   PARM                    WANTO
     C                   PARM                    WBRUDD            1
     C                   MOVE      WAVD          WUAVD
     C                   MOVE      WPRO          WUPRO
     C                   MOVE      WKNA          WUKNA
     C                   MOVE      WMN           WUMN
     C                   Z-ADD     0             WANTO
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    CREP0         WREP0
     C     *LIKE         DEFINE    CREP1         WREP1
     C     *LIKE         DEFINE    CREP2         WREP2
     C     *LIKE         DEFINE    CREP3         WREP3
     C     *LIKE         DEFINE    CREP4         WREP4
     C     *LIKE         DEFINE    CREP5         WREP5
     C     *LIKE         DEFINE    CREP6         WREP6
     C     *LIKE         DEFINE    CREP7         WREP7
     C     *LIKE         DEFINE    CREP8         WREP8
     C                   Z-ADD     0             ØX                2 0                      --I
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
     C                   Z-ADD     1             WEPOS1            4 0
     C                   READ      FIRM                                   51
      * DEF AV KEYS
     C     TURKEY        KLIST
     C                   KFLD                    WUAVD
     C                   KFLD                    WUPRO
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WKUNDN
     C     KODLKK        KLIST
     C                   KFLD                    KLKUNI
     C                   KFLD                    KLKLK
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR CONSIGNEMENT INFORMATION                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_____________
      * Get tripinfo
      *"""""""""""""
     C     TURKEY        CHAIN     TURER                              51
      *
     C     WUPRO         SETLL     HEAD11                                 20
     C     *IN20         IFEQ      '1'
     C     WUPRO         READE     HEAD11                                 51
     C     *IN51         DOWEQ     '0'
     C     HEST          IFNE      'S'
     C     HEUR          ANDEQ     'B'
     C     WBRUDD        IFEQ      'J'
     C     HEKNA         ANDNE     WUKNA
     C                   GOTO      SRA1
     C                   ENDIF
      *
     C                   ADD       1             WANTO             5 0
     C                   ADD       HENT          WENT              9 0
     C                   ADD       HEVKT         WEVKT            11 0
     C                   ADD       HELM          WELM              7 2
     C                   ADD       HEM3          WEM3             11 3
      *
     C                   Z-ADD     WANTO         WREP0
     C                   EXSR      CNI15
      *
     C                   ENDIF
     C     SRA1          TAG
     C     WUPRO         READE     HEAD11                                 51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR TRIP ONFORMATION                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C                   EXSR      CNT00
     C                   EXSR      RFF01
     C                   EXSR      TDT05
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CNI15 /
      *////////////////////////////////////////////////////////////
     C     CNI15         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   CNIR
      *_______________
      * Other segments
      *"""""""""""""""
     C                   EXSR      TSR15
     C                   EXSR      TOD21
     C                   EXSR      RFF22
     C                   EXSR      NAD30
     C                   EXSR      GID37
      *______
      * CNI15
      *""""""
      * 1490 Consolidation item number
     C                   Z-ADD     WANTO         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C1490
      * 1004 Document/message number
     C                   MOVEL     W1004B        C1004
      * Write record
     C                   Z-ADD     WUMN          CMN
     C                   Z-ADD     15            CGN
     C                   Z-ADD     WREP0         CREP0
     C                   WRITE     CNIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      *____
      * Key
      *""""
      *_____________________________
      * Total number of consignments
      *"""""""""""""""""""""""""""""
     C                   CLEAR                   CNTR
     C                   Z-ADD     WUMN          CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVE      '10 '         C6069
     C                   Z-ADD     WANTO         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   WRITE     CNTR
      *___________________
      * Total gross weight
      *"""""""""""""""""""
     C                   CLEAR                   CNTR
     C                   Z-ADD     WUMN          CMN
     C                   Z-ADD     0             CGN
     C     WEVKT         IFGT      0
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVE      '7  '         C6069
     C                   Z-ADD     WEVKT         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'KGM'         C6411
     C                   WRITE     CNTR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   DTMR
     C                   Z-ADD     WUMN          DMN
     C                   Z-ADD     0             DGN
     C                   Z-ADD     WREP0         DREP0
      *
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDATO         D2380
     C     D2380         CAT       WTIDA:0       D2380
     C                   MOVEL     '203'         D2379
     C                   Z-ADD     WREP1         DREP1
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM01 /
      *////////////////////////////////////////////////////////////
     C     DTM01         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   DTMR
     C                   Z-ADD     WUMN          DMN
     C                   Z-ADD     1             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *
     C                   MOVEL     '171'         D2005
     C                   MOVEL     TUDT          D2380
     C                   MOVEL     '102'         D2379
     C                   Z-ADD     WREP1         DREP1
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM05 /
      *////////////////////////////////////////////////////////////
     C     DTM05         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   DTMR
     C                   Z-ADD     WUMN          DMN
     C                   Z-ADD     5             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *_______________
      * Departure date
      *"""""""""""""""
     C                   MOVEL     '186'         D2005
     C                   MOVEL     TUDT          D2380
     C     D2380         CAT       '0000':0      D2380
     C                   MOVEL     '203'         D2379
     C                   Z-ADD     WREP1         DREP1
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FTX37 /
      *////////////////////////////////////////////////////////////
     C     FTX37         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   FTXR
     C                   Z-ADD     WUMN          FMN
     C                   Z-ADD     37            FGN
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
     C                   Z-ADD     WREP2         FREP2
      *__________________
      * Goods description
      *""""""""""""""""""
     C                   MOVEL     'AAA'         F4451
     C                   MOVEL     HEVS1B        F4440A
     C                   WRITE     FTXR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  GID37 /
      *////////////////////////////////////////////////////////////
     C     GID37         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      *_______________
      * Other segments
      *"""""""""""""""
     C                   EXSR      FTX37
     C                   EXSR      MEA39
     C                   EXSR      PCI42
      *___________________
      * Goods item details
      *"""""""""""""""""""
     C                   CLEAR                   GIDR
     C                   MOVEL     '1'           G1496                          goods item no
     C                   Z-ADD     HENT          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           G7224A
     C                   SELECT
     C     HEVS1A        WHENEQ    'RLL'
     C                   MOVEL     'RO'          G7065A                         type of pack
     C     HEVS1A        WHENEQ    'TRM'
     C                   MOVEL     'DR'          G7065A                         type of pack
     C                   OTHER
     C                   MOVEL     'CT'          G7065A                         type of pack
     C                   ENDSL
     C                   MOVEL     'ZZZ'         G3055A
     C                   Z-ADD     WUMN          GMN
     C                   Z-ADD     37            GGN
     C                   Z-ADD     WREP0         GREP0
     C                   Z-ADD     WREP1         GREP1
     C                   WRITE     GIDR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LOC13 /
      *////////////////////////////////////////////////////////////
     C     LOC13         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   LOCR
      *___________________________
      * Transport unit nationality
      *"""""""""""""""""""""""""""
     C                   MOVEL     'ZZZ'         L3227
     C                   MOVEL     TULK          L3225
     C                   MOVEL     TULK          KLKLK
     C     KODLKK        CHAIN     KODTLK                             20
     C                   MOVEL     KLKNVN        L3224
     C                   Z-ADD     WUMN          LMN
     C                   Z-ADD     13            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   WRITE     LOCR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LOC17 /
      *////////////////////////////////////////////////////////////
     C     LOC17         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   LOCR
      *_______________
      * Other segments
      *"""""""""""""""
      *___________________________
      * Place of terms of delivery
      *"""""""""""""""""""""""""""
     C                   MOVEL     '1  '         L3227
     C                   MOVEL     HESDT         L3225
     C     HESDT         CHAIN     STED2                              20
     C                   MOVEL     ST2NVN        L3224
     C                   Z-ADD     WUMN          LMN
     C                   Z-ADD     17            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   Z-ADD     WREP2         LREP2
     C                   WRITE     LOCR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MEA39 /
      *////////////////////////////////////////////////////////////
     C     MEA39         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      *_________________________
      * Consignment gross weight
      *"""""""""""""""""""""""""
     C                   CLEAR                   MEAR
     C                   Z-ADD     WUMN          MMN
     C                   Z-ADD     39            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   Z-ADD     WREP2         MREP2
     C                   MOVEL     'WT '         M6311
     C                   MOVEL     'AAD'         M6313
     C                   MOVEL     'KGM'         M6411
     C                   Z-ADD     HEVKT         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
      *_______
      * Volume
      *"""""""
     C     HEM3          IFGT      0
     C                   CLEAR                   MEAR
     C                   Z-ADD     WUMN          MMN
     C                   Z-ADD     39            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         MREP2
     C                   MOVEL     'VOL'         M6311
     C                   MOVEL     'ABJ'         M6313
     C                   MOVEL     'MTQ'         M6411
     C                   Z-ADD     HEM3          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
     C                   ENDIF
      *_______________
      * Loading meters
      *"""""""""""""""
     C     HELM          IFGT      0
     C                   CLEAR                   MEAR
     C                   Z-ADD     WUMN          MMN
     C                   Z-ADD     39            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         MREP2
     C                   MOVEL     'LMT'         M6311
     C                   MOVEL     'MTR'         M6411
     C                   Z-ADD     HELM          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   ADD       1             WREP2
     C                   WRITE     MEAR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD09 /
      *////////////////////////////////////////////////////////////
     C     NAD09         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      *_____________
      * Consolidator
      *"""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WUMN          NMN
     C                   Z-ADD     9             NGN
     C                   Z-ADD     WREP0         NREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         NREP1
     C                   MOVEL     'CS '         N3035
     C                   MOVEL     'NO000501'    N3039
     C                   MOVEL     'ZZZ'         N3055
     C                   MOVEL     'NORTRAIL'    N3036A
     C                   WRITE     NADR
      *_______________
      * Deconsolidator
      *"""""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WUMN          NMN
     C                   Z-ADD     9             NGN
     C                   Z-ADD     WREP0         NREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         NREP1
     C                   MOVEL     'DC '         N3035
     C                   MOVEL     'FODE0473'    N3039
     C     N3039         CAT       '73':0        N3039
     C                   MOVEL     'ZZZ'         N3055
     C                   MOVEL     'YYY'         N3036A
     C                   WRITE     NADR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD30 /
      *////////////////////////////////////////////////////////////
     C     NAD30         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      *__________
      * CONSIGNEE
      *""""""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP1
     C                   MOVEL     'CN'          N3035
     C                   Z-ADD     HEKNK         WKUNDN
     C     KUNKEY        CHAIN     CUNDL01                            20
     C     *IN20         IFEQ      '0'
     C                   MOVEL     KNAVN         N3036A                         party
     C                   MOVEL     ADR1          N3042A                         street
     C     ADR2          IFNE      *BLANKS
     C                   MOVEL     ADR2          N3042B                         street
     C                   ENDIF
     C                   MOVEL     ADR3          N3164                          city
     C     SYPOGE        IFNE      *BLANKS
     C                   MOVEL     SYPOGE        N3251                          postnr
     C                   ELSE
     C                   MOVEL     POSTNR        N3251                          postnr
     C                   ENDIF
     C                   MOVEL     SYLAND        N3207                          land
     C                   ELSE
     C                   MOVEL     HENAK         N3036A                         party
     C                   MOVEL     HEADK1        N3042A                         street
     C     HEADK2        IFNE      *BLANKS
     C                   MOVEL     HEADK2        N3042B                         street
     C                   ENDIF
     C                   MOVEL     HEADK3        WADR                           city
     C     W3164A        IFEQ      *BLANKS
     C                   MOVEL     W3164B        N3164                          city
     C                   ELSE
     C                   MOVEL     W3164         N3164                          city
     C                   ENDIF
     C                   MOVEL     W3251         N3251                          postnr
     C                   MOVEL     HELKK         N3207                          land
     C                   ENDIF
      *
     C                   Z-ADD     WUMN          NMN
     C                   Z-ADD     30            NGN
     C                   Z-ADD     WREP0         NREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         NREP1
     C                   WRITE     NADR
      *__________________________
      * Delivery name and address
      *""""""""""""""""""""""""""
     C                   CLEAR                   NADR
     C                   MOVEL     'DP '         N3035
     C                   MOVEL     HENAK         N3036A                         party
     C                   MOVEL     HEADK1        N3042A                         street
     C     HEADK2        IFNE      *BLANKS
     C                   MOVEL     HEADK2        N3042B                         street
     C                   ENDIF
     C                   MOVEL     HEADK3        WADR                           city
     C     W3164A        IFEQ      *BLANKS
     C                   MOVEL     W3164B        N3164                          city
     C                   ELSE
     C                   MOVEL     W3164         N3164                          city
     C                   ENDIF
     C                   MOVEL     W3251         N3251                          postnr
     C                   MOVEL     HELKK         N3207                          land
      *
     C                   Z-ADD     WUMN          NMN
     C                   Z-ADD     30            NGN
     C                   Z-ADD     WREP0         NREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         NREP1
     C                   WRITE     NADR
      *__________
      * CONSIGNOR
      *""""""""""
     C                   CLEAR                   NADR
     C                   MOVEL     'CZ '         N3035
     C     HEKNSF        IFNE      *ZEROS
     C                   Z-ADD     HEKNSF        WKUNDN
     C                   ELSE
     C                   Z-ADD     HEKNS         WKUNDN
     C                   ENDIF
     C     KUNKEY        CHAIN     CUNDL01                            20
     C     *IN20         IFEQ      '0'
     C                   MOVEL     KNAVN         N3036A                         party
     C                   MOVEL     ADR1          N3042A                         street
     C     ADR2          IFNE      *BLANKS
     C                   MOVEL     ADR2          N3042B                         street
     C                   ENDIF
     C                   MOVEL     ADR3          N3164                          city
     C     SYPOGE        IFNE      *BLANKS
     C                   MOVEL     SYPOGE        N3251                          postnr
     C                   ELSE
     C                   MOVEL     POSTNR        N3251                          postnr
     C                   ENDIF
     C                   MOVEL     SYLAND        N3207                          land
     C                   ENDIF
      *
     C                   Z-ADD     WUMN          NMN
     C                   Z-ADD     30            NGN
     C                   Z-ADD     WREP0         NREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         NREP1
     C                   WRITE     NADR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PCI42 /
      *////////////////////////////////////////////////////////////
     C     PCI42         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   PCIR
     C                   Z-ADD     WUMN          PMN
     C                   Z-ADD     42            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '28'          P4233
     C                   MOVEL     HEGM1         P7102A
     C                   MOVEL     HEGM2         P7102B
     C                   Z-ADD     WREP2         PREP2
     C                   WRITE     PCIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   RFFR
      *
     C                   EXSR      DTM01
      * Key
     C                   Z-ADD     WUMN          RMN
     C                   Z-ADD     1             RGN
     C                   Z-ADD     WREP0         RREP0
      * Data
     C                   MOVEL     'AFC'         R1153
     C                   MOVEL     WPRO          R1154
      * Write record
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF22 /
      *////////////////////////////////////////////////////////////
     C     RFF22         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      * FORWARDER REFERENCE
     C                   CLEAR                   RFFR
     C                   Z-ADD     WUMN          RMN
     C                   Z-ADD     22            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'FF'          R1153
     C                   MOVEL     W1004B        R1154
     C                   WRITE     RFFR
      * CUSTOMER REFERENCE
     C     HERFA         IFNE      *BLANKS
     C                   CLEAR                   RFFR
     C                   Z-ADD     WUMN          RMN
     C                   Z-ADD     18            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'CR'          R1153
     C                   MOVEL     HERFA         R1154
     C                   WRITE     RFFR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF29 /
      *////////////////////////////////////////////////////////////
     C     RFF29         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   RFFR
      *
     C*                    EXSR DTM01
      * Key
     C                   Z-ADD     WUMN          RMN
     C                   Z-ADD     29            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   Z-ADD     WREP2         RREP2
      * Data
     C                   MOVE      'ACL'         R1153
     C                   MOVEL     HEAVD         WEREF             9
     C                   MOVE      HEPRO         WEREF
     C                   MOVEL     WEREF         R1154
      * Write record
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TDT05 /
      *////////////////////////////////////////////////////////////
     C     TDT05         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   TDTR
      *_______________
      * Other segments
      *"""""""""""""""
     C                   EXSR      DTM05
     C                   EXSR      NAD09
      *______
      * TDT05
      *""""""
     C                   MOVEL     '20 '         T8051
     C                   MOVEL     '30 '         T8067
     C                   MOVEL     '31'          T8179
     C                   MOVEL     'XXX'         T3128
     C                   MOVEL     TUBILN        T8212
     C                   MOVEL     TULK          T8453
      *_____________
      * Write record
      *"""""""""""""
     C                   Z-ADD     WUMN          TMN
     C                   Z-ADD     5             TGN
     C                   Z-ADD     WREP0         TREP0
     C                   WRITE     TDTR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TOD21 /
      *////////////////////////////////////////////////////////////
     C     TOD21         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   TODR
     C                   Z-ADD     WUMN          TMN
     C                   Z-ADD     21            TGN
     C                   Z-ADD     WREP0         TREP0
      *__________________
      * Terms of delivery
      *""""""""""""""""""
     C                   MOVEL     '5'           T4055
     C                   SELECT
     C     HEFR          WHENEQ    '001'
     C                   MOVEL     'EXW'         T4053
     C     HEFR          WHENEQ    '005'
     C                   MOVEL     'ZZZ'         T4053
     C     HEFR          WHENEQ    '016'
     C                   MOVEL     'DAF'         T4053
     C     HEFR          WHENEQ    '019'
     C                   MOVEL     'DD1'         T4053
     C     HEFR          WHENEQ    '022'
     C                   MOVEL     'DDU'         T4053
     C     HEFR          WHENEQ    '024'
     C                   MOVEL     'DDP'         T4053
     C                   OTHER
     C                   MOVEL     'ZZZ'         T4053
     C                   ENDSL
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         TREP1
     C                   WRITE     TODR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TSR15 /
      *////////////////////////////////////////////////////////////
     C     TSR15         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   TSRR
     C                   Z-ADD     WUMN          TMN
     C                   Z-ADD     15            TGN
     C                   Z-ADD     WREP0         TREP0
      *__________________
      * Terms of delivery
      *""""""""""""""""""
     C                   MOVEL     '5'           T4055
     C                   MOVEL     'XXX'         T4053
     C                   MOVEL     'ZZZ'         T3055
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         TREP1
     C                   WRITE     TSRR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'                                        <-------------I
     C                   MOVE      ØA153H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA153X        ØA12                                         I
     C                   MOVE      ',   '        ØA12                                         I
     C                   MOVE      ØA153D        ØA12                                         I
     C                   END                                                    <-------------I
      * search from left and find first sign over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'                                        <-------------I
     C                   SUB       1             ØX                                           I
     C                   ENDIF                                                  <-------------I
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
