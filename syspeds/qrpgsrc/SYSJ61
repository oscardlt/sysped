      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE CARGO MANIFEST (SPEDITØR)       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 29-32 34-49 62-99
      ***********************************************************
      *
     FBOOKING   IF   E           K DISK
     FBOOKLF15  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOK15R)
     FBLLOK     IF   E           K DISK
     FBLV1LF    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTP     IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSTED2     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYSJ61WF  IF A E           K DISK
     FSYSJ61FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     FSYSJ61P   O    E             PRINTER OFLIND(*IN27)
     F                                     USROPN
     D CO              S              2    DIM(13)
     D AD              S             30    DIM(13)
      *ARRAYS FOR AVVIKENDE VALUTA
     D VV              S              3    DIM(10)
     D VP              S              7  0 DIM(10)
     D VC              S              7  0 DIM(10)
     D VG              S              7  0 DIM(10)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(SYSJ61P) -
     D                                     OUTQ(')
     D XCMD21          C                   CONST('PAGESIZE(50 160) -
     D                                     CPI(15) -
     D                                     OVRFLW(44)')
     D                 DS
     D  WSDT1D                 1      2  0
     D  WSDT1M                 3      4  0
     D  WSDT1Å                 5      6  0
     D  WSDT1                  1      6  0
     D                 DS
     D  YDT1Å                  1      4  0
     D  YDT1M                  5      6  0
     D  YDT1D                  7      8  0
     D  YDT1                   1      8  0
     D                 DS
     D  XDT3D                  1      2  0
     D  XDT3M                  3      4  0
     D  XDT3Å                  5      6  0
     D  XDT3                   1      6  0
     D                 DS
     D  BODT3Å                 1      4  0
     D  BODT3M                 5      6  0
     D  BODT3D                 7      8  0
     D  BODT3                  1      8  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C  N30
     CANN04              Z-ADD     5             LINNBR
     C  N30
     CANN04              Z-ADD     20            POSNBR
     C                   EXFMT     BILDE01
     C                   MOVEA     '0000'        *IN(96)
     C                   MOVE      '0'           *IN30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   03              GOTO      SLUTT
      *
     C   04              DO
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSLB
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      START
     C                   END
      *
     C                   EXSR      TESTB
     C   30              GOTO      START
      *
     C                   EXSR      HENTBK
      *
     C                   EXSR      PRINT
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C******************************************
     C     TESTB         BEGSR
     C******************************************
      *** TEST LOKAL BÅT
     C     WSLB          CHAIN     CDBOAT                             99
     C     *IN99         IFEQ      '1'                                          1<-B
     C                   MOVE      'SPB0102'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   ELSE                                                   1
     C                   MOVE      CBNAB         XNAB
     C                   END                                                    1<-E
      *
      *** TEST AVGANGSDATO
     C                   MOVE      WSDT1Å        UÅÅ               2 0
     C                   MOVE      WSDT1M        UMM               2 0
     C                   MOVE      WSDT1D        UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1                                            1<-B
     C                   MOVE      'SPD0001'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   END                                                    1<-E
      *
      * TEST TUREN
     C                   MOVE      WSDT1D        YDT1D
     C                   MOVE      WSDT1M        YDT1M
     C     WSDT1Å        IFGT      50                                           1<-B
     C     WSDT1Å        ADD       1900          YDT1Å
     C                   ELSE                                                   1
     C     WSDT1Å        ADD       2000          YDT1Å
     C                   END                                                    1<-E
     C     BOK15K        SETLL     BOOKLF15
     C     BOK15K        READE     BOOKLF15                               99
     C     *IN99         IFEQ      '1'                                          1<-B
     C                   MOVE      'YBC0715'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   END                                                    1<-E
      *
      * TEST PRINTERANVN
     C     WSPRT         IFEQ      *BLANKS                                      1<-B
     C                   MOVE      'YBC0716'     MSGNR
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   END                                                    1<-E
      *
     C     SLTB          ENDSR
      *
     C******************************************
     C     HENTBK        BEGSR
     C******************************************
     C     BOK15K        SETLL     BOOKLF15
     C     BOK15K        READE     BOOKLF15                               33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      'W'           W61TYP
     C     BLV1K         SETLL     BLV1LF
     C     BLV1K         READE     BLV1LF                                 34
     C     *IN34         DOWEQ     '0'
     C     BVKDC         IFNE      *BLANKS
     C                   MOVE      'C'           W61TYP
     C                   LEAVE
     C                   END
     C     BLV1K         READE     BLV1LF                                 34
     C  N34              ENDDO
      *
     C     BLLOKK        CHAIN     BLLOK                              34
     C     *IN34         IFEQ      '0'
     C                   MOVEL     BEBLNR        W61BLN
     C  N28W61BLN        COMP      *BLANKS                            2828
     C                   ELSE
     C                   MOVE      *BLANKS       W61BLN
     C                   END
      *
     C                   WRITE     SYSJ61WR
      *
     C     BOK15K        READE     BOOKLF15                               33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C******************************************
     C     PRINT         BEGSR
     C******************************************
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD
     C     XCMD11        CAT       WSPRT:0       UCMD
     C                   CAT       ')':0         UCMD
     C                   CAT       XCMD21:1      UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C     *LOVAL        SETLL     SYSJ61WF
     C                   READ      SYSJ61WF                               33
      *
     C     *IN33         DOWEQ     '0'
     C     BOOKK         CHAIN     BOOKING                            34
     C     BOLSH         IFEQ      *BLANKS
     C                   MOVE      '.'           BOLSH
     C                   END
     C     BOVHL         IFEQ      *BLANKS
     C                   MOVE      '.'           BOVHL
     C                   END
     C     BOLSH         IFNE      XLSH
     C     BOVHL         ORNE      XVHL
     C     W61TYP        ORNE      XTYP
     C     XLSH          IFNE      *BLANKS
     C                   WRITE     SYSJ61PC
     C     1             DO        10            NR
     C     VV(NR)        CABEQ     *BLANKS       UTARR1
     C                   MOVE      VV(NR)        VVNR
     C                   MOVE      VP(NR)        VPNR
     C                   MOVE      VC(NR)        VCNR
     C                   MOVE      VG(NR)        VGNR
     C                   WRITE     SYSJ61PD
     C                   END
     C     UTARR1        TAG
     C                   CLOSE     SYSJ61P
     C                   END
     C                   EXSR      HHEAD
     C                   END
      *
     C                   EXSR      HDETJ
      *
     C     BLV1K         SETLL     BLV1LF
     C     BLV1K         READE     BLV1LF                                 34
     C     *IN34         IFEQ      '1'
     C                   MOVE      *BLANKS       BVMN
     C                   MOVE      *BLANKS       BVVS
     C                   MOVE      *ZEROS        BVVKT
     C                   MOVE      *ZEROS        BVM3
     C                   MOVE      *BLANKS       BVKDC
     C                   END
      *
     C                   ADD       1             XNR
     C                   MOVE      CO(1)         XSCN
     C                   MOVE      AD(1)         XADR
      *
     C     *IN27         IFEQ      '1'
     C                   WRITE     SYSJ61PB
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
     C                   WRITE     SYSJ61PB
     C                   END
     C                   WRITE     SYSJ61P2
     C                   ADD       BVVKT         XTVKT
     C                   ADD       BVM3          XTM3
     C                   ADD       BVNT          XTNT
      *
     C     BOVALL        IFNE      *BLANKS
      * FØRSTE ELLER LIK FØRSTE, AKKUMULER I BUNN-TOT.VARIABLER
     C     XTVAL         IFEQ      *BLANKS
     C     XTVAL         OREQ      BOVALL
     C                   MOVE      BOVALL        XTVAL
     C                   SELECT
     C     BOCPL         WHENEQ    'C'
     C                   ADD       BOBLCP        XTLC
     C     BOCPL         WHENEQ    'P'
     C                   ADD       BOBLCP        XTLP
     C                   ENDSL
     C                   ADD       BOBLG         XTBLG
     C                   ELSE
      * FINN FØRSTE LIKE/LEDIGE ARRAY-ELEMENT, ADD DER
     C     1             DO        10            NR                2 0
     C     VV(NR)        IFEQ      BOVALL
     C     VV(NR)        OREQ      *BLANKS
     C                   GOTO      UTLOOP
     C                   END
     C                   END
     C     UTLOOP        TAG
      *
     C                   MOVE      BOVALL        VV(NR)
     C                   SELECT
     C     BOCPL         WHENEQ    'C'
     C                   ADD       BOBLCP        VC(NR)
     C     BOCPL         WHENEQ    'P'
     C                   ADD       BOBLCP        VP(NR)
     C                   ENDSL
     C                   ADD       BOBLG         VG(NR)
     C                   END
     C                   END
      *
     C                   Z-ADD     0             XBLLP
     C                   Z-ADD     0             XBLLC
      *
     C     *IN34         DOWEQ     '0'
     C     BLV1K         READE     BLV1LF                                 34
     C     *IN34         IFEQ      '0'
     C     XN            IFLE      12
     C                   ADD       1             XN
     C                   MOVE      CO(XN)        XSCN
     C                   MOVE      AD(XN)        XADR
     C                   END
     C     *IN27         IFEQ      '1'
     C                   WRITE     SYSJ61PB
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
     C                   WRITE     SYSJ61PB
     C                   END
     C                   WRITE     SYSJ61P3
     C                   ADD       BVVKT         XTVKT
     C                   ADD       BVM3          XTM3
     C                   ADD       BVNT          XTNT
     C                   END
     C                   ENDDO
      *
     C     XN            IFLE      12
     C                   ADD       1             XN
     C                   MOVE      CO(XN)        XSCN
     C                   MOVE      AD(XN)        XADR
     C                   END
     C     *IN27         IFEQ      '1'
     C                   WRITE     SYSJ61PB
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
     C                   WRITE     SYSJ61PB
     C                   END
     C                   WRITE     SYSJ61P4
      *
     C     BOOB          IFNE      *BLANKS
     C     XN            IFLE      12
     C                   ADD       1             XN
     C                   MOVE      CO(XN)        XSCN
     C                   MOVE      AD(XN)        XADR
     C                   END
     C     *IN27         IFEQ      '1'
     C                   WRITE     SYSJ61PB
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
     C                   WRITE     SYSJ61PB
     C                   END
     C                   WRITE     SYSJ61P5
     C                   END
      *
     C     BODT3         IFNE      0
     C     XN            IFLE      12
     C                   ADD       1             XN
     C                   MOVE      CO(XN)        XSCN
     C                   MOVE      AD(XN)        XADR
     C                   END
     C     *IN27         IFEQ      '1'
     C                   WRITE     SYSJ61PB
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
     C                   WRITE     SYSJ61PB
     C                   END
     C                   WRITE     SYSJ61P6
     C                   END
      *
     C     BOLOH         IFNE      *BLANKS
     C     XN            IFLE      12
     C                   ADD       1             XN
     C                   MOVE      CO(XN)        XSCN
     C                   MOVE      AD(XN)        XADR
     C                   END
     C     *IN27         IFEQ      '1'
     C                   WRITE     SYSJ61PB
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
     C                   WRITE     SYSJ61PB
     C                   END
     C                   WRITE     SYSJ61P7
     C                   END
      *
     C     BOREIS        IFNE      *BLANKS
     C     BOMM          ORNE      *BLANKS
     C     XN            IFLE      12
     C                   ADD       1             XN
     C                   MOVE      CO(XN)        XSCN
     C                   MOVE      AD(XN)        XADR
     C                   END
     C     *IN27         IFEQ      '1'
     C                   WRITE     SYSJ61PB
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
     C                   WRITE     SYSJ61PB
     C                   END
     C                   WRITE     SYSJ61P8
     C                   END
      *
     C     XN            DOWLT     12
     C                   ADD       1             XN
     C     AD(XN)        IFNE      *BLANKS
     C                   MOVE      CO(XN)        XSCN
     C                   MOVE      AD(XN)        XADR
     C     *IN27         IFEQ      '1'
     C                   WRITE     SYSJ61PB
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
     C                   WRITE     SYSJ61PB
     C                   END
     C                   WRITE     SYSJ61P9
     C                   END
     C                   END
      *
     C                   WRITE     SYSJ61PA
      *
     C                   READ      SYSJ61WF                               33
     C  N33              ENDDO
      *
     C                   WRITE     SYSJ61PC
     C     1             DO        10            NR
     C     VV(NR)        CABEQ     *BLANKS       UTARR2
     C                   MOVE      VV(NR)        VVNR
     C                   MOVE      VP(NR)        VPNR
     C                   MOVE      VC(NR)        VCNR
     C                   MOVE      VG(NR)        VGNR
     C                   WRITE     SYSJ61PD
     C                   END
     C     UTARR2        TAG
     C                   CLOSE     SYSJ61P
      *
     C                   ENDSR
      *
     C******************************************
     C     HHEAD         BEGSR
     C******************************************
     C                   OPEN      SYSJ61P
     C                   MOVE      W61TYP        XTYP              1
     C                   MOVE      *ZEROS        XTVKT
     C                   MOVE      *ZEROS        XTM3
     C                   MOVE      *ZEROS        XTNT
     C                   MOVE      *ZEROS        XTLP
     C                   MOVE      *ZEROS        XTLC
     C                   MOVE      *ZEROS        XTBLG
     C                   MOVE      *BLANKS       XTVAL
     C                   MOVEA     *BLANKS       VV
     C                   MOVEA     *ZEROS        VP
     C                   MOVEA     *ZEROS        VC
     C                   MOVEA     *ZEROS        VG
      *
     C     BOLSH         CHAIN     STED2                              31
     C     *IN31         IFEQ      '0'
     C                   MOVE      ST2NVN        XNALSH
     C                   ELSE
     C                   MOVE      *BLANKS       XNALSH
     C                   MOVEL     BOLSH         XNALSH
     C                   END
     C                   MOVE      BOLSH         XLSH              5
      *
     C     BOVHL         CHAIN     STED2                              31
     C     *IN31         IFEQ      '0'
     C                   MOVE      ST2NVN        XNAVHL
     C                   ELSE
     C                   MOVE      *BLANKS       XNAVHL
     C                   MOVEL     BOVHL         XNAVHL
     C                   END
     C                   MOVE      BOVHL         XVHL              5
      *
     C                   MOVE      *ZEROS        XNR
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ61P1
      *
     C                   ENDSR
      *
     C******************************************
     C     HDETJ         BEGSR
     C******************************************
     C                   MOVE      *BLANKS       CO
     C                   MOVE      *BLANKS       AD
     C                   MOVE      *ZEROS        XN                3 0
     C                   MOVE      BODT3D        XDT3D
     C                   MOVE      BODT3M        XDT3M
     C                   MOVE      BODT3Å        XDT3Å
      *
     C     BOOB          CHAIN     CDBOAT                             31
     C     *IN31         IFEQ      '0'
     C                   MOVE      CBNAB         XNAOB
     C                   ELSE
     C                   MOVE      *BLANKS       XNAOB
     C                   MOVEL     BOOB          XNAOB
     C                   END
      *
     C     BLLOKK        CHAIN     BLLOK                              31
     C     *IN31         IFEQ      '0'                                          1<-B
      * LOKAL B/L FINNES  -->  BRUK LOKAL B/L
      * SHIPPER
     C                   ADD       1             XN
     C                   MOVE      'S:'          CO(XN)
     C                   MOVE      BENAA         AD(XN)
     C     BEAD1A        IFNE      *BLANKS                                       2<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD1A        AD(XN)
     C                   END                                                     2<-E
     C     BEAD2A        IFNE      *BLANKS                                       2<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD2A        AD(XN)
     C                   END                                                     2<-E
     C     BEAD3A        IFNE      *BLANKS                                       2<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD3A        AD(XN)
     C                   END                                                     2<-E
      * CONSIGNEE
     C     BENAM         IFNE      *BLANKS                                       2<-B
     C                   ADD       1             XN
     C                   MOVE      'C:'          CO(XN)
     C                   MOVE      BENAM         AD(XN)
     C     BEAD1M        IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD1M        AD(XN)
     C                   END                                                      3<-E
     C     BEAD2M        IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD2M        AD(XN)
     C                   END                                                      3<-E
     C     BEAD3M        IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD3M        AD(XN)
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * NOTIFY
     C     BENAN         IFNE      *BLANKS                                       2<-B
     C                   ADD       1             XN
     C                   MOVE      'N:'          CO(XN)
     C                   MOVE      BENAN         AD(XN)
     C     BEAD1N        IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD1N        AD(XN)
     C                   END                                                      3<-E
     C     BEAD2N        IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD2N        AD(XN)
     C                   END                                                      3<-E
     C     BEAD3N        IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      BEAD3N        AD(XN)
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *
     C                   ELSE                                                   1
      *
      * LOKAL B/L FINNES IKKE  -->  BRUK BOOKING
     C     CUN1KS        CHAIN     CUNDL01                            31
     C     *IN31         IFEQ      '0'                                           2<-B
      * SHIPPER (SPEDITØR FINNES  -->  BRUK SPEDITØR)
     C                   ADD       1             XN
     C                   MOVE      'S:'          CO(XN)
     C                   MOVE      KNAVN         AD(XN)
     C     ADR1          IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      ADR1          AD(XN)
     C                   END                                                      3<-E
     C     ADR2          IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      ADR2          AD(XN)
     C                   END                                                      3<-E
     C     POSTNR        IFNE      0                                              3<-B
     C     ADR3          ORNE      *BLANKS
     C                   ADD       1             XN
     C                   MOVEL     POSTNR        AD(XN)
     C                   MOVE      ADR3          AD(XN)
     C                   END                                                      3<-E
      *
     C                   ELSE                                                    2
      *
      * SHIPPER (SPEDITØR FINNES IKKE  -->  BRUK AVDELINGSKUNDE)
     C                   ADD       1             XN
     C                   MOVE      'S:'          CO(XN)
     C                   MOVE      XNAA          AD(XN)
     C     XAD1A         IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      XAD1A         AD(XN)
     C                   END                                                      3<-E
     C     XAD2A         IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      XAD2A         AD(XN)
     C                   END                                                      3<-E
     C     XAD3A         IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      XAD3A         AD(XN)
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * CONSIGNEE (REDERI)
     C     CUN1KR        CHAIN     CUNDL01                            31
     C     *IN31         IFEQ      '0'                                           2<-B
     C                   ADD       1             XN
     C                   MOVE      KNAVN         AD(XN)
     C                   MOVE      'C:'          CO(XN)
     C     ADR1          IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      ADR1          AD(XN)
     C                   END                                                      3<-E
     C     ADR2          IFNE      *BLANKS                                        3<-B
     C                   ADD       1             XN
     C                   MOVE      ADR2          AD(XN)
     C                   END                                                      3<-E
     C     POSTNR        IFNE      0                                              3<-B
     C     ADR3          ORNE      *BLANKS
     C                   ADD       1             XN
     C                   MOVEL     POSTNR        AD(XN)
     C                   MOVE      ADR3          AD(XN)
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C                   ADD       1             XN
     C                   MOVE      'C:'          CO(XN)
     C                   MOVEL     BOKNR         AD(XN)
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
      *
      * LOSSEHAVN
     C     BOLOH         CHAIN     STED2                              31
     C     *IN31         IFEQ      '0'
     C                   MOVE      ST2NVN        XNALOH
     C                   ELSE
     C                   MOVE      *BLANKS       XNALOH
     C                   MOVEL     BOLOH         XNALOH
     C                   END
      *
     C                   SELECT
     C     BOCPL         WHENEQ    'C'
     C                   MOVE      BOBLCP        XBLLC
     C     BOCPL         WHENEQ    'P'
     C                   MOVE      BOBLCP        XBLLP
     C                   ENDSL
     C                   Z-ADD     1             XN
      *
     C                   ENDSR
      *
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     CUN1KA        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KOAKNR
     C     CUN1KS        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    BOKNS
     C     CUN1KR        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    BOKNR
     C     BOK15K        KLIST
     C                   KFLD                    WSLB
     C                   KFLD                    YDT1
     C     BOOKK         KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C     BLLOKK        KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C     BLV1K         KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    UAVD
     C     KODTPK        KLIST
     C                   KFLD                    KOPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    KOPLNR
      *
     C                   MOVE      'A'           KOAUNI
     C                   MOVE      'P'           KOPUNI
     C                   Z-ADD     33            KOPLNR
     C     KODTAK        CHAIN     KODTA                              33
     C     CUN1KA        CHAIN     CUNDL01                            33
     C                   MOVE      KNAVN         XNAA
     C                   MOVE      ADR1          XAD1A
     C                   MOVE      ADR2          XAD2A
     C                   MOVEL     POSTNR        XAD3A
     C                   MOVE      ADR3          XAD3A
     C     KODTPK        CHAIN     KODTP                              33
     C                   MOVE      KOPNVN        WSPRT
     C                   MOVE      *ZEROS        WSDT1
      *
     C                   ENDSR
      *
