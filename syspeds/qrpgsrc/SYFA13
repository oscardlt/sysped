      *
      ***********************************************************
      ***       KRYSSKOBLING KUNDENR./TRANSPORTØR             ***
      ***                                                     ***
      *** PROGRAMMERT AV TORE BRÅSTAD, SYSTEMA AS, MARS 1993  ***
      ***********************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 Visd trnsp navn(og litt kunde)
********************************************************************
      *
     FFIRM      IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTTR    UF A E           K DISK
N01  FCUNDL01   IF   E           K DISK
N01  FTRAN      IF   E           K DISK
     FSYFA13FM  CF   E             WORKSTN
     F                                     SFILE(H50:ZRECNO)
     C*
     C                   EXSR      INIT
     C*
     C                   READ      FIRMR                                  15
     C                   MOVE      FIFIRM        FIRMA
     C*
     C***************
     C* TØM SUBFILE
     C***************
     C     START         TAG
     C                   CLEAR                   SAKUND
     C                   CLEAR                   SMKUND
     C                   CLEAR                   SMNAVN
     C                   CLEAR                   FAKUND
     C     LES           SETLL     KODTTRR
     C                   MOVE      '1'           *IN93
     C                   WRITE     K60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C*************************
     C* Fyll subfile
     C*************************
     C*
     C     START1        TAG
     C                   DO        13
     C                   READ(N)   KODTTRR                                94
     C     *IN94         IFEQ      '0'
     C                   Z-ADD     KTRAK1        SAKUND
     C                   Z-ADD     KTRAK2        SMKUND
     C                   MOVEL     KTRAK3        SMNAVN
N01  c     Kunkey        chain     Cundl01
N01  c                   movel     KNAVN         SFKUN
N01  c     Trakey        chain     Tran
N01  c                   movel     VMNAVN        SFTNAV
     C                   ADD       1             ZRECNO
     C                   WRITE     H50
     C                   END                                                    <*IN94
     C  N94              ENDDO
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
      ****************************
      * Vis subfile
      ****************************
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   MOVE      '1'           *IN31
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   WRITE     B20
     C                   EXFMT     B40
     C                   READ      B20                                    66
     C                   ELSE
     C                   MOVE      '0'           *IN31
     C                   WRITE     K60
     C                   EXFMT     B40
     C                   READ      K60                                    66
     C                   END
     C*
     C     FKEYS         TAG
     C*
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C*
     C                   SETOFF                                       999830
     C*** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
     C*** F5=Forny
     C     *IN05         IFEQ      '1'
     C                   CLEAR                   NAKUND
     C                   CLEAR                   NMKUND
     C                   CLEAR                   NMNAVN
     C                   CLEAR                   AVDNR
     C                   GOTO      START
     C                   END                                                    <*IN05
     C*** F6=Ny linje
     C     *IN06         IFEQ      '1'
     C                   Z-ADD     LINNBR        LINNBX
     C                   Z-ADD     POSNBR        POSNBX
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   MOVE      'TBR0027'     WMODE
     C                   EXSR      CLRSCR
     C                   MOVE      '0'           *IN40
     C                   GOTO      START5
     C                   END                                                    <*IN06
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C                   CLEAR                   SAKUND
     C                   CLEAR                   SMKUND
     C                   CLEAR                   SMNAVN
     C                   CLEAR                   MSGNR
     C                   GOTO      START
     C                   END                                                    <*IN12
     C*** Roll
     C     *IN25         IFEQ      '1'
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C     START4        TAG
     C     *IN94         IFEQ      '0'
     C                   GOTO      START1
     C                   END                                                    <*IN94
     C                   END                                                    <*IN25
     C*** *IN08=Søk på artikkelnr.
     C     NAKUND        IFNE      0
     C                   MOVE      '1'           *IN08
     C                   GOTO      START6
     C                   ELSE
     C                   MOVE      '0'           *IN08
     C                   END                                                    <FAKUND
     C*** Hent valg fra skjerm
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C*** Ingen valg foretatt
     C     *IN97         IFEQ      '1'
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   EXFMT     B40
     C                   READ      K60                                    66
     C                   MOVE      '0'           *IN97
     C                   GOTO      FKEYS
     C                   END                                                    <*IN12
     C***                  MOVE '0'       *IN97
     C*** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
     C                   END                                                    <ZRECNO
     C     *IN98         IFEQ      '1'
     C     XRECNO        ANDNE     0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END                                                    <*IN98
     C                   ADD       1             LINNBR
     C                   GOTO      START3
     C*** Ny post
     C     START5        TAG
     C                   EXFMT     B10
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   MOVE      '0'           *IN30
     C     AVDNR         IFNE      0
     C                   EXSR      SOKKUN
     C                   END                                                    <AVDNR
     C*** F3=Exit
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   EXSR      CLRSCR
     C                   Z-ADD     LINNBX        LINNBR
     C                   Z-ADD     POSNBX        POSNBR
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   ELSE
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      START5
     C                   END                                                    <*IN30
     C                   EXSR      POSLIN
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   END                                                    <*IN12
     C                   GOTO      START3
     C*** Søk på artikkelnr.
     C     START6        TAG
     C                   MOVE      NAKUND        FAKUND
     C*** Finn søkekriterier
     C     FAKUND        IFEQ      0
     C                   MOVEA     '10'          *IN(50)
     C                   ELSE
     C                   MOVEA     '01'          *IN(50)
     C                   END                                                    <*SENAM
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START
     C*
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C                   EXSR      TSTSCR
     C     *IN30         IFEQ      '1'
     C                   GOTO      ENDTST
     C                   END                                                    <*IN30
     C                   MOVE      FIRMA         KTRAFI
     C                   Z-ADD     NAKUND        KTRAK1
     C                   Z-ADD     NMKUND        KTRAK2
     C                   MOVE      NMNAVN        KTRAK3
     C     *IN40         IFEQ      '1'
     C                   UPDATE    KODTTRR                              38
     C                   ELSE
     C                   WRITE     KODTTRR
     C                   END                                                    <*IN40
     C*
     C                   Z-ADD     NAKUND        SAKUND
     C                   Z-ADD     NMKUND        SMKUND
     C                   MOVEL     NMNAVN        SMNAVN
     C     *IN40         IFEQ      '1'
     C                   UPDATE    H50
     C                   ELSE
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     H50
     C                   END                                                    <*IN40
     C                   EXSR      CLRSCR
     C*
     C     ENDTST        ENDSR
     C*
     C***********************************************************
     C     TSTSCR        BEGSR
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C*
     C     NMNAVN        IFEQ      *BLANKS
     C                   MOVE      'TBR0001'     MSGNR
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   MOVE      '1'           *IN63
     C                   MOVE      '1'           *IN30
     C                   END                                                    <NAKUND
     C     NMKUND        IFEQ      0
     C                   MOVE      'TBR0002'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   MOVE      '1'           *IN62
     C                   MOVE      '1'           *IN30
     C                   END                                                    <NAKUND
     C     NAKUND        IFEQ      0
     C                   MOVE      'TBR0003'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     37            POSNBR
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN30
     C                   END                                                    <NAKUND
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     GETOPT        BEGSR
     C                   Z-ADD     0             LINNBR
     C                   Z-ADD     0             OPTNBR            2 0
     C                   SETOFF                                       919299
     C                   MOVE      '1'           *IN97
     C     *IN98         DOWEQ     '0'
     C                   READC     H50                                    98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   END                                                    <*IN99
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
     C                   MOVE      '0'           *IN97
     C*** 2=Endre
     C     WSNR          IFEQ      '2 '
     C     WSNR          OREQ      ' 2'
     C     WSNR          OREQ      '02'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'TBR0028'     WMODE
     C                   MOVE      '1'           *IN40
     C                   EXSR      CHGST
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   MOVE      '0'           *IN40
     C                   END                                                    <WSNR=2
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C*** 4=Slette
     C     WSNR          IFEQ      '4 '
     C     WSNR          OREQ      ' 4'
     C     WSNR          OREQ      '04'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'TBR0029'     WMODE
     C                   EXSR      DLTST
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR=4
     C                   ENDDO
     C*
     C     ENDGET        TAG
     C                   EXSR      POSLIN
     C     OPTNBR        IFGT      *ZEROS
     C                   ADD       7             LINNBR
     C                   ELSE
     C                   ADD       8             LINNBR
     C                   END                                                    <OPTNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGST         BEGSR
     C*
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     37            POSNBR
     C*
     C     SOK2          CHAIN     KODTTRR                            77
     C                   Z-ADD     KTRAK1        NAKUND
     C                   Z-ADD     KTRAK2        NMKUND
     C                   MOVEL     KTRAK3        NMNAVN
     C*
     C     STCHG         TAG
     C                   EXFMT     B10
     C     AVDNR         IFNE      0
     C                   EXSR      SOKKUN
     C                   END                                                    <AVDNR
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   Z-ADD     NAKUND        SAKUND
     C                   Z-ADD     NMKUND        SMKUND
     C                   MOVEL     NMNAVN        SMNAVN
     C                   CLEAR                   NAKUND
     C                   CLEAR                   NMKUND
     C                   CLEAR                   NMNAVN
     C                   CLEAR                   AVDNR
     C                   UPDATE    H50
     C                   GOTO      CHGEND
     C                   END                                                    <*IN12
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      STCHG
     C                   END                                                    <*IN30
     C*
     C     CHGEND        TAG
     C                   ENDSR
     C*
     C***********************************************
     C     DLTST         BEGSR
     C*
     C                   MOVE      '1'           *IN99
     C                   MOVEA     '11'          *IN(88)
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     37            POSNBR
     C     SOK2          CHAIN     KODTTRR                            77
     C                   Z-ADD     KTRAK1        NAKUND
     C                   Z-ADD     KTRAK2        NMKUND
     C                   MOVEL     KTRAK3        NMNAVN
     C                   EXFMT     B10
     C*
     C                   MOVEA     '00'          *IN(88)
     C     *IN11         IFEQ      '1'
     C     *IN77         IFEQ      '0'
     C                   DELETE    KODTTRR
     C                   END                                                    <*IN77
     C                   MOVE      NAKUND        SAKUND
     C                   END                                                    <*IN11
     C                   CLEAR                   NAKUND
     C                   CLEAR                   NMKUND
     C                   CLEAR                   NMNAVN
     C                   CLEAR                   AVDNR
     C                   UPDATE    H50
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CLRSCR        BEGSR
     C*
     C                   MOVE      '0'           *IN40
     C                   CLEAR                   NAKUND
     C                   CLEAR                   NMKUND
     C                   CLEAR                   NMNAVN
     C                   CLEAR                   AVDNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     POSLIN        BEGSR
     C*
     C     OPTNBR        IFGT      *ZERO
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C                   END                                                    <OPTNBR
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C*
     C     *LIKE         DEFINE    LINNBR        LINNBX
     C     *LIKE         DEFINE    POSNBR        POSNBX
     C                   MOVE      'A'           KODE              1
     C                   MOVE      *BLANKS       FIRMA             2
     C*
     C     LES           KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    NAKUND
     C*
     C     SOK           KLIST
     C                   KFLD                    KODE
     C                   KFLD                    AVDNR
     C*
     C     SOK2          KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    SAKUND
     C                   KFLD                    SMKUND
N01  C     KUNKEY        KLIST
N01  C                   KFLD                    FIRMA
N01  C                   KFLD                    SAKUND
N01  C     TRAKEY        KLIST
N01  C                   KFLD                    FIRMA
N01  C                   KFLD                    SMKUND
     C*
     C                   CLEAR                   SAKUND
     C                   MOVE      '1'           *IN50
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SOKKUN        BEGSR
     C*
     C     SOK           CHAIN     KOAR                               10
     C     *IN10         IFEQ      '0'
     C                   Z-ADD     KOAKNR        NAKUND
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   ELSE
     C                   Z-ADD     0             NAKUND
     C                   MOVE      'TBR0004'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   END
     C                   EXSR      TSTSCR
     C                   EXFMT     B10
     C*
     C                   ENDSR
