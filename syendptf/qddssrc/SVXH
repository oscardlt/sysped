********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11133 * 01 YBC PTF11 VEKT ER ENDRET FRA 9.1 TIL 11.3
12XXX * 02 CB  PTF12 MÅNGA NYA VARIABLAR FÖR NCTS FAS 5
********************************************************************
      *__________________________________
      * SYSPED / Tolldekl. TRANSITT
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SVXHR
     A            THST      R               REFFLD(ST)
      * THST = ' ' --> ÅPEN TRANSITTERING
      *        'A' --> HOLD PÅ Å SENDE UT
      *        'B' --> SENDT TIL IBM
      *        'C' --> SENDT TIL TOLLVESENET
      *        'D' --> KANSELLERT
      *        'E' --> HOLD PÅ Å BLI ENDRET
      *        'F' --> FEIL VED SIGILERING/TEKNISK
      *        'K' --> SIGILERING OK
      *        'M' --> FEILMELDING FRA TOLLVESENET
      *        'N' --> EDI-FACT OK
      *        'P' --> UTSKREVET IE29 MOTTATT
      *        'U' --> HAR FÅTT MRN-NUMMER
      *        'V' --> VAREKONTROLL
      *        'W' --> INGEN FRIGIVELSE
      *        'Z' --> TRANSITT AVSLUTTET
     A            THAVD          4S 0       COLHDG('AVDELING')
     A            THTDN          7S 0       COLHDG('OPPDRAGSNR')
     A            THSG      R               REFFLD(SG)
     A            THDT      R               REFFLD(DT)
     A            THPRO     R               REFFLD(PRO)
     A            THENKL         1A         COLHDG('FORENKLET PROSEDYRE')
     A            THDK           5A         COLHDG('DEKLARASJONSKODE')
      * HOVEDANSVARLIG
     A            THKNA     R               REFFLD(KN)
     A                                      COLHDG('HOVEDANSVARLIG')
     A            THNAA     R               REFFLD(NA)
     A                                      COLHDG('Navn ANSVARLIG')
     A            THADA1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 ANSVARLIG')
     A            THPNA          9A         COLHDG('POSTNR ANSVARLIG')
     A            THPSA         24A         COLHDG('POSTSTED ANSVARLIG')
     A            THLKA          2A         COLHDG('LANDKODE ANSVARLIG')
     A            THSKA          2A         COLHDG('SPRÅKKODE ANSVARLIG')
     A            THTINA        17A         COLHDG('TIN-KODE ANSVARLIG')
      * SELGER
     A            THKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            THNAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            THADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            THPNS          9A         COLHDG('POSTNR SELGER')
     A            THPSS         24A         COLHDG('POSTSTED SELGER')
     A            THLKS          2A         COLHDG('LANDKODE SELGER')
     A            THSKS          2A         COLHDG('SPRÅKKODE SELGER')
     A            THTINS        17A         COLHDG('TIN-KODE SELGER')
      * KJØPER
     A            THKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            THNAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            THADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            THPNK          9A         COLHDG('POSTNR KJØPER')
     A            THPSK         24A         COLHDG('POSTSTED KJØPER')
     A            THLKK          2A         COLHDG('LANDKODE KJØPER')
     A            THSKK          2A         COLHDG('SPRÅKKODE KJØPER')
     A            THTINK        17A         COLHDG('TIN-KODE KJØPER')
      * BESTEMMELSES LAND  RUBR. 17A
     A            THBLK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL B.LAND')
      * AUTORISERT LAGRINGSSTED
     A            THLSD         17A         COLHDG('AUTO.LAGRINGSSTED')
      * LASTSTED
     A            THLASD        17A         COLHDG('LASTESTED')
     A            THLAS2        17A         COLHDG('SUB-LASTSTED')
      * AVSENDELAND  RUBR. 15A
     A            THALK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL A.LAND')
      * TRANSPORTMÅTE  RUBR. 25
     A            THTRM     R               REFFLD(TRM)
      * TRANSPORTMÅTE  RUBR. 25 INNLAND
     A            THTRMI    R               REFFLD(TRM)
      * TRANSPORTMIDDEL VED AVGANG  RUBR. 18
     A            THTAID        27A         COLHDG('IDENTITET')
     A            THTASK         2A         COLHDG('SPRÅK')
     A            THTALK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
      * TRANSPORTMIDDEL VED GRENSEPASSERING  RUBR. 21
     A            THTGID        27A         COLHDG('IDENTITET')
     A            THTGSK         2A         COLHDG('SPRÅK')
     A            THTGLK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
      * SPRÅKKODE FOR FØLGEDOKUMENTER
     A            THSKFD         2A         COLHDG('SPRÅK FØLGEDOK.')
      * RUBR. C
     A            THCATS         8A         COLHDG('AVGANGS TOLLSTED')
      * TRANSITTERINGS TOLLSTEDER  RUBR. 51
     A            THTSD1         8A         COLHDG('TR.TOLLSTED 1')
     A            THTSD2         8A         COLHDG('TR.TOLLSTED 2')
     A            THTSD3         8A         COLHDG('TR.TOLLSTED 3')
     A            THTSD4         8A         COLHDG('TR.TOLLSTED 4')
     A            THTSD5         8A         COLHDG('TR.TOLLSTED 5')
     A            THTSD6         8A         COLHDG('TR.TOLLSTED 6')
     A            THTSD7         8A         COLHDG('TR.TOLLSTED 7')
     A            THTSD8         8A         COLHDG('TR.TOLLSTED 8')
      * BESTEMMELSES TOLLSTED RUBR. 53
     A            THTSB          8A         COLHDG('BESTEMMELSES T.STED')
      * RUBR. D/J
     A            THDDT          8S 0       COLHDG('TIDFRIST')
     A            THDFKD        20A         COLHDG('FORSEGLING-ID KODE')
     A            THDFSK         2A         COLHDG('FORSEGLING-ID SPRÅK')
     A            THDKR          2A         COLHDG('KONTROLL RESULTAT')
     A            THDANT         4S 0       COLHDG('ANTALL FORSEGLING')
     A            THDDTK         8S 0       COLHDG('DATO FOR KONTROLL')
     A            THDATS         8A         COLHDG('KRL. AVG.TOLLSTED')
     A            THDKAV        35A         COLHDG('KONTROLLERT AV')
     A            THDKSK         2A         COLHDG('KONTROLLERT AV LNG')
      * GARANTI RUBR. 52
     A            THGKD          1S 0       COLHDG('GARATIKODE')
     A            THGFT1        24A         COLHDG('GARANTI')
     A            THGFT2        35A         COLHDG('ANNEN GARANTI')
     A            THGADK         4A         COLHDG('ADGANGSKODE')
     A            THGBL         13S 2       COLHDG('GARATIBELØP')
     A            THGVK          3A         COLHDG('VALUTA')
     A            THGBGI         1S 0       COLHDG('BEGRENSNING INNEN EF')
     A            THGBGU         2A         COLHDG('BEGRENSNING UTENFOR')
      * CONTAINER  RUBR. 19
     A            THKDC     R               REFFLD(KDC)
      * TILBAKE SENDES TIL
     A            THTSSD         8A         COLHDG('TILBAKE SENDES T.ST.')
     A            THTSN1        35A         COLHDG('TOLLSTED NAVN')
     A            THTSN2        35A         COLHDG('GATENAVN OG NR')
     A            THTSPN         9A         COLHDG('POSTNR')
     A            THTSPS        24A         COLHDG('POSTSTED')
     A            THTSLK         2A         COLHDG('LANDKODE')
     A            THTSSK         2A         COLHDG('SPRÅKKODE')
      * ANDRE
     A            THLSTL         4S 0       COLHDG('ANTALL LASTELISTE')
     A            THVPOS         5S 0       COLHDG('VAREPOST')
     A            THNTK     R               REFFLD(NTK)
S01  A*           THVKB          9S 1       COLHDG('BRUTTOVEKT')
N01  A            THVKB         11S 3       COLHDG('BRUTTOVEKT')
     A            THDST     R               REFFLD(DST)
     A            THDSK          2A         COLHDG('DEKL. SPRÅK')
     A            THTARF        16          COLHDG('TARIFFØRSNAVN')
     A            THWS      R               REFFLD(WS)
      * TRANSISTTERINGSNUMMER OG GODKJENNINGSDATO
     A            THTRNR        18A         COLHDG('TRANSITTERINGSNR')
     A            THTRDT         8S 0       COLHDG('GODKJENNINGSDATO')
     A            THOMD          1S 0       COLHDG('OMDIRIGERING FORBUDT')
     A            THBIND       140A         COLHDG('BINDENDE REISERUTE')
     A            THTET          1S 0       COLHDG('TET RERUTEKSEMPLAR')
      * TVINN - VARIABLER
     A            THMF      R               REFFLD(MF)
     A            THMP      R               REFFLD(MP)
     A            THPKL     R               REFFLD(PK)
     A                                      COLHDG('Print ved linje ?')
     A            THKDV     R               REFFLD(KDV)
     A            TH0035    R               REFFLD(N0035)
      * EKSTRA FELTER FOR SVENSK NCTS
     A            THTUID        14A         COLHDG('TULLID')
     A            THSGDK         6A         COLHDG('DOKUMENTSIGILL')
     A            THSGME         3A         COLHDG('SIGILLMETODE')
     A            THSGUT         6A         COLHDG('UTSTÆLLARSIGILL')
     A            THSGID        10A         COLHDG('ID PÅ UTSTÆLLARE')
     A            THSGDT        12A         COLHDG('DAUM FØR SIGILL')
     A            THBYTE         7S 0       COLHDG('ANTALL BYTE EDI')
N02   *___________________________________________
N02   * ALLT NEDANFÖR HÄR ÄR NA VARIBLAR FÖR FAS 5
N02   *"""""""""""""""""""""""""""""""""""""""""""
      * TRANSIT OPERATION
     A            THDKT          1A         COLHDG('ADDI. DECL.TYPE')
     A            THTIRN        12A         COLHDG('TIR CARNET NUMBER')
     A            THSECU         1S 0       COLHDG('SECURITY')
     A            THRDIN         1S 0       COLHDG('REDUCED DATASET IND.')
     A            THSCIN         3A         COLHDG('SP. CIRCUMSTANCE IND')
     A            THCLKD         2A         COLHDG('LANGUAGE DEPARTURE')
     A            THBINI         1S 0       COLHDG('BINGING ITINEERARY')
     A            THLIDT        10A         COLHDG('LIMIT DATE')
      * AUTORISASJON
     A            THATYP         4A         COLHDG('1. AUTORISATIONSTYPE')
     A            THAREF        35A         COLHDG('1. AUTORISATIONSREF.')
      * CUSTOMS OFFICE OF TRANSIT, RUBR. 51
     A            THETA1         8S 0       COLHDG('EST. ARRIVAL DATE 1')
     A            THETAT1        4S 0       COLHDG('EST. ARRIVAL TIME 1')
     A            THETA2         8S 0       COLHDG('EST. ARRIVAL DATE 2')
     A            THETAT2        4S 0       COLHDG('EST. ARRIVAL TIME 2')
     A            THETA3         8S 0       COLHDG('EST. ARRIVAL DATE 3')
     A            THETAT3        4S 0       COLHDG('EST. ARRIVAL TIME 3')
     A            THETA4         8S 0       COLHDG('EST. ARRIVAL DATE 4')
     A            THETAT4        4S 0       COLHDG('EST. ARRIVAL TIME 4')
     A            THETA5         8S 0       COLHDG('EST. ARRIVAL DATE 5')
     A            THETAT5        4S 0       COLHDG('EST. ARRIVAL TIME 5')
     A            THETA6         8S 0       COLHDG('EST. ARRIVAL DATE 6')
     A            THETAT6        4S 0       COLHDG('EST. ARRIVAL TIME 6')
     A            THETA7         8S 0       COLHDG('EST. ARRIVAL DATE 7')
     A            THETAT7        4S 0       COLHDG('EST. ARRIVAL TIME 7')
     A            THETA8         8S 0       COLHDG('EST. ARRIVAL DATE 8')
     A            THETAT8        4S 0       COLHDG('EST. ARRIVAL TIME 8')
      * CUSTOMS OFFICE OF EXIT FOR TRANSIT
     A            THTSDE         8A         COLHDG('1. OFFICE FOR EXIT')
      * HOLDER OF THE TRANSIT PROCEDURE
     A            THKNH     R               REFFLD(KN)
     A                                      COLHDG('HOLDER OG TRANSIT')
     A            THTINH        17A         COLHDG('ID NUMMER')
     A            THTIRH        17A         COLHDG('TIR HOLDER ID NUMBER')
     A            THNAH         30A         COLHDG('HOLDER NAVN')
     A            THAD1H        30A         COLHDG('HOLDER ADDESS 1')
     A            THPNH         17A         COLHDG('HOLDER POST CODE')
     A            THPSH         24A         COLHDG('HOLDER CITYE')
     A            THLKH          2A         COLHDG('HOLDER COUNTRY')
     A            THCPH         30A         COLHDG('HOLDER CONT. PERSON')
     A            THTLH         20A         COLHDG('HOLDER TEL.')
     A            THEMH         50A         COLHDG('HOLDER EMAIL')
      * REPRESENTATIVE, HOVEDANSVARLIG
     A            THSTA          1S 0       COLHDG('STATUS ANSVARLIG')
     A            THCPA         30A         COLHDG('REPRE. CONT. PERSON')
     A            THTLA         20A         COLHDG('REPRE. TEL.')
     A            THEMA         50A         COLHDG('REPRE. EMAIL')
      *  CONTAINER  RUBR. 19
     A            THTMIN         1S 0       COLHDG('INLAND TR.MOD.')
     A            THTMGR         1S 0       COLHDG('GRENSE TR.MOD.')
     A            THUCR         35A         COLHDG('UCR REFERANSE NR')
      *  CARRIER
     A            THTINC        17A         COLHDG('TIN-KODE TRANSPORTØR')
     A            THKNC     R               REFFLD(KN)
     A                                      COLHDG('KUNDENR TRANSPORTØR')
     A            THNAC         30A         COLHDG('NAVN TRANSPORTØR')
     A            THTLC         20A         COLHDG('TRANSP. TEL.')
     A            THEMC         50A         COLHDG('TRANSP. EMAIL')
      *  SELGER
     A            THCPS         30A         COLHDG('KONTAKTPERSON SELGER')
     A            THTLS         20A         COLHDG('SELGER TEL.')
     A            THEMS         50A         COLHDG('EMAIL K.PERS. SELGER')
      *  ADDITIONAL SUPPLY CHAIN ACTOR
     A            THROLE         3A         COLHDG('ROLE')
     A            THIDR         17A         COLHDG('ROLE ID.NR')
      *  TRANSPORT EQUIPMENT
     A            THCNR         17A         COLHDG('CONTAINE')
      *   GOODS REFERENCE
     A            THGINR         5S 0       COLHDG('GOODS ITEM NUMBER')
      *  LOCATION OF GOODS
     A            THLGTY         1A         COLHDG('TYPE')
     A            THLGID         1A         COLHDG('QUALIFIER OF IDENT.')
     A            THLGAU        35A         COLHDG('AUTORISATION NUMBER')
     A            THLGAI         4A         COLHDG('ADDITIONAL IDENT.')
     A            THLGUN        17A         COLHDG('UN LOCCODE')
      *   CUSTOMS OFFICE
     A            THLGTS         8A         COLHDG('LOC. TOLLSTED')
      *   GNSS
     A            THLGLA        17A         COLHDG('LOC. LATITUDE')
     A            THLGLO        17A         COLHDG('LOC. LONGITUDE')
      *   ECONOMIC OPERATOR
     A            THKNE     R               REFFLD(KN)
     A                                      COLHDG('KUNDENR ØKONOMI OPR')
     A            THTINE        17A         COLHDG('TIN-KODE ØKONOMI OPR')
     A            THADE1        30A         COLHDG('ADR-1 ØKONOMI OPR')
     A            THPNE         17A         COLHDG('POSTNR ØKONOMI OPR')
     A            THPSE         24A         COLHDG('POSTSTED ØKONOMI OPR')
     A            THLKE          2A         COLHDG('LANDKODE ØKONOMI OPR')
     A            THADE2        17A         COLHDG('HUS.NR ØKONOMI OPR')
     A            THPNE2        17A         COLHDG('POSRNR HUS ØKONOMI')
     A            THLKE2         2A         COLHDG('LANDKODE HUS ØKONOMI')
     A            THCPE         30A         COLHDG('KONTAKTPERSON ØKONOM')
     A            THTLE         20A         COLHDG('TLF K.PERS. ØKONOMI')
     A            THEME         50A         COLHDG('EMAIL K.PERS. ØKONOM')
      *  COUNTRY OF ROUTING OF CONSIGNMENT
     A            THLKR1         2A         COLHDG('LANDKODE 1')
     A            THLKR2         2A         COLHDG('LANDKODE 2')
     A            THLKR3         2A         COLHDG('LANDKODE 3')
     A            THLKR4         2A         COLHDG('LANDKODE 4')
     A            THLKR5         2A         COLHDG('LANDKODE 5')
     A            THLKR6         2A         COLHDG('LANDKODE 6')
      *  ACTIVE BORDER TRANSPORT MEANS
     A            THTGTS         8A         COLHDG('TOLLSTED VED GRENSE')
     A            THTRMG    R               REFFLD(TRM)
     A            THTGCN        17A         COLHDG('CONVOYANCE REF.NR')
      *  PLACE OF LOADING
     A            THLSUN        17A         COLHDG('LASTESTED UN KODE')
     A            THLSLK         2A         COLHDG('LASTESTED LANDKODE')
     A            THLSNA        35A         COLHDG('LASTESTED NAVN')
      *  PLACE OF UNLOADING
     A            THLOUN        17A         COLHDG('LOSSESTED UN KODE')
     A            THLOLK         2A         COLHDG('LOSSESTED LANDKODE')
     A            THLONA        35A         COLHDG('LOSSESTED NAVN')
      *  PREVIOUS DOCUMENT
     A            THPDTY         4A         COLHDG('DOK. TYPEE')
     A            THPDRF        70A         COLHDG('DOK. REF.NR')
     A            THPDIN        35A         COLHDG('KOMPL. INFO.')
      *  SUPPORTING DOCUMENT
     A            THSDTY         4A         COLHDG('DOK. TYPEE')
     A            THSDRF        70A         COLHDG('DOK. REF.NR')
     A            THSDLN         5S 0       COLHDG('LINE ITEM NUMBER')
     A            THSDIN        35A         COLHDG('KOMPL. INFO.')
      *  TRANSPORT DOCUMENT
     A            THTDTY         4A         COLHDG('DOK. TYPEE')
     A            THTDRF        70A         COLHDG('DOK. REF.NR')
      *  ADDITIONAL DOCUMENT
     A            THADTY         4A         COLHDG('DOK. TYPEE')
     A            THADRF        70A         COLHDG('DOK. REF.NR')
      *  ADDITIONAL INFORMATION
     A            THAICD         5A         COLHDG('INFO. CODE')
     A            THAITX        70A         COLHDG('INFO. TEXT')
      *  TRANSPORT CHARGES
     A            THTRMP         1A         COLHDG('BETALINGSMÅTE')
      *
     A            THVERS         1A         COLHDG('NCTS VERSION')

     A          K THAVD
     A          K THTDN
