*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P00XXA* 01 JOV VER5 REKKEFØLGE PÅ KOSTN.KONTOER I SUBFILA FEIL..
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA04PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV GEBYR/FAKT-
     A                                      URAKODER')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            DALOPN         3S 0H      TEXT('LOPENO')
     A            WSNR           1A  I  8  2VALUES(' ' '1' '2' '4')
     A            DAUNNR    R        O  8  6REFFLD(GSSDGAR/DAUNNR *LIBL/GSSDGA)
     A            DACOG     R        O  9  6REFFLD(GSSDGAR/DACOG *LIBL/GSSDGA)
     A            DAPSN         60   O  8 16
     A            DAPSN2        60   O  9 16
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 52DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 63TIME
     A                                      COLOR(BLU)
     A                                  1  3'SHIPSYS/GSS078D'
     A                                      COLOR(BLU)
     A                                  4  2'Options:  1=Pick  2=Change   4=Del-
     A                                      ete'
     A                                      COLOR(BLU)
     A                                  1 20'Work with ADR file'
     A                                      COLOR(WHT)
     A                                  6  1'Opt  UN-NO     Proper shipping nam-
     A                                      e'
     A                                      COLOR(WHT)
     A                                  7  6'Class'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR POSNBR)
     A                                      OVERLAY
     A                                      WINDOW(*DFT 6 75 *NOMSGLIN)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  2  1'UN-NO.......:'
     A            WAUNNR    R        B  2 15REFFLD(GSSDGFR/DGUNNR *LIBL/GSSDGF)
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A            WALOPN    R        B  2 21REFFLD(GSSDGAR/DALOPN *LIBL/GSSDGA)
     A                                      DSPATR(PR)
     A                                      COLOR(WHT)
     A                                  3  1'Prop.Sh Name:'
     A            WAPSN     R        B  3 15REFFLD(GSSDGFR/DGPSN *LIBL/GSSDGF)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WAPSN2    R        B  4 15REFFLD(GSSDGFR/DGPSN *LIBL/GSSDGF)
     A                                      COLOR(YLW)
     A                                  5  1'Cls of goods:'
     A            WACOG     R        B  5 15REFFLD(GSSDGFR/DGCOG *LIBL/GSSDGF)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A  30        MSGTXT        58A  O  6  1MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              6  1MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A                                      WINDOW(*DFT 6 75 *NOMSGLIN)
     A            MSGNR          7A  P
     A            DAUNNR    R        O  2 15REFFLD(GSSDGFR/DGUNNR *LIBL/GSSDGF)
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A            DALOPN    R        O  2 21REFFLD(GSSDGAR/DALOPN *LIBL/GSSDGA)
     A                                      COLOR(WHT)
     A                                  3  1'Proper name:'
     A            DAPSN2        60   O  3 15COLOR(WHT)
     A*           DAPSN     R        O  3 19REFFLD(GSSDGFR/DGPSN *LIBL/GSSDGF)
     A*                                     COLOR(WHT)
     A                                  4  1'Class......:'
     A            DACOG     R        O  4 15REFFLD(GSSDGFR/DGCOG *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                  4 25'Packing group: '
     A  30        MSGTXT        58A  O  6  1MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5  1MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  1'UN-NO......:'
      *
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1 52DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 63TIME
     A                                      COLOR(BLU)
     A                                  1  3'SHIPSYS/GSS078D'
     A                                      COLOR(BLU)
     A                                  1 20'Work with ADR file'
     A                                      COLOR(WHT)
     A                                  4  2'Options:  1=Pick  2=Change   4=Del-
     A                                      ete'
     A                                      COLOR(BLU)
     A                                  6  1'Opt  UN-NO     Proper shipping nam-
     A                                      e'
     A                                      COLOR(WHT)
     A                                  7  6'Class'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SØKGEB
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                  2  2'Search from UN Number----->'
     A            W2UNNR         4Y 0I  2 33DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
