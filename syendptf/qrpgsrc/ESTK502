      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK502 MODULE(*LIBL/ESTK502 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse.............................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 FULL OMSKRIVING - ETT kall gjelde uansett kun ETT oppdrag - forenkling
10XXX * 02 JOV PTF10 Merk "dup-loggførig" med "KOP"=KOPI FRA ANNET   I TTEDRE - REASON
10XXX * 03 JOV PTF10 Test om fr.brevsnr alt ligger i Frisok ved HENT  (TKsign tester ikke leneger)
10XXX * 04 JOV PTF10 adskilt POD-melding ved TOLLPASS
P10047* 05 Jov PTF10 Error ved ukjent user flyttes til INIT-rutine
10172 * 06 SH  PTF10 norsk text til Track and trace
      * 07 JOV PTF10 Feilatig test ved DUP-spredning
      * 08 JOV PTF10 Oppdatere ATD/ATA ved hendelse COL og POD
      * 09 JOV PTF10 FRA V03.12 - UTF8 TEGNSETT (FORUTSETTER DefaultNetCCSID 01208 I HTTP)
      * 10 JOV PTF10 Konsekvens av PTF8 - Info om hentetid må oppdateres/sykroniseres)unngå E-varsel
      * 11 JOV PTF10 feil test i ptf09
      * %%%%%%%%%%%%%%%%%%%%%%
10172 * 12 JOV PTF11 Feil i norsk tket (ptf 06)
      * 13 JOV PTF11 Protect på oppdragets tot ved endr av vekt/vol mm (HEADF etter 201502xx)
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Pxxxxx* 14 Jov PTF12 motta sjåførangitt ny ETA
      * 15 jov PTF12 variabelnavn gpshenTkl
********************************************************************
TMP   *H debug(*yes)
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FHeadf     UF   E           K DISK    usropn
     FTurer14   UF   E           K DISK    usropn
S03  F*RISOK    O  A E           K DISK    Usropn
N03  FFRISOK    IF A E           K DISK    Usropn
     FTRACKF    O  A E           K DISK    Usropn
     FTRACKT    UF A E           K DISK    Usropn
     FTKSGM     UF   E           K DISK    USROPN
     F*Q         O  A F32000        DISK    Usropn
     FQ         O  A F30000        DISK    Usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D WSuser          s             10
     D Avd             s              4
     D AvdN            s              4  0
n04  D Opd             s              7
n04  D OpdN            s              7  0
N04  D SndL            s             25
     D Snd             s             17
     D AVVIK           s             50
     D KOMM            s             50
     D AVVIK1          s             50
     D KOMM1           s             50
     D AVVIK2          s             50
     D KOMM2           s             50
     D AVVIK3          s             50
     D KOMM3           s             50
     D AVVIK4          s             50
     D KOMM4           s             50
     D AVVIK5          s             50
     D KOMM5           s             50
     D SIGN            s             50
     D VERSJON         s             10
     D Stype           s              1
     D Svar            s              5
     D Term            s             20
     D AntKollL        s             10
     D LEVN            s              7  0
     D LM              s             10
     D LMN             s              4  2
     D Volum           s             10
     D VolumN          s              7  3
     D Vekt            s             10
     D VektN           s              9  0
     D HenAnkLat       s              9
     D HenAnkLong      s             10
     D LevAnkLat       s              9
     D LevAnkLong      s             10
     D Lat             s              9
     D Lon             s             10
     D LatN            s              8  6
     D LonN            s              9  6
     D NumBig          s             19  9
     D LinTxt          s            500
     D*Fil             s          32000
     D Fil             s          30000
     D GSMnr           s             15
     D GSMtxt          s            150
     D GSMcmd          s            150
     D tur             s              8
     D TurNr           s              8  0
     D Del             s              4
N14  D gpsHenKl        s             11
N14  D gpsHenKod       s              1
N14  D gpsLevKl        s             11
N14  D gpsLevKod       s              1
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D EKSTRA          s            256
     D EKSTRA2         s            256
     D Tegn            s              1
     D HexTegn         s              2
      * Definere  hex Æ Ø Å æ ø å  i UTF8
     D HexÆ            c                   x'6606'
     D HexØ            c                   x'6638'
     D HexÅ            c                   x'6615'
     D HexÆl           c                   x'6670'
     D HexØl           c                   x'669D'
     D HexÅl           c                   x'66B2'
      * Definere  hex Æ Ø Å æ ø å  i TK-spesialvri..
     D TK_ÆØÅ          c                   x'2618193F271C'
     D ÆØÅ             c                    'ÆØÅæøå'
     D Appo            c                   x'7D'
     D                 DS
     D  DUP                    1   1100  0
     D                                     DIM(100)
     D                 DS
     D  AvdOpd                 1     11  0
     D   DsAvd                 1      4  0
     D   DsOpd                 5     11  0
     D                 DS
     D  Kunderef               1     20
     D  KR1_3                  1      3
     D  KR1_17                 1     17
     D  KR4_20                 4     20
     D  KR17                  17     17
     D  KR18_20               18     20
     D                 DS
     D  WDsDÅMD                1      8
     D  WDsDÅ                  1      4
     D  WDsDM                  5      6
     D  WDsDD                  7      8
     D  WDsKlokke              9     14
N10  D  WDsTi                  9     10
N10  D  WDsMi                 11     12
     D  DatoKl                 1     14
N10  D                 DS
N10  D  TGIN02                 1     70
N10  D  TGIN02_9               1      9
N10  D                 DS
N10  D  TGIN03                 1     70
N10  D  TGIN03_9               1      9
N10  D                 DS
N10  D  TGIN04                 1     70
N10  D  TGIN04_9               1      9
     D                 DS
     D  HenAnkDt               1      8
     D  HenAnkKl               9     14
     D  HenAnkTid              1     14
     D                 DS
     D  LevAnkDt               1      8
     D  LevAnkKl               9     14
     D  LevAnkTid              1     14
     D                 DS
     D  TimeTmp                1     26
     D  TimeTmpÅ               1      4
     D  TimeTmpM               6      7
     D  TimeTmpD               9     10
     D  TimeTmpT              12     16
     D  TimeTmpTT             12     13
     D  TimeTmpTM             15     16
     D* TimeTmpTS             18     18
     D  TimeTmpTS             18     19
     D                 DS
     D  Idag                   1      8
     D  IdagTmpÅ               1      4
     D  IdagTmpM               5      6
     D  IdagTmpD               7      8
     D                 DS
     D  IdagTID                1      6
     D  IdagTT                 1      2
     D  IdagTM                 3      4
     D  IdagTTM                3      4
     D  IdagTS                 5      6
     D                 DS
     D  TID                    1     16
     D  Time1D                 1      2
     D  Time1P1                3      3
     D  Time1M                 4      5
     D  Time1P2                6      6
     D  Time1Å                 7     10
     D  Time1B                11     11
     D  Time1T                12     16
     D  Time1P3               14     14
     D                 DS
     D  TTTXTA                 1     70
     D  TXBY                   1     10
     D  XXSGM                 11     40
     D  TXWEBU                41     44
     D  XXWEBU                46     70
      *
      * Receive query string from the browser
      /copy syspeds/qrpgsrcpy,prolog3
      *
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
     C                   eval      *inlr = *on
     C                   return
      *
      **************************************************
     C     Parse         begsr
      **************************************************
      * Parse variables from QUERY_STRING environment variable
     C                   eval      WSuser = zhbgetvar('user')
     C                   eval      VERSJON = zhbgetvar('V')
     C                   eval      TUR  = zhbgetvar('TUR')
     C                   eval      TURNR   = c2n2(TUR)
     C                   eval      AVD  = zhbgetvar('AVD')
     C                   eval      AVDN    = c2n2(AVD)
     C                   eval      OPD  = zhbgetvar('OPD')
     C                   eval      OPDN    = c2n2(OPD)
     C                   eval      DEL  = zhbgetvar('DEL')
      *                            &del=*JP* =Parker, &del=*JM* =Manuelt, &del=*JF* =Ferdig
     C                   eval      SType = ZhbGetVar('SType')
     C                   eval      Snd     = ZhbGetVar('Snd')
     C                   eval      AntKollL= ZhbGetVar('AntKollL')
     C                   eval      NumBig  = c2n2(AntKollL)
     C     NumBig        ifle      9999999
     C                   eval      LevN    = NumBig
     C                   end
     C**                 eval      Volum   = ZhbGetVar('Volum')
     C                   eval      Vekt    = ZhbGetVar('Vekt')
     C                   eval      NumBig  = c2n2(Vekt)
     C     NumBig        ifle      999999999
     C                   eval      VektN   = NumBig
     C                   end
     C                   eval      Volum   = ZhbGetVar('Volum')
     C                   eval      NumBig  = c2n2(Volum)
     C     NumBig        ifle      9999.999
     C                   eval      VolumN  = NumBig
     C                   end
     C                   eval      Lm      = ZhbGetVar('LM')
     C                   eval      NumBig  = c2n2(LM)
     C     NumBig        ifle      99.99
     C                   eval      LmN     = NumBig
     c                   end
      * inntil 5 avik   Kode / kommentar
     C                   eval      Avvik1= ZhbGetVar('Avvik1')
     C                   eval      Avvik2= ZhbGetVar('Avvik2')
     C                   eval      Avvik3= ZhbGetVar('Avvik3')
     C                   eval      Avvik4= ZhbGetVar('Avvik4')
     C                   eval      Avvik5= ZhbGetVar('Avvik5')
     C     Avvik1        ifeq      'Annet'
     C                   eval      Avvik1= *blanks
     C                   end
     C     Avvik2        ifeq      'Annet'
     C                   eval      Avvik2= *blanks
     C                   end
     C     Avvik3        ifeq      'Annet'
     C                   eval      Avvik3= *blanks
     C                   end
     C     Avvik4        ifeq      'Annet'
     C                   eval      Avvik4= *blanks
     C                   end
     C     Avvik5        ifeq      'Annet'
     C                   eval      Avvik5= *blanks
     C                   end
     C                   eval      Komm1   = ZhbGetVar('Komm1')
     C                   eval      Komm2   = ZhbGetVar('Komm2')
     C                   eval      Komm3   = ZhbGetVar('Komm3')
     C                   eval      Komm4   = ZhbGetVar('Komm4')
     C                   eval      Komm5   = ZhbGetVar('Komm5')
     C     Komm1         Ifne      *blanks
     C                   eval      EKSTRA = Komm1
     C                   exsr      UTF8I
     C                   eval      Komm1 = EKSTRA
     C                   end
     C     Komm2         Ifne      *blanks
     C                   eval      EKSTRA = Komm2
     C                   exsr      UTF8I
     C                   eval      Komm2 = EKSTRA
     C                   end
     C     Komm3         Ifne      *blanks
     C                   eval      EKSTRA = Komm3
     C                   exsr      UTF8I
     C                   eval      Komm3 = EKSTRA
     C                   end
     C     Komm4         Ifne      *blanks
     C                   eval      EKSTRA = Komm4
     C                   exsr      UTF8I
     C                   eval      Komm4 = EKSTRA
     C                   end
     C     Komm5         Ifne      *blanks
     C                   eval      EKSTRA = Komm5
     C                   exsr      UTF8I
     C                   eval      Komm5 = EKSTRA
     C                   end
     C                   eval      Sign  = ZhbGetVar('Sign')
     C                   eval      EKSTRA = Sign
     C                   exsr      UTF8I
     C                   eval      Sign  = EKSTRA
     c                   eval      DatoKl = ZhbGetVar('Datokl')
     C                   eval      Fil = ZhbGetVar('Fil')
     c                   eval      Svar   = ZhbGetVar('Svar')
     c                   eval      Kunderef= ZhbGetVar('Kunderef')
     c                   eval      Term   = ZhbGetVar('Term')
     c                   eval      HenAnkTid = ZhbGetVar('HenAnkTid')
     c                   eval      HenAnkLat = ZhbGetVar('HenAnkLat')
     c                   eval      HenAnkLong= ZhbGetVar('HenAnkLong')
     c                   eval      LevAnkTid = ZhbGetVar('LevAnkTid')
     c                   eval      LevAnkLat = ZhbGetVar('LevAnkLat')
     c                   eval      LevAnkLong= ZhbGetVar('LevAnkLong')
N02   *
s15  C*                  eval      gpsHenKl  = zhbgetvar('gpsHenKl')
s15  C*                  eval      gpsHenKod = zhbgetvar('gpsHenKod')
N15  C                   eval      gpsHenKl  = zhbgetvar('gpsHentKl')
N15  C                   eval      gpsHenKod = zhbgetvar('gpsHentKod')
N14  C                   eval      gpsLevKl  = zhbgetvar('gpsLevKl')
N14  C                   eval      gpsLevKod = zhbgetvar('gpsLevKod')
TMP  c*                  DUMP
     C                   endsr
      **************************************************
     C     Exit          begsr
      **************************************************
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     HEADF
     C                   CLOSE     FRISOK
     C                   CLOSE     TRACKF
     C                   CLOSE     TRACKT
     C                   CLOSE     TKSGM
     C                   endsr
      **************************************************
     C     SetAll        begsr
      **************************************************
      * Send section top
     C                   callp     wrtsection('top')
S05  C* Ugyldig userID
S05  C*    *IN50         IFEQ      '1'
S05  C*                  eval      LinTxt = '#ERROR# Ukjent userID'
S05  C*                  callp     updHTMLvar('LINTXT':LinTxt)
S05  C*                  callp     wrtsection('lin')
S05  C*                  goto      utles
S05  C*                  end
      * Tur slettet fra PDA  - &del=*JM*(manuelt) &del=*JF*(Ferdig)
     C     Turnr         IFne      *zeros
     C     Del           andne     *blanks
     C                   exsr      DELtur
     C                   goto      utles
     c                   end
      * Handling/loggføring utfra STYPE
     C                   select
N14  C     gpsHenkl      WHENNE    *blanks                                      Ny estim. tid fra
N14  C     gpsLevkl      ORNE      *blanks                                      sjåfør basert på
N14  C                   exsr      NyEstim                                      GPS mm
     C     SType         WHENEQ    'H'                                          Henting
     C                   exsr      TTHent
     C     SType         WHENEQ    'L'                                          Levering
     C                   exsr      TTLevert
     C     SType         WHENEQ    'N'                                          Bekreft/avvis
     C                   exsr      TTBekAvv
     C     SType         WHENEQ    'O'                                          Endring Lest
     C                   exsr      TTLest
     C                   endsl
      *
     C* Signatur mottas normal kun ved Levering men KAN mottas også ved Henting
      *  mottas som x/y-koordinater -Lagre som txt-fil
     C     Fil           Ifne      *blanks
     c                   exsr      SignSR
     C                   end
     C*
      * foreløpig ubetinget..
     C                   eval      LinTxt = '#OK#;'+snd
     C                   callp     updHTMLvar('LINTXT':LinTxt)
     C                   callp     wrtsection('lin')
      *
     C                   eval      LinTxt = '#OK#;ALLE'
     C                   callp     updHTMLvar('LINTXT':LinTxt)
     C                   callp     wrtsection('lin')
      * Send section bot
     C     UTLES         TAG
     C                   callp     wrtsection('bot')
     C
     C                   endsr
     C
N14   *****************************************************
N14  C     NyEstim       begsr
N14   *****************************************************
N14  C                   movel     timedata1     TimeTmp
N14   *                                         12345678901
N14   * Ny ETD/ETA fra sjåfør  format er  f.eks 04.11 09:30
N14  c                   move      *zeros        TmpDTa            8
N14  c                   move      *zeros        TmpKLa            4
N14  c     HeadKey       chain     Trackt                             33
N14  C     *in33         Ifeq      *OFF
N14   * ny estim avg tid - ETD2
N14  c     gpsHenkl      ifne      *blanks
N14  c                   eval      TmpDTa=TimeTmpÅ
N14  c                             +%subst(gpsHenKl:4:2)+%subst(gpsHenKl:1:2)
N14  c                   eval      TmpKLa= %subst(gpsHenKl:7:2)
N14  c                             +%subst(gpsHenKl:10:2)
N14  c                   move      TmpDTa        TXETDD2
N14  c                   move      TmpKLa        TXETDK2
N14  c                   move      'S'           TXETD2K
N14  C                   end
N14   * ny estim lev tid - ETA2
N14  c     gpsLevkl      ifne      *blanks
N14  c                   eval      TmpDTa=TimeTmpÅ
N14  c                             +%subst(gpsLevKl:4:2)+%subst(gpsLevKl:1:2)
N14  c                   eval      TmpKLa= %subst(gpsLevKl:7:2)
N14  c                             +%subst(gpsLevKl:10:2)
N14  c                   move      TmpDTa        TXETAD2
N14  c                   move      TmpKLa        TXETAK2
N14  c                   move      'S'           TXETA2K
N14  C                   end
N14  c                   update    tracktr
N14  C                   end
N14  C                   endsr
      ***************************************************
     C     DELtur        begsr
      ***************************************************
      * &del=*JM* = manuelt = Åpne PDA-status - fjern sjåførnr (kontor sette ny??)
      * &del=*JP* = Parker  = Åpne PDA-status - (turen forblir i Hent Liste)
      * &del=*JF* = FERDIG  = PDA-status fra 1->2 = Fullført /fjernet fra PDA
     C     Del           ifeq      '*JM*'
     C     Del           oreq      '*JP*'
     C     Del           oreq      '*JF*'
     C                   OPEN      TURER14
     C     Turnr         chain     TURER14                            33
     C     *IN33         ifeq      *OFF
     C                   move      ' '           TUTST4
     C     Del           ifeq      '*JM*'
     C                   move      *zeros        TUSJA1
     C                   MOVE      'TKIPD'       PAID
     C     FiParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      *OFF
     C                   z-add     Pavrdn        Tusja1
     C                   end
     C                   end
     C     Del           ifeq      '*JF*'
     C                   move      '2'           TUTST4
     C                   end
     C                   update    turerr
     C                   end
     C                   Close     TURER14
     C                   end
     C                   endsr
     C
      *
     C***********************************************
     C     TTLevert      BEGSR
     C***********************************************
     c     HeadKey       Chain     Headf                              30
     c     *in30         ifeq      '0'
     c     Hesgm         IFEQ      *blanks
     c     Hesgm         oreq      'X'
     c     Hesgm         oreq      'LEVERT'
     C                   movel     Sign          HESGM
     C                   movel     WDsDÅMD       HEDTMO
     C                   movel     WDsKlokke     HEKLMO
N08  C                   movel     WDsDÅMD       TRSDTD
N08  C                   movel     WDsKlokke     TRSDTK
     C                   end
     C                   update    headfr
     C                   end
      * denne SR logger POD (TEI ved term på henting, TE2 ved TERM-annet(til Term på HO/VF))
     C                   MOVE      AvdN          TTAVD
     C                   MOVE      OpdN          TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   movel     timedata1     TimeTmp
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      Idag          TTDATL
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
     C                   MOVE      IdagTID       TTTIML
     C                   MOVEL     WSUser        TTUSER
      * Hvis Ank-tid er oppgitt legg det ut som egen datapost..
     c     LevAnkTid     ifne      *blanks
     C                   MOVE      LevAnkDt      TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     LevAnkKl      TTTIME
     C                   MOVEL     LevAnkLat     Lat
     C                   MOVEL     LevAnkLong    Lon
     C                   MOVE      'ARD'         TTACTI                         Arrived Delivery
     c                   exsr      LATLON
     C                   EVAL      TTTEXT = 'Arrived at delivery place.'
N06  C                   EVAL      TTTEXL = 'Ankommet leveringssted.'
     c     LatN          Ifne      *zeros
     c     LonN          andne     *zeros
     C                   EVAL      TTTEXT = %trim(TTTEXT) +
     c                             '  Lat: ' + %trim(%editc(LatN:'3')) +
     c                             '  Long: ' + %trim(%editc(LonN:'3'))
n06  C                   EVAL      TTTEXL = %trim(TTTEXL) +
n06  c                             '  Lat: ' + %trim(%editc(LatN:'3')) +
n06  c                             '  Long: ' + %trim(%editc(LonN:'3'))
     C                   end
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     C                   end
      *
     C                   MOVE      'POD'         TTACTI
     C                   MOVE      WDsDÅMD       TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     WDsKlokke     TTTIME
     C                   MOVEL     'Signed by '  TXBY
     C                   MOVEL     Sign          XXSGM
     C                   MOVEL     HESGM         TTNAME
     C                   MOVEL     TTTXTA        TTTEXT
n06  C                   MOVEL     TTTXTA        TTTEXL
     c     Term          ifne      *blanks
     C                   eval      TTTEXT = %trim(TTTEXT) +
     c                             ' at terminal '+term
n06  C                   eval      TTTEXL = %trim(TTTEXL) +
n06  c                             ' på terminal '+term
     c     HeadKey       Chain(N)  Headf                              30
     c     *in30         ifeq      '0'
     C     TRSLAG        IFEQ      'FF'
     C                   MOVE      'TEI'         TTACTI
     C                   else
     C                   MOVE      'TE2'         TTACTI
     C                   endif
     C                   end
     c                   movel     term          TTDEPO
     c                   end
N04   * ved POD legg på merknad hvis TOLLPASS
N04  c     TTACTI        ifeq      'POD'
N04  C                   exsr      TSTTP
N04  c                   endif
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
      * Hjelpervariabel for aktivitet knyttet til evt signatur
     c                   MOVE      TTACTI        SignACTI          3
      *
      * Actual time of ARRIVAL  i trackt
     c     HeadKey       chain     Trackt                             33
     C     *in33         Ifeq      *OFF
     c                   move      WDsDÅMD       TXATAD
     c                   movel     WDsKlokke     TXATAK
     c                   update    tracktr
     C                   end
      * POD på sms - foreløpig hardkodet.
     c     HEPRO         Ifeq      80000059
     c     Term          ANDEQ     *blanks
     C                   eval      GSMNr  = '4748052470'
     C                   eval      GSMTXT = 'POD-varsel: ' +
     C                             'Sending ' +
     C                             %trim(Snd) + ' fra ' +
     C                             %trim(HENAS) + ' ' +
     C                             'er kvittert mottatt av ' +
     C                             %trim(HESGM) + ' ' +
     C                             %trim(HEDTMO)+ ' Kl ' +
     C                             %trim(%editw(HEKLMO:'  :  '))
     C                   eval      GSMCMD = 'SYSMS SMSNR(' + %trim(GSMNR) +
     C                             ') SMSTXT('+Appo+%trim(GSMTXT)+Appo+')'
     c****               eval      rc = docmd(%trim(GSMCMD))
     C                   end
      * Logg evt avvik knyttet til hendelsen
     C                   exsr      AVVIKSR
      *
     C                   ENDSR
     C***********************************************
     C     TTHENT        BEGSR
     C***********************************************
      * denne SR logger COL (TEU ved henting på term)
     C                   MOVE      AvdN          TTAVD
     C                   MOVE      OpdN          TTOPD
     C                   MOVE      *ZEROS        TTFBNR
     C                   movel     timedata1     TimeTmp
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      Idag          TTDATL
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
     C                   MOVE      IdagTID       TTTIML
     C                   MOVEL     wsUser        TTUSER
     C                   MOVEL     wsUser        TTNAME
     C                   MOVE      *blanks       TTDEPO
      * Hvis Ank-tid er oppgitt legg det ut som egen datapost..
     c     HenAnkTid     ifne      *blanks
     C                   MOVE      HenAnkDt      TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     HenAnkKl      TTTIME
     C                   MOVEL     HenAnkLat     Lat
     C                   MOVEL     HenAnkLong    Lon
     C                   MOVE      'ARC'         TTACTI                         Arrived Collection
     c                   exsr      LATLON
     C                   EVAL      TTTEXT = 'Arrived at collection place.'
n06  C                   EVAL      TTTEXL = 'Ankommet hentestedet.'
     c     LatN          Ifne      *zeros
     c     LonN          andne     *zeros
     C                   EVAL      TTTEXT = %trim(TTTEXT) +
     c                             '  Lat: ' + %trim(%editc(LatN:'3')) +
     c                             '  Long: ' + %trim(%editc(LonN:'3'))
n06  C                   EVAL      TTTEXL = %trim(TTTEXL) +
n06  c                             '  Lat: ' + %trim(%editc(LatN:'3')) +
n06  c                             '  Long: ' + %trim(%editc(LonN:'3'))
     C                   end
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     C                   end
      *
     C                   MOVE      'COL'         TTACTI
     C                   MOVE      WDsDÅMD       TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     WDsKlokke     TTTIME
     c     Term          ifeq      *blanks
     C*                  eval      TTTEXT = 'Collected at customer'
n06  C*                  eval      TTTEXL = 'Hentet hos kunden'
     C                   eval      TTTEXT = 'Collected at sender'
n06  C                   eval      TTTEXL = 'Hentet hos avsender'
     c                   else
     C                   MOVE      'TEU'         TTACTI
     C                   EVAL      TTTEXT = 'Departed terminal ' +%trim(Term)
s12  C*                  EVAL      TTTEXL = 'Ankommet terminal ' +%trim(Term)
n12  C                   EVAL      TTTEXL = 'Avgått terminal ' +%trim(Term)
     C                   MOVEL     Term          TTDEPO
     c                   end
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
      *
      * OPPDATER ACTUAL TIME TIME OF DEPARTURE
     c     Term          ifeq      *blanks
     c     HeadKey       chain     Trackt                             33
     C     *in33         Ifeq      *OFF
     c                   move      WDsDÅMD       TXATDD
     c                   movel     WDsKlokke     TXATDK
     c                   update    tracktr
N08  c     HeadKey       chain     Headf                              33
N08  C     *in33         Ifeq      *OFF
N08  C                   movel     WDsDÅMD       TRSDFD
N08  C                   movel     WDsKlokke     TRSDFK
N08  c                   update    headfr
N08  C                   end
     C                   end
     c                   end
      * Hjelpervariabel for aktivitet knuttet til evt signatur
     c                   MOVE      TTACTI        SignACTI
      * Logg evt avvik knyttet til hendelsen
     C                   exsr      AVVIKSR
      *
      *
      * Endringer i antall /vekt volum....
      * OBS! Forutsetter at en henting direkte hos kunde må være ETT oppdrag
      * (kan IKKE være et av flere fraktbrev som ved en levering/spredning)
     C     HeadKey       Chain     Headf                              33
     C     *IN33         IFEQ      *OFF
     C     LevN          IFNE      HENT
     C     VektN         ORNE      HEVKT
     C     VolumN        ORNE      HEM3
     C     LmN           ORNE      HELM
N13  C                   MOVE      'P'           HESTL4                         Protect Totaler
     C                   MOVE      'CHG'         TTACTI
     C                   eval      TTTEXT = 'Changed'
n06  C                   eval      TTTEXL = 'Endret'
     C     LevN          IFNE      HENT
     C                   eval      TTTEXT = %trim(TTTEXT) +' Cll:'+
     C                             %trim(%editc(HENT:'3'))+'->'+
     C                             %trim(%editc(LevN:'3'))
n06  C                   eval      TTTEXL = %trim(TTTEXL) +' Cll:'+
n06  C                             %trim(%editc(HENT:'3'))+'->'+
n06  C                             %trim(%editc(LevN:'3'))
     C                   END
     C     VektN         IFNE      HEVKT
     C                   eval      TTTEXT = %trim(TTTEXT) +' Wt:'+
     C                             %trim(%editc(HEVKT:'3'))+'->'+
     C                             %trim(%editc(VektN:'3'))
n06  C                   eval      TTTEXL = %trim(TTTEXL) +' Vkt:'+
n06  C                             %trim(%editc(HEVKT:'3'))+'->'+
n06  C                             %trim(%editc(VektN:'3'))
     C                   END
     C     VolumN        IFNE      HEM3
     C                   eval      TTTEXT = %trim(TTTEXT) +' Vol:'+
     C                             %trim(%editc(HEM3:'L'))+'->'+
     C                             %trim(%editc(VolumN:'L'))
n06  C                   eval      TTTEXL = %trim(TTTEXL) +' Vol:'+
n06  C                             %trim(%editc(HEM3:'L'))+'->'+
n06  C                             %trim(%editc(VolumN:'L'))
     C                   END
     C     LmN           IFNE      HELM
     C                   eval      TTTEXT = %trim(TTTEXT) +' Lm:'+
     C                             %trim(%editc(HELM:'L'))+'->'+
     C                             %trim(%editc(LmN:'L'))
n06  C                   eval      TTTEXL = %trim(TTTEXL) +' Lm:'+
n06  C                             %trim(%editc(HELM:'L'))+'->'+
n06  C                             %trim(%editc(LmN:'L'))
     C                   END
     C                   WRITE     TRACKFR
      * burde logges også på ande i DUP-kjeden ????
     c*??????            exsr      DupOppd
      *
     C                   eval      HENT  = LevN
     C                   eval      HEVKT = VektN
     C                   eval      HEM3  = VolumN
     C                   eval      HELM  = LmN
     C     TKSGMKey      CHAIN     TKSGM                              35
     C     *in35         IFEQ      '0'
     C                   move      HENT          TGNT
     C                   move      HEVKT         TGVKT
     C                   move      HELM          TGLM
     C                   move      HEM3          TGM3
     C  N35              update    TKSGMR
     C                   END
     C                   END
     C                   Update    HEADFR
     C                   END
N10
N10   * oppdater hentetidspkt dersom det ligger i Fritekst (info2, 3, eller 4 )
N10  c     Versjon       ifge      '03.13'
N10  C     TKSGMKey      CHAIN     TKSGM                              35
N10  C     *in35         IFEQ      '0'
N10  c                   select
N10  c                   when      TGIN02_9='H/L-Tid: '
N10  c                   eval      TGIN02='H/L-Tid: '
N10  c                             +WDsDD+'.'+WDsDM+' '+WDsTi+':'+WDsMi
N10  c                             +%subst(TGIN02:21:20)                        fra og med /...
N10  c*
N10  c                   when      TGIN03_9='H/L-Tid: '
N10  c                   eval      TGIN03='H/L-Tid: '
N10  c                             +WDsDD+'.'+WDsDM+' '+WDsTi+':'+WDsMi
N10  c                             +%subst(TGIN03:21:20)                        fra og med /...
N10  c*
N10  c                   when      TGIN04_9='H/L-Tid: '
N10  c                   eval      TGIN04='H/L-Tid: '
N10  c                             +WDsDD+'.'+WDsDM+' '+WDsTi+':'+WDsMi
N10  c                             +%subst(TGIN04:21:20)                        fra og med /...
N10  C                   endsl
N10  C  N35              update    TKSGMR
N10  C                   endif
N10  C                   endif
      *KUNDEREF
      *KUNDEREF
      * e/02.38 sendes Kunderef alltid fordi avs_opd nå er nøkkel/Tksign kan ikke sammenligne med gm
     C     KundeRef      ifne      *blanks
     C                   Move      *blanks       Snd17            20
     C                   Move      *blanks       FSSOK
     C                   SELECT
     C     KR1_3         WHENEQ    '401'
     C                   Movel     KR4_20        Snd17
     C     KR17          WHENNE    ' '
     C     KR18_20       andeq     *blanks
     C                   Movel     KR1_17        Snd17
     C                   endsl
     c     Snd17         ifeq      *blanks
     C                   move      'ORD'         FSKODE
     C                   movel     KundeRef      FSSOK
     c                   else
     C                   move      'IFB'         FSKODE
     C                   movel     Snd17         FSSOK
N03  C     IFBkey        Chain     FRISOK                             33
N03  c     *IN33         cabeq     '0'           UtHent
     c                   end
     C                   move      Heavd         FSAVD
     C                   move      Heopd         FSOPD
     C                   move      HedtOp        FSDtOp
     c                   write     frisokr
     C                   end
     C     Uthent        ENDSR
     C***********************************************
     C     AVVIKSR       BEGSR
     C***********************************************
      * PDA sender kun de avvik som ikke er sendt før..
      * Avvik/merknad...
     C     Avvik1        Ifne      *blanks
     C     KOMM1         Orne      *blanks
     C                   eval      Avvik    =    Avvik1
     C                   eval      KOMM     =    KOMM1
     C                   exsr      AVVIKSR2
     c                   end
     C     Avvik2        Ifne      *blanks
     C     KOMM2         Orne      *blanks
     C                   eval      Avvik    =    Avvik2
     C                   eval      KOMM     =    KOMM2
     C                   exsr      AVVIKSR2
     c                   end
     C     Avvik3        Ifne      *blanks
     C     KOMM3         Orne      *blanks
     C                   eval      Avvik    =    Avvik3
     C                   eval      KOMM     =    KOMM3
     C                   exsr      AVVIKSR2
     c                   end
     C     Avvik4        Ifne      *blanks
     C     KOMM4         Orne      *blanks
     C                   eval      Avvik    =    Avvik4
     C                   eval      KOMM     =    KOMM5
     C                   exsr      AVVIKSR2
     c                   end
     C     Avvik5        Ifne      *blanks
     C     KOMM5         Orne      *blanks
     C                   eval      Avvik    =    Avvik5
     C                   eval      KOMM     =    KOMM5
     C                   exsr      AVVIKSR2
     C                   end
      *
     C                   endsr
     C***********************************************
     C     AVVIKSR2      BEGSR
     C***********************************************
     C                   MOVE      AvdN          TTAVD
     C                   MOVE      OpdN          TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   MOVE      'AVV'         TTACTI
     C                   MOVEL     WSUSER        TTUSER
     C                   MOVE      *blanks       TTTEXT
n06  C                   MOVE      *blanks       TTTEXL
     C     AVVIK         Ifne      *blanks
     C     AVVIK         CAT       '/':1         TTTEXT
     C                   CAT       KOMM:1        TTTEXT
n06  C     AVVIK         CAT       '/':1         TTTEXL
n06  C                   CAT       KOMM:1        TTTEXL
     c                   else
     C                   Movel     KOMM          TTTEXT
n06  C                   Movel     KOMM          TTTEXL
     c                   end
      *
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
      *
     C                   ENDSR
      *
     C***********************************************
     C     TTBekAvv      BEGSR
     C***********************************************
      * Type = N (bekrefte Ny eller avvise NY/GML ordre fra PDA)
      * BEKREFTE NY ordre (v/Svar=*JA*), eller avvise-Ny/GML-(Svar=ULIK *JA*, *NEI* el *OPD*)
      * Dersom svar=*OPD* er det et LØST OPPDRAG (skal ikke ta av tur...)
     C     HeadKey       Chain(N)  Headf                              33
     C     TKSGMKey      CHAIN     TKSGM                              35
     C                   MOVE      AvdN          TTAVD
     C                   MOVE      OpdN          TTOPD
     C                   MOVE      *ZEROS        TTFBNR
      * Svar= *JA* = BEKREFTER ny ordre
     C     Svar          Ifeq      '*JA*'
     C  N35              MOVE      'B'           TGKOH                          Bekreftet
     C  N35              UPDATE    TKSGMR
     C                   MOVE      'NJA'         TTACTI
     C                   eval      TTTEXT = 'New order accepted by driver'
n06  C                   eval      TTTEXL = 'Ny ordre akseptert av sjåfør'
     c                   else
      * Svar= *NEI* eller *OPD*(Løst oppdrag) = Avvis / fjern
     C  N35              DELETE    TKSGMR                                       Fjern også i logg
     C                   MOVE      'NEI'         TTACTI
      * LØST oppdrag på PDA..
     C     Svar          Ifeq      '*OPD*'
     C                   eval      TTTEXT = 'Removed from PDA by driver. '+
     C                             %trim(KOMM1)
n06  C                   eval      TTTEXL = 'Fjernet fra PDA av sjåfør. '+
n06  C                             %trim(KOMM1)
     c                             +' (standalone order)'
     C                   else
     c                   MOVE      HEPRO         HEPROA            8
     C                   eval      TTTEXT = 'Rejected by driver: '+
     C                             %trim(KOMM1)+
     C                             ' Rmvd from ' + HEPROA
n06  C                   eval      TTTEXL = 'Avvist av sjåfør: '+
n06  C                             %trim(KOMM1)+
n06  C                             ' Fjernet fra ' + HEPROA
     C                   eval      TTDEPO  = 'T:'+ HEPROA                       (v/laaang reason..)
     C                   Call      'SYGE21B'
     C                   PARM                    HEPRO
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG            1
     C                   end
     c                   end
     C                   movel     timedata1     TimeTmp
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      Idag          TTDATL
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
     C                   MOVE      IdagTID       TTTIML
     C                   MOVE      Idag          TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     IdagTID       TTTIME
     C                   MOVEL     wsUser        TTUSER
     C                   MOVEL     wsUser        TTNAME
     c                   move      ' '           TTMANU
     C     TTACTI        ifeq      'NEI'
     c                   move      'S'           TTMANU
     c                   end
     C                   WRITE     TRACKFR
      * avvisning av oppdrag logges også på andre i kjeden
     C     TTACTI        ifeq      'NEI'
     c                   exsr      DupOppd
     c                   endif
     C                   ENDSR
      *
     C***********************************************
     C     TTLest        BEGSR
     C***********************************************
     C     HeadKey       Chain(N)  Headf                              33
     C     TKSGMKey      CHAIN     TKSGM                              35
     C  N35              MOVE      'B'           TGKOH
     C  N35              UPDATE    TKSGMR
     C                   MOVE      AvdN          TTAVD
     C                   MOVE      OpdN          TTOPD
     C                   MOVE      *ZEROS        TTFBNR
     C                   MOVE      'CHR'         TTACTI
     C                   eval      TTTEXT = 'Changes on order read by driver'
n06  C                   eval      TTTEXL = 'Endring på ordre lest av sjåfør'
     C                   movel     timedata1     TimeTmp
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      Idag          TTDATL
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
     C                   MOVE      IdagTID       TTTIML
     C                   MOVE      Idag          TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     IdagTID       TTTIME
     C                   MOVEL     wsUser        TTUSER
     C                   MOVEL     wsUser        TTNAME
     C                   WRITE     TRACKFR
      * changes read by driver logges IKKE på andre i DUP-kjeden
      *                  exsr      DupOppd
     C                   ENDSR
      *
      **************************************************************************
     C     DupOppd       begsr
      **************************************************************************
N02  C                   MOVE      'KOP'         TTEDRE                         KOPI FRA ANNET...
N07  c                   movel     AvdN          AvdOpdN          11 0
N07  c                   move      OpdN          AvdOpdN
      * Dupliser hendelse til alle oppdrag i "DUP-struktur"
     c     1             do        100           Nr
     c     Dup(Nr)       cabeq     *zeros        UtDupArr
     c                   move      Dup(Nr)       AvdOpd
s07  C*    DsAvd         IFNE      AvdN
s07  C*    DsOpd         andne     OpdN
N07  c     AvdOpd        ifne      AvdopdN
     C                   MOVE      TTAVD         TmpAVD            4 0
     C                   MOVE      TTOPD         TmpOPD            7 0
     C                   MOVE      DsAvd         TTAVD
     C                   MOVE      DsOpd         TTOPD
     C                   WRITE     TRACKFR
     C                   MOVE      TmpAVD        TTAVD
     C                   MOVE      TmpOPD        TTOPD
      * dersom POD oppdater også oppdrag....
     C                   ENDIF
     c                   enddo
     C     UtDupArr      tag
N02  C                   MOVE      *BLANKS       TTEDRE                         EVENT PÅ DETTE ..
     C                   ENDSR
N04   **************************************************
N04  C     TSTTP         begsr
N04   **************************************************
N04  C     TKSGMKey      CHAIN(N)  TKSGM                              35
N04  c     *IN35         ifeq      '0'
N04  c                   if        (%subst(TGin01:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin02:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin03:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin04:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin05:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin06:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin07:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin08:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin09:1:9)='TOLLPASS!')
N04  c                             or (%subst(TGin10:1:9)='TOLLPASS!')
N04  c                   eval      TTTEXT = %trim(TTTEXT)+', TOLLPASS!'
N06  c                   eval      TTTEXL = %trim(TTTEXL)+', TOLLPASS!'
N04  c                   endif
N04  c                   endif
N04   *
N04  C                   ENDSR
N04   *
      **************************************************
     C     INIT          begsr
      **************************************************
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
N05   * Ukjent UserID
N05  c     *in50         ifeq      '1'
N05  C                   callp     gethtml('HTMLSRC':'*LIBL':'ESTK502':
N05  c                             '/CGITAG/')
N05  C                   callp     wrtsection('top')
N05  C                   eval      LinTxt = '#ERROR# Ukjent userID'
N05  C                   callp     updHTMLvar('LINTXT':LinTxt)
N05  C                   callp     wrtsection('lin')
N05  C                   callp     wrtsection('bot')
N05  C                   callp     wrtsection('*fini')
N05  C                   close     BRIDF
N05  C                   eval      *inlr = *on
N05  C                   return
N05  c                   endif
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      BRPARF
     C                   OPEN      HEADF
     C                   OPEN      FRISOK
     C                   OPEN      TRACKF
     C                   OPEN      TRACKT
     C                   OPEN      TKSGM
     C     HeadKey       KLIST
     C                   KFLD                    AvdN
     C                   KFLD                    OpdN
N03  C     IFBKey        KLIST
N03  C                   KFLD                    AvdN
N03  C                   KFLD                    OpdN
N03  C                   KFLD                    FSKODE
N03  C                   KFLD                    FSSOK
      *
     C     TKSGMKey      KLIST
     C                   KFLD                    HEPRO
     C                   KFLD                    Xblank            1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FiParKey      klist
     C                   kfld                    FIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   movel     '*KUNDFT*'    FIBRID           10
     C                   move      BIFIRM        FIBRID
      *
     C     DsHeaKey      KLIST
     C                   KFLD                    DsAVD
     C                   KFLD                    DsOPD
     C     HEAKY0        KLIST
     C                   KFLD                    TRAVD0
     C                   KFLD                    TROPD0
      * Finn ut om oppdraget er en del av en "DUP-struktur" - legg alle i ARRAY
     C* Hent opp ingangsoppdraget
     C     HeadKey       CHAIN(N)  HEADF                              33
      * FINN TOPP-NIVÅET
     C     TROPD0        DOWNE     0
     C     HEAKY0        CHAIN     HEADF                              33
     C                   ENDDO
     C* Legg inn topp-oppdraget som start i array
     c                   clear                   DUP
     c                   z-add     1             Nr                3 0
     c                   movel     heavd         Dup(Nr)
     c                   move      heopd         Dup(Nr)
     c     1             do        100           Cur               3 0
     c     Dup(Cur)      cabeq     *zeros        UtAdd
     c                   move      Dup(Cur)      AvdOpd
     C     DSHeaKey      CHAIN     HEADF                              33
     C  N33TrOpd1        IFNE      *ZEROS
     C                   add       1             Nr
     c                   movel     TrAvd1        Dup(Nr)
     c                   move      TrOpd1        Dup(Nr)
     C                   ENDIF
     C  N33TrOpd2        IFNE      *ZEROS
     C                   add       1             Nr
     c                   movel     TrAvd2        Dup(Nr)
     c                   move      TrOpd2        Dup(Nr)
     C                   ENDIF
     c                   enddo
     C     UtAdd         tag
      *
      * LOAD HTML
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESTK502':'/CGITAG/')
     C                   ENDSR
      *
     C***********************************************
     C     LATLON        BEGSR
     C***********************************************
     C                   eval      LatN    = 0
     C                   eval      LonN    = 0
     C                   eval      NumBig  = c2n2(LAT)
     c     NumBig        Ifge      -90
     c     NumBig        andle     90
     C                   eval      LatN    = NumBig
     c                   end
     C                   eval      NumBig  = c2n2(LON)
     c     NumBig        Ifge      -180
     c     NumBig        andle     180
     C                   eval      LONN    = NumBig
     c                   end
     C                   ENDSR
      *
      **************************************************************************
     C     UTF8I         begsr
      **************************************************************************
      * først når alle klienter er på v03.12 eller høyere kan en fjerne alt..
n09  C*    Versjon       cabge     'V03.12'      UT8
n11  C     Versjon       cabge     '03.12'       UT8
      * Midlertidig??? Mottar ikke UTF8 - kan bruke Xlate
     C     TK_ÆØÅ:ÆØÅ    XLATE     EKSTRA        EKSTRA
      * reste av rutinen benyttes ikke nå...
     C                   goto      UT8
     C                   move      'Æ'           Tegn
     C                   move      HexÆ          Hextegn
     C                   exsr      UTF8IB
     C                   move      'Ø'           Tegn
     C                   move      HexØ          Hextegn
     C                   exsr      UTF8IB
     C                   move      'Å'           Tegn
     C                   move      HexÅ          Hextegn
     C                   exsr      UTF8IB
     C                   move      'æ'           Tegn
     C                   move      HexÆl         Hextegn
     C                   exsr      UTF8IB
     C                   move      'ø'           Tegn
     C                   move      HexØl         Hextegn
     C                   exsr      UTF8IB
     C                   move      'å'           Tegn
     C                   move      HexÅl         Hextegn
     C                   exsr      UTF8IB
     C     UT8           ENDSR
      **************************************************************************
     C     UTF8IB        begsr
      **************************************************************************
      *
     c                   move      *zeros        f                 3 0
     C     FlerTegn      tag
     C     Hextegn       SCAN      EKSTRA        f
     C     f             cabeq     *zeros        UtUTF8                         ikke flere...
     C                   EVAL      EKSTRA2 = %SUBST(EKSTRA:1:f-1) +
     C                             Tegn+
     C                             %trim(%SUBST(EKSTRA:f+2:255-f))
     C                   eval      EKSTRA = EKSTRA2
     C                   goto      FlerTegn
      *
     C     UtUTF8        endsr
      *
      **************************************************
     C     SignSR        begsr
      **************************************************
     C                   eval      rc = docmd(' +
     C                             dltf FILE(QTEMP/Q)  +
     C                             ')
     C                   eval      rc = docmd(' +
     C*                            CRTPF FILE(QTEMP/Q) RCDLEN(32000) +
     C                             CRTPF FILE(QTEMP/Q) RCDLEN(30000) +
     C                             ')
     C                   OPEN      Q
     C                   EXCEPT    QData
     C                   close     Q
     c                   eval      SndL=Snd
     c     SignACTI      ifne      'POD'
     c     SignACTI      andne     '   '
     c                   eval      SndL=SignACTI+'_'+Snd
     c                   endif
     C                   eval      rc = docmd(' +
     C                             CPYTOSTMF  +
     C                             FROMMBR("/QSYS.LIB/QTEMP.LIB/Q.FILE/Q.MBR") +
     C                             TOSTMF("/syspeddev/tksigntmp/'+%trim(SndL)+
     C                             '.txt") STMFOPT(*REPLACE) +
     C                             CVTDTA(*AUTO) STMFCODPAG(*PCASCII) +
     C                             ENDLINFMT(*CRLF) +
     C                             ')
      * endre til full *RWX-tilgang (kan ikke være sikker på rettighetene
      *  til den user som har åpnet nettverksstasjon/ starter retegne-prog.
      *  ellers måtte denne ha *ALLOBJ-tilgang. dette  er nå unødig )
     C                   eval      rc = docmd(' +
     c                             CHGAUT +
     C                             OBJ("/syspeddev/tksigntmp/'+%trim(Snd)+
     C                             '.txt") USER(*PUBLIC) DTAAUT(*RWX)+
     C                             ')
     C                   ENDSR
     OQ         EADD         Qdata
     O*                      Fil              32000
     O                       Fil              30000
