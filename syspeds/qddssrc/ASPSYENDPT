     A*%%TS  SD  20100623  131718  SVEIN       REL-V5R4M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/DOKUFE)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A          R SYCTA01DA
     A*%%TS  SD  20100623  131718  SVEIN       REL-V5R4M0  5722-WDS
     A                                  1  3'SYCTRMYR'
     A                                      COLOR(BLU)
     A                                  1 15'Kontaktopplysninger'
     A                                      COLOR(WHT)
     A            ZE_TYP        20O  O  1 39COLOR(WHT)
     A                                  2 15'Ved MyPack'
     A                                      COLOR(WHT)
     A                                  4  1'Avd/Opd/Fbnr:'
     A            ZE_DFAVD  R        O  4 15REFFLD(FE_DFAVD)
     A                                      EDTCDE(4)
     A            ZE_DFOPD  R        O  4 20REFFLD(FE_DFOPD)
     A                                      EDTCDE(4)
     A            ZE_DFFBNR R        O  4 28REFFLD(FE_DFFBNR)
     A                                      EDTCDE(4)
     A                                  5 15'MOTTAGER'
     A                                      COLOR(WHT)
     A                                  6  2'Navn . . . :'
     A            WSNAVM        30A  B  6 15TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'Adresse. . :'
     A            WSAD1M        30A  B  7 15TEXT('Adresse Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  2'Postn/sted.:'
     A            WSAD3M        30A  B  8 15TEXT('P-adr Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  2'Telefon  . :'
     A            ZE_C3148A R        B  9 15REFFLD(FE_C3148A)
     A                                 10  2'Mobil  . . :'
     A            ZE_C3148B R        B 10 15REFFLD(FE_C3148B)
     A                                 11  2'Telefax  . :'
     A            ZE_C3148C R        B 11 15REFFLD(FE_C3148C)
     A                                 12  2'SMS  . . . :'
     A            ZE_C3148D R        B 12 15REFFLD(FE_C3148D)
     A                                 13  2'E-Post . . :'
     A            ZE_C3148E R        B 13 15REFFLD(FE_C3148E)
     A                                      CHECK(LC)
     A                                 15 15'TOLLPOST SERVICEPARTNER'
     A                                      COLOR(WHT)
     A                                 16  2'Navn . . . :'
     A            WSNAVL        30A  B 16 15TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  2'Adresse. . :'
     A            WSAD1L        30A  B 17 15TEXT('Leveringsadresse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2'Postn/sted.:'
     A            WSPOUL    R        B 18 15REFFLD(DOKUFR/DFPOUL *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                      EDTWRD('    ')
     A            WSADUL        25A  B 18 20TEXT('Utleveringssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2'Kundenr....:'
     A            WSKUNR        12   O 19 15COLOR(YLW)
     A                                 23  1'F3=Avslutt   F4=Søk Servicepartner-
     A                                            F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SYCTA01DB
     A                                      ASSUME
     A                                  1  4' '
