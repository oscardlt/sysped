     H DEBUG(*YES)
     Fwrkfor    IF   E           K DISK
     FARKIVP    o  a e             DISK
     FLEVEL01   if   e           K DISK
     FNAVLEV    if   e           K DISK
     FNAVAVD    if   e           K DISK
     FFRISOL01  if   e           K DISK
     FTURER14   UF   E           K DISK
     FKOSBU1    uf   e           k DISK
     FEDIRE1K   if   e           K DISK
     Fkostt     uf   e           k DISK
     Fheadf     if   e           k DISK
     F*akt      If   e           k DISK
     Ffirm      if   e           k DISK
     Ffirmna    if   e           k DISK
     Fkosta     uf a e           k DISK
     FHEAD18    IF   E           K DISK
     F                                     RENAME(HEADFR:HEA18R)
     Fkosta14   if a e           k DISK
     F                                     RENAME(kostar:kostar2)
     Fkostb     o  a e             DISK
     FVALUL01   IF   E           K DISK
     FKONTL01   IF   E           K DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  KO                     1     40  0
     D                                     DIM(8)                               I/O. frankatur
     D  NAVKO1                 1      5  0
     D  NAVKO2                 6     10  0
     D  NAVKO3                11     15  0
     D  NAVKO4                16     20  0
     D  NAVKO5                21     25  0
     D  NAVKO6                26     30  0
     D  NAVKO7                31     35  0
     D  NAVKO8                36     40  0
     D  NAVKO28                6     40  0
     D                 DS
     D  AV                     1     32  0
     D                                     DIM(8)                               I/O. frankatur
     D  NAVAV1                 1      4  0
     D  NAVAV2                 5      8  0
     D  NAVAV3                 9     12  0
     D  NAVAV4                13     16  0
     D  NAVAV5                17     20  0
     D  NAVAV6                21     24  0
     D  NAVAV7                25     28  0
     D  NAVAV8                29     32  0
     D  NAVAV28                5     32  0
     D                 DS
     D  BA                     1     32  0
     D                                     DIM(8)                               I/O. frankatur
     D  NAVBA1                 1      4  0
     D  NAVBA2                 5      8  0
     D  NAVBA3                 9     12  0
     D  NAVBA4                13     16  0
     D  NAVBA5                17     20  0
     D  NAVBA6                21     24  0
     D  NAVBA7                25     28  0
     D  NAVBA8                29     32  0
     D  NAVBA28                5     32  0
     D                 DS
     D  PR                     1     64  0
     D                                     DIM(8)                               I/O. frankatur
     D  NAVPR1                 1      8  0
     D  NAVPR2                 9     16  0
     D  NAVPR3                17     24  0
     D  NAVPR4                25     32  0
     D  NAVPR5                33     40  0
     D  NAVPR6                41     48  0
     D  NAVPR7                49     56  0
     D  NAVPR8                57     64  0
     D  NAVPR28                9     64  0
     D                 DS
     D  BUTXT                  1     35
     d  BUTXTT                 1      7
     D                 DS
     D  data                   1   1024
     d  rectyp                 1      1
     D  da                     1   1024
     D                                     DIM(1024)
     D                 DS
     D  felta                  1    120
     D  fa                     1    120
     D                                     DIM(120)
     D                 DS
     D  dta                    1  12000
     D                                     DIM(120)
     D                 DS
     D  Xtime                  1     14  0
     D  Xtm                    1      6  0
     D  Xdg                    7      8  0
     D  Xmn                    9     10  0
     D  Xar                   11     14  0
     D                 DS
     D  kadte                  1      8  0
     D  KAARE                  1      4  0
     D  KAMNE                  5      6  0
     D  KADGE                  7      8  0
     D                 DS
     D  kbrefc                 1     35
     D  fastko                 1      3
     D  kbrefcko               4      8
     D  fastav                 9     11
     D  kbrefcav              12     15
     D  fastba                16     18
     D  kbrefcba              19     22
     D  fastpr                23     25
     D  kbrefcpr              26     33
     D                 DS
     D  dato                   1     10
     D  dag                    1      2
     D  mnd                    4      5
     D  aar                    7     10
     D                 DS
     D  kafdt                  1      8  0
     D  kafaar                 1      4  0
     D  kafmnd                 5      6  0
     D  kafdag                 7      8  0
     D                 DS
     D  kaffdt                 1      8  0
     D  kaffaa                 1      4  0
     D  kaffmn                 5      6  0
     D  kaffda                 7      8  0
      *
      * INITIERER VARIABLE
      *
     C                   EXSR      INIT
      *
      * leser edire1k
      *
     c     mbr           setll     edire1k
     c     *in44         doweq     '0'
     c     mbr           reade     edire1k                                44
      *
     c     *in44         ifeq      '0'
      *
     c                   movel     rdata         data
     c                   z-add     2             fra               4 0
     c                   z-add     2             til               4 0
     c                   z-add     0             DN                4 0
     c     rectyp        ifeq      'K'
     c     til           dowlt     1024
     c                   add       1             fra               4 0
     c                   add       1             til               4 0
     C     da(til)       ifeq      ';'
     c                   add       1             fra               4 0
     c                   add       1             til               4 0
     c                   goto      videre
     c                   end
     c                   end
     c                   end
     c     videre        tag
      *
      * leser til neste skilletegn ';'
      *
     c     til           dowlt     1024
     C     da(til)       ifne      ';'
     c     til           andne     1023
     c                   add       1             til
     c                   else
      *
      * fra og til er funnet
      *
     c                   exsr      dekod
     c                   add       1             til
     c                   z-add     til           fra
     c                   endif
     c                   enddo
      *
      * Heading record går til KOSTA
      *
     c     rectyp        ifeq      'H'
      *
      * summerer moms forrige
      *
     c     lestb         ifeq      'J'
     c                   exsr      updkosta
     c                   end
     c                   exsr      kostasr
     c                   exsr      arksr
     c                   end
      *
      *
      * detalver V= varelinjer
      *
     c     rectyp        ifeq      'V'
     c                   exsr      kostbsr
     c                   end
     C     enden         tag
      *
      * tømm inleste data
      *
     c                   movea     *blanks       dta
      *
      *
      *
      * summerer moms forrige ved last record
      *
     c                   else
      *
      * ved last record
      *
     c     lestb         ifeq      'J'
     c                   exsr      updkosta
     c                   else
      * direkte regnskap
     c     doku          ifne      'T'
     c                   exsr      skrivkb
     c                   end
     c                   end
     c                   endif
     c                   enddo
      *
      *
     c                   seton                                        lr
     c                   return
     c***************************************************************
     c     dekod         begsr
     c***************************************************************
      *
      * dekoder innlest post
      *
     c     til           sub       fra           antp              4 0
     c     antp          ifgt      0
     c                   exsr      dokoda
     c                   add       1             dn
     c                   movel     felta         dta(dn)
     C                   ELSE
     c                   add       1             dn
     c                   end
     c                   endsr
     c***************************************************************
     c     dokoda        begsr
     c***************************************************************
     c*    dekoder  TIL FELTA
     c*
     c                   move      *blanks       felta
     c                   z-add     fra           nr                4 0
     c                   z-add     1             n2                4 0
     c     antp          ifgt      120
     c                   z-add     120           antp
     c                   end
     c                   do        antp
     c                   move      da(nr)        fa(n2)
     c                   add       1             n2
     c                   add       1             nr
     c                   enddo
     c                   endsr
     c***************************************************************
     c     kostasr       begsr
     c***************************************************************
     c                   move      *zeros        xxavd
     c                   move      'N'           lestb             1
     c                   move      *zeros        xmvagr           11 2
     c                   clear                   kostar
     C                   MOVE      'EDI'         KASG
     C                   MOVE      'N'           GMMVA             1
      *
      * henter unik kode  2
      *
     c                   movea     dta(2)        UNIK             70
      *
      *
      * henter dokumentkode  3
      *
     c                   movea     dta(3)        DOKU              1
     c     doku          ifeq      'T'
     c                   move      'FRA'         kavk
     c                   end
      *
      *
      * leverandørnummer 4
      *
     c                   movea     dta(4)        a
     c                   exsr      getnum
     c                   z-add     znum          kalnr
     C     KEYLEV        CHAIN     LEVEL01                            31
     C     KEYLEV        CHAIN     NAVLEV                             31
     c  N31navsg         ifne      *blanks
     C                   MOVE      NAVSG         KASG
     c                   end
      *
      *
      * invoice  5
      *
     c                   movea     dta(5)        katxt
     c     doku          ifne      'T'
     c                   move      '*L1*'        katxt
     c                   end
      *
      *
      * fakturanummer 6
      *
     c                   movel     dta(6)        kafnr
      *
      *
      * fakturadato   7
      *
     c                   movel     dta(7)        dato
     c                   move      dag           kafdag
     c                   move      mnd           kafmnd
     c                   move      aar           kafaar
     c                   move      kafaar        kapÅr
     c                   move      kafmnd        kapmn
      *
      *
      * forfallsdato  8
      *
     c                   movel     dta(8)        dato
     c                   move      dag           kaffda
     c                   move      mnd           kaffmn
     c                   move      aar           kaffaa
      *
      *
      * mva %    9
      *
     c                   movea     dta(9)        a
     c                   exsr      getnum
     c                   move      *zeros        mvapro
     c                   z-add     znum          mvaproh           3 1
     c                   move      mvaproh       mvapro            5 3
     c                   add       1             mvapro
     c                   move      'F'           kamva
     c     mvapro        ifne      0
     c                   move      'P'           kamva
     c                   end
      *
      *
      * valutakode 10
      *
     c                   movel     dta(10)       kaval
      *
      * beløp  11
      *
     c                   movea     dta(11)       a
     c                   exsr      getnum
     c                   z-sub     znum          kabl
      *
      * valutabehandling
      *
     C     KAVAL         IFNE      '   '
     C     KEYVAL        CHAIN     VALUL01                            31
     C     *IN31         IFEQ      '1'
     C                   SETON                                        95
     C                   ELSE
      * VALBELØP
     C                   Z-ADD     VALKU1        KAVKU
     C                   Z-ADD     OMRFAK        KAOMRF
     C                   END
     C                   ELSE
      * norske kroner
     C                   Z-ADD     100           KAVKU
     C                   Z-ADD     100           KAOMRF
     C                   MOVEL     'NOK'         KAVAL
     C                   END
      *
      * beregne herav mva
      *
     c                   move      *zeros        netto
     c     kamva         ifeq      'P'
      * finn netto
     c     kabl          div(h)    mvapro        netto            15 2
     c     kabl          sub       netto         kablm
     c                   end
      *
      * KID  12
      *
     c                   movea     dta(12)       kalkid
      *
      * konto 13
      *
     c                   movea     dta(13)       a
     c                   exsr      getnum
     c                   z-sub     znum          kontonr          15 0
      *
      *
      * henter bilagsnummer
      *
     c     NASERI        chain     kostt
     c                   z-add     ktnr          kabnr2
     c                   add       1             ktnr
     c                   update    kosttr
      *---------------------------------------------------------------
      *
      * henter preregnummer
      *
     c     'Ø'           chain     kostt
     c                   z-add     ktnr          kabnr
     c                   add       1             ktnr
     c                   update    kosttr
     c                   time                    xtime
     C                   MOVE      'EDI'         KAUSER
     C                   MOVE      'NAVITRO'     KAUSER
     C                   MOVE      XTM           KATME
     C                   MOVE      XDG           KADGE
     C                   MOVE      XMN           KAMNE
     C                   MOVE      XAR           KAARE
     C                   MOVE      KADTE         KAbdt
     C                   MOVE      KADTE         KADTR
     C                   MOVE      KATME         KATDR
     C                   MOVE      *ZEROS        KAJOUR
      *
      * skriver til KOSTA
      *
     c                   write     kostar
     C     endasr        tag
     c                   endsr
     c***************************************************************
     c     kostbsr       begsr
     c***************************************************************
     c                   move      'J'           lestb             1
      *
     c                   clear                   kostbr
     c                   move      *blanks       sendnr           20
     c                   move      *zeros        turnr
     c                   move      ' '           kontyp            1
      *
      *
      * FLYTTER INNREGNUMMER
      *
      *
     c                   z-add     KABNR         kBbnr
     c
     c
      *
      * turnummer 2
      *
     c                   movea     dta(2)        a
     c                   exsr      getnum
     c                   z-add     znum          turnr             8 0
     c
     c     turnr         ifne      *zeros
     c     turnr         chain     turer14                            34
     c     *in34         ifeq      '0'
     c                   move      'T'           kontyp            1
     c                   move      tuavd         xxavd             4 0
     c                   else
     c                   movel     turnr         kbkkey
     c                   exsr      feil
     c                   end
     c                   end
      *
      *
      * sendingsnummer 3
      *
     c                   movea     dta(3)        sendnr
     c
     c     sendnr        ifne      *blanks
     c                   eval      fssok=sendnr
     c     frikey        chain     frisol01                           34
     c     *in34         ifeq      '0'
     c                   move      'O'           kontyp            1
     c                   move      fsavd         kbavd
     c                   move      fsopd         kbopd
     c                   move      fsavd         xxavd             4 0
     c                   else
     c                   movel     sendnr        kbkkey
     c                   movel     sendnr        kbfree
     c                   exsr      feil
     c                   end
     c                   end
      *
      *
      * moms Y/N    4
      *
     c                   movea     dta(4)        mvayn             1
     c     mvayn         ifeq      'Y'
     C     levtyp        andne     'I'
     c                   move      'P'           KBKDPF
     c                   z-add     1             KBKDM
     c                   else
     c                   z-add     0             KBKDM
     c                   move      'F'           KBKDPF
     c                   end
      *
      *
      * BELØP  5
      *
     c                   movea     dta(5)        a
     c                   exsr      getnum
     c                   z-add     znum          KBBLHB
      * plusser på moms
     c     kamva         ifeq      'P'
     c     KBBLHB        mult      mvapro        work             15 2
     c     work          div(h)    100           mvab             11 2
     c                   add       mvab          kbblhb
     c                   add       kbblHB        xmvagr           11 2
     c                   end
      *
      *
      * i kostb skal det norske beløp være reg.
      *
     c     KAVAL         IFNE      '   '
     c     KAVAL         andne     FICURR
     c     KAOMRF        ANDNE     0
     C     KBBLHB        MULT      KAVKU         KBBLHX           15 2
     C     KBBLHX        DIV       KAOMRF        KBBLHB
     C                   END
      *
      *
      * gebyrkode 11 eller konteringsstreng
      *
     c     doku          ifeq      'T'
     c                   move      'FRA'         kbvk
     c                   else
     c                   exsr      konter
     c                   end
      *
      *
      * HENTER KUNDENUMMER
      *
     c     kontyp        ifeq      'O'
     C     HEAKEY        CHAIN     HEADF                              20
     C   20              MOVE      *ZEROS        HEAVD
     C   20              MOVE      *ZEROS        HEOPD
     C   20              seton                                        95
     C     *IN20         IFEQ      '0'
     c     hekdfk        ifne      'X'
     C                   MOVE      HEKNKF        KBKN
     c                   else
     c     hekdfs        ifne      'X'
     c                   move      heknSf        KBKN
     c                   end
     c                   end
     c                   else
     c                   z-add     9             KBNØKK
     c                   end
     c                   end
      *
      * fordeler ved turnummer mottatt
      *
     c     kontyp        ifeq      'T'
     c                   exsr      fordel
      *
      * ELLERS SKRIVES POST INTERNT TIL KOSTB
      *
     c                   else
     c                   z-add     kbavd         kbkavd
     c                   movel     kbopd         kbkkey
     c                   write     kostbr
     c                   end
     c                   endsr
     c***************************************************************
     c     skrivkb       begsr
     c***************************************************************
     c* direkte regnskapsbilag
      *
     c                   clear                   kostbr
     c                   move      ' '           kontyp            1
      *
      * LEGGER INN KONTERINGSBEGREP
      *
     C                   EXSR      KONTER
      *
      *
      * FLYTTER INNREGNUMMER
      *
     c                   z-add     KABNR         kBbnr
     c
     c
      *
      *
      * momskode
      *
     c     netto         ifne      0
     C     levtyp        andne     'I'
     c                   move      'P'           KBKDPF
     c                   z-add     1             KBKDM
     c                   else
     c                   z-add     0             KBKDM
     c                   move      'F'           KBKDPF
     c                   end
      *
      *
      * BELØP
      *
     c                   z-sub     kabl          KBBLHB
      * plusser på moms
     c     kamva         ifeq      'P'
     c                   add       kbblHB        xmvagr           11 2
     c                   end
      *
      *
      * i kostb skal det norske beløp være reg.
      *
     c     KAVAL         IFNE      '   '
     c     KAVAL         andne     FICURR
     c     KAOMRF        ANDNE     0
     C     KBBLHB        MULT      KAVKU         KBBLHX           15 2
     C     KBBLHX        DIV       KAOMRF        KBBLHB
     C                   END
      *
     c                   write     kostbr
      *
     c                   endsr
      *
      **********************************************************
     C     KONTER        BEGSR
      **********************************************************
     c                   move      '*K*'         fastko
     c                   move      '*A*'         fastav
     c                   move      '*B*'         fastba
     c                   move      '*P*'         fastpr
      *
      * KONTONUMMER
      *
     C     NAVKO1        IFNE      *zeros
     C     NAVKO28       ANDEQ     *zeros
     C                   MOVEL     NAVKO1        KBREFCKO
     c     keykont       chain     kontl01                            33
     c   33              z-add     9             kbnØkk
     C   33              MOVE      *zeros        KBREFCKO
     c                   else
     c                   z-add     9             kbnØkk
     C                   MOVE      *zeros        KBREFCKO
     c
     C                   END
      *
      *
      * AVDELING
      *
     C     NAVAV1        IFNE      *zeros
     C     NAVAV28       ANDEQ     *zeros
     C                   MOVEL     NAVAV1        KBREFCAV
     c                   else
     C                   MOVE      *zeros        KBREFCAV
     c     stedk         ifeq      'J'
     c                   z-add     9             kbnØkk
     c                   end
     C                   END
      *
      *
      * BARER
      *
     C     NAVBA1        IFNE      *zeros
     C     NAVBA28       ANDEQ     *zeros
     C                   MOVEL     NAVBA1        KBREFCBA
     c                   else
     C                   MOVE      *zeros        KBREFCBA
     c     barerk        ifeq      'J'
     c                   z-add     9             kbnØkk
     c                   end
     C                   END
      *
      *
      * PROSJEKT
      *
     C     NAVPR1        IFNE      *zeros
     C     NAVPR28       ANDEQ     *zeros
     C                   MOVEL     NAVPR1        KBREFCPR
     c                   else
     C                   MOVE      *zeros        KBREFCPR
     c     prosjk        ifeq      'J'
     c                   z-add     9             kbnØkk
     c                   end
     C                   END
      *
     C                   ENDSR
     c*****************************************
     c*    srsys         begsr
     c*****************************************
     C*                  setoff                                       34
     C*    heakey        setll     fakt
     C*    *in(34)       doweq     '0'
     C*    heakey        reade     fakt                                   34
     C*    *in(34)       ifeq      '0'
     C*    FAKDA         andne     'K'
     C*    FAOPKO        ANDGE     'C'
     c*    favk          andeq     kbvk
     c*                  add       fabeln        faksys           11 2
     c*                  end
     c*                  end
     c*                  endsr
     c*****************************************
     c     fordel        begsr
     c*****************************************
      *
     c                   move      turnr         heproa            8
     c                   move      kbblhb        tubela           13
     C                   CALL      'SYGE90'
     C                   PARM                    HEPROA
     C                   PARM                    tuBELA
      *  fordeler på enkeltlinjer
      *
     c                   setoff                                       33
     c     turnr         setll     wrkfor
     c     *in33         doweq     '0'
     c     turnr         reade     wrkfor                                 33
     c     *in33         ifeq      '0'
     c                   z-add     heavd         kbavd
     c                   z-add     heopd         kbopd
     c                   z-add     heandel       kbblhb
     c                   exsr      hentku
      *
     c                   write     kostbr
      *
      *
     c                   endif
     c                   enddo
     c                   endsr
     c*****************************************
     c     getnum        begsr
     c*****************************************
     c                   move      'N'           minus             1
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * minus
     c     A(AX)         ifeq      '-'
     c                   move      'J'           minus             1
     c                   add       1             ax
     c                   end
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     c     minus         ifeq      'J'
     c                   z-sub     znum          znum
     c                   end
     C                   ENDSR
     c
     c*****************************************
     c     FEIL          begsr
     c*****************************************
     c     turnr         ifne      *zeros
     c                   end
     c                   z-add     9             kbnØkk
     c                   write     kostBr
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     c     *entry        plist
     c                   parm                    mbr              10
     C*
     C     KEYVAL        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KAVAL
     C*
     C*
     C     KEYKONT       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    NAVKO1
     C*
     C*
     C     KEYLEV        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    kalnr
     C*
     C     frikey        KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     c                   move      'IFB'         FSKODE
      *
     c     heakey        klist
     c                   kfld                    kbavd
     c                   kfld                    kbopd
     c     budkey        klist
     c                   kfld                    xxst
     c                   kfld                    bubnr
     C                   move      ' '           XXST              1
     c                   MOVEA     *ZEROS        KO
      *
     c                   read      firm                                   34
     c     fifirm        chain     firmna                             34
     c                   time                    xtime
     C                   MOVE      XTM           KATME
     C                   MOVE      XDG           KADGE
     C                   MOVE      XMN           KAMNE
     C                   MOVE      XAR           KAARE
      *
      * GAMMEL DATO VED MOMSSKIFTE MVAK=8
      *
     c                   MOVE      *ZEROS        GMDATO
     C     KAARE         SUB       1             KAAREG            4 0
     C                   MOVEL     KAAREG        GMDATO            8 0
     C                   MOVE      1231          GMDATO
      * VARIABLE VAT
     C                   MOVE      FITAX         FITAXB            4 4
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     KADTE         ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAXB            4 4
     C                   END
     C                   ENDSR
      ********************************************************************
     C     ARKSR         BEGSR
      ********************************************************************
      * Skriv til ARKIVP
     C                   CLEAR                   ARKIVR

     C                   EVAL      ARSTAT = 'A'
     C                   EVAL      ARTYPE = NADTDO
     C                   EVAL      ARFIRM = FIFIRM
     C                   EVAL      ARAVD  = 0
     C                   EVAL      AROPD  = 0
     C                   MOVE      KABNR         KABNRA            7
     C                   EVAL      ARUNDE = 'KL:' + KABNRA
     C                   EVAL      ARUSER = 'SY400USR'
     C                   move      KADTE         ARDATE
     C                   movel     XTM           ARTIME
     C                   EVAL      ARKUND = 0
     C                   EVAL      ARLINK = UNIK

     C                   WRITE     ARKIVR

     C                   ENDSR
     c***************************************************************
     c     updkosta      begsr
     c***************************************************************
     c     kabnr         chain     kosta                              33
     c     *in33         ifne      '1'
     c     xxavd         ifne      0
     c     xxavd         chain     NAVAVD                             33
     c  n33navsg         ifne      *blanks
     c                   move      navsg         kasg
     c                   end
     c                   end
      * herav mva
     c     kamva         ifeq      'P'
     c     xmvagr        andne     0
     c     xmvagr        mult      mvapro        work             15 2
     c     work          div(h)    100           kablm
     c                   end
     c                   update    kostar
     c                   end
     c                   endsr
     C***********************************************
     C     HENTKU        BEGSR
     C***********************************************
     C     HEAKEY        CHAIN     HEADF                              20
     c     *in20         ifne      '1'
      * mottak kosnad EYESHARE
      *
     c                   move      *zeros        kbkn
      *
      *
     C     HEKDFS        IFEQ      'X'
     C     HEKDFK        ANDNE     'X'
     C                   Z-ADD     HEKNKF        kbkn
     C                   END
     C     HEKDFK        IFEQ      'X'
     C     HEKDFS        ANDNE     'X'
     C                   Z-ADD     HEKNSF        kbkn
     C                   END
      * Begge parter fins, sett etter imp/eksp
     C     HEKDFS        IFNE      'X'
     C     HEKDFK        ANDNE     'X'
      *
     C                   SELECT
     C     HEUR          WHENEQ    'H'
     C     HELKA         IFEQ      'NO'                                         EKSPORT
     C     HETRI         ANDNE     'NO'
     C                   MOVE      HEKNSF        kbkn
     C                   ELSE                                                   IMP. ELLER
     C                   MOVE      HEKNKF        kbkn                             INNLAND
     C                   END
     C     HEUR          WHENEQ    'A'
     C     HEUR          OREQ      'C'                                          IMPORT
     C     HEUR          OREQ      'F'
     C                   MOVE      HEKNKF        kbkn
     C     HEUR          WHENEQ    'B'                                          EKSPORT
     C     HEUR          OREQ      'D'
     C     HEUR          OREQ      'G'
     C                   MOVE      HEKNSF        kbkn
     C                   ENDSL
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
