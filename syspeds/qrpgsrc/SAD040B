      *
      ***********************************************************
      *** PROGRAM SKAL SPØRRE PÅ TRANSITT (TILLEGGSARK)
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 02 04 06 07 09 10 13-24
      *** LEDIGE INDIKATORER 28 29 34-37 39-90 96 97
      ***********************************************************
      *
     FHEADF     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSADTRV    IF   E           K DISK
     FSADTRV02  IF   E           K DISK
     F                                     RENAME(SADTRVR:SADTRVR2)
     FSADTRV04  IF   E           K DISK
     F                                     RENAME(SADTRVR:SADTRVR4)
     FSAD040FB  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  SVFT                   1     17
     D  SVFT2                  1     11
     D                 DS
     D  SVTOA                  1     22
     D  SVTOB                 23     45
     D  SVTOPL                 1     45
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N31
     CANN32SATV2K        SETLL     SADTRV02
     C   31
     CANN32SATV4K        SETLL     SADTRV04
     C   32SATV1K        SETLL     SADTRV
     C     START1        TAG
     C  N31
     CANN32HEADFK        READE     SADTRV02                               94
     C   31
     CANN32HEADFK        READE     SADTRV04                               94
     C   32HEADFK        READE     SADTRV                                 94
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        17
     C                   ADD       1             ZRECNO
     C  N32              MOVE      SVLN          WXLN
     C   32              MOVE      SVLI          WXLN
     C                   WRITE     SUBFIL
     C  N31
     CANN32HEADFK        READE     SADTRV02                               94
     C   31
     CANN32HEADFK        READE     SADTRV04                               94
     C   32HEADFK        READE     SADTRV                                 94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      ***
      *
     C                   SETOFF                                       9830
      *
      *** F1  ---> BYTTE AVDELINGER
     C   01              Z-ADD     1             UFLAGG
      *** F3  ---> AVSLUTTE SPØRRING
     C   03              Z-ADD     3             UFLAGG
      *** F12 ---> AVBRYT OG GÅ TILBAKE TIL DEL-1
     C   12              Z-ADD     9             UFLAGG
     C   01
     COR 03
     COR 12              GOTO      SLUTT
      *** F5  ---> VISE STARTEN AV SUBFIL
     C   05              GOTO      START
      *** F8  ---> VELGE SORTERINGSMÅTE OG START POST FOR SUBFIL
     C   08              GOTO      START7
      *** F11 ---> SE PÅ OPPDRAG
     C   11              DO
     C                   EXSR      SEOPPD
     C                   GOTO      START3
     C                   END
      *** ROLL UP ---> BLA TIL NESTE SIDE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
      *** SJEKKE OM BRUKER HAR VALGT OPSJONER. HIVS SÅ,T UTFØRE DISSE.
     C     ZRECNO        IFNE      0
     C                   EXSR      VLHENT
     C                   END
      *** 98 PÅ ---> BRUKER HAR IKKE VALGT OPSJON. VISE SAMME POSTER
      ***            I SUBFIL DVS. SAMME RECORDNR.
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** VELGE SORTERINGSMÅTE OG START POST FOR SUBFIL
      *** 31 PÅ    ---> SORTERE PÅ TARIFFNR. + LANDKODE     (SADTRV02)
      *** 32 PÅ    ---> SORTERE PÅ REGISTRERINGS REKKEFØLGE (SADTRV)
      *** 31,32 AV ---> SORTERE PÅ UTSKRIFTS REKKEFØLGE     (SADTRV04)
     C     START7        TAG
     C                   MOVE      *ZEROS        W3VNT
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C     W3LI          COMP      0                                  3232
     C  N32              DO
     C     W3VNT         COMP      0                                      31
     C                   END
     C   32              SETOFF                                       31
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     SEOPPD        BEGSR
     C***********************************************
     C*** SJEKKE OM OPPDRAG FINNES. HVIS IKKE VISE FEILMELDING.
     C*** ELLERS VISER OPPDRAGET.
     C     HEADFK        CHAIN     HEADF                              30
     C  N30              DO
     C                   Z-ADD     0             UFLAGG
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      0
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UFLAGG
     C                   END
     C                   END
      *
     C   30              MOVE      'SPO0032'     MSGNR
      *
     C                   ENDSR
      *
     C***********************************************
     C     VLHENT        BEGSR
     C***********************************************
      *** SJEKKE OM BRUKER HAR VALGT NOEN OPSJONER. HVIS IKKE, SETTER
      *** IND. 98 PÅ OG GIR FEILMELDING TIL BRUKER.
      *** OPSJON 5 ---> VISE DETALJER AV POST.
      *** LINNBR, POSNBR ---> RETUR POSISJON AV CURSOR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       17            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     17            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   SETON                                        99
     C                   WRITE     CLEARS
     C                   EXFMT     VAREL3
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SATVK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SATV1K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3LI
     C     SATV2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3VNT
     C     SATV4K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3LN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UFLAGG            1 0
     C                   MOVE      *ZEROS        UFLAGG
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   SETOFF                                       33
      *
     C                   ENDSR
      *
