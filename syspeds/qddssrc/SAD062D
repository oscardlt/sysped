     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  4VALUES(' ' '4' '2')
     A            TARIFF         8Y 0O  5 10
     A  31                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            BESKR1        31A  O  5 21
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD062R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av søketekst til tarif-
     A                                      f'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  3  2'Velg opsjon:    4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Opsjon  Tariffnr   Beskrivelse'
     A                                      COLOR(WHT)
     A                                  3 29'2=Endre'
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SOEK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2'F3=Avslutt   F5=Frisk   F6=Lag ny -
     A                                        F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 10'Tariffnr   Beskrivelse'
     A                                      COLOR(WHT)
     A            WSTANR         8Y 0B  2 10DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSBESK        31A  B  2 21
     A  89                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 10'Tariffnr   Beskrivelse'
     A                                      COLOR(WHT)
     A            TARIFF         8Y 0O  2 10DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A            BESKR1        31A  O  2 21DSPATR(UL)
     A                                      COLOR(RED)
     A                                  3  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SOEK')
     A                                  1  2'SYSPED/SAD062R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av søketekst til tarif-
     A                                      f'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  4  2'Opsjon  Tariffnr   Beskrivelse'
     A                                      COLOR(WHT)
     A                                  3  2'F3=Avslutt   F6=Lag ny   F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SOKTAR
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'Søk fra tariffnr.  --->'
     A            W2TANR         8Y 0B  1 26TEXT('TARIFFNR.')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  2  2'Søk fra tekst      --->'
     A            W2BESK        31A  I  2 26TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2'F12=Avbryt'
     A                                      COLOR(BLU)
