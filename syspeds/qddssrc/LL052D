********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 KOPIER ENDRET LINJE TIL ALLE MED SAMME PRISTYPE
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A  27
     AO 35                                  DSPATR(PR)
     A            GSLI           5  0H
     A            GSMN           7  0H
     A            GSLPT          3A  H
     A            KFTXT         50A  H
     A            GSGEB          3A  H
     A            XTEKST        34A  O  5  5
     A  35                                  DSPATR(HI)
     A            GSANT          9  2O  5 40EDTCDE(4)
     A  35                                  DSPATR(ND)
     A            GSPR           7  2O  5 51EDTCDE(4)
     A  35                                  DSPATR(ND)
     A            GSBL           9  2O  5 60EDTCDE(4)
     A  35                                  DSPATR(ND)
     A            GSMVA          1A  O  5 71
     A  35                                  DSPATR(ND)
     A            GSVAL          3A  O  5 75
     A  35                                  DSPATR(ND)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA02(02 'Vis innlegg/uttak')
     A                                      CA05(05 'Frisk')
     A N27                                  CA07(07 'BEREGN PÅ NYTT')
     A                                      CA12(12 'AVSLUTT')
     A N27                                  CA23(23 'BEKREFT')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL052R'
     A                                      COLOR(BLU)
     A N36                              1 19MSGCON(40 YBC1582 SYMSGF)
     A                                      DSPATR(HI)
     A  36                              1 19MSGCON(40 YBC1583 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(04 YBC1584 SYMSGF)
     A            YAVD           4  0O  2  7DSPATR(UL)
     A                                  2 12MSGCON(04 YBC1585 SYMSGF)
     A            YOPD           7  0O  2 17DSPATR(UL)
     A                                  2 25MSGCON(04 YBC1586 SYMSGF)
     A            YGRP           8  0O  2 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            XXGNV2        10A  O  2 39COLOR(BLU)
     A                                  2 50MSGCON(04 YBC1587 SYMSGF)
     A            YPNR           8  0O  2 55DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            XXPNV2        15A  O  2 64COLOR(BLU)
     A N27                              3  2MSGCON(50 YBC1588 SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(78 YBC1589 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                 21 48MSGCON(06 YBC1590 SYMSGF)
     A                                      DSPATR(HI)
     A            XXBLT          7  2O 21 55EDTCDE(4)
     A                                      DSPATR(HI)
     A N36N27                          23  2MSGCON(78 YBC1591 SYMSGF)
     A                                      COLOR(BLU)
     A  36N27                          23  2MSGCON(78 YBC1592 SYMSGF)
     A                                      COLOR(BLU)
     A N36 27                          23  2MSGCON(78 YBC1781 SYMSGF)
     A                                      COLOR(BLU)
     A  36 27                          23  2MSGCON(78 YBC1782 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CF04(04 'SØKING')
N01  A                                      CF10(10 'KOPIER')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(75 YBC1593 SYMSGF)
     A                                      DSPATR(HI)
     A            GSLPT          3A  O  2  5DSPATR(UL)
     A                                      DSPATR(HI)
     A            XXTXT         23A  O  2  9DSPATR(UL)
     A                                      DSPATR(HI)
     A            WSGEB          3A  B  2 33DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A            WSANT          9Y 2B  2 40DSPATR(UL)
     A N30                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            WSPR           7Y 2B  2 51DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSBL           9Y 2B  2 60DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSMVA          1A  B  2 71DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A            WSVAL          3A  B  2 75DSPATR(UL)
     A  88                                  DSPATR(RI)
     A  88                                  DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
N01  A                                  3 42'F10=Kopier til alle med samme pr.t-
N01  A                                      ype'
N01  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(75 YBC1593 SYMSGF)
     A                                      DSPATR(HI)
     A            GSLPT          3A  O  2  5DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            XXTXT         23A  O  2  9DSPATR(UL)
     A                                      DSPATR(HI)
     A            GSGEB          3A  O  2 33DSPATR(UL)
     A                                      DSPATR(HI)
     A            GSANT          9Y 2O  2 40DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            GSPR           7Y 2O  2 51DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            GSBL           9Y 2O  2 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            GSMVA          1A  O  2 71DSPATR(UL)
     A                                      DSPATR(HI)
     A            GSVAL          3A  O  2 75DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA02(02 'Vis mottak/uttak')
     A                                      CA07(07 'Ny beregning')
     A                                      CA12(12 'AVSLUTT')
     A                                      CA23(23 'BEKREFT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL052R'
     A                                      COLOR(BLU)
     A N36                              1 19MSGCON(40 YBC1582 SYMSGF)
     A                                      DSPATR(HI)
     A  36                              1 19MSGCON(40 YBC1583 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(04 YBC1584 SYMSGF)
     A            YAVD           4  0O  2  7DSPATR(UL)
     A                                  2 12MSGCON(04 YBC1585 SYMSGF)
     A            YOPD           7  0O  2 17DSPATR(UL)
     A                                  2 25MSGCON(04 YBC1586 SYMSGF)
     A            YGRP           8  0O  2 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            XXGNV2        10A  O  2 39COLOR(BLU)
     A                                  2 50MSGCON(04 YBC1587 SYMSGF)
     A            YPNR           8  0O  2 55DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            XXPNV2        15A  O  2 64COLOR(BLU)
     A                                  3  2MSGCON(50 YBC1588 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1589 SYMSGF)
     A                                      DSPATR(HI)
     A N36                             23  2MSGCON(78 YBC1591 SYMSGF)
     A                                      COLOR(BLU)
     A  36                             23  2MSGCON(78 YBC1592 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R LLEIER
     A                                      CA03(03 'AVSLUTT')
     A                                      CA12(12 'AVSLUTT')
     A                                  1  2'SYSPED/LL052R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1594 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  3  2MSGCON(12 YBC1595 SYMSGF)
     A            YAVD           4  0O  3 16DSPATR(UL)
     A                                  4  2MSGCON(12 YBC1596 SYMSGF)
     A            YOPD           7  0O  4 16DSPATR(UL)
     A                                  5  2MSGCON(12 YBC1597 SYMSGF)
     A            YGRP           8  0O  5 16DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            XXGNV         30A  O  5 26COLOR(BLU)
     A                                  6  2MSGCON(12 YBC1598 SYMSGF)
     A            HEKNSF         8  0O  6 16DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            XXPNV         30A  O  6 26COLOR(BLU)
     A                                  9  2MSGCON(16 YBC1599 SYMSGF)
     A            WSÅR           2Y 0B  9 20DSPATR(UL)
     A                                      EDTWRD('0 ')
     A                                  9 42MSGCON(16 YBC1600 SYMSGF)
     A            WSÅRS          2Y 0O  9 60DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 11  2MSGCON(16 YBC1601 SYMSGF)
     A            WSUM           2Y 0B 11 20DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A                                 11 42MSGCON(16 YBC1602 SYMSGF)
     A            WSUMS          2Y 0O 11 60DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 13  2MSGCON(16 YBC1603 SYMSGF)
     A            WSUMTY         1   B 13 20DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A                                      VALUES(' ' 'U' 'M')
     A                                 13 22MSGCON(18 YBC1604 SYMSGF)
     A                                 13 42MSGCON(16 YBC1605 SYMSGF)
     A            WSUMTS         1   O 13 60DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
