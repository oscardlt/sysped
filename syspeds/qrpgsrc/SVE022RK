     FSVEH      IF   E           K DISK
     FSVEF      IF   E           K DISK
     FSVEX      IF   E           K DISK
     FSVTVK     IF   E           K DISK
     FSVTFI     IF   E             DISK
     FSVEHE     O  A E             DISK
      *____________________
      * DS for input string
      *""""""""""""""""""""
     D                 DS
     D  A                      1     70
     D                                     DIM(70)                              Input string
     D  ADS                    1     70
      *_____________________
      * DS for output string
      *"""""""""""""""""""""
     D                 DS
     D  B                      1     80
     D                                     DIM(80)                              Output string
     D  SVEHE_DTA              1     80
      *________________________________________
      * DS for å venstrestille numeriske felter
      *""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN154                  1     15  4
     D  ØA154H                 1     11
     D  ØA154D                12     15
     D                 DS
     D  ØA16                   1     16
     D  Ø16                    1     16
     D                                     DIM(16)                              Num edit
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVEH_SYAV     UVEH_SYAV
     C     *LIKE         DEFINE    SVEH_SYOP     UVEH_SYOP
      *
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             W0074             7 0
     C                   Z-ADD     80            LEN               5 0
     C                   MOVEL     'EUNOAO  '    TABLE            10
     C*                  MOVEL     'SVGSENO '    TABLE            10
     C                   MOVEL     '*LIBL   '    LIB              10
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVEH_SYAV
     C                   KFLD                    UVEH_SYOP
      *_____________
      * Init av felt
      *"""""""""""""
N16  C                   CALL      'SYGE15C'
N16  C                   PARM                    WDT               8
N16  C                   PARM                    WTM               6
N33  C                   MOVE      WDT           WDT80             8 0
      * Workfields for Array's
     C                   Z-ADD     0             AX                2 0          Index for A
     C                   Z-ADD     1             BX                2 0          Index for B
     C                   Z-ADD     0             AXV               2 0          Leftindx A
     C                   Z-ADD     0             AXH               2 0          Rightindx A
     C                   MOVE      *BLANKS       WCHAR             1            Test character
     C                   MOVE      'Y'           WCHART            1            Test character
      * Workfields for separation character  '
     C                   BITON     '123457'      WAPSTR            1
     C                   BITOFF    '06'          WAPSTR
     C                   EVAL      SVEHE_SYA=UVEH_SYAV
     C                   EVAL      SVEHE_SYO=UVEH_SYOP
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVEH_SYAV
     C                   PARM                    UVEH_SYOP
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Start av file                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     SVEH

      *_____________
      * Huvudsektion
      *"""""""""""""
      * Meddelandeingress
     C                   EXSR      UNH00
      * Meddelandebørjan
     C                   EXSR      BGM00
      * Plats lokaliseringsidentifikation
     C                   EXSR      LOC00
      * Referenser
     C                   EXSR      RFF01
      * Transportdetaljer
     C                   EXSR      TDT04
      * Seksjonskille
     C                   EXSR      UNSD
      *______________
      * Detaljsektion
      *""""""""""""""
      * Seksjonskille
     C                   EXSR      UNSS
      *_____________
      * Summasektion
      *"""""""""""""
      * Meddelandeslut
     C                   EXSR      UNT00
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for output string, read input from left              SRÅ
      *////////////////////////////////////////////////////////////
     C     SRÅ           BEGSR
      *_________________
      * Trim left blanks
      *"""""""""""""""""
     C                   MOVEL(P)  ADS           ADS2             70
     C                   EVAL      ADS=*BLANKS
     C                   EVAL      ADS=%TRIML(ADS2)
      *_______________
      * Find positions
      *""""""""""""""""
     C                   EVAL      AXV=1
     C     ' '           CHECKR    ADS           AXH
      *________________________________________________
      * Read input string and place it in output string
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AXV           DO        AXH           AXV                            <-------------I
     C                   MOVE      A(AXV)        WCHAR                                        I
      * If invalid character (' + : ?) then replace with 'Ø'             I
     C     WCHART        IFEQ      'Y'                                           <-----------II
     C     WCHAR         IFEQ      ';'                                                      III
     C                   MOVE      ':'           WCHAR
     C                   ENDIF                                                    <---------III
     C     WCHAR         COMP      WAPSTR                                 83                III
     C  N83WCHAR         COMP      '+'                                    83                III
     C  N83WCHAR         COMP      ':'                                    83                III
     C  N83WCHAR         COMP      '?'                                    83                III
     C   83              EXSR      SRÅA                                                     III
     C                   END                                                     <-----------II
      * Move to output string                                            I
     C                   MOVE      WCHAR         B(BX)                                        I
     C                   ADD       1             BX                                           I
      * If arrayindex for output array is over 80 Then add a record      I
     C     BX            IFGT      80                                            <-----------II
     C                   CALL      'QDCXLATE'                                                II
     C                   PARM                    LEN                                         II
     C                   PARM                    SVEHE_DTA                                   II
     C                   PARM                    TABLE                                       II
     C                   PARM                    LIB                                         II
     C                   WRITE     SVEHER                                                    II
     C                   Z-ADD     1             BX                                          II
     C                   MOVE      *BLANKS       SVEHE_DTA                                   II
     C                   END                                                     <-----------II
      *                                                                  I
     C                   END                                                    <-------------I
      *
     C                   MOVE      'Y'           WCHART
     C                   EVAL      ADS=*BLANKS
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for replace control characters                      SRÅA
      *////////////////////////////////////////////////////////////
     C     SRÅA          BEGSR
      * Move '?' before the character
     C                   MOVE      '?'           B(BX)
     C                   ADD       1             BX
     C     BX            IFGT      80                                           <-------------I
     C                   CALL      'QDCXLATE'                                                II
     C                   PARM                    LEN                                         II
     C                   PARM                    SVEHE_DTA                                   II
     C                   PARM                    TABLE                                       II
     C                   PARM                    LIB                                         II
     C                   WRITE     SVEHER                                                     I
     C                   Z-ADD     1             BX                                           I
     C                   MOVE      *BLANKS       SVEHE_DTA                                    I
     C                   END                                                    <-------------I
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA16
     C     ØA154D        IFEQ      '0000'                                       <-------------I
     C                   MOVE      ØA154H        ØA16                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA154H        ØA16                                         I
     C                   MOVE      '.    '       ØA16                                         I
     C                   MOVE      ØA154D        ØA16                                         I
     C     Ø16(16)       IFEQ      '0'
     C                   MOVE      ' '           Ø16(16)
     C     Ø16(15)       IFEQ      '0'
     C                   MOVE      ' '           Ø16(15)
     C                   END
     C                   END
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        15            ØX                             <-------------I
     C     Ø16(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø16(ØX)       ØAN
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Meddelandeingress                                  UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
      * special, starta med en apostrof, tas bort senare
     C*                  MOVEL     WAPSTR        ADS
     C*                  MOVE      'N'           WCHART
     C*                  EXSR      SRÅ
      * Segmentnavn
      * W0074  --->  Teller for antall segment
     C                   Z-ADD     1             W0074
     C                   MOVEL     'UNH+'        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Meldingsreferanse
     C                   MOVEL     SVEH_SYAV     W0062            11
     C                   MOVE      SVEH_SYOP     W0062
     C                   MOVEL     W0062         ADS
     C                   EXSR      SRÅ
      *
     C                   MOVEL     '+'           ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Meldingstype  (= CUSDEC)
     C                   MOVEL     'CUSDEC'      ADS
     C                   EXSR      SRÅ
      *
     C                   MOVEA     ':D:96B:U'    A(1)
     C                   MOVEA     'N:145050'    A(9)
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Slutt segment
     C                   MOVEL(P)  WAPSTR        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Meddelandebørjan                                   BGM00 /
      *////////////////////////////////////////////////////////////
     C     BGM00         BEGSR
      * Segmentnavn
     C                   ADD       1             W0074
     C                   EVAL      ADS='BGM+'+SVEH_MTYP+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Plats/lokaliseringsidentifikation                  LOC00 /
      *////////////////////////////////////////////////////////////
     C     LOC00         BEGSR
      *_____________
      * Godslokalkod
      *"""""""""""""
     C     SVEH_GOLK     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='LOC+18+'+SVEH_GOLK+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
      *__________________
      * Utfartstullkontor
      *""""""""""""""""""
     C     SVEH_UTFA     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='LOC+17+'+SVEH_UTFA+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
      *______________
      * Lastplatskode
      *""""""""""""""
     C     SVEH_LAST     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='LOC+9+'+SVEH_LAST+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Seksjonskille                                       UNSD /
      *////////////////////////////////////////////////////////////
     C     UNSD          BEGSR
      * Segmentnavn
      * W0074  --->  Teller for antall segment
     C                   ADD       1             W0074
     C                   MOVEL     'UNS+D'       ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Slutt segment
     C                   MOVEL     WAPSTR        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Seksjonskille                                       UNSS /
      *////////////////////////////////////////////////////////////
     C     UNSS          BEGSR
      * Segmentnavn
      * W0074  --->  Teller for antall segment
     C                   ADD       1             W0074
     C                   MOVEL     'UNS+S'       ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Slutt segment
     C                   MOVEL     WAPSTR        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Referens                                           RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
      *_______
      * Tullid
      *"""""""
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+ZTI:'+%trimr(SVEH_TUID)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *________________________________________
      * Ytterligare referensnummer (godsnummer)
      *""""""""""""""""""""""""""""""""""""""""
     C     SVEH_RFAC     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+ACD:'+%trimr(SVEH_RFAC)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportdetaljer                                  TDT04 /
      *////////////////////////////////////////////////////////////
     C     TDT04         BEGSR
      *______________________________
      * Typ av inrikes transportmedel
      *""""""""""""""""""""""""""""""
     C     SVEH_TRIN     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='TDT+1++'+%trimr(SVEH_TRIN)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
      *_____________________________
      * Typ av aktivt transportmedel
      *"""""""""""""""""""""""""""""
     C     SVEH_TRGR     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='TDT+11++'+%trimr(SVEH_TRGR)+'+++++:::'
     C                             + ':'+%trimr(SVEH_TRAL)
     C                             + WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Meldingsslutt                                      UNT00 /
      *////////////////////////////////////////////////////////////
     C     UNT00         BEGSR
      * Segmentnavn
     C                   ADD       1             W0074
     C                   MOVEL     'UNT+'        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Antall segmenter i meldingen (inkludert UNH og UNT)
     C                   EVAL      ØN154=W0074
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           ADS
     C                   EXSR      SRÅ
      *
     C                   MOVEL     '+'           ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Meldingsreferanse
     C                   MOVEL     W0062         ADS
     C                   EXSR      SRÅ
      * Slutt segment
     C                   MOVEL     WAPSTR        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *
     C     BX            IFGT      1
     C                   CALL      'QDCXLATE'                                                II
     C                   PARM                    LEN                                         II
     C                   PARM                    SVEHE_DTA                                   II
     C                   PARM                    TABLE                                       II
     C                   PARM                    LIB                                         II
     C                   WRITE     SVEHER
     C                   ENDIF
      *
     C                   ENDSR
