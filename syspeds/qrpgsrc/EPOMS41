********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 OPPDATER REASON CODE PÅ EPOH OG EPOL
********************************************************************
      * Når Oppdrags ETA, ATA mm endres i eCare eller annet(grønskjerm)
      *     Oppdater status2->S send statusmelding til ...
      * samtidig settes 'hovedstaus' til 2,3,4, el 5 avhengig av hvor langt i kjeden Oppdraget er
     FEPOH02    UF   E           K DISK
     FEPOL02    UF   E           K DISK
     FBLV1      IF   E           K DISK
N01  D                 DS
N01  D  UUDS2                  1    256
N01  D  U2REAS                 1      3
      *
     C                   EXSR      INIT
      *
      * minimumm status = 2 booked (=på oppdrag)
      * Dersom MERKE/NR (CONT.NR) SATT - STAUS FRA 2 BOOKED TIL 3 CONFIRMED
      *
      * Hele PO-headers knyttet til oppdraget
     C     HeaKey        SETLL     EPOH02
     C     HeaKey        READE     EPOH02                                 33
     C     *in33         doweq     '0'
     c     BVMN          ifeq      *blanks
     c                   movel     '2    '       EHSTA                          Booked
     c                   else
     c                   movel     '3    '       EHSTA                          Confirmed
     c                   endif
     c     UUATD         ifne      *zeros
     c                   movel     '4    '       EHSTA                          Shipped
     c                   end
     c     UUATA         ifne      *zeros
     c                   movel     '5    '       EHSTA                          Arrived
     c                   end
     C                   MOVE      'S'           EHSTA2
N01  C                   IF        U2REAS <> *BLANKS
N01  C                   EVAL      EHREAS = U2REAS
N01  C                   END
     C                   Update    EPOHR
     C     HeaKey        READE     EPOH02                                 33
     C                   enddo
      *
      * Løse PO-lines knyttet til oppdraget
     C     HeaKey        SETLL     EPOL02
     C     HeaKey        READE     EPOL02                                 33
     C     *in33         doweq     '0'
     c     BVMN          ifeq      *blanks
     c                   movel     '2    '       ELSTA                          Booked
     c                   else
     c                   movel     '3    '       ELSTA                          Confirmed
     c                   endif
     c     UUATD         ifne      *zeros
     c                   movel     '4    '       ELSTA                          Shipped
     c                   end
     c     UUATA         ifne      *zeros
     c                   movel     '5    '       ELSTA                          Arrived
     c                   end
     C                   MOVE      'S'           ELSTA2
N01  C                   IF        U2REAS <> *BLANKS
N01  C                   EVAL      ELREAS = U2REAS
N01  C                   END
     C                   Update    EPOLR
     C     HeaKey        READE     EPOL02                                 33
     C                   enddo
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUAVD             4 0
     C                   PARM                    UUOPD             7 0
     C                   PARM                    UUETD             8 0
     C                   PARM                    UUETA             8 0
     C                   PARM                    UUATD             8 0
     C                   PARM                    UUATA             8 0
     C                   PARM                    UUDS            256
      * Tar med ALLE datoene for å forenkle dersom de i framtid skal oppdateres på EHOH/L
      * (men kun ATD/ATA er relevante pr sept 2012..)
      * tar også med et alfafelt for å forenkle tillegsparametre i fall behov (reason codes mm)
     C     HeaKey        klist
     C                   kfld                    UUAVD
     C                   kfld                    UUOPD
      * hent merke/Nr (cont-nr)
     C                   MOVE      *BLANKS       BVMN
     C     HeaKey        CHAIN     BLV1
     C     BVMN          ifeq      '?'
     C                   MOVE      *BLANKS       BVMN
     C                   endif
N01  C                   EVAL      UUDS2 = UUDS
      *
     C                   endsr
      *
