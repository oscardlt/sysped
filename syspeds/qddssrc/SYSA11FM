     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTLK)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            KLKLK     R    2   O  6  9
     A            KLKNVN    R   30   O  6 18
     A            KLKALF    R    2   O  6 49
     A            KLKLK2    R    2   O  6 62
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 25'Vedlikehold av alfa landkode'
     A                                      DSPATR(HI)
     A                                  3  2'Velg option, trykk Enter. '
     A                                      COLOR(BLU)
     A                                  4  5 '2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  5  2'Option Landkode Fullt navn på land-
     A                                                   Nasjonalitet Alfalandk-
     A                                      ode'
     A                                      DSPATR(HI)
     A          R BUNN
     A                                 23 10'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                 23 30'F5=Frisk'
     A                                      COLOR(BLU)
     A                                 23 50'F6=Lage ny'
     A                                      COLOR(BLU)
     A                                 23 70'F8=Søk'
     A                                      COLOR(BLU)
     A  98                             24 10'Du må velge riktig option'
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2'       Landkode Fullt navn på land-
     A                                                   Nasjonalitet Alfalandk-
     A                                      ode'
     A            WSLK           2   B  2  9DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40                                  DSPATR(PC)
     A  70                                  ERRMSG('Landkode kan ikke være blan-
     A                                      k.' 70)
     A            WSNVN         30   B  2 18DSPATR(UL)
     A  40                                  DSPATR(PC)
     A  69                                  ERRMSG('Navn på land må tastes.' 69)
     A            WSALF          2   B  2 49DSPATR(UL)
     A            WSLK2          2   B  2 62DSPATR(UL)
     A                                  3 10'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                  3 30'F12=Forrige'
     A                                      COLOR(BLU)
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2'Søk fra -->'
     A            W2LK           2   I  2 14DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3 10'F3=Avslutt'
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2'       Landkode Fullt navn på land-
     A                                                   Nasjonalitet Alfalandk-
     A                                      ode'
     A            KLKLK     R    2   O  2  6DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            KLKNVN    R   30   O  2 18DSPATR(UL)
     A                                      DSPATR(HI)
     A            KLKALF    R    2   O  2 49DSPATR(UL)
     A                                      DSPATR(HI)
     A            KLKLK2    R    2   O  2 62DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 30'F12=Forrige'
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1 25'Vedlikehold av alfa landkode'
     A                                      DSPATR(HI)
     A                                  3  2'Velg option, trykk Enter. '
     A                                      COLOR(BLU)
     A                                  4  5 '2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  5  2'Option Landkode Fullt navn på land-
     A                                                   Nasjonalitet Alfalandk-
     A                                      ode'
     A                                      DSPATR(HI)
     A                                 23 10'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                 23 30'F5=Frisk'
     A                                      COLOR(BLU)
     A                                 23 50'F6=Lage ny'
     A                                      COLOR(BLU)
     A                                 23 70'F8=Søk'
     A                                      COLOR(BLU)
