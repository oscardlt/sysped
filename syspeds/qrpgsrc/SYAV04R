     FSYAV02F   IF   E           K DISK
     FDOKEF     IF   E           K DISK
     FDOKEM     IF   E           K DISK
     FKODTFS    IF   E           K DISK
     FFAK2      IF   E           K DISK
     FSYAV04P   O    E             PRINTER OFLIND(*IN98)
      *__________________
      * Array for samlast
      *""""""""""""""""""
     D EAR             S              6    DIM(999)
      *_____________
      * DS for DOKEM
      *"""""""""""""
     D*HESAML ER FLYTTET TIL HERFA I FORRIGE PROGRAM
     D                 DS
     D  HERFA                  1     15
     D  HERFA6                 1      6
     D  WFSD                   1      3
     D  WFLNR                  4      6  0
     D                 DS
     D  HEGN                   1     15
     D  HEGN11                 5     15
     D                 DS
     D  HEDTOP                 1      8  0
     D  XEDTOP                 3      8  0
      *___________________________
      * DS for fraktberegningsvekt
      *"""""""""""""""""""""""""""
     D                 DS
     D  FV                     1     42  1
     D                                     DIM(6)
     D  DFFBV1                 1      7  1
     D  DFFBV2                 8     14  1
     D  DFFBV3                15     21  1
     D  DFFBV4                22     28  1
     D  DFFBV5                29     35  1
     D  DFFBV6                36     42  1
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    HEAVD         WEAVD
     C     *LIKE         DEFINE    HEOPD         WEOPD
     C     *LIKE         DEFINE    HEFLY         WEFLY
     C     *LIKE         DEFINE    DFRI          WFRI
     C     *LIKE         DEFINE    DFRI          WFRIC
     C     *LIKE         DEFINE    HEGN          WEGN
     C     *LIKE         DEFINE    FASK          WASK
     C     *LIKE         DEFINE    KFSUNI        WFSUNI
     C                   Z-ADD     0             WE                3 0
     C                   Z-ADD     0             WFV01
      *__________________
      * Definiere nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST                                                  SYAV02F
     C                   KFLD                    WEGN
     C     KEYB          KLIST                                                  DOKEF
     C                   KFLD                    WFRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KEYC          KLIST
     C                   KFLD                    WFRIC                          DOKEM
     C                   KFLD                    WFSD
     C                   KFLD                    WFLNR
     C     KEYD          KLIST                                                  KODTFS
     C                   KFLD                    WFSUNI
     C                   KFLD                    HEFLY
     C     KEYE          KLIST                                                  FAK2
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    WASK
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     ZAVDF         PARM                    ZAVDF
     C     ZAVDT         PARM                    ZAVDT
     C     ZFLYF         PARM                    ZFLYF
     C     ZFLYT         PARM                    ZFLYT
     C     ZDTF          PARM                    ZDTF
     C     ZDTT          PARM                    ZDTT
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'F'           WFRI
     C                   MOVE      'M'           WFRIC
     C                   MOVE      'FS'          WFSUNI
     C                   MOVE      'F'           WASK
     C                   MOVE      *BLANKS       WEGN
     C                   Z-ADD     999           WEFLY
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv ut                                             SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_________________
      * Skriv ut forside
      *"""""""""""""""""
     C                   WRITE     SYAV04PA
      *________________________
      * Les poster fra workfile
      *""""""""""""""""""""""""
     C     KEYA          SETLL     SYAV02F
     C                   READ      SYAV02F                                51
     C     *IN51         DOWEQ     '0'
      *______________________
      * Brudd på flyselskap ?
      *""""""""""""""""""""""
     C     HEFLY         CASNE     WEFLY         SRBA
     C                   END
      *___________
      * Overflow ?
      *"""""""""""
     C     *IN98         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   WRITE     SYAV04PB
     C                   END
      *_________
      * Detaljer
      *"""""""""
     C                   EXSR      SRBB
      *
     C                   READ      SYAV02F                                51
     C  N51              END
      *____________
      * Last Record
      *""""""""""""
     C                   EXSR      SRBA
     C                   WRITE     SYAV04PØ
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Totaltid                                            SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      * Skriv ut totaler for gammelt flyselskap, ikke første gang
     C     *IN61         IFEQ      '1'
      *
     C     WCCL1         ADD       WPPL1         WCPL1
     C     WCPL1         MULT      KFSKOM        WKML1
     C                   ADD       WPPL1         WTTL1
     C                   SUB       WKML1         WTTL1
     C                   SUB       WFBL1         WTTL1
     C                   ADD       WSSL1         WTTL1
      *
     C                   Z-ADD     WPPL1         WPPL1X
     C                   Z-ADD     WFBL1         WFBL1X
     C                   Z-ADD     WSSL1         WSSL1X
      *
     C                   WRITE     SYAV04PD
      *
     C                   Z-ADD     0             WTTL1
     C                   Z-ADD     0             WANL1
     C                   Z-ADD     0             WNTL1
     C                   Z-ADD     0             WVEL1
     C                   Z-ADD     0             WFVL1
     C                   Z-ADD     0             WCCL1
     C                   Z-ADD     0             WPPL1
     C                   Z-ADD     0             WCPL1
     C                   Z-ADD     0             WKML1
     C                   Z-ADD     0             WFBL1
     C                   Z-ADD     0             WSSL1
      *
     C                   END
      * Skriv ut heading for nytt flyselskap
     C     *IN51         IFEQ      '0'
     C     KEYD          CHAIN     KODTFS                             52
     C     *IN52         IFEQ      '1'
     C                   MOVE      *BLANKS       KFSNAV
     C                   Z-ADD     0             KFSKOM
     C                   Z-ADD     0             KFSMC
     C                   END
     C                   MOVE      '1'           *IN61
     C                   Z-ADD     HEFLY         WEFLY
     C                   Z-ADD     0             WE
     C                   MOVE      *BLANKS       EAR
     C                   WRITE     SYAV04PB
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Detaljtid                                           SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
     C                   MOVEA     '00'          *IN(30)
      *________________________
      * Bearbeide singeloppdrag
      *""""""""""""""""""""""""
     C     HERFA6        IFEQ      *BLANKS
      *
     C     KEYB          CHAIN     DOKEF                              52
     C   52              GOTO      SRBB9
      *
     C                   MOVE      '1'           *IN30
     C     DFCCPP        IFEQ      'CC'
     C                   Z-ADD     DFBLT         WCC01
     C                   Z-ADD     0             WPP01
     C                   Z-ADD     0             WMCBEL
     C                   ELSE
     C                   Z-ADD     DFBLT         WPP01
     C                   Z-ADD     0             WCC01
     C                   Z-ADD     KFSMC         WMCBEL
     C                   END
      *
     C                   EXSR      SRBBA
      *
     C                   ADD       WCC01         WCCL1
     C                   ADD       WPP01         WPPL1
     C                   ADD       DFNTT         WNTL1
     C                   ADD       DFVKTT        WVEL1
     C     DFRC1         IFEQ      'B'
     C     DFRC2         ANDEQ     'K'
     C                   Z-ADD     0             DFFBV2
     C                   ENDIF
     C                   XFOOT     FV            WFV01
     C                   ADD       WFV01         WFVL1
     C                   ADD       WFB01         WFBL1
     C                   ADD       WMCBEL        WSSL1
     C*
      *__________________
      * Bearbeide samlast
      *""""""""""""""""""
     C                   ELSE
      *
     C                   SORTA     EAR
     C     HERFA6        LOOKUP    EAR                                    53
     C  N53KEYC          CHAIN     DOKEM                              53
     C   53              GOTO      SRBB9
      *
     C                   MOVEA     '11'          *IN(30)
     C                   ADD       1             WE
     C                   MOVE      HERFA6        EAR(WE)
     C     DFCCPP        IFEQ      'CC'
     C                   Z-ADD     DFBLT         WCC01
     C                   Z-ADD     0             WPP01
     C                   Z-ADD     0             WMCBEL
     C                   ELSE
     C                   Z-ADD     DFBLT         WPP01
     C                   Z-ADD     0             WCC01
     C                   Z-ADD     KFSMC         WMCBEL
     C                   END
      *
     C                   EXSR      SRBBA
      *
     C                   ADD       WCC01         WCCL1
     C                   ADD       WPP01         WPPL1
     C                   ADD       DFNTT         WNTL1
     C                   ADD       DFVKTT        WVEL1
     C     DFRC1         IFEQ      'B'
     C     DFRC2         ANDEQ     'K'
     C                   Z-ADD     0             DFFBV2
     C                   ENDIF
     C                   XFOOT     FV            WFV01
     C                   ADD       WFV01         WFVL1
     C                   ADD       WFB01         WFBL1
     C                   ADD       WMCBEL        WSSL1
      *
     C                   END
     C                   ADD       1             WANL1
     C                   WRITE     SYAV04PC
     C     SRBB9         ENDSR
      *//////////////////////////////////////////////////////////
      * SR FOR BEARBEDNING AV 01 DETALJ   B                 SRBBA
      *//////////////////////////////////////////////////////////
     C     SRBBA         BEGSR
      *__________________________________
      * Addere varebeløp (kun status='F')
      *""""""""""""""""""""""""""""""""""
     C                   Z-ADD     0             WFB01
     C     KEYE          CHAIN     FAK2                               53
     C     *IN53         DOWEQ     '0'
      *
     C     FAVK          IFNE      'FFE'
     C     FAVK          ANDNE     'FC '
     C     FAVK          ANDNE     'MC '
     C                   ADD       FABELN        WFB01
     C                   END
     C     KEYE          READE     FAK2                                   53
     C  N53              END
     C                   ENDSR
