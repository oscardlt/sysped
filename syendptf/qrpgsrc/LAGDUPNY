      * basert på V11 - LAGDUP, men mange nye parameter mm - så lager nytt prog
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 MANGE NYE PARAMETRE  mm
12XXX * 02 JOV PTF12 innfører også skaping av Rekvisisjon
********************************************************************
N02
     FHEADF     UF A E           K DISK
     FDISP      O  A E             DISK
     FHESTED    O  A E             DISK
     FFRISOK    O  A E             DISK
     FFREETXT   IF A E           K DISK
     FFAKT      O  A E             DISK
     FDOKUFM    O  A E             DISK
     FTRACKF    O  A E             DISK
     FTRACKT    O  A E             DISK
n02  FHETRIU    UF A E           K DISK
n02  FKOSTT     UF A E           K DISK
n02  FKOSBU     UF A E           K DISK
n02  FTURER14   IF   E           K DISK
n02  FTRAN      IF   E           K DISK
     FTELL      UF   E           K DISK
     FKODTA     IF   E           K DISK
     FNOPNR     IF   E           K DISK
     FKODTRI    IF   E           K DISK
     FSTED2D    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFIRINL    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     D                 DS
     D  HXHEAD                 1    256
     D  XXUR                   1      1
     D  XXFR                   2      4
     D  XXKDPL                 5      5
     D  XXLKA                  6      7
     D  XXSDF                  8     12
     D  XXTRI                 13     14
     D  XXSDT                 15     19
     D  XXLKS                 20     21
     D  XXSDFF                22     26
     D  XXLKK                 27     28
     D  XXSDVT                29     33
     D  XXVKT                 34     42  0
     D  XXFBV                 43     51  0
     D  XXM3                  52     58  3
     D  XXLM                  59     62  2
     D  XXNT                  63     69  0
     D  XXVERV                70     72
     D  XXVERB                73     75  2
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
N02  D                 DS
N02  D  HEDTÅ                  1      4  0
N02  D  HEDTM                  5      6  0
N02  D  HEDTD                  7      8  0
N02  D  HEDTOP                 1      8  0
      *
     c                   exsr      INIT
      * ved feil - hopp ut
     c     DUPIND        cabeq     '1'           Slutt
      *
      *
      * OK!!
      * Lag DUP ......
N02  c                   if        NyAvd <> *zeros
     c     DupFFVF       ifeq      'FF'
     c                   exsr      LAGFF
     c                   else
     c                   exsr      LAGVF
     c                   endif
     c                   if        ErrMsg = *blanks
     c                   exsr      SkrivDup
     c                   endif
N02  c                   else
N02  c                   exsr      LagRekv
N02  c                   endif
      *
     C     Slutt         tag
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *
     C***********************************************
     C     LAGFF         BEGSR
     C***********************************************
      * Innhenting! Indiker at VIA på fraside (DUP) ER utført  på mor-oppdraget
      *   hent avd/oppdr.nr for innh.dup
     C                   MOVE      'O'           TRSTAF
     C                   MOVEL     DUPTELK       HELKS
     C                   MOVEL     DUPTERM       HESDFF
     C                   MOVE      TRSDFD        TRSFFD
      * Hoved-oppd må referere til Duppens avd/opd (nedadgående ref)
     c                   IF        NYAVD <> 0
     C                   Z-ADD     NYAVD         TRAVD1
     c                   ELSE
      * Finn avd basert på frasted v henting
     C                   MOVEL     HEADS3        TSTPNR            4
     C                   TESTN                   TSTPNR               33
     C  N33              MOVEL     HESDF         TSTPNR            4
     C  N33              TESTN                   TSTPNR               33
     C     *IN33         IFEQ      '1'
     c                   exsr      GetAvd
     c                   endif
     C                   Z-ADD     RIAVDI        TRAVD1                         RIAVDI=Inngående
     C                   Z-ADD     RIAVDI        NYAVD
     c                   ENDIF
      *
     C                   MOVE      *ZEROS        TROPD1
     C     TRAVD1        CHAIN     TELL                               33
     C   33AVDZER        CHAIN     TELL                               33
     c                   if        *in33 = '1'
     c                   eval      ErrMsg = 'Avdeling '+ %char(NYAVD)
     c                                    + ' er ugyldig.'
     c                   goto      UtLagFF
     c                   endif
     C                   MOVE      TEOPDN        NYOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   MOVE      NYOPD         TROPD1
      *
      * Lagre endringer i Mor-oppdraget
     C                   Update    HEADFR
      *
      * KUNDE FOR DUPFRAKT
     C                   MOVE      *ZEROS        DUPKNR            8 0
     C                   MOVE      NYAVD         YYAVD
     C     KODTAK        CHAIN     KODTA                              33
     C  N33              MOVE      KOAKNR        DUPKNR
      * INNHENTINGS DUP-OPPDRAGET, ENDRE VISSE DATA
     C                   MOVE      'DUP'         HESG
     C                   MOVEL     SIGN          HELB
     C                   MOVE      ' '           HEST
     C                   MOVE      ' '           TRSTAF
     C                   MOVE      ' '           TRSTAE
     C                   MOVE      *ZEROS        HEKNA                           AGENT
      * VIA-data fra hovedoppdraget blankes - Tilsted=terminal
     C*DUP-OPPDRAGET FÅR ALLTID PREFIX 402 FOR Å SKILLES FRA HOVED.OPPD
      * (om det er Tr-modul innland -  ellers tildel innlands sendingsnr)
     c                   if        KOAIE='1' or KOAIE='2'
     c                   If        HXsndn <> *blanks
     C                   MOVEL     '402'         HXSNDN
     c                   else
     C                   CALL      'SYGE15'
     C                   PARM                    HXSNDN
     c                   endif
     C                   MOVE      *ZEROS        HXPALL
     C     HEVKT         IFLE      IFOTVG
     C                   MOVEL     IFOTFF        HEOT
     C                   ELSE
     C                   MOVEL     IFO2FF        HEOT
     C                   END
     C                   MOVE      IFFRFF        HEFR
     c                   endif
     C                   MOVE      *ZEROS        HEKNKF
     C                   MOVEL     'X'           HEKDFK
     C                   MOVEL     ' '           HEKDFS
     C                   MOVEL     'S'           TRKDFF
     C                   MOVE      DUPKNR        HEKNSF
     C                   MOVE      DUPKNR        KUNDNR
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              MOVE      KNAVN         HENASF
      *
     C                   MOVE      *ZEROS        HEPRO
     C                   MOVE      *BLANKS       HEHAWB
     C                   MOVE      *ZEROS        TRRUND
     C                   MOVE      *BLANKS       TRSTAF
     C                   MOVE      *BLANKS       TRSTAE
     C                   MOVE      *BLANKS       HELKS
     C                   MOVE      *BLANKS       HESDFF
     C                   MOVE      *ZEROS        TRSFFD
     C                   MOVE      *BLANKS       HELKK
     C                   MOVE      *BLANKS       HESDVT
     C                   MOVE      *ZEROS        TRSVTD
     C                   MOVEL     'NO'          HETRI
     C                   MOVE      *BLANKS       HESDT
     C                   MOVEL     DUPTERM       HESDT
     C                   MOVEL     'FF'          TRSLAG
      * REF TIL OVERORNET OPPDRAG...
     C                   MOVE      HEAVD         TRAVD0
     C                   MOVE      HEOPD         TROPD0
      * DUP.OPPDRAGETS AVD/OOPDR.NR
     C                   MOVE      TRAVD1        HEAVD
     C                   MOVE      TROPD1        HEOPD
     C     UTlagFF       ENDSR
     C***********************************************
     C     LAGVF         BEGSR
     C***********************************************
      * Utkjøring! Indiker at VIA på Tilside (DUP) ER utført  på mor-oppdraget
      *   hent avd/oppdr.nr for innh.dup
     C                   MOVE      'O'           TRSTAE
     C                   MOVEL     DUPTELK       HELKK
     C                   MOVEL     DUPTERM       HESDVT
     C                   MOVE      TRSDTD        TRSVTD
      * Hoved-oppd må referere til Duppens avd/opd (nedadgående ref)
     c                   IF        NYAVD <> 0
     C                   Z-ADD     NYAVD         TRAVD2
     c                   ELSE
      * Finn avd basert på tilsted v utkjøring
     C                   MOVEL     HEADK3        TSTPNR            4
     C                   TESTN                   TSTPNR               33
     C  N33              MOVEL     HESDT         TSTPNR            4
     C  N33              TESTN                   TSTPNR               33
     C     *IN33         IFEQ      '1'
     c                   exsr      GetAvd
     c                   endif
     C                   Z-ADD     RIAVDU        TRAVD2                         RIAVDI=Inngående
     C                   Z-ADD     RIAVDU        NYAVD
     c                   ENDIF
      *
     C                   MOVE      *ZEROS        TROPD2
     C     TRAVD2        CHAIN     TELL                               33
     C   33AVDZER        CHAIN     TELL                               33
     c                   if        *in33 = '1'
     c                   eval      ErrMsg = 'Avdeling '+ %char(NYAVD)
     c                                    + ' er ugyldig.'
     c                   goto      UtLagVF
     c                   endif
     C                   MOVE      TEOPDN        NYOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   MOVE      NYOPD         TROPD2
      *
      * Lagre endringer i Mor-oppdraget
     C                   Update    HEADFR
      *
      * KUNDE FOR DUPFRAKT
     C                   MOVE      *ZEROS        DUPKNR            8 0
     C                   MOVE      TRAVD2        YYAVD
     C     KODTAK        CHAIN     KODTA                              33
     C  N33              MOVE      KOAKNR        DUPKNR
      * INNHENTINGS DUP-OPPDRAGET, ENDRE VISSE DATA
     C                   MOVE      'DUP'         HESG
     C                   MOVEL     SIGN          HELB
     C                   MOVE      ' '           HEST
     C                   MOVE      ' '           TRSTAF
     C                   MOVE      ' '           TRSTAE
     C                   MOVE      *ZEROS        HEKNA                           AGENT
      * VIA-data fra hovedoppdraget blankes - Tilsted=terminal
     C*DUP-OPPDRAGET FÅR ALLTID PREFIX 402 FOR Å SKILLES FRA HOVED.OPPD
      * (om det er Tr-modul innland -  ellers tildel innlands sendingsnr)
     c                   if        KOAIE='1' or KOAIE='2'
     c                   If        HXsndn <> *blanks
     C                   MOVEL     '402'         HXSNDN
     c                   else
     C                   CALL      'SYGE15'
     C                   PARM                    HXSNDN
     c                   endif
     C                   MOVE      *ZEROS        HXPALL
     C     HEVKT         IFLE      IFOTVG
     C                   MOVEL     IFOTFF        HEOT
     C                   ELSE
     C                   MOVEL     IFO2FF        HEOT
     C                   END
     C                   MOVE      IFFRFF        HEFR
     c                   endif
     C                   MOVE      *ZEROS        HEKNKF
     C                   MOVEL     'X'           HEKDFK
     C                   MOVEL     ' '           HEKDFS
     C                   MOVEL     'S'           TRKDFF
     C                   MOVE      DUPKNR        HEKNSF
     C                   MOVE      DUPKNR        KUNDNR
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              MOVE      KNAVN         HENASF
      *
     C                   MOVE      *ZEROS        HEPRO
     C                   MOVE      *BLANKS       HEHAWB
     C                   MOVE      *ZEROS        TRRUND
     C                   MOVE      *BLANKS       TRSTAF
     C                   MOVE      *BLANKS       TRSTAE
     C                   MOVE      *BLANKS       HELKS
     C                   MOVE      *BLANKS       HESDFF
     C                   MOVE      *ZEROS        TRSFFD
     C                   MOVE      *BLANKS       HELKK
     C                   MOVE      *BLANKS       HESDVT
     C                   MOVE      *ZEROS        TRSVTD
     C                   MOVEL     'NO'          HETRI
     C                   MOVE      *BLANKS       HESDF
     C                   MOVEL     DUPTERM       HESDF
     C                   MOVEL     'VF'          TRSLAG
      * REF TIL OVERORNET OPPDRAG...
     C                   MOVE      HEAVD         TRAVD0
     C                   MOVE      HEOPD         TROPD0
      * DUP.OPPDRAGETS AVD/OOPDR.NR
     C                   MOVE      TRAVD2        HEAVD
     C                   MOVE      TROPD2        HEOPD
     C     UtlagVF       ENDSR
      *
      ******************************************
     C     SkrivDUP      BEGSR
     C******************************************
     C                   MOVE      'H'           HEUR
     C                   if        NyModul <> ' '
     C**?? kontroll...   MOVE      NyModul       HEUR
     c                   endif
     c                   if        NYHEOT <> *blanks
     c                   eval      HEOT = NYHEOT
     c                   endif
     c                   if        NYHEFR <> *blanks
     c                   eval      HEFR = NYHEFR
     c                   endif
     c                   if        NYPKOD <> *blanks
     c                   eval      HEKDPL = NYPKOD
     c                   endif
     C                   MOVE      *ZEROS        TRAVD1
     C                   MOVE      *ZEROS        TROPD1
     C                   MOVE      *ZEROS        TRAVD2
     C                   MOVE      *ZEROS        TROPD2
     C                   MOVE      ' '           HESTN1
     C                   MOVE      ' '           HESTN2
     C                   MOVE      ' '           HESTN3
     C                   MOVE      ' '           HESTN4
     C                   MOVE      ' '           HESTN5
      * AUTOPRISE DUP-OPPDRAGET
     c                   if        NyBelK <>' '
     C                   MOVEL     NyBelK        TRFRAK                         a/e
     C                   Z-ADD     NyBel         TRFRAB
     c                   else
     C                   EXSR      BERFRP
     C                   MOVEL     ' '           TRFRAK
     C                   MOVE      *ZEROS        TRFRAB
     C                   MOVE      'N'           TRMVA
     C     UUBELØ        IFNE      *ZEROS
     C                   MOVEL     'M'           TRFRAK
     C                   Z-ADD     UUBELØ        TRFRAB
     C                   END
     c                   endif
     C                   MOVE      'N'           HEPK1
     C                   WRITE     HEADFR
      * oppdater xref
N02  C                   MOVE      UUAVD         TIAVD
N02  C                   MOVE      UUOPD         TIOPD
N02  C                   MOVE      DUPFFVF       TISLAG
N02  C     TRIKEY        CHAIN     HETRIU
N02  C                   MOVE      HEAVD         TIAVDD
N02  C                   MOVE      HEOPD         TIOPDD
N02  C                   if        not %found
N02  C                   WRITE     HETRIUR
N02  C                   else
N02  C                   UPDATE    HETRIUR
N02  C                   endif
      *
      * skriv til Fakt/disp mm
     c                   exsr      FAKDISMM
      *
     C                   ENDSR
      *
      ******************************************
     C     FAKDISMM      BEGSR
     C******************************************
      * Varelinjer  fra mor-oppdrag
N19  C                   CALL      'SYGE86'
N19  C                   PARM                    TRAVD0
N19  C                   PARM                    TROPD0
N19  C                   PARM                    HEAVD
N19  C                   PARM                    HEOPD
      * Faktura-post
     C     TRFRAB        IFNE      *ZEROS
     C                   CLEAR                   FAKTR
     C                   MOVE      *ZEROS        FALI
     C                   MOVE      HEAVD         FAAVD
     C                   MOVE      HEOPD         FAOPD
     C                   Z-ADD     TRFRAB        FABELV
     C                   Z-ADD     TRFRAB        FABELN
     C                   MOVE      'FRA'         FAVK
     C                   MOVE      FICURR        FAVAL
     C                   MOVE      TRMVA         FAKDM
     C                   MOVE      'M'           FAKDA
     C                   Z-ADD     TRAVD0        FAAVDR                          *DUP AV..
     C                   MOVE      HEKNSF        FAKUNR
     C                   MOVE      'I'           FASK
     C                   WRITE     FAKTR
     C                   END
      *
      * Stedsnavn i egen fil
     C                   CLEAR                   HESTEDR
     C                   MOVE      HEAVD         HSAVD
     C                   MOVE      HEOPD         HSOPD
     C                   MOVE      HELKA         ST2LK
     C                   MOVEL     HESDF         HSFROM
     C                   MOVEL     HESDF         ST2KOD
     c     StedKey       Chain     Sted2D                             33
     C  N33              movel     ST2NVN        HSFROM
     C                   MOVE      HESDT         HSTO
     C                   MOVEL     HESDT         ST2KOD
     c     StedKey       Chain     Sted2D                             33
     C  N33              movel     ST2NVN        HSTO
     C                   WRITE     HESTEDR
      *
     C                   CLEAR                   DISPR
     C                   MOVE      HEOPD         DIOPD
     C                   MOVE      HEAVD         DIAVD
     C                   MOVE      HEOT          DIOT
     C                   MOVE      HESG          DISIGN
     C                   MOVE      HESDF         DISTEF
     C                   MOVE      TRSDFD        DIDATF
     C                   MOVE      TRSDFK        DITIMF
     C                   MOVE      HESDT         DISTET
     C                   MOVE      TRSDTD        DIDATT
     C                   MOVE      TRSDTK        DITIMT
     C                   MOVE      ' '           DISTA1
      *
     C                   WRITE     DISPR
      *
     C                   MOVE      HEAVD         TXAVD
     C                   MOVE      HEOPD         TXOPD
     C                   MOVE      TRSDFD        TXETDD
     C                   MOVE      TRSDFK        TXETDK
     C                   MOVE      TRSDTD        TXETAD
     C                   MOVE      TRSDTK        TXETAK
     C                   WRITE     TRACKTR
      *
      *
     C     FIURBL        IFEQ      'J'
     C                   EXSR      TTSR
     C                   END
      *
      * Dupliser fritekst
     c                   if        NYFTXT <>  'N'
     c     HeaKey        setll     FREETXT
     c     HeaKey        reade     FREETXT
     c                   dow       not %eof
     C                   MOVE      HEAVD         FRTAVD
     C                   MOVE      HEOPD         FRTOPD
     c                   write     FREETXTR
     c     HeaKey        reade     FREETXT
     c                   enddo
     c                   endif
      *
     C                   ENDSR
      ******************************************
     C     GETAVD        BEGSR
     C******************************************
     C                   move      'N'           ObsAvd            1
      * FINN AVD
     C     KODRIK        CHAIN     NOPNR                              33
     C*
     C     *IN33         IFEQ      *OFF
     C     NOAVDU        ANDNE     *ZEROS
     C                   MOVE      NOAVDU        RIAVDU
     C                   MOVE      NOAVDI        RIAVDI
     C                   MOVE      NOAVDH        RIAVDH
     C                   goto      AvdOK
     C                   END
      * Fantes ikke i i NOPNR - hent via områder i KODTRI
     C     KODRIK        SETGT     KODTRI
     C     XXTERM        READPE    KODTRI                                 33
     C  N33NUMPNR        IFGE      RIPNRF
     C     NUMPNR        ANDLE     RIPNRT
     C                   goto      AvdOk
     C                   END
      * Ikke treff - Hardkodet avd - OBS!!
     C                   move      'J'           ObsAvd
     C                   z-add     1             RIAVDU
     C                   z-add     1             RIAVDI
     C                   z-add     1             RIAVDH
      *
     C     AvdOk         Tag
     C                   ENDSR
      *
     C******************************************
     C     BERFRP        BEGSR
     C******************************************
     C* POSTNUMMERBASERT KALKULASJON
     C                   MOVE      'H'           XXUR
     C                   MOVE      'NO'          XXLKA
     C                   MOVE      'NO'          XXTRI
     C                   MOVE      *BLANKS       XXLKS
     C                   MOVE      *BLANKS       XXSDFF
     C                   MOVE      *BLANKS       XXLKK
     C                   MOVE      *BLANKS       XXSDVT
     C                   MOVE      *BLANKS       XXVERV
     C                   MOVE      *ZEROS        XXVERB
     C                   MOVE      *ZEROS        N5                5 0
     C                   MOVE      *ZEROS        NOPD              7 0
     C                   MOVE      ' '           USKA              1
     C                   Z-ADD     HEKNSF        UUKN              8 0
      * Finn frakt i kundersfraktavtale
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      'FRA'         UGEB              3
     C                   MOVE      'FRA'         UGEBKO            3
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE14'
     C                   PARM                    UGEBKO
     C                   PARM                    UGEB
     C                   PARM                    UAVD              4 0
     C                   PARM                    NOPD
     C                   PARM                    UUKN
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    UAVNR             9 0
     C                   PARM                    UAVMSG           50
     C                   PARM                    USKA
     C                   PARM                    UHEAD
      * BRUTTO
     C     UUBELØ        IFEQ      0
     C                   MOVE      *BLANKS       USONE
     C                   MOVE      *BLANKS       UUVAL
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE13'
     C                   PARM                    UGEB
     C                   PARM                    UAVD
     C                   PARM                    NOPD
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UUKN
     C                   PARM                    UHEAD
n05  C                   PARM                    ATDUM             1
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
      * Registrert (SKREVET TIL HEADF...)
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      'STR'         TTACTI
     C                   MOVE      ULDT          TTDATE
     C                   MOVE      XULTM         TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVEL     UUUSER        TTUSER
     C                   eval      TTTEXT = 'Order entered (via web)'
     C                                      +' - DUP from '+AvdA+'/'+OPDA
     C                   eval      TTTEXL =  'Ordre opprettet (via web)'
     C                                      +' - DUP fra '+AvdA+'/'+OPDA
     C                   WRITE     TRACKFR
      *
     C                   ENDSR

N02  C***********************************************
N02  C     LagRekv       BEGSR
N02  C***********************************************
N02  c     DupFFVF       ifeq      'FF'
N02  C                   MOVE      'F'           TRSTAF
N02  C                   MOVEL     DUPTELK       HELKS
N02  C                   MOVEL     DUPTERM       HESDFF
N02  C                   MOVE      TRSDFD        TRSFFD
N02  c                   else
N02  C                   MOVE      'F'           TRSTAE
N02  C                   MOVEL     DUPTELK       HELKK
N02  C                   MOVEL     DUPTERM       HESDVT
N02  C                   MOVE      TRSDTD        TRSVTD
N02  c                   endif
N02   * Lagre endringer i Mor-oppdraget
N02  C                   Update    HEADFR
N02   *
N02   * Lag Budsjettposten ++
N02  C                   MOVE      UUAVD         TIAVD
N02  C                   MOVE      UUOPD         TIOPD
N02  C                   MOVE      DUPFFVF       TISLAG
N02  C     TRIKEY        CHAIN(N)  HETRIU
N02  C                   IF        %FOUND
N02  C                             AND  TIBURF <> *ZEROS
N02  C                   Z-ADD     TIBURF        KTNR
N02  C                   ELSE
N02  C     'Å'           CHAIN     KOSTT                              34
N02  C   34              MOVE      'Å'           KTTYP
N02  C   34              Z-ADD     1000000       KTNR
N02  C   34              WRITE     KOSTTR
N02  C  N34              ADD       1             KTNR
N02  C  N34              UPDATE    KOSTTR
N02  C                   ENDIF
N02  C*
N02  C     KTNR          CHAIN     KOSBU                              34
N02  C   34              MOVE      ' '           BUST
N02  C   34              MOVE      FICURR        BUVAL                                DSFFTX
N02  C   34              MOVE      'FRA'         BUVK                                 DSFT1
N02  C* TILLATER IKKE ENDRING HVIS REKVISISJONEN HAR STATUS.
N02  C     BUST          IFEQ      ' '
N02  C                   MOVE      KTNR          BUBNR
N02  C                   MOVEL     HEDTÅ         BUPCC
N02  C                   MOVE      HEDTÅ         BUPÅR
N02  C                   MOVE      HEDTM         BUPMN
N02  C                   MOVE      NYBNR         BUBILN
N02  C                   MOVE      NYTRAN        BUTNR
N02  C     TRAKEY        CHAIN     TRAN
N02  C                   IF        %found
N02  C                   MOVE      VMTRLE        BULNR
N02  C                   ENDIF
N02  C                   z-add     NYBEL         BUBL
N02  C                   MOVE      NYBELK        BUTYPE
N02  c                   if        BUTYPE <> 'A' or NYBELK <> 'E'
N02  C                   MOVE      'A'           BUTYPE
N02  c                   endif
N02  C                   MOVE      NYKOMM        BUTXT
N02  C                   IF        BUTXT = *blanks
N02  c                   if        DupFFVF = 'FF'
N02  C                   eval      BUTXT='INNHENTING '+HESDF+' --> '+HESDFF
N02  c                   else
N02  C                   eval      BUTXT='UTKJØRING  '+HESDVT+' --> '+HESDT
N02  c                   endif
N02  C                   ENDIF
N02  C                   MOVE      HESG          BUSG                                 DXPNS1
N02  C                   MOVE      *ZEROS        BUTAVD                               DSFT2
N02  C                   MOVE      *ZEROS        BUTUNR                               DXPNS3
N02  C     HEPRO         IFNE      *ZEROS
N02  C     HEPRO         CHAIN     TURER14                            35
N02  C                   IF        %found
N02  C                   MOVE      TUAVD         BUTAVD
N02  C                   MOVE      TUPRO         BUTUNR
N02  C                   ENDIF
N02  C                   ENDIF
N02  C                   MOVE      HEAVD         BUOAVD
N02  C                   MOVE      HEOPD         BUOPD
N02  C                   MOVE      'T'           BUBLST
N02  C                   MOVE      *BLANKS       BUBILK
N02  C                   MOVE      HEDTOP        BUFEDT
N02  C                   MOVE      'O'           BUFER
N02  C                   MOVE      *ZEROS        BUBNR2
N02  C                   MOVE      *ZEROS        BUBNR3
N02  C                   MOVE      *ZEROS        BULNR3
N02  C                   MOVE      *ZEROS        BUBLFD
N02  C                   ENDIF                                                  BUST = ' '
N02  C   34              WRITE     KOSBUR
N02  C  N34              UPDATE    KOSBUR
N02   *
N02  C     TRIKEY        CHAIN     HETRIU                             34
N02  C                   MOVE      KTNR          TIBURF
N02  C                   if        not %found
N02  C                   WRITE     HETRIUR
N02  C                   else
N02  C                   UPDATE    HETRIUR
N02  C                   endif
N02   *
N02  c                   ENDSR
N02   *
N02   *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *entry        plist
     c                   parm                    UUAVD             4 0          mor-op.Avd INNparam
     c                   parm                    UUOPD             7 0          mor-op.Avd INNparam
     c                   parm                    DUPTELK           2            Dup via LK
     c                   parm                    DUPTERM           5            Dup via Stes
     c                   parm                    DUPFFVF           2            FF / VF-Dup
     c                   parm                    UUUSER           10            User som lager DUP
     c                   parm                    SIGN              3            HESG fra mor-oppdrag
     c                   parm                    NYAVD             4 0
     c                   parm                    NYOPD             7 0
     c                   parm                    NYHEOT            2            Otype
     c                   parm                    NYHEFR            3            Franka
     c                   parm                    NYFTXT            1            Kopier Fritekst
     c                   parm                    NYMODUL           1            ModulKo,Bla.=H=Trmod
     c                   parm                    NYPKOD            1            Produktkode
     c                   parm                    NYBNR            10            BILnummer, Rekvis
     c                   parm                    NYTRAN            8 0          Transpnr., Rekvis
     c                   parm                    NYKOMM           35            Kommentar, Rekvis
     c                   parm                    NYBEL            13 2          Beløp,     Rekvis
     c                   parm                    NYBELK            1            BeløpsKode Rekvis
     c                   parm                    ERRmsg          150
     c                   parm                    DUPIND            1            '0'=OK / '1'=Feilet
      *
      * I utgangspunket misslyket om en ikke kommer gjennom alle tetster mm
     c                   move      '1'           DUPIND
     c                   move      UUAVD         AVDA              4
     c                   move      UUOPD         OPDA              7
     C     HeaKey        klist
     c                   kfld                    UUAVD             4 0
     c                   kfld                    UUOPD             7 0
     C     STEDKEY       klist
     c                   kfld                    ST2LK
     c                   kfld                    ST2KOD
      * key i KODRIK og NOPNR forutsetter et det er NORSK dup.
     C     KODRIK        KLIST
     c                   KFLD                    XXTERM            4 0
     C                   KFLD                    NUMPNR            4 0
     C                   MOVEL     DUPTERM       TSTPNR            4
     C                   TESTN                   TSTPNR               33
     C     *IN33         IFEQ      '1'
     C                   MOVE      TSTPNR        XXTERM
     C                   endif
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     KODTAK        KLIST
     C                   KFLD                    YYAUNI
     C                   KFLD                    YYAVD             4 0
     C                   MOVE      'A'           YYAUNI            1
N02  C     TRIKEY        KLIST
N02  C                   KFLD                    TIAVD
N02  C                   KFLD                    TIOPD
N02  C                   KFLD                    TISLAG
N02  C     TRAKEY        KLIST
N02  C                   KFLD                    FIFIRM
N02  C                   KFLD                    BUTNR
N02   *
N02  C                   MOVE      *ZEROS        HEDTOP
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      * hent firmaverdirer innland
     C                   MOVE      *ZEROS        AVDZER            4 0
     C     AVDZER        CHAIN     FIRINL                             33
      * Finnes oppdrag
     C     HeaKey        chain     HEADF                              33
     c     *in33         cabeq     '1'           UtIni
      *
      * Dup allerede uført ...
     c     DupFFVF       ifeq      'FF'
     c     TROPD1        cabne     *zeros        UtIni
     c                   else
     c     TROPD2        cabne     *zeros        UtIni
     c                   endif
      *
      *  Tester ok
     c                   move      '0'           DUPIND
      *
     C                   MOVE      *ZEROS        XXVKT
     C                   MOVE      *ZEROS        XXFBV
     C                   MOVE      *ZEROS        XXM3
     C                   MOVE      *ZEROS        XXLM
     C                   MOVE      *ZEROS        XXNT
     C                   MOVE      *ZEROS        XXVERB
     C     utIni         ENDSR
