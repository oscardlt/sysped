********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 F10 priisjusteringer
10XXX * 02 SH  PTF10 F11 priisjusteringer type 2
10XXX * 03 SH  PTF12 sjekk utelat listen
10XXX * 04 SH  PTF12 vedlikeholde utelatte kunder
********************************************************************
      *
      ***********************************************************
      *** PROGRAM FOR VEDLIKEH. AV OLJE/VAL-TILLEGG M.M.      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
N03  FKUIPRL01  IF   E           K DISK
     FFRTIL     UF A E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYFA23FB  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C*
     C                   EXSR      INIT
     C*
     C     UKUNDE        IFNE      *ZEROS
     C                   MOVE      UKUNDE        WSKUNR
     C                   GOTO      STARTX
     C                   END
     C*
     C     START0        TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       9930
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKUNR
     C     KUND1K        CHAIN     CUNDL01                            99
     C  N99              GOTO      START
     C                   MOVE      *ZEROS        WSKUNR
     C                   SETOFF                                       99
     C                   GOTO      START0
     C                   END
     C     STARTX        TAG
     C     WSKUNR        IFNE      *ZEROS
     C     KUND1K        CHAIN     CUNDL01                            99
     C   99              MOVE      'SPK0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START0
     C                   MOVE      KNAVN         WSKNAV
     C                   ELSE
     C                   CLEAR                   WSKNAV
     C                   MOVEL     '*ALL'        WSKNAV
     C                   END
     C*
      * START PÅ DETALJER PÅ KUNDEN (ELLER ALLE)
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2ODEL        YSODEL            1
     C                   MOVE      W2OAVD        YSOAVD            4 0
     C                   MOVE      W2OKUN        YSOKUN            8 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSF1        SETLL     FRTIL
     C     START1        TAG
     C                   READ(N)   FRTIL                                  94
      *
     C  N94WSKUNR        IFNE      *ZEROS
     C     WSKUNR        ANDNE     FRTKUN
     C                   GOTO      START1
     C                   END
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C     FRTKUN        IFNE      *ZEROS
     C     KUNKEY        CHAIN     CUNDL01                            34
     C                   ELSE
     C                   CLEAR                   KNAVN
     C                   MOVEL     '*ALL'        KNAVN
     C                   END
     C                   SETOFF                                       88
     C  N94WSKUNR        IFEQ      *ZEROS
     C     FRTKUN        ANDNE     *ZEROS
     C                   SETON                                        88
     C                   END
     C                   WRITE     SUBFIL
     C     NEXT          TAG
     C                   READ(N)   FRTIL                                  94
     C  N94WSKUNR        IFNE      *ZEROS
     C     WSKUNR        ANDNE     FRTKUN
     C                   GOTO      NEXT
     C                   END
     C  N94              MOVE      FRTDEL        YSODEL
     C  N94              MOVE      FRTAVD        YSOAVD
     C  N94              MOVE      FRTKUN        YSOKUN
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
      *** FORRIGE
     C     *IN12         IFEQ      '1'
     C     UKUNDE        IFEQ      *ZEROS
     C                   GOTO      START0
     C                   ELSE
     C                   GOTO      SLUTT
     C                   END
     C                   END
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   MOVE      *BLANKS       WSTDEL
     C                   MOVE      *ZEROS        WSTAVD
     C                   MOVE      WSKUNR        WSTKUN                           PROTECT!!
     C                   MOVE      *BLANKS       WSTGEB
     C                   MOVE      *ZEROS        WSTSAT
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
N01  C*** F10 prisjuster
N01  c     *in10         ifeq      '1'
n01  C                   exsr      juster
n01  c                   goto      start
n01  c                   end
     C*
N04  C*** F07 utelate kunder
N04  c     *in07         ifeq      '1'
n04  C                   call      'SYPO30CL'
n04  c                   goto      start
n04  c                   end
N02  C*** F11 prisjuster
N02  c     *in11         ifeq      '1'
n02  C                   exsr      justerb
n02  c                   goto      start
n02  c                   end
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSF3        SETLL     FRTIL
     C     NEXT2         TAG
     C  N94              READ(N)   FRTIL                                  94
     C  N94WSKUNR        IFNE      *ZEROS
     C     WSKUNR        ANDNE     FRTKUN
     C                   GOTO      NEXT2
     C                   END
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     16            POSNBR
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C                   SELECT
     C     POSNBR        WHENEQ    8
     C                   MOVEL     'HEUR'        UTYP
     C                   CALL      'SYFAKDA'
     C                   PARM                    UTYP             10
     C                   PARM                    VEDL              1
     C                   PARM                    RETURK            5
     C                   MOVEL     RETURK        WSTDEL
      *
     C     POSNBR        WHENGE    15
     C     POSNBR        ANDLE     18
     C                   CALL      'SYFA14B'
     C                   PARM                    WSTAVD
      *
     C     POSNBR        WHENGE    61
     C     POSNBR        ANDLE     63
     C                   CALL      'SYFI10D'
     C                   PARM                    WSTGEB
     C                   ENDSL
     C                   ENDSR
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
      * TESTER VED NY
     C  N40              DO
     C     WSTDEL        COMP      *BLANKS                                90
     C   90              MOVE      'SHU0534'     MSGNR
     C   90              Z-ADD     8             POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSFK        CHAIN     FRTIL                              33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    FRTILR
     C   33              WRITE     FRTILR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     FRTGEB        COMP      '*DL'                                  98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     FRTGEB        COMP      '*DL'                                  98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODDEL        CHAIN     FRTIL                              33
     C  N33              DELETE    FRTILR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       FRTDEL
     C                   MOVE      *ZEROS        FRTAVD
     C                   MOVE      *ZEROS        FRTKUN
     C                   MOVEL     '*DL'         FRTGEB
     C                   MOVE      *ZEROS        FRTSAT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSTDEL
     C                   MOVE      *ZEROS        WSTAVD
     C                   MOVE      *BLANKS       WSTGEB
     C                   MOVE      *ZEROS        WSTSAT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      FRTDEL        WSTDEL
     C                   MOVE      FRTAVD        WSTAVD
     C                   MOVE      FRTKUN        WSTKUN
     C                   MOVE      FRTGEB        WSTGEB
     C                   MOVE      FRTSAT        WSTSAT
     C                   SETON                                        40
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     61            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSKUNR        FRTKUN
     C                   MOVE      WSTDEL        FRTDEL
     C                   MOVE      WSTAVD        FRTAVD
     C                   END
     C     FRTKUN        IFNE      *ZEROS
     C     KUNKEY        CHAIN     CUNDL01                            34
     C                   ELSE
     C                   CLEAR                   KNAVN
     C                   MOVEL     '*ALL'        KNAVN
     C                   END
     C*
     C                   MOVE      WSTGEB        FRTGEB
     C                   MOVE      WSTSAT        FRTSAT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UKUNDE            8 0
     C*
     C     KODDEL        KLIST
     C                   KFLD                    FRTDEL
     C                   KFLD                    FRTAVD
     C                   KFLD                    WSKUNR
     C     KODSFK        KLIST
     C                   KFLD                    WSTDEL
     C                   KFLD                    WSTAVD
     C                   KFLD                    WSKUNR
     C     KODSF1        KLIST
     C                   KFLD                    W2ODEL
     C                   KFLD                    W2OAVD
     C                   KFLD                    W2OKUN
     C     KODSF3        KLIST
     C                   KFLD                    YSODEL
     C                   KFLD                    YSOAVD
     C                   KFLD                    YSOKUN
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FRTKUN
     C*
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C*
     C*
N01  C***********************************************
N01  C     JUSTER        BEGSR
n01  C***********************************************
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     15            POSNBR
n01   *
n01  c                   setoff                                       30
n01   *
n01  c     startju       tag
n01  C                   EXFMT     PRISJU
n01  c                   setoff                                       30
n01  c   12              goto      endju
n01  c     wstsat        ifeq      0
n01  C                   MOVE      'SHU1381'     MSGNR
n01  c                   seton                                        30
n01  c                   goto      startju
n01  c                   end
n01  c     *in02         ifeq      '0'
n01  C                   MOVE      'SHU1383'     MSGNR
n01  c                   seton                                        30
n01  c                   goto      startju
n01  c                   end
n01  c                   exsr      juster2
n01  c     endju         endsr
N01  C***********************************************
N01  C     JUSTER2       BEGSR
n01  C***********************************************
n01   *
n01  c                   setoff                                       33
n01  c     *loval        setll     frtil
n01  c     *in33         doweq     '0'
n01  c     lesny         tag
n01  c                   read      frtil                                  33
n01   *
n01  c     *in33         ifeq      '0'
n01   *
n03   *
n03  c     kunkey        chain     KUIPRL01                           34
n03  c  N34              goto      lesny
n03   *
n01  c     wstdel        ifne      *blanks
n01  c     wstdel        andne     frtdel
n01  c                   goto      lesny
n01  c                   end
n01   *
n01  c     wstavd        ifne      *zeros
n01  c     wstavd        andne     frtavd
n01  c                   goto      lesny
n01  c                   end
n01   *
n01  c     wstkun        ifne      *zeros
n01  c     wstkun        andne     frtkun
n01  c                   goto      lesny
n01  c                   end
n01  c*
n01  c     wstgeb        ifne      *blanks
n01  c     wstgeb        andne     frtgeb
n01  c                   goto      lesny
n01  c                   end
n01  c*
n01   *
n01  c     frtsat        add       wstsat        frtsat
     c     ukopi         ifne      'J'
     c                   call      'SYFA23BCLC'
     c                   move      'J'           ukopi             1
     c                   end
n01  c                   update    frtilr
n01   *
n01  c                   end
n01  c                   end
n01  c                   endsr
N02  C***********************************************
N02  C     JUSTERB       BEGSR
n02  C***********************************************
n02  C                   Z-ADD     21            LINNBR
n02  C                   Z-ADD     2             posnbr
n02   *
n02  c                   setoff                                       30
n02   *
n02  c     startjub      tag
n02  C                   EXFMT     PRISJU2
n02  c                   setoff                                       30
n02  c   12              goto      endjub
N02  c     wstsatf       ifeq      0
N02  c     wstsatt       oreq      0
N02  C                   MOVE      'SHU2080'     MSGNR
n02  c                   seton                                        30
n02  c                   goto      startjub
n02  c                   end
N02  c     wstgeb        ifeq      *blanks
N02  C                   MOVE      'SHU2081'     MSGNR
n02  c                   seton                                        30
n02  c                   goto      startjub
n02  c                   end
n02  c     *in02         ifeq      '0'
n02  C                   MOVE      'SHU1383'     MSGNR
n02  c                   seton                                        30
n02  c                   goto      startjub
n02  c                   end
n02  c                   exsr      justerb2
n02  c     endjub        endsr
N02  C***********************************************
N02  C     JUSTERB2      BEGSR
n02  C***********************************************
n02   *
n02  c                   setoff                                       33
n02  c     *loval        setll     frtil
n02  c     *in33         doweq     '0'
n02  c     lesnyb        tag
N02  c                   read      frtil                                  33
N02   *
n02  c     *in33         ifeq      '0'
n03   *
n03  c     kunkey        chain     KUIPRL01                           34
n03  c  N34              goto      lesnyb
n03   *
n02  c* gebyr må stemme
N02  c     wstgeb        ifne      frtgeb
N02  c                   goto      lesnyb
n02  c                   end
n02  c*
n02  c* kun kundeavtaler
n02  c     frtkun        ifeq      *zeros
n02  c                   goto      lesnyb
n02  c                   end
n02   *
n02   *
n02  c     wstsatf       ifne      frtsat
n02  c                   goto      lesnyb
N02  c                   end
N02   *
n02   *
n02  c                   z-add     wstsatt       frtsat
n02  c                   update    frtilr
n02   *
n01  c                   endif
n01  c                   enddo
n01  c                   endsr
