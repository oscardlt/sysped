      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SAD136R MODULE(*LIBL/SAD136R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSADEFL03  UF   E           K DISK    USROPN
     FSADEFCL1  UF   E           K DISK    USROPN
     FEDIS2     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D UUID            S             36
     D JSONstring      s          32000
     D Error           S            150
     D ErrorT          S          32000

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     C     UUID          CHAIN     SADEFL03                           33
     C                   IF        *IN33 = *OFF
     C                   EVAL      EFERR = %TRIM(ERROR) + ' ' + %TRIM(ERRORT)
     C                   IF        ERROR = *BLANKS
     C                   EVAL      EFST = 'C'
     C                   ELSE
     C                   EVAL      EFST = 'M'
     C                   END
     C                   UPDATE    SADEFFR
      *
     C                   MOVE      EFPRO         XAN08             8
     C                   EVAL      X0020 = 'EF' + %TRIM(XAN08)
     C     X0020         SETGT     EDIS2
     C     X0020         READPE    EDIS2                                  33
     C                   DOW       *IN33 = *OFF
     C                   IF        SSR = 'S' AND SST = 'B'
     C                   IF        ERROR = *BLANKS
     C                   EVAL      SST = 'C'
     C                   ELSE
     C                   EVAL      SST = 'E'
     C                   END
     C                   UPDATE    EDISR
     C                   LEAVE
     C                   END
     C                   UPDATE    EDISR
     C     X0020         READPE    EDIS2                                  33
     C                   ENDDO
      *
     C                   IF        ERRORT <> *BLANKS
     C                   MOVE      EFPRO         U_EFPRO           8
     C                   CALL      'SAD137R'
     C                   PARM                    U_EFPRO
     C                   PARM                    ERRORT
     C                   END
      *
     C     EFPRO         SETLL     SADEFCL1
     C     EFPRO         READE     SADEFCL1                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        EFST = 'S'
     C                   DELETE    SADEFCFR
     C                   ELSE
     C                   EVAL      EFST = 'C'
     C                   UPDATE    SADEFCFR
     C                   END
     C     EFPRO         READE     SADEFCL1                               33
     C                   ENDDO
      *
     C                   END
      *
     C                   exsr      SRUTJSON
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C                   CLOSE     SADEFL03
     C                   CLOSE     SADEFCL1
     C                   CLOSE     EDIS2

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UUID    = zhbgetvar('UUID')
     C                   EVAL      Error   = zhbgetvar('Error')
     C                   EVAL      ErrorT  = zhbgetvar('ErrorTxt')
      *
     c                   endsr
      *
      *********************************************************
     C     SRUTJSON      Begsr
      *********************************************************
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
     C                   eval      error = ' '
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(UUID)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      SADEFL03
     C                   OPEN      SADEFCL1
     C                   OPEN      EDIS2
      *
     C     *LIKE         DEFINE    S0020         X0020
      *
     C                   ENDSR
      *
