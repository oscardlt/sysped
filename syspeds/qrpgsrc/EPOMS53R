Jov   * gjort om slik at det kalles basert på AVD/OPD (= en ASN) (kan være flere oppdrag på en tur)
      * leser bare EPOL via avd oppdrag ... !"!!!!!!!!!!!!!!!!!
      *
     FEPOH      IF   E           K DISK
     FEPOL02    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FTRACKT    IF   E           K DISK
     FBILLOF    IF   E           K DISK
     FBLV1      IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      IF A E             DISK
     FEUNH      O  A E             DISK
     FECNT96A   O  A E             DISK
     FEDTM96A   O  A E             DISK
     FERFF96A   O  A E             DISK
     FENAD96A   O  A E             DISK
     FECTA96A   O  A E             DISK
     FETDT96A   O  A E             DISK
     FELIN96A   O  A E             DISK
     FEPIA96A   O  A E             DISK
     FEPAC96A   O  A E             DISK
     FEPCI96A   O  A E             DISK
     FEIMD96A   O  A E             DISK
     FEQTY96A   O  A E             DISK
     FEGIN96A   O  A E             DISK
     FECPS96A   O  A E             DISK
     FEUNS      O  A E             DISK
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D LEVNR           S             17    DIM(100)
     D                 DS
     D  ØN152                  1     15S 2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
     D                 DS
     D  AVDOPD                 1     12
     D   DSAVD                 1      4
     D   DSstrek               5      5
     D   DSOPD                 6     12
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRHLEVN
     C     1             DO        100           XN03
     C                   IF        LEVNR(XN03) = *BLANKS
     C                   LEAVE
     C                   END
     C                   EXSR      SRB
     C                   ENDDO
     C                   EVAL      *INLR=*ON
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
     c     *entry        Plist
     c                   parm                    UAVD              4 0
     c                   parm                    UOPD              7 0
      * INIT AV ARBEIDSFELT
     C                   Z-ADD     0             ØX                2 0
     C                   Z-ADD     0             WC6066            7 0
     C                   Z-ADD     0             WL1082            7 0
     C                   Z-ADD     0             WDIFF             9 0
     C                   MOVE      *BLANKS       ØAN              12
     C     *LIKE         DEFINE    CMN           WMN
     C     *LIKE         DEFINE    EHSTA2        WEHSTA2
     C     *LIKE         DEFINE    EHKUND        WEHKUND
     C                   EVAL      WEHSTA2='S'
     C                   EVAL      WEHKUND=21647
      * DEF AV KEYS
     C     EPOHKEY       KLIST
     C                   KFLD                    ELKUND
     C                   KFLD                    ELBES
     C     HEAKEY        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     c     HEAkey        chain     Headf
     c                   move      *blanks       XXBLNR           16
     c                   move      TRSDFD        XXATD             8 0
     c                   move      TRSDFD        XXETA             8 0
     C     HeaKey        CHAIN     TRACKT                             33
     c  N33              move      TXATDD        XXATD
     c  N33              move      TXETAD        XXETA
     C     HeaKey        CHAIN     BILLOF
     c                   eval      XXBLNR = BLBLNR
     c     EHBLNR        ifne      *blanks
     c                   eval      XXBLNR = EHBLNR
     c                   endif
     c     ELBLNR        ifne      *blanks
     c                   eval      XXBLNR = ELBLNR
     c                   endif
      * hent contnr fra første BLLINJ - contnr = BVMN
     C     HeaKey        CHAIN     BLV1
      *
     C                   MOVEL     UAVD          DSAVD
     C                   MOVEL     '/'           DSstrek
     C                   MOVEL     UOPD          DSOPD
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les linjer                                           SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C*                  EXSR      SRHOVED
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      *BLANKS       HeadUt            1
      ***     lese  EPOL02 på gitt oppdrag
     C                   EVAL      *IN52=*OFF
     C     HEAKEY        SETLL     EPOL02
     C     HEAKEY        READE     EPOL02                                 52
     C                   DOW       *IN52=*OFF
     C     EPOHKEY       CHAIN     EPOH                               33
     C  N33              IF        EHLEVN = LEVNR(XN03)
     C                   EXSR      CPS10
     C                   END
     C     HEAKEY        READE     EPOL02                                 52
     C                   ENDDO
     C                   EXSR      SRSLUTT

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  HENT LEVERANDØR                                  SRHLEVN /
      *////////////////////////////////////////////////////////////
     C     SRHLEVN       BEGSR
     C                   MOVE      *BLANKS       LEVNR
      *
     C     HEAKEY        SETLL     EPOL02
     C     HEAKEY        READE     EPOL02                                 52
     C                   DOW       *IN52=*OFF
     c     EPOHKEY       CHAIN     EPOH                               33
     C     1             DO        100           XN03              3 0
     C                   SELECT
     C                   WHEN      LEVNR(XN03) = EHLEVN
     C                   LEAVE
     C                   WHEN      LEVNR(XN03) = *BLANKS
     C                   EVAL      LEVNR(XN03) = EHLEVN
     C                   LEAVE
     C                   ENDSL
     C                   ENDDO
     C     HEAKEY        READE     EPOL02                                 52
     C                   ENDDO

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Hoved                                            SRHOVED /
      *////////////////////////////////////////////////////////////
     C     SRHOVED       BEGSR
      * Hent recordnummer for melding
     C     1             CHAIN     EDIC                               51
     C     *IN51         CABEQ     *ON           SRA9
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
      * Nullstill noen verdier
     C                   EVAL      WC6066=1
      * DTM - 137 = dokumentdato
     C                   CLEAR                   DTMR
     C                   EVAL      DMN=WMN
     C                   EVAL      DGN=0
     C                   EVAL      DREP0=1
     C                   EVAL      D2005='137'
     C                   EVAL      D2380=WDT
     C                   EVAL      D2379='102'
     C                   WRITE     DTMR
      * DTM - 11  = ATD
     C                   CLEAR                   DTMR
     C                   EVAL      DMN=WMN
     C                   EVAL      DGN=0
     C                   EVAL      DREP0=2
     C                   EVAL      D2005='11'
     C                   MOVEL     XXATD         D2380
     C                   EVAL      D2379='102'
     C                   WRITE     DTMR
      * DTM - 132 = ETA
     C                   CLEAR                   DTMR
     C                   EVAL      DMN=WMN
     C                   EVAL      DGN=0
     C                   EVAL      DREP0=3
     C                   EVAL      D2005='132'
     C                   MOVEL     XXETA         D2380
     C                   EVAL      D2379='102'
     C                   WRITE     DTMR
      * RFF
     C                   CLEAR                   RFFR
     C                   EVAL      RMN=WMN
     C                   EVAL      RGN=1
     C                   EVAL      RREP0=1
     C                   EVAL      R1153='ZZ1'
     C                   EVAL      R1154=HEVS2                                  VOICE PAKKSEDDELNR
     C                   WRITE     RFFR
      * RFF
     C                   CLEAR                   RFFR
     C                   EVAL      RMN=WMN
     C                   EVAL      RGN=1
     C                   EVAL      RREP0=2
     C                   EVAL      R1153='BM '
     C                   EVAL      R1154=XXBLNR
     C                   WRITE     RFFR
      * NAD+BY
     C                   CLEAR                   NADR
     C                   EVAL      NMN=WMN
     C                   EVAL      NGN=2
     C                   EVAL      NREP0=1
     C                   EVAL      N3035='BY'
     C                   EVAL      N3039='7080001240011'
     C                   EVAL      N3055='9'
     C                   WRITE     NADR
      * NAD+SU
     C                   CLEAR                   NADR
     C                   EVAL      NMN=WMN
     C                   EVAL      NGN=2
     C                   EVAL      NREP0=2
     C                   EVAL      N3035='SU'
     C                   EVAL      N3039=EHLEVN
     C                   EVAL      N3055='9'
     C                   WRITE     NADR
      * TDT
     C                   CLEAR                   TDTR
     C                   EVAL      TMN=WMN
     C                   EVAL      TGN=6
     C                   EVAL      TREP0=1
     C                   EVAL      T8051='20'
     C                   EVAL      T8067='001'
     C                   WRITE     TDTR

     C     SRA9          TAG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  CPS                                                CPS10 /
      *////////////////////////////////////////////////////////////
     C     CPS10         BEGSR
      * hent headerinfo
     c*    EPOHKEY       chain     EPOH
      *
      * header ennå ikke lagt ut ...
     c     HeadUt        ifeq      ' '
     c                   move      'x'           HeadUt            1
     C                   EXSR      SRHOVED
     c                   endif
      *
     C                   ADD       1             WC6066
      * CPS 10
     C                   CLEAR                   CPSR
     C                   EVAL      CMN=WMN
     C                   EVAL      CGN=10
     C                   EVAL      CREP0=WC6066
     C                   EVAL      ØN152=WC6066
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C7164
     C                   EVAL      C7166='1'
     C                   WRITE     CPSR
      * PAC 11 - 1
     C                   CLEAR                   PACR
     C                   EVAL      PMN=WMN
     C                   EVAL      PGN=11
     C                   EVAL      PREP0=WC6066
     C                   EVAL      PREP1=1
     C                   EVAL      P7224='1'
     C                   EVAL      P7065='1'
     C                   WRITE     PACR
      * PCI 13
     C                   CLEAR                   PCIR
     C                   EVAL      PMN=WMN
     C                   EVAL      PGN=13
     C                   EVAL      PREP0=WC6066
     C                   EVAL      PREP1=1
     C                   EVAL      PREP2=1
     C                   EVAL      P4233='33E'
     C*                  EVAL      P7102A='1234567'
     C*                  EVAL      P7102B='1234567890'
     C                   WRITE     PCIR
      * GIN 14
     C                   CLEAR                   GINR
     C                   EVAL      GMN=WMN
     C                   EVAL      GGN=14
     C                   EVAL      GREP0=WC6066
     C                   EVAL      GREP1=1
     C                   EVAL      GREP2=1
     C                   EVAL      G7405='BJ'
     C*                  EVAL      G7402A='123456789012345678'
     C                   EVAL      G7402A=BVMN
     C                   WRITE     GINR
      * PAC 11 - 2
     C                   CLEAR                   PACR
     C                   EVAL      PMN=WMN
     C                   EVAL      PGN=11
     C                   EVAL      PREP0=WC6066
     C                   EVAL      PREP1=2
     C                   EVAL      P7224='1'
     C                   EVAL      P7065='CT'
     C                   WRITE     PACR

     C                   EXSR      LIN15

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  LIN                                                LIN15 /
      *////////////////////////////////////////////////////////////
     C     LIN15         BEGSR
     C                   EVAL      WL1082=WC6066-1
      * LIN
     C                   CLEAR                   LINR
     C                   EVAL      LMN=WMN
     C                   EVAL      LGN=15
     C                   EVAL      LREP0=WC6066
     C                   EVAL      LREP1=WL1082
     C                   EVAL      ØN152=WL1082
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           L1082A
     C                   EVAL      L1229='5'
     C                   EVAL      L7140=ELART
     C                   EVAL      L7143='EN'
     C                   WRITE     LINR
      * PIA
     C                   CLEAR                   PIAR
     C                   EVAL      PMN=WMN
     C                   EVAL      PGN=15
     C                   EVAL      PREP0=WC6066
     C                   EVAL      PREP1=WL1082
     C                   EVAL      PREP2=1
     C                   EVAL      P4347='1'
     C                   EVAL      P7140A=ELARTL
     C                   EVAL      P7143A='SA'
     C                   EVAL      P3055A='91'
     C                   WRITE     PIAR
      * IMD
     C                   CLEAR                   IMDR
     C                   EVAL      IMN=WMN
     C                   EVAL      IGN=15
     C                   EVAL      IREP0=WC6066
     C                   EVAL      IREP1=WL1082
     C                   EVAL      IREP2=1
     C                   EVAL      I7077='F'
     C                   EVAL      I7008A=ELABES
     C                   WRITE     IMDR
      * QTY
     C                   CLEAR                   QTYR
     C                   EVAL      QMN=WMN
     C                   EVAL      QGN=15
     C                   EVAL      QREP0=WC6066
     C                   EVAL      QREP1=WL1082
     C                   EVAL      QREP2=1
     C                   EVAL      Q6063='12'
     C                   IF        ELNTSL > 0
     C                   EVAL      ØN152=ELNTSL
     C                   ELSE
     C                   EVAL      ØN152=ELANTS
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6060
     C                   EVAL      Q6411='PCE'
     C                   WRITE     QTYR
      * RFF
     C                   CLEAR                   RFFR
     C                   EVAL      RMN=WMN
     C                   EVAL      RGN=16
     C                   EVAL      RREP0=WC6066
     C                   EVAL      RREP1=WL1082
     C                   EVAL      RREP2=1
     C                   EVAL      R1153='ON'
     C                   EVAL      R1154=EHBES
     C                   EVAL      ØN152=ELLIN
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1156
     C**                 IF        ELULIN>0
     C                   EVAL      ØN152=ELULIN
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R4000
     C**                 ENDIF
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Meldingsslutt                                    SRSLUTT /
      *////////////////////////////////////////////////////////////
     C     SRSLUTT       BEGSR
      * WRITE TO EUNS
     C                   CLEAR                   UNSR
     C                   EVAL      UMN=WMN
     C                   EVAL      UGN=50
     C                   EVAL      UREP0=1
     C                   EVAL      U0081='S'
     C                   WRITE     UNSR
      * WRITE TO ECPS96A
     C                   CLEAR                   CPSR
     C                   EVAL      CMN=WMN
     C                   EVAL      CGN=10
     C                   EVAL      CREP0=1
     C                   EVAL      C7164='1'
     C                   WRITE     CPSR
      * WRITE TO EPAC96A
     C                   CLEAR                   PACR
     C                   EVAL      PMN=WMN
     C                   EVAL      PGN=11
     C                   EVAL      PREP0=1
     C                   EVAL      PREP1=1
     C                   EVAL      ØN152=WL1082
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           P7224
     C                   EVAL      P7065='1'
     C                   WRITE     PACR
      * WRITE TO ECNT96A
     C                   CLEAR                   CNTR
     C                   EVAL      CMN=WMN
     C                   EVAL      C6069='2'
     C                   EVAL      ØN152=WC6066
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   WRITE     CNTR
      * WRITE TO EUNH
     C                   CLEAR                   UNHR
     C                   EVAL      UMN=WMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'DESADV'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '96A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'EAN005'      U0057
     C                   MOVEL     EHBES         U0068
     C                   MOVEL     '351'         U1001
     C*                  MOVEL     EHBLNR        U1004
     C                   MOVEL     AVDOPD        U1004
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
      * WRITE TO EDIM
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004='7080003141941'
     C                   EVAL      M0010='7080001240011'
     C                   MOVEL     U0062A        M0062
     C                   MOVE      U0065         M0065
     C                   MOVEL     U0068         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   MOVEL     U1225         M1225
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   WRITE     EDIMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Minus value ?
     C     ØN152         IFLT      0                                            <-------------I
     C                   Z-SUB     ØN152         ØN152                                        I
     C                   MOVE      '-'           ØSIGN             1                          I
     C                   ELSE                                                   <-------------I
     C                   MOVE      '+'           ØSIGN                                        I
     C                   ENDIF                                                  <-------------I
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'                                         <-------------I
     C                   MOVE      ØA152H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA152X        ØA12                                         I
     C                   MOVE      ',  '         ØA12                                         I
     C                   MOVE      ØA152D        ØA12                                         I
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C     Ø12(ØX)       IFEQ      ','                                           <-----------II
     C                   SUB       1             ØX
     C                   ENDIF                                                   <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C     ØSIGN         IFEQ      '-'                                          <-------------I
     C                   SUB       1             ØX                                           I
     C                   MOVE      '-'           Ø12(ØX)                                      I
     C                   ENDIF                                                  <-------------I
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
