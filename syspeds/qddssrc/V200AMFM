     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/VALUF)
     A                                      PRINT
     A                                      HLPDOC('VEDVA010' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            VALKOD    R        O  5  4
     A            VALTEK    R        O  5 10
     A            VALKU1    R        O  5 29
     A            VALKU2    R        O  5 40
     A            OMRFAK    R        O  5 53
     A            WSDT2          6  0O  5 61EDTCDE(Y)
     A            AKTKOD    R        O  5 72
     A            FIRMA     R        O  5 75
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF03(03 'Avslutt')
     A  91N93                               CF05(05 'Frisk')
     A  91N93N99                            CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A  91N93 99                            CF12(12 'AVBRYT')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A  99        MSG101        30   O  1 19MSGID(DIV 1208 SYMSGF)
     A                                      COLOR(WHT)
     A N99        MSG102        30A  O  1 23MSGID(DIV 1209 SYMSGF)
     A                                      COLOR(WHT)
     A            WSTXT         11A  O  1 50COLOR(WHT)
     A  99        MSG103        25A  O  3  2MSGID(DIV 1034 SYMSGF)
     A                                      COLOR(BLU)
     A N99        MSG104        40A  O  3  2MSGID(DIV 1033 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        78A  O  4  1MSGID(DIV 1210 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1  2'SYSPED/V200AM'
     A                                      COLOR(BLU)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A                                  2 29'AMETA'
     A                                      COLOR(WHT)
     A          R BUNN
     A N99        MSG201        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A  99        MSG202        18A  O 23  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A N99        MSG204        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG205        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG206        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG301        75A  O  1  4MSGID(DIV 1211 SYMSGF)
     A                                      COLOR(WHT)
     A            WSVKOD         3A  B  2  4DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            WSVKU1         7Y 4B  2 10DSPATR(UL)
     A  40N30
     AO 68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A                                      EDTCDE(J)
     A            WSVKU2         7Y 4B  2 21EDTCDE(J)
     A            WSOFAK         3Y 0B  2 33EDTCDE(4)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A            WSVTEK        15A  B  2 39DSPATR(UL)
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A                                      CHECK(LC)
     A            WSDT           6Y 0B  2 61EDTCDE(Y)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            MSG303        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG401        15A  O  2  2MSGID(DIV 1212 SYMSGF)
     A            W2VKOD         3A  I  2 18DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG501        75A  O  1  4MSGID(DIV 1211 SYMSGF)
     A            VALKOD         3A  O  2  4DSPATR(HI)
     A                                      DSPATR(PC)
     A            VALTEK        15A  O  2 39DSPATR(HI)
     A            VALKU1         7Y 4O  2 10EDTCDE(J)
     A                                      DSPATR(HI)
     A            VALKU2         7Y 4O  2 21EDTCDE(J)
     A                                      DSPATR(HI)
     A            OMRFAK         3Y 0O  2 33EDTCDE(4)
     A                                      DSPATR(HI)
     A            WSDT2          6Y 0O  2 61EDTCDE(Y)
     A                                      DSPATR(HI)
     A            MSG502        18A  O  3  4MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A N99                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A N99                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A  99                                  CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/V200AM'
     A                                      COLOR(BLU)
     A  99        MSG601        30A  O  1 19MSGID(DIV 1208 SYMSGF)
     A                                      COLOR(WHT)
     A N99        MSG602        30A  O  1 23MSGID(DIV 1209 SYMSGF)
     A                                      COLOR(WHT)
     A            WSTXT         11A  O  1 50COLOR(WHT)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A  99        MSG603        25A  O  3  2MSGID(DIV 1034 SYMSGF)
     A                                      COLOR(BLU)
     A N99        MSG604        25A  O  3  2MSGID(DIV 1033 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG605        25A  O  4  1MSGID(DIV 1210 SYMSGF)
     A                                      DSPATR(HI)
     A N99        MSG606        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A  99        MSG607        18A  O 23  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG608        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A N99        MSG609        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG610        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
