      ************************************************************************
      *  Info til transportnett om sending..                                 *
      *  Kjøres hver gang en godsliste printes (uavheng. av transp. mm)      *
      *  Programet aktiveres som ren kundespesial (se BHR / ITELLA m.fl)     *
      *    Hensikt er at Tr.nett skal vite hvem som "eier"/har interesse i   *
      *    et gitt sendingsnr, slik at de kan sende POD mm i retur...        *
      ************************************************************************
     FHEAD14    IF   E           K DISK
     FDOKUFM    IF   E           K DISK
     Ftrackl01  IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDTM96A   O  A E             DISK
     FEFTX96A   O  A E             DISK
     FECNI96A   O  A E             DISK
     FESTS96A   O  A E             DISK
     FEPCI96A   O  A E             DISK
     FEUNH      O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  WDsDate                1      8
     D  WDsTime                9     14
     D  WDsDatTim              1     12
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15S 2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
      *
     C                   EXSR      SRA
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      */////////////////////////////////////////////////////////////////////////
      *// HOVEDRUTINE                                                         //
      */////////////////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      * GET RECORDNUMBER FOR EDIM
     C     1             CHAIN     EDIC                               51
     C     *IN51         CABEQ     *ON           SRA9
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
      * ADD TO EDI-DB
     C                   EXSR      DTM00                                        Fritekst
     C*                  EXSR      FTX00                                        Fritekst
     C                   Z-ADD     0             WREP0
     C     UPRO2         SETLL     HEAD14
     C     UPRO2         READE     HEAD14                                 50
     C     *IN50         DOWEQ     '0'
     C                   EXSR      GETTT
     C                   EXSR      CNI04
     C     Dato0         IFNE      *zeros
     c                   move      dato0         DatTid           12
     c                   move      StKod0        STAKOD            3
     c                   move      StTxt0        STATTXT          35
     c*                  move      StÅKod0       STAÅrsakKod       3
     c*                  move      StÅTxt0       STAÅrsakTxt      35
     C                   EXSR      STS05                                        Transportdetaljer
     C                   end
     C     Dato1         IFNE      *zeros
     c                   move      dato1         DatTid
     c                   move      StKod1        STAKOD
     c                   move      StTxt1        STATTXT
     c*                  move      StÅKod1       STAÅrsakKod
     c*                  move      StÅTxt1       STAÅrsakTxt
     C                   EXSR      STS05                                        Transportdetaljer
     C                   end
      *
     c                   move      dato2         DatTid
     c                   move      StKod2        STAKOD
     c                   move      StTxt2        STATTXT
     c*                  move      StÅKod2       STAÅrsakKod
     c*                  move      StÅTxt2       STAÅrsakTxt
     C                   EXSR      STS05                                        Transportdetaljer
      *
     c                   move      dato3         DatTid
     c                   move      StKod3        STAKOD
     c                   move      StTxt3        STATTXT
     c*                  move      StÅKod3       STAÅrsakKod
     c*                  move      StÅTxt3       STAÅrsakTxt
     C                   EXSR      STS05                                        Transportdetaljer
     C     UPRO2         READE     HEAD14                                 50
     C                   END
      * WRITE TO EUNH
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTSTA'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '96A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'NOSM12'      U0057
     C*                  MOVEL     UPRO          U0068
     C                   MOVEL     '700'         U1001
     C                   MOVEL     UPRO          U1004
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
      * WRITE TO EDIM
     C                   CLEAR                   EDIMR
     C                   MOVEL     W0004         M0004
     C                   MOVEL     W0010         M0010
     C                   MOVE      ' '           M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVE      U0065         M0065
      * legg ut avd / tur / agent for å sjekke om sendt før
BHR  C*                  MOVEL     U0068         M0068
BHR  C                   MOVEL     W0068         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   Z-ADD     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   WRITE     EDIMR

     C     SRA9          TAG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Hent track-datoer 1,2,3,4,                         GETTT /
      *////////////////////////////////////////////////////////////
     C     GetTT         begsr
     C                   Move      *zeros        Dato0            12
     C                   Move      *zeros        Dato1            12
     C                   Move      *zeros        Dato2            12
     C                   Move      *zeros        Dato3            12
     C                   Move      *blanks       StKod0            3
     C                   Move      *blanks       StKod1            3
     C                   Move      *blanks       StKod2            3
     C                   Move      *blanks       StKod3            3
     C                   Move      *blanks       StTxt0           35
     C                   Move      *blanks       StTxt1           35
     C                   Move      *blanks       StTxt2           35
     C                   Move      *blanks       StTxt3           35
      *
     c     TrackKey      setll     trackl01
     c     TrackKey      reade     trackl01                               50
     c     *in50         doweq     *OFF
BHR   * 00-ED1 - WEB-registrert
     C                   select
BHR  C     TTacti        WhenEq    'ED1'
BHR  C                   move      TTDATE        WDsDate
BHR  C                   move      TTTIME        WDsTime
     C                   move      WDsDatTim     Dato0
BHR  C                   eval      StKod0  = 'Z00'
BHR  C                   eval      StTxt0  = 'Oppdrag registrert i '+
BHR  C                                       'WEB kundeløsning.'
BHR   * 01-ED2 - Hentet
BHR  C     TTacti        WhenEq    'ED2'
BHR  C                   move      TTDATE        WDsDate
BHR  C                   move      TTTIME        WDsTime
     C                   move      WDsDatTim     Dato1
BHR  C                   eval      StKod1  = 'Z01'
BHR  C                   eval      StTxt1  = 'Oppdrag hentet hos av'+
BHR  C                                       'sender.'
BHR   *
BHR   * 02-STR - Oppdrag registrert i sysped
BHR  C     TTacti        WhenEq    'STR'
BHR  C                   move      TTDATE        WDsDate
BHR  C                   move      TTTIME        WDsTime
     C                   move      WDsDatTim     Dato2
BHR  C                   eval      StKod2  = 'Z02'
BHR  C                   eval      StTxt2  = 'Mottatt terminal/o'+
BHR  C                                       'ppdrag registrert.'
BHR   *
BHR   * 03 XXX2 - Utgående/godsliste printet / skutt
BHR  C     TTacti        WhenEq    'MAN'
BHR  C     TTacti        OrEq      'MAS'
BHR  C                   move      TTDATE        WDsDate
BHR  C                   move      TTTIME        WDsTime
     C                   move      WDsDatTim     Dato3
BHR  C                   eval      StKod3  = 'Z03'
BHR  C                   eval      StTxt3  = 'Avgått terminal BHR  '
BHR  c                   endsl
BHR   *
     c     TrackKey      reade     trackl01                               50
     c                   enddo
     c                   endsr
      *////////////////////////////////////////////////////////////
      *  Dato for alle sendingene                           DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
      *
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   Z-ADD     1             DREP0
      *
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDATO         D2380
     C                   MOVEL     '102'         D2379
      *
     C                   WRITE     DTMR
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Consignment info                                   CNI04 /
      *////////////////////////////////////////////////////////////
     C     CNI04         BEGSR
      *
     C                   MOVE      *zeros        WREP1
     C                   MOVE      *zeros        WREP2
     C                   CLEAR                   CNIR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     4             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
      * 1490 sendindsnr (løpenr)
     C                   Z-ADD     CREP0         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C1490
      * 1004 Document/message number
     C                   MOVEL     HXSNDN        C1004
      *
     C                   WRITE     CNIR
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
      *  Statuser som logges                               sts05  /
      *////////////////////////////////////////////////////////////
     C     STS05         BEGSR
     C                   MOVE      *zeros        WREP2
      *
     C                   CLEAR                   STSR
     C                   Z-ADD     WMN           SMN
     C                   Z-ADD     5             SGN
     C                   Z-ADD     WREP0         SREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         SREP1
      *
     C                   MOVEL     STAKOD        S9011
      * En glipp i gjør at element 9010 ikke er med i STS-segmentet/ESTS96A
     C*                  MOVEL     STATTXT       S9010
      * senere bruk...
     C*                  MOVEL     STAÅrsakKod   S9013
     C*                  MOVEL     STAÅrsakTxt   S9012
      *
     C                   WRITE     STSR
      * Dato tid for hendelsen
     C                   EXSR      DTM05                                        Transp.utstyr
      * Hvilke kolli-ID inngår i hendelsen
     C                   EXSR      PCI05                                        Transp.utstyr
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Dato/tid for hendelsen                             DTM05 /
      *////////////////////////////////////////////////////////////
     C     DTM05         BEGSR
     C                   Z-ADD     0             WREP2
      *
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     5             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         DREP2
      *
     C                   MOVEL     '183'         D2005
     C                   MOVEL     DatTid        D2380
     C                   MOVEL     '203'         D2379
      *
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kolli ID                                           PCI05 /
      *////////////////////////////////////////////////////////////
     C     PCI05         BEGSR
     C                   Z-ADD     0             WREP2
      * KOLLI-ID...
      *
     C     DOKUFMKEY     SETLL     DOKUFM
     C     DOKUFMKEY     READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   CLEAR                   PCIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     5             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         PREP2
     C                   MOVEL     '18'          P4233
     C                   MOVEL     FMMRK1        P7102A
     C                   WRITE     PCIR
     C                   END
     C     DOKUFMKEY     READE     DOKUFM                                 33
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C     *LIKE         DEFINE    CMN           WMN
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2

      * DEF AV KEYS
     C     TrackKey      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     DOKUFMKEY     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     C                   MOVE      'F'           FMRI
     C                   z-add     1             FMFBNR
     C                   MOVE      *ZEROS        FMLINR
      * Input parameters
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8            AVS
      * Interchg ID sender
     C                   PARM                    W0004            35            AVS
      * Interchg ID Reciever
     C                   PARM                    W0010            35            MOTT
      * Comm. Acc. ref.
     C                   PARM                    W0068            35            MSG-ID
      *
     C                   Move      UPRO          UPRO2             8 0
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Minus value ?
     C     ØN152         IFLT      0                                            <-------------I
     C                   Z-SUB     ØN152         ØN152                                        I
     C                   MOVE      '-'           ØSIGN             1                          I
     C                   ELSE                                                   <-------------I
     C                   MOVE      '+'           ØSIGN                                        I
     C                   ENDIF                                                  <-------------I
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'                                         <-------------I
     C                   MOVE      ØA152H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA152X        ØA12                                         I
     C                   MOVE      ',  '         ØA12                                         I
     C                   MOVE      ØA152D        ØA12                                         I
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C     Ø12(ØX)       IFEQ      ','                                           <-----------II
     C                   SUB       1             ØX
     C                   ENDIF                                                   <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C     ØSIGN         IFEQ      '-'                                          <-------------I
     C                   SUB       1             ØX                                           I
     C                   MOVE      '-'           Ø12(ØX)                                      I
     C                   ENDIF                                                  <-------------I
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
