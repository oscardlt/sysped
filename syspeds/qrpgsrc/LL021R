********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Når post er basert på plukkseddel vis mme info fra LLhead
10XXX * 02 JOV PTF10 Ved ny manuell linje ikke plukk forklarende tekst fra TYPE
********************************************************************
      *
     FLLVARE01  UF   E           K DISK
     FLLAGJD01  UF A E           K DISK
     FLLAGJL01  UF   E           K DISK
N01  FLLHEAD    IF   E           K DISK
     FLLHEAD07  IF   E           K DISK
     F                                     RENAME(LLHEADR:LLHEADR7)
     FKUFAST    IF   E           K DISK
     FLL021D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  WTIME                  1     14  0
     D  WTM                    1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  DATO                   1      8  0
     D  DTÅ                    1      4  0
     D  DTM                    5      6  0
     D  DTD                    7      8  0
     D                 DS
     D  WXTXT1                 1     14
     D  WXTXT2                16     30
     D  WXTEXT                 1     30
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LESKEY        SETLL     LLAGJD01
     C     START1        TAG
     C     LESKEY        READE(N)  LLAGJD01                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        10
     C     VDAVD         IFEQ      0
     C                   MOVE      VDTEXT        WXTEXT
     C                   ELSE
     C     LLH7K         CHAIN     LLHEAD07                           41
     C                   MOVE      *BLANKS       WXTEXT
     C                   MOVEL     VDTEXT        WXTXT1
     C                   MOVEL     LHPAK         WXTXT2
     C                   END
     C                   ADD       1             ZRECNO
     C     VDLANT        COMP      0                                    41
     C                   WRITE     SUBFIL
     C     LESKEY        READE(N)  LLAGJD01                               94
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
     C                   SETOFF                                       9830
      *
     C     *IN02         IFEQ      '1'
     C                   CALL      'LL030C'
     C                   GOTO      START3
     C                   END
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *
      * SETT FLAGG FOR OM EN SKAL STARTE SUBFIL-HENTING PÅ NY..
     C                   MOVE      'N'           STRSFL            1
      *** LAG NY
     C     *IN06         IFEQ      '1'
     C                   MOVEA     '100'         *IN(50)
     C                   EXSR      POST
     C     STRSFL        IFEQ      'J'
     C                   GOTO      START                                          NY LAGET
     C                   ELSE
     C                   GOTO      START3
     C                   END
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C     STRSFL        IFEQ      'J'
     C                   GOTO      START                                          SLETTET
     C                   ELSE
     C                   GOTO      START3
     C                   END
     C                   END
      *
     C     SLUTT         TAG
      *
     C     *IN12         IFEQ      *ON
     C                   MOVE      '2'           UFLAGG
     C                   ELSE
     C                   MOVE      '3'           UFLAGG
     C                   END
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       9998
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     *IN27         ANDEQ     '0'
     C     VDLDAT        ANDNE     99999999
     C     VDLTYP        ANDNE     'LAGOT'
     C     VDLTYP        ANDNE     'PLIST'
     C     VDST          ANDEQ     ' '
     C     DETKEY        CHAIN(N)  LLAGJD01                           98
     C   98              GOTO      SLHENT
      * IN50= NY POST,  IN51= PROTECT  52= SLETT
     C                   MOVEA     '000'         *IN(50)
     C                   EXSR      POST
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     *IN27         ANDEQ     '0'
     C     VDLDAT        ANDNE     99999999
     C     VDLTYP        ANDNE     'LAGOT'
     C     VDLTYP        ANDNE     'PLIST'
     C     VDST          ANDEQ     ' '
     C     DETKEY        CHAIN(N)  LLAGJD01                           98
     C   98              GOTO      SLHENT
      * IN50= NY POST,  IN51= PROTECT  52= SLETT
     C                   MOVEA     '011'         *IN(50)
     C                   EXSR      POST
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C  N98WSNR          IFEQ      '5'
     C     DETKEY        CHAIN(N)  LLAGJD01                           98
     C   98              GOTO      SLHENT
      * IN50= NY POST,  IN51= PROTECT  52= SLETT
     C                   MOVEA     '010'         *IN(50)
     C                   EXSR      POST
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
     C                   ENDSR
      *
     C***********************************************
     C     POST          BEGSR
     C***********************************************
      * IN50= NY POST,  IN51= PROTECT  52= SLETT (WSNR='4')
      *   99= MINST EN OPTION ER UTFØRT
     C                   MOVE      '1'           *IN99
N01  c*basert på plukkseddel - hent div info
N01  c                   clear                   LHALN
N01  c                   clear                   LHBNA
N01  c                   clear                   LHFLN
N01  c                   clear                   LHORDR
N01  c                   clear                   LHPAK
      *
     C     *IN50         IFEQ      '0'
N01  c     VDMNL         ifne      *zeros
N01  c     VDMNL         chain     LLHEAD
N01  c                   endif
     C                   MOVE      VDLDAT        WSLDAT
     C                   MOVE      VDSAE0        WSSAE0
     C                   MOVE      VDSAE1        WSSAE1
     C                   MOVE      VDSAE2        WSSAE2
     C                   MOVE      VDSAE3        WSSAE3
     C                   MOVE      VDLANT        WSLANT
     C                   MOVE      VDLANT        XSLANT            5 0
     C                   MOVE      VDTEXT        WSTEXT
     C                   MOVE      VDLTYP        WSLTYP
     C                   MOVE      VDDAGE        WSDAGE
     C                   MOVE      VDAVD         WSAVD
     C                   MOVE      VDOPD         WSOPD
     C                   MOVE      VDFBNR        WSFBNR
     C                   ELSE
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      IDAG          WSLDAT
     C                   MOVE      *ZEROS        WSLANT
     C                   MOVE      *ZEROS        XSLANT
     C                   MOVE      *ZEROS        WSSAE0
     C                   MOVE      *ZEROS        WSSAE1
     C                   MOVE      *ZEROS        WSSAE2
     C                   MOVE      *ZEROS        WSSAE3
     C                   MOVE      *ZEROS        VDLANT
     C                   MOVE      *ZEROS        VDSAE0
     C                   MOVE      *ZEROS        VDSAE1
     C                   MOVE      *ZEROS        VDSAE2
     C                   MOVE      *ZEROS        VDSAE3
     C                   MOVE      *BLANKS       WSTEXT
     C                   MOVE      *BLANKS       WSLTYP
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *ZEROS        WSOPD
     C                   MOVE      *ZEROS        WSFBNR
     C                   END
      *
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   MOVEA     '000000'      *IN(30)
      *
     C     BILTAG        TAG
     C                   EXFMT     BILDE
      *
     C     *IN04         IFEQ      '1'
     C                   CALL      'SYKUKD'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    WSLTYP
     C                   GOTO      BILTAG
     C                   END
      *
     C     *IN02         IFEQ      '1'
     C                   CALL      'LL030C'
     C                   GOTO      BILTAG
     C                   END
      * AVBRUDD (ELLER ENTER VED OPT 5)
     C     *IN12         IFEQ      '1'
     C     WSNR          OREQ      '5'
     C                   GOTO      ENDPOS
     C                   END
      * SLETTING
     C     WSNR          IFEQ      '4'
     C     DETKEY        CHAIN     LLAGJD01                           89
     C     *IN89         IFEQ      '0'
     C     LESKEY        CHAIN     LLAGJL01                           33
     C     *IN33         IFEQ      '0'
     C     VADATO        IFNE      99999999
     C     LLVARK        CHAIN     LLVARE01                           33
     C                   ADD       VDLANT        LVSALD
     C                   UPDATE    LLVARER
     C                   END
     C                   ADD       VDLANT        VASALD
     C                   ADD       VDSAE0        VASAE0
     C                   ADD       VDSAE1        VASAE1
     C                   ADD       VDSAE2        VASAE2
     C                   ADD       VDSAE3        VASAE3
     C                   UPDATE    LLAGJR
     C                   END
     C                   DELETE    LLAGJDR
     C                   MOVE      'J'           STRSFL
     C                   END
     C                   GOTO      ENDPOS
     C                   END
      *
      * ENDRING.. GML POST MÅ FINNES...
     C     *IN50         IFEQ      '0'
     C     DETKEY        CHAIN     LLAGJD01                           89
     C   89              GOTO      ENDPOS
     C                   END
      *
      *
     C                   MOVEA     '000000'      *IN(30)
     C* TEST
      * ANTALL UTTATT...
     C                   EXSR      TESTB
     C   30              GOTO      BILTAG
      * DATO...
     C     WSLDAT        IFEQ      *ZEROS
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   MOVE      'JOV0816'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      BILTAG
     C                   END
      * DOKUMENTASJON ENTEN AVD/OPD/FBNR ELLER FRITEKST
     C     WSTEXT        IFEQ      *BLANKS
     C     WSAVD         ANDEQ     *ZEROS
s02  C*                  Z-ADD     9             LINNBR
N02  C                   Z-ADD     10            LINNBR
     C                   Z-ADD     16            POSNBR
     C                   MOVE      'JOV0819'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      BILTAG
     C                   END
      *
      * OPPDATER PÅ HEADFIL
     C                   EXSR      OPDAGF
     C   30              GOTO      BILTAG
      *
      * HENT NESTE NR VED NYREGISTR.
     C     *IN50         IFEQ      '1'
     C                   MOVE      999           VDVN
     C     DETKEY        SETLL     LLAGJD01
     C     LESKEY        READPE    LLAGJD01                               33
     C  N33              UPDATE    LLAGJDR
     C   33              MOVE      *ZEROS        VDVN
     C                   ADD       1             VDVN
     C                   END
      * Skriver posten
     C                   MOVE      WSLDAT        VDLDAT
     C                   MOVE      WSLANT        VDLANT
     C                   MOVE      WSSAE0        VDSAE0
     C                   MOVE      WSSAE1        VDSAE1
     C                   MOVE      WSSAE2        VDSAE2
     C                   MOVE      WSSAE3        VDSAE3
     C                   MOVE      WSTEXT        VDTEXT
     C                   MOVE      WSLTYP        VDLTYP
     C                   MOVE      WSDAGE        VDDAGE
     C                   MOVE      WSAVD         VDAVD
     C                   MOVE      WSOPD         VDOPD
     C                   MOVE      WSFBNR        VDFBNR
     C                   MOVE      ZUSER         VDBUSE
     C                   TIME                    WTIME
     C                   MOVE      WDD           DTD
     C                   MOVE      WMM           DTM
     C                   MOVE      WÅÅ           DTÅ
     C                   Z-ADD     DATO          VDBDAT
     C                   Z-ADD     WTM           VDBKL
     C     *IN50         IFEQ      '0'
     C                   UPDATE    LLAGJDR
     C                   ELSE
     C                   MOVE      *ZEROS        VDMNL
     C                   MOVE      *ZEROS        VDVNL
     C                   MOVE      *BLANKS       VDST
     C                   WRITE     LLAGJDR
     C                   MOVE      'J'           STRSFL
     C                   END
      *
     C     ENDPOS        TAG
      *
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     WSLANT        IFEQ      0
     C     WSSAE0        ANDEQ     0
     C     WSSAE1        ANDEQ     0
     C     WSSAE2        ANDEQ     0
     C     WSSAE3        ANDEQ     0
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0586'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   GOTO      SLTST
     C                   END
      *
     C                   MOVE      'N'           XBEGGE            1
     C     WSLANT        IFNE      0
     C     WSSAE0        IFNE      0
     C     WSSAE1        ORNE      0
     C     WSSAE2        ORNE      0
     C     WSSAE3        ORNE      0
     C                   MOVE      'J'           XBEGGE
     C                   MOVE      WSLANT        GMLANT
     C                   MOVE      *ZEROS        WSLANT
     C                   END
     C                   END
      *
      * BEREGN ANTALL STYKK ELLER ANDRE ALTERNATIVE ENHETER.
     C     WSLANT        IFEQ      0
     C                   Z-ADD     WSSAE0        WSLANT
     C     WSSAE1        IFNE      0
     C     WSSAE1        MULT      VAAN01        XNT05             7 0
     C                   ADD       XNT05         WSLANT
     C                   END
     C     WSSAE2        IFNE      0
     C     WSSAE2        MULT      VAAN02        XNT05
     C                   ADD       XNT05         WSLANT
     C                   END
     C     WSSAE3        IFNE      0
     C     WSSAE3        MULT      VAAN03        XNT05
     C                   ADD       XNT05         WSLANT
     C                   END
     C                   MOVE      *ZEROS        XNT05
     C                   ELSE
     C     VAAN03        IFGT      0
     C     WSLANT        DIV       VAAN03        WSSAE3
     C                   MVR                     WSSAE0
     C                   ELSE
     C                   MOVE      WSLANT        WSSAE0
     C                   END
     C     VAAN02        IFGT      0
     C     WSSAE0        DIV       VAAN02        WSSAE2
     C                   MVR                     WSSAE0
     C                   END
     C     VAAN01        IFGT      0
     C     WSSAE0        DIV       VAAN01        WSSAE1
     C                   MVR                     WSSAE0
     C                   END
     C                   END
      *
     C     XBEGGE        IFEQ      'J'
     C     WSLANT        ANDNE     GMLANT
     C                   MOVE      GMLANT        WSLANT
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0600'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   GOTO      SLTST
     C                   END
      *
     C     WSLANT        IFGT      VASALD
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0594'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   GOTO      SLTST
     C                   END
      *
      * TEST OM ENHET IKKE SKAL BRUKES.
     C     VAAN01        IFEQ      0
     C     WSSAE1        ANDNE     0
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0592'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     60            POSNBR
     C                   END
     C     VAAN02        IFEQ      0
     C     WSSAE2        ANDNE     0
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0592'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     60            POSNBR
     C                   END
     C     VAAN03        IFEQ      0
     C     WSSAE3        ANDNE     0
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0592'     MSGNR
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     60            POSNBR
     C                   END
     C   30              GOTO      SLTST
      *
      * TEST ANTALL PASSER MED LAVESTE SALGSENEHT
     C                   SELECT
     C     VAMIN         WHENGT    0
     C     WSSAE0        ANDGT     0
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0593'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     60            POSNBR
     C     LVMIN         WHENEQ    1
     C     WSSAE1        COMP      0                                      30
     C   30VAAN02        IFGT      0
     C     WSSAE2        COMP      0                                      30
     C                   END
     C   30VAAN03        IFGT      0
     C     WSSAE3        COMP      0                                      30
     C                   END
     C   30              DO
     C                   MOVE      'YBC0589'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     60            POSNBR
     C                   END
     C     VAMIN         WHENEQ    2
     C     WSSAE1        IFNE      0
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     60            POSNBR
     C                   MOVE      'YBC0587'     MSGNR
     C                   ELSE
     C     WSSAE2        COMP      0                                      30
     C   30VAAN03        IFGT      0
     C     WSSAE3        COMP      0                                      30
     C                   END
     C   30              Z-ADD     9             LINNBR
     C   30              Z-ADD     60            POSNBR
     C   30              MOVE      'YBC0588'     MSGNR
     C                   END
     C     VAMIN         WHENEQ    3
     C                   SELECT
     C     WSSAE1        WHENNE    0
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     60            POSNBR
     C                   MOVE      'JOV0844'     MSGNR
     C     WSSAE2        WHENNE    0
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     60            POSNBR
     C                   MOVE      'YBC0590'     MSGNR
     C     WSSAE3        WHENEQ    0
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     60            POSNBR
     C                   MOVE      'YBC0591'     MSGNR
     C                   ENDSL
     C                   ENDSL
      *
     C     *IN30         IFEQ      '0'
     C     WSLTYP        COMP      'LAGOT'                                30
     C  N30WSLTYP        COMP      'PLIST'                                30
     C  N30KUFASK        CHAIN     KUFAST                             30
     C     *IN30         IFEQ      '1'
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   MOVE      'YBC0865'     MSGNR
s02  C*                  ELSE
s02  C*    WSTEXT        IFEQ      *BLANKS
s02  C*                  MOVEL     KFTXT         WSTEXT
s02  C*                  END
     C                   END
     C                   END
      *
     C     SLTST         ENDSR
      *
     C***********************************************
     C     OPDAGF        BEGSR
     C***********************************************
     C     LESKEY        CHAIN     LLAGJL01                           33
      *
     C     WSSAE0        IFLE      VASAE0                                       1<-B
     C     WSSAE1        ANDLE     VASAE1
     C     WSSAE2        ANDLE     VASAE2
     C     WSSAE3        ANDLE     VASAE3
     C                   SUB       WSSAE0        VASAE0
     C                   SUB       WSSAE1        VASAE1
     C                   SUB       WSSAE2        VASAE2
     C                   SUB       WSSAE3        VASAE3
     C                   ELSE                                                   1
      * BEREGN ANDRE ALT. ENHETER
     C                   MOVE      WSLANT        WSSAE0
      * ALT. 3
     C     VAAN03        IFNE      0                                             2<-B
     C     WSSAE0        DIV       VAAN03        WSSAE3
     C                   MVR                     WSSAE0
     C     WSSAE3        IFGT      VASAE3                                         3<-B
     C     WSSAE3        SUB       VASAE3        XNT05
     C                   MULT      VAAN03        XNT05
     C                   ADD       XNT05         WSSAE0
     C                   Z-ADD     VASAE3        WSSAE3
     C                   MOVE      *ZEROS        VASAE3
     C                   ELSE                                                     3
     C                   SUB       WSSAE3        VASAE3
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * ALT. 2
     C     VAAN02        IFNE      0                                             2<-B
     C     WSSAE0        DIV       VAAN02        WSSAE2
     C                   MVR                     WSSAE0
      *
     C     STOL01        TAG
     C     WSSAE2        IFGT      VASAE2                                         3<-B
     C     WSSAE2        MULT      VAAN02        XNT05
     C                   ADD       WSSAE0        XNT05
     C     VASAE2        MULT      VAAN02        XNT052            7 0
     C     VASAE1        MULT      VAAN01        XNT053            7 0
     C                   ADD       XNT053        XNT052
     C                   ADD       VASAE0        XNT052
     C     XNT05         IFGT      XNT052                                          4<-B
     C                   SUB       1             VASAE3
     C                   ADD       VAAN23        VASAE2
     C                   GOTO      STOL01
     C                   END                                                       4<-E
      *
     C     WSSAE2        SUB       VASAE2        XNT05
     C                   MULT      VAAN02        XNT05
     C                   ADD       XNT05         WSSAE0
     C                   Z-ADD     VASAE2        WSSAE2
     C                   MOVE      *ZEROS        VASAE2
     C                   ELSE                                                     3
     C                   SUB       WSSAE2        VASAE2
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * ALT. 1
     C     VAAN01        IFNE      0                                             2<-B
     C     WSSAE0        DIV       VAAN01        WSSAE1
     C                   MVR                     WSSAE0
      *
     C     STOL02        TAG
     C     WSSAE1        IFGT      VASAE1                                         3<-B
     C     WSSAE1        MULT      VAAN01        XNT05
     C                   ADD       WSSAE0        XNT05
     C     VASAE1        MULT      VAAN01        XNT052
     C                   ADD       VASAE0        XNT052
     C     XNT05         IFGT      XNT052                                          4<-B
     C     VASAE2        IFEQ      0                                                5<-B
     C                   SUB       1             VASAE3
     C                   IF        VAAN12 = 0                                        6<-B
     C                   ADD       VAAN23        VASAE1
     C                   ELSE                                                        6
     C                   ADD       VAAN23        VASAE2
     C                   END                                                         6<-E
     C                   END                                                        5<-E
     C                   IF        VAAN12 <> 0                                      5<-B
     C                   SUB       1             VASAE2
     C                   ADD       VAAN12        VASAE1
     C                   END                                                        5<-E
     C                   GOTO      STOL02
     C                   END                                                       4<-E
      *
     C     WSSAE1        SUB       VASAE1        XNT05
     C                   MULT      VAAN01        XNT05
     C                   ADD       XNT05         WSSAE0
     C                   Z-ADD     VASAE1        WSSAE1
     C                   MOVE      *ZEROS        VASAE1
     C                   ELSE                                                     3
     C                   SUB       WSSAE1        VASAE1
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * LØSE STYKK
     C     STOL03        TAG
     C     WSSAE0        IFGT      VASAE0                                        2<-B
     C                   MOVE      VASAE0        XASAE0            7 0
     C                   SELECT
     C     VAAN01        WHENEQ    0
     C     VAAN12        ANDEQ     0
     C                   SUB       1             VASAE3
     C                   ADD       VAAN03        VASAE0
     C     VAAN12        WHENEQ    0
     C                   SUB       1             VASAE3
     C                   ADD       VAAN23        VASAE1
     C                   SUB       1             VASAE1
     C                   ADD       VAAN01        VASAE0
     C     VAAN01        WHENEQ    0
     C                   SUB       1             VASAE3
     C                   ADD       VAAN23        VASAE2
     C                   SUB       1             VASAE2
     C                   ADD       VAAN12        VASAE0
     C                   OTHER
     C     VASAE1        IFEQ      0                                              3<-B
     C     VASAE2        IFEQ      0                                               4<-B
     C                   SUB       1             VASAE3
     C                   ADD       VAAN23        VASAE2
     C                   END                                                       4<-E
     C                   SUB       1             VASAE2
     C                   ADD       VAAN12        VASAE1
     C                   END                                                      3<-E
     C     VAAN01        IFEQ      0                                              3<-B
     C                   MOVE      VASAE1        VASAE0
     C                   MOVE      *ZEROS        VASAE1
     C                   ELSE                                                     3
     C                   SUB       1             VASAE1
     C                   ADD       VAAN01        VASAE0
     C                   END                                                      3<-E
     C                   END
     C     VASAE0        IFEQ      XASAE0                                         3<-B
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   MOVE      'YBC1846'     MSGNR
     C                   MOVE      *ON           *IN30
     C     LESKEY        CHAIN     LLAGJL01                           33
     C                   UPDATE    LLAGJR
     C                   GOTO      SLOPDA
     C                   END                                                      3<-E
     C                   GOTO      STOL03
     C                   END                                                     2<-E
      *
     C                   SUB       WSSAE0        VASAE0
     C                   END                                                    1<-E
      *
     C     VADATO        IFNE      99999999
     C     LLVARK        CHAIN     LLVARE01                           33
     C                   SUB       WSLANT        LVSALD
     C                   UPDATE    LLVARER
     C                   END
     C                   SUB       WSLANT        VASALD
     C                   ADD       XSLANT        VASALD
     C                   UPDATE    LLAGJR
      *
     C     SLOPDA        TAG
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    WSLANT        GMLANT
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGRUP             8 0
     C                   PARM                    UKJED             8 0
     C                   PARM                    UMN               7 0
     C                   PARM                    UFLAGG            1
      * UFLAGG INN = UPDATEMODUS (27 ON = SPØRRING)
      * UFLAGG UT  = AVSLUTTING  ('3'=F3 '2'=F12)
     C     UFLAGG        COMP      'J'                                2727
      *
     C     LESKEY        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    UMN
     C     DETKEY        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    VDMN
     C                   KFLD                    VDVN
     C     LLVARK        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    VAANR
     C     LLH7K         KLIST
     C                   KFLD                    VDAVD
     C                   KFLD                    VDOPD
     C     KUFASK        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    WSLTYP
     C                   MOVEL     'LAGDETAL'    KFTYP
     C                   MOVE      'JT'          KFTYP
     C                   MOVE      'J'           UUPDAT            1
      *
      * HENT INFO FRA HEADINGPOSTEN I LLAGJF
     C     LESKEY        CHAIN(N)  LLAGJL01                           33
     C                   MOVE      VAGRUP        VDGRUP
     C                   MOVE      VAKJED        VDKJED
     C                   MOVE      VAMN          VDMN
     C     LLVARK        CHAIN(N)  LLVARE01                           33
      *
     C                   TIME                    WTIME
     C                   MOVE      WDD           DTD
     C                   MOVE      WMM           DTM
     C                   MOVE      WÅÅ           DTÅ
     C                   Z-ADD     DATO          IDAG              8 0
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
