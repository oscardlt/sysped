     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  7  3VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            STIPNF    R        O  7  8REFFLD(STIR/STIPNF *LIBL/FRSTIS)
     A                                  7 14'->'
     A            STIPNT    R        O  7 17REFFLD(STIR/STIPNT *LIBL/FRSTIS)
     A            STIHF     R        O  7 30REFFLD(STIR/STIHF  *LIBL/FRSTIS)
     A            STIFF     R        O  7 46REFFLD(STIR/STIFF  *LIBL/FRSTIS)
     A            STIVF     R        O  7 61REFFLD(STIR/STIVF  *LIBL/FRSTIS)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYFB07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 JOV0732 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(020 JOV0733 *LIBL/SYMSGF)
     A            STIFRA    R        O  3 23REFFLD(STIR/STIFRA *LIBL/FRSTIS)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(078 JOV0734 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG206        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 JOV0734 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSIPNF         4  0B  2  8TEXT('GEBYRKODE')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSIPNT         4  0B  2 17EDTCDE(4)
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSIHF          3  0B  2 30EDTCDE(4)
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WSIFF          3  0B  2 46EDTCDE(4)
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A            WSIVF          3  0B  2 61EDTCDE(4)
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2IPNF    R        I  2 15REFFLD(STIR/STIPNF *LIBL/FRSTIS)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2MSGCON(078 JOV0734 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            STIPNF    R        O  2  8REFFLD(STIR/STIPNF *LIBL/FRSTIS)
     A                                  2 14'->'
     A            STIPNT    R        O  2 17REFFLD(STIR/STIPNT *LIBL/FRSTIS)
     A            STIHF     R        O  2 30REFFLD(STIR/STIHF *LIBL/FRSTIS)
     A            STIFF     R        O  2 46REFFLD(STIR/STIFF *LIBL/FRSTIS)
     A            STIVF     R        O  2 61REFFLD(STIR/STIVF *LIBL/FRSTIS)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SYFB07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 JOV0732 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A            STIFRA    R        O  3 23REFFLD(STIR/STIFRA *LIBL/FRSTIS)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(078 JOV0734 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG511        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(020 JOV0733 *LIBL/SYMSGF)
