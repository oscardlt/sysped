      *********************************************************************
      * Endre artikkel
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS157 MODULE(*LIBL/SS157
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSORF    IF   E           k DISK    USROPN
     FGSSAKF    UF   E           k DISK    USROPN
     FLLVARE01  IF   E           K DISK    USROPN
     FLLGRPK05  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSANR           S             15
     D WSONO           s              7
     D W2ONO           s              7  0
     D WSLNO           s              3
     D W2LNO           s              3  0
     D WSSTK           s              5
     D W2STK           s              5  0
     D WSSTKM          s              5
     D W2STKM          s              5  0
     D WSTXT           s             30
     D WKEY            S            150
     D LAGREART        S              1
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL01          C                   CONST('Article no. is not valid.')
     D FEIL02          C                   CONST('You have to give ordered -
     D                                     pieces.')
     D lstbrd          s              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     c     LAGREART      IFEQ      'Y'
     c                   exsr      tstfeil
     c                   ELSE
     c     GSSAKFK       chain(N)  GSSAKF                             33
     c                   EVAL      WSANR = AKANR
     c                   EVAL      W2STK = AKSTK
     c                   EVAL      W2STKM = AKSTKM
     c                   EVAL      WSTXT = AKTXT
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
      * Test artikkelnr
     c     LLVARE01K     CHAIN     LLVARE01                           33
     C                   IF        *IN33
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
     c     W2STK         COMP      0                                      33
     C                   IF        *IN33
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * ALT OK - Oppdater  - Deretter avslutt
     C                   MOVE      'Y'           AVSLUTT
      * Hvis endring..
     c     GSSAKFK       chain     GSSAKF                             33
     c                   IF        *IN33 = *OFF
     c                   EVAL      AKANR = WSANR
     c                   EVAL      AKSTK = W2STK
     c                   EVAL      AKSTKM = W2STKM
     c                   EVAL      AKTXT = WSTXT
     c                   UPDATE    GSSAKFR
     c                   end
      *
     C     UtTest        endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      WKEY    = zhbgetvar('WKEY')
     C                   EVAL      WSONO   = zhbgetvar('ORONO')
     C                   eval      W2ONO   = c2n2(WSONO)
     C                   EVAL      WSLNO   = zhbgetvar('WSLNO')
     C                   eval      W2LNO   = c2n2(WSLNO)
     C                   EVAL      WSANR   = zhbgetvar('WSANR')
     C                   EVAL      WSSTK   = zhbgetvar('WSSTK')
     C                   eval      W2STK   = c2n2(WSSTK)
     C                   EVAL      WSSTKM  = zhbgetvar('WSSTKM')
     C                   eval      W2STKM  = c2n2(WSSTKM)
     C                   EVAL      WSTXT   = zhbgetvar('WSTXT')
     C                   eval      LAGREART= zhbgetvar('LAGREART')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   callp     updhtmlvar('WKEY':WKEY)
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(W2ONO:'Z')))
     C                   callp     updhtmlvar('WSLNO':
     C                             %trim(%editc(W2LNO:'Z')))
     C                   callp     updhtmlvar('WSGRP':
     C                             %trim(%editc(GKGRP:'Z')))
     C                   callp     updhtmlvar('WSPNR':
     C                             %trim(%editc(GKPNR:'Z')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
     C                   callp     updHtmlVar('WSANR':WSANR)
     C                   callp     updhtmlvar('WSSTK':
     C                             %trim(%editc(W2STK:'Z')))
     C                   callp     updhtmlvar('WSSTKM':
     C                             %trim(%editc(W2STKM:'Z')))
     C                   callp     updHtmlVar('WSTXT':WSTXT)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS157'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSORF
     C                   OPEN      GSSAKF
     C                   open      LLVARE01
     C                   OPEN      LLGRPK05
      *
     C     LLVARE01K     klist
     C                   kfld                    GKGRP
     C                   kfld                    GKPNR
     C                   kfld                    WSANR
     C     GSSAKFK       klist
     C                   kfld                    W2ONO
     C                   kfld                    W2LNO
      *
     C     W2ONO         CHAIN     GSSORF                             33
     C     ORCNO         CHAIN     LLGRPK05                           33
      *
     c                   move      *blanks       feilmelding
     c                   move      *blanks       fmeldnr
     C                   MOVE      'N'           AVSLUTT           1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSAKF
     C                   close     LLVARE01
     C                   close     LLGRPK05
     C                   eval      *inlr = *on
      *
     C                   endsr
