      *
      ***********************************************************
      *** PROGRAM SKAL REGISTRERE EKSPR.FORT. MANIFEST (HODE) ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-02 06-08 10 11 13-26
      *** LEDIGE INDIKATORER 27-29 31 32 34-46 50-79          ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FTRACKL09  IF A E           K DISK
     FSADEFL01  UF A E           K DISK
     FSADEFDEF  IF   E           K DISK
     FTURER14   IF   E           K DISK
     FTELL      UF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FKODTTST   IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FSAD131D   CF   E             WORKSTN INFDS(FBAREA)
     D                SDS
     D  UUSR                 254    263
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSETAD                 1      2  0
     D  WSETAM                 3      4  0
     D  WSETAA                 5      6  0
     D  WSETA                  1      6  0
     D                 DS
     D  EFETAC                 1      2  0
     D  EFETAA                 3      4  0
     D  EFETAM                 5      6  0
     D  EFETAD                 7      8  0
     D  EFETA                  1      8  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRA                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  EFDTRC                 1      2  0
     D  EFDTRA                 3      4  0
     D  EFDTRM                 5      6  0
     D  EFDTRD                 7      8  0
     D  EFDTR                  1      8  0
     D                 DS
     D  WSSJADTD               1      2  0
     D  WSSJADTM               3      4  0
     D  WSSJADTA               5      6  0
     D  WSSJADT                1      6  0
     D                 DS
     D  EFSJADTC               1      2  0
     D  EFSJADTA               3      4  0
     D  EFSJADTM               5      6  0
     D  EFSJADTD               7      8  0
     D  EFSJADT                1      8  0
     D                 DS
     D  WSETMH                 1      2  0
     D  WSETMM                 3      4  0
     D  WSETMS                 5      6  0
     D  WSETM                  1      4  0
     D  WSETM6                 1      6  0
     D                 DS
     D  WSTMT                  1     50
     D  WSTMT_30               1     30
     D                 DS
     D  WSKTYPT                1     50
     D  WSKTYPT_30             1     30
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
      *
      *  Prototype for 'SOEKKUN'
     d SOEKKUN         pr                  extpgm('SOEKKUN')
     d wsknd_                              like(wsknd)
     d fifirm_                             like(fifirm)
     d wsnad_                              like(wsnad)
      *  Prototype for 'SYFAKDA'
     d SYFAKDA         pr                  extpgm('SYFAKDA')
     d kftyp_                              like(kftyp)
     d uupdat_                             like(uupdat)
     d xkfkod_                             like(xkfkod)
      *  Prototype for 'SYFI10L'
     d SYFI10L         pr                  extpgm('SYFI10L')
     d wsklk_                              like(wsklk)
     d usped_                              like(usped)
      *  Prototype for 'SAD061J'
     d SAD061J         pr                  extpgm('SAD061J')
     d wstsd_                              like(wstsd)
      *  Prototype for 'TESTDATO'
     d TESTDATO        pr                  extpgm('TESTDATO')
     d xdg_                                like(XDG)
     d xmn_                                like(XMN)
     d xar_                                like(XAR)
     d uflagg_                             like(UFLAGG)
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'UUIDC'
     d UUIDC           pr                  extpgm('UUIDC')
     d efuuid_                             like(efuuid)
      *  Prototype for 'SAD132R'
     d SAD132R         pr                  extpgm('SAD132R')
     d efavd_                              like(efavd)
     d efpro_                              like(efpro)
     d efuuid_                             like(efuuid)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for SAD131R
     d sad131r         pr
     d uavd_                               like(uavd)
     d upro_                               like(upro)
     d usg_                                like(usg)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  *ENTRY Interface for Main Procedure
     d sad131r         pi
     d uavd                           4  0
     d upro                           8  0
     d usg                            3a
     d utype                          3a
     d uflagg                         1  0
      *
     d wdt             s              8a
     d wtm             s              6a
     d usped           s              1a
     d uupdat          s              1a
     d xkfkod          s                   like(kfkod)
     d xrecno          s              4  0
     d xidag           s              8  0
     d xtid            s              4  0
     d xdg             s              2  0
     d xmn             s              2  0
     d xar             s              2  0
     d i               s              3s 0
      *
      /free

         exsr init;

         dow *in03 = *off and *in12 = *off;
           if EFUUID = *blanks;
             *in47 = *off;
           else;
             *in47 = *on;
           endif;
           exfmt bilde01;
           //*in(83) = '00000000000000000';
           for i = 83 to 99;
             *in(i) = *off;
           endfor;
           *in30 = *off;

           select;
           when *in04;
             exsr soekdta;
           when *in12;
             if utype = 'CRT' or utype = 'CHG';
               chain (uavd: efpro) sadefl01;
               if %found;
                 efst = ' ';
                 update sadeffr;
               endif;
             endif;
           when *in03 = *off;
             if *in49 = *off;
               exsr testb1;
               if *in30 = *off;
                 exsr opdtur;
               endif;
             endif;
             if *in30 = *off;
               exsr toefc;
             endif;
           endsl;

         enddo;

         select;
         when *in03;
           uflagg = 3;
         when *in12;
           uflagg = 1;
         other;
           uflagg = 0;
         endsl;

         *inlr = *on;
         return;

      //*********************************************
       begsr soekdta;

       linnbr = wscloc / 256;
       posnbr = wscloc - (%int(linnbr) * 256);
       xrecno = wssubn;

         select;
         //when linnbr = 5 and posnbr >= 20 and posnbr <= 27;
         // SØK DEKLARANT
         //  soekkun (wsknd: fifirm: wsnad);
         //  if wsknd <> 0;
         //    exsr testd;
         //  endif;
         //  linnbr = 7;
         //  posnbr = 20;

         when linnbr = 7 and posnbr >= 20 and posnbr <= 22;
         // SØK transportmåte
           // xxxxxxx (wstm);
           linnbr = 8;
           posnbr = 20;

         when linnbr = 8 and posnbr >= 20 and posnbr <= 21;
         // SØK kjøretøy kode
           kftyp = 'SADEFBKODE';
           uupdat = 'N';
           xkfkod = wsktyp;
           syfakda (kftyp: uupdat: xkfkod);
           wsktyp = xkfkod;
           chain (kftyp: xkfkod) kofast;
           if %found;
             wsktypt = kftxt;
           else;
             wsktypt = *blanks;
           endif;
           linnbr = 9;
           posnbr = 20;

         when linnbr = 9 and posnbr >= 20 and posnbr <= 21;
         // SØK landkode
           syfi10l (wsklk: usped);
           linnbr = 10;
           posnbr = 20;

         when linnbr = 10 and posnbr >= 20 and posnbr <= 21;
         // SØK landkode
           syfi10l (wsplk: usped);
           linnbr = 11;
           posnbr = 20;

         when linnbr = 13 and posnbr >= 20 and posnbr <= 21;
         // SØK landkode
           syfi10l (wssjalk: usped);
           posnbr = 38;

         when linnbr = 15 and posnbr >= 20 and posnbr <= 23;
         // SØK tollsted
           sad061j (wstsd);
           posnbr = 44;

         other;
           msgnr = 'DIV1037';
           *in30 = *on;

         endsl;

       endsr;

      //*********************************************
       begsr testb1;

      //*** TEST AVDELING
       if *in48 = *off;
         chain(n) wsavd tell;
         if not %found;
           *in99 = *on;
         endif;
       endif;

      //*** TEST DEKLARANT
      // if wsknd <> 0;
      //   exsr testd;
      // endif;

       //if wsrgd = *blanks;
       //  *in97 = *on;
       //endif;

       select;
       when wstm = 'BIL';
         wstmt = '(30) Bil (veitransport)';
       other;
         *in96 = *on;
       endsl;

       kfkod = wsktyp;
       chain ('SADEFBKODE': kfkod) kofast;
       if %found;
         wsktypt = kftxt;
       else;
         *in95 = *on;
         wsktypt = *blanks;
       endif;

       chain ('S2 ': wsklk) kodts2;
       if not %found;
         *in94 = *on;
       endif;

       if wskmrk = *blanks;
         *in93 = *on;
       endif;

       if wsktyp = '01' or wsktyp = '02' or wsplk <> *blanks;
         chain ('S2 ': wsplk) kodts2;
         if not %found;
           *in92 = *on;
         endif;
       endif;

       if (wsktyp = '01' or wsktyp = '02') and wspmrk = *blanks
          or wspmrk = wskmrk;
         *in91 = *on;
       endif;

       if wssjaf = *blanks;
         *in90 = *on;
       endif;

       if wssjae = *blanks;
         *in89 = *on;
       endif;

       chain ('S2 ': wssjalk) kodts2;
       if not %found;
         *in88 = *on;
       endif;

       xar = wssjadta;
       xmn = wssjadtm;
       xdg = wssjadtd;
       testdato (xdg: xmn: xar: uflagg);
       if uflagg = 1;
         *in87 = *on;
       endif;

       xar = wsetaa;
       xmn = wsetam;
       xdg = wsetad;
       testdato (xdg: xmn: xar: uflagg);
       select;
       when uflagg = 0;
         EFETAD = WSETAD;
         EFETAM = WSETAM;
         EFETAA = WSETAA;
         EFETAC = 20;
         syge15c (wdt: wtm);
         xidag = %dec(wdt:8:0);
         xtid = %dec(%subst(wtm:1:4):4:0) + 100;
         select;
         when efeta < xidag;
           *in86 = *on;
         when efeta = xidag and wsetm < xtid;
           *in85 = *on;
         endsl;
       when uflagg = 1;
         *in86 = *on;
       endsl;

       if wsetmh >= 24 or wsetmm >= 60;
         *in85 = *on;
       endif;

       chain ('TST': wstsd) kodttst;
       if not %found;
         *in84 = *on;
       endif;

       if ws3039e = 0;
         *in83 = *on;
       endif;

       exsr hmsg;

       endsr;

      //***********************************************
       begsr hmsg;

       select;
       when *in99;
         msgnr = 'SPA0001';
         *in30 = *on;
         linnbr = 2;
         posnbr = 7;

       when *in98;
         msgnr = 'YBC2037';
         *in30 = *on;
         linnbr = 5;
         posnbr = 20;

       //when *in97;
       //  msgnr = 'YBC2038';
       //  *in30 = *on;
       //  linnbr = 5;
       //  posnbr = 39;

       when *in96;
         msgnr = 'YBC2082';
         *in30 = *on;
         linnbr = 7;
         posnbr = 20;

       when *in95;
         msgnr = 'YBC2083';
         *in30 = *on;
         linnbr = 8;
         posnbr = 20;

       when *in94;
         msgnr = 'YBC2084';
         *in30 = *on;
         linnbr = 9;
         posnbr = 20;

       when *in93;
         msgnr = 'YBC2085';
         *in30 = *on;
         linnbr = 9;
         posnbr = 38;

       when *in92;
         msgnr = 'YBC2086';
         *in30 = *on;
         linnbr = 10;
         posnbr = 20;

       when *in91;
         msgnr = 'YBC2087';
         *in30 = *on;
         linnbr = 10;
         posnbr = 38;

       when *in90;
         msgnr = 'YBC2088';
         *in30 = *on;
         linnbr = 11;
         posnbr = 20;

       when *in89;
         msgnr = 'YBC2089';
         *in30 = *on;
         linnbr = 12;
         posnbr = 20;

       when *in88;
         msgnr = 'YBC2090';
         *in30 = *on;
         linnbr = 13;
         posnbr = 20;

       when *in87;
         msgnr = 'YBC2091';
         *in30 = *on;
         linnbr = 13;
         posnbr = 38;

       when *in86;
         msgnr = 'YBC2092';
         *in30 = *on;
         linnbr = 14;
         posnbr = 20;

       when *in85;
         msgnr = 'YBC2093';
         *in30 = *on;
         linnbr = 14;
         posnbr = 36;

       when *in84;
         msgnr = 'SPK0042';
         *in30 = *on;
         linnbr = 15;
         posnbr = 20;

       when *in83;
         msgnr = 'YBC2087';
         *in30 = *on;
         linnbr = 15;
         posnbr = 44;

       endsl;

       endsr;

      //***********************************************
       begsr movefw;

       WSAVD    = EFAVD;
       WSDTRD   = EFDTRD;
       WSDTRM   = EFDTRM;
       WSDTRA   = EFDTRA;

       WSKND    = EFKND;
       CHAIN (FIFIRM: WSKND) CUNDL01;
       IF %FOUND;
         WSNAD = KNAVN;
       ELSE;
         WSNAD = *BLANKS;
       ENDIF;
       WSRGD    = EFRGD;
       WSTSD    = EFTSD;
       WS3039E  = EF3039E;
       //WSEID    = EFEID;
       WSTM     = EFTM;
       WSTMT    = EFTMT;
       WSKTYP   = EFKTYP;
       WSKTYPT  = EFKTYPT;
       WSKLK    = EFKLK;
       WSKMRK   = EFKMRK;
       WSPLK    = EFPLK;
       WSPMRK   = EFPMRK;
       WSSJAF   = EFSJAF;
       WSSJAE   = EFSJAE;
       WSSJALK  = EFSJALK;
       WSSJADTD = EFSJADTD;
       WSSJADTM = EFSJADTM;
       WSSJADTA = EFSJADTA;
       WSBEKR   = EFBEKR;
       WSETAD   = EFETAD;
       WSETAM   = EFETAM;
       WSETAA   = EFETAA;
       WSETM6   = EFETM;

       endsr;

      //***********************************************
       begsr movwef;

       EFDTRD   = WSDTRD;
       EFDTRM   = WSDTRM;
       EFDTRA   = WSDTRA;
       EFDTRC   = 20;
       EFKND    = WSKND;
       EFRGD    = WSRGD;
       EFTSD    = WSTSD;
       EF3039E  = WS3039E;
       //EFEID    = WSEID;
       EFTM     = WSTM;
       EFTMT    = WSTMT;
       EFKTYP   = WSKTYP;
       EFKTYPT  = WSKTYPT;
       EFKLK    = WSKLK;
       EFKMRK   = WSKMRK;
       EFPLK    = WSPLK;
       EFPMRK   = WSPMRK;
       EFSJAF   = WSSJAF;
       EFSJAE   = WSSJAE;
       EFSJALK  = WSSJALK;
       EFSJADTD = WSSJADTD;
       EFSJADTM = WSSJADTM;
       EFSJADTA = WSSJADTA;
       if WSSJADT <> 0;
         if WSSJADTA > 30;
           EFSJADTC = 19;
         else;
           EFSJADTC = 20;
         endif;
       endif;
       EFBEKR   = WSBEKR;
       EFETAD   = WSETAD;
       EFETAM   = WSETAM;
       EFETAA   = WSETAA;
       EFETAC   = 20;
       EFETM    = WSETM6;

       endsr;

      //***********************************************
       begsr testd;

       if wsknd = 0;               // FEIL: Kundenr er 0.
         *in98 = *on;
       else;
         chain (fifirm: wsknd) cundl01;
         if %found;                // OK: Hent info. fra kunderegister.
           wsnad  = knavn;
           if syfor9 <> *zeros;
             wsrgd = syrg;
           endif;
         else;                     // FEIL: Kundenr finnes ikke i kunderegister
           *in98 = *on;
         endif;
       endif;

       endsr;

      //***********************************************
       begsr opdtur;

       if efpro = 0;
         // henter neste nummer
         chain wsavd tell;
         efpro = teturn;
         upro  = teturn;
         teturn = teturn + 1;
         update tellr;
         efavd = wsavd;
         efst = ' ';
       endif;

       chain (wsavd: efpro) sadefl01;
       exsr ttsr;
       if efuuid = *blanks;
         uuidc (efuuid);
       endif;

       // FLYTTER SKJERMVARIABLE TIL FILVARIABLE
       exsr movwef;

       // ikke %found -> NY TUR
       if not %found (sadefl01);
         efavd  = wsavd;
         efsg   = usg;
         efdtr  = %dec(wdt:8:0);
         write sadeffr;
         wsdtrd = efdtrd;
         wsdtrm = efdtrm;
         wsdtra = efdtra;
       else;
         update sadeffr;
       endif;

       endsr;

      //***********************************************
       begsr toefc;

       sad132r (efavd: efpro: efuuid: utype: uflagg);
       select;
       when uflagg = 1;
         // *in12 = *on;
       when uflagg = 3;
         *in03 = *on;
       endsl;

       endsr;

      //***********************************************
       begsr init;

       syge15c (wdt: wtm);

       setll *loval firm;
       read firm;

       usped = 'N';
       wsdtr = 0;
       wsetad = 0;
       wssjadt = 0;
       wstmt = *blanks;
       wsktypt = *blanks;
       wsetm6 = 0;
       efdtr = 0;
       efetad = 0;
       efsjadt = 0;

       select;
       when utype = 'DSP';
         *in49 = *on;
       when utype = 'CHG';
         *in48 = *on;
       endsl;

       chain (uavd: upro) sadefl01;
       if %found;
         if *in49 = *off;
           if utype = 'CPY';
             update sadeffr;
             efst = ' ';
             efst2 = ' ';
             efpro = 0;
             efuuid = *blanks;
             efdtr = 0;
             efeta = 0;
             efetm = 0;
           else;
             efst = 'X';
             update sadeffr;
           endif;
         else;
           update sadeffr;
         endif;
       else;
         if utype = 'CRT';
           chain uavd sadefdef;
           if not %found;
             setll *loval sadefdef;
             read sadefdef;
           endif;
           efst2 = ' ';
           if upro <> 0;
             chain upro turer14;
             if %found;
               efpro  = upro;
               efkmrk = tubiln;
               efklk  = tulk;
               efpmrk = tuheng;
               efplk  = tulkh;
               if tuheng <> *blanks;
                 efktyp = '03';
               endif;
             endif;
           endif;
         endif;
         efavd = uavd;
         efdtr = 0;
         efeta = 0;
         efsjadt = 0;
       endif;
       exsr movefw;

       select;
       when efst2 = 'S';
         wsst2 = 'SUMBITTED';
       when efst2 = 'C';
         wsst2 = 'COMPLETED';
       when efst2 = 'D';
         wsst2 = 'DELETED';
       when efst2 = ' ';
         wsst2 = *blanks;
       endsl;

       write windowsz;

       endsr;

      //***********************************************
       begsr ttsr;

       setll efpro trackl09;
       reade efpro trackl09;
       dow not %eof;
         if ttacti = 'EFC';
           leave;
         endif;
         reade efpro trackl09;
       enddo;
       ttavd  = efavd;
       tttur  = efpro;
       ttacti = 'EFC';
       ttdate = %dec(wdt:8:0);
       ttdatl = ttdate;
       tttime = %dec(wtm:6:0);
       ttuser = uusr;
       if not %eof (trackl09);
         tttext = 'Ekspresfortolling manifest er endret.';
         tttexl = 'Ekspresfortolling manifest er endret.';
         ttedev = 'CHG';
       else;
         tttext = 'Ekspresfortolling manifest er laget.';
         tttexl = 'Ekspresfortolling manifest er laget.';
         ttedev = 'CRT';
       endif;

       write trackfr;

       endsr;

      /end-free
