      *
     FSAML      UF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFAK8      UF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYFA56FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  KNAVN                  1     30
     D  XNAVN                  1     20
     D  XDLT                   1      7
     D                 DS
     D  SADATO                 1      8  0
     D  SADATÅ                 1      4  0
     D  SADATM                 5      6  0
     D  SADATD                 7      8  0
     D                 DS
     D  WSDATO                 1      6  0
     D  WSDATÅ                 1      2  0
     D  WSDATM                 3      4  0
     D  WSDATD                 5      6  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      * Sett ind, for underutvalg  (*IN50 brukes i DSPF)
     C                   MOVE      '0'           *IN50                          EKSKLUDERT??
     C                   MOVE      'N'           UNDUTV            1
     C                   MOVE      'J'           INCL              1
     C     W2KN          IFNE      *ZEROS
     C     W2VAL         ORNE      *BLANKS
     C     W2AVD         ORNE      *ZEROS
     C     W2OPD         ORNE      *ZEROS
     C     W2PRO         ORNE      *ZEROS
     C     W2UR          ORNE      *BLANKS
     C     W2GRP         ORNE      *BLANKS
     C                   MOVE      'J'           UNDUTV
     C                   MOVE      ' '           INCL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     SAML
     C*
     C                   READ(N)   SAML                                   94
     C     *IN94         DOWEQ     '0'
     C                   MOVE      SADATD        WSDATD
     C                   MOVE      SADATM        WSDATM
     C                   MOVE      SADATÅ        WSDATÅ
     C     UNDUTV        IFEQ      'J'
     C                   EXSR      INCLUD
     C                   END
     C     INCL          IFEQ      'J'
     C                   ADD       1             ZRECNO
     C     KUNKEY        CHAIN     CUNDL01                            77
     C                   WRITE     SUBFIL
     C                   ELSE
     C                   MOVE      '1'           *IN50                           EKSKLUDERT
     C                   END
     C                   READ(N)   SAML                                   94
     C  N94              END
      *
     C                   MOVE      'J'           FRTOPP            1
      * Blank Underutvalg (slik at F5 gi alle)
     C                   EXSR      MOBLW2
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C* Ingen underutvalg, likevel ingen poster ->FORIGE uansett tast..
     C     *IN50         IFEQ      '0'
     C                   MOVE      '1'           *IN03
     C                   GOTO      SLUTT
     C                   END
     C                   ELSE
     C     FRTOPP        IFEQ      'J'
     C                   Z-ADD     1             ZRECNO
     C                   MOVE      'N'           FRTOPP            1
     C                   END
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9899
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** Underutvalg
     C     *IN08         IFEQ      '1'
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C                   GOTO      START
     C                   END
     C*
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
      * Enter - også uten utført valg - vis samme bilde/poster
     C                   GOTO      START3
     C*
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     INCLUD        BEGSR
     C***********************************************
     C                   MOVE      'J'           INCL
      *
     C     W2KN          IFNE      *ZEROS
     C     W2KN          ANDNE     SAKN
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2VAL         IFNE      *BLANKS
     C     W2VAL         ANDNE     SAVAL
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2AVD         IFNE      *ZEROS
     C     W2AVD         ANDNE     SAAVD
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2OPD         IFNE      *ZEROS
     C     W2OPD         ANDNE     SAOPD
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2PRO         IFNE      *ZEROS
     C     W2PRO         ANDNE     SAPRO
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2UR          IFNE      *BLANKS
     C     W2UR          ANDNE     SAUR
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2GRP         IFNE      *BLANKS
     C     W2GRP         ANDNE     SAGRP
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C*
     C     ENDINC        ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '2'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '5'                                          SE OPPDRAG
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      SEOPD
     C                   END
     C     WSNR          IFEQ      '6'                                          SE FAKTURA
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      SEFAK
     C                   END
     C*
     C                   END
     C*
     C     SLHENT        TAG
     C*
     C                   ENDSR
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VARELI
     C  N12              DO
     C     SAMLK         CHAIN     SAML                               33
     C                   MOVE      WSGRP         SAGRP
     C  N33              UPDATE    SAMLR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C  N12              DO
     C     SAMLK         CHAIN     SAML                               33
     C     *IN33         IFEQ      '0'
     C                   DELETE    SAMLR
     C     FAK8K         SETLL     FAK8
     C     FAK8K         READE     FAK8                                   33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      ' '           FAOPKO
     C                   UPDATE    FAKTR
     C     FAK8K         READE     FAK8                                   33
     C                   END
     C                   EXSR      MOBLWS
     C                   END
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
      *//////////////////////////////////////////////////////////////////////
      * VIEW ORDER                                                   CHGKB //
      *//////////////////////////////////////////////////////////////////////
     C     SEOPD         BEGSR
      *
     C                   MOVE      '1'           *IN99
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    SAAVD
     C                   PARM                    SAOPD
     C                   PARM                    DUMFLA
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *//////////////////////////////////////////////////////////////////////
      * VIEW Invoice picture                                         CHGKB //
      *//////////////////////////////////////////////////////////////////////
     C     SEFAK         BEGSR
      *
     C                   MOVE      '1'           *IN99
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    SAAVD
     C                   PARM                    SAOPD
     C                   PARM                    DUMFLA
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
     C*
     C***********************************************
     C     MOBLW2        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        W2KN
     C                   MOVE      *ZEROS        W2AVD
     C                   MOVE      *ZEROS        W2OPD
     C                   MOVE      *ZEROS        W2PRO
     C                   MOVE      *BLANKS       W2VAL
     C                   MOVE      *BLANKS       W2UR
     C                   MOVE      *BLANKS       W2GRP
     C                   ENDSR
     C***********************************************
     C     MOBLWS        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        SAKN
     C                   MOVE      *ZEROS        SAAVD
     C                   MOVE      *ZEROS        SAOPD
     C                   MOVE      *ZEROS        SAPRO
     C                   MOVE      *ZEROS        SADATO
     C                   MOVE      *ZEROS        WSDATO
     C                   MOVE      *BLANKS       SAVAL
     C                   MOVE      *BLANKS       SAUR
     C                   MOVE      *BLANKS       SAGRP
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVEL     '*DELETE'     KNAVN
     C*
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    SAKN
     C     SAMLK         KLIST
     C                   KFLD                    SAKN
     C                   KFLD                    SAVAL
     C                   KFLD                    SAAVD
     C                   KFLD                    SAOPD
     C     FAK8K         KLIST
     C                   KFLD                    SAAVD
     C                   KFLD                    SAOPD
     C                   KFLD                    XXFAKT
     C                   KFLD                    SAKN
     C                   KFLD                    SAVAL
     C     *LIKE         DEFINE    FAFAKT        XXFAKT
     C                   MOVE      *ZEROS        XXFAKT
      *
      * OPPDATER TURNR PÅ ALLE OPPDRAG
     C                   CALL      'SYFA27D'
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
