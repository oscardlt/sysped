      *
      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT SPEDISJONSJOURNAL  (EKSPORT) ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04-11 13-26                ***
      *** LEDIGE INDIKATORER 27-29 31-40 43-49 54-97          ***
      ***********************************************************
      *
     Fsysthlp   IF   E             DISK
     FKODTFM    IF   E           K DISK
     FSAEH1     IF   E           K DISK
     FSAEH6     IF   E           K DISK
     F                                     RENAME(SAEHR:SAEH6R)
     FSAD028FM  CF   E             WORKSTN
     FDATALIST  O    F  132        PRINTER OFLIND(*INOF)
     F                                     FORMLEN(48)
     F                                     FORMOFL(44)
     D                 DS
     D  XÅRF2                  1      4  0
     D  XMNDF2                 5      6  0
     D  XDAGF2                 7      8  0
     D  XDATOF                 1      8  0
     D                 DS
     D  XÅRT2                  1      4  0
     D  XMNDT2                 5      6  0
     D  XDAGT2                 7      8  0
     D  XDATOT                 1      8  0
     D                 DS
     D  XDAGF                  1      2  0
     D  XMNDF                  3      4  0
     D  XÅRF                   5      6  0
     D  WSDTF                  1      6  0
     D                 DS
     D  XDAGT                  1      2  0
     D  XMNDT                  3      4  0
     D  XÅRT                   5      6  0
     D  WSDTT                  1      6  0
     D                 DS
     D  SEKTA                  1      5  0
     D  SEKTB                  6      7  0
     D  XKT                    1      7  0
     D                 DS
     D  SEDTG                  1     10
     D  SEDTG8                 1      8
     C*
     C     *ENTRY        PLIST
     C     LASER         PARM      LASER         LASER             1
     C     STRING        PARM      STRING        STRING            5
     C     COPY          PARM      COPY          COPY              2 0
     C*
     C     STRING        IFNE      *BLANKS
     C     STRING        CHAIN     KODTFM                             33
     C                   END
     C     LASER         IFEQ      'A'
     C                   SETON                                        88
     C                   END
     C     SAEH1K        KLIST
     C                   KFLD                    WSAVDF
     C                   KFLD                    XDATOF
     C     SAEH6K        KLIST
     C                   KFLD                    WSAVDF
     C                   KFLD                    WSGNF
     C                   MOVE      *ZEROS        WSDTF
     C                   MOVE      *ZEROS        WSDTT
     C*
     C     START         TAG
     C                   EXFMT     BILDE01
     c   01              do
     c                   movel     'sad0281'     hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START
     c                   end
     C                   SETOFF                                       42
     C   03              GOTO      SLUTT
     C*
     C     WSVALG        COMP      1                                  999941
     C   99WSVALG        COMP      2                                  999942
     C   99              GOTO      START
     C                   EXSR      MOVBWS
     C   42              GOTO      START3
     C*
     C     START2        TAG
     C                   EXFMT     BILDE02
     c   01              do
     c                   movel     'sad0282'     hlpna
     c                   exsr      helpsr
     C                   GOTO      START2
     c                   end
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C   12              MOVE      *ZEROS        WSVALG
     C   12              GOTO      START
     C*
     C                   EXSR      TESTB2
     C   30              GOTO      START2
     C                   EXSR      PRINT
     C                   GOTO      SLUTT
     C*
     C     START3        TAG
     C                   EXFMT     BILDE03
     c   01              do
     c                   movel     'sad0283'     hlpna
     c                   exsr      helpsr
     C                   GOTO      START3
     c                   end
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C   12              MOVE      *ZEROS        WSVALG
     C   12              GOTO      START
     C*
     C                   EXSR      TESTB3
     C   30              GOTO      START3
     C                   EXSR      PRINT
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB2        BEGSR
     C***********************************************
     C     WSDTF         IFEQ      0
     C                   Z-ADD     010151        WSDTF
     C                   END
     C                   MOVE      XDAGF         XDAGF2
     C                   MOVE      XMNDF         XMNDF2
     C     XÅRF          IFGT      50
     C     XÅRF          ADD       1900          XÅRF2
     C                   ELSE
     C     XÅRF          ADD       2000          XÅRF2
     C                   END
     C                   MOVE      XDAGT         XDAGT2
     C                   MOVE      XMNDT         XMNDT2
     C     XÅRT          IFGT      50
     C     XÅRT          ADD       1900          XÅRT2
     C                   ELSE
     C     XÅRT          ADD       2000          XÅRT2
     C                   END
     C     XDATOF        COMP      XDATOT                             99
     C   99              SETON                                        30
     C     WSAVDF        COMP      *ZEROS                               9898
     C  N98WSAVDF        COMP      WSAVDT                             98
     C   98              SETON                                        30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TESTB3        BEGSR
     C***********************************************
     C     WSGNF         COMP      *BLANKS                                99
     C  N99WSGNF         COMP      WSGNT                              99
     C   99              SETON                                        30
     C     WSAVDF        COMP      *ZEROS                               9898
     C  N98WSAVDF        COMP      WSAVDT                             98
     C   98              SETON                                        30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBWS        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSDTF
     C                   MOVE      *ZEROS        WSDTT
     C                   MOVE      *BLANKS       WSGNF
     C                   MOVE      *BLANKS       WSGNT
     C                   MOVE      *BLANKS       WSKKUN
     C                   MOVE      *BLANKS       WSKUTS
     C                   MOVE      *ZEROS        WSAVDF
     C                   MOVE      *ZEROS        WSAVDT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C     WSKKUN        COMP      'K'                                    50
     C     WSKUTS        COMP      'X'                                    51
     C*
     C     WSAVDF        DOWLE     WSAVDT
     C   41SAEH1K        SETLL     SAEH1
     C   42SAEH6K        SETLL     SAEH6
     C                   SETOFF                                       305253
     C*
     C     STPRI         TAG
     C   41WSAVDF        READE     SAEH1                                  30
     C   42WSAVDF        READE     SAEH6                                  30
     C   41
     CANN30SEDT          COMP      XDATOT                             30
     C   42
     CANN30SEGN          COMP      WSGNT                              30
     C*
     C  N30              DO
     C   50XKT           COMP      *ZEROS                             5252
     C   51
     CANN52SEST          COMP      'P'                                5252
     C*
     C  N52              DO
     C  N53              EXCEPT    DLHEAD
     C  N53              SETON                                        53
     C   OF              EXCEPT    DLHEAD
     C                   ADD       1             XANTOP            5 0
     C                   ADD       SEVKB         XTKG              9 0
     C                   ADD       SENTK         XTNTK             9 0
     C                   EXCEPT    DLDET
     C                   END
     C*** END PÅ <N52 DO>
     C*
     C                   GOTO      STPRI
     C                   END
     C*** END PÅ <N30 DO>
     C*
     C   53
     CAN OF              EXCEPT    DLHEAD
     C   53              EXCEPT    DLTOT
     C                   MOVE      *ZEROS        XANTOP            5 0
     C                   MOVE      *ZEROS        XTKG              9 0
     C                   MOVE      *ZEROS        XTNTK             9 0
     C     WSAVDF        IFLT      9999
     C                   ADD       1             WSAVDF
     C                   ELSE
     C                   GOTO      SLPRI
     C                   END
     C                   END
     C*** END PÅ <WSAVDF  DOWLE  WSAVDT>
     C*
     C     SLPRI         ENDSR
     C***********************************************
     C     HELPSR        BEGSR
     C***********************************************
     c
     c                   move      *blanks       pcocmd           70
     c     1             chain     systhlp                            33
     c     *in(33)       ifeq      '0'
      *
      * intern
     c                   movel     'http://'     pcocmd
     C     pcocmd        CAT       systip:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       systba:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       hlpna:0       pcocmd
     C     pcocmd        CAT       '.htm':0      pcocmd
      *
     c                   call      'SYGE34CL'
     c                   parm                    pcocmd
     c                   end
     C                   ENDSR
     C*
     ODATALIST  E            DLHEAD           01
     O                       KFMT1               75
     ODATALIST  E            DLHEAD           02
     O                       KFMT2               75
     O               88                          90 '¤&l'
     O               88      COPY                92
     O               88                          93 'X'
     O               88                         100 '¤(s12H'
     O               88                         106 '¤&l1O'
     ODATALIST  E            DLHEAD           03
     O               88                          10 '¤&a-3R'
     O               88                          35 '¤(12U¤(s0p12h0s3b4099T'
     ODATALIST  E            DLHEAD           03
     O               88                          24 '¤*c3350A¤*c190B¤*c11G¤*c'
     O               88                          26 '2P'
     ODATALIST  E            DLHEAD           04
     O                                           24 'S P E D I S J O N S J O '
     O                                           48 'U R N A L    AVD.       '
     O                       WSAVDF        Z     46
     O               41                          58 'FRA DATO'
     O               41      WSDTF         Y     67
     O               41                          77 'TIL DATO'
     O               41      WSDTT         Y     86
     O               42                          58 'FRA GODSNR'
     O               42      WSGNF               74
     O               42                          87 'TIL GODSNR'
     O               42      WSGNT              102
     O                                          130 'UTSKRIFT DATO    .  .  '
     O                       UDAY               124
     O                       UMONTH             127
     O                       UYEAR              130
     O          E            DLHEAD           05
     O               50                          24 'K U N    K O N T A N T K'
     O               50                          48 ' U N D E R              '
     O               51                          24 'K U N    U T S K R E V N'
     O               51                          48 ' E   S A D              '
     O          E            DLHEAD           06
     O                                            8 'SPED.NR.'
     O                                           24 'GODSNR---------'
     O                                           32 '-KOLLI-'
     O                                           42 '---VEKT--'
     O                                           53 'LØPENR----'
     O                                           60 'EKSP.S'
     O                                           69 '--DATO--'
     O                                           94 'AVSENDERSNAVN-----------'
     O                                          100 '------'
     O                                          125 'MOTTAKERSNAVN-----------'
     O                                          131 '------'
     O          E            DLHEAD           07
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O                                          132 '------------'
     O          E            DLHEAD         1 08
     O               88                          25 '¤(12U¤(s0p12h0s0b4099T'
     O          E            DLDET          1
     O                       SETDN         Z      8
     O                       SEGN                24
     O                       SENTK         Z     32
     O                       SEVKB         Z     42
     O                       SETLL               53
     O                       SETLE               60
     O                       SEDTG8              69
     O                       SENAK              100
     O                       SENAS              131
     O          E            DLTOT       2
     O                                           10 '* TOTALT *'
     O                       XTNTK         Z     30
     O                       XTKG          Z     40
     O                                           70 'ANTALL OPPDRAG I PERIODE'
     O                                           72 'N:'
     O                       XANTOP        Z     78
