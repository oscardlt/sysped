      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE LAGERSTEDER               ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FLLTKF     UF A E           K DISK
     FLL007D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  TKTXT                  1     50
     D  XDLT                   1      7
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     LLTKF
     C     START1        TAG
     C                   READ(N)   LLTKF                                  94
      *
     C     START2        TAG
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ(N)   LLTKF                                  94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9998
     C                   SETOFF                                       30
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C     START4        TAG
     C   06              DO
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     START5        TAG
     C                   MOVE      '0'           *IN40
     C                   EXFMT     VARELI
     C                   MOVEA     '00000'       *IN(86)
     C                   MOVE      '0'           *IN30
      *
     C     *IN12         IFEQ      '1'
     C                   EXSR      MOVB2
     C                   ELSE
     C                   EXSR      TESTB
     C     *IN30         CABEQ     '1'           START5
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C  N40WSLLT         IFEQ      *BLANKS
     C                   MOVE      '1'           *IN90
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0783'     MSGNR
     C                   GOTO      SLTB
     C                   ELSE
     C     WSLLT         CHAIN(N)  LLTKF                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      '1'           *IN90
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0784'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C                   END
      *
     C     WSTXT         IFEQ      *BLANKS
     C                   MOVE      '1'           *IN89
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0785'     MSGNR
     C                   GOTO      SLTB
     C                   END
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSLLT         CHAIN     LLTKF                              33
     C                   EXSR      MOVWTK
      *
     C     *IN33         IFEQ      '0'
     C                   UPDATE    LLTKFR
     C                   UPDATE    SUBFIL
     C                   ELSE
     C                   MOVE      WSLLT         TKLLT
     C                   WRITE     LLTKFR
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   END
     C                   EXSR      MOVB2
      *
     C     SLTB          ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGTK
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTTK
     C   12              GOTO      SLHENT
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTK         BEGSR
     C***********************************************
     C                   EXSR      MOVTKW
     C                   MOVE      '1'           *IN40
     C                   Z-ADD     11            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C   04              GOTO      STCHG
     C  N12              EXSR      TESTB
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTTK         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     TKLLT         CHAIN     LLTKF                              33
     C  N33              DELETE    LLTKFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWTK        BEGSR
     C***********************************************
     C                   MOVE      WSLLT         TKLLT
     C                   MOVE      WSTXT         TKTXT
     C                   MOVE      WSOMRF        TKOMRF
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVTKW        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      TKLLT         WSLLT
     C                   MOVE      TKTXT         WSTXT
     C                   MOVE      TKOMRF        WSOMRF
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     11            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       TKLLT
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *BLANKS       TKTXT
     C                   MOVE      *ZEROS        TKOMRF
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSLLT
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      *ZEROS        WSOMRF
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C                   ENDSR
      *
