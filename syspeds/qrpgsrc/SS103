      *********************************************************************
      * OPPDATER KOLLI FOR DELLEVERING
      * PROGRAM BEHANDLER INPUT FRA SS102
      *
CRTPG+*   CRTPGM SYSPED/SS103 MODULE(*LIBL/SS103 *LIBL/SS101B
CRTPGM*    *LIBL/SS101C *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FGSSORF    UF A E           K DISK    USROPN
     FGSSAKF    UF A E           K DISK    USROPN
     FGSSPAF    UF A E           K DISK    USROPN
     FGSSRFF    UF A E           K DISK    USROPN
     FGSSPOF    UF A E           K DISK    USROPN
     FGSSCNF    UF   E           K DISK
     FGSSLGF    O    E             DISK    USROPN
     FKUFAST    IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D ItemCount       s             10i 0
     D i               s             10i 0
     D TB              S              1    DIM(15)

     D WSONO           S              7
     D WSLNO           S              7
     D WSUSER          S             10
     D WSNA            S             30
     D WSKN            S              8
     D WSPOF           S             20
     D WSSTKM          S              5
     D WSTXT           S             30
     D WSCRT           S              1
     D WSDTL           S             10
     D WSST2           S              5
     D WSREADY         S              1
     D BWONO           S              7  0
     D BWLNO           S              3  0
     D BWSTKM          S              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('*LIBL/SS101B')
     D*XXTXT01         C                   CONST('Stauts is changed at')
     D XXTXT01         C                   CONST('Ready for pick up')
     D XXTXT02         C                   CONST('Partial order from')
     D XXTXT03         C                   CONST('Cancle partial order.')
     D XXTXT04         C                   CONST('Del. colli is changed.')
     D                 DS
     D XXDTÅÅ                  1      4  0
     D XXDTMM                  5      6  0
     D XXDTDD                  7      8  0
     D XXDTL                   1      8  0
     D                 DS
     D XTXT                    1     35
     D XT                      1     35    DIM(35)
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Oppdater ordre
     C                   MOVE      *ZEROS        XXONO             7 0
     C                   MOVE      *ZEROS        XXDTL2            8 0
     C                   MOVE      'N'           XDELLEV           1
     C                   MOVE      'N'           XLAGNY            1
     C                   eval      ItemCount = ZhbGetVarCnt('AKONO')
     C                   eval      i =1
     C     i             DOWLE     ItemCount
     C                   EXSR      INPUTFRAWEB
     C                   EXSR      OPPDAT
     C                   eval      i = i + 1
     C                   ENDDO
      *
     C                   IF        XXONO <> 0
     C                   EXSR      OPPDAT2
     C                   END
      * VIS ORDRE
     C                   EXSR      VISORDRE
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Input fra web
      *=====================================================================
     C     INPUTFRAWEB   begsr
     C                   EVAL      WSONO   = ZhbGetVar('AKONO':i)
     C                   EVAL      BWONO   = c2n2(WSONO)
     C                   EVAL      WSLNO   = ZhbGetVar('AKLNO':i)
     C                   EVAL      BWLNO   = c2n2(WSLNO)
     C                   EVAL      WSSTKM  = ZhbGetVar('WSSTKM':i)
     C                   EVAL      BWSTKM  = c2n2(WSSTKM)
     C                   EVAL      WSTXT   = zhbgetvar('WSTXT':i)
     C                   EVAL      WSCRT   = zhbgetvar('LAGNY':i)
     C                   EVAL      WSDTL   = zhbgetvar('WSDTL':i)
     C                   eval      XXDTL   = c2n2(WSDTL)
     C                   EVAL      WSKN    = zhbgetvar('WSKN')
     C                   EVAL      WSPOF   = zhbgetvar('WSPOF')
      *
     C                   endsr
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
     C                   CLOSE     GSSORF
     C                   CLOSE     GSSAKF
     C                   CLOSE     GSSPAF
     C                   CLOSE     GSSPOF
     C                   CLOSE     GSSRFF
     C                   CLOSE     GSSLGF
     C                   CLOSE     KUFAST
     C                   CLOSE     BRIDF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSNA    = zhbgetvar('WSNA')
     C                   eval      WSST2   = zhbgetvar('WSST2')
     C                   eval      WSREADY = zhbgetvar('WSREADY')

     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSORF                               50
     C                   open      GSSAKF                               50
     C                   open      GSSPAF                               50
     C                   open      GSSPOF                               50
     C                   open      GSSRFF                               50
     C                   open      GSSLGF                               50
     C                   open      KUFAST                               50
      *
     C     GSSAKFK       klist
     C                   kfld                    BWONO
     C                   kfld                    BWLNO
     C     KUFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    ORST2
      *
     C                   MOVEL     'SSSTATUS'    KFTYP
      *
     C                   endsr
      *
      *=====================================================================
      *  Oppdater ordre
      *=====================================================================
     C     OPPDAT        begsr
     C     GSSAKFK       CHAIN     GSSAKF                             33
     C                   IF        *IN33 = *OFF
     C                   EXSR      OPDLOGG2
     C                   MOVE      BWSTKM        AKSTKM
     C                   IF        WSTXT = *BLANKS
     C                   IF        AKSTKM <> AKSTK
     C                   EVAL      AKTXT = 'SPLIT ORDER'
     C                   END
     C                   ELSE
     C                   MOVE      WSTXT         AKTXT
     C                   END
     C                   UPDATE    GSSAKFR
     C                   IF        WSCRT = 'J'
     C                   MOVE      'J'           XLAGNY
     C                   END
     C                   IF        AKSTKM <> AKSTK
     C                   MOVE      'J'           XDELLEV
     C                   END
     C                   IF        ((WSCRT = 'J') AND
     C                              (AKSTKM <> AKSTK))
     C                   MOVE      XXDTL         XXDTL2
     C                   END
     C                   END
      *
     C                   IF        XXONO <> BWONO
     C                   IF        XXONO <> 0
     C                   EXSR      OPPDAT2
     C                   END
     C                   IF        WSREADY = 'J'
     C     BWONO         CHAIN     GSSORF                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      LGAGM = ORST2
     C     KUFASTK       CHAIN     KUFAST                             33
     C                   CAT       KFTXT:2       LGAGM
     C                   MOVEL     'RFP'         ORST2
     C                   UPDATE    GSSORFR
     C                   MOVEL     XXTXT01       LGTXT
     C                   CAT       ORLC:1        LGTXT
     C                   Z-ADD     3             LGNIVÅ
     C                   EXSR      OPDLOGG1
     C                   END
     C                   END
     C                   END
      *
     C                   MOVE      BWONO         XXONO
      *
     C                   endsr
      *=====================================================================
      *  Oppdater dellevering
      *=====================================================================
     C     OPPDAT2       begsr
     C     XXONO         CHAIN     GSSPAF                             33
     C                   IF        *IN33 = *OFF
     C                   IF        XDELLEV = 'J'
     C                   MOVE      'P'           PAPKD
     C                   EVAL      PATXT1 = 'PARTIAL DELEVERIED'
     C                   ELSE
     C                   IF        PATXT1 <> *BLANKS
     C                   MOVE      'C'           PAPKD
     C                   END
     C                   END
     C                   UPDATE    GSSPAFR
     C                   ELSE
     C                   IF        XDELLEV = 'J'
     C                   CLEAR                   GSSPAFR
     C                   MOVE      XXONO         PAONO
     C                   MOVE      'P'           PAPKD
     C                   EVAL      PATXT1 = 'PARTIAL DELEVERIED'
     C                   WRITE     GSSPAFR
     C                   END
     C                   END
     C                   MOVE      'N'           XDELLEV
      *
     C                   IF        XLAGNY = 'J'
     C                   EXSR      LAGRESTORD
     C                   END
     C                   endsr
      *=====================================================================
      *  Lage restordre
      *=====================================================================
     C     LAGRESTORD    begsr
     C                   MOVE      'N'           XLAGNY2           1
     C                   MOVE      *ZEROS        CNNO
     C     XXONO         SETLL     GSSAKF
     C     XXONO         READE(N)  GSSAKF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        CNNO = 0
     C                   MOVE      AKONOR        CNNO
     C                   END
     C                   IF        AKSTKM <> AKSTK
     C                   MOVE      'J'           XLAGNY2
     C                   END
     C     XXONO         READE(N)  GSSAKF                                 33
     C  N33              ENDDO
      *
     C                   SELECT
     C                   WHEN      ((XLAGNY2 = 'N') AND (CNNO = 0))
     C                   WHEN      ((XLAGNY2 = 'N') AND (CNNO <> 0))
      * INGEN ENDRING OG FJERN RESTORDRE
     C                   EXSR      SLTGMORD
      * NULLSTILL PEKER TIL RESTORDRE
     C     XXONO         SETLL     GSSAKF
     C     XXONO         READE     GSSAKF                                 33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      *ZEROS        AKONOR
     C                   UPDATE    GSSAKFR
     C     XXONO         READE     GSSAKF                                 33
     C  N33              ENDDO
     C                   WHEN      ((XLAGNY2 = 'J') AND (CNNO = 0))
      * MINST EN ARTIKKELLINJE ER ENDRET. LAG RESTORDRE
     C     'T'           CHAIN     GSSCNF                             33
     C                   ADD       1             CNNO
     C                   UPDATE    GSSCNFR
     C                   SUB       1             CNNO
     C                   EXSR      LAGNYORD
     C                   WHEN      ((XLAGNY2 = 'J') AND (CNNO <> 0))
      * MINST EN ARTIKKELLINJE ER ENDRET. ENDRE RESTORDRE
     C                   EXSR      SLTGMORD
     C                   EXSR      LAGNYORD
     C                   ENDSL
      *
     C                   MOVE      'N'           XLAGNY
      *
     C                   endsr
      *=====================================================================
      *  Lage nytt ordre
      *=====================================================================
     C     LAGNYORD      begsr
      * ARTIKKEL LINJER
     C                   MOVE      *BLANKS       XNYPONR          20
     C                   MOVE      *BLANKS       XTXT
     C     XXONO         SETLL     GSSAKF
     C     XXONO         READE     GSSAKF                                 33
     C                   MOVE      *ZEROS        XXAMT            11 2
     C                   MOVE      *ZEROS        XXAMT2           11 2
     C                   DOW       *IN33 = *OFF
     C                   IF        AKSTK <> AKSTKM
     C                   MOVE      CNNO          AKONOR
     C                   UPDATE    GSSAKFR
     C                   MOVE      CNNO          AKONO
     C                   MOVE      *ZEROS        AKONOR
     C                   EVAL      AKSTK = AKSTK - AKSTKM
     C                   EVAL      AKSTKM = AKSTK
     C                   EVAL      AKTXT = *BLANKS
     C                   IF        XNYPONR = *BLANKS
     C                   MOVEL     AKCONO        XTXT
     C                   EXSR      FINNESTE
     C                   MOVEL     XTXT          XNYPONR
     C                   END
     C                   MOVEL     XNYPONR       AKCONO
     C                   WRITE     GSSAKFR
     C                   IF        AKPR <> 0,01
     C                   EVAL      XXAMT2 = AKPR * AKSTK
     C                   EVAL      XXAMT = XXAMT + XXAMT2
     C                   END
     C                   ELSE
     C                   UPDATE    GSSAKFR
     C                   END
     C     XXONO         READE     GSSAKF                                 33
     C  N33              ENDDO
      * P.O.NR
     C     XXONO         SETLL     GSSPOF
     C     XXONO         READE(N)  GSSPOF                                 33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      CNNO          POONO
     C                   IF        XNYPONR = *BLANKS
     C                   MOVEL     POCONO        XTXT
     C                   EXSR      FINNESTE
     C                   MOVEL     XTXT          XNYPONR
     C                   END
     C                   MOVEL     XNYPONR       POCONO
     C                   WRITE     GSSPOFR
     C     XXONO         READE(N)  GSSPOF                                 33
     C  N33              ENDDO
      * REFERANSE
     C     XXONO         SETLL     GSSRFF
     C     XXONO         READE(N)  GSSRFF                                 33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      CNNO          RFONO
     C*                  MOVEL     RFREF         XTXT
     C*                  EXSR      FINNESTE
     C*                  MOVEL     XTXT          RFREF
     C                   WRITE     GSSRFFR
     C     XXONO         READE(N)  GSSRFF                                 33
     C  N33              ENDDO
      * DELLEVERING
     C                   CLEAR                   GSSPAFR
     C                   MOVE      CNNO          PAONO
     C                   MOVE      'P'           PAPKD
     C                   EVAL      PATXT1 = 'PARTIAL DELEVERIED'
     C                   WRITE     GSSPAFR
      * ORDRE
     C     XXONO         CHAIN(N)  GSSORF                             33
     C                   MOVE      CNNO          ORONO
     C                   MOVE      '0'           ORST
     C                   MOVEL     'IPR'         ORST2
     C                   MOVE      *ZEROS        ORTNO
     C                   MOVE      *ZEROS        ORTNO2
     C                   MOVE      *ZEROS        ORTNO3
     C                   MOVE      *ZEROS        ORWT
     C                   MOVE      *ZEROS        ORVOL
     C                   IF        XXAMT = 0
     C                   EVAL      ORAMT = 0,01
     C                   ELSE
     C                   MOVE      XXAMT         ORAMT
     C                   END
     C                   MOVE      *ZEROS        ORCLNF
     C                   MOVE      *ZEROS        ORCLNT
     C                   MOVE      XXDTL2        ORDTL
     C                   MOVE      *ZEROS        ORDTL2
     C                   MOVE      *ZEROS        ORDTL3
     C                   MOVE      *BLANKS       ORLC2
     C                   MOVE      *BLANKS       ORLC3
     C                   EVAL      ORFT1 = 'RESTORDRE FRA'
     C                   MOVE      XXONO         XXONO2            7
     C                   CAT       XXONO2:1      ORFT1
     C                   MOVE      WDT           ORDTR
     C                   WRITE     GSSORFR
     C                   EVAL      LGAGM = 'At'
     C                   CAT       ORLC:1        LGAGM
     C                   EVAL      LGTXT = XXTXT02
     C     XXONO         SETLL     GSSPOF
     C     XXONO         READE(N)  GSSPOF                                 33
     C  N33              CAT       POCONO:1      LGTXT
     C                   Z-ADD     3             LGNIVÅ
     C                   EXSR      OPDLOGG1
      *
     C                   endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG1      begsr
      *
     C                   MOVE      WSUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'SS103'       LGPGM
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      ORONO         LGONO
     C                   MOVE      *ZEROS        LGLNO
     C                   EVAL      LGANY = ORST2
     C     KUFASTK       CHAIN     KUFAST                             33
     C                   CAT       KFTXT:2       LGANY
     C                   WRITE     GSSLGFR
      *
     C                   endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG2      begsr
      *
     C                   IF        BWSTKM <> AKSTKM
     C                   MOVE      WSUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'SS103'       LGPGM
     C                   Z-ADD     5             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'ART'         LGFTYP
     C                   MOVE      AKONO         LGONO
     C                   MOVE      AKLNO         LGLNO
     C                   EVAL      LGTXT = XXTXT04
     C                   EVAL      LGANY = 'Artkkelno. is'
     C                   CAT       AKANR:1       LGANY
     C                   EVAL      LGNNY = BWSTKM
     C                   EVAL      LGNGM = AKSTKM
     C                   WRITE     GSSLGFR
     C                   END
      *
     C                   endsr
      *=====================================================================
      *  FINN NESTE NR
      *=====================================================================
     C     FINNESTE      begsr
      *
     C     1             DO        35            XN                3 0
     C                   SELECT
     C                   WHEN      XT(XN) = *BLANKS
     C                   CAT       '-01':0       XTXT
     C                   LEAVE
     C                   WHEN      XT(XN) = '-'
     C     XN            ADD       1             XN2               3 0
     C                   IF        ((XT(XN2)>='0') AND (XT(XN2)<='9'))
     C                   MOVEL     XT(XN2)       XN02              2 0
     C                   ADD       1             XN2               3 0
     C                   IF        ((XT(XN2)>='0') AND (XT(XN2)<='9'))
     C                   MOVE      XT(XN2)       XN02              2 0
     C                   ADD       1             XN02
     C                   ADD       1             XN
     C                   MOVEL     XN02          XT(XN)
     C                   MOVE      XN02          XT(XN2)
     C                   LEAVE
     C                   END
     C                   END
     C                   ADD       1             XN
     C                   MOVE      '0'           XT(XN)
     C                   ADD       1             XN
     C                   MOVE      '1'           XT(XN)
     C                   LEAVE
     C                   ENDSL
     C                   ENDDO
      *
     C                   endsr
      *=====================================================================
      *  Slett gamel restordre
      *=====================================================================
     C     SLTGMORD      begsr
      * ARTIKKEL LINJER
     C     CNNO          SETLL     GSSAKF
     C     CNNO          READE     GSSAKF                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    GSSAKFR
     C     CNNO          READE     GSSAKF                                 33
     C  N33              ENDDO
      * P.O.NR
     C     CNNO          SETLL     GSSPOF
     C     CNNO          READE     GSSPOF                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    GSSPOFR
     C     CNNO          READE     GSSPOF                                 33
     C  N33              ENDDO
      * REFERANSE
     C     CNNO          SETLL     GSSRFF
     C     CNNO          READE     GSSRFF                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    GSSRFFR
     C     CNNO          READE     GSSRFF                                 33
     C  N33              ENDDO
      * DELLEVERING
     C     CNNO          SETLL     GSSPAF
     C     CNNO          READE     GSSPAF                                 33
     C                   DOW       *IN33 = *OFF
     C                   UPDATE    GSSPAFR
     C     CNNO          READE     GSSPAF                                 33
     C  N33              ENDDO
      * ORDRE
     C     CNNO          CHAIN     GSSORF                             33
     C                   IF        *IN33 = '0'
     C                   DELETE    GSSORFR
     C                   EVAL      LGAGM = ORST2
     C     KUFASTK       CHAIN     KUFAST                             33
     C                   CAT       KFTXT:2       LGAGM
     C                   EVAL      LGTXT = XXTXT03
     C                   Z-ADD     3             LGNIVÅ
     C                   EXSR      OPDLOGG1
     C                   END
      *
     C                   endsr
      *=====================================================================
      * VIS ORDRE
      *=====================================================================
     C     VISORDRE      begsr
      *
     C                   CALLB     PGM2
     C                   PARM                    WSNA
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    WSST2
     C                   PARM                    bckgnd
     C                   PARM                    WSKN
     C                   PARM                    WSPOF
      *
     C                   endsr
