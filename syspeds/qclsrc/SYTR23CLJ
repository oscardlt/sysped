/*  BYTTET GETPR2 MED GETPRN FOR DRAWER OG ANTAL.L KOPIER */
    /* UTSKRIFT AV TRANSPORTØRAVREGNING (KREDITNOTA) */
             PGM        PARM(&TNR &AAR &PER &PRT &PT &FHE &VEDL &AVD +
                          &SG &ALFA &FAKT)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PRNR) TYPE(*DEC) LEN(3 0) VALUE(102)
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MAIL) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MAILJN) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('ta')
             DCL        VAR(&AUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRT) TYPE(*CHAR) LEN(10) /* Printernavn */
             DCL        VAR(&PT) TYPE(*CHAR) LEN(10)  /* Papirtype */
             DCL        VAR(&FHE) TYPE(*CHAR) LEN(1)  /* HEADING */
             DCL        VAR(&VEDL) TYPE(*CHAR) LEN(1) /* VEDLEGG */
             DCL        VAR(&SG) TYPE(*CHAR) LEN(3)   /* SIGN    */
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)  /* AVD     */
             DCL        VAR(&TNR) TYPE(*DEC) LEN(8 0) /*TRANSP   */
             DCL        VAR(&PER) TYPE(*DEC) LEN(2 0) /*PERMND   */
             DCL        VAR(&AAR) TYPE(*DEC) LEN(2 0) /*PERÅR    */
             DCL        VAR(&ALFA) TYPE(*CHAR) LEN(1) /*SORTERING*/
             DCL        VAR(&FAKT) TYPE(*DEC) LEN(7 0) /*KOPI BIL.NR*/
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(9)
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(10)
             RTVJOBA    USER(&AUSER) NBR(&JOB)
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
             CALL       PGM(GETPRN) PARM(&AVD &PRNR &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
    /* */
    /* SKRIV AVR-DOKUMENT  IKKE PDF      */
    /* */
             IF         COND(&LASER *NE 'P') THEN(DO)
             OVRPRTF    FILE(SYTR23PF) PAGESIZE(72 80) CPI(10) +
                          OVRFLW(72) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT) COPIES(&COPIES)
             OVRPRTF    FILE(SYTR12PH) PAGESIZE(72 120) CPI(15) +
                          OVRFLW(67) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT) FORMTYPE(*STD) +
                          COPIES(&COPIES)
             ENDDO
    /* */
    /* SKRIV AVR-DOKUMENT  PDF      */
    /* */
             IF         COND(&LASER *EQ 'P') THEN(DO)
             OVRPRTF    FILE(SYTR23PF) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          80) CPI(10) OVRFLW(72) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT)
             OVRPRTF    FILE(SYTR12PH) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          120) CPI(15) OVRFLW(67) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PRTQLTY(*DRAFT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT) FORMTYPE(*STD)
             ENDDO
 /* FIKSER WORKFILE */
 RTVTIME:    RTVJOBA    NBR(&JOB)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &JOB)
             RMVM       FILE(WRKTAVR) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(WRKTAVR) MBR(&UNIK)
             MONMSG     MSGID(CPF7306) EXEC(GOTO CMDLBL(RTVTIME))
             OVRDBF     FILE(WRKTAVR) TOFILE(WRKTAVR) MBR(&UNIK) +
                          FRCRATIO(1)
             OVRDBF     FILE(TRAVV2) TOFILE(TRAVV2) FRCRATIO(1)
    /* */
             CALL       PGM(SYTR23) PARM(&AVD &SG &FHE &VEDL +
                          &TNR &AAR &PER &ALFA &FAKT &MAIL)
/*                   */
/*                   */
/* FOR MAILKUNDER          */
/*                   */
             IF         COND(&FAKT = 0) THEN(DO)
             CHGVAR     VAR(&MAIL) VALUE('J')
             CHGVAR     VAR(&PRT) VALUE('OUTMAIL   ')
             OVRPRTF    FILE(SYTR23PF) OUTQ(&PRT) PAGESIZE(72 80) +
                          CPI(10) OVRFLW(72) FORMTYPE(&PT)
             OVRPRTF    FILE(SYTR12PH) OUTQ(&PRT) PAGESIZE(72 120) +
                          CPI(15) OVRFLW(67) PRTQLTY(*DRAFT) +
                          FORMTYPE(*STD)
             CALL       PGM(SYTR23) PARM(&AVD &SG &FHE &VEDL +
                          &TNR &AAR &PER &ALFA &FAKT &MAIL)
                          ENDDO
/* ARKIVERING FLYTTET TIL SYKR35CLJ */
/*           SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK) */
             ENDPGM
