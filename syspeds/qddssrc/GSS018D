     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 50 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R GSS018DA                  SFL
     A            WSNR           1A  B  5  2VALUES(' ' '2' '5' '7')
     A  27                                  DSPATR(PR)
     A            OLLNO          2  0O  5  5
     A            OLLC          20A  O  5  8
     A            OLBOX          3  0O  5 29EDTCDE(4)
     A            OLTNO         11Y 0O  5 33EDTCDE(4)
     A            OLST           1A  O  5 45
     A            OLMN          35A  H
      *
     A          R GSS018DB                  SFLCTL(GSS018DA)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'AVBRYT')
     A                                      CF03(12 'AVBRYT')
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS018D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with locations'
     A            ZRECNO         4S 0O  1 46SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Order No.:'
     A            OLONO          7Y 0O  2 13EDTCDE(4)
     A                                      COLOR(BLU)
     A                                  2 21'Stock po:'
     A            UPOS1          5A  O  2 31COLOR(BLU)
     A N27                              3  2'Options:  2=Change  5=Display  7=A-
     A                                      rticle'
     A                                      COLOR(BLU)
     A                                  4  2'Op Cl Location             Box Tri-
     A                                      p No.... St'
     A                                      COLOR(WHT)
      *
     A          R GSS018DC
     A                                      WINDOW(WINDOWSZ)
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'AVBRYT')
     A                                      CF03(12 'AVBRYT')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(13)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F5=Refresh   F12=End'
     A                                      COLOR(BLU)
     A  30        MSGTXT        48A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS018DD
     A                                      WINDOW(*DFT 06 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF12(12 'Forrige')
     A                                  1  2'SHIPSYS/GSS018D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with locations'
     A                                      COLOR(WHT)
     A                                  2  2'Order No.:'
     A            OLONO          7Y 0O  2 13EDTCDE(4)
     A                                  2 22'Line No..:'
     A            OLLNO          2Y 0O  2 33EDTCDE(4)
     A                                  3  2'Location:'
     A            WSLC          20A  B  3 13DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  28                                  DSPATR(PR)
     A N30
     AO 90
     AON28                                  DSPATR(PC)
     A                                  4  2'Box No..:'
     A            WSBOX          3  0B  4 13DSPATR(UL)
     A  28                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  5  2'Marks...:'
     A            WSMN          35A  B  5 13DSPATR(UL)
     A N30                              6  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        48A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS018DG
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SHIPSYS/GSS018D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with locations'
     A                                      COLOR(WHT)
     A                                  2  2'Order No.:'
     A            OLONO          7Y 0O  2 13EDTCDE(4)
     A                                      COLOR(BLU)
     A                                  2 21'Stock po:'
     A            UPOS1          5A  O  2 31COLOR(BLU)
     A                                  4  2'Op Cl Location             Box Tri-
     A                                      p No.  St'
     A                                      COLOR(WHT)
     A                                 13  2'F12=End'
     A                                      COLOR(BLU)
      *
     A          R GSS018DE
     A                                      WINDOW(*DFT 06 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF12(12 'Forrige')
     A                                  1  2'SHIPSYS/GSS018D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with locations'
     A                                      COLOR(WHT)
     A                                  2  2'Order No.:'
     A            OLONO          7Y 0O  2 13EDTCDE(4)
     A                                  2 22'Line No..:'
     A            OLLNO          2Y 0O  2 33EDTCDE(4)
     A                                  3  2'Location:'
     A            OLLC          20A  O  3 13DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(RI)
     A                                  4  2'Box No..:'
     A            OLBOX          3  0O  4 13DSPATR(UL)
     A                                      DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  5  2'Marks...:'
     A            OLMN          35A  O  5 13DSPATR(UL)
     A                                      DSPATR(RI)
     A                                  6  2'F12=Cancel'
     A                                      COLOR(BLU)
