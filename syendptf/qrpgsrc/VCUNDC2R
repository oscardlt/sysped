********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 NUSTILLER CAVD
********************************************************************
     FCUNDC     UF A E             DISK
     F                                     INFDS(CUNDS)
     FCUNDC02   IF   E           K DISK
     F                                     INFDS(CUNDS1)
     F                                     RENAME(CUNDCR:CUNDCR1)
     FCUNDL01   IF   E           K DISK
     FVCUNDC2D  CF   E             WORKSTN
     F                                     SFILE(VCUNDCD4:ZRECNO)
     F                                     INFDS(FBAREA)
     D TXT             S             15    DIM(1) CTDATA PERRCD(1)
     D CUNDS           DS
     D  RRNP                 397    400B 0
     D CUNDS1          DS
     D  RRNP1                397    400B 0
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *______________
      * Clear Subfile
      *""""""""""""""
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     VCUNDCD5
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fill Subfile
      *"""""""""""""
     C     CUNKY1        SETLL     CUNDC02                            77
      *
     C     START1        TAG
     C                   DO        7
     C     UFIRMA        READE     CUNDC02                                94
     C     *IN94         IFEQ      '0'
     C     CUNKY2        CHAIN     CUNDL01                            51
     C                   MOVE      RRNP1         SFRRN
     C                   MOVE      CCONTA        SFONTA
     C                   MOVEL     KNAVN         SFNAVN
     C                   MOVEL     CPHONE        SFHONE
     C                   MOVEL     CEMAIL        SFMAIL
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        CURS1
     C                   WRITE     VCUNDCD4
     C                   END                                                    <*IN94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     YRECNO        IFGT      0
     C     YRECNO        SUB       1             ZRECNO
     C     ZRECNO        DIV       13            ZRECNO
     C     ZRECNO        MULT      13            ZRECNO
     C                   ADD       1             ZRECNO
     C                   ENDIF
      *________________
      * Display Subfile
      *""""""""""""""""
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   MOVE      '0'           *IN31
     C                   WRITE     VCUNDCD5
     C                   Z-ADD     WSSUBN        XRECNO
     C                   EXFMT     VCUNDCD3
     C                   READ      VCUNDCD5                               66
      *
     C     FKEYS         TAG
      *
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      *
     C                   SETOFF                                       999830
      *** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F5=Refresh
     C     *IN05         IFEQ      '1'
     C                   CLEAR                   SFONTA
     C                   CLEAR                   SFNAVN
     C                   CLEAR                   SFHONE
     C                   GOTO      START
     C                   END                                                    <*IN05
      *** F6=NY
     C     *IN06         IFEQ      '1'
     C                   MOVE      *ZEROS        CCOMPN
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       CCONTA
     C                   MOVE      *BLANKS       CTYPE
     C                   MOVE      *BLANKS       CPHONE
     C                   MOVE      *BLANKS       CMOBIL
     C                   MOVE      *BLANKS       CFAX
     C                   MOVE      *BLANKS       CEMAIL
     C                   GOTO      START5
     C                   END                                                    <*IN03
      *** F8=SØK
     C     *IN08         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     15            POSNBR
     C                   EXFMT     VCUNDCD8
     C     *IN12         IFEQ      '0'
     C                   MOVE      WSSOK         USOK
     C                   END
     C                   GOTO      START
     C                   END                                                    <*IN03
      *** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   CLEAR                   SFONTA
     C                   CLEAR                   SFNAVN
     C                   CLEAR                   SFHONE
     C                   GOTO      START
     C                   END                                                    <*IN12
      *** Roll
     C     *IN25         IFEQ      '1'
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C     START4        TAG
     C     *IN94         IFEQ      '0'
     C                   GOTO      START1
     C                   END                                                    <*IN94
     C                   END                                                    <*IN25
      *** Get options from screen
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
      *** No option selected
     C     *IN97         IFEQ      '1'
     C                   EXFMT     VCUNDCD3
     C                   READ      VCUNDCD5                               66
     C                   MOVE      '0'           *IN97
     C                   GOTO      FKEYS
     C                   END                                                    <*IN12
      *** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
     C                   END                                                    <ZRECNO
     C     *IN98         IFEQ      '1'
     C     XRECNO        ANDNE     0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END                                                    <*IN98
     C                   GOTO      START3
      *** New record
     C     START5        TAG
     C                   MOVEL     'Ny      '    WMODE
     C                   EXFMT     VCUNDCD1
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   MOVE      '0'           *IN30
      *** F3=Exit
      *** F12=Cancel
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   EXSR      CLRSCR
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   ELSE
      *
      * 04=SØKK KUNDE
      *
     C   04              DO
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    CCOMPN            8 0
     C                   PARM                    UFIRMA
     C                   PARM                    USØKKN           30
     C                   END
     C     CUNKY2        CHAIN     CUNDL01                            70
     C   70              SETON                                        30
     C   70              MOVE      'SPK0001'     MSGNR
     C   70              GOTO      START5
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      START5
     C                   END                                                    <*IN30
     C                   END                                                    <*IN12
     C                   GOTO      START3
      *
     C     ENDPGM        TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*          N40 = NEW
     C*           40 = CHANGE
     C***********************************************
     C                   EXSR      TSTSCR
     C     *IN30         IFEQ      '1'
     C                   GOTO      ENDTST
     C                   END                                                    <*IN30
     C     CUNKY2        CHAIN     CUNDL01                            51
     C     *IN40         IFEQ      '1'
     C                   UPDATE    CUNDCR                               38
     C                   ELSE
     C                   MOVEL     UFIRMA        CFIRMA
N01  C                   MOVE      *ZEROS        CAVD
     C                   WRITE     CUNDCR
     C                   END                                                    <*IN40
     C*
     C                   MOVE      CCONTA        SFONTA
     C                   MOVEL     KNAVN         SFNAVN
     C                   MOVEL     CPHONE        SFHONE
     C     *IN40         IFEQ      '1'
     C                   UPDATE    VCUNDCD4
     C                   ELSE
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      RRNP          SFRRN
     C                   WRITE     VCUNDCD4
     C                   END                                                    <*IN40
     C                   EXSR      CLRSCR
      *
     C     ENDTST        ENDSR
      ***********************************************************
     C     TSTSCR        BEGSR
      ***********************************************************
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C*
     C     CCONTA        IFEQ      *BLANKS
     C                   MOVE      'CBA0130'     MSGNR
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN30
     C                   GOTO      TSTEND
     C                   END                                                    <CCONTA
     C*
     C     TSTEND        ENDSR
     C*
      ***********************************************
     C     GETOPT        BEGSR
      ***********************************************
     C                   Z-ADD     0             OPTNBR            2 0
     C                   SETOFF                                       919299
     C                   MOVE      '1'           *IN97
     C     *IN98         DOWEQ     '0'
     C                   READC     VCUNDCD4                               98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   END                                                    <*IN99
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
     C                   MOVE      '0'           *IN97
     C                   Z-ADD     CURS1         ZRECNO
      *** 1=PICK E=EPOST F=FAX
     C     WSNR          IFEQ      '1 '
     C     WSNR          OREQ      ' 1'
     C     WSNR          OREQ      '01'
     C     WSNR          OREQ      'E '
     C     WSNR          OREQ      ' E'
     C     WSNR          OREQ      'F '
     C     WSNR          OREQ      ' F'
     C                   MOVE      SFRRN         URRN
     C     WSNR          IFEQ      'F '
     C     WSNR          OREQ      ' F'
     C                   MOVE      'F'           EPOFAX
     C                   END
     C     WSNR          IFEQ      'E '
     C     WSNR          OREQ      ' E'
     C                   MOVE      'E'           EPOFAX
     C                   END
     C                   SETON                                        LR
     C                   RETURN
     C                   END                                                    <SFNAVN
      *** 2=Change
     C     WSNR          IFEQ      '2 '
     C     WSNR          OREQ      ' 2'
     C     WSNR          OREQ      '02'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Endre   '    WMODE
     C     SFNAVN        IFEQ      TXT(1)
     C                   MOVE      'CBA0120'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      ENDGET
     C                   END                                                    <SFNAVN
     C                   MOVE      '1'           *IN40
     C                   EXSR      CHGST
      *** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   MOVE      '0'           *IN40
     C                   END                                                    <WSNR=2
      *** 3=Copy
     C     WSNR          IFEQ      '3 '
     C     WSNR          OREQ      ' 3'
     C     WSNR          OREQ      '03'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Kopier  '    WMODE
     C     SFNAVN        IFEQ      TXT(1)
     C                   MOVE      'CBA0120'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      ENDGET
     C                   END                                                    <SFNAVN
     C                   EXSR      CPYST
      *** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR=3
      *** 4=Delete
     C     WSNR          IFEQ      '4 '
     C     WSNR          OREQ      ' 4'
     C     WSNR          OREQ      '04'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Slett   '    WMODE
     C     SFNAVN        IFEQ      TXT(1)
     C                   MOVE      'CBA0120'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      ENDGET
     C                   END                                                    <SFNAVN
     C                   EXSR      DLTST
      *** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR=4
      *** 5=Display
     C     WSNR          IFEQ      '5 '
     C     WSNR          OREQ      ' 5'
     C     WSNR          OREQ      '05'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Vise    '    WMODE
     C     SFNAVN        IFEQ      TXT(1)
     C                   MOVE      'CBA0120'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      ENDGET
     C                   END                                                    <SFNAVN
     C     SFRRN         CHAIN(N)  CUNDC                              77
     C  N77CUNKY2        CHAIN     CUNDL01                            51
     C                   MOVE      '1'           *IN88
     C                   EXFMT     VCUNDCD1
      *** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   UPDATE    VCUNDCD4
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   MOVE      '0'           *IN88
     C                   UPDATE    VCUNDCD4
     C                   END                                                    <WSNR=5
     C                   ENDDO
      *
     C     ENDGET        TAG
      *
     C                   ENDSR
      ***********************************************
     C     CHGST         BEGSR
      ***********************************************
     C                   MOVE      '0'           *IN88
     C     SFRRN         CHAIN     CUNDC                              77
     C  N77CUNKY2        CHAIN     CUNDL01                            51
     C                   MOVE      '1'           *IN99
     C     STCHG         TAG
     C                   EXFMT     VCUNDCD1
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   UPDATE    VCUNDCD4
     C                   GOTO      CHGEND
     C                   END                                                    <*IN12
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      STCHG
     C                   END                                                    <*IN30
      *
     C     CHGEND        TAG
     C                   ENDSR
     C*
      ***********************************************
     C     CPYST         BEGSR
      ***********************************************
     C                   MOVE      '0'           *IN88
     C     SFRRN         CHAIN(N)  CUNDC                              77
     C  N77CUNKY2        CHAIN     CUNDL01                            51
     C                   MOVE      '1'           *IN99
     C                   UPDATE    VCUNDCD4
     C     STCPY         TAG
     C                   EXFMT     VCUNDCD1
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   EXSR      CLRSCR
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   GOTO      CPYEND
     C                   END                                                    <*IN12
     C                   MOVE      '0'           *IN40
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      STCPY
     C                   END                                                    <*IN30
      *
     C     CPYEND        TAG
     C                   ENDSR
     C*
      ***********************************************
     C     DLTST         BEGSR
      ***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVEA     '11'          *IN(88)
     C     SFRRN         CHAIN     CUNDC                              77
     C  N77CUNKY2        CHAIN     CUNDL01                            51
     C                   EXFMT     VCUNDCD1
     C                   MOVEA     '00'          *IN(88)
     C     *IN11         IFEQ      '1'
     C     *IN77         IFEQ      '0'
     C                   DELETE    CUNDCR
     C                   END                                                    <*IN77
     C                   MOVE      CCONTA        SFONTA
     C                   MOVEL     TXT(1)        SFNAVN
     C                   END                                                    <*IN11
     C                   UPDATE    VCUNDCD4
      *
     C                   ENDSR
      ***********************************************
     C     CLRSCR        BEGSR
      ***********************************************
     C                   MOVE      '0'           *IN40
     C                   CLEAR                   CCONTA
     C                   CLEAR                   CTYPE
     C                   CLEAR                   CPHONE
      *
     C                   ENDSR
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *LIKE         DEFINE    CFIRMA        UFIRMA
     C     *ENTRY        PLIST
     C                   PARM                    UFIRMA
     C                   PARM                    USOK             30
     C                   PARM                    URRN             10 0
     C                   PARM                    EPOFAX            1
     C                   MOVEL     USOK          USOK1             1
     C     USOK1         IFEQ      '*'
     C                   MOVE      '1'           UMODE             1
     C                   CALL      'EPOS02'
     C                   PARM                    UMODE
     C                   PARM                    UDISTL            7
     C                   MOVEL     UDISTL        USOK
     C                   SETON                                        LR
     C                   RETURN
     C                   END
      *--------------------------------------------------------------
      * DEFINE KEY FOR CUNDC01
      *--------------------------------------------------------------
     C     CUNKY1        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    USOK
     C     CUNKY2        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    CCOMPN
      *
      *
     C                   CLEAR                   SFONTA
     C                   MOVE      '1'           *IN50
      *
     C     USOK          IFEQ      *BLANKS
     C                   MOVEL     '.'           USOK
     C                   END
     C                   ENDSR
      *
**
*** SLETTET ***
