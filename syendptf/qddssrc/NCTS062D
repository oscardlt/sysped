     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5')
     A            TVLI           1Y 0O  4  5EDTCDE(4)
     A            TVKD           1A  O  4  8
     A            TVTXT_35      35A  O  4 10
     A            WSADT          6Y 0O  4 46EDTWRD('  .  .  ')
     A            TVASD_20      20A  O  4 55
     A            TV01           1   H
     A            TVADT          8Y 0H
     A            TVAAUT        35A  H
     A            TVASD         35A  H
     A            TVALK          2A  H
     A            TVQID          1A  H
     A            TVSD          17A  H
     A            TVLK           2A  H
     A            TVLGLA        17A  H
     A            TVLGLO        17A  H
     A            TVAD1         30A  H
     A            TVPN          17A  H
     A            TVPS          24A  H
     A            TVCNR         17A  H
     A            TVANTS         4Y 0H
     A            TVSID         20A  H
     A            TVDLI          5Y 0H
     A            TVKDC          1Y 0H
     A            TVTTY          2Y 0H
     A            TVTID         35A  H
     A            TVTLK          2A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS062R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis'
     A                                      COLOR(BLU)
     A                                  3  2'Op LN Hendelses kode og beskrivels-
     A                                      e         Hendelses dato og sted   -
     A                                             '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Send til TET')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til bilde02')
     A N27                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              4  2'F3=Neste/avslutt   F5=Forny   F6=L-
     A                                      ag ny   F8=Hopp til'
     A                                      COLOR(BLU)
     A  27                              4  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A N36                              4 58'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A N27                              5  2'F12=Forrige   F17=Ny NCTS kopier f-
     A                                      ra denne'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A    N36                               CF07(07 'SØKE OPPDRAG')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/NCTS062R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 63'-'
     A                                      COLOR(WHT)
     A            WSLI           1Y 0O  1 65COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'HENDELSE UNDERVEIS:'
     A                                      COLOR(WHT)
     A                                  3 22'Kode:'
     A            WSKD           1A  B  3 28TEXT('kode')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 22'Beskrivelse:'
     A            WSTXT        512A  B  4 35TEXT('Beskrivelse')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
      * WSTXT tar 7 linjer
     A                                 11  2'Dato:'
     A            WSADT          6Y 0B 11 12TEXT('Dato')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 11 22'Autorit:'
     A            WSAAUT        35A  B 11 32TEXT('Autoritet')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12  2'Sted:'
     A            WSASD         35A  B 12 12TEXT('Sted')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 52'Land:'
     A            WSALK          2A  B 12 58TEXT('Land')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'Lokasjon:'
     A            WSQID          1A  B 13 12TEXT('Kvalfier ID')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14  2'Lok.kode:'
     A            WSSD          17A  B 14 12TEXT('Stedskode')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14 32'Land:'
     A            WSLK           2A  B 14 42TEXT('Land')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15  2'Adresse:'
     A            WSAD1         30A  B 15 12TEXT('Adresse')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPN          17A  B 16 12TEXT('Postnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPS          24A  B 16 30TEXT('Poststed')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17  2'Cont:'
     A            WSCNR         17A  B 17 12TEXT('Containernr')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18  2'Ant:'
     A            WSANTS         4Y 0B 18 12TEXT('Antall seal')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18 17'Seal:'
     A            WSSID         20A  B 18 23TEXT('Seal id')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18 44'Itemnr:'
     A            WSDLI          5Y 0B 18 52TEXT('Item nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 18 62'Cont.:'
     A            WSKDC          1Y 0B 18 69TEXT('Container?')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 19  2'Tr.midl.:'
     A            WSTTY          2Y 0B 19 12TEXT('Transporttype')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            WSTID         35A  B 19 15TEXT('Tr.middel id')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTLK          2A  B 19 51TEXT('Tr.middel land')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 23  2'F4=Søk   F12=Avbryt   F15=Tillegg -
     A                                      opplysning'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                  1  2'SYSPED/NCTS062R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 63'-'
     A                                      COLOR(WHT)
     A            TVLI           1Y 0O  1 65COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'HENDELSE UNDERVEIS:'
     A                                      COLOR(WHT)
     A                                  3 22'Kode:'
     A            TVKD           1A  O  3 28TEXT('Sted')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4 22'Beskrivelse:'
     A            TVTXT        512A  O  4 35TEXT('Beskrivelse')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
      * WSTXT tar 7 linjer
     A                                 11  2'Dato:'
     A            WSADT          6Y 0O 11 12TEXT('Dato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                 11 22'Autorit:'
     A            TVAAUT        35A  O 11 32TEXT('Autoritet')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2'Sted:'
     A            TVASD         35A  O 12 12TEXT('Sted')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12 52'Land:'
     A            TVALK          2A  O 12 12TEXT('Land')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2'Lokasjon:'
     A            TVQID          1A  O 13 12TEXT('Kvalfier ID')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  2'Lok.kode:'
     A            TVSD          17A  O 14 12TEXT('Stedskode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14 32'Land:'
     A            TVLK           2A  O 14 42TEXT('Land')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15  2'Adresse:'
     A            TVAD1         30A  O 15 12TEXT('Adresse')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVPN          17A  O 16 12TEXT('Postnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVPS          24A  O 16 30TEXT('Poststed')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17  2'Cont:'
     A            TVCNR         17A  O 17 12TEXT('Containernr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 18  2'Seal:'
     A            TVANTS         4Y 0O 18 12TEXT('Antall seal')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TVSID         20A  O 18 22TEXT('Seal id')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 18 44'Itemnr:'
     A            TVDLI          5Y 0O 18 52TEXT('Item nr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                 18 62'Cont.:'
     A            TVKDC          1Y 0O 18 69TEXT('Container?')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                 19  2'Tr.midl.:'
     A            TVTTY          2Y 0O 19 12TEXT('Transporttype')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Z)
     A            TVTID         35A  O 19 15TEXT('Tr.middel id')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVTLK          2A  O 19 51TEXT('Tr.middel land')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 23  2'F12=Forrige   F15=Tillegg opplysni-
     A                                      nger'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til bilde02')
     A N27                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                  1  2'SYSPED/NCTS062R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis'
     A                                      COLOR(BLU)
     A                                  3  2'Op LN Hendelses kode og beskrivels-
     A                                      e         Hendelses dato og sted   -
     A                                             '
     A                                      COLOR(WHT)
     A N27                             22  2'F3=Neste/avslutt   F5=Forny   F6=L-
     A                                      ag ny   F8=Hopp til'
     A                                      COLOR(BLU)
     A  27                             22  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A N36                             22 58'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A N27                             23  2'F12=Forrige   F17=Ny NCTS kopier f-
     A                                      ra denne'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  3  2'Hopp til postnr. -->'
     A            W3LI           1S 0I  3 24DSPATR(UL)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SUBFIL2                   SFL
     A            WSNR           1A  O  5  2
     A            TVLI           1Y 0O  5  5EDTCDE(4)
     A            TVKD           1A  O  5  8
     A            TVTXT_35      35A  O  5 10
     A            WSADT          6Y 0O  5 46EDTWRD('  .  .  ')
     A            TVASD_20      20A  O  5 55
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N93N94                            ROLLUP(25)
     A            ZRECN2         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS062R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Trykk Enter for å bekrefte at du v-
     A                                      algte 4=Slette.'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk F12 for å gå tilbake og endr-
     A                                      e valget.'
     A                                      COLOR(BLU)
     A                                  4  2'Op LN Hendelssted                 -
     A                                              K F Omlasting              -
     A                                             '
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'Gå tilbake')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F12=Avbryt'
     A                                      COLOR(BLU)
