     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLAVD0' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 70 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1Y 0B  6  2EDTCDE(4)
     A            FE_N3035       3A  O  5  5COLOR(WHT)
     A            FE_C3412      35A  O  5  9COLOR(WHT)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CF20(20 'A/I')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYCTA00R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC0238 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(25 DIV1034 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(27 YBC0239 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KOD          4Y 0I  4  5DSPATR(UL)
     A            W2SØK         30A  I  4 10DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(38 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYCTA00R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC0238 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(25 DIV1034 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(27 YBC0239 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KOD          4Y 0I  4  5DSPATR(UL)
     A            W2SØK         30A  I  4 10DSPATR(UL)
     A N30                             15  2MSGCON(38 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
