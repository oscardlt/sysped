      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR06GL MODULE(*LIBL/TJETUR06GL *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FTURER14   IF   E           K DISK    USROPN
     FKODTH     IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D WSUSER          S             10
     D WSAVD           S              4
     D WSTUR           S              8
     D BSTUR           S              8  0
     D IFSFIL          S             50
     D EMNE            S             44
     D SENDER          S             17
     D CMD             S           5000
     D MELD            S           5000
     D XEPMOTT         S             50    DIM(5)
     D JSONstring      s          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE

     C                   EXSR      INIT
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬wstur¬ : ¬'+%trim(WSTUR)+'¬, '
        +'¬ifsfil¬ : ¬'+%trim(IFSFIL)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   IF        ERROR=*BLANKS

     C                   EVAL      JSONstring ='"prtgodslist" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EXSR      PRINT

     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
     C                   ENDIF
      * Quit
     C                   EXSR      EXIT
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         BEGSR
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSTUR   = zhbgetvar('WSTUR')
     C                   EVAL      BSTUR   = c2n2(WSTUR)
     C                   ENDSR
      *=====================================================================******
      * Send MEDLING TIL BRUKER
      *=====================================================================******
     C     SNDEMAIL      BEGSR

     C                   Z-ADD     1024          CMDL             15 5
     C                   EVAL      SENDER = '*CURRENT'
     C                   MOVE      TUPRO         XTUPRO            8

     C     1             DO        5             XN                3 0
     C                   IF        XEPMOTT(XN) <> *BLANKS

     C                   EVAL      EMNE = 'Print goodslist '
     C                                    + %trim(XTUPRO)

     C                   EVAL      MELD = 'Goodslist is printed by '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ':/NTrip No. ' + %trim(%editc(TUPRO:'Z'))
     C                             + ':/P:/PThis message is created '
     C                             + 'automaticly by the booking system.'
     C                             + ':/NPlease do not reply!'

     C                   EVAL      cmd = 'SNDDST TYPE(*LMSG) TOINTNET('
     C                             +'('+ X'7D' +%trim(XEPMOTT(XN))+ X'7D' +')) +
     C                             DSTD('+ X'7D' + %trim(EMNE) + X'7D' + ') +
     C                             LONGMSG('+ X'7D' + %trim(MELD) + X'7D' +') +
     C                             USRID('+  %trim(SENDER) +') SNDFMT(*NOCHG)'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    CMD
     C                   PARM                    CMDL

     C                   ENDIF
     C                   ENDDO

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      BRPARF
     C  N50              OPEN      TURER14
     C  N50              OPEN      KODTH
     C  N50              OPEN      KODTA

     C     KOHKEY        KLIST
     C                   KFLD                    KOHUNI
     C                   KFLD                    TUAVD
     C                   MOVE      'H'           KOHUNI
     C     KOAKEY        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    TUAVD
     C                   MOVE      'A'           KOAUNI
     C     BrParKey      KLIST
     C                   KFLD                    WSUSER
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID

     C                   MOVE      'EPOST'       PAID
     C                   Z-ADD     0             XN
     C     BrParKey      CHAIN     BRPARF                             33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVEL     PAVRDA        XEPMOTT(XN)
     C     XN            COMP      5                                      33
     C  N33BrParKey      READE     BRPARF                                 33
     C                   ENDDO
     C  N50BSTUR         CHAIN     TURER14                            51
     C   51              eval      Error = 'Invalid trip'

     C                   IF        *IN50=*OFF
     C                   IF        *IN51=*OFF
     C     KOHKEY        CHAIN     KODTH                              33
      *  Kall prog for å generere html-fil eller PDF-fil i IFS.
     C                   TIME                    Timedata1
     C                   MOVEL     timedata1     TS26             26
     C                   MOVE      TS26          TS15             15
     C                   MOVE      BSTUR         WSTUR
     C                   EVAL      IFSFil = '/pdf/tmp/GL'
     C                             + WSTUR + '_' + TS15 + '.PDF'

     C                   MOVE      TUAVD         WSAVD
     C                   CALL      'ETUR06CGL'
     C                   PARM                    WSAVD
     C                   PARM                    WSTUR
     C                   PARM                    IFSFIL
     C                   PARM                    WSUSER
     C                   PARM                    KOHLS2                         L=LIGG/S=STÅENDE
     C                   ENDIF
     C                   ENDIF

     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR

     C                   CLOSE     BRIDF
     C  N50              CLOSE     BRPARF
     C  N50              CLOSE     TURER14
     C  N50              CLOSE     KODTH
     C  N50              CLOSE     KODTA

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *_______________________________________________________
      * Skriv ut                                         PRINT
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     PRINT         BEGSR

     C                   IF        XEPMOTT(1) <> *BLANKS
     C                   EXSR      SNDEMAIL
     C                   ENDIF
     C     KOAKEY        CHAIN     KODTA                              33
     C                   IF        *in33='0' and KOAIE='1'
      * Generer FB når de ikke finnes
     C                   CALL      'SYFA55IFB'
     C                   PARM                    WSTUR
     C                   PARM      'J'           FBFUNK            1
      * Print FB til standard printer
     C                   CALL      'PRTFBPDAC'
     C                   PARM      BIFIRM        FBFIRM            2
     C                   PARM                    WSAVD
     C                   PARM                    WSTUR
      * Print GL til standard printer
     C                   CALL      'PRTGLPDAC'
     C                   PARM                    WSAVD
     C                   PARM                    WSTUR
     C                   ENDIF
     C                   ENDSR
