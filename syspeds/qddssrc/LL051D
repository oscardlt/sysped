      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P06XX * 01 YBC PTF6 KOPI AV ENKEL AVTALE
P06XX * 02 JOV PTF6 VIS TEKSTER PÅ VARELI / FLYTTET 1 LINJE OPP
P8068 * 05 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LLGRPK LLGRPKR)
     A                                      PRINT
N05  A                                      HLPPNLGRP(HLPSCR LL051PNL)
N05  A                                      HLPTITLE('ARBEIDE MED GRUPPE/KUNDEA-
N05  A                                      VTALER PR KUNDE')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
S01  A*           WSNR           1A  I  5  2VALUES(' ' '2' '4')
N01  A            WSNR           1A  I  5  2VALUES(' ' '2' '3' '4')
     A            KATYPE         1A  O  5  5
     A            KALLT          3A  O  5  9
     A            KALPT          3A  O  5 15
     A            KAGEB          3A  O  5 21
     A            KAPR           7  2O  5 27EDTCDE(4)
     A            KAPRM          7  2O  5 36EDTCDE(4)
     A            KAMDAG         2  0O  5 45EDTCDE(4)
     A            KADAG          2  0O  5 55
     A            KADTUT         8  0H
     A            KAMVA          1A  O  5 61
     A            KAVAL          3A  O  5 65
     A            XXDTUT         6  0O  5 72EDTCDE(Y)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'Lag ny')
     A                                      CA12(12 'AVSLUTT')
     A                                      OVERLAY
S05  A*         H                           HLPDOC('LL05101' 'LL051' 'SY400LO/L-
S05  A*                                     OGI')
N05  A          H                           HLPPNLGRP(LL05101 LL051PNL)
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/LL051R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1569 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(07 YBC1577 SYMSGF)
     A            UGRUP          8Y 0O  2 10DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            UGNV          20A  O  2 19COLOR(BLU)
     A                                  2 42MSGCON(06 YBC1578 SYMSGF)
     A            UKJED          8Y 0O  2 49DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            UPNV          20A  O  2 58COLOR(BLU)
     A                                  3  2MSGCON(50 YBC1579 SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(78 YBC1580 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  5MSGCON(75 YBC1581 SYMSGF)
     A                                      DSPATR(HI)
     A            WSTYPE         1A  B  2  5DSPATR(UL)
     A N30N40
     AO 90N40                               DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A                                      VALUES('I' 'U' 'A')
     A            WSLLT          3A  B  2  9DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSLPT          3A  B  2 15DSPATR(UL)
     A  88                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSGEB          3A  B  2 21DSPATR(UL)
     A  87                                  DSPATR(RI)
     A N30 40
     AO 87                                  DSPATR(PC)
     A            WSPR           7Y 2B  2 27DSPATR(UL)
     A  86                                  DSPATR(RI)
     A  86                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            WSPRM          7Y 2B  2 36DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSMDAG         2Y 0B  2 45DSPATR(UL)
     A  85                                  DSPATR(RI)
     A  85                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            WSDAG          2Y 0B  2 55DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSMVA          1A  B  2 61DSPATR(UL)
     A  84                                  DSPATR(RI)
     A  84                                  DSPATR(PC)
     A                                      VALUES(' ' 'J' 'N')
     A            WSVAL          3A  B  2 65DSPATR(UL)
     A  83                                  DSPATR(RI)
     A  83                                  DSPATR(PC)
     A            WSDTUT         6Y 0B  2 72DSPATR(UL)
     A  82                                  DSPATR(RI)
     A  82                                  DSPATR(PC)
     A            WSLTTX        35A  O  3  5
     A            WSBMTX        40A  O  3 41
     A                                  4  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A            WSGETX        24A  O  4 41
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(75 YBC1581 SYMSGF)
     A                                      DSPATR(HI)
     A            KATYPE         1A  O  2  5DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            KALLT          3A  O  2  9DSPATR(UL)
     A                                      DSPATR(HI)
     A            KALPT          3A  O  2 15DSPATR(UL)
     A                                      DSPATR(HI)
     A            KAGEB          3A  O  2 21DSPATR(UL)
     A                                      DSPATR(HI)
     A            KAPR           7Y 2O  2 27DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KAPRM          7Y 2O  2 36DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KAMDAG         2Y 0O  2 45DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KADAG          2  0O  2 55DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KAMVA          1A  O  2 61DSPATR(UL)
     A                                      DSPATR(HI)
     A            KAVAL          3A  O  2 65DSPATR(UL)
     A                                      DSPATR(HI)
     A            XXDTUT         6Y 0O  2 72DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A*
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA06(06 'Lag ny')
     A                                      CA12(12 'AVSLUTT')
S05  A*         H                           HLPDOC('LL05101' 'LL051' 'SY400LO/L-
S05  A*                                     OGI')
N05  A          H                           HLPPNLGRP(LL05101 LL051PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL051R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1569 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(07 YBC1577 SYMSGF)
     A            UGRUP          8Y 0O  2 10DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            UGNV          20A  O  2 19COLOR(BLU)
     A                                  2 42MSGCON(06 YBC1578 SYMSGF)
     A            UKJED          8Y 0O  2 49DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            UPNV          20A  O  2 58COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1580 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
