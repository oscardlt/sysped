      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TARC001R MODULE(*LIBL/TARC001R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          c                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D WTYPE           S              2
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d X               S             10  0
     d hopp            S              1
     d STOPP5          S             35
     d pos             S              5U 0
     d MyPoDS          ds                  Qualified  dim(500)
     d  ARTYPE                        2
     d  ARTXT                        15
     d  ARKJN                         1
     d  ARKSND                        1
     d  ARKLAG                        2
     d  ARKDAG                        5S 0
     d  ARKVED                       60
     d  ARSLAB                        1
     d  ARSBAN                       50
     d  ARMRG                         1
     d  ARVEDL                       40
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬type¬ : ¬'+%trim(WTYPE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"arkivkodelist" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC) +
         ' ARTYPE <> ' + cQuote + ' ' + cQuote +' ';

       If    WTYPE  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(ARTYPE) LIKE UPPER(' + cQuote + '%' +
         %trim(WTYPE) + '%' + cQuote + ')';
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select ARTYPE, ARTXT, ARKJN, ARKSND, ' +
                             'ARKLAG, ARKDAG, ARKVED, ARSLAB, ' +
                             'ARSBAN, ARMRG, ARVEDL ' +
                             'from ARKTXT ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'ARTYPE desc ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         wtype   = zhbgetvar('TYPE');

        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).artype = *blanks;
         leave;
       endif;

           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬artype¬ : ¬'+%trim(mypods(x).ARTYPE)+'¬, '
          +'¬artxt¬ : ¬'+%trim(mypods(x).ARTXT)+'¬, '
          +'¬arkjn¬ : ¬'+%trim(mypods(x).ARKJN)+'¬, '
          +'¬arksnd¬ : ¬'+%trim(mypods(x).ARKSND)+'¬, '
          +'¬arklag¬ : ¬'+%trim(mypods(x).ARKLAG)+'¬, '
          +'¬arkdag¬ : ¬'+%trim(%editc(mypods(x).ARKDAG:'Z'))+'¬, '
          +'¬arkved¬ : ¬'+%trim(mypods(x).ARKVED)+'¬, '
          +'¬arslab¬ : ¬'+%trim(mypods(x).ARSLAB)+'¬, '
          +'¬arsban¬ : ¬'+%trim(mypods(x).ARSBAN)+'¬, '
          +'¬armrg¬ : ¬'+%trim(mypods(x).ARMRG)+'¬, '
          +'¬arvedl¬ : ¬'+%trim(mypods(x).ARVEDL)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
