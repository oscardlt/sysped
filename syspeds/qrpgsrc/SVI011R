     FSVIH      UF   E           K DISK
     FSVIHE     UF   E           K DISK
     FSVTHA     IF   E           K DISK
     FSVI011D   CF   E             WORKSTN
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *____________________
      * Definiere variabler
      *""""""""""""""""""""
     C     *LIKE         DEFINE    SVIH_SYAV     ZVIH_SYAV
     C     *LIKE         DEFINE    SVIH_SYOP     ZVIH_SYOP
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVIH_KEY      KLIST
     C                   KFLD                    ZVIH_SYAV
     C                   KFLD                    ZVIH_SYOP
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SVIH_KEY      CHAIN(N)  SVIH                               90
     C                   EVAL      ZVIH_SGDK=SVIH_SGDK
     C                   EVAL      ZVIH_SGUT=SVIH_SGUT
      * Sigiller via keypad eller automatsik ?
     C     SVIH_SYSG     CHAIN     SVTHA
     C     SVTH_DKEY     IFNE      *BLANKS
     C                   EXSR      SRCC
     C                   ENDIF
      * Viser bilde
     C     TAG01         TAG
     C                   EXFMT     SVI011DA
      * andre funksjonstaster
     C     *IN03         CABEQ     *ON           SRB8
     C     *IN12         CABEQ     *ON           SRB9

     C*                  GOTO      TAG01
     C     SRB8          TAG
     C     SVIH_KEY      CHAIN     SVIH                               90
     C                   EVAL      SVIH_SGDK=ZVIH_SGDK
     C                   EVAL      SVIH_SGUT=ZVIH_SGUT
     C                   UPDATE    SVIHR
      * Slett edifact
     C     SVIH_KEY      SETLL     SVIHE
     C     SVIH_KEY      READE     SVIHE                                  51
     C     *IN51         DOWEQ     *OFF
     C                   DELETE    SVIHER
     C     SVIH_KEY      READE     SVIHE                                  51
     C                   ENDDO
      * Lag edifact pånytt med riktig sigill
     C     SVIH_SGDK     IFNE      *BLANKS
     C     SVIH_SGDK     ANDNE     '000000'
     C                   MOVEL     SVIH_MTYP     WVIH_MTYP1        1
     C                   SELECT
     C     SVIH_MTYP     WHENEQ    'HBK'
     C                   CALL      'SVI022RK'
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
     C                   PARM                    ZVIH_SGDK
     C                   PARM                    ZVIH_SGUT
     C     SVIH_MTYP     WHENEQ    'DBK'
     C                   CALL      'SVI022RK'
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
     C                   PARM                    ZVIH_SGDK
     C                   PARM                    ZVIH_SGUT
     C     WVIH_MTYP1    WHENEQ    'H'
     C                   CALL      'SVI022RH'
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
     C                   PARM                    ZVIH_SGDK
     C                   PARM                    ZVIH_SGUT
     C                   OTHER
     C                   CALL      'SVI022RN'
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
     C                   PARM                    ZVIH_SGDK
     C                   PARM                    ZVIH_SGUT
     C                   ENDSL

     C                   ENDIF

     C     SRB9          TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna utstællarsigill                             SRCC /
      *////////////////////////////////////////////////////////////
     C     SRCC          BEGSR
      * Send oppdrag til sigillering
     C                   MOVEL     ZVIH_SYAV     QTFIEL
     C                   MOVE      ZVIH_SYOP     QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM      'SVGSRQ'      QTNAME           10
     C                   PARM      '*LIBL'       QTLIB            10
     C                   PARM      11            QTLEN             5 0
     C                   PARM                    QTFIEL           11
      * Sigillering
     C                   CALL      'SEALCTL'
     C                   PARM      'SVG003R   '  QPARM            10
      * Retur fra sigillering
     C                   MOVEL     ZVIH_SYAV     QKEY
     C                   MOVE      ZVIH_SYOP     QKEY
     C                   CALL      'QRCVDTAQ'
     C                   PARM      'SVGSSQ'      QRNAME           10
     C                   PARM      '*LIBL'       QRLIB            10
     C                   PARM      6             QRLEN             5 0
     C                   PARM                    ZVIH_SGUT
     C                   PARM      99            QWAIT             5 0
     C                   PARM      'EQ'          QORDER            2
     C                   PARM      11            QKILEN            3 0
     C                   PARM                    QKEY             11
     C                   PARM      10            QSNDRL            3 0
     C                   PARM                    QSNDR            10
     C                   ENDSR
