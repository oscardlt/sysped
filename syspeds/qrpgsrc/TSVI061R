      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVI061R MODULE(*LIBL/TSVI061R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVIFZL01  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D UNIKA           S              9
     D REFF            S             17
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬unik¬ : ¬'+%trim(UNIKA)+'¬, '
        +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   EVAL      JSONstring ='"getexternfakt" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta detta ena ærendet.
      *
     c     Error         ifeq      *blanks
     C     SVIFKEY       CHAIN     SVIFZL01                           55
     C     *IN55         IFEQ      '0'
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬svif_syav¬ : ¬'+%trim(%editc(sviF_SYAV:'Z'))+'¬, '
          +'¬svif_syop¬ : ¬'+%trim(%editc(sviF_SYOP:'Z'))+'¬, '
          +'¬svif_faty¬ : ¬'+%trim(sviF_FATY)+'¬, '
          +'¬svif_fatx¬ : ¬'+%trim(sviF_FATX)+'¬, '
          +'¬svif_vakd¬ : ¬'+%trim(sviF_VAKD)+'¬, '
          +'¬svif_vaku¬ : ¬'+%trim(%editc(sviF_VAKU:'4'))+'¬, '
          +'¬svif_fabl¬ : ¬'+%trim(%editc(sviF_FABL:'4'))+'¬, '
          +'¬svif_unik¬ : ¬'+%trim(%editc(sviF_UNIK:'Z'))+'¬, '
          +'¬svif_reff¬ : ¬'+%trim(sviF_REFF)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   ENDIF
     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     sviFZL01

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UNIKA   = zhbgetvar('UNIK')
     C                   EVAL      UNIK  = c2n2(UNIKA)
     C                   EVAL      REFF    = zhbgetvar('REFF')
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C     *LIKE         DEFINE    sviF_UNIK     UNIK
     C     sviFKEY       KLIST
     C                   KFLD                    UNIK
     C                   KFLD                    REFF
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      sviFZL01

     c                   endsr
