/*  BYTTET GETPR2 MED GETPRN FOR DRAWER OG ANTAL.L KOPIER */
/*N02 FEIL I ETT AV GETPRN-KALLENE                        */
    /* PRINT OF INVOICE */
    /* N01  IKKE KOSTNADSOVERSLAG TIL BLINDKØ */
             PGM        PARM(&FFAK &IN05 &IN06 &IN07 &IN08 &KOPI)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&FAKTS) TYPE(*DEC) LEN(3 0) VALUE(073)
             DCL        VAR(&FAKTL) TYPE(*DEC) LEN(3 0) VALUE(019)
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&FFAK) TYPE(*CHAR) LEN(1) /* Hvis 'J', +
                          kaller SYFA20 for å skrive alle +
                          ferdigmeldte fakturaer. */
             DCL        VAR(&IN05) TYPE(*LGL) LEN(1) /* Hvis &in05 +
                          på, betyr programkallinng fra BORDERAUR. */
             DCL        VAR(&KOPI) TYPE(*CHAR) LEN(1) /* Hvis &KOPI +
                          =J, betyr programkallinng fra KOPI       */
             DCL        VAR(&IN06) TYPE(*LGL) LEN(1) /* Hvis &in06 +
                          på, betyr programkalleing fra MANEFEST. */
             DCL        VAR(&IN07) TYPE(*LGL) LEN(1) /* Hvis &in07 +
                          på, betyr programkallinng fra KREDITERING. */
             DCL        VAR(&IN08) TYPE(*LGL) LEN(1) /* Hvis &in08 +
                          på, betyr programkallinng fra FAKTURERING. */
             DCL        VAR(&PRT) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til fakturaer. */
             DCL        VAR(&PT) TYPE(*CHAR) LEN(10) /* Papirtype til +
                          fakturaer. */
             DCL        VAR(&PRTS) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til fakturaer SJØ */
             DCL        VAR(&PTS) TYPE(*CHAR) LEN(10) /* Papirtype til+
                          fakturaer SJØ */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&PRANTE) TYPE(*CHAR) LEN(2) /* Antall +
                          eposter     */
             DCL        VAR(&PRANTS) TYPE(*CHAR) LEN(2) /* Antall +
                          samlefakt   */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(10) /* Unikkode +
                          til register SORT. */
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(9)
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('fa')
             DCL        VAR(&AUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&GIRO) TYPE(*DEC) LEN(3 0) VALUE(027)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&GIRON) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GIROP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PRTPP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PTPP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORMPP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LPI) TYPE(*DEC) LEN(2 0) VALUE(06)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&LPP) TYPE(*DEC) LEN(3 0) VALUE(72)
             DCL        VAR(&FAKT) TYPE(*DEC) LEN(3 0) VALUE(004)
             DCL        VAR(&EMAIL) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&NYPA) TYPE(*CHAR) LEN(1)
             DLTOVR     FILE(*ALL) LVL(*JOB)
             MONMSG     MSGID(CPF0000)
    /* */
    /* */
    /* FIX INVOICE PRINTER FILE */
    /* */
             RTVDTAARA  DTAARA(*LDA (681 10)) RTNVAR(&PRT)
             RTVDTAARA  DTAARA(*LDA (691 10)) RTNVAR(&PT)
             RTVDTAARA  DTAARA(*LDA (961 10)) RTNVAR(&PRTS)
             RTVDTAARA  DTAARA(*LDA (971 10)) RTNVAR(&PTS)
             RTVDTAARA  DTAARA(*LDA (81 1)) RTNVAR(&LTYPE)
             RTVDTAARA  DTAARA(*LDA (901 10)) RTNVAR(&PRTPP)
             RTVDTAARA  DTAARA(*LDA (911 10)) RTNVAR(&PTPP)
    /* */
    /* RETRIVE DEVICEDESCRIPTION */
    /* */
             RTVDTAARA  DTAARA(*LDA (48 1)) RTNVAR(&TYPE)
    /* */
    /* TESTER LASER OG LPI                   */
    /* */
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
             CALL       PGM(GETPRN) PARM(&AVD &FAKT &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             CALL       PGM(GETPRT) PARM(&AVD &GIRO &GIROP &GIRON)

    /*---------------------------------------*/
    /* SETTER LPI TIL 08                     */
    /*---------------------------------------*/
             IF         COND(&LPI2 = 08) THEN(DO)
             CHGVAR     VAR(&LPP) VALUE(096)
             CHGVAR     VAR(&LPI) VALUE(08)
             ENDDO
    /*---------------------------------------*/
    /* SETTER ANTALL LINJER TIL 70/93 VED LASER */
    /*---------------------------------------*/
             IF         COND(&LASER = 'J') THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('070')
             IF         COND(&LPI = 08) THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('093')
             ENDDO
             ENDDO
    /*---------------------------------------*/
    /* SETTEL ANTALL LINJER TIL 70/93 VED LASER */
    /*---------------------------------------*/
             IF         COND(&LASER = 'A') THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('070')
             IF         COND(&LPI = 08) THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('093')
             ENDDO
             ENDDO
    /*---------------------------------------*/
    /* LASER TYPE F GÅR SOM P             */
    /*---------------------------------------*/
             IF         COND(&LASER *EQ 'F') THEN(CHGVAR VAR(&LASER) +
                          VALUE('P'))

    /*---------------------------------------*/
    /* START FOR IKKE PDF DOKUMENTER      */
    /*---------------------------------------*/
STARTNY:     IF         COND(&LASER *NE 'P') THEN(DO)

    /*---------------------------------------*/
    /* STANDARDFAKTURA LAND/FLY              */
    /*---------------------------------------*/
             CALL       PGM(GETPRN) PARM(&AVD &FAKT &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             OVRPRTF    FILE(FAKTURA) PAGESIZE(72 85) CPI(10) +
                          OVRFLW(72) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT) COPIES(&COPIES)
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF FILE(SYFA20P) +
                          DEVTYPE(*IPDS) PAGESIZE(&LPP 80) +
                          LPI(&LPI) CPI(10) OVRFLW(&LPP) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) FONT(011) +
                          CHRID(590 340) FRONTOVL(&LOGO) OUTQ(&PRT) +
                          FORMTYPE(&PT) COPIES(&COPIES))
             ELSE       CMD(OVRPRTF FILE(SYFA20P) DEVTYPE(*SCS) +
                          PAGESIZE(&LPP 80) LPI(&LPI) CPI(10) +
                          OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT) COPIES(&COPIES))
    /*---------------------------------------*/
    /* STANDARDFAKTURA SJØ                   */
    /*---------------------------------------*/
             CALL       PGM(GETPRN) PARM(&AVD &FAKTS &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             IF         COND(&LTYPE = 'S') THEN(OVRPRTF +
                          FILE(SYFA20PS) PAGESIZE(96 80) CPI(10) +
                          OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRTS) FORMTYPE(&PTS))
             ELSE       CMD(OVRPRTF FILE(SYFA20PS) PAGESIZE(72 80) +
                          LPI(6) CPI(10) OVRFLW(72) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRTS) FORMTYPE(&PTS))
             OVRPRTF    FILE(SYFA20PT) PAGESIZE(&LPP 80) LPI(&LPI) +
                          CPI(10) OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRTS) FORMTYPE(&PTS)
             OVRPRTF    FILE(SYFA20PD) PAGESIZE(96 80) CPI(10) +
                          OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT) COPIES(&COPIES)
    /*---------------------------------------*/
    /* FRITTSTÅENDE GIRO                     */
    /*---------------------------------------*/
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF +
                          FILE(SYFA20PG) OUTQ(&GIROP) DEVTYPE(*IPDS) +
                          PAGESIZE(24 80) CPI(10) OVRFLW(24) +
                          FONT(011) CHRID(590 340) +
                          FORMTYPE(&GIRON))
             ELSE       CMD(OVRPRTF FILE(SYFA20PG) OUTQ(&GIROP) +
                          DEVTYPE(*SCS) PAGESIZE(24 80) CPI(10) +
                          OVRFLW(24) FORMTYPE(&GIRON))
             IF         COND(&EMAIL = 'N') THEN(DO)
             CALL       PGM(GETPRN) PARM(&AVD &FAKTL &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             OVRPRTF    FILE(SYFA27PP) PAGESIZE(70 80) CPI(10) +
                          OVRFLW(66) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRTPP) FORMTYPE(&PTPP) +
                          COPIES(&COPIES) /* Pirnterfil av +
                          samlefaktura liste */
             ENDDO
             ENDDO
    /*                                    */
    /* SLUTT PÅ IKKE PDF                  */
    /*                                    */
    /*                                    */
    /* START FOR PDF DOKUMENTER           */
    /*                                    */
             IF         COND(&LASER = 'P') THEN(DO)
    /*---------------------------------------*/
    /* STANDARDFAKTURA LAND/FLY              */
    /*---------------------------------------*/
   /*S02     CALL       PGM(GETPRN) PARM(&AVD &FAKT &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO) */
   /*N02*/   CALL       PGM(GETPRN) PARM(&AVD &FAKT &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
    /* */
             OVRPRTF    FILE(FAKTURA) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          85) CPI(10) OVRFLW(72) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FNTCHRSET(*LIBL/C0D0GT10 +
                          *LIBL/T1V10277) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT) COPIES(&COPIES)
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF FILE(SYFA20P) +
                          DEVTYPE(*AFPDS) PAGESIZE(&LPP 80) +
                          LPI(&LPI) CPI(10) OVRFLW(&LPP) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) FONT(011) +
                          CHRID(590 340) FNTCHRSET(*LIBL/C0D0GT10 +
                          *LIBL/T1V10277) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT) COPIES(&COPIES))
             ELSE       CMD(OVRPRTF FILE(SYFA20P) DEVTYPE(*AFPDS) +
                          PAGESIZE(&LPP 80) LPI(&LPI) CPI(10) +
                          FRONTMGN(0.2 0.1) OVRFLW(&LPP) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) +
                          FNTCHRSET(*LIBL/C0D0GT10 *LIBL/T1V10277) +
                          FRONTOVL(&LOGO) OUTQ(&PRT) FORMTYPE(&PT) +
                          COPIES(&COPIES))
    /*---------------------------------------*/
    /* SJØFAKTURA                            */
    /*---------------------------------------*/
             CALL       PGM(GETPRN) PARM(&AVD &FAKTS &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             IF         COND(&LTYPE = 'S') THEN(OVRPRTF +
                          FILE(SYFA20PS) DEVTYPE(*AFPDS) +
                          PAGESIZE(96 80) CPI(10) OVRFLW(96) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) +
                          FNTCHRSET(*LIBL/C0D0GT10 *LIBL/T1V10277) +
                          FRONTOVL(&LOGO) OUTQ(&PRTS) +
                          FORMTYPE(&PTS) COPIES(&COPIES))
             ELSE       CMD(OVRPRTF FILE(SYFA20PS) DEVTYPE(*AFPDS) +
                          PAGESIZE(72 80) LPI(6) CPI(10) OVRFLW(72) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) +
                          FNTCHRSET(*LIBL/C0D0GT10 *LIBL/T1V10277) +
                          FRONTOVL(&LOGO) OUTQ(&PRTS) +
                          FORMTYPE(&PTS) COPIES(&COPIES))
             OVRPRTF    FILE(SYFA20PT) DEVTYPE(*AFPDS) PAGESIZE(&LPP +
                          80) LPI(&LPI) CPI(10) OVRFLW(&LPP) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) +
                          FNTCHRSET(*LIBL/C0D0GT10 *LIBL/T1V10277) +
                          FRONTOVL(&LOGO) OUTQ(&PRTS) +
                          FORMTYPE(&PTS) COPIES(&COPIES)
             OVRPRTF    FILE(SYFA20PD) DEVTYPE(*AFPDS) PAGESIZE(96 +
                          80) CPI(10) OVRFLW(96) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FNTCHRSET(*LIBL/C0D0GT10 +
                          *LIBL/T1V10277) FRONTOVL(&LOGO) +
                          OUTQ(&PRT) FORMTYPE(&PT) COPIES(&COPIES)
    /*---------------------------------------*/
    /* FRITTSTÅENDE GIRO                     */
    /*---------------------------------------*/
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF +
                          FILE(SYFA20PG) DEVTYPE(*AFPDS) +
                          PAGESIZE(24 80) CPI(10) OVRFLW(24) +
                          FONT(011) CHRID(590 340) +
                          FNTCHRSET(*LIBL/C0D0GT10 *LIBL/T1V10277) +
                          OUTQ(&GIROP) +
                          FORMTYPE(&GIRON))
             ELSE       CMD(OVRPRTF FILE(SYFA20PG) DEVTYPE(*AFPDS) +
                          PAGESIZE(24 80) CPI(10) OVRFLW(24) +
                          FNTCHRSET(*LIBL/C0D0GT10 *LIBL/T1V10277) +
                          OUTQ(&GIROP) +
                          FORMTYPE(&GIRON))
             IF         COND(&EMAIL = 'N') THEN(DO)
             CALL       PGM(GETPRN) PARM(&AVD &FAKTL &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             OVRPRTF    FILE(SYFA27PP) DEVTYPE(*AFPDS) PAGESIZE(70 +
                          80) CPI(10) OVRFLW(66) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FNTCHRSET(*LIBL/C0D0GT10 +
                          *LIBL/T1V10277) FRONTOVL(&LOGO) +
                          OUTQ(&PRTPP) FORMTYPE(&PTPP) +
                          COPIES(&COPIES) /* Pirnterfil av +
                          samlefaktura liste */
             ENDDO
             ENDDO
    /*                                    */
    /* SLUTT PÅ  PDF                      */
    /*                                    */

/* SORT OGSÅ I LØSNING 1 - INLIBL FOR Å SIKRE SORT I RIKTIG BIBL.*/
             RTVDTAARA  DTAARA(FIRMDT (141 10)) RTNVAR(&LIB)
             OVRDBF     FILE(SORT) TOFILE(&LIB/SORT)

 RTVTIME:    RTVJOBA    USER(&AUSER) NBR(&TIME)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &TIME)
             RMVM       FILE(&LIB/SORT) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(&LIB/SORT) MBR(&UNIK)
             MONMSG     MSGID(CPF7306) EXEC(GOTO CMDLBL(RTVTIME))
             OVRDBF     FILE(SORT) TOFILE(&LIB/SORT) MBR(&UNIK)
    /* */
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
             CALL       PGM(SYFA20) PARM(&PRANT &PRANTE &PRANTS +
                          &FFAK &IN05 &IN06 &IN07 &IN08 &KOPI +
                          &EMAIL &NYPA)
    /*                                                          */
    /*   SENDER MELDING TIL BRUKER                              */
    /*                                                          */
             SNDPGMMSG  MSG(' ' *BCAT &PRANT *BCAT 'fakturaer er +
                          klare til utskrift på printer ' *BCAT +
                          &PRT *BCAT '.') TOUSR(&AUSER) MSGTYPE(*INFO)
             SNDPGMMSG  MSG(' ' *BCAT &PRANTE *BCAT 'fakturaer er +
                          fakturert til epostmottagere ') +
                          TOUSR(&AUSER) MSGTYPE(*INFO)
             SNDPGMMSG  MSG(' ' *BCAT &PRANTS *BCAT 'fakturaer er +
                          lagt over i samlefakturakøen ') +
                          TOUSR(&AUSER) MSGTYPE(*INFO)
             RMVM       FILE(&LIB/SORT) MBR(&UNIK)
/*           SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &AUSER +
                          &FINS)) JOB(ARKIVFA) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)  */
/*            CALL PGM(ARC002FACL) PARM(&ATYPE &AUSER)      */
SBMJOB     CMD(CALL PGM(ARC002FACL) PARM(&ATYPE &AUSER +
             &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
             JOBQ(SYJOBQARK)
             RCLRSC
/* &KOPI = I -> SEND INTERNEDI... */
             IF         COND(&KOPI = I) THEN(DO)
             SBMJOB     CMD(CALL PGM(SYKS38CL)) JOB(SYINVE) +
                          JOBD(*USRPRF) JOBQ(SYJOBQPRT)
             CHGVAR     VAR(&KOPI) VALUE(' ')
             ENDDO
             DLTOVR     FILE(*ALL)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(*ALL) LVL(*JOB)
             MONMSG     MSGID(CPF0000)
 SLUTT:      ENDPGM
