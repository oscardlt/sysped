      *********************************************************************
      * Lage ny mottagersadresse
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYL008 MODULE(*LIBL/SYL008
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 LANDKODE
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%
11XXX * 02 JOV PTF11 tekstinfo vedr krav om kundenr - mulighet for autotildel
11XXX * 03 YBC PTF11 TEST PÅ FEIL KODE VED LAGE NY
11XXX * 04 YBC PTF11 FEILRETTING PÅ ENDRING 03
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FVADR      UF A E           K DISK    USROPN
     FKUNKO     UF A E           K DISK    USROPN
     FKUNKO2    IF   E           K DISK    USROPN
     F                                     RENAME(KUNKOR:KUNKOR2)
     FCUNDL01   UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D INNTYPE         S              1
     D WSUSER          S             10
     D WSDRNR          S              5
     D WBDRNR          S              5  0
     D WSKURE          S             17
     D WSDRNA          S             30
     D WSDRN1          S             30
     D WSDRN2          S             30
     D WSDRN3          S             30
N01  D WSLAND          S              2
     D LAGREMAD        S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL011         C                   CONST('Mottakersnr kan ikke være bla-
     D                                     nk.')
     D FEILEN011       C                   CONST('Number of goods receiver can -
     D                                     not be blanks.')
     D FEIL012         C                   CONST('Mottakersnr finnes fra før.')
     D FEILEN012       C                   CONST('Number of goods receiver is -
     D                                     already registered.')
     D FEIL02          C                   CONST('Mottakersnavn må oppgis.')
     D FEILEN02        C                   CONST('Name of goods receiver can -
     D                                     not be blanks.')
     D FEIL03          C                   CONST('Mottakersadresse1 må oppgis.')
     D FEILEN03        C                   CONST('Address 1 has to be given.')
     D FEIL04          C                   CONST('Mottakers postadresse må oppg-
     D                                     is.')
     D FEILEN04        C                   CONST('Post address has to be given-
     D                                     .')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
     C                   IF        ((INNTYPE = 'E') AND (LAGREMAD <> 'Y'))
     C     VADRKOPD      CHAIN(N)  VADR                               33
     C                   MOVE      VAKURE        WSKURE
     C                   MOVE      VADRNA        WSDRNA
     C                   MOVE      VADRN1        WSDRN1
     C                   MOVE      VADRN2        WSDRN2
     C                   MOVE      VADRN3        WSDRN3
N01  C                   MOVE      VALAND        WSLAND
     C                   END
      * hvis en har tastet submit fra vedlikehodsbildet, test/lagre
      * (men bildet vises på ny dersom det er feil ...)
     c     LAGREMAD      IFEQ      'Y'
     c                   exsr      tstfeil
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   IF        ((XKRAVKN = 'N') AND (FMELDNR = *BLANKS))
      * SETT CURSOR
     C                   EVAL      FMELDNR = '02'
     C                   END
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
     C                   IF        XKRAVKN = 'J'
     C                   callp     wrtsection('KUNDENR1')
     C                   ELSE
     C                   callp     wrtsection('KUNDENR2')
     C                   END
     C                   callp     wrtsection('KUNDENR3')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
N01  C     WSKURE        IFEQ      *BLANKS
N01  C     XKRAVKN       ANDEQ     'A'
N01   * Nummer blank = Autotildel - prio 1 5 første av Navn, 10 første  ellers 17 første
N01  c                   eval      WSKURE=%subst(WSDRNA:1:5)
N01  C     KUNKO2K       CHAIN     KUNKO2                             33
N01  c   33              eval      WSKURE=%subst(WSDRNA:1:10)
N01  C   33KUNKO2K       CHAIN     KUNKO2                             33
N01  c   33              eval      WSKURE=WSDRNA
N01  C   33KUNKO2K       CHAIN     KUNKO2                             33
N01  c                   endif
      * Er MOTTAKERSNR BLANK
     C     WSKURE        IFEQ      *BLANKS
     C     XKRAVKN       ANDEQ     'J'
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN011     FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL011       FEILMELDING
     C                   END
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      * Er MOTTAKERSNR FINNES FRA FØR
     C                   IF        WSKURE <> *BLANKS
     C     KUNKO2K       CHAIN     KUNKO2                             33
     C  N33              IF        ((INNTYPE <> 'E') OR (KUVADR <> WBDRNR))
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN012     FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL012       FEILMELDING
     C                   ENDIF
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
     C                   END
      * Test NAVN PÅ MOTTAKER
     c     WSDRNA        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN02      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL02        FEILMELDING
     C                   ENDIF
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * Test ADRESSE 1
     c     WSDRN1        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN03      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL03        FEILMELDING
     C                   ENDIF
     C                   MOVE      '03'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * Test MOTTAKERS POSTADRESSE
     c     WSDRN3        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN04      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL04        FEILMELDING
     C                   ENDIF
     C                   MOVE      '04'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * ALT OK - eksisterend tur eller lag ny - Deretter avslutt
     C                   MOVE      'Y'           AVSLUTT
      *
S03  C*                  IF        INNTYPE = 'E'
N03  C                   IF        INNTYPE = 'E' OR INNTYPE = 'F'
     C     VADRKOPD      CHAIN     VADR                               33
S03  C*                  ELSE
N04  C                   ELSE
N04  C                   EVAL      *IN33 = *ON
N04  C                   END
N03  C                   IF        *IN33
      * HENT SISTADRESSENR
     C     2             DO        99998         XN                5 0
     C     VADRK         CHAIN(N)  VADR                               33
     C                   IF        *IN33
     C                   MOVE      XN            WSADNR            5 0
     C                   LEAVE
     C                   END
     C                   ENDDO
      *
     C                   CLEAR                   VADRR
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      BIFIRM        FIRMA
     C                   MOVE      BIKUNR        KUNDNR
     C                   MOVE      XN            VADRNR
N03  C                   EVAL      *IN33 = *ON
N03  C                   END
S04  C*                  END
      *
     C                   MOVE      WSDRNA        VADRNA
     C                   MOVEL     WSDRNA        SONAVN
     C                   MOVE      WSDRN1        VADRN1
     C                   MOVE      WSDRN2        VADRN2
     C                   MOVE      WSDRN3        VADRN3
N01  C                   MOVE      WSLAND        VALAND
     C                   IF        XKRAVKN = 'J'
     C                   MOVE      WSKURE        VAKURE
     C                   ELSE
     C                   MOVE      *BLANKS       VAKURE
     C                   MOVEL     VADRNR        VAKURE
     C                   MOVEL     VAKURE        WSKURE
     C                   END
      *
S03  C*                  IF        INNTYPE = 'E'
S04  C*                  IF        INNTYPE = 'E' OR INNTYPE = 'F'
S04  C*                  IF        *IN33 = *OFF
N04  C                   IF        *IN33 = *OFF AND
N04  C                             (INNTYPE = 'E' OR INNTYPE = 'F')
     C  N33              UPDATE    VADRR
     C  N33KUNKOK        CHAIN     KUNKO                              33
     C  N33              MOVE      SONAVN        KUALFA
     C  N33              UPDATE    KUNKOR
     C                   ELSE
     C                   WRITE     VADRR
      *
     C                   MOVE      BIKUNR        KUKUN1
     C                   MOVE      BIKUNR        KUKUN2
     c                   MOVE      WSKURE        KUINTN
     c                   MOVE      SONAVN        KUALFA
     c                   MOVE      VADRNR        KUVADR
     C                   WRITE     KUNKOR
S04  C*                  END
     C                   END

      *
     C     UtTest        endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      LAGREMAD = zhbgetvar('LAGREMAD')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      INNTYPE = zhbgetvar('INNTYPE')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSDRNR  = zhbgetvar('WSDRNR')
     C                   eval      WBDRNR  = c2n2(WSDRNR)
     C                   eval      WSKURE  = zhbgetvar('WSKURE')
     C                   eval      WSKURE  = uppify(WSKURE)
     C                   IF        WSKURE  = 'UNDEFINED'
     C                   eval      WSKURE  = *BLANKS
     C                   END
     C                   eval      WSDRNA  = zhbgetvar('WSDRNA')
     C                   eval      WSDRNA  = uppify(WSDRNA)
     C                   eval      WSDRN1  = zhbgetvar('WSDRN1')
     C                   eval      WSDRN1  = uppify(WSDRN1)
     C                   eval      WSDRN2  = zhbgetvar('WSDRN2')
     C                   eval      WSDRN2  = uppify(WSDRN2)
     C                   eval      WSDRN3  = zhbgetvar('WSDRN3')
     C                   eval      WSDRN3  = uppify(WSDRN3)
N01  C                   eval      WSLAND  = zhbgetvar('WSLAND')
N01  C                   eval      WSLAND  = uppify(WSLAND)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updhtmlvar('WSNAVN':KNAVN)
     C                   callp     updhtmlvar('WSKURE':WSKURE)
     C                   callp     updhtmlvar('WSDRNA':WSDRNA)
     C                   callp     updhtmlvar('WSDRN1':WSDRN1)
     C                   callp     updhtmlvar('WSDRN2':WSDRN2)
     C                   callp     updhtmlvar('WSDRN3':WSDRN3)
N01  C                   callp     updhtmlvar('WSLAND':WSLAND)
     C                   callp     updHtmlVar('INNTYPE':INNTYPE)
     C                   callp     updhtmlvar('WSDRNR':WSDRNR)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYL008'
     C                   CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
     C                   OPEN      VADR
     C                   open      KUNKO
     C                   open      KUNKO2
     C                   open      BRPARF                               50
     C                   open      CUNDL01                              50
      *
     C     KUNKEY        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     KUNKEY        chain     cundl01
     C     VADRK         klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   kfld                    XN
     C     VADRKOPD      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   kfld                    WBDRNR
     C     KUNKOK        klist
     C                   kfld                    BIKUNR
     C                   kfld                    KUNDNR
     C                   kfld                    WBDRNR
     C     KUNKO2K       klist
     C                   kfld                    BIKUNR
     C                   kfld                    WSKURE
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      PABRID = WSUSER
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   MOVEL     'HEUKN'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        ((*IN33 OR (PAVRDA = 'J')) OR
     C                              (PAVRDA = 'j'))
     C                   MOVE      'J'           XKRAVKN           1
     C                   ELSE
     C                   MOVE      'N'           XKRAVKN
     C                   END
      *
     C                   MOVE      *BLANKS       FEILMELDING
     C                   move      *BLANKS       FMELDNR
     C                   MOVE      'N'           AVSLUTT           1
      *
     C                   endsr
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     VADR
     C                   close     KUNKO
     C                   close     KUNKO2
     C                   close     cundl01
     C                   eval      *inlr = *on
      *
     C                   endsr
