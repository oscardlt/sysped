********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 UNNGÅ LOOP
********************************************************************
      *
      *****************************************************************
      * PROGRAM FOR Å Synkroniser ant/vekt/vol mm i en DUP-kjede    ***
      ***
      *  Bør fikses senere
      *     - dersom det er spredning (dup via flere fraktbrev skipp)
      *     - Hvis IKKE (ett oppdrag/ett farktbrev= nr 001) oppdater også FRbrev
      *     - dersom oppdraget er på tur akkumuler turtotaler
      *****************************************************************
      *
     FHEADF     UF   E           K DISK
     FTRACKL02  IF A E           K DISK
      *
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  DUP                    1   1100  0
     D                                     DIM(100)
     D                 DS
     D  AvdOpd                 1     11  0
     D   DsAvd                 1      4  0
     D   DsOpd                 5     11  0
      *
     C                   EXSR      INIT
     c                   exsr      AddDup
     c                   exsr      DupOppd
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *******************************************************
     C     INIT          BEGSR
      *******************************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UTTACTI           3
     C                   PARM                    UTTDATE           8 0
     C                   PARM                    UTTTIME           6 0
      *
      *
     C     HeaKey        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     HEAKY0        KLIST
     C                   KFLD                    TRAVD0
     C                   KFLD                    TROPD0
     C     DsHeaKey      KLIST
     C                   KFLD                    DsAVD
     C                   KFLD                    DsOPD
     C     TTKEY         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    UTTACTI
     C                   KFLD                    UTTDATE
     C                   KFLD                    UTTTIME
      *
     C     HeaKey        Chain(N)  Headf                              33
     C     *IN33         IFEQ      *ON
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDIF
      *
      * FINN SISTE ACTIVITY AV AKTUELL KODE - DUPLISER OGSÅ DEN
     C     UTTACTI       IFNE      *blanks
     C     TTKEY         chain     TRACKL02                           33
     C                   ENDIF
     C     *LIKE         DEFINE    HENT          NYNT
     C     *LIKE         DEFINE    HEVS1         NYVS1
     C     *LIKE         DEFINE    HEVS2         NYVS2
     C     *LIKE         DEFINE    HEGM1         NYGM1
     C     *LIKE         DEFINE    HEGM2         NYGM2
     C     *LIKE         DEFINE    HEVKT         NYVKT
     C     *LIKE         DEFINE    HELM          NYLM
     C     *LIKE         DEFINE    HEM3          NYM3
     C     *LIKE         DEFINE    HEFBV         NYFBV
      * TA VARE PÅ VERDIER FRA OPPDRAGET SOM ER ENDRET - DISSE KAL INN PÅ ALLE ANDRE
     C                   MOVE      HENT          NYNT
     C                   MOVE      HEVS1         NYVS1
     C                   MOVE      HEVS2         NYVS2
     C                   MOVE      HEGM1         NYGM1
     C                   MOVE      HEGM2         NYGM2
     C                   MOVE      HEVKT         NYVKT
     C                   MOVE      HELM          NYLM
     C                   MOVE      HEM3          NYM3
     C                   MOVE      HEFBV         NYFBV

     C                   ENDSR

      ***********************************************
     C     ADDDUP        BEGSR
      ***********************************************
      * Finn ut om oppdraget er en del av en "DUP-struktur" - legg alle i ARRAY
      * FINN TOPP-NIVÅET
     C     TROPD0        DOWNE     0
     C     HEAKY0        CHAIN(N)  HEADF                              33
S01  C*                  ENDDO
N01  C  N33              ENDDO
     C* Legg inn topp-oppdraget som start i array - Gå nedover i begge retninger
     c                   clear                   DUP
     c                   z-add     1             Nr                3 0
     c                   movel     heavd         Dup(Nr)
     c                   move      heopd         Dup(Nr)
     c     1             do        100           Cur               3 0
     c     Dup(Cur)      cabeq     *zeros        UtAdd
     c                   move      Dup(Cur)      AvdOpd
     C     DSHeaKey      CHAIN(N)  HEADF                              33
     C  N33TrOpd1        IFNE      *ZEROS
     C                   add       1             Nr
     c                   movel     TrAvd1        Dup(Nr)
     c                   move      TrOpd1        Dup(Nr)
     C                   ENDIF
     C  N33TrOpd2        IFNE      *ZEROS
     C                   add       1             Nr
     c                   movel     TrAvd2        Dup(Nr)
     c                   move      TrOpd2        Dup(Nr)
     C                   ENDIF
     c                   enddo
     C     UtAdd         ENDSR
      **************************************************************************
     C     DupOppd       begsr
      **************************************************************************
     C                   MOVE      'KOP'         TTEDRE                         KOPI FRA ANNET...
     c                   movel     UAVD          AvdOpdN          11 0
     c                   move      UOPD          AvdOpdN
      * Dupliser hendelse til alle oppdrag i "DUP-struktur"
     c     1             do        100           Nr
     c     Dup(Nr)       cabeq     *zeros        UtDupArr
     c                   move      Dup(Nr)       AvdOpd
     c     AvdOpd        ifne      AvdopdN
      * Oppdater vekt mm
     C     DSHeaKey      CHAIN     HEADF                              33
     C     *IN33         ifeq      *off
     C                   MOVE      NYNT          HENT
      * klargjort også for merke / vareslag
     C*                  MOVE      NYVS1         HEVS1
     C*                  MOVE      NYVS2         HEVS2
     C*                  MOVE      NYGM1         HEGM1
     C*                  MOVE      NYGM2         HEGM2
     C                   MOVE      NYVKT         HEVKT
     C                   MOVE      NYLM          HELM
     C                   MOVE      NYM3          HEM3
     C                   MOVE      NYFBV         HEFBV
     c                   update    HEADFR
      * Oppdater T&T dersom endringen var logget på inngangsoppdrag
     c     TTACTI        ifne      *blanks
     C                   MOVE      DsAvd         TTAVD
     C                   MOVE      DsOpd         TTOPD
     C                   move      'S'           TTMANU
     c     TRSLAG        ifne      'HO'
     C                   move      ' '           TTMANU
     c                   endif
     C                   WRITE     TRACKFR
     c                   endif
     c                   endif
      *
     C                   ENDIF
     c                   enddo
      *
     C     UtDupArr      ENDSR
      *
