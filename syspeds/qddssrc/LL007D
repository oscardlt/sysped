********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXX * 01 YBC PTF7 NYTT FELT FOR OMREGNINGSFAKTOR (I.F.T. EURO-PALL)
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
N02  A                                      HLPPNLGRP(HLPSCR LL007PNL)
N02  A                                      HLPTITLE('VEDLIKEHOLD LOKASJONSTYPE-
N02  A                                      R LOGISTIKK')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            TKLLT          3A  O  5  5
     A            TKTXT         50A  O  5 11
N01  A            TKOMRF         3  2O  5 62EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      OVERLAY
S02  A*         H                           HLPDOC('LL00701' 'LL007' 'SY400LO/L-
S02  A*                                     OGI')
N02  A          H                           HLPPNLGRP(HLPSCR LL007PNL)
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/LL007R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC0962 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(40 YBC0963 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0964 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  5MSGCON(75 YBC0965 SYMSGF)
     A                                      DSPATR(HI)
     A            WSLLT          3A  B  2  5DSPATR(UL)
     A N30N40
     AO 90N40                               DSPATR(PC)
     A  90                                  DSPATR(RI)
     A     40                               DSPATR(PR)
     A     40                               DSPATR(HI)
     A            WSTXT         50A  B  2 11DSPATR(UL)
     A     40                               DSPATR(PC)
N01  A  89                                  DSPATR(RI)
N01  A            WSOMRF         3  2B  2 62DSPATR(UL)
N01  A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(75 YBC0965 SYMSGF)
     A                                      DSPATR(HI)
     A            TKLLT          3A  O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            TKTXT         50A  O  2 11DSPATR(UL)
     A                                      DSPATR(HI)
N01  A            TKOMRF         3  2O  2 62DSPATR(UL)
N01  A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA06(06 'LAGE NY')
S02  A*         H                           HLPDOC('LL00701' 'LL007' 'SY400LO/L-
S02  A*                                     OGI')
N02  A          H                           HLPPNLGRP(LL00701 LL007PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL007R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC0962 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  3  2MSGCON(40 YBC0963 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0964 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
