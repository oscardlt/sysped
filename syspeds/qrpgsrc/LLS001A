      *********************************************************************
      *  SPØRRING PÅ LAGERPOSTER - LOGISTIKK-SYSTEM
      * PROGRAM FOR Å ANGI SØKEUTVALG/FILTER
      *********************************************************************
      *  RPG ILE MODULE *LIBL/LLS001A
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/LLS001A  MODULE(*LIBL/LLS001A *LIBL/INCGI)
CRTPGM*                             ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *
      * 1 - Receives input from SYMN html form
      *     telling which backgrond color should be used
      * 2 - Executes subroutime "setvardata"
      *     to set skeleton output html member LLS001A
      *     then writes output html section "/$all"
      *     and quits.
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FFIRM      if   e           k disk    usropn
     FCUNDC01   if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
      *--------------------------------------------------------------------
     D lng             s              2
     D BckGnd          s             10
     D quit            s              4
     D UserId          s             10
      *--------------------------------------------------------------------
      * Other variables
      *--------------------------------------------------------------------
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D BridSw          s              1
     D GmUserId        s             10
     D GmBiLibl        s             10
     D GmBiStdL        s              1
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
xxxxx * Get program start time for calculating execution time
xxxxxC                   time                    timedata1
      * Parse input string
     C                   eval      lng    = zhbgetvar('lng')
     C                   eval      BckGnd = zhbgetvar('bckgnd')
     C                   eval      quit   = zhbgetvar('quit')
     C                   eval      userid = zhbgetvar('userid')
      * Convert to uppercase
     C                   eval      lng = uppify(lng)
     C                   eval      BckGnd = uppify(BckGnd)
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      * User changed?? - Set libl ..
     C                   exsr      UserInit
      *------------------
      * Write sections of HTML.
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * 2-Contact persons
     C                   exsr      DspCper
      * 3-End html
     C                   exsr      SetEnd
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C     quit          IFNE      *blanks
     C     BridSw        ifne      *blank
     C                   close     BRIDF
     C                   close     FIRM
     C                   close     CUNDC01
     C                   endif
     C                   eval      *inlr = *on
     C                   ENDIF
      *
     C                   return
      *
      *
      *
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     UserInit      begsr
      * Ved første gangs kall må også User-id-fil åpnes
      * Den ligger i SYCGI (=CURLIB), så libl er her ikke av betydning
     C     BridSW        ifeq      *blank
     C                   open      BRIDF
     C                   eval      BridSW = 'x'
     C                   endif
     C*
     C     UserID        ifne      GmUserID
     C                   move      UserID        GmUserId
     C     UserID        CHAIN     BRIDF                              30
      * Annen library list enn forige kall??  (midlertidig hardkodet)
     C     *IN30         ifeq      *off
     C     BILIBL        ifne      GmBiLibl
     C     BISTDL        orne      GmBiStdL
     C                   move      BILIBL        GmBilibl
     C                   move      BISTDL        GmBiStdL
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   close     cundc01                              30
     C                   close     firm                                 30
     C                   eval      rc = docmd('OVRDBF FILE(CUNDC01) +
     C                             TOFILE(*LIBL/CUNDC01) +
     C                             SECURE(*YES)')
     C                   open      CUNDC01
     c     CundcKey      klist
     c                   kfld                    CFIRMA
     c                   kfld                    CCOMPN
      *
     C                   eval      rc = docmd('OVRDBF FILE(FIRM) +
     C                             TOFILE(*LIBL/FIRM) +
     C                             SECURE(*YES)')
     C                   open      FIRM
     C     *loval        setll     FIRM
     C                   read      FIRM
      *
     C                   endif
     C                   endif
     C                   endif
      *
     C                   move      FIFIRM        CFIRMA
     C                   move      BIKUNR        CCOMPN
      *
     C                   endsr
      *=====================================================================******
      *  Display contact persons to select from
      *=====================================================================******
     C     DspCper       begsr
      *
     C     CundcKey      setll     CUNDCR
     C     CundcKey      reade     CUNDCR                                 40
     C     *in40         doweq     '0'
     C                   exsr      SetSltRow
     C                   callp     wrtsection('typsltrow')
     C     CundcKey      reade     CUNDCR                                 40
     C                   enddo
      *
     C                   callp     wrtsection('typsltend')
      *
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$top
      *=====================================================================******
     C     SetTop        begsr
      *------------------
      * Repeat for the next invocation
      * 1)background color
     C                   callp     updhtmlvar('BCKGND':bckgnd:
     C                             InitHTMLVars)
      * 2)national language
     C                   callp     updhtmlvar('LNG':lng)
      * 3)userid
     C                   callp     updhtmlvar('USERID':userid)
      *------------------
      * <BODY> variables
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
      * Texts color
     C                   callp     updhtmlvar('COLOR':'black')
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('COLOR':'white')
     C                   endif
      * If background black, text must be bold
     C                   callp     updhtmlvar('BOLD':' ')
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   endif
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow
      *=====================================================================******
     C     SetSltRow     begsr
     C                   callp     updhtmlvar('BTTYPE1':CConta)
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'LLS001A'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      *
     C                   endsr
