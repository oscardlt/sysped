      ***********************************************************
      *** PROGRAM SKAL SENDE XML AV MELDING FRA SAD           ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 34-49 51-99                ***
      ***********************************************************
     FXMLTILP   O  A E           K DISK    USROPN
     FFRISOK    IF   E           K DISK
     FTVINF     IF   E           K DISK
     FFIRM      IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FEDIC      UF A E             DISK    USROPN
     FEDIS      O  A E             DISK    USROPN
     FEDIM      O  A E             DISK    USROPN
     FEDISE     O  A E             DISK    USROPN
     D TXT             S             80    DIM(77) CTDATA PERRCD(1)
     D                 DS
     D  XTXT801                1     80
     D  XT801                  1     80    DIM(80)
     D                 DS
     D  XTXT802                1     80
     D  XT802                  1     80    DIM(80)
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
     D  XMBRC                  2     10
     D                 DS
     D  WDT                    1      8
     D  WDTÅ                   1      4
     D  WDTM                   5      6
     D  WDTD                   7      8
     D                 DS
     D  WTM                    1      6
     D  WTMH                   1      2
     D  WTMM                   3      4
     D  WTMS                   5      6
     D                 DS
     D  XPARM1                 1   1024
     D  XP101                  1     10                                         IMP/EKSP
     D  XP102                 11     26                                         SYSPEDREF
     D  XP1021                11     14                                         AVDELING
     D  XP1022                15     21                                         OPPDRAGSNR
     D  XP1023                22     24                                         SIGNATUR
     D  XP103                 27     41                                         GODSNR
     D  XP104                 42     48                                         POSISJON
     D  XP105                 49     83                                         SENDINGSREF
     D  XP106                 84     91                                         MELDINGSDATO
     D  XP1061                84     87                                         MELDINGSDATO ÅR
     D  XP1062                88     89                                         MELDINGSDATO MND
     D  XP1063                90     91                                         MELDINGDATO DAG
     D  XP107                 92     99                                         KLARERINGSDATO
     D  XP1071                92     95                                         KLARERINGSDATO ÅR
     D  XP1072                96     97                                         KLARERINGSDATO MND
     D  XP1073                98     99                                         KLARERINGSDATO DAG
     D  XP108                100    102                                         MELDINGSTYPE
     D  XP109                103    104                                         MELDINGSCKATAGORI
     D  XP110                105    106                                         FUNSJONSKODE
     D  XP111                107    109                                         KVALIFIKATOR
     D  XP112                110    124                                         TOTAL AVGIFTER
     D  XP113                125    140                                         LØPENR
     D  XP114                141    143                                         KODE FOR LØPENR
     D  XP115                144    160                                         PART-ID
     D  XP116                161    195                                         PARTSNAVN
     D  XP117                196    212                                         ANSVARLIG ID
     D  XP118                213    219                                         ANTALL KOLLI
     D  XP118T               213    219    DIM(7)                               ANTALL KOLLI
     D  XP119                220    229                                         RELEASE/ERROR
     D  XP120                230    238                                         MELDINGSNR
     D                 DS
     D  XPARM2                 1   1024
     D  XP201                  1     20                                         ERP+??
     D  XP201T                 1     20    DIM(10)                              ERP+??
     D  XP202                 21    370                                         ERP+??:??????
     D  XP202T                21    370    DIM(10)                              ERP+??:??????
     D  XP203                371    520                                         ERC+???
     D  XP203T               371    520    DIM(50)                              ERC+???
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRINIT
     C                   EXSR      SRBYGGF
      *
     C                   EXSR      SRSTART
      *
     C                   EXSR      SRMSGDET
      *
     C                   EXSR      SRCUSRES
      *
     C                   EXSR      SRSLUTT
     C                   EXSR      SRCLOSE
     C                   EXSR      SRSEND
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRINIT        BEGSR
      ***********************************************
     C                   OPEN      EDIC
     C                   OPEN      EDIS
     C                   OPEN      EDIM
     C                   OPEN      EDISE
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       XMBR
     C                   MOVE      'F'           XMBRA
     C                   MOVE      CSN           XMBRB
     C                   MOVE      XMBR          SEMBR
     C                   MOVE      *BLANKS       SDATA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM
      *
     C                   EVAL      S0004 = 'SYSPEDNO'
     C                   EVAL      S0010 = 'AMPHORA'
     C                   EVAL      S0020 = FSSOK
     C                   EVAL      S0026 = 'XML-CUSTOMSRES'
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'C'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = '*LIBL'
     C                   EVAL      SFILE = 'EDISE'
     C                   EVAL      SMBR  = SEMBR
     C                   EVAL      SMBRS = 0
     C                   EVAL      S0036 = 1
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRBYGGF       BEGSR
      ***********************************************
     C                   CALL      'SYXML03B'
     C                   OPEN      XMLTILP
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSTART       BEGSR
      ***********************************************
     C                   EVAL      SDATA = TXT(001)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(002)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = %trim(TXT(003))
     C                             + %trim(XMBR)
     C                             + '"'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(004)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRMSGDET      BEGSR
      ***********************************************
      * <MessageDetails>
     C                   EVAL      SDATA = TXT(006)
     C                   EXSR      TILFIL
      * <MessageSender>
     C                   EVAL      SDATA = %trim(TXT(008))
     C                             + ' partyId="'
     C                             + %trim(S0004)
     C                             + '">'
     C                   EXSR      TILFIL
      * </MessageSender>
     C                   EVAL      SDATA = TXT(009)
     C                   EXSR      TILFIL
      * <MessageRecipient>
     C                   EVAL      SDATA = %trim(TXT(010))
     C                             + ' partyId="'
     C                             + %trim(S0010)
     C                             + '">'
     C                   EXSR      TILFIL
      * </MessageRecipient>
     C                   EVAL      SDATA = TXT(011)
     C                   EXSR      TILFIL
      * <MessageDate>
     C                   EVAL      SDATA = %trim(TXT(012))
     C                             + ' timeZone="+01:00"'
     C                             + ' dstIndicator="YES">'
     C                             + WDTÅ + '-' + WDTM + '-' + WDTD + 'T'
     C                             + WTMH + ':' + WTMM + ':' + WTMS
     C                   EXSR      TILFIL
      * </MessageDate>
     C                   EVAL      SDATA = TXT(013)
     C                   EXSR      TILFIL
      * </MessageDetails>
     C                   EVAL      SDATA = TXT(007)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCUSRES      BEGSR
      ***********************************************
      * <ConsignmentStructure>
     C                   EVAL      SDATA = %trim(TXT(014))
     C                             + ' structureType="Single">'
     C                   EXSR      TILFIL
      * <ConsignmentSet>
     C                   EVAL      SDATA = TXT(016)
     C                   EXSR      TILFIL
      * Consignment
     C                   EXSR      SRCONSIG
      * CustomsClearance
     C                   EXSR      SRCUSCLR
      * ConsignmentGoodsItemSet
     C                   EXSR      SRCONGIT
      * Party
     C                   EXSR      SRPARTY
      * </ConsignmentSet>
     C                   EVAL      SDATA = TXT(017)
     C                   EXSR      TILFIL
      *
      * </ConsignmentStructure>
     C                   EVAL      SDATA = TXT(015)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONSIG      BEGSR
      ***********************************************
      * <Consignment
     C                   EVAL      XTXT801 = FSSOK
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(018))
     C                             + ' consignmentId="'
     C                             + %trim(XTXT802)
     C                             + '" idType="730">'
     C                   EXSR      TILFIL
      * <NumberOfPackages>
     C     1             DO        7             XN1               1 0
     C                   IF        XP118T(XN1) <> '0'
     C                   LEAVE
     C                   END
     C                   ENDDO
     C     8             SUB       XN1           XN2               1 0
     C                   EVAL      SDATA = %trim(TXT(020))
     C                             + %trim(%subst(XP118:XN1:XN2))
     C                             + %trim(TXT(021))
     C                   EXSR      TILFIL
      * <Reference>
     C                   EVAL      SDATA = TXT(022)
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   EVAL      SDATA = %trim(TXT(024))
     C                             + %trim(FSSOK)
     C                             + %trim(TXT(025))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   EVAL      SDATA = %trim(TXT(026))
     C                             + 'CMR'
     C                             + %trim(TXT(027))
     C                   EXSR      TILFIL
      * </Reference>
     C                   EVAL      SDATA = TXT(023)
     C                   EXSR      TILFIL
      * <Reference>
     C                   EVAL      SDATA = TXT(022)
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   EVAL      SDATA = %trim(TXT(024))
     C                             + %trim(XP102)
     C                             + %trim(TXT(025))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   EVAL      SDATA = %trim(TXT(026))
     C                             + 'AAS'
     C                             + %trim(TXT(027))
     C                   EXSR      TILFIL
      * </Reference>
     C                   EVAL      SDATA = TXT(023)
     C                   EXSR      TILFIL
      * </Consignment>
     C                   EVAL      SDATA = TXT(019)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCUSCLR      BEGSR
      ***********************************************
      * <CustomsClearence
     C                   EVAL      SDATA = %trim(TXT(028))
     C                             + ' status="'
     C                             + %trim(XP101)
     C                             + '">'
     C                   EXSR      TILFIL
      *
     C                   IF        ((XP101 = 'Import') AND (XP119 = 'Release'))
      * <EntryNumber>
     C                   EVAL      SDATA = %trim(TXT(030))
     C                             + %trim(XP103)
     C                             + %trim(TXT(031))
     C                   EXSR      TILFIL
      * <EntryPositionNumber>
     C                   EVAL      SDATA = %trim(TXT(032))
     C                             + %trim(XP104)
     C                             + %trim(TXT(033))
     C                   EXSR      TILFIL
      *
     C                   END
      *
      * <CustomsResponse>
     C                   EVAL      SDATA = %trim(TXT(034))
     C                             + ' responseType="'
     C                             + %trim(XP119)
     C                             + '" responseId="'
     C                             + %trim(XP105)
     C                             + '">'
     C                   EXSR      TILFIL
      * <ResponseDates>
     C                   EVAL      SDATA = %trim(TXT(036))
     C                             + ' subClass="DocumentDate">'
     C                   EXSR      TILFIL
      * <Date>
     C                   EVAL      SDATA = %trim(TXT(038))
     C                             + XP1061 + '-'
     C                             + XP1062 + '-'
     C                             + XP1063
     C                             + %trim(TXT(039))
     C                   EXSR      TILFIL
      * </ResponseDates>
     C                   EVAL      SDATA = %trim(TXT(037))
     C                   EXSR      TILFIL
      *
     C                   IF        XP119 = 'Release'
      * <ResponseDates>
     C                   EVAL      SDATA = %trim(TXT(036))
     C                             + ' subClass="ClearanceDate">'
     C                   EXSR      TILFIL
      * <Date>
     C                   EVAL      SDATA = %trim(TXT(038))
     C                             + XP1071 + '-'
     C                             + XP1072 + '-'
     C                             + XP1073
     C                             + %trim(TXT(039))
     C                   EXSR      TILFIL
      * </ResponseDates>
     C                   EVAL      SDATA = %trim(TXT(037))
     C                   EXSR      TILFIL
      *
     C                   END
      *
      * <DocumentCode>
     C                   EVAL      SDATA = %trim(TXT(040))
     C                             + %trim(XP108)
     C                             + %trim(TXT(041))
     C                   EXSR      TILFIL
      * <Category>
     C                   EVAL      SDATA = %trim(TXT(042))
     C                             + %trim(XP109)
     C                             + %trim(TXT(043))
     C                   EXSR      TILFIL
      *
     C                   IF        XP119 = 'Release'
      * <Taxes>
     C                   EVAL      SDATA = %trim(TXT(044))
     C                   EXSR      TILFIL
      * <FunctionCode>
     C                   EVAL      SDATA = %trim(TXT(046))
     C                             + %trim(XP110)
     C                             + %trim(TXT(047))
     C                   EXSR      TILFIL
      * <AmountQualifier>
     C                   EVAL      SDATA = %trim(TXT(048))
     C                             + %trim(XP111)
     C                             + %trim(TXT(049))
     C                   EXSR      TILFIL
      * <Amount>
     C                   EVAL      SDATA = %trim(TXT(050))
     C                             + %trim(XP112)
     C                             + %trim(TXT(051))
     C                   EXSR      TILFIL
      * </Taxes>
     C                   EVAL      SDATA = %trim(TXT(045))
     C                   EXSR      TILFIL
      * <ResponseRereference>
     C                   EVAL      SDATA = %trim(TXT(052))
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   EVAL      SDATA = %trim(TXT(024))
     C                             + %trim(XP113)
     C                             + %trim(TXT(025))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   EVAL      SDATA = %trim(TXT(026))
     C                             + %trim(XP114)
     C                             + %trim(TXT(027))
     C                   EXSR      TILFIL
      * </ResponseRereference>
     C                   EVAL      SDATA = %trim(TXT(053))
     C                   EXSR      TILFIL
      *
     C                   ELSE
      *
      * Send feil melding  <ApplicationError> </ApplicationError>
      *
     C     1             DO        10            XN3               3 0
     C                   IF        XP201T(XN3) <> *BLANKS
     C                   EXSR      SRERR
     C                   END
     C                   ENDDO
      *
     C     FMN           SETLL     TVINF
     C     FMN           READE     TVINF                                  33
     C                   DOW       *IN33 = *OFF
     C                   IF        ((F0077 = *BLANKS) AND (F0078A <> *BLANKS))
      * <ResponseDescription>
     C                   EVAL      SDATA = TXT(072)
     C                   EXSR      TILFIL
      * <Usage>
     C                   EVAL      SDATA = %trim(TXT(074))
     C                             + 'AAP' + %trim(TXT(075))
     C                   EXSR      TILFIL
      * <Text>
     C                   EVAL      SDATA = %trim(TXT(076))
     C                             + %trim(F0078A) + %trim(TXT(077))
     C                   EXSR      TILFIL
     C                   IF        F0078B <> *BLANKS
     C                   EVAL      SDATA = %trim(TXT(076))
     C                             + %trim(F0078B) + %trim(TXT(077))
     C                   EXSR      TILFIL
     C                   END
     C                   IF        F0078C <> *BLANKS
     C                   EVAL      SDATA = %trim(TXT(076))
     C                             + %trim(F0078C) + %trim(TXT(077))
     C                   EXSR      TILFIL
     C                   END
     C                   IF        F0078D <> *BLANKS
     C                   EVAL      SDATA = %trim(TXT(076))
     C                             + %trim(F0078D) + %trim(TXT(077))
     C                   EXSR      TILFIL
     C                   END
     C                   IF        F0078E <> *BLANKS
     C                   EVAL      SDATA = %trim(TXT(076))
     C                             + %trim(F0078E) + %trim(TXT(077))
     C                   EXSR      TILFIL
     C                   END
      * </ResponseDescription>
     C                   EVAL      SDATA = TXT(073)
     C                   EXSR      TILFIL
     C                   END
     C     FMN           READE     TVINF                                  33
     C                   ENDDO
      *
     C                   END
      *
      * </CustomsResponse>
     C                   EVAL      SDATA = %trim(TXT(035))
     C                   EXSR      TILFIL
      * </CustomsClearence>
     C                   EVAL      SDATA = TXT(029)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRERR         BEGSR
      ***********************************************
      * <ApplicationError>
     C                   EVAL      SDATA = TXT(062)
     C                   EXSR      TILFIL
      * <ErrorPointDetails>
     C                   EVAL      SDATA = TXT(064)
     C                   EXSR      TILFIL
      * <PointCode>
     C                   EVAL      SDATA = %TRIM(TXT(066))
     C                             + %TRIM(XP201T(XN3))
     C                             + %TRIM(TXT(067))
     C                   EXSR      TILFIL
      * <ItemNumber>
     C                   EVAL      SDATA = %TRIM(TXT(068))
     C                             + %TRIM(XP202T(XN3))
     C                             + %TRIM(TXT(069))
     C                   EXSR      TILFIL
      * </ErrorPointDetails>
     C                   EVAL      SDATA = TXT(065)
     C                   EXSR      TILFIL
      *
     C     XN3           MULT      5             XN3A              3 0
     C                   SUB       5             XN3A
     C                   DO        5
     C                   ADD       1             XN3A
     C                   IF        XP203T(XN3A) <> *BLANKS
      * <ErrorInformation
     C                   EVAL      SDATA = %TRIM(TXT(070))
     C                             + ' codeListIdentifier="111"'
     C                             + ' codeListAgencyIdentifier="NO1">'
     C                             + %TRIM(XP203T(XN3A))
     C                   EXSR      TILFIL
      * </ErrorInformation>
     C                   EVAL      SDATA = %TRIM(TXT(071))
     C                   EXSR      TILFIL
     C                   END
     C                   ENDDO
      * </ApplicationError>
     C                   EVAL      SDATA = TXT(063)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONGIT      BEGSR
      ***********************************************
      * <ConsignmentGoodsItemSet>
     C                   EVAL      SDATA = TXT(054)
     C                   EXSR      TILFIL
      * <GoodsItem>
     C                   EVAL      SDATA = %TRIM(TXT(056))
     C                             + %TRIM(TXT(057))
     C                   EXSR      TILFIL
      * </ConsignmentGoodsItemSet>
     C                   EVAL      SDATA = TXT(055)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRPARTY       BEGSR
      ***********************************************
     C                   IF        XP119 = 'Release'
      * <Party
     C                   EVAL      SDATA = %trim(TXT(058))
     C                             + ' subClass="'
     C                             + %trim(XP101)
     C                             + 'er" partyId="'
     C                             + %trim(XP115)
     C                             + '" idType="NO1">'
     C                   EXSR      TILFIL
      * <Name>
     C                   EVAL      SDATA = %trim(TXT(060))
     C                             + %trim(XP116)
     C                             + %trim(TXT(061))
     C                   EXSR      TILFIL
      * </Party>
     C                   EVAL      SDATA = %trim(TXT(059))
     C                   EXSR      TILFIL
      *
     C                   ELSE
      * <Party>
     C                   EVAL      SDATA = %trim(TXT(058))
     C                             + ' subClass="'
     C                             + %trim(XP101)
     C                             + 'er">'
     C                             + %trim(TXT(059))
     C                   EXSR      TILFIL
      *
     C                   END
      * <Party>
     C                   IF        XP117 = *BLANKS
     C                   OPEN      FIRM
     C                   OPEN      KODTA
     C                   OPEN      CUNDL01
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      'A'           KOAUNI
     C                   MOVE      XP1021        KOAAVD
     C     KODTAK        CHAIN     KODTA                              33
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   MOVEL     SYRG          XP117
     C                   CLOSE     FIRM
     C                   CLOSE     KODTA
     C                   CLOSE     CUNDL01
     C                   END
     C                   EVAL      SDATA = %trim(TXT(058))
     C                             + ' subClass="CustomsBroker"'
     C                             + ' partyId="'
     C                             + %trim(XP117)
     C                             + '" idType="NO1">'
     C                             + %trim(TXT(059))
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSLUTT       BEGSR
      ***********************************************
      *
     C                   EVAL      SDATA = TXT(005)
     C                   EXSR      TILFIL
      *
      * Oppdater EDIS
     C                   WRITE     EDISR
      * Oppdater EDIM
     C                   EXSR      OPDEDIM
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
     C                   CLOSE     EDIC
     C                   CLOSE     EDIS
     C                   CLOSE     EDIM
     C                   CLOSE     EDISE
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSEND        BEGSR
      ***********************************************
     C                   CLOSE     XMLTILP
     C                   EVAL      XFILNVN = 'XXXX'
     C                   CAT       XMBRC:0       XFILNVN          16
     C                   CALL      'SYXML08S'
     C                   PARM                    XFILNVN
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDEDIM       BEGSR
      ***********************************************
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EVAL      M0004 = S0004
     C                   EVAL      M0010 = S0010
     C                   MOVEL     CMN           M0062
     C                   EVAL      M0065 = 'XMLRES'
     C                   EVAL      M0068 = FSSOK
     C                   EVAL      M1001 = XP108
     C                   EVAL      M1004 = XP102
     C                   EVAL      M1225 = XP109
     C                   EVAL      MSN = SSN
     C                   EVAL      MMN = CMN
     C                   EVAL      MSR = 'S'
     C                   EVAL      MST = 'C'
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      XP1021        MAVD
     C                   MOVE      XP1022        MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     TILFIL        BEGSR
      ***********************************************
     C                   WRITE     EDISER
      *
     C                   MOVE      SDATA         XTPDTA
     C                   WRITE     XMLTILPR
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVST         BEGSR
      ***********************************************
     C     XN53          SUB       4             XN54              3 0
     C     XN53          SUB       5             XN55              3 0
     C     XN53          SUB       6             XN56              3 0
     C                   Z-ADD     0             XN52              3 0
     C                   MOVE      *BLANKS       XTXT802
     C     1             DO        80            XN51              3 0
     C                   IF        (((((XT801(XN51) = '&') OR
     C                                 (XT801(XN51) = '"')) OR
     C                                 (XT801(XN51) = XAPS)) OR
     C                                 (XT801(XN51) = '<')) OR
     C                                 (XT801(XN51) = '>'))
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   SELECT
     C                   WHEN      XT801(XN51) = '&'
     C                   IF        XN51 < XN55
     C                   MOVEA     '&amp;'       XT802(XN52)
     C                   ADD       4             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '"'
     C                   IF        XN51 < 75
     C                   MOVEA     '&quot;'      XT802(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = XAPS
     C                   IF        XN51 < XN56
     C                   MOVEA     '&apos;'      XT802(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '>'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&gt;'        XT802(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '<'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&lt;'        XT802(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   ENDSL
     C                   END
     C                   ELSE
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   MOVE      XT801(XN51)   XT802(XN52)
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     FRISOKK       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    KOAAVD
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KOAKNR
      *
     C     *ENTRY        PLIST
     C     XPARM1        PARM                    UPARM1         1024
     C     XPARM2        PARM                    UPARM2         1024
      *
     C                   MOVE      XP1021        FSAVD
     C                   MOVE      XP1022        FSOPD
     C                   IF        XP101 = 'Import'
     C                   MOVE      '*CR'         FSKODE
     C                   ELSE
     C                   MOVE      '*CE'         FSKODE
     C                   END
     C                   MOVEL     XP120         FMN
     C                   BITON     '123457'      XAPS              1
     C                   BITOFF    '06'          XAPS
      *
     C     FRISOKK       CHAIN     FRISOK                             33
      *
     C                   ENDSR
      *
** TXT
<?xml version="1.0" encoding="ISO-8859-1"?>                                     001
<Transport messageFunction="CustomsResponse" domain="" profile=""               002
version="1.1" collaboration="Instruction" messageId="                           003
xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">                          004
</Transport>                                                                    005
<MessageDetails>                                                                006
</MessageDetails>                                                               007
<MessageSender                                                                  008
</MessageSender>                                                                009
<MessageRecipient                                                               010
</MessageRecipient>                                                             011
<MessageDate                                                                    012
</MessageDate>                                                                  013
<ConsignmentStructure                                                           014
</ConsignmentStructure>                                                         015
<ConsignmentSet>                                                                016
</ConsignmentSet>                                                               017
<Consignment                                                                    018
</Consignment>                                                                  019
<NumberOfPackages>                                                              020
</NumberOfPackages>                                                             021
<Reference>                                                                     022
</Reference>                                                                    023
<ReferenceNo>                                                                   024
</ReferenceNo>                                                                  025
<ReferenceType>                                                                 026
</ReferenceType>                                                                027
<CustomsClearance                                                               028
</CustomsClearance>                                                             029
<EntryNumber>                                                                   030
</EntryNumber>                                                                  031
<EntryPositionNumber>                                                           032
</EntryPositionNumber>                                                          033
<CustomsResponse                                                                034
</CustomsResponse>                                                              035
<ResponseDates                                                                  036
</ResponseDates>                                                                037
<Date>                                                                          038
</Date>                                                                         039
<DocumentCode>                                                                  040
</DocumentCode>                                                                 041
<Category>                                                                      042
</Category>                                                                     043
<Taxes>                                                                         044
</Taxes>                                                                        045
<FunctionCode>                                                                  046
</FunctionCode>                                                                 047
<AmountQualifier>                                                               048
</AmountQualifier>                                                              049
<Amount>                                                                        050
</Amount>                                                                       051
<ResponseRereference>                                                           052
</ResponseRereference>                                                          053
<ConsignmentGoodsItemSet>                                                       054
</ConsignmentGoodsItemSet>                                                      055
<GoodsItem>                                                                     056
</GoodsItem>                                                                    057
<Party                                                                          058
</Party>                                                                        059
<Name>                                                                          060
</Name>                                                                         061
<ApplicationError>                                                              062
</ApplicationError>                                                             063
<ErrorPointDetails>                                                             064
</ErrorPointDetails>                                                            065
<PointCode>                                                                     066
</PointCode>                                                                    067
<ItemNumber>                                                                    068
</ItemNumber>                                                                   069
<ErrorInformation                                                               070
</ErrorInformation>                                                             071
<ResponseDescription>                                                           072
</ResponseDescription>                                                          073
<Usage>                                                                         074
</Usage>                                                                        075
<Text>                                                                          076
</Text>                                                                         077
