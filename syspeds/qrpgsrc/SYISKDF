      *
      *************************************************************
      ***       VEDLIKEHOLD AV KODEREGISTER FOR                 ***
      ***             PARTSFORKLARING                           ***
      ***                                                       ***
      *** PROGRAMMERT AV TORE BRÅSTAD, SYSTEMA AS, JANUAR 1994  ***
      *************************************************************
      *
     FISOKDF    UF A E           K DISK
     FKUFAST    IF   E           K DISK
     FSYISKDFD  CF   E             WORKSTN
     F                                     SFILE(H50:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
      ***************
      * TØM SUBFILE
      ***************
     C     START         TAG
     C     FVKODE        SETLL     ISKDFR
     C                   MOVE      *BLANKS       FVKODE
     C                   MOVE      '1'           *IN93
     C                   WRITE     K60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *************************
      * Fyll subfile
      *************************
      *
     C     START1        TAG
     C                   DO        13
     C                   READ      ISKDFR                                 94
     C     *IN94         IFEQ      '0'
     C                   MOVEL     FKODE         SVKODE
     C                   MOVEL     FTEXT         SVTEXT
     C                   MOVEL     FFARGE        SVFARG
     C                   ADD       1             ZRECNO
     C                   WRITE     H50
     C                   END                                                    <*IN94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
      ****************************
      * Vis subfile
      ****************************
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   MOVE      '1'           *IN31
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   WRITE     B20
     C                   EXFMT     B40
     C                   READ      B20                                    66
     C                   ELSE
     C                   MOVE      '0'           *IN31
     C                   WRITE     K60
     C                   EXFMT     B40
     C                   READ      K60                                    66
     C                   END
      *
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      *
     C                   SETOFF                                       999830
      *** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F5=Forny
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN05
      *** F10=Ledetekst
     C     *IN10         IFEQ      '1'
     C                   EVAL      UTYP = 'AVLETEKST'
     C                   EVAL      UKOD = 'ISPAR'
     C                   CALL      'SYFAKDV'
     C                   PARM                    UTYP             10
     C                   PARM                    UKOD              5
     C                   GOTO      START3
     C                   END
      *** F6=Ny linje
     C     *IN06         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVEL     'TBR0106'     WMODNR
     C                   EXSR      CLRSCR
     C                   MOVE      '0'           *IN40
     C                   GOTO      START5
     C                   END                                                    <*IN06
      *** Roll
     C     *IN25         IFEQ      '1'
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C  N94              GOTO      START1
     C                   END                                                    <*IN25
      *
     C     FVKODE        CABNE     *BLANKS       START
      *** Hent valg fra skjerm
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
      *** F3=Exit
     C     *IN03         CABEQ     '1'           ENDPGM
     C                   END                                                    <ZRECNO
     C                   GOTO      START3
      *** Ny post
     C     START5        TAG
     C                   EXFMT     B10
     C                   MOVE      '0'           *IN30
      * F4 SØK FARGE
     C                   IF        *IN04
     C                   MOVE      'J'           UUPDAT
     C                   MOVEL     'ESPEDFARGE'  XKFTYP           10
     C                   MOVE      *BLANKS       XKFKOD            5
     C                   CALL      'SYKUKD'
     C                   PARM                    XKFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    XKFKOD
     c     XKFKOD        IFNE      '     '
     C     KUFASK        CHAIN     KUFAST                             33
     C  N33              MOVEL     KFTXT         FVFARG
     C                   END
     C                   GOTO      START5
     C                   END
      *** F3=Exit
      *** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   EXSR      CLRSCR
     C                   ELSE
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           START5
     C                   END                                                    <*IN12
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
      *
     C     ENDPGM        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
     C                   EXSR      TSTSCR
     C     *IN30         IFEQ      '1'
     C                   GOTO      ENDTST
     C                   END                                                    <*IN30
     C                   MOVEL     FVKODE        FKODE
     C                   MOVEL     FVTEXT        FTEXT
     C                   MOVEL     FVFARG        FFARGE
     C     *IN40         IFEQ      '1'
     C                   UPDATE    ISKDFR                               38
     C                   ELSE
     C                   WRITE     ISKDFR
     C                   END                                                    <*IN40
      *
     C                   MOVEL     FVKODE        SVKODE
     C                   MOVEL     FVTEXT        SVTEXT
     C                   MOVEL     FVFARG        SVFARG
     C     *IN40         IFEQ      '1'
     C                   UPDATE    H50
     C                   ELSE
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     H50
     C                   END                                                    <*IN40
     C                   EXSR      CLRSCR
      *
     C     ENDTST        ENDSR
      *
      ***********************************************************
     C     TSTSCR        BEGSR
      ***********************************************************
      *
     C     FVKODE        IFEQ      *BLANKS
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      '1'           *IN30
     C                   END                                                    <NAKUND
     C                   ENDSR
      *
      ***********************************************
     C     GETOPT        BEGSR
      ***********************************************
     C                   Z-ADD     1             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     H50                                    98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   END                                                    <*IN99
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
      *** 2=Endre
     C     WSNR          IFEQ      '2'
     C                   IF        SVTEXT <> '*SLETTET'
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'TBR0107'     WMODNR
     C                   MOVE      '1'           *IN40
     C                   EXSR      CHGST
     C                   EXSR      POSLIN
      *** F12=Avbryt
     C     *IN03         CABEQ     '1'           ENDGET
     C     *IN12         CABEQ     '1'           ENDGET
     C                   MOVE      '0'           *IN40
     C                   END
     C                   END                                                    <WSNR=2
      *** 4=Slette
     C     WSNR          IFEQ      '4'
     C                   IF        SVTEXT <> '*SLETTET'
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'TBR0108'     WMODNR
     C                   EXSR      DLTST
     C                   EXSR      POSLIN
      *** F12=Avbryt
     C     *IN03         CABEQ     '1'           ENDGET
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR=4
      *
     C                   ENDDO
      *
     C     ENDGET        TAG
     C                   ADD       7             LINNBR
     C                   Z-ADD     3             POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     CHGST         BEGSR
      ***********************************************
      *
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     28            POSNBR
      *
     C     SVKODE        CHAIN     ISKDFR                             77
     C                   MOVEL     FKODE         FVKODE
     C                   MOVEL     FTEXT         FVTEXT
     C                   MOVEL     FFARGE        FVFARG
      *
     C     STCHG         TAG
     C                   EXFMT     B10
     C                   MOVE      '0'           *IN30
      * F4 SØK FARGE
     C                   IF        *IN04
     C                   MOVE      'J'           UUPDAT
     C                   MOVEL     'ESPEDFARGE'  XKFTYP           10
     C                   MOVE      *BLANKS       XKFKOD            5
     C                   CALL      'SYKUKD'
     C                   PARM                    XKFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    XKFKOD
     c     XKFKOD        IFNE      '     '
     C     KUFASK        CHAIN     KUFAST                             33
     C  N33              MOVEL     KFTXT         FVFARG
     C                   END
     C                   GOTO      STCHG
     C                   END
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   UPDATE    H50
     C                   GOTO      CHGEND
     C                   END                                                    <*IN12
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      STCHG
     C                   END                                                    <*IN30
      *
     C     CHGEND        TAG
     C                   ENDSR
      *
      ***********************************************
     C     DLTST         BEGSR
      ***********************************************
      *
     C                   MOVE      '1'           *IN99
     C                   MOVEA     '11'          *IN(88)
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVEL     SVKODE        FVKODE
     C                   MOVEL     SVTEXT        FVTEXT
     C                   MOVEL     SVFARG        FVFARG
     C                   EXFMT     B30
      *
     C     *IN12         IFEQ      '0'
     C     *IN03         ANDEQ     '0'
     C     SVKODE        CHAIN     ISKDFR                             77
     C  N77              DELETE    ISKDFR
     C                   MOVE      *BLANKS       SVKODE
     C                   EVAL      SVTEXT = '*SLETTET'
     C                   END                                                    <*IN77
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    H50
      *
     C     DLTEND        ENDSR
      *
      ***********************************************
     C     CLRSCR        BEGSR
      ***********************************************
      *
     C                   MOVE      '0'           *IN40
     C                   CLEAR                   FVKODE
     C                   CLEAR                   FVTEXT
     C                   CLEAR                   FVFARG
      *
     C                   ENDSR
      *
      ***********************************************
     C     POSLIN        BEGSR
      ***********************************************
      *
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     KUFASK        KLIST
     C                   KFLD                    XKFTYP
     C                   KFLD                    XKFKOD
      *
     C     *LIKE         DEFINE    KFTYP         XKFTYP
     C     *LIKE         DEFINE    KFKOD         XKFKOD
     C     *LIKE         DEFINE    LINNBR        LINNBX
     C     *LIKE         DEFINE    POSNBR        POSNBX
      *
     C                   CLEAR                   SVKODE
     C                   MOVE      '1'           *IN50
      *
     C                   ENDSR
      *
