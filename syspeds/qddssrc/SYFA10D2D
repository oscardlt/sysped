     A*%%TS  SD  20150817  161454  JOVO        REL-V7R1M0  5770-WDS
     A* BRINGTMS-PROSJEKT
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLAVD0' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 20 90 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A*%%TS  SD  20150817  161454  JOVO        REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            ADUNNR    R        O  5  5REFFLD(ADRUNNRR/ADUNNR ADRUNNRF)
     A                                      COLOR(WHT)
     A            ADEMBG    R        O  5 10REFFLD(ADRUNNRR/ADEMBG ADRUNNRF)
     A                                      COLOR(WHT)
     A            ADKLAS    R        O  5 16REFFLD(ADRUNNRR/ADKLAS ADRUNNRF)
     A            ADSEDD    R        O  5 22REFFLD(ADRUNNRR/ADSEDD ADRUNNRF)
     A            ADTRES    R        O  5 34REFFLD(ADRUNNRR/ADTRES ADRUNNRF)
     A            ADFAKT    R        O  5 45REFFLD(ADRUNNRR/ADFAKT ADRUNNRF)
     A                                      EDTCDE(4)
     A            UNBESKA       40A  O  5 50COLOR(WHT)
     A            UNBESKB       85A  O  6  5COLOR(WHT)
     A            ADINDX    R        O  5 14REFFLD(ADRUNNRR/ADINDX *LIBL/ADRUNN-
     A                                      RF)
     A                                      COLOR(WHT)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20150215  201724  JOVO        REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYFA10D2'
     A                                      COLOR(BLU)
     A                                  1 15'Søk/Plukk UnNummer '
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 38SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2'Op UnNr Emb.g FareK Fareseddel  Tu-
     A                                      nellrest Fakt Beskrivelse....  '
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KOD          4A  I  4  5DSPATR(UL)
     A            W2SØK         30A  I  4 50DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(20)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYFA10D2'
     A                                      COLOR(BLU)
     A                                  1 15'Søk/Plukk UnNummer '
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2'Op UnNr Emb.g FareK Fareseddel  Tu-
     A                                      nellrest Fakt Beskrivelse....  '
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KOD          4A  I  4  5DSPATR(UL)
     A            W2SØK         30A  I  4 50DSPATR(UL)
     A N30                             20  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O 20  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
