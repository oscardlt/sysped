********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXX * 01 YBC PTF7 BEVARE REFERANSE
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 SLETT OPSJON
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 60 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
S02  A*           WSNR           1A  I  6  2VALUES(' ' '1')
N02  A            WSNR           1A  I  6  2VALUES(' ' '1' '4')
N01  A            ZFUNIK         9  0H
N01  A            ZFREFF        17A  H
     A            ZFTXT         17A  O  6  5
     A            ZFDT           8Y 0H
     A            WSDT           6Y 0O  6 23EDTCDE(Y)
     A            ZFBL28        12Y 2O  6 32EDTCDE(4)
     A            ZFVK28         3A  O  6 46
     A            ZFKR28         6Y 3O  6 50EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD031'
     A                                      COLOR(BLU)
     A                                  1 10'Plukk fakturanr'
     A                                      COLOR(WHT)
S02  A*                                 3  2'Velg opsjon;   1=Plukk'
N02  A                                  3  2'Velg opsjon   1=Plukk   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(55 DIV1830 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1')
     A            W2TXT         17A  I  5  5
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N30                             12  2'F5=Frisk   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD031'
     A                                      COLOR(BLU)
     A                                  1 10'Plukk fakturanr'
     A                                      COLOR(WHT)
S02  A*                                 3  2'Velg opsjon;   1=Plukk'
N02  A                                  3  2'Velg opsjon   1=Plukk   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(55 DIV1830 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1')
     A            W2TXT         17A  I  5  5
     A N30                             12  2'F5=Frisk   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
