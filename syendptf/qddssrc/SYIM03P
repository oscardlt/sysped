********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 02 YBC PTF12 OMEBEREGNING INFORMASJON
12248 * 03 YBC PTF12 VEKT, KOLLI, MENGDE ER UTVIDET.
********************************************************************
     A                                      REF(HEADF)
      *________
      * Forside
      *""""""""
     A          R SYIM03PA                  SKIPB(08)
     A                                    41'TOLLAGERREGNSKAP'
     A                                    59DATE EDTCDE(Y)
     A                                    69TIME EDTWRD('0 :  :  ')
     A                                    78'SYIM03R'
      *
     A                                    41'FRA AVDELING  . . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(2)
     A            ZAVDF          4S 0     91EDTCDE(3)
      *
     A                                    41'TIL AVDELING  . . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(1)
     A            ZAVDT          4S 0     91EDTCDE(3)
      *
     A                                    41'FRA GODSNUMMER  . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(2)
     A            ZGNF          15        91
      *
     A                                    41'TIL GODSNUMMER  . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(1)
     A            ZGNT          15        91
      *
     A                                    41'KUN LØPENR.. (3. siste siffer i Gn+
     A                                      r =). :'
     A                                      SPACEB(1)
     A            ZLNRF          1        91
     A                                    93'-'
     A            ZLNRT          1        95
      *
     A                                    41'FRA OG MED DATO . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(2)
     A            ZDTF           6S 0     91
      *
     A                                    41'TIL OG MED DATO . . . . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(1)
     A            ZDTT           6S 0     91
      *
     A                                    41'UTELAT OPPDRAGSTYPER  . . . . . . +
     A                                      . . . :'
     A                                      SPACEB(2)
     A            ZOT01          2        83
     A            ZOT02          2        87
     A            ZOT03          2        91
     A            ZOT04          2        95
     A            ZOT05          2        99
      *
     A            ZOT06          2        83
     A                                      SPACEB(1)
     A            ZOT07          2        87
     A            ZOT08          2        91
     A            ZOT09          2        95
     A            ZOT10          2        99
      *
     A                                    41'KUN OPPDRAG UTEN LØPENR/UTEN NYTT +
     A                                      GODSNR:'
     A                                      SPACEB(2)
     A            ZUNG           1        91
     A                                    10'Denne tollager regnskapslista tar +
     A                                      for seg alle import-oppdrag i ønske+
     A                                      t godsnummerserie.                 '
     A                                      SPACEB(5)
     A                                    10'Det forventes at HELE oppdraget ( +
     A                                      posisjonen) innfortolles/forpasses +
     A                                      samlet (med fullt antall/vekt)     '
     A                                      SPACEB(1)
     A                                    10'Dersom et oppdrag tollmessig IKKE +
     A                                      HÅNDTERES SLIK, må sideliggende (ma+
     A                                      nuelle) system dokumentere dette!! '
     A                                      SPACEB(1)
     A                                    10'Poster med uthevet trykk (og tekst+
     A                                      ten *EXPORT*) er reeksportert gods.'
     A                                      SPACEB(2)
     A                                    10'I noen tilfeller vil disse stå i d+
     A                                      irekte forhold til det importerte o+
     A                                      oppdrag (lenger opp i lista)       '
     A                                      SPACEB(1)
     A                                    10'I andre tilfeller kan det være del+
     A                                      er av et oppdrag som er reeksporter+
     A                                      t.                                 '
     A                                      SPACEB(1)
     A                                    10'Opplistingen av reeksport må betra+
     A                                      ktes som et HJELPEMIDDEL til nevnte+
     A                                       avstemming i sideliggende system. '
     A                                      SPACEB(1)
     A                                    10'Når et godsnr omfatter mer enn en +
     A                                      posisjon(importoppdrag) skrives en +
     A                                      sumpost med ant posisj. kolli/vekt '
     A                                      SPACEB(3)
     A                                    10'(Denne sumposten inneholder ikke/ +
     A                                      tar ikke hensyn til ant/vekt på de +
     A                                      evt. listede reeksportene)         '
     A                                      SPACEB(1)
      *________
      * Heading
      *""""""""
     A          R SYIM03PB                  SKIPB(4)
     A                                      HIGHLIGHT
     A                                     2'TOLLAGERREGNSKAP'
     A                                    84DATE EDTCDE(Y)
     A                                    93TIME EDTWRD('0 :  :  ')
     A                                   103'SYIM03R Side:'
     A                                    +1PAGNBR EDTCDE(Z)
      *
     A                                    89'-TOLLDEKLARERT/FORPASS/REEKS-'
     A                                      SPACEB(2)
      *
     A                                     3'AVD OPPDNR. SIG OT GODSNUMMER'
     A                                      SPACEB(1)
     A                                    38'POS POS2  ANTALL ENHET/VARESLAG'
     A                                    86'VEKT  EKS.E.  LØPENUMMER   DATO'
     A                                     2'TURNUMMER.AVSENDER.............ADR+
     A                                      .1................MOTTAKER.........+
     A                                      ....ADR.1................ADR.3.....+
     A                                      ..............'
     A                                      SPACEB(1)
      *
      *_____________
      * Detaljlinjer
      *"""""""""""""
     A          R SYIM03PC                  SPACEB(2)
     A            HEAVD     R              2EDTCDE(Z)
     A            HEOPD     R              7EDTCDE(Z)
     A            HESG      R             15
     A            HEOT      R             19
     A            HEGN      R             22HIGHLIGHT
     A            HEPOS1    R             38
     A            HEPOS2    R             42EDTCDE(Z)
     A            HENT      R             47EDTCDE(Z)
     A            HEVS1     R             55
     A            HEVKT     R             81EDTCDE(Z)
     A            HETLE     R             91HIGHLIGHT
     A            HETLL     R             98HIGHLIGHT
     A            SIDTG         10       109
     A  41                                12'OMBEREGNING'
     A                                      SPACEB(1)
     A  41        SINTK          7  0     47EDTCDE(Z)
     A  41        SIVKB          9  0     81EDTCDE(Z)
     A  41        UTLE           6        91HIGHLIGHT
     A  41        UTLL          10        98HIGHLIGHT
     A  41        UDTG          10       109SPACEB(1)
     A            HEPRO     R              3EDTCDE(Z)
     A                                      SPACEB(1)
     A            ZZNAS         20        12
     A            ZZADS1        20        33
     A            ZZNAK         20        54
     A            ZZADK1        20        75
     A            ZZADK3        20        96
     A                                     1' '
     A                                      SPACEB(1)
     A                                     2'La:'
     A            ZZKDLS         2         5
     A            ZZLS           8         8
     A  66                                17'Nytt godsnr:'
     A                                      HIGHLIGHT
     A  66        HEGNN     R             29HIGHLIGHT
     A  66        TTDTG         10        45
     A  67                                48'Vareverdi:'
     A  67        VAVEKO         3        58
     A  67        TRVERV         3        61
     A  67        TRVERB        13S 2     64EDTCDE(L)
     A  68                                80'Ekstern. ref:'
     A  68        EXTREF        15        94
     A                                   111'STATUS:'
     A            SIST           1       119HIGHLIGHT
     A                                     2'Transpmåte:'
     A                                      SPACEB(1)
     A            SITRM          2S 0     14
     A                                    18'Landkode/ID'
     A            SILKT          2        30
     A            SITRID        17        33
     A                                    54'Ku/Fo:'
     A            HEKNK          8S 0     60
     A                                    68'/'
     A            SYRG          14        69
     A* Totalsum denne posisjon
     A          R SYIM03PD                  SPACEB(1)
     A                                      HIGHLIGHT
     A                                     4'*TOTAL DENNE POSISJON*'
     A            POSANT         7S 0     28EDTCDE(Z)
     A                                    36'Poster'
     A            POSKLL         7S 0     47EDTCDE(Z)
     A                                    55'Kolli '
     A            POSVKT         9S 0     81EDTCDE(Z)
     A                                    91'Kilo  '
     A* Totalsum dette godsnr
     A          R SYIM03PE                  SPACEB(1)
     A                                      HIGHLIGHT
     A                                     2'**TOTAL DETTE GODSNUMMER**'
     A            GODANT         7S 0     28EDTCDE(Z)
     A                                    36'Poster'
     A            GODKLL         7S 0     47EDTCDE(Z)
     A                                    55'Kolli '
     A            GODVKT         9S 0     81EDTCDE(Z)
     A                                    91'Kilo  '
     A* REEKSPORT PÅ DETTE GODSNR??? - HELE RECORDEN HIGHLIGHTES
     A          R SYIM03RE                  SPACEB(1)
     A                                      HIGHLIGHT
     A            SEAVD          4S 0      1EDTCDE(Z)
     A            SETDN          7S 0      6EDTCDE(Z)
     A            SESG           3        14
XX   A*           HEGN      R             21
XX   A            SEGN          15        21
     A            SEPOS          8        37
S03  A*           SENTK          7S 0     47EDTCDE(Z)
N03  A            SENTK         10S 0     47EDTCDE(Z)
     A                                    63'*EXPORT*'
S03  A*           SEVKB          9S 0     81EDTCDE(Z)
N03  A            SEVKB         11S 0     79EDTCDE(Z)
     A            SETLE          6        91
     A            SETLL         10        98
     A            SEDTG         10       109
      *____________
      * Last Record
      *""""""""""""
     A          R SYIM03PØ                  SPACEB(2)
     A                                     2'----------------------------------+
     A                                      --------   S i s t e   s i d e n   +
     A                                      -----------------------------------+
     A                                      ----------------'
     A* URNOM ORDRE
     A          R SYIM03TI0                 SPACEB(1)
     A                                     2'*TOLLDEKLARASJON UTEN ORDRE*'
     A          R SYIM03TI                  SPACEB(1)
     A                                      HIGHLIGHT
     A            SIAVD          4S 0      1EDTCDE(Z)
     A            SITDN          7S 0      6EDTCDE(Z)
     A            SISG           3        14
     A            SIGN          15        21
     A            SIPOS          9        37
     A            SINTK          7S 0     47EDTCDE(Z)
     A                                    63'*UTEN O*'
     A            SIVKB          9S 0     81EDTCDE(Z)
     A            SITLE          6        91
     A            SITLL         10        98
     A            SIDTG         10       109
