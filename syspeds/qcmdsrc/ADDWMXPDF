CMD  PROMPT('ADD WATERMARK, PAGE, ADDRESS')

PARM FROMPDF    TYPE(*PNAME) LEN(60) MIN(1) +
                PROMPT('INPUT PDF')

PARM TOPDF      TYPE(*PNAME) LEN(60) MIN(1) +
                PROMPT('OUTPUT PDF')

PARM WTMK1NAME  TYPE(*PNAME) LEN(50) DFT(*NONE) +
                SPCVAL((*NONE 'NONE')) +
                PROMPT('WATERMARK 1 PATH AND NAME')

PARM WTMK1SCALE TYPE(*DEC) LEN(3) DFT(100) RANGE(10 100) +
                PROMPT('WATERMARK 1 SCALE PERCENT')

PARM WTMK2NAME  TYPE(*PNAME) LEN(50) DFT(*NONE) +
                SPCVAL((*NONE 'NONE')) +
                PROMPT('WATERMARK 2 PATH AND NAME')

PARM WTMK2SCALE TYPE(*DEC) LEN(3) DFT(100) RANGE(10 100) +
                PROMPT('WATERMARK 2 SCALE PERCENT')

PARM PAGENBR    TYPE(*CHAR) LEN(4) RSTD(*YES) +
                DFT(*NO) VALUES(*YES *NO) +
                PROMPT('PAGE NUMBER')

PARM PAGENBRPOS TYPE(COORDS) PMTCTL(PAGECTL) +
                PROMPT('PAGE NUMBER POSITION')

PARM ADDRESS    TYPE(*CHAR) LEN(4) RSTD(*YES) +
                DFT(*NO) VALUES(*YES *NO) +
                PROMPT('ADDRESS')

PARM ADDRESSLN  TYPE(LINES) PMTCTL(ADDRCTL) +
                PROMPT('ADDRESS LINES')

PARM ADDRESSPOS TYPE(COORDS) PMTCTL(ADDRCTL) +
                PROMPT('ADDRESS POSITION')

PARM DATELN     TYPE(*CHAR) LEN(50) DFT(*NONE) +
                SPCVAL((*NONE 'NONE')) +
                PROMPT('DATE LINE')

PARM DATEPOS    TYPE(COORDS) PMTCTL(DATECTL) +
                PROMPT('DATE LINE POSITION')

PARM DATEEDIT   TYPE(*CHAR) LEN(4) RSTD(*YES) +
                DFT(*NO) VALUES(*YES *NO) PMTCTL(DATECTL) +
                PROMPT('ALLOW DATE EDITING')

PARM JDEBUG     TYPE(*CHAR) LEN(4) RSTD(*YES) +
                DFT(*NO) VALUES(*YES *NO) +
                PROMPT('DEBUG JAVA')

PAGECTL: PMTCTL CTL(PAGENBR) COND((*NE *NO))
ADDRCTL: PMTCTL CTL(ADDRESS) COND((*NE *NO))
DATECTL: PMTCTL CTL(DATELN) COND((*NE 'NONE'))

COORDS:      ELEM       TYPE(*INT2) DFT(0) REL(*GE 0) EXPR(*YES) +
                          PROMPT('X POS')
             ELEM       TYPE(*INT2) DFT(0) REL(*GE 0) EXPR(*YES) +
                          PROMPT('Y POS')

LINES:       ELEM       TYPE(*CHAR) LEN(50) EXPR(*YES) +
                          PROMPT('LINE 1')
             ELEM       TYPE(*CHAR) LEN(50) EXPR(*YES) +
                          PROMPT('LINE 2')
             ELEM       TYPE(*CHAR) LEN(50) EXPR(*YES) +
                          PROMPT('LINE 3')
             ELEM       TYPE(*CHAR) LEN(50) EXPR(*YES) +
                          PROMPT('LINE 4')
             ELEM       TYPE(*CHAR) LEN(50) EXPR(*YES) +
                          PROMPT('LINE 5')
             ELEM       TYPE(*CHAR) LEN(50) EXPR(*YES) +
                          PROMPT('LINE 6')
