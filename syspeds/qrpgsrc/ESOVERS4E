      ****************************************************************************************
      * Generere MEDLINGelse til web-booking                                                 *
      ****************************************************************************************
     FFIRM      IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FBRIDF     IF   E           K DISK
     FBRPARF    IF   E           K DISK
     FEPOSRPL   IF   E           K DISK
     FARKSRC160 O    E             DISK    RENAME(ARKSRC160:ARCDAT)
     D XEMNE           S             50
     D EMNE            S             44
     D EMNE2           S             10
     D SENDER          S             17
     D CMD             S           5000
     D MELD            S           5000
     D MELD2           S            100
     D                 DS
     D  MAIL                   1    250    DIM(5)
     D  XINTAD1                1     50
     D  XINTAD2               51    100
     D  XINTAD3              101    150
     D  XINTAD4              151    200
     D  XINTAD5              201    250
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA2                1      2
     D  PAVRDA48               3     50
     D                 DS
     D  HEAVD                  1      4  0
     D  XXAVD                  1      4
     D                 DS
     D  HEOPD                  1      7  0
     D  XXOPD                  1      7
      *
     C                   EXSR      INIT
      *
     C     UUSR          CHAIN     BRIDF                              33
     C                   MOVE      UUSR          PABRID
     C                   MOVE      'HEUMA'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      FIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      *ZEROS        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             33
     C                   END
     C                   END
      *
     C                   IF        *IN33
     C                   MOVE      'J'           XGML              1
     C                   ELSE
     C                   EVAL      UAVAD = PAVRDA
     C                   END
      *
     C     CUNDL01K      CHAIN     CUNDL01                            33
      *
     C                   MOVE      'EPOST'       PAID
     C                   MOVE      *ZEROS        XN                3 0
     C                   MOVE      *BLANKS       MAIL
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             40
     C                   ADD       1             XN
     C                   IF        *IN40
     C                   EVAL      MAIL(XN) = 'BING@SYSTEMA.NO'
     C                   ELSE
     C                   MOVEL     PAVRDA        MAIL(XN)
     C                   END
      *
     C                   IF        XGML = 'J'
     C                   EXSR      SNDEM
     C                   ELSE
     C                   EXSR      LAGINH
     C                   MOVE      'J'           UOK
     C                   MOVE      XINTAD1       UINTAD1
     C                   MOVE      XINTAD2       UINTAD2
     C                   MOVE      XINTAD3       UINTAD3
     C                   MOVE      XINTAD4       UINTAD4
     C                   MOVE      XINTAD5       UINTAD5
     C                   END
      *
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ******************************************************************************
     C     SNDEM         BEGSR
      ******************************************************************************
     C                   Z-ADD     1024          CMDL             15 5
     C     'QTMHHTP1  '  CHAIN     EPOSRPL                            33
     C   33'ESPED     '  CHAIN     EPOSRPL                            33
     C     *IN33         IFEQ      *ON
     C                   EVAL      SENDER = '*CURRENT'
     C                   ELSE
     C                   EVAL      SENDER = DIRNAM + ' ' + DIRADR
     C                   END
      * Emne
     C                   SELECT
     C                   WHEN      UTYPE = 'SHIPPING'
     C                   Eval      EMNE = 'Shipping window is later '
     C                             + 'than deadline (' + %trim(UAVD)
     C                             + %trim(UAVD) + '/' + %trim(UOPD) + ')'
      *
     C     Mail(1)       cabeq     *blanks       UtIMail
     C     BIFIRM        CHAIN     FIRM
     C                   EVAL      MELD = 'Shipping window is later '
     C                             + 'than deadline ('
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ')'
     C                             + ':/NOrder No: ' + %trim(UAVD)
     C                             + '/' + %trim(UOPD)
     C                             + ':/PShipping deadline: ' + %trim(UDT1)
     C                             + ':/PShipping window:'
     C                             + ':/N     ' + %trim(UDT2)
     C                             +  ' - ' + %trim(UDT3)
     C                   WHEN      UTYPE = 'ARRIVAL'
     C                   Eval      EMNE = 'Arrival window is later '
     C                             + 'than deadline (' + %trim(UAVD)
     C                             + %trim(UAVD) + '/' + %trim(UOPD) + ')'
      *
     C     Mail(1)       cabeq     *blanks       UtIMail
     C     BIFIRM        CHAIN     FIRM
     C                   EVAL      MELD = 'Arrival window is later '
     C                             + 'than deadline ('
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ')'
     C                             + ':/NOrder No: ' + %trim(UAVD)
     C                             + '/' + %trim(UOPD)
     C                             + ':/PArrival deadline: ' + %trim(UDT1)
     C                             + ':/PArrival window:'
     C                             + ':/N     ' + %trim(UDT2)
     C                             +  ' - ' + %trim(UDT3)
     C                   ENDSL
      *
     C     SENDER        IFEQ      '*CURRENT '
     C                   EVAL      MELD2=  ':/P:/PThis message is created '
     C                             + 'automaticly by the overseas system.'
     C                             + ':/NPlease do not reply!'
     C                   cat       MELD2:0       MELD
     C                   END
      *
      * param for tointnet og longmsg må være i apostrofer ( hex 7D )
      * (strenger i single-parametere som inneholder blank el. spesialtegn)
     C                   eval      cmd = 'SNDDST TYPE(*LMSG) TOINTNET('
     C                             +'('+ X'7D' + %trim(Mail(1)) + X'7D' +') '
     C                             +'('+ X'7D' + %trim(Mail(2)) + X'7D' +') '
     C                             +'('+ X'7D' + %trim(Mail(3)) + X'7D' +') '
     C                             +'('+ X'7D' + %trim(Mail(4)) + X'7D' +') '
     C                             +'('+ X'7D' + %trim(Mail(5)) + X'7D' +')) +
     C                             DSTD('+ X'7D' + %trim(EMNE) + X'7D' + ') +
     C                             LONGMSG('+ X'7D' + %trim(MELD) + X'7D' +') +
     C                             USRID('+  %trim(SENDER) +') SNDFMT(*NOCHG)'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    CMD
     C                   PARM                    CMDL
      *
     C     UtImail       TAG
     C                   MOVE      'N'           UOK
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     LAGINH        BEGSR
      ******************************************************************************
      *
     C                   MOVE      *ZEROS        SRCSEQ
      *
     C                   SELECT
     C                   WHEN      UTYPE = 'SHIPPING'
     C                   Eval      UEMNE = 'Shipping window '
     C                                    + %trim(UAVD) + '/' + %trim(UOPD)
     C                   WHEN      UTYPE = 'ARRIVAL'
     C                   Eval      UEMNE = 'Arrival window '
     C                                    + %trim(UAVD) + '/' + %trim(UOPD)
     C                   ENDSL
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   SELECT
     C                   WHEN      UTYPE = 'SHIPPING'
     C                   EVAL      SRCDTA = 'Shipping window is later '
     C                             + 'than deadline ('
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ')'
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   EVAL      SRCDTA = 'Order No: '
     C                             + %trim(UAVD) + '/' + %trim(UOPD)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   EVAL      SRCDTA = 'Shipping deadline: '
     C                             + %trim(UDT1)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   EVAL      SRCDTA = 'Shipping window: '
     C                             + %trim(UDT2) +  ' - ' + %trim(UDT3)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   WHEN      UTYPE = 'ARRIVAL'
     C                   EVAL      SRCDTA = 'Arrival window is later '
     C                             + 'than deadline ('
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ')'
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   EVAL      SRCDTA = 'Order No: '
     C                             + %trim(UAVD) + '/' + %trim(UOPD)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   EVAL      SRCDTA = 'Arrival deadline: '
     C                             + %trim(UDT1)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   EVAL      SRCDTA = 'Arrival window: '
     C                             + %trim(UDT2) +  ' - ' + %trim(UDT3)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   ENDSL
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = 'This message is created '
     C                             + ' automaticly by the booking system.'
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = 'Please do not reply!'
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     INIT          BEGSR
      ******************************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    UUSR             10
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
     C                   PARM                    UDT1              8
     C                   PARM                    UDT2              8
     C                   PARM                    UDT3              8
     C                   PARM                    UTYPE            10
     C                   PARM                    UEMNE            70
     C                   PARM                    UAVNA            30
     C                   PARM                    UAVAD            50
     C                   PARM                    UINTAD1          50
     C                   PARM                    UINTAD2          50
     C                   PARM                    UINTAD3          50
     C                   PARM                    UINTAD4          50
     C                   PARM                    UINTAD5          50
     C                   PARM                    UOK               1
      *
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    BIKUNR
     C     BRPARKEY      KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
      *
     C                   ENDSR
      *
