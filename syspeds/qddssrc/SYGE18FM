********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH PTF8 NY HELP - FUNKSJON
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('SYGE1801' 'SYGE18' 'SY400LO-
S01  A*                                     /FELLES')
N01  A                                      HLPPNLGRP(HLPSCR SYGE11PNL)
N01  A                                      HLPTITLE('OVERSTYRING SKRIVERFIL')
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A* MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      OVERLAY
     A                                      WINDOW(7 25 10 35 *NOMSGLIN)
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'søking')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
S01  A*         H                           HLPDOC('SYGE1801' 'SYGE18' 'SY400LO-
S01  A*                                     /FELLES')
S01  A*                                     HLPARA(03 010 03 020)
S01  A*         H                           HLPDOC('SYGE1802' 'SYGE18' 'SY400LO-
S01  A*                                     /FELLES')
S01  A*                                     HLPARA(04 014 04 023)
S01  A*         H                           HLPDOC('SYGE1803' 'SYGE18' 'SY400LO-
S01  A*                                     /FELLES')
S01  A*                                     HLPARA(05 014 05 014)
S01  A*         H                           HLPDOC('SYGE1804' 'SYGE18' 'SY400LO-
S01  A*                                     /FELLES')
S01  A*                                     HLPARA(06 014 06 020)
S01  A*         H                           HLPDOC('SYGE1805' 'SYGE18' 'SY400LO-
S01  A*                                     /FELLES')
S01  A*                                     HLPARA(07 014 07 024)
S01  A*         H                           HLPDOC('SYGE1806' 'SYGE18' 'SY400LO-
S01  A*                                     /FELLES')
S01  A*                                     HLPARA(08 014 08 019)
S01  A*         H                           HLPDOC('SYGE1807' 'SYGE18' 'SY400LO-
S01  A*                                     /FELLES')
S01  A*                                     HLPARA(09 014 09 015)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(023 SHU0700 *LIBL/SYMSGF)
     A            WSDOKN         3  0O  1 27DSPATR(HI)
     A            UTPTXT        30      2  2
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(10 YBC0232 SYMSGF)
     A            WSVAVD         4Y 0B  3 14DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A  50                              4  2MSGCON(010 SHU0701 *LIBL/SYMSGF)
     A  50        WSPRIN        10   B  4 14
     A  98                                  DSPATR(RI)
     A  50                              5  2MSGCON(010 SHU0702 *LIBL/SYMSGF)
     A  50        WSLASE         1   B  5 14
     A  97                                  DSPATR(RI)
     A  50                              6  2MSGCON(010 SHU0703 *LIBL/SYMSGF)
     A  50        WSSTRI         5   B  6 14
     A  96                                  DSPATR(RI)
     A  50                              7  2MSGCON(010 SHU0706 *LIBL/SYMSGF)
     A  50        WSPATY        10   B  7 14
     A  95                                  DSPATR(RI)
     A  50                              8  2MSGCON(010 SHU0711 *LIBL/SYMSGF)
     A  50        WSHOLD         1   B  8 14
     A  94                                  DSPATR(RI)
     A                                  8 16'/'
     A  50        WSSAVE         1   B  8 18
     A  93                                  DSPATR(RI)
     A  50                              9  2MSGCON(010 SHU0714 *LIBL/SYMSGF)
     A  50        WSKOPI         2  0B  9 14EDTCDE(4)
     A  92                                  DSPATR(RI)
     A  30        MSGTXT        28   O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             10  2MSGCON(024 YBC0234 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
