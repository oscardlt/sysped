********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFNR:*SEK SIG VERS  BESKRIVELSE...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 FEILMELDING V10 PGA. DOBBELT TRYKK PÅ SAMMEN LINJE
10XXX * 02 SH  PTF10 BARKODE LASTELISTE / OVERLAY
10XXX * 03 TH  PTFXX JUSTERING AV POS.
XXXXXX*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
11XXX * 04 YBC PTF11 KOL. 118-120 KAN IKKE BRUKES.
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
11XXX * 14 JOV PTF12 SKRIV OGSÅ G-LINJER + GODSMERKE 1/2
********************************************************************
     A          R HEAD0
     A            KFMT1         75      1  1
     A  06        HP1           80      1  1HIGHLIGHT
     A  06        HP2           80      2  1HIGHLIGHT
     A  06        HP3           80      3  1HIGHLIGHT
     A  06        HP4           80      4  1HIGHLIGHT
     A  06        HP5           80      5  1HIGHLIGHT
     A  06        HP6           80      6  1HIGHLIGHT
     A  06        HP7           80      7  1HIGHLIGHT
      **
N02  A  80        AUPRO          8     10 60BARCODE(CODE128 2 *HRITOP *HRZ +
N02  A                                      (*WIDTH 0.013) (*RATIO 3) X'02')
     A                                 10  1MSGCON(7 JOV0690 SYMSGF)
     A                                      HIGHLIGHT
     A            ZUSER         10     10 11HIGHLIGHT
N06  A            PDAUSR        10     10 22HIGHLIGHT
      **
     A N70N79                          11  1MSGCON(8 PRI0004 SYMSGF)
     A  70                             11  1'Date...:'
     A  79                             11  1'Datum..:'
     A            UDATO1         6  0  11 11EDTCDE(Y)
     A N70N79                          11 36MSGCON(25 PRI0001 SYMSGF)
     A  70                             11 37'L O A D I N G L I S T   '
     A                                      HIGHLIGHT
     A  79                             11 37'L A D E L I S T E       '
     A                                      HIGHLIGHT
     A N70N79                          11 78MSGCON(12 PRI0002 SYMSGF)
     A                                      HIGHLIGHT
     A  70                             11 78'Tripno.....:'
     A                                      HIGHLIGHT
     A  79                             11 78'Reise nr...:'
     A                                      HIGHLIGHT
     A            HEPRO          8  0  11 91
     A                                      HIGHLIGHT
     A N70N79                          12  1MSGCON(8 PRI0003 SYMSGF)
     A  70                             12  1'Time...:'
     A  79                             12  1'Time...:'
     A            TID            6  0  12 11EDTWRD('  :  :  ')
     A        45                       12 33'ETD/ETD.....:'
     A        45  XETD           6  0  12 47EDTCDE(Y)
     A        45                       12 56'/'
     A        45  XETA           6  0  12 58EDTCDE(Y)
     A N70N79N45                       12 78MSGCON(12 PRI0005 SYMSGF)
     A  70   N45                       12 78'Agentref...:'
     A  79   N45                       12 78'Agentref...:'
     A       N45  TUTARF        15     12 91
     A        45                       12 78'Containerno:'
     A        45  TUCON1        17     12 91
     A        45  TUCKD1         2     12110
     A N70N79N45                       13  1MSGCON(8 PRI0006 SYMSGF)
     A  70   N45                       13  1'Car No.:'
     A  79   N45                       13  1'Bilnr..:'
     A       N45  TUBILN        10     13 11
     A       N45  TUHENG        10     13 22
     A        45                       13  1'B/L No.:'
     A        45  TUBLNR        17     13 11
     A N70N79N45                       13 33MSGCON(13 PRI0007 SYMSGF)
     A  70   N45                       13 33'Transporter.:'
     A  79   N45                       13 33'Transporteu.:'
     A       N45  TUNAT         30     13 47
     A        45                       13 33'Vessel name.:'
     A        45  TUBNV         30     13 47
     A N70N79                          13 78MSGCON(10 PRI0008 SYMSGF)
     A  70                             13 78'Agent....:'
     A  79                             13 78'Agent....:'
     A            KNAVN         30     13 89
      **
     A                                 14  1'------------------------'
     A                                 14 25'------------------------'
     A                                 14 49'------------------------'
     A                                 14 73'------------------------'
     A                                 14 97'------------------------'
      **
     A N70N79                          15  1MSGCON(120 PRI0009 SYMSGF)
     A  70                             15  1'Ref/place'
     A  79                             15  1'Ref/place'
     A  70                             15 12'Marks/Numbers'
     A  79                             15 12'Zeichen/nr.'
     A  70                             15 30'Shipper'
     A  79                             15 30'Absender'
E04  A  70                             15 59'Consignee'
E04  A  79                             15 59'Empfanger'
E04  A  70                             15 88'No.pcs'
E04  A  79                             15 88'Anzahl'
E04  A  70                             15 98'Weight'
E04  A  79                             15 98'Gewicht'
E04  A  70                             15107'M3='
E04  A  70                             15114'LM='
E04  A  79                             15107'M3='
E04  A  79                             15114'LM='
      **
     A                                 16  1'------------------------'
     A                                 16 25'------------------------'
     A                                 16 49'------------------------'
     A                                 16 73'------------------------'
     A                                 16 97'------------------------'
     A*
     A                                 17  1' '
      **
     A          R DET
N09  A  94                                 2'Ref:'
N09  A  94        XXRFA         35         7SPACEA(1)
     A            HEAVD          4  0      1
     A                                     5'/'
     A            HEOPD          7  0      6
S14  A*           HEGM          15        14
N14  A            HEGM1         15        14
E04  A            HENAS28       28        30
E04  A            HENAK27       27        59
E04  A            HENT           7  0     87EDTCDE(Z)
E04  A            HEVKT          9  0     95EDTCDE(Z)
E04  A            HEM3           7  3    104EDTCDE(3)
E04  A            HELM           4  2    113EDTCDE(3)
     A                                      SPACEA(1)
      *
     A            HESG           3         1
S14  A*           HEGM2         15         8
N14  A            HEGM2         15        14
S03  A*           HEADS1        30        25
E04  A            HEADS128      28        30
S03  A*           HEADK1        30        56
E04  A            HEADK127      27        59
S03  A*N70N79                             87MSGCON(11 PRI0010 SYMSGF)
E04  A N70N79                             87MSGCON(11 PRI0010 SYMSGF)
S03  A* 70                                87'Fr.weight: '
E04  A  70                                87'Fr.w:'
S03  A* 79                                87'Fr.Gewicht:'
E04  A  79                                87'Fr.G:'
E04  A            BVEKT          9  0     95EDTCDE(Z)
E04  A  77        HESDF          5       106
E04  A  77                               111'->'
E04  A  77        HESDT          5       113
     A                                   120' '
     A                                      SPACEA(1)
      *
E02  A  77N70N79                           1MSGCON(6 PRI0011 SYMSGF)
E02  A  77 70                              1'Posit:'
E02  A  77 79                              1'Posit:'
E02  A  77        HEKDL         15         7
     A N77        HESDF2         5         1
     A N77                                 6'->'
     A N77        HESDT2         5         8
S03  A*           HEADS2        30        25
E04  A            HEADS228      28        30
S03  A*           HEADK2        30        56
E04  A            HEADK227      27        59
E04  A            HEVS1         25        87
E04  A            HEFR           3       113
     A                                      SPACEA(1)
      *
     A            HESDL2        15         6
S03  A*           HEADS3        30        25
E04  A            HEADS328      28        30
S03  A*           HEADK3        30        56
E04  A            HEADK327      27        59
S03  A*           HEVS2         25        90
E04  A            HEVS2         25        87SPACEA(1)
      **
S01  A*         R DETFS                     SPACEA(1)
S03  A*         R DETFS                     SPACEB(1)
N03  A          R DETFS                     SPACEA(1)
     A            KFSOTX        30         1
     A                                    31':'
     A            FSSOK         35        33
      **
S01  A*         R DETUFB                    SPACEA(1)
S03  A*         R DETUFB                    SPACEB(1)
N03  A          R DETUFB                    SPACEA(1)
     A                                      HIGHLIGHT
     A                                     1MSGCON(24 YBC0491 SYMSGF)
     A                                    31':'
     A            HEFT1A        30        33
     A            HEFT1B        15        64
      **
S01  A*         R DET2                      SPACEA(1)
S03  A*         R DET2                      SPACEB(1)
N03  A          R DET2                      SPACEA(1)
     A            FRTTXT        70         1
      **
     A          R DET3                      SPACEA(1)
     A                                     1'________________________'
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________________________'
     A                                    97'________________________'
N02  A          R BARLEFT
E03YBA            HXSNDNB       20         8
N02  A            HXSNDN        20         8BARCODE(CODE128 2 *NOHRI *HRZ +
N02  A                                      (*WIDTH 0.015) X'02')
N02  A                                      SPACEA(2)
N02  A          R BARRIGHT
E03YBA            HXSNDNB       20        80
N02  A            HXSNDN        20        80BARCODE(CODE128 2 *NOHRI *HRZ +
N02  A                                      (*WIDTH 0.015) X'02')
N02  A                                      SPACEA(2)
N02  A          R BARLEFTO
E03YBA            HEAVDOPDB     12         8
N02  A            HEAVDOPD      12         8BARCODE(CODE128 2 *NOHRI *HRZ +
N02  A                                      (*WIDTH 0.015) X'02')
N02  A                                      SPACEA(2)
N02  A          R BARRIGHTO
E03YBA            HEAVDOPDB     12        80
N02  A            HEAVDOPD      12        80BARCODE(CODE128 2 *NOHRI *HRZ +
N02  A                                      (*WIDTH 0.015) X'02')
N02  A                                      SPACEA(2)
N02  A          R BARSPACE                  SPACEB(1)
N02  A                                     3' '
N02   **
     A          R PTOT                      SPACEB(2)
     A                                      HIGHLIGHT
     A N70N79                              1MSGCON(24 PRI0012 SYMSGF)
     A  70                                 1'******  T O T A L  *****'
     A  79                                 1'****  GESAMTBETRAG  ****'
     A N70N79                             28MSGCON(14 PRI0013 SYMSGF)
     A  70                                28'Positions....:'
     A  79                                28'Positionen...:'
     A            POSNR          3  0     42EDTCDE(Z)
     A N70N79                             47MSGCON(17 PRI0014 SYMSGF)
     A  70                                47'No. of pcs......:'
     A  79                                47'Anzahl..........:'
     A            TANTAL         7  0     64EDTCDE(Z)
     A N70N79                             73MSGCON(15 PRI0015 SYMSGF)
     A  70                                73'Weight........:'
     A  79                                73'Gewicht.......:'
     A            TVEKT          9  0     87EDTCDE(Z)
     A N70N79                             98MSGCON(11 PRI0010 SYMSGF)
     A  70                                98'Fr.weight.:'
     A  79                                98'Fr.Gewicht:'
     A            TBVEKT         9  0    109EDTCDE(Z)
     A                                      SPACEA(1)
     A            KFMT2         75         1
      *
