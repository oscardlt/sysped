      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE22R MODULE(*LIBL/TJGE22R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 nytt felt - FFINDX .....
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%
12038 * 02 JOV PTF12 endring DOKUFVF / FFANTE
      * 03 JOV PTF12 kall Med UNIK/REFF = kall bra booking/Z-filer
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDOKUFV    IF   E           K DISK    USROPN
     FDOKUFVF   IF   E           K DISK    USROPN
     FDOKUFM    IF   E           K DISK    USROPN
N03  FZOKUFV2   IF   E           K DISK    USROPN
N03  F                                     RENAME(DOKUFVR:ZOKUFVR)
N03  FZOKUFVF2  IF   E           K DISK    USROPN
N03  F                                     RENAME(DOKUFVFR:ZOKUFVFR)
N03  FZOKUFM2   IF   E           K DISK    USROPN
N03  F                                     RENAME(DOKUFMR:ZOKUFMR)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D FBN             S              3
     D FBNN            S              3  0
N03  D WA              S             15
N03  D UNIK            S              9S 0
N03  D REFF            S             17
     D LIN             S              2
     D LINN            S              2  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬fbn¬ : ¬'+%trim(%editc(FBNN:'Z'))+'¬, '
N03     +'¬unik¬ : ¬'+%trim(%editc(UNIK:'Z'))+'¬, '
N03     +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
        +'¬lin¬ : ¬'+%trim(%editc(LINN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"awblineGet" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Hæmta detta ena ærendet.

     C     Error         IFEQ      *blanks
N03  c     OPDN          ifne      *zeros
     C     DOKUFVK       CHAIN     DOKUFV                             55
N03  c                   else
N03  C     ZOKUFVK       CHAIN     ZOKUFV2                            55
N03  c                   endif
     C     *IN55         IFEQ      '0'
N03  c     OPDN          ifne      *zeros
     C     DOKUFVFK      CHAIN     DOKUFVF                            56
N03  c                   else
N03  C     ZOKUFVFK      CHAIN     ZOKUFVF2                           56
N03  c                   endif
     C                   IF        *IN56
     C                   EVAL      FFUNNR=*BLANKS
     C                   EVAL      FFEMBG=*BLANKS
N01  C                   EVAL      FFINDX=*BLANKS
     C                   EVAL      FFKLAS=*BLANKS
     C                   EVAL      FFSEDD=*BLANKS
     C                   EVAL      FFTRES=*BLANKS
     C                   EVAL      FFFAKT=*ZEROS
     C                   EVAL      FFANTK=*ZEROS
     C                   EVAL      FFANTE=*ZEROS
     C                   EVAL      FFENH=*BLANKS
     C                   EVAL      FFPOEN=*ZEROS
     C                   ENDIF
N03  c     OPDN          ifne      *zeros
     C     DOKUFMK       CHAIN     DOKUFM                             56
N03  c                   else
N03  C     ZOKUFMK       CHAIN     ZOKUFM2                            56
N03  c                   endif
     C                   IF        *IN56
     C                   EVAL      FMMRK1=*BLANKS
     C                   ENDIF
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬fvavd¬ : ¬'+%trim(%editc(fvavd:'Z'))+'¬, '
          +'¬fvopd¬ : ¬'+%trim(%editc(fvopd:'Z'))+'¬, '
          +'¬fvfbnr¬ : ¬'+%trim(%editc(fvfbnr:'Z'))+'¬, '
          +'¬fvlinr¬ : ¬'+%trim(%editc(fvlinr:'Z'))+'¬, '
          +'¬fmmrk1¬ : ¬'+%trim(fmmrk1)+'¬, '
          +'¬fvant¬ : ¬'+%trim(%editc(fvant:'Z'))+'¬, '
          +'¬fvpakn¬ : ¬'+%trim(fvpakn)+'¬, '
          +'¬fvvt¬ : ¬'+%trim(fvvt)+'¬, '
          +'¬fvvkt¬ : ¬'+%trim(%editc(fvvkt:'Z'))+'¬, '
          +'¬fvvol¬ : ¬'+%trim(%editc(fvvol:'4'))+'¬, '
          +'¬fvlm¬ : ¬'+%trim(%editc(fvlm:'4'))+'¬, '
          +'¬fvlm2¬ : ¬'+%trim(%editc(fvlm2:'4'))+'¬, '
          +'¬fvlen¬ : ¬'+%trim(%editc(fvlen:'Z'))+'¬, '
          +'¬fvbrd¬ : ¬'+%trim(%editc(fvbrd:'Z'))+'¬, '
          +'¬fvhoy¬ : ¬'+%trim(%editc(fvhoy:'Z'))+'¬, '
          +'¬ffunnr¬ : ¬'+%trim(ffunnr)+'¬, '
          +'¬ffembg¬ : ¬'+%trim(ffembg)+'¬, '
N01       +'¬ffindx¬ : ¬'+%trim(ffindx)+'¬, '
          +'¬ffklas¬ : ¬'+ %trim(ffklas)+'¬, '
          +'¬ffsedd¬ : ¬'+%trim(ffsedd)+'¬, '
          +'¬fftres¬ : ¬'+%trim(fftres)+'¬, '
          +'¬fffakt¬ : ¬'+%trim(%editc(fffakt:'Z'))+'¬, '
          +'¬ffantk¬ : ¬'+%trim(%editc(ffantk:'Z'))+'¬, '
S02      // +'¬ffante¬ : ¬'+%trim(%editc(ffante:'Z'))+'¬, '
N02       +'¬ffante¬ : ¬'+%trim(%editc(ffante:'4'))+'¬, '
          +'¬ffenh¬  : ¬'+%trim(ffenh)+'¬, '
          +'¬ffpoen¬ : ¬'+%trim(%editc(ffpoen:'Z'))+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDIF
     C                   ENDIF
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     C                   EVAL      JSONstring =']}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Quit
     C                   EXSR      EXIT
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DOKUFV
     C  N50              CLOSE     DOKUFVF
     C  N50              CLOSE     DOKUFM
N03  C  N50              CLOSE     ZOKUFV2
N03  C  N50              CLOSE     ZOKUFVF2
N03  C  N50              CLOSE     ZOKUFM2

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   EVAL      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   EVAL      OPDN    = c2n2(OPD)
     C                   EVAL      FBN     = zhbgetvar('FBN')
     C                   EVAL      FBNN    = c2n2(FBN)
N03  C                   if        FBNN = *zeros
N03  C                   EVAL      FBNN = 1
N03  C                   endif
N03  C                   eval      REFF    = zhbgetvar('REFF')
N03  C                   eval      WA      = zhbgetvar('UNIK')
N03  C                   eval      UNIK    = c2n2(WA)
     C                   EVAL      LIN     = zhbgetvar('LIN')
     C                   EVAL      LINN    = c2n2(LIN)
     C                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   CALL      BILIBL
     C                   end
     C
     C     DOKUFVK       klist
     C                   KFLD                    WFVRI
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    FBNN
     C                   KFLD                    LINN
N03  C     ZOKUFVK       klist
N03  C                   KFLD                    UNIK
N03  C                   KFLD                    FBNN
N03  C                   KFLD                    LINN
     C     DOKUFMK       KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    FVAVD
     C                   KFLD                    FVOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
N01  C                   KFLD                    FMMKNR
N01  C                   KFLD                    FMLONR
N01  C                   EVAL      FMMKNR=1
N01  C                   EVAL      FMLONR=1
N03  C     ZOKUFMK       KLIST
N03  C                   KFLD                    UNIK
N03  C                   KFLD                    FVFBNR
N03  C                   KFLD                    FVLINR
N03  C                   KFLD                    FMMKNR
N03  C                   KFLD                    FMLONR
     C     DOKUFVFK      KLIST
     C                   KFLD                    FVAVD
     C                   KFLD                    FVOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
N01  C                   KFLD                    FFLIN2
     C     *LIKE         DEFINE    FVRI          WFVRI
     C                   EVAL      WFVRI='F'
N01  C                   EVAL      FFLIN2=1
N03  C     ZOKUFVFK      KLIST
N03  C                   KFLD                    UNIK
N03  C                   KFLD                    FVFBNR
N03  C                   KFLD                    FVLINR
N03  C                   KFLD                    FFLIN2

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      DOKUFV
     C  N50              OPEN      DOKUFVF
     C  N50              OPEN      DOKUFM
N03  C  N50              OPEN      ZOKUFV2
N03  C  N50              OPEN      ZOKUFVF2
N03  C  N50              OPEN      ZOKUFM2

     C                   ENDSR
