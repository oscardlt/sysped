      *
      **************************************************************
      *** PROGRAM SOM SKRIVER TRANSPORTØR-KREDITNOTA ARBEIDSLISTE***
      **************************************************************
      **************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10204 * 01 JOV PTF10 Unngå loop dersom transp 99999999 benyttes
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
12046 * 02 JOV PTF12 Håndtere turer for avregning via workfile - unngå at NYE kommer inn
12046 * 03 JOV PTF12 feil i PTF2
12046 * 04 sh  PTF12 engelsk utskrift
      **************************************************************
      *
     FTRAVH     IF   E           K DISK
     FTRAVV2    IF   E           K DISK
     FKOSBU     IF   E           K DISK
     FKOSTA     IF   E           K DISK
     FTURER     IF   E           K DISK
     FTRAN      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYPARL1   IF   E           K DISK
s03  F*RKCGI    UF A E           K DISK
N03  FWRKCGI    UF   E           K DISK
     FSYTR12PF  O    E             PRINTER OFLIND(*IN88)
N02  D                SDS
N02  D  ZJOB                 244    253
N02  D  ZUSER                254    263
N02  D  ZJOBNR               264    269  0
N02   *
N02  D                 DS
N02  D  WKEY2                  1     30
N02  D    THST                 1      1
N02  D    THTNR                2      9  0
N02  D    THRTNR              10     17  0
N02  D    THKDM               18     18
N02  D    THTAVD              19     22  0
N02  D    THTUNR              23     30  0
     D                 DS
     D  XDAG1                  1      4  0
     D  XDAG2                  5      6  0
     D  XDAG3                  7      8  0
     D  XDAG                   1      8  0
     D                 DS
     D  IDAGXD                 1      2  0
     D  IDAGXM                 3      4  0
     D  IDAGXÅ                 5      6  0
     D  IDAGX                  1      6  0
     C*
     C                   EXSR      INIT
     C     UTNR          IFEQ      *ZEROS
     C                   EXSR      ALLE
     C                   ELSE
     C                   EXSR      ENTRAN
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     ALLE          BEGSR
     C***********************************************
     C*
     C*------------------------------------------------------*
     C*                                                      *
     C*     FINN ALLE TRANSPORTØRER MED ' '-RECORD I TRAVH.  *
     C*     (" " = ÅPEN           )                          *
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""*
S02  C*                  MOVE      ' '           YYST              1
S02  C*
S02  C*    TAG1          TAG
S02  C*                  SETOFF                                       32
S02  C*    TRAHK1        SETLL     TRAVH
S02  C*                  READ      TRAVH                                  32
S02  C* N32THST          COMP      YYST                               32
S02  C* N32              EXSR      ENTRAN
S02  C* FLERE TRANSPORTØRER TIL AVREGNING???
S02  c*    THTNR         ifne      99999999
S02  C* N32THTNR         ADD       1             YTNR
S02  C* N32              GOTO      TAG1
S02  c*                  endif
S03  c* finn alle ulike transportører i wrkfile
S03  c*    WINTEG        setll     WRKCGI
S03  c*    WINTEG        reade     WRKCGI
S03  c*                  dow       not %eof(WRKCGI)
S03  c*                  if        THTNR <> GMTNR
S03  c*                  exsr      ENTRAN
S03  c*                  endif
S03  c*                  move      THTNR         GMTNR             8 0
S03  c*    WINTEG        reade     WRKCGI
S03  c*                  enddo
N03   *
N03  c* finn neste transportør i wrkfile
N03  c     NextTran      tag
N03  c     WINTEG        chain(N)  WRKCGI
N03  c                   if        %found(WRKCGI)
N03  C                   exsr      ENTRAN
N03  c                   goto      NextTran
N03  c                   endif
N03   *
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     ENTRAN        BEGSR
     C***********************************************
     C                   EXSR      GETTRA
S03  c* VED "ALLE" KUN DE MED KREDITNOTAKODE PÅ UTSKRIFT
S03  c* ( ER alt filtrert i SYTR12D )
S03  c*    UTNR          IFEQ      *ZEROS
S03  c*    VMINCR        ANDNE     0
S03  c*    VMINCR        ANDNE     3
S03  c*                  GOTO      UTTRAN
S03  c*                  END
     C* TVUNGEN OVERFLOW GIR HEADING
     C                   SETON                                        88
     C                   MOVE      *ZEROS        XSIDE             2 0
     C                   MOVE      *ZEROS        XTOTAL           13 2
     C                   MOVE      *ZEROS        XMVAGR           13 2
     C                   EXSR      NEWPAG
     C*
N02  c                   move      THTNR         GMTNR             8 0
N02  c                   move      THTNR         THTNRA            8
N02  c                   eval      WKEY2=' '+THTNRA
     C                   SETOFF                                       94
S02  C*    TRAHK2        SETLL     TRAVH
N02  C     EnTraKey      SETLL     WRKCGI
     C*
s02  C*    TRAHK2        READE     TRAVH                                  94
N02  C     WINTEG        READE     WRKCGI                                 94
     C     *IN94         DOWEQ     '0'
N02  c     THTNR         cabne     GMTNR         UtEnTran
N02  C     TrhKeyF       CHAIN     TRAVH                              33
     C                   EXSR      DECI
     C                   ADD       THTOT         XTOTAL
     C     THKDM         COMP      'J'                                    34
     C   34              ADD       THTOT         XMVAGR
     C*
     C* 98 ON - GODSKRIV
     C     THRTNR        COMP      99999998                               98
     C  N98              DO
     C*
     C     TURERK        CHAIN     TURER                              33
     C* KONVERTER TURDATO TIL LOKALT FORMAT
     C   33              MOVE      *ZEROS        TUDT
     C                   MOVE      TUDT          TUDATO
     C     FIDTFM        IFNE      3                                            SNU DATO TIL
     C                   CALL      'DATFM3'                                     LOKALT FORMA
     C                   PARM                    FIDTFM
     C                   PARM                    TUDATO
     C                   END
     C     TUTLM         COMP      *ZEROS                                 96
     C     TUTM3         COMP      *ZEROS                                 97
     C     THSTAR        COMP      ' '                                    99    *UBESKYTTET
     C                   EXSR      NEWPAG
     C                   WRITE     DET
     C                   WRITE     DETST
     C                   EXSR      VARELI
     C                   END
     C* SKRIV DETALJLINJE
     C   98THTOT         IFNE      *ZEROS
     C     THBUDA        ORNE      *ZEROS
     C                   EXSR      GODSKR
     C                   END
N03  c                   delete    WRKCGIR
s02  C*    TRAHK2        READE     TRAVH                                  94
N02  C     WINTEG        READE     WRKCGI                                 94
     C  N94              END
     C*
N02  c     UtEnTran      TAG
     C* SKRIV BUNNTOTAL FOR DENNE TRANSPORTØR
     C                   EXSR      TOTAL
     C*
     C     UTTRAN        ENDSR
     C*
     C***********************************************
     C     VARELI        BEGSR
     C***********************************************
      * UNDERTRYK
     C* 61 - 67 - TYPE TEKST ER SKREVET.
     C                   MOVEA     '0000000'     *IN(61)
     C                   SETOFF                                       95
     C*
     C                   EXSR      NEWPAG
     C                   WRITE     DET2H
     C*
     C     TRAVVV        SETLL     TRAVV2
     C     TRAVVV        READE     TRAVV2                                 95
     C     *IN95         DOWEQ     '0'
     C     TVTYP         COMP      'A'                                    51    *AVTALT
     C     TVTYP         COMP      'B'                                    52    *BR-PROV
     C     TVTYP         COMP      'C'                                    53    *INNH.
     C     TVTYP         COMP      'D'                                    54    *UTKJ.
     C     TVTYP         COMP      'E'                                    55    *FØRT REK
     C     TVTYP         COMP      'F'                                    56    *BUD.REK
     C     TVTYP         COMP      'G'                                    57    *JUSTER.
     C   51TVBLL         IFNE      *ZEROS
     C                   EXSR      NEWPAG
     C                   WRITE     DET2
     C                   END
     C     *IN52         IFEQ      '1'
     C     TVOPKO        COMP      ' '                                    50    *U-FAKT*
     C                   END
     C  N51              EXSR      NEWPAG
      * Ved *AVTALT honorar OG param for at da skal detalj undertykkes - skriv kun hvis U-FAKT
     C     UNDETA        IFEQ      'J'
     C     TUTAKO        ANDEQ     'A'
     C     *IN52         IFEQ      '1'
     C     *IN50         ANDEQ     '1'
     C                   WRITE     DET2UFAK
     C                   END
     C                   ELSE
     C  N51              WRITE     DET2
     C                   END
     C     TRAVVV        READE     TRAVV2                                 95
     C   51              SETON                                            61
     C   52              SETON                                            62
     C   53              SETON                                            63
     C   54              SETON                                            64
     C   55              SETON                                            65
     C   56              SETON                                            66
     C   57              SETON                                            67
     C  N95              END
     C*
     C* SIKRER AT TOTAL FOR TUREN KOMMER PÅ SAMME SIDE - IKKE TEST OVERFLOW
     C                   WRITE     DETTOT
     C                   ENDSR
     C***********************************************
     C     GODSKR        BEGSR
     C***********************************************
     C*
     C* RUNDTURNR 99999998 ER REKVISISJONER HVOR DENNE TRANSPORTØR ER
     C* DEN UTFØRENDE PART. OG SKAL GODSKRIVES PÅ KREDITNOTA.
     C* SKRIV "HEADING" - SPESIFISER ALLE REKVISISJONER
     C* OGSÅ TREKK FOR UTLEGG SOM IKKE ER KNYTTET TIL TUR/OPPDRAG
     C*
     C                   EXSR      NEWPAG
     C                   MOVE      'Y'           FØRSTT            1            skriv THEAD
     C                   MOVE      'Y'           FØRSTG            1            skriv GHEAD
     C*
     C                   SETOFF                                           95
     C     TRAVVV        SETLL     TRAVV2
     C     TRAVVV        READE     TRAVV2                                 95
     C     *IN95         DOWEQ     '0'
     C     TVBNRB        CHAIN     KOSBU                              33
     C   33              MOVE      *ZEROS        BUFEDT
     C                   MOVE      BUFEDT        BUDATO            6 0
     C     FIDTFM        IFNE      3                                            SNU DATO TIL
     C     BUFEDT        ANDNE     *ZEROS
     C                   CALL      'DATFM3'                                     LOKALT FORMA
     C                   PARM                    FIDTFM
     C                   PARM                    BUDATO
     C                   END
     C                   EXSR      NEWPAG
     C* Er det TREKK - momskode fra KOSBU ("N"=DEFAULT)
     C     BUTUNR        IFEQ      *ZEROS
     C     BUOPD         ANDEQ     *ZEROS
     C     BUKDM         COMP      'J'                                    34
     C     FØRSTT        IFEQ      'Y'
     C                   WRITE     THEAD
     C                   MOVE      'N'           FØRSTT                         ikke skriv
     C                   END
     C* Hvis det er kostn.ført OG endelig bil.nr gitt, vis det
     C     BUBNR2        IFNE      *ZEROS
     C     BUBNR2        CHAIN     KOSTA                              33
     C  N33KABNR2        IFNE      *ZEROS
     C                   MOVE      KABNR2        BUBNR2
     C                   END
     C                   END
     C                   WRITE     TDET
     C                   ELSE
     C* Er det Godskr. - momskode default J (antar at det er innenlands)
     C     BUKDM         COMP      'N'                                3434
     C     FØRSTG        IFEQ      'Y'
     C                   WRITE     GHEAD
     C                   MOVE      'N'           FØRSTG                         ikke skriv
     C                   END
     C                   WRITE     GDET
     C                   END
     C   34              ADD       TVBLL         XMVAGR
     C     TRAVVV        READE     TRAVV2                                 95
     C  N95              END
     C* SIKRER AT TOTAL  KOMMER PÅ SAMME SIDE - IKKE TEST OVERFLOW
     C                   WRITE     GDETT
     C                   ENDSR
     C***********************************************
     C     GETTRA        BEGSR
     C***********************************************
     C*
     C                   MOVE      *ZEROS        YKUNDN            8 0
     C                   MOVE      *BLANKS       YNAVN            30
     C                   MOVE      *BLANKS       YADR1            30
     C                   MOVE      *BLANKS       YADR2            30
     C                   MOVE      *BLANKS       YADR3            35
     C* TRANSPORTØRENS KUNDENR/ HENT NAVN- SPRÅK
     C     TRANKE        CHAIN     TRAN                               33
     C   33              MOVE      9             VMINCR
     C  N33VMTRKU        IFNE      *ZEROS
     C                   MOVE      VMTRKU        YKN
     C     KUND1K        CHAIN     CUNDL01                            33
     C                   MOVE      KUNDNR        YKUNDN            8 0
     C                   MOVE      KNAVN         YNAVN            30
     C                   MOVE      ADR1          YADR1            30
     C                   MOVE      ADR2          YADR2            30
     C* KOMPONER SISTE ADRESSELINJE UTFRA GITT POSTNR/LANGT POSTNR...
     C                   MOVE      *BLANKS       YADR3            35
     C     POSTNR        COMP      0                                      37
     C     SYPOGE        COMP      *BLANKS                                38
     C  N37              MOVEL     POSTNR        YADR3                          *NORSK
     C  N37              MOVEL     ADR3          YADR32           30              POSTNR
     C  N37              MOVE      YADR32        YADR3
     C   37
     CANN38              MOVEL     SYPOGE        YADR3                          *UTENLANDSK
     C   37
     CANN38              MOVE      ADR3          YADR3                            POSTNR
     C   37
     CAN 38              MOVEL     ADR3          YADR3                          *IKKE POSTNR
     C                   ELSE
     C                   MOVE      VMNAVN        YNAVN            30
     C                   END
     C                   ENDSR
     C***********************************************
     C     TOTAL         BEGSR
     C***********************************************
     C     XMVAGR        MULT(H)   FITAX         XMOMS            13 2
     C     XTOTAL        ADD       XMOMS         XBRUTT
     C*
     C* KAN RISIKERE AT MOMS + TOTAL KOMMER PÅ EGEN SIDE. MEN FOR Å UNNGÅ
     C* DET MÅTTE EN HA SATT OVERFLOW TIL 42 OG FÅR LITE UTAV HVER SIDE
     C     XMOMS         IFNE      *ZEROS
     C                   EXSR      NEWPAG
     C                   WRITE     TOTA
     C                   END
     C* ALLTID PLASS FOR SLUTTTOTAL ( 1 BLANK + 2 LINJER) - IKKE TEST OVERFL.
     C                   WRITE     TOTC
     C                   ENDSR
     C*
     C***********************************************
     C     NEWPAG        BEGSR
     C***********************************************
     C*  88 ER OVERFLOWINDIKATOR
     C   88              DO
     C                   SETOFF                                       88
     C                   ADD       1             XSIDE             2 0
     C                   EXSR      KDTFM1
     C                   WRITE     KJØP
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM1        BEGSR
     C***********************************************
     C                   MOVE      UDAY          UDT1              2 0
     C                   MOVE      UMONTH        UDT2              2 0
     C                   MOVE      UYEAR         UDT3              2 0
     C                   EXSR      KDTFMT
     C                   MOVE      UDT1          IDAGXD
     C                   MOVE      UDT2          IDAGXM
     C                   MOVE      UDT3          IDAGXÅ
     C*
     C                   ENDSR
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     DECI          BEGSR
     C***********************************************
     C* TEST FIRMS NUMBER OF DECIMALS
     C*
     C* NO DECIMALS
     C     FIDECI        IFEQ      0
     C                   Z-ADD(H)  THTOT         FABEL0           11 0
     C                   Z-ADD     FABEL0        THTOT
     C                   ELSE
     C* 1 DECIMAL
     C     FIDECI        IFEQ      1
     C                   Z-ADD(H)  THTOT         FABEL1           12 1
     C                   Z-ADD     FABEL1        THTOT
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *ENTRY        PLIST
     C* TIL SLUTT LEGGES PARAMETRE SOM SKILLER SEG FRA PROG TIL PROG.
     C                   PARM                    UUTNR             8            *TRANSP
     C                   PARM                    UUPERÅ            2            *PERIODEÅR
     C                   PARM                    UUPERM            2            *PERIODEMND
     C**
     C     UUTNR         IFGE      '00000000'
     C     UUTNR         ANDLE     '99999999'
     C                   MOVE      UUTNR         UTNR              8 0
     C                   ELSE
     C                   Z-ADD     99999999      UTNR
     C                   END
     C*
     C* KUNDEKEY
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YKN               8 0
     C* KUNDES LANDKODE
     C     LKKEY         KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    SYLAND
S02  C* FOR Å FINNE NESTE TRANSPORTØR
S02  C*    TRAHK1        KLIST
s02  C*                  KFLD                    YYST
s02  C*                  KFLD                    YTNR
s02  C*                  MOVE      *ZEROS        YTNR              8 0
S02  C* FOR READE  PÅ EN FUNNET TRANSPORTØR
S02  C*    TRAHK2        KLIST
S02  C*                  KFLD                    YYST
S02  C*                  KFLD                    THTNR
N02  C* Full key
N02  C     TRHKEYF       KLIST
N02  C                   KFLD                    THST
N02  C                   KFLD                    THTNR
N02  C                   KFLD                    THRTNR
N02  C                   KFLD                    THKDM
N02  C                   KFLD                    THTAVD
N02  C                   KFLD                    THTUNR
N02  C* Full key
N02  C     EnTraKey      KLIST
N02  C                   KFLD                    WINTEG
N02  C                   KFLD                    WKEY2
N02   *
N02   * WINTEG ER SAMME SOM I FOREGÅENDE PROG SYTR12D  SOM HAR LAGT UT WORKLISTE!!!!
N02   * SIDEN BEGGE JOBBER KALLES FAR SYTR12CLJ FÅR DE UANSETT SAMME JOBNR
N02  c                   z-add     ZJOBNR        WINTEG
     C*
     C     TURERK        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C     TRAVVV        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    YYTVST            1
     C                   MOVE      ' '           YYTVST
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    THTNR
     C     UdetaKey      KLIST
     C                   KFLD                    XXPAKN            8 0
     C                   KFLD                    XXPAID            5
     C                   movel     'UDETA'       XXPAID
     C*
     C                   MOVEL     'LK'          YLKUNI            3
     C                   MOVE      *ZEROS        YKUNDN
     C*
     C*
     C                   READ      FIRM                                   33
N04  C     SYLAND        COMP      'NO'                               7070
     C* SJEKK EVT AVVIKENDE MOMSSATS MOT DATO SAMMENSATT AV PERIODE/ÅR
     C                   MOVE      UUPERM        W1PERM
     C                   MOVE      UUPERÅ        W1PERÅ
     C                   MOVE      31            XDAG3
     C     W1PERM        IFEQ      4
     C     W1PERM        OREQ      6
     C     W1PERM        OREQ      9
     C     W1PERM        OREQ      11
     C                   MOVE      30            XDAG3
     C                   ELSE
     C     W1PERM        IFEQ      2
     C                   MOVE      28            XDAG3
     C                   END
     C                   END
     C                   MOVE      W1PERM        XDAG2
     C     W1PERÅ        IFGE      50
     C     W1PERÅ        ADD       1900          XDAG1
     C                   ELSE
     C     W1PERÅ        ADD       2000          XDAG1
     C                   END
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     XDAG          ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAX
     C                   END
     C*
     C                   MOVE      FITAX         PRITAX            4 2
     C                   MOVE      FICURR        XVAL              3
     C* DERSOM KUN FOR 1 TRANSP-FÅ RETT TRAH2 KEY
     C     UTNR          IFNE      *ZEROS
     C                   MOVE      UTNR          THTNR
     C                   END
      * Undertrykk detaljer ved *AVTALT
     c     UdetaKey      chain     SYPARL1                            33
     C  N33              MOVEL     SYVRDA        UNDETA            1
     C*
     C                   ENDSR
     C*
