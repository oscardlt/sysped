      *
      **************************************************************
      *** PROGRAM FOR UTSKRIFTER M.M.BASERT P≈ PALLEBYTTE-PARTER ***
      **************************************************************
      *
     FPAPA      IF   E           K DISK
     FPAPAL01   IF   E           K DISK
     F                                     RENAME(PAPAR:PAPAR2)
     FFIRM      IF   E           K DISK
     FPALLDS01  IF   E           K DISK
     FSYPA02FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   MOVE      '+'           W2STPM
     C                   READ      FIRM                                   33
      * FINNES DET ≈PNE BUNTER... -> GI WARNING, MEN KUN EN GANG
     C     'B'           CHAIN     PALLDS01                           33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'J'           ≈PNBUN            1
     C                   ELSE
     C                   MOVE      'N'           ≈PNBUN
     C                   END
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      * HUSK SIST BRUKTE UTVALG..(SORTERINGSKEY)
     C                   MOVE      W2PPNR        WWPPNR            8 0
     C                   MOVE      W2KUNR        WWKUNR            8 0
     C                   MOVE      W2KATE        WWKATE            1
     C                   MOVE      W2SPPM        WWSPPM            1
     C                   MOVE      W2STPM        WWSTPM            1
     C                   MOVE      W2ALFA        WWALFA            1
     C* 51 ON ALFA SORTERING/OFF PARTSNR SORTERING
     C     W2ALFA        COMP      'X'                                    51
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C  N51*LOVAL        SETLL     PAPA
     C   51*LOVAL        SETLL     PAPAL01
     C*
     C  N51              READ(N)   PAPA                                   94
     C   51              READ      PAPAL01                                94
      *
      *
     C     *IN94         DOWEQ     '0'
     C     NESTE         TAG
     C                   EXSR      TSTEXC
     C     EXCLUD        IFEQ      'J'
     C  N51              READ(N)   PAPA                                   94
     C   51              READ      PAPAL01                                94
     C  N94              GOTO      NESTE
     C   94              GOTO      START3
     C                   END
     C                   ADD       1             ZRECNO
     C     FIDTFM        IFNE      3
     C                   MOVE      PPSPDT        WPSPDT            6 0
     C                   MOVE      *ZEROS        DATO0             6 0
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WPSPDT
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C                   WRITE     SUBFIL
     C  N51              READ(N)   PAPA                                   94
     C   51              READ      PAPAL01                                94
     C  N94              END
     C*
     C     START3        TAG
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   Z-ADD     1             ZRECNO
     C     *IN30         IFEQ      '0'
     C     ≈PNBUN        ANDEQ     'J'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0962'     MSGNR
     C                   MOVE      'N'           ≈PNBUN
     C                   END
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C***
     C*
     C                   MOVE      '0'           *IN30
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** SÿK PART/KUNDE
     C   04              DO
     C                   EXSR      SÿK
     C                   GOTO      START3
     C                   END
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
      *_________________
      * Nytt s¯k/utvalg
      *"""""""""""""""""
     C     W2PPNR        IFNE      WWPPNR
     C     W2KUNR        ORNE      WWKUNR
     C     W2KATE        ORNE      WWKATE
     C     W2SPPM        ORNE      WWSPPM
     C     W2STPM        ORNE      WWSTPM
     C     W2ALFA        ORNE      WWALFA
     C     *IN16         IFEQ      '1'
     C     *IN17         OREQ      '1'
     C     *IN18         OREQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0660'     MSGNR
     C                   END
     C                   GOTO      START
     C                   END
      * ER MODUL INSTALLERT ?????
     C     MODOK         IFNE      'J'
     C                   MOVEL     'SYPALL  '    UMODUL           15
     C                   MOVE      '       '     UMODUL
     C                   CALL      'SYPRODT'
     C                   PARM                    UMODUL
     C                   PARM                    UMFLAG            1 0
     C     UMFLAG        IFEQ      1
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C                   MOVE      'J'           MODOK             1
     C                   END
     C     *IN16         IFEQ      '1'
     C                   MOVE      W2PPNR        PPNR
     C                   EXSR      PRTSPE
     C                   GOTO      START
     C                   END
     C*
     C     *IN17         IFEQ      '1'
     C                   MOVE      W2PPNR        PPNR
     C                   EXSR      PRTPUR
     C                   GOTO      START
     C                   END
     C*
     C     *IN18         IFEQ      '1'
     C                   MOVE      W2PPNR        PPNR
     C                   EXSR      LAGOPD
     C                   GOTO      START
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C     SALEND        IFEQ      'J'
     C                   GOTO      START
     C                   END
     C                   END
     C                   GOTO      START3
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***************************************************
     C     SÿK           BEGSR
     C***************************************************
     C                   MOVE      *ZEROS        UXXNR             8 0
     C                   MOVE      *BLANKS       UNAVN            30
     C     POSNBR        IFEQ      4
     C                   CALL      'SYPA04A'
     C                   PARM                    UXXNR
     C                   PARM                    UNAVN
     C     UXXNR         IFNE      *ZEROS
     C                   MOVE      UXXNR         W2PPNR
     C                   END
     C                   END
     C     POSNBR        IFEQ      13
     C                   CALL      'SOEKKUN'
     C                   PARM                    UXXNR
     C                   PARM                    FIFIRM
     C                   PARM                    UNAVN
     C     UXXNR         IFNE      *ZEROS
     C                   MOVE      UXXNR         W2KUNR
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVE      'N'           SALEND            1            SALDO-ENDR???
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       30
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     WSNR          IFEQ      '5'                                          SE PART
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   CALL      'SYPA04B'
     C                   PARM                    PPNR
     C                   SETON                                        99
     C                   END
      *
     C     WSNR          IFEQ      '6'                                          SPESIFIKASJ
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      PRTSPE
     C                   SETON                                        99
     C                   END
      *
     C     WSNR          IFEQ      '7'                                          PURRELISTE
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      PRTPUR
     C                   SETON                                        99
     C                   MOVE      'J'           SALEND            1            SALDO-ENDR???
     C                   END
      *
     C     WSNR          IFEQ      '8'                                          LAG OPPDRAG
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C     PPSALP        COMP      *ZEROS                               9898
     C     *IN98         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0658'     MSGNR
     C                   GOTO      SLHENT
     C                   END
     C                   EXSR      LAGOPD
     C                   SETON                                        99
     C                   MOVE      'J'           SALEND            1            SALDO-ENDR???
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     PRTSPE        BEGSR
     C***********************************************
      * DATOUTVALG
     C     FEILD6        TAG
     C                   EXSR      KDTFMT
     C                   EXFMT     VALG6
     C                   EXSR      KDTFMT
     C                   SETOFF                                       307172
     C     *IN12         IFEQ      '1'
     C                   GOTO      END6
     C                   ELSE
     C*** TEST FRA-DATO  (DATO ER N≈ ALT SNUDD TIL ≈≈MMDD)
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   MOVE      WSFDAT        XMM≈≈             4 0
     C                   MOVE      XMM≈≈         UDD               2 0
     C                   MOVEL     XMM≈≈         UMM               2 0
     C                   MOVEL     WSDATO        U≈≈               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    U≈≈
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      72
     C*** TEST TIL-DATO  (DATO ER N≈ ALT SNUDD TIL ≈≈MMDD)
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   MOVE      WSDATO        XMM≈≈             4 0
     C                   MOVE      XMM≈≈         UDD               2 0
     C                   MOVEL     XMM≈≈         UMM               2 0
     C                   MOVEL     WSDATO        U≈≈               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    U≈≈
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      71
     C     *IN71         IFEQ      '1'
     C     *IN72         OREQ      '1'
     C                   MOVE      '1'           *IN30
     C                   GOTO      FEILD6
     C                   END
     C                   END
      *
      * ALLTID UTSKRIFT - SELV OM 0 I SALDO PR. GITT DATO...
     C                   MOVE      ' '           MINST1            1
     C                   CALL      'SYPA06'
     C                   PARM                    PPNR
     C                   PARM                    WSFDAT
     C                   PARM                    WSDATO
     C                   PARM                    W2KUNR
     C                   PARM                    W2KATE
     C                   PARM                    W2SPPM
     C                   PARM                    W2STPM
     C                   PARM                    W2ALFA
     C                   PARM                    MINST1
      *
     C* F16 - SPESIFIKASJ.  FOR ALLE I UTVALGET
     C     *IN16         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0966'     MSGNR
     C                   END
      *
     C     END6          ENDSR
     C*
      *
     C* F17 - PURRING  FOR ALLE I UTVALGET
     C***********************************************
     C     PRTPUR        BEGSR
     C***********************************************
      * DATOUTVALG
     C     FEILD7        TAG
     C                   EXSR      KDTFMT
     C                   EXFMT     VALG7
     C                   EXSR      KDTFMT
     C                   SETOFF                                       3071
     C     *IN12         IFEQ      '1'
     C                   GOTO      END7
     C                   ELSE
     C*** TEST DATO  (DATO ER N≈ ALT SNUDD TIL ≈≈MMDD)
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   MOVE      WSDATO        XMM≈≈             4 0
     C                   MOVE      XMM≈≈         UDD               2 0
     C                   MOVEL     XMM≈≈         UMM               2 0
     C                   MOVEL     WSDATO        U≈≈               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    U≈≈
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      71
     C     *IN71         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   GOTO      FEILD7
     C                   END
     C                   END
      *
     C                   MOVE      ' '           MINST1            1
     C                   CALL      'SYPA07'
     C                   PARM                    PPNR
     C                   PARM                    WSDATO
     C                   PARM                    W2KUNR
     C                   PARM                    W2KATE
     C                   PARM                    W2SPPM
     C                   PARM                    W2STPM
     C                   PARM                    W2ALFA
     C                   PARM                    MINST1
      * Ved bestilling pÂ kun EN part varsel kun nÂr misslykket
      * Ved bestilling via F17 (alle i utvalget) ALLTID varsel
     C     MINST1        IFEQ      ' '
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0710'     MSGNR
     C                   ELSE
     C* F17 - PURRING  FOR ALLE I UTVALGET
     C     *IN17         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0965'     MSGNR
     C                   END
     C                   END
      *
      *
     C     END7          ENDSR
     C*
     C***********************************************
     C     LAGOPD        BEGSR
     C***********************************************
      * DATOUTVALG
     C     FEILD8        TAG
     C                   EXSR      KDTFMT
     C                   EXFMT     VALG8
     C                   EXSR      KDTFMT
     C                   SETOFF                                       3071
     C     *IN12         IFEQ      '1'
     C                   GOTO      END8
     C                   ELSE
     C*** TEST DATO  (DATO ER N≈ ALT SNUDD TIL ≈≈MMDD)
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   MOVE      WSDATO        XMM≈≈             4 0
     C                   MOVE      XMM≈≈         UDD               2 0
     C                   MOVEL     XMM≈≈         UMM               2 0
     C                   MOVEL     WSDATO        U≈≈               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    U≈≈
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      71
     C     *IN71         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   GOTO      FEILD8
     C                   END
     C                   END
      *
     C                   MOVE      ' '           MINST1
     C                   CALL      'SYPA08'
     C                   PARM                    PPNR
     C                   PARM                    WSDATO
     C                   PARM                    W2KUNR
     C                   PARM                    W2KATE
     C                   PARM                    W2SPPM
     C                   PARM                    W2STPM
     C                   PARM                    W2ALFA
     C                   PARM                    MINST1
      *
      * Ved bestilling pÂ kun EN part varsel kun nÂr misslykket
      * Ved bestilling via F18 (alle i utvalget) ALLTID varsel
     C     MINST1        IFEQ      ' '
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0710'     MSGNR
     C                   ELSE
     C* F18 - LAG FOR ALLE I UTVALGET
     C     *IN18         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0663'     MSGNR
     C                   END
     C                   END
     C*
     C     END8          ENDSR
      *
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C     WSFDAT        IFNE      *ZEROS
     C                   CALL      'DATFM5'
     C                   PARM                    FIDTFM
     C                   PARM                    WSFDAT
     C                   END
     C     WSDATO        IFNE      *ZEROS
     C                   CALL      'DATFM5'
     C                   PARM                    FIDTFM
     C                   PARM                    WSDATO
     C                   END
     C*
     C                   ENDSR
     C***********************************************
     C     TSTEXC        BEGSR
     C***********************************************
     C                   MOVE      'N'           EXCLUD            1
     C     W2PPNR        IFNE      *ZEROS
     C     W2PPNR        ANDNE     PPNR
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C*
     C     W2KUNR        IFNE      *ZEROS
     C     W2KUNR        ANDNE     PPKUNR
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2KATE        IFNE      ' '
     C     W2KATE        ANDNE     PPKATE
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
      * HOPP OVER INAKTIVE OM DISSE IKKE EKSPLISITT ER VALGT
     C     W2KATE        IFNE      'I'
     C     PPKATE        ANDEQ     'I'
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
      * SALDO PURRET
     C     W2SPPM        IFEQ      '+'
     C     PPSALP        ANDLE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2SPPM        IFEQ      '-'
     C     PPSALP        ANDGE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2SPPM        IFEQ      '0'
     C     PPSALP        ANDNE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
      * SALDO TOTALT
     C     W2STPM        IFEQ      '+'
     C     PPSALT        ANDLE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2STPM        IFEQ      '-'
     C     PPSALT        ANDGE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2STPM        IFEQ      '0'
     C     PPSALT        ANDNE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C*
     C     UTEXCL        ENDSR
