     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  8  2VALUES(' ' '1' '4' '5' '9')
     A            HEVS1         25A  H
     A            HEVS2         25A  H
     A            HEGM1         15A  H
     A            HEGM2         15A  H
     A            WSTXT         50A  H
     A            WSMED          1A  O  8  7DSPATR(BL)
     A                                      COLOR(RED)
     A            WSFT           1A  O  8 10DSPATR(BL)
     A                                      COLOR(RED)
     A            HEAVD          4Y 0O  8 12EDTCDE(4)
     A            HEOPD          7Y 0O  8 17EDTCDE(4)
     A            WSTLE          6A  B  8 25
     A            WSTLL         10A  B  8 32
     A            WSTLKU         1A  B  8 44VALUES(' ' 'X')
     A            HEST           1A  O  8 47
     A            HEOT           2A  O  8 49
     A            WSSDF          7A  O  8 52
     A            WSNAS         20A  O  8 60
     A            HENT           7Y 0O  9  4EDTCDE(4)
     A            WSVS1         12A  O  9 12
     A            HEVKT          9Y 0H
     A            WSVKT          5Y 0O  9 25EDTCDE(4)
     A            HEM3           7Y 3H
     A            WSM3           4Y 2O  9 31EDTCDE(4)
     A            HELM           4Y 2O  9 37EDTCDE(4)
     A            HEFR           3A  O  9 43
     A            WSSDT          7A  O  9 47
     A            WSNAK         25A  O  9 55
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'UTSKRIFT AV LASTELISTE'
     A                                      COLOR(WHT)
     A                                  1 26'Avd.'
     A            UAVD           4Y 0O  1 31COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 37'Tur:'
     A            UPRO           8Y 0O  1 42COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 52'Sign.'
     A            USG            3A  O  1 58COLOR(BLU)
     A                                  1 63'Td.ty.'
     A            UDTY           2A  O  1 70COLOR(BLU)
     A                                  2  2'Turens innhold'
     A                                  2 20'Fra:'
     A            TUSDF         20A  O  2 25COLOR(BLU)
     A                                  2 52'Til:'
     A            TUSDT         20A  O  2 58COLOR(BLU)
     A                                  3  2'Agent:'
     A            TUKNA          8Y 0O  3 10COLOR(BLU)
     A                                      EDTCDE(4)
     A            WSNAA         30A  O  3 20COLOR(BLU)
     A                                  3 52'Aref.'
     A            TUTARF        15A  O  3 58COLOR(BLU)
     A                                  4  2'Transp:'
     A            TUKNT          8Y 0O  4 10COLOR(BLU)
     A                                      EDTCDE(4)
     A            TUNAT         30A  O  4 20COLOR(BLU)
     A                                  4 52'Stat.'
     A            TUST           1A  O  4 58COLOR(BLU)
     A                                  5  2'Velg option:  1=Plukke 4=Slette 5=-
     A                                      Vise oppd. 9=Fritekst'
     A                                      COLOR(BLU)
     A                                  6  2'Op Med FT Avd. Oppdnr. Tolln/eks  -
     A                                           K.ut St Qt Stedfra Avsender...-
     A                                      .........'
     A                                      COLOR(WHT)
     A                                  7  2'  Antall. Vareslag.... Vekt. LastM-
     A                                       Kubik Fr. Stedtil Mottaker........-
     A                                      .........'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF07(07 'Utskrift')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42'F7=Utskrift'
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF07(07 'Utskrift')
     A                                      CF12(12 'FORRIGE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'UTSKRIFT AV LASTELISTE'
     A                                      COLOR(WHT)
     A                                  1 26'Avd.'
     A            UAVD           4Y 0O  1 31COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 37'Tur:'
     A            UPRO           8Y 0O  1 42COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 52'Sign.'
     A            USG            3A  O  1 58COLOR(BLU)
     A                                  1 63'Td.ty.'
     A            UDTY           2A  O  1 70COLOR(BLU)
     A                                  2  2'Turens innhold'
     A                                  2 20'Fra:'
     A            TUSDF         20A  O  2 25COLOR(BLU)
     A                                  2 52'Til:'
     A            TUSDT         20A  O  2 58COLOR(BLU)
     A                                  3  2'Agent:'
     A            TUKNA          8Y 0O  3 10COLOR(BLU)
     A                                      EDTCDE(4)
     A            WSNAA         30A  O  3 20COLOR(BLU)
     A                                  3 52'Aref.'
     A            TUTARF        15A  O  3 58COLOR(BLU)
     A                                  4  2'Transp:'
     A            TUKNT          8Y 0O  4 10COLOR(BLU)
     A                                      EDTCDE(4)
     A            TUNAT         30A  O  4 20COLOR(BLU)
     A                                  4 52'Stat.'
     A            TUST           1A  O  4 58COLOR(BLU)
     A                                  5  2'Velg option:  1=Plukke 4=Slette 5=-
     A                                      Vise oppd. 9=Fritekst'
     A                                      COLOR(BLU)
     A                                  6  2'Op Med FT Avd. Oppdnr. Tolln/eks  -
     A                                      .ut St Oty Stedfra Avsender........-
     A                                      .........'
     A                                      COLOR(WHT)
     A                                  7  2'  Antall. Vareslag.... Vekt. LastM-
     A                                       Kubik Fr. Stedtil Mottaker........-
     A                                      .........'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42'F7=Utskrift'
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                  2  2'FRITEKST  -->'
     A            WSTXT         50A  B  2 16DSPATR(UL)
     A                                      DSPATR(PC)
