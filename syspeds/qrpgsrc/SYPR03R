********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 feil recortdnummer etter kopiering
      *%%%%%%%%%%%%%
10XXX * xx JOV PTF11 Tillat/vis negativ numerisk verdi (kun DDS - editcode M)
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE PARAMETRE SYSPED BRUKER/KUNDE...
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24
      *** LEDIGE INDIKATORER 27-29 31 34-39 41-83 95-97
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FSYPARF    UF A E           K DISK
     FSYPARL1   IF   E           K DISK
     F                                     RENAME(SYPARFR:SYPARFR1)
     FTELLGE    UF A E           K DISK
     FSYPR03D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  SYVRDA                 1     50
     D  XAVRDA                 1     40
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
      * leser uansett på KUNDENR, forventer at det er 0 ved inngang fra firma/avd/user
     C     UKUNR         SETLL     SYPARL1
     C     START1        TAG
     C     UKUNR         READE     SYPARL1                                94
      *
     C     *IN94         DOWEQ     '0'
     c     SYUSER        cabne     UUSER         NextPar
     c     UAVD          ifne      *blanks
     c     UAVD          andne     *zeros
     c                   move      SYAVD         SYAVDA            4
     c     SYAVDA        cabne     UAVD          NextPar
     c                   end
     c                   movel     SYVRDA        TMPVRDA          50
     c     SYPAID        ifne      GMPAID
     c                   movel     SYPAID        WSPAID
     C     KOFASTK       CHAIN     KOFAST                             33
     c                   movel     KFTXT         SYVRDA
     c                   Move      *ON           *IN50
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     c                   movel     TMPVRDA       SYVRDA
     c                   Move      *OFF          *IN50
     c                   end
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     c                   move      SYPAID        GMPAID            5
     c     NextPar       tag
     C     UKUNR         READE     SYPARL1                                94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      * FRISK OPP
     C   05              GOTO      START
      * LAGE NY
     C   06              DO                                                     1<-B
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C*** CREATE A RECORD
     C     START5        TAG
     C     UKUNR         IFNE      *ZEROS
     C                   MOVE      UKUNR         WSKUNR
     C                   END
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                       30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C   03              Z-ADD     1             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSKUNR        IFNE      0
     C     WSKUNR        ANDNE     99999999
     C     CUND1K        CHAIN     CUNDL01                            90
     C   90              DO
     C                   MOVE      'SPK0001'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     KOFASTK       CHAIN     KOFAST                             89
     C   89              DO
     C                   MOVE      'YBC1861'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C                   END
      *
     C     WSVRDN        COMP      0                                      88
     C   88WSVRDA        COMP      *BLANKS                                88
     C   88
     CANN30              DO
     C                   MOVE      'YBC1862'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C   30              GOTO      SLTB1
      *
     c     wsrecn        COMP      *zeros                                 33
     C  N33WSRECN        CHAIN     SYPARF                             33
     C                   EXSR      MOVWBP
     C                   MOVE      ' '           WSNR
     C   40              UPDATE    SUBFIL
     C  N40              DO
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   WRITE     SUBCTL
      *
     C  N33              UPDATE    SYPARFR
     C   33              WRITE     SYPARFR
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
N01  C                   SETOFF                                       40
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C     SYPAID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGBP
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '3'
     C     SYPAID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CPYBP
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C     SYPAID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTBP
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '5'
     C     SYPAID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DSPBP
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGBP         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   SETON                                        40
     C     SYRECN        CHAIN(N)  SYPARF                             33
     C                   EXSR      MOVBPW
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     20            POSNBR
     c                   move      *blanks       KFTXT
     c                   move      *blanks       KFTXTE
     C     KOFASTK       CHAIN     KOFAST                             33
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   MOVE      '0'           *IN30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C   12              DO
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYBP         BEGSR
     C***********************************************
     C     SYRECN        CHAIN(N)  SYPARF                             33
     C                   EXSR      MOVBPW
     C                   MOVE      *zeros        WSRECN
     C                   SETON                                        99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     25            POSNBR
     c                   move      *blanks       KFTXT
     c                   move      *blanks       KFTXTE
     C     KOFASTK       CHAIN     KOFAST                             33
      *
     C     STCPY         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   MOVE      '0'           *IN30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCPY
     C                   END
     C     *IN12         IFEQ      '1'
     C                   EXSR      MOVB2
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      STCPY
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTBP         BEGSR
     C***********************************************
     C     SYRECN        CHAIN(N)  SYPARF                             33
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     SYRECN        CHAIN     SYPARF                             33
     C  N33              DELETE    SYPARFR
     C                   EXSR      MOVB1
     C                   MOVE      '0'           *IN32
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPBP         BEGSR
     C***********************************************
     C     SYRECN        CHAIN(N)  SYPARF                             33
     C                   SETON                                        99
     c                   movel     SYPAID        WSPAID
     c                   move      *blanks       KFTXT
     c                   move      *blanks       KFTXTE
     C     KOFASTK       CHAIN     KOFAST                             33
     C                   EXFMT     VAREL3
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWBP        BEGSR
     C***********************************************
     C     WSRECN        IFEQ      0
     C     'SYPAR   '    CHAIN     TELLGE                             34
     C                   MOVE      GENO          WSRECN
     C                   ADD       1             GENO
     C                   UPDATE    TELLGER
     C                   END
     C                   MOVE      WSRECN        SYRECN
     C                   MOVE      UUSER         SYUSER
     C                   MOVE      WSKUNR        SYKUNR
     C                   MOVE      WSPAID        SYPAID
     C                   MOVE      WSSORT        SYSORT
     C                   MOVE      WSVRDN        SYVRDN
     C                   MOVE      WSVRDA        SYVRDA
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVBPW        BEGSR
     C***********************************************
     C                   MOVE      SYRECN        WSRECN
     C                   MOVE      SYKUNR        WSKUNR
     C                   MOVE      SYPAID        WSPAID
     C                   MOVE      SYSORT        WSSORT
     C                   MOVE      SYVRDN        WSVRDN
     C                   MOVE      SYVRDA        WSVRDA
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   CLEAR                   SYPARFR
     C                   MOVEL     '*DLT'        SYPAID
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *zeros        WSRECN
     C                   MOVE      *ZEROS        WSKUNR
     C                   MOVE      *BLANKS       WSPAID
     C                   MOVE      *ZEROS        WSSORT
     C                   MOVE      *ZEROS        WSVRDN
     C                   MOVE      *BLANKS       WSVRDA
     C                   MOVE      *BLANKS       KFTXT
     C                   MOVE      *BLANKS       KFTXTE
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     12
     C                   MOVE      *BLANKS       USØKKN
     C                   CALL      'SOEKKUN'
     C     WSKUNR        PARM      WSKUNR        UKN               8 0
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     14
     C     POSNBR        ANDLE     18
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    WSPAID
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     KOFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    WSPAID
      *
     C     *ENTRY        PLIST
     C                   PARM                    UKUND             8
     C                   PARM                    UAVD              4
     C                   PARM                    UUSER            10
     C                   PARM                    UFLAGG            1 0
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   99
     C                   MOVEL     'SYPAR     '  KFTYP
     C                   MOVE      ' '           UUPDAT            1
     C                   MOVE      *ZEROS        UFLAGG
     C     UKUND         IFNE      *BLANKS
     C     UKUND         ANDNE     *ZEROS
     C                   MOVEL     UKUND         UKUNR             8 0
     C                   MOVEL     UKUNR         WSKUNR            8 0
     C     CUND1K        CHAIN     CUNDL01                            33
     C                   MOVEL     KNAVN         UNAVN
     C                   END
      * finnes teller
     C     'SYPAR   '    CHAIN(N)  TELLGE                             34
     c     *IN34         ifeq      '1'
     C                   movel     'SYPAR   '    GECO
     C                   ADD       1             GENO
     C                   write     TELLGER
     C                   END
      *
     C                   ENDSR
      *
