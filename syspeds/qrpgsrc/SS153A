********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 BIAKT=L ER ENDRET TIL Å TESTE AUTORISASJON
********************************************************************
      *********************************************************************
      * VIS ORDREINNHOLD
      * PROGRAM BEHANDLER INPUT FRA SS153
      *
      * OBS! LAG KUN MODUL (OPT 15) AV SS153A IKKE PROGRAM.
      *     (INNGÅR SOM MODUL I PGM SS153, og SS156 )
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
N01  FBRPARF    IF   E           K DISK    USROPN
     FGSSORF    IF   E           K DISK    USROPN
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSRFF    IF   E           K DISK    USROPN
     FGSSAKF    IF   E           K DISK    USROPN
     FLLVARE01  IF   E           K DISK    USROPN
     FLLGRPK05  IF   E           K DISK    USROPN
     FVADR      IF   E           K DISK    USROPN
     FDOKUF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSONO           s              7
     D W2ONO           s              7  0
     D WKEY            S            256
     D WSCNA           s             36
     D lstbrd          s              1
     D NIER            s             30a   inz('999999999999999999999999999999')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D WKEY1                   1    256
     D CURTUR                  2      9
     D KUNITUR                11     11
     D AREA                   12     13
     D STATUS                 14     14
     D KUNDE                  15     22
     D XXLEVERT              256    256
     D                 DS
     D ORBCD                   1      5
     D ORBCD2                  2      5
     D                 DS
     D ORFT4                   1     30
     D ORAREA                  1      2
     D ORAAR                   4      7
     D ORMND                   8      9
     D ORAARMND                4      9
     D ORLMA                  11     14
     D                 DS
     D DF1004                  1     35
     D WSFBNR                  1     20
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      */copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   EXSR      INIT
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
S01  C*                  IF        BIAKT = 'L'
N01  C                   IF        XAUT = 'L'
     C                   callp     wrtsection('Superbruker')
     C                   ELSE
     C                   callp     wrtsection('Vanligbruker')
     C                   END
     C                   callp     wrtsection('Felles')
      *
     C                   exsr      SetData
     C                   callp     wrtsection('foundsome')
     C                   SELECT
     C                   WHEN      XXLEVERT = 'J'
     C                   callp     wrtsection('foundback2')
     C                   WHEN      XXLEVERT = 'S'
     C                   callp     updhtmlvar('CURTUR':CURTUR)
     C                   callp     updhtmlvar('KUNITUR':KUNITUR)
     C                   callp     updhtmlvar('STATUS':STATUS)
     C                   callp     updhtmlvar('AREA':AREA)
     C                   callp     updhtmlvar('KUNDE':KUNDE)
     C                   callp     wrtsection('foundback3')
     C                   WHEN      XXLEVERT = 'N'
     C                   callp     wrtsection('foundback1')
     C                   ENDSL
     C                   callp     wrtsection('foundback4')
      *
     C                   exsr      SetEnd
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WKEY':WKEY)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C     W2ONO         CHAIN     GSSORF                             33
     C                   callp     updHTMLvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
      *
     C                   IF        FMELDNR = *BLANKS
     C                   callp     updhtmlvar('ORSNM':ORSNM)
     C                   callp     updHTMLvar('ORDTL':
     C                             %trim(%editw(ORDTL:'    .  .  ')))
     C                   callp     updHTMLvar('ORCLI':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   callp     updHTMLvar('ORWT':
     C                             %trim(%editc(ORWT:'3')))
     C                   callp     updHTMLvar('ORVOL':
     C                             %trim(%editc(ORVOL:'3')))
     C                   IF        ORLMA = *BLANKS
     C                   MOVE      *ZEROS        ORLM              4 2
     C                   ELSE
     C                   MOVE      ORLMA         ORLM
     C                   END
     C                   callp     updHTMLvar('ORLM':
     C                             %trim(%editc(ORLM:'3')))
     C                   callp     updhtmlvar('WSAREA':ORAREA)
     C                   callp     updHTMLvar('WSTYM':ORAARMND)
     C                   callp     updHTMLvar('ORDTWL':
     C                             %trim(%editw(ORDTWL:'    .  .  ')))
     C     W2ONO         CHAIN     GSSPOF                             33
     C                   callp     updhtmlvar('POCONO':POCONO)
     C     W2ONO         SETLL     GSSRFF
     C     W2ONO         READE     GSSRFF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        RFKD = 'CPO'
     C                   LEAVE
     C                   END
     C     W2ONO         READE     GSSRFF                                 33
     C  N33              ENDDO
     C   33              MOVE      *BLANKS       RFREF
     C                   callp     updhtmlvar('WSACC':RFREF)
     C     W2ONO         SETLL     GSSRFF
     C     W2ONO         READE     GSSRFF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        RFKD = 'SAL'
     C                   LEAVE
     C                   END
     C     W2ONO         READE     GSSRFF                                 33
     C  N33              ENDDO
     C   33              MOVE      *BLANKS       RFREF
     C                   callp     updhtmlvar('WSSAL':RFREF)
      *
     C                   ELSE
      *
     C                   callp     updhtmlvar('POCONO':WSCONO)
     C                   callp     updhtmlvar('ORSNM':WSSNM)
     C                   callp     updhtmlvar('WSACC':WSACC)
     C                   callp     updhtmlvar('ORDTL':WSDTL)
     C                   callp     updhtmlvar('ORCLI':WSCLI)
     C                   callp     updhtmlvar('ORWT':WSWT)
     C                   callp     updhtmlvar('ORVOL':WSVOL)
     C                   callp     updhtmlvar('ORLM':WSLM)
     C                   callp     updhtmlvar('WSSAL':WSSAL)
     C                   callp     updhtmlvar('WSAREA':WSAREA)
     C                   callp     updHTMLvar('WSTYM':WSTYM)
     C                   callp     updhtmlvar('ORDTWL':WSDTWL)
      *
     C                   END
      *
     C                   callp     updHTMLvar('ORTNO':
     C                             %trim(%editc(ORTNO:'Z')))
     C                   SELECT
     C                   WHEN      ORTNO <> 0
     C                   MOVEL     ORTNO         DFAVD
     C                   MOVE      ORTNO         DFOPD
     C                   WHEN      ORTNO2 <> 0
     C                   MOVEL     ORTNO2        DFAVD
     C                   MOVE      ORTNO2        DFOPD
     C                   WHEN      ORTNO3 <> 0
     C                   MOVEL     ORTNO3        DFAVD
     C                   MOVE      ORTNO3        DFOPD
     C                   ENDSL
     C     DOKUFK        CHAIN     DOKUF                              33
     C   33              MOVE      *BLANKS       WSFBNR
     C                   callp     updhtmlvar('WSFBNR':WSFBNR)
     C                   MOVE      ORBCD2        VADRNR
     C     VADRK         CHAIN     VADR                               33
     C                   EVAL      WSCNA = ORBCD
     C                   CAT       '/':0         WSCNA
     C                   CAT       VADRN3:0      WSCNA
     C                   callp     updhtmlvar('WSCNA':WSCNA)
      *
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS153'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section
      *  (search criteria)
      *=====================================================================******
     C     SetData       begsr
      *
     C                   callp     wrtsection('tablestr')
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   callp     updhtmlvar('WKEY':WKEY)
     C                   callp     updhtmlvar('ORONO':WSONO)
     C     ORCNO         CHAIN     LLGRPK05                           33
      *
     C     W2ONO         SETLL     GSSAKF
     C     W2ONO         READE     GSSAKF                                 33
     C                   DOW       *IN33 = *OFF
     C                   callp     updhtmlvar('AKLNO':
     C                             %trim(%editc(AKLNO:'Z')))
     C                   callp     updhtmlvar('AKANR':AKANR)
     C     LLVARE1K      CHAIN     LLVARE01                           34
     C   34              MOVE      *BLANKS       LVVB
     C                   callp     updhtmlvar('LVVB':LVVB)
     C                   callp     updhtmlvar('AKSTK':
     C                             %trim(%editc(AKSTK:'Z')))
     C                   callp     updhtmlvar('AKSTKM':
     C                             %trim(%editc(AKSTKM:'Z')))
     C                   SELECT
S01  C*                  WHEN      BIAKT = 'L'
N01  C                   WHEN      XAUT = 'L'
     C                   callp     updhtmlvar('WSPR':
     C                             %trim(%editc(AKPR:'3')))
     C                   WHEN      AKPR = 0,01
     C                   callp     updhtmlvar('WSPR':'$$')
     C                   OTHER
     C                   callp     updhtmlvar('WSPR':' ')
     C                   ENDSL
     C                   callp     updhtmlvar('AKTXT':AKTXT)
     C                   callp     wrtsection('tablerow')
      *
     C     W2ONO         READE     GSSAKF                                 33
     C  N33              ENDDO
      *
     C                   callp     wrtsection('tableend')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C     *ENTRY        PLIST
     C                   PARM                    LNG
     C                   PARM                    BCKGND
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    WSNA
     C                   PARM                    WKEY
     C                   PARM                    WSONO
     C                   PARM                    WSCONO           20
     C                   PARM                    WSPO             20
     C                   PARM                    WSSNM            30
     C                   PARM                    WSACC            35
     C                   PARM                    WSDTL            10
     C                   PARM                    WSCLI             5
     C                   PARM                    WSWT              8
     C                   PARM                    WSVOL             6
     C                   PARM                    WSLM              5
     C                   PARM                    WSSAL            35
     C                   PARM                    WSAREA            2
     C                   PARM                    WSTYM             7
     C                   PARM                    WSDTWL           10
     C                   PARM                    LSTBRD
     C                   PARM                    FMELDNR           2
     C                   PARM                    FEILMELDING      60
      *
     C                   OPEN      BRIDF
N01  C                   OPEN      BRPARF
     C                   OPEN      GSSORF
     C                   OPEN      GSSPOF
     C                   OPEN      GSSRFF
     C                   OPEN      GSSAKF
     C                   OPEN      VADR
     C                   OPEN      DOKUF
     C                   OPEN      LLVARE01
     C                   open      LLGRPK05
      *
     C     VADRK         KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    ORCNO
     C                   KFLD                    VADRNR
     C     LLVARE1K      klist
     C                   kfld                    GKGRP
     C                   kfld                    ORCNO
     C                   kfld                    AKANR
     C     DOKUFK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
N01  C     BrParKey      klist
N01  C                   kfld                    PABRID
N01  C                   kfld                    PAKUNR
N01  C                   kfld                    PAID
N01  C                   EVAL      PABRID = WSUSER
N01   *
N01  C                   MOVE      'AUTOR'       PAID
N01  C     BrParKey      chain     BRPARF                             33
N01  C                   IF        *IN33
N01  C                   MOVE      'N'           XAUT              1
N01  C                   ELSE
N01  C                   MOVEL     PAVRDA        XAUT
N01  C                   END
      *
     C                   MOVE      'F'           DFRI
     C                   Z-ADD     1             DFFBNR
     C                   MOVE      WKEY          WKEY1
     C                   eval      W2ONO   = c2n2(WSONO)
     C     WSUSER        CHAIN     BRIDF                              50
S01  C*                  IF        BIAKT = 'L'
N01  C                   IF        XAUT = 'L'
     C                   IF        ((XXLEVERT = 'J') OR (XXLEVERT = 'S'))
S01  C*                  MOVE      'A'           BIAKT
N01  C                   MOVE      'N'           XAUT
     C                   END
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
N01  C                   close     BRPARF
     C                   close     GSSORF
     C                   close     GSSPOF
     C                   close     GSSRFF
     C                   close     GSSAKF
     C                   close     VADR
     C                   close     DOKUF
     C                   close     LLVARE01
     C                   CLOSE     LLGRPK05
     C                   eval      *inlr = *on
      *
     C                   endsr
