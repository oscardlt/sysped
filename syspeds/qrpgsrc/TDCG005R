      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDCG005R MODULE(*LIBL/TDCG005R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKXKODL01 IF   E           K DISK    USROPN
     FDKXFKDF   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D JSONstring      s          32000
     D Error           S            150
     D Wtyp            S              3
     D TARIFFN         S                   LIKE(TFTANR)
     D TARIFF          S              6
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬typ¬ : ¬'+%trim(WTYP)+'¬, '
        +'¬tariff¬ : ¬'+%trim(TARIFF)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"kodlista" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta koder
      *
     c     Error         ifeq      *blanks
     C                   IF        WTYP = '064'
     C     *LOVAL        SETLL     DKXFKDF
     C                   READ      DKXFKDF                                55
     C  N55              MOVEL     TFTANR        TKKODE
     C  N55              MOVEL     TFTXT         TKTXTN
     C                   ELSE
     C     WTYP          SETLL     DKXKODL01
     C     WTYP          READE     DKXKODL01                              55
     C                   END
     C     *IN55         DOWEQ     *OFF
     C                   IF        WTYP = '064'
     C                   IF        TARIFF <> *BLANKS
     C                   IF        TARIFFN <> TFTANR
     C                   GOTO      UTLES
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tkkode¬ : ¬'+%trim(TKKODE)+'¬, '
          +'¬tktxtn¬ : ¬'+%trim(TKTXTN)+'¬, '
          +'¬tfkode¬ : ¬'+%trim(TFKODE)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C     UTLES         TAG
     C                   IF        WTYP = '064'
     C                   READ      DKXFKDF                                55
     C  N55              MOVEL     TFTANR        TKKODE
     C  N55              MOVEL     TFTXT         TKTXTN
     C                   ELSE
     C     WTYP          READE     DKXKODL01                              55
     C                   END
     C                   ENDDO
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKXKODL01
     C  N50              CLOSE     DKXFKDF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WTYP    = zhbgetvar('TYP')
     C                   EVAL      TARIFF  = zhbgetvar('TARIFF')
     C                   EVAL      TARIFFN = c2n2(TARIFF)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKXKODL01
     C  N50              OPEN      DKXFKDF

     c                   endsr
      *
