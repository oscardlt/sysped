********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10026 * 01 YBC PTF10 VIS ORDRETYPE
********************************************************************
     A          R GSS015LA
     A            KFMT1         75      1  1
     A            KFMT2         75      1  1
     A N52        STRI03        70      2  1
     A N52        STRI01        70      2 20
     A                                  2 45'STOCK REPORT'
     A                                  4  2'SHIPPER:'
     A            KNAVN         30      4 16
     A            FINM          30      4 60
     A            ADR1          30      5 16
     A            FIAD1         30      5 60
     A            ADR2          30      6 16
     A            FIAD2         30      6 60
     A            POSTNR         4  0   7 16
     A            ADR3          24      7 24
     A            FIAD3         30      7 60
     A                                  9  2'MASTER:'
     A            CBNAB         30      9 16
     A                                  9 60'DATE:'
     A                                  9 66DATE EDTCDE(Y)
     A                                  9 77'PAGE:'
     A            XSIDE          3  0   9 84
     A          R GSS015LA2
     A*                                     CPI(15)
     A N52        STRI02        70     11  1
     A                                 11  2'-------------------------'
     A                                 11 27'-------------------------'
     A                                 11 52'-------------------------'
     A                                 11 77'-------------------------'
     A                                 11102'------------------'
     A                                 12  2'SUPPLIER'
     A                                 12 32'|P. ORDER NO.'
     A                                 12 53'|DATE'
     A                                 12 62'|CONTENTS'
     A                                 12 83'|PCS'
     A                                 12 89'|WEIGHT'
     A                                 12 97'|CUSTOMS VALUE'
     A                                 12114'|P'
N01  A                                 12117'T'
     A                                 13  2'-------------------------'
     A                                 13 27'-------------------------'
     A                                 13 52'-------------------------'
     A                                 13 77'-------------------------'
     A                                 13102'------------------'
     A          R GSS015LB                  SPACEB(1)
     A            ORSNM         30A        2
     A            XCONO1        20A       33
     A N39        XDTL           6  0     54EDTWRD('  .  .  ')
     A  39        XDTWL          6  0     54EDTWRD('  .  .  ')
     A            XDSC20        20A       63
     A            ORCLI          5  0     84EDTCDE(Z)
     A            ORWT           7  1     89EDTCDE(3)
     A            ORAMT         11  2     98EDTCDE(4)
     A            ORCURC         3A      111
     A            PAPKD          1A      115
N01  A            ORTYPE         1A      117
      *
     A          R GSS015LR                  SPACEB(1)
     A                                    10'* RETURN *'
     A          R GSS015LS                  SPACEB(1)
     A                                    10'* STRATEGIC *'
     A          R GSS015LC                  SPACEB(1)
     A  40        XSTATU        15A        2
     A  40        XTNO          12A       18
     A            XCON          20A       35
     A            GDDSC         55A       65
     A          R GSS015LD                  SPACEB(2)
     A N39                                 2'STORED AT'
     A                                      HIGHLIGHT
     A  39                                 2'EXPECTED '
     A                                      HIGHLIGHT
     A            XLCNV         30A       12HIGHLIGHT
     A                                      SPACEA(1)
     A          R GSS015LE                  SPACEB(1)
     A            GDDSC         55A       65
     A          R GSS015LF                  SPACEB(1)
     A            XATXT1        50A       15
     A            XATXT2        50A       66
     A          R GSS015LG                  SPACEB(1)
     A N61                                15'COMMENTS:'
     A            XXFT1         30A       25
     A            XXFT2         30A       56
     A          R GSS015LH                  SPACEB(1)
     A N62                                15'POSISJON:'
     A            XXPO01        10A       25
     A            XXPO02        10A       36
     A            XXPO03        10A       47
     A            XXPO04        10A       58
     A            XXPO05        10A       69
     A            XXPO06        10A       80
     A            XXPO07        10A       91
     A            XXPO08        10A      102
