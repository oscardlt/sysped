********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068F* 03 TH  PTF8 ny helpfunksjon
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S03  A*                                     HLPDOC('SAD00701' 'SAD007' 'SY400-
S03  A*                                     LO/SADTVINN')
S03  A*                                     HELP
S03  A*                                     ALTHELP
N03  A                                      HELP(01)
N03  A                                      CF01(01)
      *
     A          R BILDE
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Bytt avdeling')
     A                                  2  5'SAD007'
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(045 DIV1777 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 21MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  5 34COLOR(BLU)
     A            UAVDNV        33A  O  5 39COLOR(BLU)
     A                                  7 21MSGCON(015 SHU0626 *LIBL/SYMSGF)
     A            WSALLE         1   B  7 41COLOR(YLW)
     A                                  7 49MSGCON(008 SHU0627 *LIBL/SYMSGF)
     A                                  8 21MSGCON(015 DIV1778 *LIBL/SYMSGF)
     A            WSDTF          6D 0B  8 41DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  9 21MSGCON(015 DIV1779 *LIBL/SYMSGF)
     A            WSDTT          6D 0B  9 41DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11 21MSGCON(018 DIV1780 *LIBL/SYMSGF)
     A            WSKL           1A  B 11 41DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'A' 'M')
     A                                 11 49MSGCON(025 DIV1781 *LIBL/SYMSGF)
     A                                 13 21MSGCON(015 DIV1782 *LIBL/SYMSGF)
     A            WSKNF          8Y 0B 13 41DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 21MSGCON(015 DIV1783 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B 14 41DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 21MSGCON(015 DIV1784 *LIBL/SYMSGF)
     A            WSDETJ         1A  B 16 41DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J')
     A                                 16 49MSGCON(015 DIV1785 *LIBL/SYMSGF)
     A                                 23 20MSGCON(015 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(015 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
