      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE K/S AVTALER PÅ KUNDER     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     FKAVT      UF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FKODTOT2   IF   E           K DISK
     FSYFA98FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSTST                  1      6
     D  WSSK                   1      1
     D  WSOPDT                 2      3
     D  WSFRAN                 4      6
     D                 DS
     D  SFTST                  1      6
     D  SFSK                   1      1
     D  SFOPDT                 2      3
     D  SFFRAN                 4      6
     C*
     C                   EXSR      INIT
     C*
     C     CHOISE        TAG
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     17            POSNBR
     C                   EXFMT     BILDE1
     C                   SETOFF                                       9930
     C*
     C**  AVSLUTT
     C*
     C     *IN03         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END                                                    <*IN03
     C*
     C**  SØK
     C*
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKUNR
     C     KUNKEY        CHAIN     CUNDL01                            99
     C     *IN99         IFEQ      '1'
     C                   MOVE      *ZEROS        W1KNR
     C                   SETOFF                                       99
     C                   GOTO      CHOISE
     C                   END                                                    <*IN99
     C                   END                                                    <*IN04
     C*
     C     KUNKEY        CHAIN     CUNDL01                            35
     C     *IN35         IFEQ      '0'
     C                   MOVE      KNAVN         W1NAVN
     C                   ELSE
     C                   MOVE      '1'           *IN30
     C                   MOVE      'DIV2480'     MSGNR
     C                   GOTO      CHOISE
     C                   END                                                    <*IN35
     C*
     C     START         TAG
     C                   CLEAR                   SFSK
     C                   CLEAR                   SFOPDT
     C                   CLEAR                   SFFRAN
     C     STARTF        TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SFKEY         SETLL     KAVT
      *
     C     START1        TAG
     C                   DO        15
     C     W1KNR         READE(N)  KAVT                                   94
     C     *IN94         IFEQ      '0'
     C                   MOVE      KASK          SFSK
     C                   MOVE      KAOPDT        SFOPDT
     C                   MOVE      KAFRAN        SFFRAN
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END                                                    <*IN94
     C  N94              ENDDO
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYKUNDE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   Z-ADD     WSSUBN        XRECNO
     C                   EXFMT     BUNN
     C                   END
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C*
     C                   SETOFF                                       999830
     C*** AVSLUTT
     C     *IN03         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END                                                    <*IN03
     C*** FRISK OPP
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN05
     C*** LAGE
     C     *IN06         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   GOTO      START5
     C                   END                                                    <*IN06
     C*** SØK
     C     *IN08         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     19            POSNBR
     C                   GOTO      START6
     C                   END                                                    <*IN08
     C*
     C*** FORRIGE
     C     *IN12         IFEQ      '1'
     C                   GOTO      CHOISE
     C                   END                                                    <*IN12
     C*** BLA VIDERE
     C     *IN25         IFEQ      '1'
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C     *IN94         IFEQ      '0'
     C     SFKEY         SETLL     KAVT
     C                   END                                                    <*IN94
     C     START4        TAG
     C     *IN94         IFEQ      '0'
     C                   READ      KAVT                                   94
     C                   GOTO      START1
     C                   END                                                    <*IN94
     C                   END                                                    <*IN25
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END                                                    <ZRECNO
     C     *IN98         IFEQ      '1'
     C     XRECNO        ANDNE     0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END                                                    <*IN98
     C                   GOTO      START3
     C*** NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000'       *IN(86)
     C                   SETOFF                                           30
     C     *IN12         IFEQ      '1'
     C                   EXSR      MOVBWS
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   END                                                    <*IN12
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*** SØK FRA AVDELING
     C     START6        TAG
     C                   EXFMT     VAREL2
     C     *IN12         IFNE      '1'
     C                   MOVE      WSSK          SFSK
     C                   MOVE      WSOPDT        SFOPDT
     C                   MOVE      WSFRAN        SFFRAN
     C                   GOTO      STARTF
     C                   END                                                    <*IN12
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*          N40 = NY
     C*           40 = ENDRING
     C*
     C*  TEST FOR GYLDIG KODE S/K
     C*
     C     WSSK          IFNE      'S'
     C     WSSK          ANDNE     'K'
     C                   MOVE      'DIV2481'     MSGNR
     C                   Z-ADD     06            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END                                                    <WSSK
     C*
     C*  TEST FOR GYLDIG OPPDRAGSTYPE
     C*
     C     KODO2K        CHAIN     KODTOT2                            37
     C     *IN37         IFNE      '0'
     C                   MOVE      'DIV2483'     MSGNR
     C                   Z-ADD     19            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN37
     C                   GOTO      SLTB
     C                   END                                                    <*IN37
     C*
     C*  TEST FOR GYLDIG FRANKATUR
     C*
     C     KODFRK        CHAIN     KODTFR                             36
     C     *IN36         IFNE      '0'
     C                   MOVE      'DIV2482'     MSGNR
     C                   Z-ADD     31            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN36
     C                   GOTO      SLTB
     C                   END                                                    <*IN36
     C*
     C  N40WSKEY         CHAIN     KAVT                               33
     C*
     C*  TEST OM K/S AVTALE FINNES FRA FØR VED ENDRING
     C*
     C     *IN40         IFEQ      '1'
     C     WSTST         IFNE      SFTST
     C     WSKEY         CHAIN     KAVT                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'DIV2479'     MSGNR
     C                   Z-ADD     06            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END                                                    <*IN33
     C                   END                                                    <WSTST
     C                   END                                                    <*IN40
     C*
     C   40SFKEY         CHAIN     KAVT                               33
     C*
     C*  TEST OM K/S AVTALE FINNES FRA FØR VED NYREGISTRERING
     C*
     C     *IN33         IFEQ      '0'
     C     *IN40         ANDEQ     '0'
     C                   MOVE      'DIV2479'     MSGNR
     C                   Z-ADD     9             POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END                                                    <*IN33
     C*
     C                   EXSR      MOVWKA
     C     *IN33         IFEQ      '1'
     C                   WRITE     KAVTR
     C                   ELSE
     C                   UPDATE    KAVTR                                38
     C                   END                                                    <*IN33
     C*
     C                   EXSR      MOVWSF
     C     *IN40         IFEQ      '1'
     C                   UPDATE    SUBFIL
     C                   ELSE
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END                                                    <*IN40
     C                   EXSR      MOVBWS
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              MOVE      '1'           *IN30
     C                   GOTO      SLHENT
     C                   ENDDO
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C*
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     SFFRAN        IFEQ      '*DE'
     C                   MOVE      'SPS0029'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLHENT
     C                   END                                                    <SFFRAN
     C                   EXSR      CHGST
     C     *IN12         IFEQ      '1'
     C                   GOTO      SLHENT
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR
     C*
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     SFFRAN        IFEQ      '*DE'
     C                   MOVE      'SPS0029'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLHENT
     C                   END                                                    <SFFRAN
     C                   EXSR      DLTST
     C     *IN12         IFEQ      '1'
     C                   GOTO      SLHENT
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR
     C                   ENDDO
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTST         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   EXFMT     VAREL3
     C     *IN12         IFEQ      '0'
     C     SFKEY         CHAIN     KAVT                               33
     C  N33              DELETE    KAVTR
     C                   EXSR      MOVBSF
     C                   END                                                    <*IN12
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGST         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   MOVE      '1'           *IN99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
     C     *IN12         IFEQ      '1'
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVBWS
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      STCHG
     C                   END                                                    <*IN12
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      SFSK          WSSK
     C                   MOVE      SFOPDT        WSOPDT
     C                   MOVE      SFFRAN        WSFRAN
     C                   MOVE      '1'           *IN40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     06            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWKA        BEGSR
     C***********************************************
     C                   MOVE      W1KNR         KAKNR
     C                   MOVE      WSSK          KASK
     C                   MOVE      WSOPDT        KAOPDT
     C                   MOVE      WSFRAN        KAFRAN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C                   MOVE      WSSK          SFSK
     C                   MOVE      WSOPDT        SFOPDT
     C                   MOVE      WSFRAN        SFFRAN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBSF        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       SFSK
     C                   MOVE      *BLANKS       SFOPDT
     C                   MOVEL     '*DEL '       SFFRAN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBWS        BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSSK
     C                   MOVE      *BLANKS       WSOPDT
     C                   MOVE      *BLANKS       WSFRAN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KAVTKY        KLIST
     C                   KFLD                    W1KNR
     C                   KFLD                    KASK
     C                   KFLD                    KAOPDT
     C                   KFLD                    KAFRAN
     C*
     C     SFKEY         KLIST
     C                   KFLD                    W1KNR
     C                   KFLD                    SFSK
     C                   KFLD                    SFOPDT
     C                   KFLD                    SFFRAN
     C*
     C     WSKEY         KLIST
     C                   KFLD                    W1KNR
     C                   KFLD                    WSSK
     C                   KFLD                    WSOPDT
     C                   KFLD                    WSFRAN
     C*
     C     *LIKE         DEFINE    FIRMA         ZFIRMA
     C     KUNKEY        KLIST
     C                   KFLD                    ZFIRMA
     C                   KFLD                    W1KNR
     C*
     C     KODFRK        KLIST
     C                   KFLD                    YFRUNI
     C                   KFLD                    WSFRAN
     C*
     C     KODO2K        KLIST
     C                   KFLD                    YO2UNI
     C                   KFLD                    WSOPDT
     C*
     C*
     C*
     C                   MOVE      'FR'          YFRUNI            2
     C                   MOVE      'O2Y'         YO2UNI            3
     C*
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        ZFIRMA
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W1KNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C*
