             PGM        PARM(&USER &ADR)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(8)
             DCL        VAR(&ADR) TYPE(*CHAR) LEN(8)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JUL) TYPE(*CHAR) LEN(3)
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCLF       FILE(E102CQ)
/*_______________________*/
/* Hente oversikt        */
/*"""""""""""""""""""""""*/
             WRKNETF    USER(&USER) OUTPUT(*OUTFILE) +
                          OUTFILE(E102CQ) OUTMBR(*FIRST +
                          *REPLACE)
/*______________________________*/
/* Leser oversikt over netfiler */
/*""""""""""""""""""""""""""""""*/
 START:      RCVF
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))
             IF         COND(&NFDFT *NE '*DTA   ' *OR &NFDUID *NE +
                          &USER *OR &NFDIDQ *NE &ADR) THEN(GOTO +
                          CMDLBL(START))
/*_____________*/
/* Lage ny MBR */
/*"""""""""""""*/
             RCVNETF    FROMFILE(&NFDFIL) TOFILE(E102C) +
                          FROMMBR(&NFDMBR) TOMBR(*FIRST) +
                          MBROPT(*REPLACE) NBR(&NFDNBR) USER(&USER)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(END))

 NYMBR:      CHGJOB     DATFMT(*JUL)
             RTVJOBA    DATE(&DATE)
             CHGJOB     DATFMT(*SYSVAL)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
             CHGVAR     VAR(&JUL) VALUE(%SST(&DATE 3 3))
             CHGVAR     VAR(&MEMBER) VALUE('N' *CAT &JUL *CAT &TIME)
             CPYF       FROMFILE(E102C) TOFILE(EDI79F) +
                          TOMBR(&MEMBER) MBROPT(*ADD) FMTOPT(*NOCHK)
             MONMSG     MSGID(CPF7306) EXEC(GOTO CMDLBL(NYMBR))

             GOTO       CMDLBL(START)
END:         ENDPGM
