             PGM
        /* Forutsetter at de 4 filene er manuellt hentet inn fra:   */
        /* http://www.skat.dk/download/told/f-7557a.rtf */
        /* http://www.skat.dk/download/told/f-7557b.rtf */
        /* http://www.skat.dk/download/told/f-7557c.rtf */
        /* http://www.skat.dk/download/told/f-7557d.rtf */
        /* og plassert i katalog: /&LIB/SKAT/   &LIB=Filbibliotek/katalog */
        /*___________________*/
        /* Declare variables */
        /*"""""""""""""""""""*/
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILEA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&FILEB) TYPE(*CHAR) LEN(256)
             DCL        VAR(&FILEC) TYPE(*CHAR) LEN(256)
             DCL        VAR(&FILED) TYPE(*CHAR) LEN(256)
        /*__________________*/
        /* Hent katalognavn */
        /*""""""""""""""""""*/
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
        /*_____________*/
        /* Lag filnavn */
        /*"""""""""""""*/
             CHGVAR     VAR(&FILEA) VALUE('/' *TCAT &LIB *TCAT +
                          '/SKAT/f-7557a.rtf')
             CHGVAR     VAR(&FILEB) VALUE('/' *TCAT &LIB *TCAT +
                          '/SKAT/f-7557b.rtf')
             CHGVAR     VAR(&FILEC) VALUE('/' *TCAT &LIB *TCAT +
                          '/SKAT/f-7557c.rtf')
             CHGVAR     VAR(&FILED) VALUE('/' *TCAT &LIB *TCAT +
                          '/SKAT/f-7557d.rtf')
        /*______________________________*/
        /* Tøm og/eller lag arbetsfiler */
        /*""""""""""""""""""""""""""""""*/
             CLRPFM     FILE(QTEMP/QA)
             MONMSG     MSGID(CPF0000)
             CLRPFM     FILE(QTEMP/QB)
             MONMSG     MSGID(CPF0000)
             CLRPFM     FILE(QTEMP/QC)
             MONMSG     MSGID(CPF0000)
             CLRPFM     FILE(QTEMP/QD)
             MONMSG     MSGID(CPF0000)

             CRTPF      FILE(QTEMP/QA) RCDLEN(1024)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/QB) RCDLEN(1024)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/QC) RCDLEN(1024)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/QD) RCDLEN(1024)
             MONMSG     MSGID(CPF0000)
        /*___________________________*/
        /* kopiere *IFS-filer til DB */
        /*"""""""""""""""""""""""""""*/
             CPYFRMSTMF FROMSTMF(&FILEA) +
                          TOMBR('/QSYS.LIB/QTEMP.LIB/QA.FILE/QA.MBR') +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF0000)
             CPYFRMSTMF FROMSTMF(&FILEB) +
                          TOMBR('/QSYS.LIB/QTEMP.LIB/QB.FILE/QB.MBR') +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF0000)
             CPYFRMSTMF FROMSTMF(&FILEC) +
                          TOMBR('/QSYS.LIB/QTEMP.LIB/QC.FILE/QC.MBR') +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF0000)
             CPYFRMSTMF FROMSTMF(&FILED) +
                          TOMBR('/QSYS.LIB/QTEMP.LIB/QD.FILE/QD.MBR') +
                          MBROPT(*ADD)
             MONMSG     MSGID(CPF0000)
        /*_____________________*/
        /* Konverter rare tegn */
        /*"""""""""""""""""""""*/
             OVRDBF     FILE(QX) TOFILE(QTEMP/QA)
             CALL       PGM(DKG201R1)
             OVRDBF     FILE(QX) TOFILE(QTEMP/QB)
             CALL       PGM(DKG201R1)
             OVRDBF     FILE(QX) TOFILE(QTEMP/QC)
             CALL       PGM(DKG201R1)
             OVRDBF     FILE(QX) TOFILE(QTEMP/QD)
             CALL       PGM(DKG201R1)
        /*______________________*/
        /* Tøm produksjonsfiler */
        /*""""""""""""""""""""""*/
             CALL       PGM(DKG201R2)
        /*____________________________________*/
        /* Kopiere arbetsfiler til produksjon */
        /*""""""""""""""""""""""""""""""""""""*/
             CPYF       FROMFILE(QTEMP/QA) TOFILE(DKTARA) +
                          MBROPT(*ADD) FMTOPT(*NOCHK)
             MONMSG     MSGID(CPF0000)
             CPYF       FROMFILE(QTEMP/QB) TOFILE(DKTARB) +
                          MBROPT(*ADD) FMTOPT(*NOCHK)
             MONMSG     MSGID(CPF0000)
             CPYF       FROMFILE(QTEMP/QC) TOFILE(DKTARC) +
                          MBROPT(*ADD) FMTOPT(*NOCHK)
             MONMSG     MSGID(CPF0000)
             CPYF       FROMFILE(QTEMP/QD) TOFILE(DKTARD) +
                          MBROPT(*ADD) FMTOPT(*NOCHK)
             MONMSG     MSGID(CPF0000)
        /*______________________________*/
        /* Reorganiser produksjonsfiler */
        /*""""""""""""""""""""""""""""""*/
             RGZPFM     FILE(DKTARA) KEYFILE(*FILE)
             MONMSG     MSGID(CPF0000)
             RGZPFM     FILE(DKTARB) KEYFILE(*FILE)
             MONMSG     MSGID(CPF0000)
             RGZPFM     FILE(DKTARC) KEYFILE(*FILE)
             MONMSG     MSGID(CPF0000)
             RGZPFM     FILE(DKTARD) KEYFILE(*FILE)
             MONMSG     MSGID(CPF0000)

             ENDPGM
