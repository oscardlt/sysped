     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R BILDE
     A                                      WINDOW(*DFT 12 73 *NOMSGLIN)
     A                                      CF12(12 'Avslutt')
     A*                                     BLINK
     A*                                     WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A*                                     ::.:'))
     A                                  1  2'SYTR33M'
     A                                      COLOR(BLU)
     A                                  1 12'Merking av oppdrag mottatt termina-
     A                                      l '
     A                                      COLOR(WHT)
     A                                  3  2'Bestilt via avd/oppd:'
     A            UAVD           4Y 0O  3 29COLOR(WHT)
     A                                      EDTCDE(4)
     A            UOPD           7Y 0O  3 35COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2'Gjelder alle i EDI snd.nr:'
     A            UEDSNR         7Y 0O  4 29COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 41'Ant oppdrag :'
     A            ANTOPD         4S 0O  4 55COLOR(WHT)
     A                                  6  2'Rutinen vil endre disponeringsstat-
     A                                      us fra E (eller V) til U.      '
     A                                  8  2'Også merke mottatt... '
     A            WSMOTT         1A  B  8 27
     A                                  8 29'(J=Ja) på term.'
     A            FITERM        25A  O  8 46COLOR(WHT)
     A                                  9 13'Dato......'
     A            WSDATO         8Y 0B  9 27EDTWRD('    .  .  ')
     A                                 10 13'Dato......'
     A            WSTID          4Y 0B 10 27EDTWRD('  :  ')
     A                                 12 13'Enter = Oppdater       '
     A                                      COLOR(BLU)
     A                                 12 43MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
