********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10017 * 01 YBC PTF10 SKILLE TET-EKSPORT OG FORHÅNDSVARSLING IMPORT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      *
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 65 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '4')
     A            THREFF        17   O  5  5
     A  50                                  DSPATR(BL)
     A  50                                  COLOR(PNK)
     A N50                                  COLOR(GRN)
     A            THUNIK         9S 0H
     A            THAVD          4S 0H
     A            THTDN          7S 0H
     A            THKNK          8  0O  5 23EDTCDE(4)
     A  50                                  DSPATR(BL)
     A  50                                  COLOR(PNK)
     A N50                                  COLOR(GRN)
     A            THNAK         30   O  5 32
     A  50                                  DSPATR(BL)
     A  50                                  COLOR(PNK)
     A N50                                  COLOR(GRN)
     A            THST           1   O  5 63
     A  50                                  DSPATR(BL)
     A  50                                  COLOR(PNK)
     A N50                                  COLOR(GRN)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'TR011DB'
     A                                      COLOR(BLU)
     A                                  1 10'Plukk tolldeklarasjon'
     A                                      COLOR(WHT)
     A                                  2  2'Velg opsjon:   1=Plukk   4=Slett'
     A                                      COLOR(BLU)
     A                                  3  2'Op Referanse         Avsender Navn-
     A                                       til mottaker              St'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2REFF        17A  B  4  5DSPATR(UL)
     A            W2KNK          8  0B  4 23DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2NAK         30A  B  4 32DSPATR(UL)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3/F12=Avslutt   F4=Søk   F5=Frisk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'TR011RB'
     A                                      COLOR(BLU)
S01  A*                                 1 10'Plukk TET'
N01  A                                  1 10'Plukk NCTS'
     A                                      COLOR(WHT)
     A                                  2  2'Velg opsjon:   1=Plukk   4=Slett'
     A                                      COLOR(BLU)
     A                                  3  2'Op Referanse         Avsender Navn-
     A                                       til mottaker              St'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2REFF        17A  B  4  5DSPATR(UL)
     A            W2KNK          8  0B  4 23DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2NAK         30A  B  4 32DSPATR(UL)
     A                                 15  2'F3/F12=Avslutt   F4=Søk   F5=Frisk'
     A                                      COLOR(BLU)
      *
     A          R BEKREFT
     A                                      WINDOW(*DFT 7 35 *NOMSGLIN)
     A                                      CA12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Plukking av tolldeklarasjon'
     A                                      COLOR(WHT)
     A                                  3  2'Denne transitteringen er allerede'
     A                                      COLOR(YLW)
S01  A*                                 4  2'plukket til TET'
N01  A                                  4  2'plukket til NCTS'
     A                                      COLOR(YLW)
     A            THAVD          4  0O  4 20EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  4 25'/'
     A                                      COLOR(YLW)
     A            THTDN          7  0O  4 27EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  4 33'.'
     A                                      COLOR(YLW)
     A                                  5  2'Vil du plukke denne en gang til?'
     A                                      COLOR(BLU)
     A                                  6  2'JA. trykk på ENTER.'
     A                                      COLOR(BLU)
     A                                  7  2'NEI. trykk på F12.'
     A                                      COLOR(BLU)
