     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLAVD0' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 68 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '5')
     A            KOAAVD         4Y 0O  5  5EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSANVN        20A  O  5 10
     A            WSNTO          7Y 0O  5 33EDTCDE(4)
     A            WSNT           7Y 0O  5 42EDTCDE(4)
     A            WSVKT          7Y 0O  5 51EDTCDE(4)
     A            WSCBM          7Y 2O  5 60EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYAV00 '
     A                                      COLOR(BLU)
     A                                  1 11'Totaloversikt udisponert gods på d-
     A                                      istrib.avd.'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option    2=Arb. m/turplanleg-
     A                                      ging   5=Se detaljer udisponert'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(027 YBC0239 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3 33'Ant.opd  Ant.Kll     Vekt  Kubikk '
     A                                      COLOR(WHT)
     A            W2KOD          4Y 0B  4  5DSPATR(UL)
     A            W2KODT         4Y 0B  4 10DSPATR(UL)
     A                                  4 15'(<-Fra/til avd.)'
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(17)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  1'F3=Avslutt'
     A                                      COLOR(BLU)
     A            WTNTO          7Y 0O  1 33EDTCDE(4)
     A                                      COLOR(WHT)
     A            WTNT           7Y 0O  1 42EDTCDE(4)
     A                                      COLOR(WHT)
     A            WTVKT          7Y 0O  1 51EDTCDE(4)
     A                                      COLOR(WHT)
     A            WTCBM          7Y 2O  1 60EDTCDE(4)
     A                                      COLOR(WHT)
     A  30        MSGTXT        60A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYAV00 '
     A                                      COLOR(BLU)
     A                                  1 11'Totaloversikt udisponert gods på d-
     A                                      istrib.avd.'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option    2=Arb. m/turplanleg-
     A                                      ging   5=Se detaljer udisponert'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(027 YBC0239 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 33'Ant.opd  Ant.Kll     Vekt  Kubikk '
     A                                      COLOR(WHT)
     A            W2KOD          4Y 0B  4  5DSPATR(UL)
     A            W2KODT         4Y 0B  4 10DSPATR(UL)
     A                                  4 15'(<-Fra/til avd.)'
     A                                 17  1'F3=Avslutt'
     A                                      COLOR(BLU)
     A            WTNTO          7Y 0O 17 33EDTCDE(4)
     A                                      COLOR(WHT)
     A            WTNT           7Y 0O 17 42EDTCDE(4)
     A                                      COLOR(WHT)
     A            WTVKT          7Y 0O 17 51EDTCDE(4)
     A                                      COLOR(WHT)
     A            WTCBM          7Y 2O 17 60EDTCDE(4)
     A                                      COLOR(WHT)
     A  30        MSGTXT        60A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
