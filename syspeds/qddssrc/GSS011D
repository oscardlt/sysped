********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10004 * 01 YBC PTF10 LEVERANDØRSNR ER ØKT TIL 5.0
10027 * 02 YBC PTF10 VIS ORDRETYPE
10029 * 03 YBC PTF10 NY MODUS FOR Å ENDRE DEL AV ORDRE
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R GSS011DA
     A N29                                  CF03(03 'AVSLUTT')
     A N29                                  CF04(04 'Søking')
     A                                      CF05(05 'TIL FAKT.GR.LAG')
     A N29                                  CF06(06 'Beregn kubikk')
     A                                      CF07(07 'REFERANSE')
S03  A*N29                                  CF08(08 'PENDING ORDRE')
N03  A N29N38                               CF08(08 'PENDING ORDRE')
     A                                      CF09(09 'DELLEVERING')
     A                                      CF10(10 'FLERE GODSBESK.')
     A                                      CF11(11 'STYKK PR ART.')
     A                                      CF12(12 'Avbryt')
     A                                      CF14(14 'Farlig gods')
     A                                      CF15(15 'MORE PONR')
S03  A*N29                                  CF16(16 'NESTE')
N03  A N29N38                               CF16(16 'NESTE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SHIPSYS/GSS011D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with orders'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A  31                              2  2'Status . . . . .:'
     A  31        WSST2          5A  O  2 22COLOR(YLW)
     A  31        XFTXTE        50A  O  2 28COLOR(YLW)
     A                                  3  2'Order No. . . . .:'
     A  27                              3 22'*New order*'
     A                                      COLOR(RED)
     A N27        ORONO          7Y 0O  3 22DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 31'Port'
     A            WSPORT         5   B  3 36
N03  A  29
N03  AON29 38                               DSPATR(PR)
N03  A  29
N03  AON29 38                               DSPATR(HI)
     A            WSPORD         6Y 0B  3 42EDTCDE(4)
N03  A  29
N03  AON29 38                               DSPATR(PR)
N03  A  29
N03  AON29 38                               DSPATR(HI)
     A                                  3 49'Stat'
     A            WSST           1A  O  3 54DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 56'Trip No.'
     A            WSTNO         11Y 0O  3 65DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2'Return(R)Strat(S):'
     A            WSRTNG         1A  B  4 22DSPATR(UL)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A                                      VALUES(' ' 'R' 'S')
N02  A                                  4 24'Type:'
N02  A            WSTYPE         1A  B  4 30DSPATR(UL)
N02  A  81                                  DSPATR(RI)
N02  A  29                                  DSPATR(PR)
N02  A  29                                  DSPATR(HI)
N02  A                                      VALUES(' ' 'C' 'T' 'X')
     A N32                              4 32'Sendingsno.'
     A N32        WSFBNR        20A  O  4 44COLOR(YLW)
     A            WSTNO2        11Y 0O  4 65DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2'Vessel  . . . . .:'
     A            WSBCD          5A  B  5 22DSPATR(UL)
     A  99                                  DSPATR(RI)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A            WSBNM         30A  O  5 34DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSTNO3        11Y 0O  5 65DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6  2'Clients No. . . .:'
     A            WSCNO          8Y 0B  6 22DSPATR(UL)
     A  98                                  DSPATR(RI)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A                                      EDTCDE(4)
     A            WSCNM         30A  O  6 34DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Supplier No.  . .:'
S01  A*           WSSNO          3Y 0B  7 22DSPATR(UL)
N01  A            WSSNO          5Y 0B  7 22DSPATR(UL)
     A  97                                  DSPATR(RI)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A                                      EDTCDE(4)
     A            WSSNM         30A  B  7 34DSPATR(UL)
     A  97                                  DSPATR(RI)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A                                  8  2'Partial delivery :'
     A            WSTXT1        50A  O  8 22DSPATR(UL)
     A                                      DSPATR(HI)
     A            WSPKD          1A  O  8 75DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9  2'Colli . . . . . .:'
     A            WSCLI          5Y 0B  9 22DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  9 32'Weight:'
     A            WSWT           7Y 1B  9 41DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  9 52'Volume:'
     A            WSVOL          5Y 2B  9 61DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 10  2'Goods description:'
     A            WSDSC         30A  B 10 22DSPATR(UL)
     A  93                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A  39                                  COLOR(RED)
     A                                 11  2'Location  . . . .:'
     A            WSLC           5A  B 11 22DSPATR(UL)
     A  92                                  DSPATR(RI)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A            WSLC2          5A  O 11 38DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSLC3          5A  O 11 45DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2'Date delivered  .:'
     A            WSDTL          6Y 0B 12 22DSPATR(UL)
     A  90                                  DSPATR(RI)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 12 29'(DDMMYY)'
     A            WSDTL2         6Y 0O 12 38DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSDTL3         6Y 0O 12 45DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12 53'Del. date:'
     A            WSDTWL         6Y 0B 12 64DSPATR(UL)
     A  89                                  DSPATR(RI)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 12 71'(DDMMYY)'
     A                                 13  2'Stockpositions  .:'
     A            WSPOS1         5A  B 13 22DSPATR(UL)
     A  91                                  DSPATR(RI)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A            WSPOS2         5A  B 13 28DSPATR(UL)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A            WSPOS3         5A  B 13 34DSPATR(UL)
E03  A  29
E03  AON29 38                               DSPATR(PR)
E03  A  29
E03  AON29 38                               DSPATR(HI)
     A                                 13 44'Ord.type:'
     A            WSOT           2A  B 13 54DSPATR(UL)
     A  85                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                 13 58'Del. way:'
     A            WSFR           3A  B 13 68DSPATR(UL)
     A  84                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                 14  2'Value . . . . . .:'
     A            WSAMT         11Y 2B 14 22DSPATR(UL)
     A  86                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A            WSCURC         3A  B 14 36DSPATR(UL)
     A  88                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                 15  2'Clients order No.:'
     A            WSCON1        20A  B 15 22DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCON2        20A  B 15 44DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCON3        20A  B 16 22DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCON4        20A  B 16 44DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCON5        20A  B 17 22DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCON6        20A  B 17 44DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                 18  2'Free text . .:'
     A            WSFTCD         2A  B 18 18DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                 18 21'St.Rap/Pick.Note/P.List/Inv./I.Bas-
     A                                      is'
     A            WSKDL1         2A  B 18 58DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      VALUES('  ' 'PN' 'PL' 'PI' 'IB' 'SR-
     A                                      ')
     A            WSKDL2         2A  B 18 61DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      VALUES('  ' 'PN' 'PL' 'PI' 'IB' 'SR-
     A                                      ')
     A            WSKDL3         2A  B 18 64DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      VALUES('  ' 'PN' 'PL' 'PI' 'IB' 'SR-
     A                                      ')
     A                                 18 67'SR/PN/PL/PI/IB'
     A            WSFT1         30A  B 19 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSFT2         30A  B 19 50DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSFT3         30A  B 20 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSFT4         30A  B 20 50DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                 21  2'Colli No. from/to:'
     A            WSCLNF         3Y 0B 21 22DSPATR(UL)
     A  87                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A            WSCLNT         3Y 0B 21 28DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 21 45'F15=More PO'
     A                                      COLOR(BLU)
     A                                 21 60'F7=Ref.'
     A                                      COLOR(BLU)
     A                                 21 70'F11=Art.'
     A                                      COLOR(BLU)
     A                                 22  2'Print labels  . .:'
     A            WSPRL          1A  B 22 22DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A                                      VALUES(' ' 'N' 'Y')
     A                                 22 30'(Y/N)'
     A                                 22 40'Reg. date:'
     A            WSDTR          6Y 0O 22 51COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 58'F14=D.goods'
     A                                      COLOR(BLU)
     A                                 22 70'F9=Partial'
     A                                      COLOR(BLU)
S03  A*N29                             23  2'F3=End F4=Qry F5=Inv.bas. F6=Calc.-
N03  A N29N38                          23  2'F3=End F4=Qry F5=Inv.bas. F6=Calc.-
     A                                       vol. F8=Pending F10=Goods desc. F1-
     A                                      6=Neste'
     A                                      COLOR(BLU)
N03  A N29 38                          23  2'F3=End F4=Qry F5=Inv.bas. F6=Calc.-
N03  A                                       vol. F10=Goods desc.'
N03  A                                      COLOR(BLU)
     A  29                             23  2'F5=Inv.basis  F10=Goods desc.  F12-
     A                                      =Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R GSS011DB
     A                                      WINDOW(*DFT 6 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print labels'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRTM        10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'Barcode     :'
     A            WSSK           1A  B  4 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  4 21'(Y/N)'
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99        WSMSGN        25A     6  2MSGID(GSS0042 SYMSGF)
      *
     A          R GSS011DC
     A                                      WINDOW(*DFT 5 40 *NOMSGLIN)
     A                                      CF08(08 'VIS ALLE')
     A                                      OVERLAY
     A                                  1  2'Following P.O.no already'
     A                                      COLOR(WHT)
     A                                  2  2'registered'
     A                                      COLOR(WHT)
     A N99                              2 13'(partial)'
     A                                      COLOR(WHT)
     A  99                              2 13'(complete)'
     A                                      COLOR(WHT)
     A            WSCON         20A  O  3  2DSPATR(UL)
     A                                      COLOR(RED)
     A            ORONO          7  0O  3 23DSPATR(UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A                                  4  2'Keep P.O.no?'
     A            WSOK           1A  B  4 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      VALUES('Y' 'N')
     A                                  4 21'(Y/N)'
     A                                  5  2'F8=Display orders with same P.O.no-
     A                                      .'
     A                                      COLOR(BLU)
      *
     A          R GSS011DD
     A                                      CF12(12 'Avbryt')
     A                                  1  2'SHIPSYS/GSS011D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with orders'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 72TIME
     A                                      COLOR(BLU)
     A                                  2  2'Order No. . . . .:'
     A                                      COLOR(RED)
     A            ORONO          7Y 0O  2 22DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  1'Clents order no:'
     A            WSCON1        20A  B  3 18DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A            WSCON2        20A  B  3 39DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A            WSCON3        20A  B  3 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PR)
     A            WSCON4        20A  B  4 18DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A            WSCON5        20A  B  4 39DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A            WSCON6        20A  B  4 60DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A            WSCON7        20A  B  5 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCON8        20A  B  5 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCON9        20A  B  5 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO10        20A  B  6 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO11        20A  B  6 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO12        20A  B  6 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO13        20A  B  7 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO14        20A  B  7 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO15        20A  B  7 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO16        20A  B  8 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO17        20A  B  8 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO18        20A  B  8 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO19        20A  B  9 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO20        20A  B  9 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO21        20A  B  9 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO22        20A  B 10 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO23        20A  B 10 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO24        20A  B 10 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO25        20A  B 11 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO26        20A  B 11 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO27        20A  B 11 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO28        20A  B 12 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO29        20A  B 12 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO30        20A  B 12 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO31        20A  B 13 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO32        20A  B 13 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO33        20A  B 13 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO34        20A  B 14 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO35        20A  B 14 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO36        20A  B 14 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO37        20A  B 15 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO38        20A  B 15 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO39        20A  B 15 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO40        20A  B 16 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO41        20A  B 16 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO42        20A  B 16 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO43        20A  B 17 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO44        20A  B 17 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO45        20A  B 17 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO46        20A  B 18 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO47        20A  B 18 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO48        20A  B 18 60DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO49        20A  B 19 18DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A            WSCO50        20A  B 19 39DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  DSPATR(HI)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
