     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            HEVS1         25A  H
     A            HEVS2         25A  H
     A            HEGM1         15A  H
     A            HEGM2         15A  H
     A            WSNR           1A  I  8  2VALUES(' ' '1' '4' '5' '9')
     A            WSTSTA         1A  I  8  4VALUES(' ' '1' '2' '3' '4')
     A            WSMED          1A  O  8  6DSPATR(BL)
     A                                      COLOR(RED)
     A            WSTXT         50A  H
     A            WSFT           1A  O  8  8DSPATR(BL)
     A                                      COLOR(RED)
     A            TRTSTA         1A  O  8 10DSPATR(BL)
     A  90                                  COLOR(RED)
     A N90 85                               COLOR(WHT)
     A N90N85                               COLOR(GRN)
     A            HEAVD          4Y 0O  8 12EDTCDE(4)
     A            HEOPD          7Y 0O  8 17EDTCDE(4)
     A            WSTLE          6A  B  8 25
     A            WSTLL         10A  B  8 32
     A            WSTLKU         1A  B  8 43VALUES(' ' 'X')
     A            HEST           1A  O  8 45
     A            HEOT           2A  O  8 47
     A            WSSDF         10A  O  8 50
     A            WSNAS         19A  O  8 61
     A            WSNAFA        13A  O  9  3DSPATR(UL)
     A  90                                  COLOR(RED)
     A N90 86                               COLOR(WHT)
     A N90N86                               COLOR(GRN)
     A            HENT           7Y 0O  9 17EDTCDE(4)
     A                                      DSPATR(UL)
     A            HEVKT          9Y 0O  9 25EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSVS1          8A  O  9 35DSPATR(UL)
     A            HEFR           3A  O  9 44DSPATR(UL)
     A            WSSDT         10A  O  9 48DSPATR(UL)
     A            WSNAK         21A  O  9 59DSPATR(UL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(1000)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/TR019R'
     A                                      COLOR(BLU)
     A                                  1 23'Plukk oppdrag til transittering'
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            ZRECNO         4S 0O  1 77SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Liste:'
     A            UDK            5A  O  2 10COLOR(WHT)
     A                                  2 20'Tur:'
     A            UAVD           4Y 0O  2 25EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 30'/'
     A                                      COLOR(WHT)
     A            UPRO           8Y 0O  2 32EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 41'St'
     A            TUST           1A  O  2 44COLOR(WHT)
     A                                  2 48'Bilnr'
     A            TUBILN        10A  O  2 55COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2'Transp:'
     A            TUKNT2         8Y 0O  3 10EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  3 20COLOR(WHT)
     A                                  3 55'Fra:'
     A            TUSDF         20A  O  3 60COLOR(WHT)
     A                                  4  2'F.agen'
     A            W1KNFA         8Y 0O  4 10EDTCDE(4)
     A                                      COLOR(WHT)
     A            W1NAFA        30A  O  4 20COLOR(WHT)
     A                                  4 55'Til:'
     A            TUSDT         20A  O  4 60COLOR(WHT)
     A                                  5  2'Velg opsjon:  1=Plukke 4=Slette 5=-
     A                                      Vise 9=Fritekst'
     A                                      COLOR(BLU)
     A                                  6  2'Ops M F T Avd. Opd.nr. Eksp.enh/Lø-
     A                                      penr KUN S OT Fra sted.. Avsender..-
     A                                      .........'
     A                                      COLOR(WHT)
     A                                  7  2' Fort.agent... Antall. Vekt..... V-
     A                                      areslag Fra Til sted.. Motteger....-
     A                                      .........'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'Frisk')
     A                                      CF07(07 'Utfør plukk')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22'F7=Utfør plukk'
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'FORRIGE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/TR019R'
     A                                      COLOR(BLU)
     A                                  1 23'Plukk oppdrag til transittering'
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Liste:'
     A            UDK            5A  O  2 10COLOR(WHT)
     A                                  2 20'Tur:'
     A            UAVD           4Y 0O  2 25EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 30'/'
     A                                      COLOR(WHT)
     A            UPRO           8Y 0O  2 32EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 41'St'
     A            TUST           1A  O  2 44COLOR(WHT)
     A                                  2 48'Bilnr'
     A            TUBILN        10A  O  2 55COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2'Transp:'
     A            TUKNT2         8Y 0O  3 10EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  3 20COLOR(WHT)
     A                                  3 55'Fra:'
     A            TUSDF         20A  O  3 60COLOR(WHT)
     A                                  4  2'F.agen'
     A            W1KNFA         8Y 0O  4 10EDTCDE(4)
     A                                      COLOR(WHT)
     A            W1NAFA        30A  O  4 20COLOR(WHT)
     A                                  4 55'Til:'
     A            TUSDT         20A  O  4 60COLOR(WHT)
     A                                  5  2'Velg option:  1=Plukke 4=Slette 5=-
     A                                      Vise'
     A                                      COLOR(BLU)
     A                                  6  2'Ops M F T Avd. Opd.nr. Eksp.enh/Lø-
     A                                      penr KUN S OT Fra sted.. Avsender..-
     A                                      .........'
     A                                      COLOR(WHT)
     A                                  7  2' Fort.agent... Antall. Vekt..... V-
     A                                      areslag Fra Til sted.. Motteger....-
     A                                      .........'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 58MSGCON(022 DIV2306 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
      *
     A          R BILDE01
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/TR019R'
     A                                      COLOR(BLU)
     A                                  1 23'Plukk oppdrag til transittering'
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Liste:'
     A            UDK            5A  O  2 10COLOR(WHT)
     A                                  2 20'Tur:'
     A            UPRO           8Y 0O  2 25EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 41'St'
     A            TUST           1A  O  2 44COLOR(WHT)
     A                                  2 48'Bilnr'
     A            TUBILN        10A  O  2 55COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2'Transp:'
     A            TUKNT2         8Y 0O  3 10EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  3 20COLOR(WHT)
     A                                  3 55'Fra:'
     A            TUSDF         20A  O  3 60COLOR(WHT)
     A                                  4 55'Til:'
     A            TUSDT         20A  O  4 60COLOR(WHT)
     A                                  7  2'Tast inn utvalgskriterium for tran-
     A                                      sittliste:'
     A                                      COLOR(WHT)
     A                                 11  2'Kun oppdrag for fortollingsagent..-
     A                                      ..'
     A            W1KNFA         8Y 0B 11 40CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  87                                  DSPATR(RI)
     A  87                                  DSPATR(PC)
     A                                 11 51'(0 = Alle)'
     A                                 15  2'Vis også pos. med skrevet T-dok...-
     A                                      ..'
     A            W1ALL          1   B 15 40TEXT('Y/N - vis også de med status -
     A                                      T-dok skrevet')
     A                                      VALUES(' ' 'N' 'J' 'Y')
     A                                 15 51'(J/N)'
     A                                 23  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                  2  2'FRITEKST  -->'
     A            WSTXT         50A  B  2 16DSPATR(UL)
     A                                      DSPATR(PC)
