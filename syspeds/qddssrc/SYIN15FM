     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Beregn verdier på fraktbre-
     A                                      v på nytt')
     A                                  2 57'Copyright: SYSTEMA A/S'
     A                                      COLOR(TRQ)
     A                                  4 22'R E G I S T R E R I N G'
     A                                      COLOR(WHT)
     A                                  5 25'A V   M A S T E R'
     A                                      COLOR(WHT)
     A                                  6 22'INNENLANDS FLYFRAKTBREV'
     A                                      COLOR(WHT)
     A                                 12 22'Tast samlastnr.'
     A            WSSD           3A  B 12 39DSPATR(UL)
     A                                      COLOR(YLW)
     A  99                                  ERRMSG('Tastet samlastnr. finnes ik-
     A                                      ke.' 99)
     A            WSLNR          3Y 0B 12 44DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                 23 16'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                 23 28'F5=Beregn verdier på fraktbrev på -
     A                                      nytt'
     A                                      COLOR(BLU)
     A          R BILDE01
     A                                      CF03(03 'Avslutt med oppdatering')
     A                                      CF04(04 'Søke selger')
     A                                      CF12(12 'Avbryt')
     A                                  1 10'Innenlands flyfraktbrev'
     A                                      COLOR(WHT)
     A                                  1 41'Samlastnr.'
     A            WSSD           3A  O  1 52DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSLNR          3Y 0O  1 56DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  1 62'AWBnr'
     A            WSAWB         11S 0O  1 68DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2  2'Status'
     A            WSST           1A  B  2 10TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2'Fra'
     A            WSFRA          5A  O  3 10TEXT('FRA-DESTINASJON')
     A                                      DSPATR(UL)
     A  99                                  ERRMSG('Fra flyplass finnes ikke i -
     A                                      stedsregister.' 99)
     A                                      COLOR(WHT)
     A                                  3 17'Til'
     A            WSTIL          5A  O  3 21TEXT('TIL-DESTINASJON')
     A                                      DSPATR(UL)
     A  98                                  ERRMSG('Til flyplass finnes ikke i -
     A                                      stedsregister.' 98)
     A                                      COLOR(WHT)
     A                                  3 27'Routing'
     A            WSRUNR         6A  O  3 35TEXT('RUNR')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDAG1         2Y 0B  3 42TEXT('DAG1')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  3 45'/'
     A            WSRUN2         6A  B  3 47TEXT('RUN2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDAG2         2Y 0B  3 54TEXT('DAG2')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  3 57'/'
     A            WSRUN3         6A  B  3 59TEXT('RUN3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDAG3         2Y 0B  3 66TEXT('DAG3')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  3 69'CC/PP'
     A            WSCCPP         2A  O  3 75TEXT('CCPP')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2'Flysel'
     A            WSPREF         3Y 0O  4 10TEXT('PREF')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  97                                  ERRMSG('Tastet flyselskapsnr. er ik-
     A                                      ke registrert.' 97)
     A                                      COLOR(WHT)
     A            WSFNAV        30A  O  4 15TEXT('FNAV')
     A                                      COLOR(WHT)
     A                                  5  2'Selger'
     A            WSSKNR         8Y 0B  5 10TEXT('SKNR')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  96                                  ERRMSG('Tastet selgersnummer er ugy-
     A                                      ldig.' 96)
     A                                      COLOR(YLW)
     A            WSSNAV        30A  B  5 19TEXT('SNAV')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSAD1        30A  B  5 50TEXT('SAD1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSAD3        30A  B  6 50TEXT('SAD3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSAD2        30A  B  6 19TEXT('SAD2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSTLF        15A  B  7 19TEXT('STLF')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 10'Tlfnr'
     A                                  8  2'Kjøper'
     A            WSKNAV        30A  B  8 19TEXT('KNAV')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKAD1        30A  B  8 50TEXT('KAD1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKAD2        30A  B  9 19TEXT('KAD2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKAD3        30A  B  9 50TEXT('KAD3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 10'Tlfnr'
     A            WSKTLF        15A  B 10 19TEXT('KTLF')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'Antall  Bvekt Vareslag Dimensjoner-
     A                                       etc.                    Fvekt Tari-
     A                                      f Pris/kg'
     A            WSANT          7Y 0B 13  2TEXT('ANT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVEKT         5Y 0B 13 10TEXT('VEKT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSSLAG        20A  B 13 16TEXT('SLAG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVSL2        20A  B 13 38TEXT('VSL2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFVKT         5Y 0B 13 61TEXT('FVKT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSTARI         1A  B 13 69TEXT('TARI')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPRIS         5Y 2B 13 73TEXT('PRIS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 15  2'Anmerk'
     A            WSMERK        50A  B 15 10TEXT('MERK')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  2'Fors.b'
     A            WSFORS         9Y 0B 16 10TEXT('FORS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 16 22'Bokst'
     A            WSFORA        25A  B 16 28TEXT('FORA')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  2'Frakt'
     A            WSFRAK         7Y 0B 17 10TEXT('FRAK')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 17 35'Ved V i tariff påplusses Wiederø m-
     A                                      inimumspris'
     A                                      COLOR(WHT)
     A                                      DSPATR(ND)
     A                                 18  2'FS-serv'
     A            WSFS           7Y 0B 18 10TEXT('FS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 18 35'Ved M i tariff benyttes inntastet -
     A                                      pris/kg'
     A                                      COLOR(WHT)
     A                                 19  2'Forsik'
     A            WSFPRE         7Y 0B 19 10TEXT('FPRE')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 20  2'Vå.geb'
     A            WSVGEB         7Y 0O 20 10TEXT('VGEB')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 20 19'Moms'
     A            WSMVA          9Y 2O 20 24TEXT('MVA')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22  2'F3=Avslutt med oppdatering'
     A                                      COLOR(BLU)
     A                                 22 30'F4=Søk selger'
     A                                      COLOR(BLU)
     A                                 22 45'F12=Forrige'
     A                                      COLOR(BLU)
     A            UMSG          62A  O 23  2COLOR(WHT)
     A                                 14  2MSGCON(007 TRO0504 *LIBL/SYMSGF)
     A            WSFT          41A  B 14 10TEXT('MERK')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
