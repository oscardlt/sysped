      *********************************************************************
      * KOPI AV ARCA001R. KALLT AV ARCA001CL                              *
      * Agent som kobler alle dok. i gitt mappe til arkiv                 *
      *       Innparameter: Mappenavn (full path)                         *
      *                     Programnavn (som skal viderebehandle poster)  *
      *********************************************************************
     H DFTACTGRP(*NO)
     FFRISOL01  IF   E           K DISK
     D PGM             S             10                                         ARCA022R
     D PGM2            S             10                                         ARCA024R
     D XPOS            S              3S 0
      *
      *_____________________________
      * Prototype for API prosedyrer
      *"""""""""""""""""""""""""""""
     Dlstat            PR            10I 0 EXTPROC('lstat')
     D                                 *   VALUE
     D                                 *   VALUE

     Dopendir          PR              *   EXTPROC('opendir')
     D                                 *   VALUE

     Dreaddir          PR              *   EXTPROC('readdir')
     D                                 *   VALUE

     Dclosedir         PR            10I 0 EXTPROC('closedir')
     D                                 *   VALUE
     D SndPgmMsg       PR              N
     D Qmsgid                         7    CONST
     D Qmsgf                         20    CONST
     D Qmsg                         128    CONST
     D Qmsgtp                        10    CONST OPTIONS(*NOPASS)

      *   stat data structure fra prosedyre: lstat()
     D StatDS          DS           128
     D  st_mode                      10U 0
     D  st_ino                       10U 0
     D  st_nlink                      5U 0
     D  reserved1                     2A
     D  st_uid                       10U 0
     D  st_gid                       10U 0
     D  st_size                      10U 0
     D  st_atime                     10U 0
     D  st_mtime                     10U 0
     D  st_ctime                     10U 0
     D  st_dev                       10U 0
     D  st_blksize                   10I 0
     D  st_allocsize                 10I 0
     D  st_objtype                   10A
     D  reserved2                     2A
     D  st_codepage                   5U 0
     D  st_reserved1                 62A
     D  st_ino_gen_id                10U 0

      *   direntry data structure fra prosedyre: readdir()
     D DirEntry        DS
     D d_reserved1                   16A
     D d_fileno_genid                10U 0
     D d_fileno                      10U 0
     D d_reclen                      10U 0
     D d_reserved3                   10I 0
     D d_reservedØ4                   6A
     D d_reserved5                    2A
     D d_ccsid                       10I 0
     D d_country_id                   2A
     D d_language_id                  3A
     D d_nls_reserved                 3A
     D d_namelen                     10U 0
     D d_name                       640A

     D Null            S              1A   Inz(X'00')
     D RETURNInt       S             10I 0
     D RETURNDir       S               *
     D PtrToEntry      S               *
     D RtnEntry        S                   BASED(PtrToEntry) Like(DirEntry)
     D EntryName       S            512A
     D EntryLen        S             10I 0
     D NewName         S            120A
     D EntryPath       S            256A
     D CmdLine         S            512
     D CmdLen          S             15  5
     D HHMMSS          S              6  0
     D DirError        C                   'Feil ved åpning av katalog'
     D Xrefstart       C                   'ZipAttachments_'

      *   Input Parametere
     D DirName         S            100A
     D DirName2        S            100A
     D FullName        S            256A

      *
     D ObjVar          S             90
     D ObjVarLen       S             10I 0 Inz(%size(ObjVar))
     D ObjVarFmt       S              8
     D ObjTyp          S             10

     D APIERR          DS
     D  ERRSIZ                 1      4B 0 INZ(256)
     D  ERRLEN                 5      8B 0 INZ(0)
     D  ERRMIC                 9     15
     D  ERRNBR                16     16
     D  ERRDTA                17    272

     D PSDS           SDS
     D  JobNbr                        6A   Overlay(PSDS:264)
     D  JobName                      10A   Overlay(PSDS:244)
     D  JobUser                      10A   Overlay(PSDS:254)

      *
     C                   EXSR      INIT

      * HOVEDRUTINE

      * Vis liste over filer i katalogbane (gå i løkke)
     C                   EXSR      LESKAT

     C                   EVAL      *inlr = *on
     C                   RETURN
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C     *entry        PLIST
     C                   PARM                    DirName
     C                   PARM                    PGM
      *
     C     Qcmdexc       PLIST
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen
     C     FRISOL01K     KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
      *
     C                   EVAL      FSKODE = 'ORD'
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les fra katalog og legg ut oversikt               LESKAT /
      *////////////////////////////////////////////////////////////
     C     LESKAT        BEGSR
     C                   EVAL      FullName = %trimr(DirName) + Null
     C     ' '           SCAN      FullName      XSTART0           3 0    33
     C  N33              Z-ADD     1             XSTART0
      * Åpne katalog
     C                   EVAL      RETURNDir = opendir(%addr(FullName))
      * Avslutt hvis feil ved åpning av katalog
     C                   IF        RETURNDir = *Null
     C                   CALLP     SndPgmMsg('CPF9898':'QCPFMSG'
     C                                       :DirError)
     C                   EVAL      *INLR = *ON
     C                   RETURN
     C                   ENDIF

     C                   DOU       PtrToEntry = *Null
      * Les neste katalog lenke
     C                   EVAL      PtrToEntry = readdir(RETURNDir)
      * Lenknavn er i feltet d_name
     C                   IF        PtrToEntry <> *Null
     C                   EVAL      DirEntry = RtnEntry
      * Hent katalog lenknavn
     C                   EVAL      EntryName = %str(%addr(d_name))
      * Vilken typ av lenke ?
     C                   EVAL      EntryPath = %trim(DirName) + '/'
     C                             + %trimr(EntryName) + Null
     C                   EVAL      RETURNInt = lstat(%addr(EntryPath)
     C                                         : %addr(StatDS))
      * Funnet gyldig fil - kall behandlingsprogram
     C                   IF        st_objtype='*STMF'
      *
      * FILEN KAN IKKE INNEHOLDE ' DA DUMPER RENAME
      *
     C     X'7D'         SCAN      EntryPath                              33
     C                   IF        *IN33 = *OFF
     C     '.zip'        SCAN      EntryPath     XPOS                     34
      * Finn startpos. for referanse i zip-filnavn.
     C   34XREFSTART     SCAN      EntryPath     XSTART            3 0    34
      * Add 15 er pga. 'ZipAttachments_' foran referanse
     C   34              ADD       15            XSTART
      *
     C                   IF        *IN34
      * SJEKK OM REFERANSE FINNES I FRISØKEVEI. HVIS IKKE -> IKKE UNZIP
     C*                  Z-ADD     XSTART0       XSTART            3 0
     C                   EVAL      FSSOK = %SUBST(EntryPath:XSTART:XPOS-XSTART)
     C     FRISOL01K     CHAIN     FRISOL01                           33
     C   33              EVAL      *IN34 = *OFF
     C                   END
      *
     C                   IF        *IN34
      * ZIP-fil --> Videre med unzip
     C                   EVAL      *IN33 = *OFF
     C                   ELSE
     C                   EVAL      *IN33 = *ON
     C                   END
     C                   END
      *
     C                   IF        *IN33=*off
     C                   EVAL      EntryPath = %trim(DirName) + '/'
     C                             + %trimr(EntryName)
     C                   EVAL      EntryLen = %len(%trim(Entryname))
     C                   IF        EntryLen > 120
     C                   EXSR      FixFilNavn
     C                   ENDIF
     C                   CALL      PGM
     C                   PARM                    DirName
     C                   PARM                    EntryName
     C                   PARM                    DirName2
      *
     C                   EVAL      PGM2 = 'ARCA024R'
     C                   MOVE      FSAVD         UAVD
     C                   MOVE      FSOPD         UOPD
     C                   CALL      'ARCA023R'                                   KOPI AV ARCA001R
     C                   PARM                    DirName2
     C                   PARM                    PGM2
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
      *
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDDO
      * Lukk katalog
     C                   EVAL      RETURNInt = closedir(RETURNDir)
      *
     C                   ENDSR
      *
      **************************************************************
     C     FixFilNavn    BEGSR
      **************************************************************
      *
     C                   EVAL      NewName = %subst(Entryname:1:100)+'__'
     C                             + %subst(Entryname:EntryLen-17:18)
     C                   eval      CmdLine = 'REN OBJ(' + X'7D'
     C                             + %trim(EntryPath) + X'7D' + ')'
     C                             + ' NEWOBJ(' + X'7D'
     C                             + %trim(NewName) + X'7D' + ')'
     C                   eval      CmdLen  = 512
     C                   CALL      'QCMDEXC'
     C                   PARM                    CmdLine
     C                   PARM                    CmdLen
     C                   EVAL      EntryName = %trim(NewName)
     C                   EVAL      EntryPath = %trim(DirName) + '/'
     C                             + %trimr(EntryName)
     C                   ENDSR
      *-------------------------------------------------------------------------
      * Send pgm message
      *-------------------------------------------------------------------------
     P SndPgmMsg       B
     D                 PI              N
     D Msgid                          7    CONST
     D MsgF                          20    CONST
     D Msgdta                       128    CONST
     D Msgtp                         10    CONST OPTIONS(*NOPASS)
      * Work variables
     D Qmsgid          S              7
     D Qmsgf           S             20
     D Qmsgdta         S            128
     D Qmsgln          S             10I 0
     D Qmsgtp          S             10
     D Qmsgq           S             10
     D Qmsgqn          S             10I 0 INZ(3)
     D Qmsgky          S              4
     D Qmsger          S             15
      * Hvis bibliotek er lik blank set in *LIBL
     C                   EVAL      Qmsgid = Msgid
     C                   EVAL      Qmsgf = Msgf
     C                   EVAL      Qmsgdta = Msgdta
     C                   IF        %subst(Qmsgf:11:10) = *blank
     C                   EVAL      %subst(Qmsgf:11:10) = '*LIBL'
     C                   ENDIF
     C                   EVAL      Qmsgln = %len(%trim(Qmsgdta))
     C                   EVAL      Qmsgq = '*'
     C                   EVAL      Qmsgtp = '*DIAG'
     C                   IF        %PARMs > 3
     C                   EVAL      Qmsgtp = Msgtp
     C                   ENDIF
     C                   IF        Qmsgtp = '*STATUS'
     C                   EVAL      Qmsgq = '*EXT'
     C                   ENDIF
      *
     C                   CALL      'QMHSNDPM'                           99
     C                   PARM                    Qmsgid
     C                   PARM                    Qmsgf
     C                   PARM                    Qmsgdta
     C                   PARM                    Qmsgln
     C                   PARM                    Qmsgtp
     C                   PARM                    Qmsgq
     C                   PARM                    Qmsgqn
     C                   PARM                    Qmsgky
     C                   PARM      *LOVAL        Qmsger
      *
     C                   RETURN    *ON
     P                 E
