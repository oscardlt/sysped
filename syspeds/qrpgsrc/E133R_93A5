     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FEGID93A   IF   E           K DISK
     FEMEA93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FTELL      UF   E           K DISK
     FHEADF     O  A E             DISK
     FDOKUF     O  A E             DISK
     FDOK29     O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  M1004                  1     35
     D  W1004A                 1      4
     D  W1004B                 5      7
     D  W1004C                 8     10
     D                 DS
     D  N3164                  1     35
     D  N3164A                 1      2
     D  N3164B                 3     35
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      * DTM00                                               DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   EVAL      *IN51=*OFF
     C                   EVAL      ZGN0=0
     C     KEYMS0        SETLL     EDTM93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     D2005         IFEQ      '200'
     C                   MOVEL     D2380         HEDTR
     C                   MOVEL     D2380         HEDTOP
     C                   ENDIF
      *
     C  N51KEYMS0        READE     EDTM93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX00                                               FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EFTX93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     F4451         IFEQ      'SIC'
     C                   MOVEL     F4440A        HEVS2
     C                   MOVEL     F4440A        D29TXT
     C                   ENDIF
      *
     C  N51KEYMS0        READE     EFTX93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GID16                                               GID16 /
      *////////////////////////////////////////////////////////////
     C     GID16         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     16            ZGN0
     C     KEYMS0        SETLL     EGID93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EGID93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     G1496         IFEQ      '1'
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          HENT
     C                   ADD       ZNUM          DFNTEU
     C                   ADD       ZNUM          DFNT
     C                   ADD       ZNUM          DFNTLA
     C                   MOVEL     G7064A        HEVS1
     C     G7064A        IFNE      'EUR'
     C     HEVS1         CAT       'Drikkevare':1HEVS1
     C     HEVS1         CAT       'r':0         HEVS1
     C                   EVAL      DFNTEU=0
     C                   ENDIF
     C                   MOVEL     HEVS1         DFVS
     C                   ELSE
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C                   MOVEL     G7224A        DFVS2
     C     DFVS2         CAT       'Krt':1       DFVS2
     C     DFVS2         CAT       'Drikkevare':1DFVS2
     C     DFVS2         CAT       'r':0         DFVS2
     C                   ENDIF
      *
     C                   Z-ADD     GREP0         ZREPB0
     C                   EXSR      MEA19
      *
     C     KEYMS0        READE     EGID93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    R1154         W1154A
     C     *LIKE         DEFINE    R1154         W1154B
     C                   MOVE      '0'           ZVS               1
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA19                                               MEA19 /
      *////////////////////////////////////////////////////////////
     C     MEA19         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     19            ZGN1
     C     KEYMS1        SETLL     EMEA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C                   Z-ADD     ZNUM          HEVKT
     C                   Z-ADD     ZNUM          DFVKT
     C     M6311         WHENEQ    'VOL'
     C                   Z-ADD     ZNUM          HEM3
     C                   Z-ADD     ZNUM          DFM3
     C                   ENDSL
      *
     C  N52KEYMS1        READE     EMEA93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD10                                               NAD10 /
      *////////////////////////////////////////////////////////////
     C     NAD10         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CN'                                         MOTTAKER
     C                   MOVEL     N3124A        HENAKF
     C                   MOVEL     N3124A        HENAK
     C                   MOVEL     N3124A        HENAKF
     C                   MOVEL     N3124B        HEADK1
     C                   MOVEL     N3124C        HEADK2
     C                   MOVEL     N3251         HEADK3
     C     N3251         CAT       N3164:2       HEADK3
     C                   MOVEA     *BLANKS       A
     C     5             SUBST     N3124A:3      W3039             5
     C                   MOVEA     W3039         A
     C                   EXSR      GETNUM
     C                   ADD       99900000      ZNUM
     C                   Z-ADD     ZNUM          HEKNK
     C                   Z-ADD     ZNUM          HEKNKF
      * FOR DOKUF
     C                   Z-ADD     HEKNK         DFKNSM
     C                   MOVEL     HEKNK         DFKNM
     C                   MOVEL     HENAK         DFNAVM
     C                   MOVEL     HEADK1        DFAD1M
     C                   MOVEL     HEADK3        DFAD3M
     C                   MOVEL     'NO'          DFLKM
     C                   MOVEL     HENAK         DFNAVL
     C                   MOVEL     HEADK1        DFAD1L
     C                   MOVEL     HEADK2        DFAD2L
     C                   MOVEL     N3251         DFPOUL
     C                   MOVEL     N3164         DFADUL
     C                   MOVEL     'NO'          DFLKL
     C                   ENDSL
      *
     C     KEYMS0        READE     ENAD93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF03                                               RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     3             ZGN0
     C     KEYMS0        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     R1153         WHENEQ    'CU '
     C     W1154A        IFEQ      *BLANKS
     C                   MOVEL     R1154         W1154A
     C                   MOVEL     R1154         DFGM
     C                   ELSE
     C     W1154B        IFEQ      *BLANKS
     C                   MOVEL     R1154         W1154B
     C                   MOVEL     R1154         DFGM2
     C                   ENDIF
     C                   ENDIF
     C                   ENDSL
      *
     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *_______________________
      * INIT. DATAFELTER HEADF
      *"""""""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   MOVE      'A'           HEUR
     C                   EVAL      HESG='IA '
     C                   Z-ADD     9489          HEAVD
      * Hent oppdragsnr.
     C     HEAVD         CHAIN     TELL                               20
     C                   EVAL      HEOPD=TEOPDN
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
      *
     C                   MOVEL     M1004         HERFA
     C                   MOVEL     'NO'          HELKA
     C                   Z-ADD     18185         HEKNS
     C                   MOVEL     'X'           HEKDFS
     C                   EVAL      HENAS='Danzas ASG Solutions Tangerud'
     C                   EVAL      HEADS1='Luhrtoppen 2'
     C                   EVAL      HEADS3='1470  Lørenskog'
     C                   Z-ADD     18185         HEKNSF
     C                   EVAL      HENASF=HENAS
     C                   MOVEL     'X'           HEKDFK
     C                   MOVEL     'FO'          HEOT
     C                   MOVEL     'XXX'         HEFR
     C                   EVAL      HEGM1='DRIKKEVARER'
     C                   EVAL      HEPK1='J'
     C                   EVAL      HEPK2='N'
     C                   EVAL      HEPK3='J'
     C                   EVAL      HEPK4='N'
      *_______________________
      * INIT. DATAFELTER DOKUF
      *"""""""""""""""""""""""
     C                   CLEAR                   DOKUFR
     C                   MOVE      'F'           DFRI
     C                   Z-ADD     HEAVD         DFAVD
     C                   EVAL      DFOPD=HEOPD
     C                   Z-ADD     1             DFFBNR
     C                   MOVEL     M1004         DF1004
     C                   Z-ADD     6583249       DFKNSS
     C                   MOVEL     '06583249'    DFKNS
     C                   EVAL      DFNAVS='Maersk Logistics Norge AS'
     C                   EVAL      DFAD1S='Brobekkveien 80  Tlf 22636000'
     C                   EVAL      DFAD3S='0510 Oslo'
     C                   Z-ADD     20            DFTRAN
     C                   EVAL      DFNAT=DFNAVS
     C                   EVAL      DFFASE='Danzas ASG Solutions Tang'
     C                   MOVEL     'X'           DFKDME
     C                   MOVEL     'S'           DFBELA
      *_______________________
      * INIT. DATAFELTER DOK29
      *"""""""""""""""""""""""
     C                   CLEAR                   DOK29R
     C                   EVAL      D29AVD=DFAVD
     C                   EVAL      D29OPD=DFOPD
     C                   EVAL      D29FNR=DFFBNR
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   EXSR      DTM00
     C                   EXSR      FTX00
     C                   EXSR      RFF03
     C                   EXSR      NAD10
     C                   EXSR      GID16
      *________________
      * WRITE TO SYSPED
      *""""""""""""""""
     C                   WRITE     HEADFR
     C                   WRITE     DOKUFR
     C                   WRITE     DOK29R
      *______________________________
      * Bestill utskrift av fraktbrev
      *""""""""""""""""""""""""""""""
      * Oppdaterer LDA fordi avd oppdrag brukes i SYFA12
     C                   CALL      'WMD23C'
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
      * Kaller på SYFA12 for å generere utskrift
     C                   CALL      'SYFA12CLJ'
     C                   PARM      'F'           ZZKODE            1
     C                   PARM      ' '           ZZUB              1
     C                   PARM      ' '           ZZEKSP            1
     C                   PARM      ' '           ZZLG              1
     C                   PARM      ' '           ZZPO              1

     C                   ENDSR
