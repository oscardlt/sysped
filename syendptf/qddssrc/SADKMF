      *__________________________________
      * SYSPED / Tolldekl. Import KURERMANIFEST
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SADKMFR
     A            KMST      R               REFFLD(ST)
     A                                      COLHDG('Status')
      * ' ' --> NY REGISTRERT MANIFEST
      * 'B' --> MANIFEST ER LAGT I UTGÅENDE MAPPE
      * 'C' --> MANIFEST ER SENDT TIL TOLLVESENET
      * 'P' --> MANIFEST ER AVSLUTTET
      * 'X' --> MANIFEST ER UNDERARBEID AV EN BRUKER
     A            KMAVD     R               REFFLD(AVD)
     A                                      COLHDG('Avdeling')
     A            KMPRO     R               REFFLD(PRO)
     A                                      COLHDG('Turnummer')
     A            KMUUID        36A         COLHDG('Unik ID - UUID')
     A            KMDTR     R               REFFLD(DT)
     A                                      COLHDG('Registreringsdato')
     A            KMSG      R               REFFLD(SG)
     A            KMERR         50A         COLHDG('Feilmelding ved SND')
      * Deklarant
     A            KMKND     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Deklarant')
     A            KMNAD     R               REFFLD(NA)
     A                                      COLHDG('Navn Deklarant')
     A            KMRGD         11A         COLHDG('Org.nr Deklarant')
     A            KMADD1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Deklarant')
     A            KMADD2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Deklarant')
     A            KMPND          9A         COLHDG('Postnr Deklarant')
     A            KMADD3    R               REFFLD(AD)
     A                                      COLHDG('Poststed Deklarant')
     A            KMLKD          2A         COLHDG('Landkode Deklarant')
      *   Kommunikasjon
     A            KMCMD         50A         COLHDG('Telefon/epost dekl')
     A            KMCMDT         3A         COLHDG('Type tlf/epost dekl')
      * Consignment Master-Level
      *   Active border transport means
     A            KMTM           2S 0       COLHDG('Transport måte')
     A            KMTRID        35A         COLHDG('Transportmiddel')
     A            KMATDD    R               REFFLD(DT)
     A                                      COLHDG('Aktuell avg.dato')
     A            KMATDT    R               REFFLD(TM)
     A                                      COLHDG('Aktuell avg.tid')
     A            KMTRRF        17A         COLHDG('Reisens ref.nr')
      *   Entry office
     A            KMTD1          8A         COLHDG('1. tollsted ankomst')
     A            KMTDA          8A         COLHDG('Akt tollsted ankomst')
      *   Carrier
     A            KMKNC     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Transportør')
     A            KMNAC     R               REFFLD(NA)
     A                                      COLHDG('Navn Transportør')
     A            KMRGC         11A         COLHDG('Org.nr Transportør')
     A            KMADC1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Transportø')
     A            KMADC2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Transportø')
     A            KMPNC          9A         COLHDG('Postnr Transportør')
     A            KMADC3    R               REFFLD(AD)
     A                                      COLHDG('Poststed Transportør')
     A            KMLKC          2A         COLHDG('Landkode Transportør')
      *     Kommunikasjon
     A            KMCMC         50A         COLHDG('Telefon/epost Tr.')
     A            KMCMCT         3A         COLHDG('Type tlf/epost Tr.')
