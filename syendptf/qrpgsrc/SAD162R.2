      *
      ***********************************************************
      *** PROGRAM SKAL E.F. MANIFEST - LISTE HouseConsignment ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADEXHL1  UF   E           K DISK
     FSADEXML1  UF   E           K DISK
     FSADEXIL1  UF   E           K DISK
     FTURER14   IF   E           K DISK
     FSADH      IF   E           K DISK
     FTRACKF    O  A E             DISK
     FECMSG1    IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E             DISK
     FEDIM      O  A E             DISK
     FSAD162D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
     D                 DS
     D  SCASTR                 1     20
     D  SCASTR4                1      4  0
     D  SCASTR7                5     11  0
     D                 DS
     D  EHRG                   1     11
     D  EH0068A               13     20  0
     D  EH0068B               22     27  0
     D  EH_IMPREF              1     27
     D                 DS
     D  EHDKH                  1     50
     D  EHDKH_30               1     30
     D                 DS
     D  EHAVD                  1      4  0
     D  EHTDN                  5     11  0
     D  EHAVDTDN               1     11
     D                 DS
     D  u_pro_a                1      8
     D  u_pro_n                1      8  0
     D                 DS
     D  u_avd_a                1      4
     D  u_avd_n                1      4  0
     D                 DS
     D  u_tdn_a                1      7
     D  u_tdn_n                1      7  0
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'ARCA000CL'
     d ARCA000CL       pr                  extpgm('ARCA000CL')
     d scatyp_                             like(scatyp)
     d scastr_                             like(scastr)
      *  Prototype for 'ARC003'
     d ARC003          pr                  extpgm('ARC003')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d u_runde_                            like(u_runde)
      *  Prototype for 'TR011C'
     d TR011C          pr                  extpgm('TR011C')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d u_pro_                              like(u_pro)
     d u_sg_                               like(u_sg)
     d u_type_                             like(u_type)
     d u_ie_                               like(u_ie)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD003'
     d SAD003          pr                  extpgm('SAD003')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d win03_                              like(win03)
      *  Prototype for 'SAD005A'
     d SAD005A         pr                  extpgm('SAD005A')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD160SND'
     d SAD160SND       pr                  extpgm('SAD160SND')
     d ujson_type_                         like(ujson_type)
     d ujson_pro_                          like(ujson_pro)
     d ujson_avd_                          like(ujson_avd)
     d ujson_opd_                          like(ujson_opd)
     d ujson_mrn_                          like(ujson_mrn)
     d ujson_lrn_                          like(ujson_lrn)
     d ujson_fil_                          like(ujson_fil)
      *  Prototype for 'SAD164R'
     d SAD164R         pr                  extpgm('SAD164R')
     d upro_                               like(upro)
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD163R'
     d SAD163R         pr                  extpgm('SAD163R')
     d uavd_                               like(uavd)
     d upro_                               like(upro)
     d umid_                               like(umid)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD162RB'
     d SAD162RB        pr                  extpgm('SAD162RB')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD162RA'
     d*SAD162RA        pr                  extpgm('SAD162RA')
     d*upro_                               like(upro)
      *  Prototype for SAD162R
     d sad162r         pr
     d uavd_                               like(uavd)
     d upro_                               like(upro)
     d umid_                               like(umid)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  *ENTRY Interface for Main Procedure
     d sad162r         pi
     d uavd                           4  0
     d upro                           8  0
     d umid                          18a
     d utype                          3a
     d uflagg                         1  0
      *
     d scatyp          s              2a
     d xtype           s              3a
     d xrecno          s                   like(zrecno)
     d yrecno          s                   like(zrecno)
     d u_avd           s              4  0
     d u_tdn           s              7  0
     d u_pro           s              8  0
     d u_sg            s              3
     d u_type          s              3
     d u_ie            s              1
     d u_runde         s             10
     d ujson_type      s             30a
     d ujson_pro       s              8a
     d ujson_avd       s              4a
     d ujson_opd       s              7a
     d ujson_mrn       s             18a
     d ujson_lrn       s             36a
     d ujson_fil       s             45a
     d wdt             s              8a
     d wtm             s              6a
     d win03           s              1a
     d x_ehst          s                   like(ehst)
     d x_sndok         s              1a
      *
      /free

         exsr init;

         *in93 = *on;
         write subctl;
         *in93 = *off;
         *in94 = *off;
         zrecno = 0;
         xrecno = 0;

         dow *in03 = *off;            // START                                   // 1<b

           setll upro sadexhl1;
           reade(n) upro sadexhl1;

           dow not %eof;                                                         // 2<b
             zrecno = zrecno + 1;
             xrecno = zrecno;
             write subfil;
             reade(n) upro sadexhl1;
           enddo;                                                                // 2<e
           yrecno = zrecno;
           if zrecno > 19;                                                       // 2<b
             zrecno = 1;
           endif;                                                                // 2<e

           dow *in03 = *off;               // START3                             // 2<b
           // vis subfil og få input
             *in91 = *on;
             *in92 = *on;
             write bunn;
             if zrecno = 0;                                                      // 3<b
               exfmt nyvare;
             else;                                                               // 3
               exfmt subctl;
             endif;                                                              // 3<e

             *in30 = *off;
             *in98 = *off;
             *in99 = *off;
             select;                                                             // 3<b
             when *in03;                                                         // 3
               exsr sropdvk;
               uflagg = 3;
               *inlr = *on;
               return;
             when *in05;                                                         // 3
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in06 or *in16;                                                // 3
               exsr lagopd;
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in10;                                                         // 3
               exsr plkopd;
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in12;                                                         // 3
               exsr sropdvk;
               uflagg = 1;
               *inlr = *on;
               return;
             when zrecno <> 0;                                                   // 3
               exsr hent;
             endsl;                                                              // 3<e

           enddo;                                                                // 2<e

         enddo;                                                                  // 1<e

         exsr sropdvk;

         *inlr = *on;
         return;

      //*********************************************
       begsr hent;

       msgnr = *blanks;
       *in90 = *off;
       *in91 = *off;
       *in92 = *off;
       *in98 = *off;
       *in99 = *off;

       readc subfil;
       dow not %eof;
         if ehdkh <> '* SLETTET *';
           select;
           when wsnr = 2 and *in49 = *off;
             *in99 = *off;
             exsr chgopd;
           when wsnr = 3 and *in49 = *off;
             *in99 = *off;
             exsr cpyopd;
           when wsnr = 4 and *in49 = *off;
             *in99 = *off;
             exsr sltopd;
           when wsnr = 5;
             *in99 = *off;
             exsr dspopd;
           when wsnr = 9;
             *in99 = *on;
             exsr dsplog;
           when wsnr = 11 and ehtdn > 0;
             *in99 = *off;
             exsr splopd;
           when wsnr = 15 and ehtdn > 0;
             *in99 = *off;
             exsr vissad;
           when wsnr = 51;
             *in99 = *off;
             exsr arkivs;
           when wsnr = 52;
             *in99 = *off;
             exsr prtskill;
           when wsnr = 90;
             //if ehst <> ' ' and ehst <> 'C' and ehst <> 'M' and ehst <> 'O';
             if ehst <> ' ' and ehst <> 'F' and ehst2 = 'O';
               *in30 = *on;
               msgnr = 'YBC2103';
               wsnr = 0;
               update subfil;
               leave;
             endif;
             *in99 = *on;
             exsr sndopd;
           when wsnr = 99;
             select;
             when ehst = 'P' and ehmid <> *blanks;
               *in33 = *off;
             other;
               *in33 = *on;
             endsl;
             if *in33;
               *in30 = *on;
               msgnr = 'YBC2102';
               wsnr = 0;
               update subfil;
               leave;
             endif;
             *in99 = *on;
             exsr avslopd;
           endsl;
         endif;

         readc subfil;
       enddo;

       if *in99 = *off;
         msgnr = 'DIV0012';
         *in30 = *on;
       endif;
       *in99 = *off;

       endsr;

      //*********************************************
       begsr dspopd;

       if EHVT = *blanks;
         xtype = 'DS2';            // SPØRRING
       else;
         xtype = 'DSP';            // SPØRRING
       endif;
       u_avd = ehavd;
       u_tdn = ehtdn;
       sad164r (upro: u_avd: u_tdn: xtype: uflagg);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr dsplog;

       win03 = '9';                // Vis edi logg
       u_avd = ehavd;
       u_tdn = ehtdn;
       sad003 (u_avd: u_tdn: win03);
       if win03 = '1';
         *inlr = *on;
         return;
       endif;
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr vissad;

       u_avd = ehavd;
       u_tdn = ehtdn;
       if ehtrnr = *blanks;
         sad005a (u_avd: u_tdn: uflagg);
       else;
         u_type = 'DSP';
         u_ie   = 'E';
         tr011c (u_avd: u_tdn: u_pro: u_sg: u_type: u_ie: uflagg);
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr arkivs;

       u_avd = ehavd;
       u_tdn = ehtdn;
       u_runde = *blanks;
       arc003 (u_avd: u_tdn: u_runde);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr prtskill;

       u_avd = ehavd;
       u_tdn = ehtdn;
       scatyp  = *blanks;
       scastr  = *blanks;
       scastr4 = ehavd;
       scastr7 = ehtdn;
       arca000cl (scatyp: scastr);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr chgopd;

       if EHVT = *blanks;
         xtype = 'CH2';            // ENDRING
       else;
         xtype = 'CHG';            // ENDRING
       endif;
       u_avd = ehavd;
       u_tdn = ehtdn;
       sad164r (upro: u_avd: u_tdn: xtype: uflagg);

       chain(n) (upro: ehavd: ehtdn) sadexhl1;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr cpyopd;

       xtype = 'CPY';            // copy
       u_avd = ehavd;
       u_tdn = ehtdn;
       sad164r (upro: u_avd: u_tdn: xtype: uflagg);

       // chain(n) (upro: ehavd: ehtdn) sadexhl1;
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       if u_avd <> ehavd or u_tdn <> ehtdn;
         chain(n) (upro: u_avd: u_tdn) sadexhl1;
         xrecno = xrecno + 1;
         zrecno = xrecno;
         write subfil;
       endif;

       endsr;

      //*********************************************
       begsr sltopd;

       chain (upro: ehavd: ehtdn) sadexhl1;
       if %found;
         ehst2  = 'D';
         ehpro = 0;
         update sadexhfr;
         ehdkh = '* SLETTET *';
         exsr ttsr;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr splopd;

       chain (ehavd: ehtdn) sadh;
       if not %found;
         u_avd = ehavd;
         u_tdn = ehtdn;
         sad162rb (u_avd: u_tdn: utype: uflagg);
       endif;

       chain(n) (upro: ehavd: ehtdn) sadexhl1;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr plkopd;

       sad163r (uavd: upro: umid: uflagg);

       endsr;

      //*********************************************
       begsr lagopd;

       select;
       when *in06;
         xtype = 'CRT';
       when *in16;
         xtype = 'CR2';
       endsl;
       u_avd = 0;
       u_tdn = 0;
       uflagg = 0;
       sad164r (upro: u_avd: u_tdn: xtype: uflagg);

       endsr;

      // ********************************************
       begsr sndopd;

       exsr testopd;

       if x_sndok = 'J';
         if ehmid = *blanks;
           wsst2 = 'S';
           *in51 = *on;
         else;
           wsst2 = 'R';
           *in51 = *off;
         endif;
         dow *in03 = *off and *in12 = *off;
           exfmt sendopd;
           if wsst2 <> ' ';
             *in03 = *on;
           endif;
         enddo;
         *in03 = *off;
         if *in12 = *off;
           if wsst2 = 'D' and ehmid = *blanks;
           // Merk sletting på manifest som aldri sendt til Tollvesenet.
             chain (ehpro: ehavd: ehtdn) sadexhl1;
             if %found;
               //ehst2 = 'D';
               update sadexhfr;
             endif;
           else;
             // Send HouseConsignment til Tollvesenet.
             chain (ehpro: ehavd: ehtdn) sadexhl1;
             //ehst  = 'C';
             //ehst2 = wsst2;
             update sadexhfr;
      /end-free
     C                   MOVE      ehpro         ujson_pro
     C                   MOVE      ehavd         ujson_avd
     C                   MOVE      ehtdn         ujson_opd
      /free
             ujson_mrn = ehmid;
             ujson_lrn = ehuuid;
             select;
             when wsst2 = 'S';
              ujson_type = 'postHouseConsignment.do';
             when wsst2 = 'R';
              ujson_type = 'putHouseConsignment.do';
             when wsst2 = 'D';
              ujson_type = 'deleteHouseConsignment.do';
             endsl;
             sad160snd (ujson_type: ujson_pro: ujson_avd: ujson_opd:
                        ujson_mrn: ujson_lrn: ujson_fil);
             chain(n) (ehpro: ehavd: ehtdn) sadexhl1;
             exsr sredis;
           endif;
           *in03 = *off;
         else;
       //  exfmt errmsg;
         endif;
       else;
         exfmt errsnd;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr testopd;

       x_sndok = 'J';

       endsr;

      // ********************************************
       begsr avslopd;


       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr ttsr;

       syge15c (wdt: wtm);

       ttavd  = ehavd;
       ttopd  = ehtdn;
       tttur  = 0;
       ttacti = 'EFC';
       ttdate = %dec(wdt:8:0);
       tttime = %dec(wtm:6:0);
       ttdatl = ttdate;
       tttiml = tttime;
       ttuser = uusr;
       tttext = 'Oppdrag er fjernet fra E.F. manifest('
               + %TRIM(%EDITC(UPRO:'Z')) + ').';
       tttexl = tttext;
       ttedev = 'RMV';

       write trackfr;

       endsr;

      // ********************************************
       begsr sredis;

        chain ('S': 'EXPREM') ecmsg1;
        chain 1 edic;
        csn = csn + 1;
        update edicr;
        s0004 = ECIIDI;
        s0010 = ECIIDC;
        u_pro_n = ehpro;
        s0020 = 'EF' + %TRIM(u_pro_a) + '-HC';
        s0026 = 'SADEXJSON_H';
        ssn   = CSN;
        ssr   = 'S';
        sst   = 'B';
        select;
        when wsst2 = 'S';
          s0036 = 1;
        when wsst2 = 'D';
          s0036 = 2;
        when wsst2 = 'R';
          s0036 = 4;
        endsl;
        sdt   = %dec(wdt:8:0);
        stm   = %dec(wtm:6:0);
        slib  = '*LIB';
        sfile = 'EDISE1K';
        smbr  = *BLANKS;
        sifs  = *blanks;
        write edisr;

        exsr sredim;

       endsr;

      // ********************************************
       begsr sredim;

        chain 1 edic;
        cmn = cmn + 1;
        update edicr;
        m0004 = s0004;
        m0010 = s0010;
        u_avd_n = ehavd;
        u_tdn_n = ehtdn;
        m0062 = 'EF' + %TRIM(u_avd_a) + %TRIM(u_tdn_a);
        m0065 = 'XMLEXH';
        msn   = ssn;
        msr   = 'S';
        mst   = 'C';
        m0068 = m0062;
        m1001 = 'EPR';
        mmn   = cmn;
        mdt   = %dec(wdt:8:0);
        mtm   = %dec(wtm:6:0);
        select;
        when wsst2 = 'S';
          m1n07 = 'HCS';
        when wsst2 = 'D';
          m1n07 = 'HCD';
        when wsst2 = 'R';
          m1n07 = 'HCR';
        other;
          m1n07 = 'HCO';
        endsl;
        mavd  = ehavd;
        mtdn  = ehtdn;
        write edimr;

       endsr;

      //*********************************************
       begsr sropdvk;

         // Summer total vekt
         chain (uavd: upro) sadexml1;
         if %found;
           setll upro sadexhl1;
           reade upro sadexhl1;
           if not %eof;
             emvkb = 0;
           endif;
           dow not %eof;
           // Oppdater deklarant og repretativ på house
             if ehrgd = *blanks;
               ehknd  = emknd;
               ehrgd  = emrgd;
               ehnad  = emnad;
               ehna2d = emna2d;
               ehad1d = emad1d;
               ehnrd  = emnrd;
               ehpnd  = empnd;
               ehpsd  = empsd;
               ehlkd  = emlkd;
               ehpbd  = empbd;
               ehemd  = ememd;
               ehemdt = ememdt;

               ehknr  = emknr;
               ehrgr  = emrgr;
               ehstr  = emstr;
               ehnar  = emnar;
               ehna2r = emna2r;
               ehad1r = emad1r;
               ehnrr  = emnrr;
               ehpnr  = empnr;
               ehpsr  = empsr;
               ehlkr  = emlkr;
               ehpbr  = empbr;
               ehemr  = ememr;
               ehemrt = ememrt;
             endif;
             //update sadexhfr;

             //if ehst = ' ' or ehst = 'O' or ehst = 'C' or ehst = 'P'
             //  or ehst = 'M' or ehst = 'D';
             if ehst = ' ' or ehst = 'F';
               chain (upro: ehavd: ehtdn) sadexil1;
               if %found;
                 ehvkb = 0;
               endif;
               setll (upro: ehavd: ehtdn) sadexil1;
               reade (upro: ehavd: ehtdn) sadexil1;
               dow not %eof;
                 ehvkb = ehvkb + eicvkb;
                 reade (upro: ehavd: ehtdn) sadexil1;
               enddo;
             endif;
             emvkb = emvkb + ehvkb;

             update sadexhfr;

             reade upro sadexhl1;
           enddo;
           if emvkb = 0;
             emvkb = 1;
           endif;
           update sadexmfr;
         endif;

       endsr;

      //*********************************************
       begsr init;

       syge15c (wdt: wtm);
       if utype = 'DSP';
         *in49 = *on;
       else;
       //chain upro turer14;
       //if %found;
         // Klagjør for SADEXHF
       //  sad162ra (upro);
       //endif;
       endif;
       write windowsz;

       eh_impref = *blanks;

       endsr;

      /end-free
