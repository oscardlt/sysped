     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 80 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'Avbryt')
     A                                      CF04(04 'Søking')
     A N47                                  CF09(09 'Hent UUID')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD139R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med ekspresfortolling manif-
     A                                      est Default'
     A                                      COLOR(WHT)
     A                                  2  2'Avd:'
     A            WSAVD          4Y 0B  2  7DSPATR(UL)
     A  49
     AO 48                                  DSPATR(PR)
     A N99
     AAN48                                  COLOR(YLW)
     A  99
     AO 48                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A*                                 2 13'Neste turnr:'
     A*           EFPRO          8Y 0O  2 26COLOR(WHT)
     A*                                     EDTCDE(4)
     A                                  2 36'R.Dato:'
     A            WSDTR          6Y 0O  2 44COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A            EFUUID        36A  H
      *
     A                                  5  2'Kundenr:'
     A            WSKND          8Y 0B  5 20TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 30'Org.nr.:'
     A            WSRGD          9A  B  5 39TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
     A            WSNAD         30A  O  6 20TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Tr.måte KD:'
     A            WSTM           3A  B  7 20TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A                                  7 25'(BIL,,,)'
     A            WSTMT         50A  H
     A            WSTMT_30      30A  O  7 40TEXT('Transportmåte tekst')
     A                                      COLOR(WHT)
     A                                  8  2'Kjøretøy KD:'
     A            WSKTYP         2A  B  8 20TEXT('Kjøretøy type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N95                                  COLOR(YLW)
     A  95                                  COLOR(WHT)
     A                                  8 25'(01,02,03,,,)'
     A            WSKTYPT       50A  H
     A            WSKTYPT_30    30A  O  8 40TEXT('Kjøretøy type')
     A                                      COLOR(WHT)
     A                                  9  2'Kjøretøy LK:'
     A            WSKLK          2A  B  9 20TEXT('Kjøretøy landkode')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N94                                  COLOR(YLW)
     A  94                                  COLOR(WHT)
     A                                  9 25'Kjennemerke:'
     A            WSKMRK        30A  B  9 38TEXT('Bilnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A                                 10  2'Passiv kj. LK:'
     A            WSPLK          2A  B 10 20TEXT('Passiv kjøretøy LK')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A                                 10 25'Kjennemerke:'
     A            WSPMRK        30A  B 10 38TEXT('Bilnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N91                                  COLOR(YLW)
     A  91                                  COLOR(WHT)
     A                                 11  2'Sjåfør fornavn:'
     A            WSSJAF        30A  B 11 20TEXT('Sjåfør fornavn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  90                                  COLOR(WHT)
     A                                      CHECK(LC)
     A                                 12  2'Sjåfør etternavn:'
     A            WSSJAE        30A  B 12 20TEXT('Sjåfør etternavn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A                                      CHECK(LC)
     A                                 13  2'Nasjonalitet:'
     A            WSSJALK        2A  B 13 20TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
     A                                 13 25'Fødselsdato:'
     A            WSSJADT        6Y 0B 13 38TEXT('Sjåfør fødselsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N87                                  COLOR(YLW)
     A  87                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 13 48'Bekreftelse J/N:'
     A            WSBEKR         1A  B 13 65TEXT('Sjåfør bekreftelse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 14  2'ETA:'
     A            WSETA          6Y 0B 14 20TEXT('Type')
     A  49                                  DSPATR(PR)
     A N86                                  COLOR(YLW)
     A  86                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 14 30'Tid:'
     A            WSETM          6Y 0B 14 36TEXT('ETA tid')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N85                                  COLOR(YLW)
     A  85                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15  2'Pass.tollsted:'
     A            WSTSD          4Y 0B 15 20TEXT('Type')
     A  49                                  DSPATR(PR)
     A N84                                  COLOR(YLW)
     A  84                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15 30'Eksped.enhet:'
     A            WS3039E        6Y 0B 15 44TEXT('Eksped.enhet')
     A  49                                  DSPATR(PR)
     A N83                                  COLOR(YLW)
     A  83                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16  2'EksportId:'
     A            WSEID         18A  B 16 20TEXT('EksportId')
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 17  2'F3=Avslutt   F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
