     H DATEDIT(*YMD)
     FEUNH      IF   E           K DISK
     FELIN93A   IF   E           K DISK
     FEPIA93A   IF   E           K DISK
     FEQTY93A   IF   E           K DISK
     FEMEA93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FSADL      IF   E           K DISK
     FE153F     O  A E           K DISK
     D A               S              1    DIM(15)
     D                UDS
     D  UMNPGM               345    353
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      CVTDTA
      *
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREP0
     C     SADLK         KLIST
     C                   KFLD                    SLKNR
     C                   KFLD                    SLALFA
      *
     C                   Z-ADD     10022913      SLKNR
     C     *LIKE         DEFINE    LGN           ZGN0
     C     *LIKE         DEFINE    LGN           ZGN1
     C     *LIKE         DEFINE    LREP0         ZREP0
     C     *LIKE         DEFINE    UMN           ZMN
     C                   MOVE      UMNPGM        ZMN
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * CONVERT DATA                                       CVTDTA /
      *////////////////////////////////////////////////////////////
     C     CVTDTA        BEGSR
     C                   CLEAR                   E153FR
     C                   Z-ADD     0             ZGN0
      *__________
      * UNH/BGM00
      *""""""""""
     C                   MOVEL     ZMN           EWMN
     C     ZMN           CHAIN     EUNH                               51
     C     *IN51         IFEQ      '0'
     C                   MOVEL     U1001         EWONO
     C                   ENDIF
      *______
      * LIN25
      *""""""
     C                   EXSR      LIN25
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LIN25                                               LIN25 /
      *////////////////////////////////////////////////////////////
     C     LIN25         BEGSR
      *
     C                   Z-ADD     25            ZGN0
     C                   Z-ADD     25            ZGN1
     C                   Z-ADD     LREP0         ZREP0
     C                   MOVE      '0'           *IN51
     C     KEYMS0        SETLL     ELIN93A                                33
     C     *IN33         IFEQ      '1'
     C     KEYMS0        READE     ELIN93A                                51
     C     *IN51         DOWEQ     '0'
     C                   MOVEL     L7140         EWITEM
     C                   MOVEL     EWITEM        SLALFA
     C     SADLK         CHAIN     SADL                               33
     C  N33              MOVE      SLOPPL        EWCCD
     C  N33              MOVE      SLTANR        EWTARC
     C  N33              MOVE      SLTXT         EWDSC
      *
      *______
      * PIA25
      *""""""
     C                   EXSR      PIA25
      *______
      * QTY25
      *""""""
     C                   EXSR      QTY25
      *______
      * MEA25
      *""""""
     C                   EXSR      MEA25
      *______
      * MOA25
      *""""""
     C                   EXSR      MOA25
      *
     C                   WRITE     E153FR
     C                   CLEAR                   E153FR
     C                   MOVEL     U1001         EWONO
      *
     C     KEYMS0        READE     ELIN93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * PIA25                                               PIA25 /
      *////////////////////////////////////////////////////////////
     C     PIA25         BEGSR
     C     KEYMS1        CHAIN     EPIA93A                            53
     C     *IN53         IFEQ      '0'
     C                   MOVEL     P7140A        EWGCD
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * QTY25                                               QTY25 /
      *////////////////////////////////////////////////////////////
     C     QTY25         BEGSR
     C     KEYMS1        CHAIN     EQTY93A                            53
     C     *IN53         IFEQ      '0'
     C                   MOVE      *BLANKS       A
     C                   MOVEA     Q6060         A(1)
     C     1             DO        5             XN                3 0
     C     A(XN)         IFNE      ' '
     C                   MULT      10            EWCOL
     C                   MOVE      A(XN)         EWCOL
     C                   END
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * MEA25                                               MEA25 /
      *////////////////////////////////////////////////////////////
     C     MEA25         BEGSR
     C     KEYMS1        CHAIN     EMEA93A                            53
     C     *IN53         IFEQ      '0'
     C                   MOVE      *ZEROS        XN15             15 0
     C                   MOVE      *BLANKS       A
     C                   MOVEA     M6314         A(1)
     C     1             DO        15            XN                3 0
     C     A(XN)         IFNE      ' '
     C                   MULT      10            XN15
     C                   MOVE      A(XN)         XN15
     C                   END
     C                   END
     C     XN15          DIV       1000          EWWT
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * MOA25                                               MOA25 /
      *////////////////////////////////////////////////////////////
     C     MOA25         BEGSR
     C     KEYMS1        SETLL     EMOA93A                                33
     C     *IN33         IFEQ      '1'                                          1<-B
     C     KEYMS1        READE     EMOA93A                                53
     C     *IN53         DOWEQ     '0'                                           2<-B
     C                   MOVE      *ZEROS        XN15
     C                   MOVE      *BLANKS       A
     C                   MOVEA     M5004         A(1)
     C     1             DO        15            XN                               3<-B
     C     A(XN)         IFNE      ' '                                             4<-B
     C                   MULT      10            XN15
     C                   MOVE      A(XN)         XN15
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C     M5025         IFEQ      '40 '                                          3<-B
     C     XN15          DIV       100           EWVAL
     C                   DIV(H)    EWCOL         EWUNPR
     C                   ELSE
     C                   MOVE      XN15          EWUPL
     C                   END                                                      3<-E
      *
     C     KEYMS1        READE     EMOA93A                                53
     C  N53              END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
