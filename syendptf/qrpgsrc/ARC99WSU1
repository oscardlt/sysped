      *
     H DFTACTGRP(*NO) BNDDIR('HTTPAPI':'QC2LE') debug(*yes)


      /copy qrpglesrc,httpapi_h
      /copy qrpglesrc,ifsio_h



     D jsonstring      s           1000A   varying
     D rc              s             10I 0
     D filename        s             80a   varying
     D dltFile         s            120a   varying
     D Server          s            200a
     D localPath       s            100a
      *
     c     *entry        Plist
     c                   parm                    Companyid        10
     c                   parm                    ulocalPath      100
     c                   parm                    uSrcFile        120
     c                   parm                    udelete           1
     c                   parm                    uNewFile         60
     c                   parm                    uok               1
     c                   move      'N'           uok
     c                   move      ulocalPath    localPath
     c                   if        localPath = *blanks
     c                   eval      localPath = '/tmp/'
     c                   endif
     c                   if        Udelete   = *blanks
     c                   eval      Udelete   = 'J'
     c                   endif
      *
      *
      /free
       if uSrcFile =  *blanks;
         *inlr = *on;
         return;
       endif;

       //server = 'http://10.11.47.61:9886/api/putfiles';
       server = 'https://10.11.47.61:9887/api/putfiles';

       // payload er jsonstring med lokalpath og array med firmaID(bucket navn)+filnavn.
       jsonstring =
       '{"localgetpath": "' + %trim(LocalPath) + '",'
         +' "files": ['
         + '{"companyid":"'+%trim(Companyid) + '",'
         + '"filename":"'+ %trim(uSrcFile) + '",'
         + '"newfilename":"'+ %trim(uNewFile) + '"}'
         +']}';

       //
       // Note:  http_debug(*ON/*OFF) can be used to turn debugging
       //        on and off.  When debugging is turned on, diagnostic
       //        info is written to an IFS file named
       //        /tmp/httpapi_debug.txt
       http_debug(*ON);

       filename = http_tempfile() + '.json';
       rc = http_url_post(%trim(server)
                          +' '
                         : %addr(jsonstring) + 2
                         : %len(jsonstring)
                         : filename
                         : HTTP_TIMEOUT
                         : HTTP_USERAGENT
                         : 'application/json');
       // hvis misslykke vent 2 sek/prøv igjen (nyskapt fil ikke helt "klar"??)
       if rc<>201;
         exsr VentLitt;
         rc = http_url_post(%trim(server)
                          +' '
                         : %addr(jsonstring) + 2
                         : %len(jsonstring)
                         : filename
                         : HTTP_TIMEOUT
                         : HTTP_USERAGENT
                         : 'application/json');
       endif;

       if rc=201;
         eval uok='J';    // ved flere betyr dette at minst enn var vellykket
         //slett fil
         if udelete='J';
           dltFile = %trim(localPath)+%trim(uSrcFile);
           unlink(dltFile);
         endif;
       endif;
       unlink(filename);
       *inlr = *on;
       return;

       // Å teste resultat via responsfil er unødig - 201 SKAL bety at fil er skapt
       //
      /end-free

     c*****************************************
     c     Ventlitt      BEGSR
     c*****************************************
     C                   Z-ADD     2             USEC              3 0
     C                   CALL      'DLYJBSEC'
     C                   PARM                    USEC
     c
     c                   ENDSR
