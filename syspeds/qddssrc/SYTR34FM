     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            HOLNR          7S 0H
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4' '7')
     A            HONAS         30A  O  5  5
     A  55                                  COLOR(WHT)
     A            HOPNRS         4  0O  5 36
     A  56                                  COLOR(YLW)
     A            HODT4          4Y 0O  5 41EDTWRD('  .  .')
     A            HOBILN        10A  O  5 48
     A            HOVKT          6Y 0O  5 62EDTCDE(4)
     A            HOM3           5Y 3O  5 69EDTWRD('  ,   ')
     A            HOLM           4Y 2O  5 76EDTWRD('  ,  ')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'SØK')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'TILBAKE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYTR34'
     A                                      COLOR(BLU)
     A  27                              1 19MSGCON(040 SHU0693 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27                              1 19MSGCON(040 SHU0696 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A                                      DSPATR(ND)
     A            AVDLES         4Y 0B  2 14EDTCDE(4)
     A                                      DSPATR(ND)
     A            KOANVN        33   O  2 19DSPATR(ND)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(040 YBC0963 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 43MSGCON(018 SHU0694 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 SHU0695 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CF02(02 'HENTSNDNR')
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      CF09(09 'TUR-PLANL')
     A                                      CF10(10 'Varelinjer')
     A                                      SLNO(17)
     A                                      CLRL(08)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'Henteoppdrag Hode'
     A                                      COLOR(WHT)
     A                                  1 21'Snd.nr'
     A            WSSNDN        20A  B  1 28DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                  1 49'Bil:'
     A            WSBILN        10A  B  1 54DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                  1 66'Tur:'
     A            WSPRO          8Y 0B  1 71EDTCDE(4)
     A  84                                  DSPATR(RI)
     A                                  2  2'Kunde:'
     A            WSKNS          8Y 0B  2  9EDTCDE(4)
     A  86                                  DSPATR(RI)
     A            WSNAS         30A  B  2 18DSPATR(UL)
     A            WSADS1        30A  B  2 49DSPATR(UL)
     A            WSPNRS         4Y 0B  3 49
     A  89                                  DSPATR(RI)
     A            WSSTDS        25A  B  3 54DSPATR(UL)
     A                                  5  2'Dato :'
     A            WSDT           6Y 0B  5  9
     A  87                                  DSPATR(RI)
     A                                  5 18'Postnr terminal:'
     A            WSPNRT         4Y 0B  5 35
     A  88                                  DSPATR(RI)
     A                                  5 41'Avd.nr:'
     A            WSDAVD         4Y 0B  5 49
     A  85                                  DSPATR(RI)
     A                                  6 64'Løpenr:'
     A            WSLNR          7Y 0B  6 72EDTCDE(4)
     A                                      COLOR(WHT)
     A                                      DSPATR(PR)
     A                                  7  2'F2=Hent Snd.nr'
     A                                      COLOR(BLU)
     A                                  7 19'F4=Søk'
     A                                      COLOR(BLU)
     A                                  7 26'F9=Tur/bil-oversikt'
     A                                      COLOR(BLU)
     A                                  7 46'F10=Varelinjer'
     A                                      COLOR(BLU)
     A                                  7 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              8 40' C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      SLNO(17)
     A                                      CLRL(08)
     A                                      OVERLAY
     A                                  1  2'Henteoppdrag Hode'
     A                                      COLOR(WHT)
     A                                  1 21'Snd.nr'
     A            HOSNDN        20A  O  1 28DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  1 49'Bil:'
     A            HOBILN        10   O  1 54COLOR(WHT)
     A                                  1 66'Tur:'
     A            HOPRO          8Y 0O  1 71EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2  2'Kunde:'
     A            HOKNS          8Y 0O  2  9EDTCDE(4)
     A                                      COLOR(WHT)
     A            HONAS         30A  O  2 18DSPATR(UL)
     A                                      COLOR(WHT)
     A            HOADS1        30A  O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A            HOPNRS         4Y 0O  3 49COLOR(WHT)
     A            HOSTDS        25A  O  3 54DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  2'Dato :'
     A            HODT           8Y 0O  5  9COLOR(WHT)
     A                                  5 18'Postnr terminal:'
     A            HOPNRT         4Y 0O  5 35COLOR(WHT)
     A                                  5 41'Avd.nr:'
     A            HODAVD         4Y 0O  5 49COLOR(WHT)
     A                                  6 64'Løpenr:'
     A            HOLNR          7Y 0O  6 72EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7 41'ENTER=Bekreft sletting'
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA06(06 'LAGE NY')
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'TILBAKE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTR34'
     A                                      COLOR(BLU)
     A  27                              1 19MSGCON(040 SHU0693 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27                              1 19MSGCON(040 SHU0696 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A                                      DSPATR(ND)
     A            AVDLES         4Y 0B  2 14EDTCDE(4)
     A            KOANVN        33   O  2 19DSPATR(ND)
     A                                  3  2MSGCON(040 YBC0963 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 43MSGCON(018 SHU0694 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 SHU0695 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
