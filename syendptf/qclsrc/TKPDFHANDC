             PGM
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&INST)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&SERVER) TYPE(*CHAR) LEN(40)
             DCL        VAR(&USER)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGLVL) TYPE(*CHAR) LEN(5)
             DCL        VAR(&ROOT)   TYPE(*CHAR) LEN(30)
             DCL        VAR(&SDEV)   TYPE(*CHAR) LEN(30) /*syspeddev*/
             DCL        VAR(&LOGFOLDER)   TYPE(*CHAR) LEN(30)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(512) VALUE('Ikke +
                          installert')
             CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)
 /*HENT INSTALLASJONSINFO */
 START:      CALL       PGM(ARCGTKPDF) PARM(&INST &SERVER &USER +
                          &LOGLVL &ROOT &SDEV)

             IF         COND(&INST *NE 'J') THEN(GOTO CMDLBL(SLUTT))

             CHGVAR     VAR(&LOGFOLDER) VALUE(&ROOT *TCAT '/log_JavaVersjon')

  /* Fornyer &CMD i hver syklus for evt å fange opp endring i LogLvl   */

             CHGVAR     VAR(&CMD) VALUE('export JAVA_HOME=/QOpenSys+
                       /QIBM/ProdData/JavaVM/jdk70/64bit; +
                       java -Djava.awt.headless=true +
                       -Djava.util.logging.config.file=/' *TCAT &SDEV *TCAT +
                       '/TKpdf/bin/config/logging.properties +
                       -classpath /'  *TCAT &SDEV *TCAT  +
                       '/TKpdf/bin:/' *TCAT &SDEV *TCAT  +
                       '/TKpdf/libs/* no.tksoftware.TKpdfHandler +
                       -server' *BCAT &SERVER *BCAT +
                       '-user' *BCAT &USER *BCAT +
                       '-logFolder' *BCAT &LOGFOLDER *BCAT +
                       '-logLevel' *BCAT &LOGLVL )

 /* kjør en behandlingssyklus */
             QSH        CMD(&CMD)

   /* HVIS STARET MED LOOP = Y - KJØR HVERT 2. MINUTT. */
              RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
              IF         COND(&TIME *LT '235000') THEN(DO)
              DLYJOB     DLY(60)
              GOTO       CMDLBL(START)
              ENDDO

 SLUTT:      ENDPGM
