     FLISTE     O    F   80        PRINTER OFLIND(*INOF)
     FKUFAST    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSTAWRK    IP   E           K DISK
     FCUNDL01   IF   E           K DISK
     D A               S             25    DIM(10)
     D T               S              9  2 DIM(10)
     D                 DS
     D  OCSB01                 1      9  2
     D  OCSB02                10     18  2
     D  OCSB03                19     27  2
     D  OCSB04                28     36  2
     D  OCSB05                37     45  2
     D  OCSB06                46     54  2
     D  OCSB07                55     63  2
     D  OCSB08                64     72  2
     D  OCSB09                73     81  2
     D  OCSB10                82     90  2
     D  B                      1     90  2
     D                                     DIM(10)
     D                 DS
     D  OCSUDA                 1      8  0
     D  OCSAAR                 1      4  0
     D  OCSMND                 5      6  0
     D  OCSDAG                 7      8  0
     D                 DS
     D  OCSFRA                 1      8  0
     D  OCSFAR                 1      4  0
     D  OCSFMN                 5      6  0
     D  OCSFDG                 7      8  0
     D                 DS
     D  OCSTIL                 1      8  0
     D  OCSTAR                 1      4  0
     D  OCSTMN                 5      6  0
     D  OCSTDG                 7      8  0
     C  N99              EXSR      INIT
     C                   SETON                                        99
     C*
     C     KEYKUN        KLIST
     C                   KFLD                    OCSFI
     C                   KFLD                    OCSKU
     C     KEYKUN        CHAIN     CUNDL01                            33
     C*
     C   OF              DO
     C                   EXCEPT    PHEAD2
     C                   SETOFF                                       OF
     C                   END
     C     1             DO        10            NR
     C                   ADD       B(NR)         T(NR)
     C                   END
     C                   ADD       OCSTOT        GRANDT           11 2
      * SKRIVER LINJESTART
     C                   EXCEPT    TLSTR
      * SKRIVER KUNDNAVN
     C                   EXCEPT    TDSTR2
     C                   EXCEPT    PKUND
     C                   EXCEPT    TDEND
     C     1             DO        10            NR
     C                   EXCEPT    TDSTR
     C     B(NR)         IFGT      0
     C                   EXCEPT    BLASTR
     C                   END
     C     B(NR)         IFLT      0
     C                   EXCEPT    REDSTR
     C                   END
     C                   EXCEPT    PDET
     C     B(NR)         IFGT      0
     C                   EXCEPT    BLAEND
     C                   END
     C     B(NR)         IFLT      0
     C                   EXCEPT    REDEND
     C                   END
     C                   EXCEPT    TDEND
     C                   END
      * SKRIVER KUNDETOTAL
     C                   EXCEPT    TDSTR
     C     OCSTOT        IFGT      0
     C                   EXCEPT    BLASTR
     C                   END
     C     OCSTOT        IFLT      0
     C                   EXCEPT    REDSTR
     C                   END
     C                   EXCEPT    PDETT
     C     OCSTOT        IFGT      0
     C                   EXCEPT    BLAEND
     C                   END
     C     OCSTOT        IFLT      0
     C                   EXCEPT    REDEND
     C                   END
     C                   EXCEPT    TDEND
      * SKRIVER LINJESLUTT
     C                   EXCEPT    TLEND
      * SKRIVER LINJESTART
     CLR                 EXCEPT    TLSTR
      * SKRIVER TOTAL ALLE KUNDER
     CLR                 EXCEPT    TDSTR2
     CLR                 EXCEPT    PTOTH
     CLR                 EXCEPT    TDEND
     CLR   1             DO        10            NR
     CLR                 EXCEPT    TDSTR
     CLR   T(NR)         IFGT      0
     CLR                 EXCEPT    BLASTR
     CLR                 END
     CLR   T(NR)         IFLT      0
     CLR                 EXCEPT    REDSTR
     CLR                 END
     CLR                 EXCEPT    PTOT
     CLR   T(NR)         IFGT      0
     CLR                 EXCEPT    BLAEND
     CLR                 END
     CLR   T(NR)         IFLT      0
     CLR                 EXCEPT    REDEND
     CLR                 END
     CLR                 EXCEPT    TDEND
     CLR                 END
      * SKRIVER KUNDETOTAL
     CLR                 EXCEPT    TDSTR
     CLR   GRANDT        IFGT      0
     CLR                 EXCEPT    BLASTR
     CLR                 END
     CLR   GRANDT        IFLT      0
     CLR                 EXCEPT    REDSTR
     CLR                 END
     CLR                 EXCEPT    PTOTT
     CLR   GRANDT        IFGT      0
     CLR                 EXCEPT    BLAEND
     CLR                 END
     CLR   GRANDT        IFLT      0
     CLR                 EXCEPT    REDEND
     CLR                 END
     CLR                 EXCEPT    TDEND
      * SKRIVER LINJESLUTT
     CLR                 EXCEPT    TLEND
      * SKRIVER TABELLEND
     CLR                 EXCEPT    THEND
     C*******************************************************
     C     INIT          BEGSR
     C*******************************************************
     C     KUKEY         KLIST
     C                   KFLD                    KFTYPI
     C                   MOVEL     'GRITYPE'     KFTYPI           10
     C*
     C                   READ      FIRM                                   34
     C                   Z-ADD     0             NR                2 0
     C     KUKEY         SETLL     KUFAST
     C     *IN33         DOWEQ     '0'
     C     KUKEY         READE     KUFAST                                 33
     C     *IN33         IFEQ      '0'
     C     KFKOD         ANDNE     ' DEFN'
     C                   ADD       1             NR
     C                   MOVEL     KFTXT         A(NR)
     C                   END
     C                   END
     C                   EXCEPT    PHEAD
      * TABELL START
     C                   EXCEPT    THSTR
      * LINJE START
     C                   EXCEPT    TLSTR
      * SKRIVER NAVNE LINJE
     C                   EXCEPT    TDSTR2
     C                   EXCEPT    PHEADK
     C                   EXCEPT    TDEND
     C     1             DO        10            NR
     C                   EXCEPT    TDSTRH
     C                   EXCEPT    PHEADA
     C                   EXCEPT    TDEND
     C                   END
      * SKRIVER TOTALHEADING
     C                   EXCEPT    TDSTR2
     C                   EXCEPT    PHEADT
     C                   EXCEPT    TDEND
      * SKRIVER LINJESLUTT
     C                   EXCEPT    TLEND
     C                   ENDSR
     C*
     OLISTE     E            PHEAD            02
     O                       FIFT                45
     O                                           60 'FRA DATO.:'
     O                       OCSFDG              63
     O                                           64 '/'
     O                       OCSFMN              66
     O                                           67 '/'
     O                       OCSFAR              71
     OLISTE     E            PHEAD            03
     O                                           60 'TIL DATO.:'
     O                       OCSTDG              63
     O                                           64 '/'
     O                       OCSTMN              66
     O                                           67 '/'
     O                       OCSFAR              71
     OLISTE     E            PHEAD          1 04
     O                                           60 'KJØREDATO:'
     O                       OCSDAG              63
     O                                           64 '/'
     O                       OCSMND              66
     O                                           67 '/'
     O                       OCSAAR              71
     OLISTE     E            PHEADK         1
     O                                           24 'KUNDENAVN...............'
     O                                           48 '........................'
     OLISTE     E            PHEADA         1
     O                       A(NR)               25
     OLISTE     E            PHEADT         1
     O                                           10 'KUNDETOTAL'
     OLISTE     E            PHEAD2         1 01
     OLISTE     E            THSTR          1
     O                                           24 '<TABLE BORDER="1" WIDTH='
     O                                           48 '100%">                  '
     OLISTE     E            TLSTR          1
     O                                           24 '<TR>                    '
     OLISTE     E            TLEND          1
     O                                           24 '</TR>                   '
     OLISTE     E            TDSTRH         1
     O*<TD WIDTH="9%" BGCOLOR="#FFFF00">
     O                                           24 '<TD WIDTH="9%" BGCOLOR="'
     O                                           48 '#FFFF00">               '
     OLISTE     E            TDSTR          1
     O                                           24 '<TD ALIGN=RIGHT WIDTH="9'
     O                                           48 '%">                     '
     O* '<TD ALIGN=RIGHT BGCOLOR-
     OLISTE     E            TDSTR2         1
     O                                           24 '<TD WIDTH="9%" BGCOLOR="'
     O                                           48 '#FFFF00">               '
     OLISTE     E            TDEND          1
     O                                           24 '</TD>                   '
     OLISTE     E            THEND          1
     O                                           24 '</TABLE>                '
     OLISTE     E            PKUND          1
     O                       KNAVN               30
     OLISTE     E            PDET           1
     O                       B(NR)         J     15
     OLISTE     E            PDETT          1
     O                       OCSTOT        J     15
     OLISTE     E            PTOTH          1
     O                                           24 '* TOTAL ALLE KUNDER *   '
     OLISTE     E            PTOT           1
     O                       T(NR)         J     15
     OLISTE     E            PTOTT          1
     O                       GRANDT        J     65
     OLISTE     E            BLASTR         1
     O                                           24 '<FONT COLOR="#3333FF"><B'
     O                                           48 '>                       '
     OLISTE     E            BLAEND         1
     O                                           24 '</B></FONT>             '
     OLISTE     E            REDSTR         1
     O                                           24 '<FONT COLOR="#FF0000"><B'
     O                                           48 '>                       '
     OLISTE     E            REDEND         1
     O                                           24 '</B></FONT>             '
