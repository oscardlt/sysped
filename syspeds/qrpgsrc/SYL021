      *********************************************************************
      * Arbeid med mottak
      * PROGRAM BEHANDLER INPUT FRA SYL001 OG VISER HEADLING
      *
CRTPG+*  CRTPGM SYSPED/SYL021 MODULE(*LIBL/SYL021
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 KOLONNE FOR GODSNR/MRN-NR
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FLLMRF     IF   E           K DISK    USROPN
     FLLMRFL01  IF   E           K DISK    USROPN
     F                                     RENAME(LLMRFR:LLMRFR1)
     FLLGRPK05  IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          S             10
     D XXMRNR          S              5
     D TESTPRT         S              1
     D IFSFIL          S             30
     D Excel           s              1
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D  UPARM2                 1     64
     D  UGRUP                  1      8
     D  UKJED                  9     16
     D  UMRNR                 17     21
     D  UOPT                  22     22
     D  UKOPI                 23     23
     D  UUSN                  24     33
     D LDTAARA        UDS
     D  UFIRM                  1      2
     D  UDSAVD                15     18  0
     D  UDSSG                 19     21
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Set output skeleton html member name
     C                   exsr      LoadHtml
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      *
     C     LLMRFL01K     SETLL     LLMRFL01
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C     STHENTMR      TAG
     C     LLMRFL01K     READE     LLMRFL01                               50
      *
     C                   IF        *IN50 = *OFF
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTMR
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTMR
     C                   GOTO      STHENTMR
     C                   END
      *
     C     SLHENTMR      TAG
     C                   callp     wrtsection('tableend')
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
     C                   callp     wrtsection('*fini')
     C     SLUTT         tag
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      XXMRNR  = zhbgetvar('MRMRNR')
     C                   EVAL      MRMRNR  = c2n2(XXMRNR)
     C                   EVAL      TESTPRT = zhbgetvar('TESTPRT')
     C                   eval      excel   = zhbgetvar('excel')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * KUNDE
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('TITLE':'Reception list')
     C                   ELSE
     C                   callp     updhtmlvar('TITLE':'Mottaksrapport')
     C                   END
     C                   callp     updhtmlvar('WSNA':BIBESK)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetRow        begsr
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('MRMRNR':
     C                             %trim(%editc(MRMRNR:'Z')))
     C                   callp     updhtmlvar('MRLNA':MRLNA)
     C                   callp     updhtmlvar('MRDT':
     C                             %trim(%editW(MRDT:'    .  .  ')))
     C                   callp     updhtmlvar('MRLREF':MRLREF)
     C                   callp     updhtmlvar('MRKREF':MRKREF)
     C                   callp     updhtmlvar('MRMOTT':MRMOTT)
N01  C                   if        MRGN=*BLANKS
N01  C                   callp     updhtmlvar('MRGN':' ')
N01  C                   else
N01  C                   callp     updhtmlvar('MRGN':
N01  C                             '<font color=red><b>'+MRGN+'</b></font>')
N01  C                   endif
N01  C                   callp     updhtmlvar('MRTRNR':MRTRNR)
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklSpool   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SPLHTMDUM'
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     LoadHtml      begsr
cccccC                   IF        XSPRÅK = 'EN'
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/SYL021EN.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
cccccC                   ELSE
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/SYL021.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
cccccC                   END
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   open      BRPARF                               50
     C                   open      LLMRF                                50
     C                   open      LLMRFL01                             50
     C                   open      LLGrpK05                             50
     C                   open      CUNDL01                              50
      *
     C     LLMRFL01K     klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C     LLMRFK        klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C                   kfld                    MRMRNR
     C     CUND1K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     CUND1K        CHAIN     CUNDL01                            50
     C     BIKUNR        chain     LLGrpK05                           33
     C                   MOVE      BIBRID        PABRID
      *
     C                   EVAL      PABRID = WSUSER
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      * hvis print excel er innparam...
     c     Excel         IFEQ      'Y'
     C                   MOVE      GKGRP         XXGRP             8
     C                   MOVE      BIKUNR        XXKNR             8
     C                   CALL      'SYL021CEX'
     C                   PARM                    XXGRP
     C                   PARM                    XXKNR
     C                   PARM                    XXMRNR
     c                   end
      *
      * Hvis MR-nr er innparameter OG brukeren har lov- avslutt/ skriv mrap.
     C     LLMRFK        chain     LLMRF                              33
     C  N33              IF        MRST = ' '
     C                   MOVE      'N'           XAVSLUTT          1
     C                   ELSE
     C                   MOVE      'Y'           XAVSLUTT
     C                   MOVE      '1'           *IN33
     C                   END
     C  N33              MOVEL     'LLAVI'       PAID
     C  N33BRPARFK       chain     BRPARF                             33
     C  N33PAVRDN        COMP      0                                      33
     C                   IF        *IN33 = *OFF
     C     *DTAARA       DEFINE    *LDA          LDTAARA
     C                   IN        LDTAARA
     C                   MOVE      MRGRUP        UGRUP
     C                   MOVE      MRKJED        UKJED
     C                   MOVE      MRMRNR        UMRNR
     C                   IF        TESTPRT = 'J'
     C                   MOVE      'V'           UOPT
     C                   ELSE
     C                   MOVE      'W'           UOPT
     C                   END
     C                   MOVE      'N'           UKOPI
     C                   MOVE      UsrSpcName    UUSN
     C                   MOVE      BIFIRM        UFIRM
     C                   MOVE      'WEB'         UDSSG
     C                   Z-ADD     PAVRDN        UDSAVD
     C                   OUT       LDTAARA
     C                   CALL      'SYL021C'
     C                   PARM      UPARM2        UPARM1           64
      *
     C                   exsr      SetSklSpool
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   eval      IFSFil='LMR'+UKJED+UMRNR+UsrSpcName+'.pdf'
     C                   callp     updhtmlvar('IFSFIL':IFSFIL)
     C                   callp     wrtsection('top')
      *
     C                   END
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRPARF
     C                   close     BRIDF
     C                   close     LLMRF
     C                   close     LLMRFL01
     C                   close     LLGrpK05
     C                   close     CUNDL01
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
