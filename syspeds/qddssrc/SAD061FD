********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXX * 01 YBC PTF5 VIS OGSÅ TARIFFNR
PXXXX * 02 YBC PTF5 HAVNEKODE BRUKES IKKE
%%%%%%*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
P0XXXX* 03 YBC PTF6 TVUNGER OPPDATERING AV SÆRAVGIFT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S01  A*                                     WINDOW(*DFT 10 48 *NOMSGLIN)
N01  A                                      WINDOW(*DFT 10 65 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            SDKDSÆ         3A  O  5  5
     A            SDBLSÆ         7  2O  5  9EDTCDE(4)
     A            SDHKOD         2  0O  5 18EDTCDE(4)
     A            SDVF           1A  O  5 21
     A            SDTRM          2  0O  5 24EDTCDE(4)
     A            WSTXT         20A  O  5 27
N03  A            WSST           7A  O  5 48
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA05(05 'Frisk')
     A                                      CA10(10 'NESTE')
     A                                      CA03(12 'AVBRYT')
     A                                      CA12(12 'AVBRYT')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  1 35SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD061D'
     A                                      COLOR(BLU)
     A                                  1 10'Plukking av avgift'
     A                                      COLOR(WHT)
     A                                  2  2'Avg.KD:'
E01  A            UKDAÆ          2A  O  2 10COLOR(WHT)
S02  A*                                 2 14'Tollstd:'
S02  A*           UHKOD          2  0O  2 23EDTCDE(4)
S02  A*                                     COLOR(WHT)
     A                                  2 27'VF:'
E01  A            UVF            1   O  2 31COLOR(WHT)
     A                                  2 35'Tr.måte:'
     A            UTRM           2  0O  2 44EDTCDE(4)
N01  A                                      COLOR(WHT)
N01  A                                  2 47'Tar.nr:'
N01  A            UVNT           8  0O  2 55COLOR(WHT)
     A                                  3  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  4  2'Op Skv avg.sats TS VF TM Beskrivel-
E03  A                                      se          Status'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                      WINDOW(WINDOWSZ)
     A N30                              1  2'F5=Forny  F12=Forrige'
     A                                      COLOR(BLU)
     A  30                              1  2MSGCON(045 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 27'F10=Neste avg.kode'
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'AVBRYT')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD061D'
     A                                      COLOR(BLU)
     A                                  1 10'Plukking av avgift'
     A                                      COLOR(WHT)
     A                                  2  2'Avg.KD:'
E01  A            UKDAÆ          2A  O  2 10COLOR(WHT)
S02  A*                                 2 14'Tollstd:'
S02  A*           UHKOD          2  0O  2 23EDTCDE(4)
S02  A*                                     COLOR(WHT)
     A                                  2 27'VF:'
     A            UVF            1   O  2 31COLOR(WHT)
     A                                  2 35'Tr.måte:'
     A            UTRM           2  0O  2 44EDTCDE(4)
N01  A                                      COLOR(WHT)
N01  A                                  2 47'Tar.nr:'
N01  A            UVNT           8  0O  2 55COLOR(WHT)
     A                                  3  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  4  2'Op Skv avg.sats TS VF TM Beskrivel-
E03  A                                      se          Status'
     A                                      COLOR(WHT)
