      *********************************************************************
      *
      * PROGRAM BEHANDLER INPUT FRA SS003 OG VISER DEL-LEVERING
      *
CRTPG+*  CRTPGM SYSPED/SS007 MODULE(*LIBL/SS007
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FGSSPAF    IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WINT            S              6
     D WSNA            S             30
     D WSONO           S              7
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *------------------
     C                   exsr      SetFound
     C                   callp     wrtsection('found')
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     GSSPAF
     C                   close     BRIDF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WINT    = zhbgetvar('WINT')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      WSONO   = zhbgetvar('WSONO')
     C                   MOVE      WSONO         PAONO
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * Protocol
     C                   callp     updHtmlVar('PROTOCOL':S_Protocol)
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('WINT':WINT)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$found (display a boat)
      *=====================================================================
     C     SetFound      begsr
      *
      * Sett inn i HTMLvariabler
     C                   MOVE      *BLANKS       PASTAT           30
     C                   SELECT
     C                   WHEN      PAPKD = ' '
     C                   EVAL      PASTAT = 'PARTIAL DELIVERY (NOT COMPL.)'
     C                   WHEN      PAPKD = 'P'
     C                   EVAL      PASTAT = 'PARTIAL DELIVERY (COMPLETE)'
     C                   WHEN      PAPKD = 'C'
     C                   EVAL      PASTAT = 'COMPLETE DELIVERY'
     C                   ENDSL
     C                   callp     updhtmlvar('PASTAT':PASTAT)
     C                   callp     updhtmlvar('PATXT1':PATXT1)
     C                   callp     updhtmlvar('PATXT2':PATXT2)
     C                   callp     updhtmlvar('PATXT3':PATXT3)
     C                   callp     updhtmlvar('PATXT4':PATXT4)
     C                   callp     updhtmlvar('PATXT5':PATXT5)
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton member before its read into memory
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS007'
     C                   endsr
      *=====================================================================
      *  Retrieve environment variables
      *=====================================================================
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      GSSPAF                               50
     C     PAONO         CHAIN     GSSPAF                             50
      *
     C                   endsr
