     FFSR00F    IF A E             DISK
     FLOGF      IF A E             DISK
     FCNFF      UF   E             DISK
     FLOCL01    IF   E           K DISK
     FISUL01    IF   E           K DISK
     FKUFAST    IF   E           K DISK
      *___________________________________
      * Definition of the parameter AUTINF
      *"""""""""""""""""""""""""""""""""""
     D                 DS
     D  AUTINF                 1     11
     D  AUTALN                 1      3
     D  AUTAWB                 4     11
      *___________________________________
      * Parameter list
      *"""""""""""""""""""""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WWAUTI           11
     C                   PARM                    WWUSER           17
     C                   PARM                    URECN             9
     C     KUFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   EVAL      KFTYP = 'SSSYCGITDV'
     C                   EVAL      KFKOD = ' DEFN'
     C     KUFASTK       CHAIN     KUFAST                             33
     C  N33              MOVEL     KFTXTE        WWUSER
     C   33              MOVEL     'BING'        WWUSER
     C                   MOVE      URECN         WRECN             9 0
      *____________________________________________________________
      * Initialize subroutine
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   EXSR      INIT
      *____________________________________________________________
      * Set autotransmit values
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     WWAUTI        AUTINF
     C                   MOVEL     AUTALN        S0211
     C                   MOVEL     AUTAWB        S0213
      *____________________________________________________________
      * Find Participant ID based on airline prefix
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      *OFF          *IN30
     C     S0211         CHAIN     ISUL01                             90
     C                   EXSR      SNDRQS
     C                   EXSR      RCVRQS
     C     *IN30         IFEQ      '1'
     C                   MOVEL     AUTINF        WWAUTI
     C                   ENDIF
     C                   GOTO      ENDPGM
     C     ENDPGM        TAG
     C                   MOVE      WRECN         URECN
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *****************************************************************
     C     INIT          BEGSR
      *****************************************************************
     C                   MOVEL     'SMADTATQ'    QTNAME           10
     C                   MOVEL     'SMADTARQ'    QRNAME           10
     C                   MOVEL     '*LIBL'       QTLIB            10
     C                   MOVEL     '*LIBL'       QRLIB            10
     C                   MOVEL     *BLANKS       QRFIEL            9
     C                   Z-ADD     9             QTLEN             5 0
     C                   Z-ADD     9             QRLEN             5 0
     C     1             CHAIN(N)  CNFF                               90
     C                   Z-ADD     CNTFSR        QWAIT             5 0
     C     WWUSER        CHAIN     LOCL01                             90
     C                   MOVE      'EQ'          QORDER            2
     C                   Z-ADD     9             QKILEN            3 0
     C                   MOVEL     *BLANKS       QKEY              9
     C                   MOVEL     *BLANKS       QBACK             9
     C                   MOVEL     *BLANKS       QTFIEL            9
     C                   Z-ADD     10            QSNDRL            3 0
     C                   MOVEL     *BLANKS       QSNDR            10
     C                   Z-ADD     0             WRECN
      *____________________________________________________________
      * Dummy read of FSR00F, FSR03F and LOGF
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     *IN90         IFEQ      *ON
     C     *IN90         ANDEQ     *OFF
     C                   READ      FSR00F                                 90
     C                   READ      LOGF                                   90
     C                   ENDIF
     C                   ENDSR
      *****************************************************************
     C     RCVRQS        BEGSR
      * Receive FSA status request
      *****************************************************************
     C                   MOVEL     CNRECN        QKEY
     C                   MOVEL     CNRECN        QBACK
     C                   CALL      'QRCVDTAQ'
     C                   PARM                    QRNAME
     C                   PARM                    QRLIB
     C                   PARM                    QRLEN
     C                   PARM                    QRFIEL
     C                   PARM                    QWAIT
     C                   PARM                    QORDER
     C                   PARM                    QKILEN
     C                   PARM                    QKEY
     C                   PARM                    QSNDRL
     C                   PARM                    QSNDR
     C                   MOVEL     QRFIEL        WRECN
     C                   ENDSR
      *****************************************************************
     C     SNDRQS        BEGSR
      * Send FSR status request
      *****************************************************************
      *____________________________________________________________
      * Retrieve last used recordnumber from CNFF and update
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     CNFF                               90
     C     *IN90         IFEQ      '0'
     C                   ADD       1             CNRECN
     C                   UPDATE    CNFR
     C                   CLEAR                   LOGR
     C                   Z-ADD     CNRECN        S00REC
     C                   Z-ADD     CNRECN        LGRECN
     C                   Z-ADD     CNRECN        LGTRID
      *____________________________________________________________
      * Retreive data from local file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     LOID          LGSID
     C                   MOVEL     ISPART        LGID
      *____________________________________________________________
      * Write record to FSR master file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   WRITE     FSR00R
      *____________________________________________________________
      * Write record to LOG file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     *BLANKS       LGIDNE
     C                   MOVEL     'FSR'         LGMSID
     C                   MOVEL     *BLANKS       LGSNET
     C                   MOVEL     'T'           LGTR
     C                   MOVEL     'A'           LGSTAT
     C                   MOVEL     UDATE         LGDATE
     C                   TIME                    LGTIME

     C                   WRITE     LOGR
      *_____________________________________________________________
      * Send record to dataq SMADTATQ and wait for reply in SMADTARQ
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     LGRECN        QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM                    QTNAME
     C                   PARM                    QTLIB
     C                   PARM                    QTLEN
     C                   PARM                    QTFIEL

     C                   END

     C                   ENDSR
