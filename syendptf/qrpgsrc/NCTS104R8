      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE60                    ***
      *** Control Decision Notification                             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH    UF   E           K DISK
     FNCTSEH05  UF   E           K DISK    RENAME(NCTSEHR:NCTSEHR5)
     FNCTSERR   IF A E           K DISK
     FTVINF     O  A E             DISK
     FTRKODL01  IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM5     IF   E           K DISK    RENAME(EDIMR:EDIMR5)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104r8
     d ncts104r8       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104r8       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d xn              s              3s 0
     d x_thtrdt        s              8
     d x_thtrnr        s                   like(thtrnr)
     d x_IE60          s              1a
     d x_sts           s              1a
     d x_typ           s              3a
     d x_dtyp          s              4a
     d x_sn            s              3s 0
     d x_hol           s             17a
     d x_rep           s             17a
     d x_xrvrd         s                   like(xrvrd)
     d x_opdft         s              1
     d wdt             s              8
     d wtm             s              6
     d                 ds
     d x_cdt                   1      8  0
     d x_cdta                  1      8
     d                 ds
     d x_ctm                   1      6  0
     d x_ctma                  1      6
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
      *
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR5
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x_opdft = 'N';

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC060C';
             x_ie60 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'CustomsOfficeOfDeparture';
           when xrnv = 'HolderOfTheTransitProcedure';
           //exsr srxml2;
           when xrnv = 'Representative';
           //exsr srxml3;
           when xrnv = 'TypeOfControls';
             exsr srxml4;
           when xrnv = 'RequestedDocument';
             exsr srxml5;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie60 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;
         f0077 = 'INF';
         f4815 = *blanks;
         xn    = 0;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'LRN';
             x0068 = xrverd;
             ft(1) = 'LRN: ' + %trim(x0068);
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
             if ft(1) = *blanks;
               ft(1) = 'MRN: ' + %trim(x_thtrnr);
             else;
               ft(1) = %trim(ft(1)) + '   MRN: ' + %trim(x_thtrnr);
             endif;
           when xrnv = 'controlNotificationDateAndTime';
             x_cdta = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
             x_ctma = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                   + %subst(xrverd:18:2);
             ft(2) = 'Control date and time: ' + %trim(xrverd);
           when xrnv = 'notificationType';
             tkunik = '384';
             tkkode = xrverd;
             chain (tkunik: tkkode) trkodl01;
             if not %found;
               tktxtn = *blanks;
             endif;
             ft(3) = 'Type: ' + %trim(tkkode) + ' ' + %trim(tktxtn);
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         write tvinff;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'identificationNumber';
             x_hol = xrverd;
           when xrnv = 'status';
             x_sts = xrverd;
           when xrnv = 'name';
             ft(3)  = 'Holder: ' + %trim(x_hol) + ' ' + %trim(xrverd);
             x_opdft = 'J';
           when xrnv = 'contactPerson';
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'identificationNumber';
             x_rep = xrverd;
           when xrnv = 'status';
             x_sts = xrverd;
             ft(4)  = 'Representative: ' + %trim(x_rep)
                     + '  Status: ' + %trim(x_sts);
             x_opdft = 'J';
           when xrnv = 'contactPerson';
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'sequenceNumber';
             x_sn = %dec(xrverd:3:0);
           when xrnv = 'type';
             x_typ = xrverd;
             if x_opdft = 'J';
               write tvinff;
               eval x_opdft = 'N';
             endif;
             ft     = *blanks;
             fln    = fln + 1;
             f0077  = '716';
             f4815  = 'CL';
             tkkode = x_typ;
             tkunik = '716';
             chain (tkunik: tkkode) trkodl01;
             if not %found;
               tktxtn = x_typ;
             endif;
             ft(1)  = %trim(%editc(x_sn:'Z')) + '. ' + %trim(x_typ) + ' '
                    + %trim(tktxtn);
             write tvinff;
           when xrnv = 'text';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             if x_opdft = 'J';
               write tvinff;
               eval x_opdft = 'N';
             endif;
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = 'TXT';
             f4815  = *blanks;
             ft(1)  = %subst(xrvrd:1:70);
             ft(2)  = %subst(xrvrd:71:70);
             ft(3)  = %subst(xrvrd:141:70);
             ft(4)  = %subst(xrvrd:211:70);
             ft(5)  = %subst(xrvrd:281:70);
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml5;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'sequenceNumber';
             x_sn = %dec(xrverd:3:0);
           when xrnv = 'documentType';
             x_dtyp = xrverd;
             tkunik = '215';
             tkkode = xrverd;
             chain (tkunik: tkkode) trkodl01;
             if not %found;
               tktxtn = x_dtyp;
             endif;
           when xrnv = 'description';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             if x_opdft = 'J';
               write tvinff;
               eval x_opdft = 'N';
             endif;
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = '215';
             f4815  = 'CL';
             ft(1)  = %trim(%editc(x_sn:'Z')) + '. ' + %trim(x_dtyp)
                     + ': ' + %trim(tktxtn);
             ft(2)  = %subst(xrvrd:1:70);
             ft(3)  = %subst(xrvrd:71:70);
             ft(4)  = %subst(xrvrd:141:70);
             ft(5)  = %subst(xrvrd:211:70);
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         if x_opdft = 'J';
           ft     = *blanks;
           xn     = 0;
           fln    = fln + 1;
           f0077  = '215';
           f4815  = 'CL';
           ft(1)  = %trim(%editc(x_sn:'Z')) + '. ' + %trim(x_dtyp)
                   + ': ' + %trim(tktxtn);
           write tvinff;
         endif;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         if x0068 = *blanks;
           chain x_thtrnr nctseh05;
           if %found;
             if thst <> 'P' and thst <> 'Z';
               thst = 'V';
             endif;
             thst060 = 'J';
             update nctsehr5;
             x0068 = thlrn;
             // Hent sendings info.
             chain x0068 edim5;
           else;
             temf = *blanks;
             setll (' ': x_thtrnr) nctserr;
             reade (' ': x_thtrnr) nctserr;
             dow not %eof;
               if temf = '060';
                 leave;
               endif;
               reade (' ': x_thtrnr) nctserr;
             enddo;
             if temf <> '060';
               testat = *blanks;
               temf   = '060';
               tetrnr = x_thtrnr;
               tesn   = xrsn;
               write nctserrr;
             endif;
             *inlr = *on;
             return;
           endif;
         else;
           chain x0068 edim5;
           if %found;
             chain (xavd: xtdn) nctseh;
             if %found;
               if thst <> 'P' and thst <> 'Z';
                 thst = 'V';
               endif;
               thst060 = 'J';
               update nctsehr;
             endif;
           endif;
         endif;

         m0065  = 'XMLRES';
         if x0068 = *blanks;
           m0068  = x_thtrnr;
         else;
           m0068  = x0068;
         endif;
         m1001  = 'TET';
         m1225  = '060';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '060';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = 'T60';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'Control decision notification: '
                  + %trim(%editw(x_cdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_ctm:'  :  :  '));
         tttexl = 'Kontroll melding fra tollvesenet: '
                  + %trim(%editw(x_cdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_ctm:'  :  :  '));
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
