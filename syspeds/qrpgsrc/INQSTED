      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/INQSTED MODULE(*LIBL/INQSTED *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
ODDEV *ODDEV   PTF9 ODDEVEN-logikk
P9XXX * 02 YBC PTF9 VARLK  : Variable navn som mottar landkode
      *             VARKOD : Variable navn som mottar stedskode
      *             WSFORMS: Formnavn som mottar verdi
      *             WSREFR : J/Y betyr program for mottak av verdi skal refreshes
P9XXX * 03 jov PTF9 Hvis kun land er oppgitt vis alle (eller max) fra det landet
P9XXX * 04 jov PTF9 Kode for vis kun... P = Potal code A = Alfa codes
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSted2A    IF   E           K DISK    USROPN
     FSted2S    IF   E           K DISK    USROPN
     F                                     RENAME(STED2R:STED2RS)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
OddEvD OddEven         s             10
      * Client input variables
     D SOKLK           s              2
xxxxxD SOKNVN          s             30    varying
xxxxxD SOKNVN2         s             30
     D USER            s             10
     D VARLK           s             10
     D VARKOD          s             10
N02  D WSFORMS         s             20
N02  D WSREFR          s              1
N02  D REFRESH         s             50
N04  D WSKUNPA         s              1
     D MaxV            s              5
     D MaxVis          s              5  0
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      SOKLK   = zhbgetvar('SOKLK')
     C                   eval      SOKLK   = uppify(SOKLK)
     C                   eval      SOKNVN  = zhbgetvar('SOKNVN')
     C                   eval      SOKNVN  = uppify(SOKNVN)
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     C                   eval      VARLK   = zhbgetvar('VARLK')
     C                   eval      VARKOD  = zhbgetvar('VARKOD')
N02  C                   eval      WSFORMS = zhbgetvar('WSFORMS')
N02  C                   IF        WSFORMS = *BLANKS
N02  C                   EVAL      WSFORMS = 'forms[0]'
N02  C                   END
N02  C                   eval      WSREFR  = zhbgetvar('WSREFR')
N02  C                   IF        ((((WSREFR = 'J') OR
N02  C                                (WSREFR = 'j')) OR
N02  C                                (WSREFR = 'Y')) OR
N02  C                                (WSREFR = 'y'))
N02  C                   eval      REFRESH = 'self.opener.document.'
N02  C                             + %trim(WSFORMS)
N02  C                             + '.submit();'
N02  C                   END
N04   * Vis kun Postnr (P) eller vis kun Alfakode (A)
N04  C                   eval      WSKUNPA = zhbgetvar('WSKUNPA')
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      USER = zhbgetvar('USER')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     Sted2A
     C                   CLOSE     Sted2S
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C*                  callp     gethtml('HTMLSRC':
     C*                            '*LIBL':
     C*                            'INQSTED':'/CGITAG/')
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'INQSTED'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('SOKLK':SOKLK)
     C                   callp     updHTMLvar('SOKNVN':SOKNVN)
     C                   callp     updHTMLvar('VARLK':VARLK)
     C                   callp     updHTMLvar('VARKOD':VARKOD)
N02  C                   callp     updHTMLvar('wsforms':WSFORMS)
N02  C                   callp     updHTMLvar('wsrefr':WSREFR)
N02  C                   callp     updHTMLvar('refresh':REFRESH)
N04  C                   callp     updHTMLvar('WSKUNPA' :WSKUNPA)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     c                   Move      *zeros        AntVist           5 0
     c                   eval      SOKNVN2   = SOKNVN
     C     SOKNVN        IFEQ      *BLANKS
N03  C     SOKLK         andeq     *BLANKS
     C                   GOTO      UTLES
     C                   END
      *
     C     SOKLK         IFNE      *BLANKS
     C     LkNvnKey      setll     Sted2A
     C     SOKLK         reade     Sted2A                                 33
     C                   ELSE
     C     NvnKey        setll     Sted2S
     C                   read      Sted2S                                 33
     C                   END
N03  C     *in33         DOWEQ     *OFF
     C     SOKNVN        Ifne      *blanks
N01  C     SOKNVN        SCAN      ST2NVN                                 34
     c     *IN34         CaBEQ     *OFF          UtLes
N03  c                   end
     C                   exsr      exclude
     c     EXCL          IFEQ      'N'
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('ST2KOD':ST2KOD)
     C                   callp     updHTMLvar('ST2NVN':ST2NVN)
     C                   callp     updHTMLvar('ST2LK':ST2LK)
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C                   END
     C     SOKLK         IFNE      *BLANKS
     C     SOKLK         reade     Sted2A                                 33
     C                   ELSE
     C                   read      Sted2S                                 33
     C                   END
     C                   end
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   Move      'J'           EXCL              1
      * diverse tester - om ikke bestått hopp ut (med J i EXCL)
N04   * vis kun postnummer (numeriske)
N04  c     WSKUNPA       Ifeq      'P'
N04  c     ST2KOD        cablt     '00000'       UtExcl
N04  c     ST2KOD        cabgt     '99999'       UtExcl
N04  c                   end
N04   * vis kun Alfakoder
N04  c     WSKUNPA       Ifeq      'A'
N04  c     ST2KOD        andge     '00000'
N04  c     ST2KOD        andle     '99999'
N04  c                   Goto      UtExcl
N04  c                   end
      * Posten skal med på liste
     C                   Move      'N'           EXCL              1
      *
     C     UTEXCL        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      Sted2A
     C                   OPEN      Sted2S
      * Key for Sted2A  landkode + stedsnavn
     C     LkNvnKey      KLIST
     C                   KFLD                    SOKLK
     C                   KFLD                    SOKNVN2
      * Key for Sted2S - Stedsnavn
     C     NvnKey        KLIST
     C                   KFLD                    SOKNVN2
      *
     C                   endsr
