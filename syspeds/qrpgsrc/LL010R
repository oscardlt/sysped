     FLL010D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FLLHEAD01  IF   E           K DISK
     FLLGRPK05  IF   E           K DISK
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  PNAVN                  1     10
     D  KNAVN                  1     30
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    WSPNR         WWPNR
     C     *LIKE         DEFINE    LHSTA         YHSTA
     C     *ENTRY        PLIST
     C                   PARM                    UPDATE            1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSPNR
     C     CUN1GK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSGRP
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    GMPNR
     C     LLH01K        KLIST
     C                   KFLD                    YHSTA
     C                   KFLD                    WSGRP
     C     LLH1KA        KLIST
     C                   KFLD                    YHSTA
     C                   KFLD                    GKGRP
     C                   KFLD                    GKPNR
      *
     C                   MOVE      99999999      GMGRP
     C                   MOVE      99999999      WSGRP
     C                   MOVE      'YBC0976'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     30            XMSGL             4 0          LENGDE PÅ MELDING
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          XTXT1            30
     C                   MOVEL     XTXT1         WSGNV
     C                   READ      FIRM                                   55
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENTMS        BEGSR
     C***********************************************
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XMSG             62
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
     C     START         TAG
     C                   MOVE      WSPNR         WWPNR
     C                   MOVE      *ZEROS        SUMMLD
     C                   MOVE      *ZEROS        SUMSTK
     C                   MOVE      *ZEROS        SUMSTL
     C                   MOVE      *ZEROS        SUMESK
     C                   MOVE      *ZEROS        SUMKRT
     C                   MOVE      *ZEROS        SUMPAL
     C                   MOVE      *ZEROS        SUMVEK
     C                   MOVE      *ZEROS        SUMKUB
     C                   MOVE      *ZEROS        GMPNR
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   MOVE      'J'           XMEDPL            1
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C                   MOVE      'N'           XNYK              1
     C     START0        TAG
     C     WSGRP         IFEQ      99999999
     C     WSPNR         ANDEQ     0
     C     WSPNR         IFEQ      0
     C     *LOVAL        SETLL     LLGRPK05
     C                   READ      LLGRPK05                               94
     C     *IN94         IFEQ      '0'
     C     LLH1KA        SETLL     LLHEAD01
     C     LLH1KA        READE     LLHEAD01                               94
     C     *IN94         IFEQ      '1'
     C                   MOVE      GKGRP         GMGRP2
     C                   MOVE      GKPNR         GMPNR
     C                   MOVE      'J'           XNYK
     C                   MOVE      '0'           *IN94
     C                   END
     C                   END
     C                   ELSE
     C                   END
     C                   ELSE
     C     LLH01K        SETLL     LLHEAD01
     C     LLH01K        READE     LLHEAD01                               94
     C                   END
     C   94WSGRP         IFEQ      99999999
     C     WSPNR         ANDNE     0
     C                   MOVE      *ZEROS        WSPNR
     C                   GOTO      START0
     C                   END
      *
     C     *IN94         DOWEQ     '0'
      * BRUDD???
     C     GMPNR         IFNE      *ZEROS
     C     GMPNR         ANDNE     LHPNR
     C     KUNKEY        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C                   SELECT
     C                   WHEN      ((XMEDPL = 'J') AND
     C                              (SUMMLD > 0))
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   WHEN      ((XMEDPL = 'N') AND
     C                              (SUMMLD = 0))
     C     GMPNR         CHAIN     LLGRPK05                           33
     C                   MOVEL     GKGRP         GMGRP2
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   ENDSL
     C                   MOVE      *ZEROS        SUMMLD
     C                   MOVE      *ZEROS        SUMSTK
     C                   MOVE      *ZEROS        SUMSTL
     C                   MOVE      *ZEROS        SUMESK
     C                   MOVE      *ZEROS        SUMKRT
     C                   MOVE      *ZEROS        SUMPAL
     C                   MOVE      *ZEROS        SUMVEK
     C                   MOVE      *ZEROS        SUMKUB
     C                   MOVE      *ZEROS        GMPNR
     C                   MOVE      *ZEROS        GMGRP2
     C                   END
     C     XNYK          CABEQ     'J'           NEXT
      *
      * FILTER - HOPP OVER VED MISSMATCH
     C     WSPNR         IFNE      0
     C     WSPNR         ANDNE     LHPNR
     C                   GOTO      NEXT
     C                   END
      *
      * AKKUMULER - HUSK BRUDDVERDI
     C                   ADD       1             SUMMLD
     C                   ADD       LHANT0        SUMSTK
     C                   ADD       LHANTL        SUMSTL
     C                   ADD       LHANT1        SUMESK
     C                   ADD       LHANT2        SUMKRT
     C                   ADD       LHANT3        SUMPAL
     C     LHWVEK        IFGT      0
     C                   ADD       LHWVEK        SUMVEK
     C                   ELSE
     C                   ADD       LHVEK         SUMVEK
     C                   END
     C     LHWKUB        IFGT      0
     C                   ADD(H)    LHWKUB        SUMKUB
     C                   ELSE
     C                   ADD(H)    LHKUB         SUMKUB
     C                   END
     C                   MOVE      LHGRP         GMGRP2
     C                   MOVE      LHPNR         GMPNR
      *
     C     NEXT          TAG
     C     WSGRP         IFEQ      99999999
     C     WSPNR         IFEQ      0
     C     XNYK          IFEQ      'J'
     C                   READ      LLGRPK05                               94
     C   94              LEAVE
     C     LLH1KA        SETLL     LLHEAD01
     C                   END
     C     LLH1KA        READE     LLHEAD01                               94
     C     *IN94         IFEQ      '1'
     C     SUMMLD        IFEQ      0
     C                   MOVE      GKGRP         GMGRP2
     C                   MOVE      GKPNR         GMPNR
     C                   ELSE
     C                   MOVE      *ZEROS        LHPNR
     C                   END
     C                   MOVE      'J'           XNYK
     C                   MOVE      '0'           *IN94
     C                   ELSE
     C                   MOVE      'N'           XNYK
     C                   END
     C                   ELSE
     C     YHSTA         READE     LLHEAD01                               94
     C                   END
     C                   ELSE
     C     LLH01K        READE     LLHEAD01                               94
     C                   END
     C  N94              END
      *
      * MINST EN POST LEST... LEGG UT SISTE BRUDDSUM
     C     GMPNR         IFNE      *ZEROS
     C     KUNKEY        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C                   SELECT
     C                   WHEN      ((XMEDPL = 'J') AND
     C                              (SUMMLD > 0))
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   WHEN      ((XMEDPL = 'N') AND
     C                              (SUMMLD = 0))
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   ENDSL
     C                   END
     C                   IF        XMEDPL = 'J'
     C                   MOVE      'N'           XMEDPL
     C                   MOVE      WSPNR         WWPNR
     C                   MOVE      *ZEROS        GMPNR
     C                   GOTO      START0
     C                   END
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C     ZRECNO        IFNE      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *_______________
      * Ingen record fantes??
      *_______________
     C     START3        TAG
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV1154'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     BLANKREC
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NOREC
     C                   ELSE
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C  N30              Z-ADD     8             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   MOVEA     '0000'        *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C     *IN02         IFEQ      '1'
     C     WSGRP         IFEQ      99999999
     C                   MOVE      *ZEROS        UGRP
     C                   ELSE
     C                   MOVE      WSGRP         UGRP              8
     C                   END
     C     WSPNR         IFEQ      0
     C                   MOVE      ' '           UMODE             1
     C                   MOVE      *ZEROS        UPNR              8
     C                   ELSE
     C                   MOVE      'S'           UMODE
     C                   MOVE      WSPNR         UPNR
     C                   END
     C                   CALL      'LL030R'
     C                   PARM                    UMODE
     C                   PARM                    UGRP
     C                   PARM                    UPNR
     C                   GOTO      START3
     C                   END
      *
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKKUN
     C                   GOTO      START
     C                   END
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *
      * TEST INNTASTET GRUPPEKUNDENR
     C     WSGRP         IFNE      GMGRP
     C     WSGRP         IFEQ      99999999
     C                   MOVE      *BLANKS       WSGNV
     C                   MOVEL     XTXT1         WSGNV
     C                   ELSE
     C     WSGRP         IFEQ      0
     C                   MOVE      *BLANKS       WSGNV
     C                   ELSE
     C     CUN1GK        CHAIN     CUNDL01                            89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'SPK0001'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     15            POSNBR
     C                   GOTO      START3
     C                   ELSE
     C                   MOVE      KNAVN         WSGNV
     C                   END
     C                   END
     C                   END
     C                   MOVE      WSGRP         GMGRP             8 0
     C                   GOTO      START
     C                   END
      *
      * TEST INNTASTET LAGERKUNDENR
     C     WSPNR         IFNE      0
     C     CUND1K        CHAIN     CUNDL01                            90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'SPK0001'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   GOTO      START3
     C                   END
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C                   SELECT
     C     WSPNR         WHENNE    0
     C     WSPNR         CHAIN     LLGRPK05                           33
     C                   CALL      'LL011R'
     C                   PARM                    GKGRP
     C                   PARM                    WSPNR
     C                   PARM                    UPDATE
     C                   PARM                    UALN              7 0
     C                   GOTO      START
     C     WSPNR         WHENNE    WWPNR
     C                   GOTO      START
     C                   ENDSL
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C     WSNR          IFEQ      '1'
     C                   CALL      'LL011R'
     C                   PARM                    GMGRP2
     C                   PARM                    GMPNR
     C                   PARM                    UPDATE
     C                   PARM                    UALN              7 0
     C                   END
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
      *
     C  N52              END
      *
     C                   GOTO      START3
      *
     C     UTHOV         ENDSR
      *
      ***********************************************
     C     SØKKUN        BEGSR
      ***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     16
     C     POSNBR        ANDLE     22
     C                   MOVE      'SØKGRP  '    UTYPE             8
     C                   CALL      'LL004R'
     C                   PARM                    UTYPE
     C                   PARM                    WSGRP
     C                   PARM                    WSPNR
     C                   END
      *
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     4
     C     POSNBR        ANDLE     11
     C                   MOVE      WSGRP         UTYPE
     C                   CALL      'LL004R'
     C                   PARM                    UTYPE
     C                   PARM                    WSGRP
     C                   PARM                    WSPNR
     C                   END
      *
     C                   ENDSR
      *
