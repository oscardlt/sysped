     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLAVD0' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 20 70 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            GOHKOD    R        O  4  2REFFLD(GODSHR/GOHKOD *LIBL/GODSHF)
     A            GOHUSR    R        O  4  4REFFLD(GODSHR/GOHUSR *LIBL/GODSHF)
     A            GOHDAT    R        O  4 15REFFLD(GODSHR/GOHDAT *LIBL/GODSHF)
     A                                      EDTWRD('    .  .  ')
     A            GOHTIM    R        O  4 26REFFLD(GODSHR/GOHTIM *LIBL/GODSHF)
     A            GOTRNR    R        O  4 33REFFLD(GODSHR/GOTRNR *LIBL/GODSHF)
     A            GOHPGM    R        O  4 54REFFLD(GODSHR/GOHPGM *LIBL/GODSHF)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0999)
     A                                      SFLPAG(0017)
     A                                      WINDOW(WINDOWSZ)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYTL11 '
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(040 SHU1079 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(007 SHU1080 *LIBL/SYMSGF)
     A            GOGN          15A  O  2 10
     A                                  3  2MSGCON(068 SHU1081 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N30                              1  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                  1  2'SYTL11 '
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(040 SHU1079 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(007 SHU1080 *LIBL/SYMSGF)
     A            GOGN          15A  O  2 10
     A                                  3  2MSGCON(068 SHU1081 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(060 SHU1082 *LIBL/SYMSGF)
     A                                      COLOR(RED)
