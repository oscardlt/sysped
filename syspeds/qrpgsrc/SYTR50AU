      *
      ************************************************************
      * GÅR IGJENNOM ALLE TURER MED STATUS "A"  ("FERDIG PLANL") *
      * DERSOM DAGENS DATO ER STØRRE ENN FRADATO                 *
      * PÅ TUREN OPPDATERES STATUS TIL  "B"     ("GÅTT")         *
      *                                                          *
      * GÅR IGJENNOM ALLE TURER MED STATUS "B"  ("GÅTT")         *
      * DERSOM DAGENS DATO ER STØRRE ENN TILDATO                 *
      * PÅ TUREN OPPDATERES STATUS TIL  "C"     ("LEVERT")       *
      ************************************************************
      *
     FTURER1    UF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTV     IF   E           K DISK
     FHEAD8     IF   E           K DISK
     FDISP      UF   E           K DISK
     D                 DS
     D  XUDTÅ                  1      4  0
     D  XUDTM                  5      6  0
     D  XUDTD                  7      8  0
     D  XUDT                   1      8  0
     C*
     C                   EXSR      INIT
     C                   EXSR      TURSTA
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TURSTA        BEGSR
     C***********************************************
     C**OPPDATER STATUS PÅ TURER / DISP.OPPDRAG  DER TUREN ER GÅTT
     C*                            (UDATE STØRRE ENN AVGANGSDATO (TUDT))
     C*              OG PÅ TURER / DISP.OPPDRAG  DER TUREN ER FERDIG
     C*                            (UDATE STØRRE ENN LEVERINGSDATO (TUDTT)
     C*
     C*
     C* TURER MED STATUS A - DERSOM KOMMET FORBI AVG.DAG - A ---> B
     C                   SETOFF                                           94
     C                   MOVE      'A'           YST               1
     C     TURE1K        SETLL     TURER1
     C     YST           READE     TURER1                                 94
     C*
     C     *IN94         DOWNE     '1'
     C     XUDT          IFGT      TUDT
     C     TUDT          ANDNE     *ZEROS
     C                   MOVE      'B'           XDISTA            1
     C                   EXSR      DISPSR
     C                   MOVE      'B'           TUST
     C                   END
     C                   UPDATE    TURERR
     C     YST           READE     TURER1                                 94
     C  N94              END
     C*
     C*
     C* TURER MED STATUS B - DERSOM KOMMET FORBI LEV.DAG - B ---> C
     C                   SETOFF                                           94
     C                   MOVE      'B'           YST               1
     C     TURE1K        SETLL     TURER1
     C     YST           READE     TURER1                                 94
     C*
     C     *IN94         DOWNE     '1'
     C     XUDT          IFGT      TUDTT
     C     TUDT          ANDNE     *ZEROS
     C                   MOVE      'C'           XDISTA            1
     C                   EXSR      DISPSR
     C                   MOVE      'C'           TUST
     C                   END
     C                   UPDATE    TURERR
     C     YST           READE     TURER1                                 94
     C  N94              END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DISPSR        BEGSR
     C***********************************************
     C* FINN ALL OPPDRAG PÅ TUREN - OPPDATER DISPONERINGSSTATUS
     C* GÅ I LØKKE FOR Å BEHANDLE ALLE AVD
     C                   Z-ADD     1             KOVAVD
     C     KOVKEY        SETLL     KODTV
     C                   SETOFF                                           34
     C     HETAG0        TAG
     C                   READ      KODTV                                  34
     C  N34HEAD8K        SETLL     HEAD8
     C     HETAG1        TAG
     C  N34              READ      HEAD8                                  34
     C  N34HEAVD         IFGT      KOVAVD
     C     HEPRO         ORGT      TUPRO
     C                   GOTO      HETAG0
     C                   END
     C* FUNNET OPPDRAG PÅ DENNE TUREN....HENT STED.
     C  N34              DO
     C     DISPK         CHAIN     DISP                               33
     C  N33              MOVE      XDISTA        DISTA1
     C  N33              UPDATE    DISPR
     C* FLERE OPPDRAG PÅ DENNE TUREN?
     C                   GOTO      HETAG1
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     TURE1K        KLIST
     C                   KFLD                    YST
     C                   KFLD                    YAVD
     C                   KFLD                    YPRO
     C*
     C     KOVKEY        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    KOVAVD
     C     HEAD8K        KLIST
     C                   KFLD                    KOVAVD
     C                   KFLD                    TUPRO
     C*
     C     DISPK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C*
     C                   MOVE      *ZEROS        YAVD              4 0
     C                   MOVE      *ZEROS        YPRO              8 0
     C*
     C                   READ      FIRM                                   33
     C*
     C                   MOVE      'V'           KOVUNI
     C*
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          XUDTÅ
     C                   ELSE
     C     UYEAR         ADD       2000          XUDTÅ
     C                   END
     C                   MOVE      UMONTH        XUDTM
     C                   MOVE      UDAY          XUDTD
     C*
     C                   ENDSR
     C*
