********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10nullstill journalnummer
10185 * 02 JOV PTF10 Oppdatere dato/tid for registrering/endring
10185 * 03 SH  PTF10 tilbakefører 1. gangseregistrert
********************************************************************
      *
      *****************************************************************
      *** PROGRAM SKAL KREDITERER BILAG                             ***
      *****************************************************************
     FKOSTT     UF   E           K DISK
S03  F*OSTA     IF A E           K DISK
N03  FKOSTA     UF A E           K DISK
     FKOSTB     IF A E           K DISK
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZUSER                254    263
N02  D                 DS
N02  D  XTIME                  1     14  0
N02  D  XTM                    1      6  0
N02  D  XDG                    7      8  0
N02  D  XMN                    9     10  0
N02  D  XÅR                   11     14  0
N02  D                 DS
N02  D  KADTE                  1      8  0
N02  D  KAÅRE                  1      4  0
N02  D  KAMNE                  5      6  0
N02  D  KADGE                  7      8  0

      *
      * KREDITERER BILAG
      *
     C                   EXSR      INIT
     C                   EXSR      KREDI
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UNR               7 0
     C                   PARM                    UNYTT             7 0
     C                   PARM                    NYBNR2            7 0
     C                   PARM                    UTXT             35
      *
     C     KEYA          KLIST
     C                   KFLD                    UNRK
     C                   MOVE      UNR           UNRK              7 0
     C                   ENDSR
      ***********************************************
     C     KREDI         BEGSR
      ***********************************************
     C* TILDEL PRE.REG.NR (LØPENR FRA FAST TELLER)...
     C     'Ø'           CHAIN     KOSTT                              33
     C                   MOVE      KTNR          NYTTNR            7 0
     C                   MOVE      KTNR          UNYTT
     C                   ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
      *
     C     KEYA          CHAIN     KOSTA                              33
N03  C                   MOVE      'K'           KAST
N03  C                   MOVE      ZUSER         KAUSER
N03  C                   EVAL      KATXT='Kreditert av:'
     C                   MOVE      NYBNR2        NYBN2A            7
N03  C     KATXT         CAT       NYBN2A:1      KATXT
N03  C                   update    kostar
      *
     C                   Z-SUB     KABL          KABL
     C                   z-add     NYBNR2        KABNR2
     C                   move      'G'           KAST
     C                   movel     utxt          KAtxt
     C                   move      Nyttnr        KABNR
     C                   MOVE      ZUSER         KAUSER
N03  C                   MOVE      '*K*'         KASG
n01  C                   move      *zeros        kajour
N02  C                   TIME                    XTIME
N02  C                   MOVE      XDG           KADGE
N02  C                   MOVE      XMN           KAMNE
N02  C                   MOVE      XÅR           KAÅRE
N02  C                   MOVE      XTM           KATME
N02  C                   MOVE      KADTE         KADTR
N02  C                   MOVE      KATME         KATDR
     C                   WRITE     KOSTAR
      *
     C                   EXSR      DEL2
     C                   ENDSR
      ***********************************************
     C     DEL2          BEGSR
      ***********************************************
      *
      * VARELINJER
     C     KEYA          SETLL     KOSTB
     C     *IN34         DOWEQ     '0'
     C     KEYA          READE     KOSTB                                  34
     C  N34              Z-SUB     kbblhb        kbblhb
     C  N34              Z-SUB     kbblf         kbblf
     C  N34              move      Nyttnr        KBBNR
     C  N34              WRITE     KOSTBR
     C                   END
     C                   ENDSR
