      *********************************************************************
      * UTSKRIFT AV ISO-SAK
      * PROGRAM BEHANDLER INPUT FRA EISO18
      *
CRTPG+*  CRTPGM SYSPED/EISO18 MODULE(*LIBL/EISO18
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10035 * 01 YBC PTF10 MELDING TIL TRANSPORTØR
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    UF   E           K DISK    USROPN
     FISOFL8    IF   E           K DISK    USROPN
     FISOINFF   UF A E           K DISK    USROPN
     FISOLOGF   O  A E             DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D SubmPressed     s              1
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSSAKN          S              7
     D BSSAKN          S              7  0
     D WSMOTT          S             50
     D WSCCT           S            150
     D WSCC01          S             50
     D WSCC02          S             50
     D WSCC03          S             50
     D WSSTD           S             20
     D WSDREF          S             35
     D WSDTS           S             10
     D BSDTS           S              8  0
     D WSHDT           S            280
     D WSHD01          S             70
     D WSHD02          S             70
     D WSHD03          S             70
     D WSHD04          S             70
     D WSTXTT          S           1050
     D WSTXT01         S             70
     D WSTXT02         S             70
     D WSTXT03         S             70
     D WSTXT04         S             70
     D WSTXT05         S             70
     D WSTXT06         S             70
     D WSTXT07         S             70
     D WSTXT08         S             70
     D WSTXT09         S             70
     D WSTXT10         S             70
     D WSTXT11         S             70
     D WSTXT12         S             70
     D WSTXT13         S             70
     D WSTXT14         S             70
     D WSTXT15         S             70
     D WSBTT           S            210
     D WSBT01          S             70
     D WSBT02          S             70
     D WSBT03          S             70
N01  D WSEPOS          S             70
N01  D WSSND           S              1
      *Array for fri-tekster
     D TX              S             70    DIM(15)
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FMELDNR         S              2
     D FEILMELDING     S             80

     D                 DS
     D  KVTXT                  1   1050
     D  KVT                    1   1050    DIM(1050)
     D                 DS
     D  KVTT50                 1    150
     D  KVT50                  1    150    DIM(3)
     D                 DS
     D  KVTT70                 1   1050
     D  KVT70                  1   1050    DIM(15)
     D                 DS
     D  TXT70                  1     70
     D  T70                    1     70    DIM(70)
     D                 DS
     D WSDTÅÅ                  1      4
     D WSDTMM                  5      6
     D WSDTDD                  7      8
     D WSDT                    1      8
      *
     D FEIL01          C                   CONST('Mottaker må oppgis.')
     D FEIL02          C                   CONST('Max 3 CC-linjer.')
     D FEIL03          C                   CONST('Sted må oppgis.')
     D FEIL04          C                   CONST('Deres ref. må oppgis.')
     D FEIL05          C                   CONST('Dato er feil.')
     D FEIL06A         C                   CONST('Heading må oppgis.')
     D FEIL06B         C                   CONST('Max 4 heading-linjer.')
     D FEIL07A         C                   CONST('Tekst må oppgis.')
     D FEIL07B         C                   CONST('Max 10 tekst-linjer.')
     D FEIL08A         C                   CONST('Bunntekst må oppgis.')
     D FEIL08B         C                   CONST('Max 3 bunn-linjer.')
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Hvis første kall OG en...
     C                   IF        SubmPressed = 'Y'
     C                   EXSR      TESTB
     C                   ELSE
     C                   EXSR      GetData
     C                   END
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('BUNN')

      *
     C                   exsr      SetEnd
     C                   callp     wrtsection('end')
N01   *
N01  C                   IF        AVSLUTT = 'J' AND WSSND = 'J'
N01  C                   call      'EISO19C'
N01  C                   parm                    WSSAKN
N01  C                   parm                    WSSND
N01  C                   PARM                    IFSFIL           50
N01  C                   END
      *
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *
      *
      *
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      SubmPressed = zhbgetvar('SubmPressed')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSNA    = zhbgetvar('WSNA')
      *
     C                   eval      WSSAKN  = zhbgetvar('WSSAKN')
     C                   eval      BSSAKN  = c2n2(WSSAKN)
     C                   eval      WSMOTT  = zhbgetvar('WSMOTT')
     C                   eval      WSCCT   = zhbgetvar('WSCCA')
     C                   eval      WSSTD   = zhbgetvar('WSSTD')
     C                   eval      WSDREF  = zhbgetvar('WSDREF')
     C                   eval      WSDTS   = zhbgetvar('WSDTS')
     C                   eval      BSDTS   = c2n2(WSDTS)
     C                   eval      WSHDT   = zhbgetvar('WSHDA')
     C                   eval      WSTXTT  = zhbgetvar('WSTXTA')
     C                   eval      WSBTT   = zhbgetvar('WSBTA')
N01  C                   eval      WSEPOS  = zhbgetvar('WSEPOS')
N01  C                   eval      WSSND   = zhbgetvar('WSSND')

     C                   endsr
      *=====================================================================
      * Set html output skeleton VED FEIL
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'EISO18'
     C*                  CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSNA':WSNA)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updhtmlvar('WSSAKN':
     C                             %trim(%editc(BSSAKN:'Z')))
     C                   callp     updHTMLvar('WSDTS':
     C                             %trim(%editw(BSDTS:'    .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   callp     updHtmlVar('WSMOTT':WSMOTT)
     C                   callp     updhtmlvar('WSCCT':WSCCT)
     C                   callp     updHtmlVar('WSSTD':WSSTD)
     C                   callp     updHtmlVar('WSDREF':WSDREF)
     C                   callp     updhtmlvar('WSHDT':WSHDT)
     C                   callp     updhtmlvar('WSTXTT':WSTXTT)
     C                   callp     updhtmlvar('WSBTT':WSBTT)
N01  C                   callp     updhtmlvar('WSSND':WSSND)
      *
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$end
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
      *
      * TEST MOTTAKER
     C                   IF        WSMOTT = *BLANKS
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      SLTBDT
     C                   END
      * MAX 3 LINJER AV CC
     C                   EVAL      KVTXT = WSCCT
     C                   Z-ADD     3             XLINL             3 0
     C                   Z-ADD     50            XPOSL             3 0
     C                   EXSR      SRTEST
     C                   IF        XANTL > 3
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ELSE
     C                   EXSR      KVTLIN
     C                   MOVEL     TX(1)         WSCC01
     C                   MOVEL     TX(2)         WSCC02
     C                   MOVEL     TX(3)         WSCC03
     C                   END
      * TEST STED
     C                   IF        WSSTD = *BLANKS
     C                   MOVEL     FEIL03        FEILMELDING
     C                   MOVE      '03'          FMELDNR
     C                   GOTO      SLTBDT
     C                   END
      * TEST UTSKRIFT DATO
     C                   IF        BSDTS <> 0
     C                   MOVE      BSDTS         WSDT
     C                   MOVE      WSDTDD        UDD               2 0
     C                   MOVE      WSDTMM        UMM               2 0
     C                   MOVE      WSDTÅÅ        UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   MOVEL     FEIL04        FEILMELDING
     C                   MOVE      '04'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
     C                   ENDIF
      * TEST DERES REF.
     C                   IF        WSDREF = *BLANKS
     C                   MOVEL     FEIL05        FEILMELDING
     C                   MOVE      '05'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST HEADING
     C                   IF        WSHDT = *BLANKS
     C                   MOVEL     FEIL06A       FEILMELDING
     C                   MOVE      '06'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
     C                   EVAL      KVTXT = WSHDT
     C                   Z-ADD     4             XLINL             3 0
     C                   Z-ADD     70            XPOSL             3 0
     C                   EXSR      SRTEST
     C                   IF        XANTL > 4
     C                   MOVEL     FEIL06B       FEILMELDING
     C                   MOVE      '06'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ELSE
     C                   EXSR      KVTLIN
     C                   MOVEL     TX(1)         WSHD01
     C                   MOVEL     TX(2)         WSHD02
     C                   MOVEL     TX(3)         WSHD03
     C                   MOVEL     TX(4)         WSHD04
     C                   END
      * TEST TEKST
     C                   IF        WSTXTT = *BLANKS
     C                   MOVEL     FEIL07A       FEILMELDING
     C                   MOVE      '07'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
     C                   EVAL      KVTXT = WSTXTT
     C                   Z-ADD     15            XLINL             3 0
     C                   Z-ADD     70            XPOSL             3 0
     C                   EXSR      SRTEST
     C                   IF        XANTL > 15
     C                   MOVEL     FEIL07B       FEILMELDING
     C                   MOVE      '07'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ELSE
     C                   EXSR      KVTLIN
     C                   MOVEL     TX(1)         WSTXT01
     C                   MOVEL     TX(2)         WSTXT02
     C                   MOVEL     TX(3)         WSTXT03
     C                   MOVEL     TX(4)         WSTXT04
     C                   MOVEL     TX(5)         WSTXT05
     C                   MOVEL     TX(6)         WSTXT06
     C                   MOVEL     TX(7)         WSTXT07
     C                   MOVEL     TX(8)         WSTXT08
     C                   MOVEL     TX(9)         WSTXT09
     C                   MOVEL     TX(10)        WSTXT10
     C                   MOVEL     TX(11)        WSTXT11
     C                   MOVEL     TX(12)        WSTXT12
     C                   MOVEL     TX(13)        WSTXT13
     C                   MOVEL     TX(14)        WSTXT14
     C                   MOVEL     TX(15)        WSTXT15
     C                   END
      * TEST BUNNTEKST
     C                   IF        WSBTT = *BLANKS
     C                   MOVEL     FEIL08A       FEILMELDING
     C                   MOVE      '08'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
     C                   EVAL      KVTXT = WSBTT
     C                   Z-ADD     3             XLINL             3 0
     C                   Z-ADD     70            XPOSL             3 0
     C                   EXSR      SRTEST
     C                   IF        XANTL > 3
     C                   MOVEL     FEIL08B       FEILMELDING
     C                   MOVE      '08'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ELSE
     C                   EXSR      KVTLIN
     C                   MOVEL     TX(1)         WSBT01
     C                   MOVEL     TX(2)         WSBT02
     C                   MOVEL     TX(3)         WSBT03
     C                   END
      *
     C                   EXSR      OPDINF
      *
     C     SLTBDT        ENDSR
      *
      *=====================================================================
      *  TEST ANTALL MELDINGSLINJER
      *=====================================================================
     C     SRTEST        begsr
      *
     C                   Z-ADD     0             XANTL             3 0
     C                   EVAL      TXT70 = *BLANKS
     C                   Z-ADD     0             XN2               5 0
     C                   Z-ADD     0             XN3               5 0
     C                   Z-ADD     0             XN4               5 0
     C                   EVAL      XN4 = XLINL * XPOSL
     C     1             DO        XN4           XN                5 0
     C                   ADD       1             XN2
     C                   SELECT
     C                   WHEN      XN2 = XPOSL
     C                   IF        KVT(XN) = XSTEGN1
     C                   EVAL      XN3 = XN + 1
     C                   IF        KVT(XN3) = XSTEGN2
     C                   EVAL      XN = XN3
     C                   ELSE
     C                   MOVE      KVT(XN)       T70(XN2)
     C                   END
     C                   ELSE
     C                   MOVE      KVT(XN)       T70(XN2)
     C                   END
     C                   IF        TXT70 <> *BLANKS
     C                   ADD       1             XANTL
     C                   END
     C                   Z-ADD     0             XN2
     C                   EVAL      TXT70 = *BLANKS
     C                   WHEN      KVT(XN) = XSTEGN1
     C                   EVAL      XN3 = XN + 1
     C                   IF        KVT(XN3) = XSTEGN2
     C                   EVAL      XN = XN3
     C                   IF        TXT70 <> *BLANKS
     C                   ADD       1             XANTL
     C                   END
     C                   Z-ADD     0             XN2
     C                   EVAL      TXT70 = *BLANKS
     C                   ELSE
     C                   MOVE      KVT(XN)       T70(XN2)
     C                   END
     C                   OTHER
     C                   MOVE      KVT(XN)       T70(XN2)
     C                   ENDSL
     C                   ENDDO
      *
     C                   ENDSR
      *
      *=====================================================================
      *  FINNE LINJE
      *=====================================================================
     C     KVTLIN        BEGSR
     C                   MOVE      *BLANKS       T70
     C                   MOVE      *BLANKS       TX
     C                   MOVE      *ZEROS        XN1               5 0
     C                   MOVE      *ZEROS        XN2               5 0
     C     XLINL         MULT      XPOSL         XN5               5 0
     C                   ADD       1             XN5               5 0
     C     1             DO        XN5           XN3               5 0
     C                   ADD       1             XN2
     C                   SELECT
     C                   WHEN      XN2 > XPOSL
     C                   ADD       1             XN1
     C                   MOVEL     TXT70         TX(XN1)
     C     XN1           CABEQ     XLINL         SLKVTLIN
     C                   MOVE      *BLANKS       T70
     C                   IF        KVT(XN3) = XSTEGN1
     C     XN3           ADD       1             XN4               5 0
     C                   IF        KVT(XN4) = XSTEGN2
     C                   EVAL      XN3 = XN4
     C                   Z-ADD     0             XN2
     C                   ELSE
     C                   Z-ADD     1             XN2
     C                   MOVE      KVT(XN3)      T70(XN2)
     C                   END
     C                   ELSE
     C                   Z-ADD     1             XN2
     C                   MOVE      KVT(XN3)      T70(XN2)
     C                   END
     C                   WHEN      KVT(XN3) = XSTEGN1
     C     XN3           ADD       1             XN4               5 0
     C                   IF        KVT(XN4) = XSTEGN2
     C                   ADD       1             XN1
     C                   MOVEL     TXT70         TX(XN1)
     C     XN1           CABEQ     XLINL         SLKVTLIN
     C                   MOVE      *BLANKS       T70
     C                   MOVE      *ZEROS        XN2
     C                   EVAL      XN3 = XN4
     C                   ELSE
     C                   MOVE      KVT(XN3)      T70(XN2)
     C                   END
     C                   OTHER
     C                   MOVE      KVT(XN3)      T70(XN2)
     C                   ENDSL
     C                   ENDDO
      *
     C     SLKVTLIN      ENDSR
      *
      *=====================================================================
      *  Oppdater informasjon
      *=====================================================================
     C     OPDINF        begsr
      *
     C     BSSAKN        CHAIN     ISOINFF                            33
     C                   EXSR      MOVEWSI
     C                   IF        *IN33
     C                   MOVE      BSSAKN        IFSAKN
     C                   MOVE      WDT           IFDTR
     C                   WRITE     ISOINFFR
      *
     C                   ELSE
      * Endring
     C                   UPDATE    ISOINFFR
     C                   ENDIF
      *
N01  C                   IF        WSSND <> 'J'
     C                   EXSR      SRLOG1
N01  C                   END
      *
     C                   MOVE      'J'           AVSLUTT
      *
     C                   ENDSR
      *
      *=====================================================================
      *  OPPDATER FELTER I FIL
      *=====================================================================
     C     MOVEWSI       BEGSR
     C                   MOVE      WSMOTT        IFMOTT
     C                   MOVE      WSCC01        IFCC01
     C                   MOVE      WSCC02        IFCC02
     C                   MOVE      WSCC03        IFCC03
     C                   MOVE      WSSTD         IFSTD
     C                   MOVE      WSDREF        IFDREF
     C                   MOVE      WSHD01        IFHD01
     C                   MOVE      WSHD02        IFHD02
     C                   MOVE      WSHD03        IFHD03
     C                   MOVE      WSHD04        IFHD04
     C                   MOVE      WSTXT01       IFTXT01
     C                   MOVE      WSTXT02       IFTXT02
     C                   MOVE      WSTXT03       IFTXT03
     C                   MOVE      WSTXT04       IFTXT04
     C                   MOVE      WSTXT05       IFTXT05
     C                   MOVE      WSTXT06       IFTXT06
     C                   MOVE      WSTXT07       IFTXT07
     C                   MOVE      WSTXT08       IFTXT08
     C                   MOVE      WSTXT09       IFTXT09
     C                   MOVE      WSTXT10       IFTXT10
     C                   MOVE      WSTXT11       IFTXT11
     C                   MOVE      WSTXT12       IFTXT12
     C                   MOVE      WSTXT13       IFTXT13
     C                   MOVE      WSTXT14       IFTXT14
     C                   MOVE      WSTXT15       IFTXT15
     C                   MOVE      WSBT01        IFBT01
     C                   MOVE      WSBT02        IFBT02
     C                   MOVE      WSBT03        IFBT03
      *
     C                   ENDSR
      *
      *=====================================================================
      *  Hent data fra fra fil ved først "syklus"  / endring av post
      *=====================================================================
     C     GetData       begsr
     C     BSSAKN        CHAIN(N)  ISOINFF                            33
      *
     C                   IF        *IN33
     C                   MOVE      *BLANKS       WSMOTT
     C                   MOVE      *BLANKS       WSCCT
     C                   MOVE      *BLANKS       WSSTD
     C                   MOVE      *BLANKS       WSDREF
     C                   MOVE      WDT           BSDTS
     C                   MOVE      *BLANKS       WSHDT
     C                   MOVE      *BLANKS       WSTXTT
     C                   MOVE      *BLANKS       WSBTT
      *
     C                   ELSE
      *
     C                   MOVE      IFMOTT        WSMOTT
     C                   EVAL      WSCCT = %TRIM(IFCC01) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFCC02) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFCC03)
     C                   MOVE      IFSTD         WSSTD
     C                   MOVE      IFDREF        WSDREF
     C                   IF        BSDTS = 0
     C                   MOVE      WDT           BSDTS
     C                   ELSE
     C                   MOVE      IFDTS         WSDTS
     C                   END
     C                   EVAL      WSHDT = %TRIM(IFHD01) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFHD02) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFHD03) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFHD04)
     C                   EVAL      WSTXTT = %TRIM(IFTXT01) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT02) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT03) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT04) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT05) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT06) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT07) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT08) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT09) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT10) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT11) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT12) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT13) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT14) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFTXT15)
     C                   EVAL      WSBTT = %TRIM(IFBT01) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFBT02) + XSTEGN1 + XSTEGN2
     C                             + %TRIM(IFBT03)
      *
     C                   END
N01   *
N01  C                   IF        WSEPOS <> *BLANKS
N01  C                   EVAL      WSMOTT = WSEPOS
N01  C                   MOVE      '99'          FMELDNR
N01  C                   END
      *
     C                   ENDSR
      *
      *=====================================================================
      *  LOGG HENDELSE (NY UTSKRIFT)
      *=====================================================================
     C     SRLOG1        BEGSR
     C     BSSAKN        CHAIN     ISOFL8                             33
     C                   CLEAR                   ISOLOGFR
     C                   MOVE      BSSAKN        ILSAKN
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           ILDTL
     C                   MOVE      WTM           ILTML
     C                   MOVE      BIBRID        ILUSR
     C                   MOVE      'W'           ILMOD
     C                   MOVE      ISFPFO        ILFPFO2
     C                   MOVE      ISFAVA        ILFAVA2
     C                   MOVE      ISFANS        ILFANS2
     C                   MOVE      ISRTYP        ILRTYP2
     C                   MOVE      ISST          ILST2
     C                   MOVE      ISSDT         ILSDT2
     C                   MOVE      ISUSRS        ILUSR2
     C                   EVAL      ILTXT = 'UTSKRIFT ER LAGET.'
     C                   WRITE     ISOLOGFR
      *
     C                   ENDSR
      *
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN(N)  BRIDF                              50
      *
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
     C                   open      ISOLOGF                              50
     C                   open      ISOINFF                              50
     C                   open      ISOFL8                               50
     C                   open      BRPARF                               50
      *
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * HENT SPRÅKKODE
     C                   MOVEL     'SPRÅK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   MOVE      'N'           AVSLUTT           1
     c                   move      *blanks       feilmelding
     c                   move      *blanks       fmeldnr
     C                   BITON     '123457'      XAPSTR            1
     C                   BITOFF    '06'          XAPSTR
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XIDAG             8 0
      *
     C                   BITON     '457'         XSTEGN1           1
     C                   BITOFF    '01236'       XSTEGN1
     C                   BITON     '257'         XSTEGN2           1
     C                   BITOFF    '01346'       XSTEGN2
     C                   BITON     '123457'      XAPSTR            1
     C                   BITOFF    '06'          XAPSTR
      *
     C                   endsr
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     ISOLOGF
     C                   close     ISOINFF
     C                   close     ISOFL8
     C                   close     BRPARF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
