      *********************************************************************
      *  STSCS2= Overføring når ren webbrowservariant benyttes ( for PDA-TKterm - se STSC9)
      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
      *
CRTPG+*  CRTPGM SYSPED/STSCS2 MODULE(*LIBL/STSCS2 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 LM kan mottas
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 03 JOV PTF12 Ignorer ren godsmerking (kun 'E'/'I' )
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FWRKCGI    UF   E           K DISK    usropn
     FCARGOF    UF A E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          s             10
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D LENA            s             15
     D BREA            s             15
     D HØYA            s             15
      *
     D                 DS
     D  WKey2                  1     30
     D  WDsUser                1     10
     D  WDsCLID               11     30
     D                 DS
     D  Wdata                  1    220
     D  WDsDT                  1      8
     D  WDsDTÅÅ                1      4
     D  WDsDTMM                5      6
     D  WDsDTDD                7      8
     D  WDsTID                 9     14
     D  WDsPALL               15     15
     D  WDsVKT                16     19
     D  WDsLBH                21     35
N01  D  WDsLM                 36     39
      * Array for konv. av alfa til numerisk
     D FE              S              1    DIM(15)
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      WSUSER   = zhbgetvar('user')
      *
      * også wrkcgi ligger i sycgi - trenger ikke libl
     C                   open      BRIDF
     C     WSUSER        CHAIN     BRIDF                              60
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      WRKCGI
     C                   open      CARGOF
      * Overfør kolli for aktuell user
      * winteg alltid 0 / wkey2 = Userid+kolliID
     C     KEY           KLIST
     C                   KFLD                    WINTEG
     C                   KFLD                    WKEY2
     C                   eval      Wkey2 = WSUSER
     C     CSKEY         KLIST
     C                   KFLD                    XXID             20
     C                   KFLD                    XXTERM           20
     C* FORELØPIG - HARDKODET SENERE STYRT AV PARAMETER..
     C                   MOVEL     'OSL'         XXTERM
      * MIDLERTIDIG KOPIER WORKFILE VED HVER FULLFØRING
     C     KEY           SETLL     WRKCGI
     C                   READ      WRKCGI                                 50
     C     *in50         doweq     '0'
     C     WDsUser       CABNE     WSUSER        Utdel
     C                   eval      XXID = WDsCLID
S01  C*    CSKEY         Chain     Cargof                             33
S01  C*  33              EXSR      LAGCS
N01  C     CSKEY         Chain     Cargof                             34
N01  C                   EXSR      LAGCS
     C                   DELETE    WRKCGIR
     C                   READ      WRKCGI                                 50
     C  N50              ENDDO
     C     UtDel         tag
      *
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     c     WSuser        ifeq      'NN'
     C                   eval      Mbr = 'STS01B'
     c                   else
     C                   eval      Mbr = 'STS02'
     c                   end
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('user':WSUSER)
     c     WSuser        ifeq      'NN'
     C                   callp     updhtmlvar('user':spaces)
     c                   end
     C                   callp     updHTMLvar('besk':BIBESK)
     C                   callp     updHTMLvar('kunr':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHTMLvar('FEILTXT':spaces)
      *
     C                   callp     wrtsection('all')
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     WRKCGI
     C                   close     CARGOF
     C                   eval      *inlr = *on
     C                   RETURN
      *
      ************************************************************
     C     LAGCS         BEGSR
      ************************************************************
N01  C     *in34         ifeq      '1'
     C                   clear                   Cargofr
N01  C                   else
N01  c                   clear                   KOAVD
N01  c                   clear                   KOOPD
N01  c                   clear                   KOLNR
N01  c                   clear                   KOLNR2
N01  C                   endif
     C                   eval      KoID        = WDsCLID
     C                   eval      KoTerm      = XXTERM
     C                   eval      KoStat      = '0'
     C                   eval      KoStNr      = '99'
     C     WDsPALL       Ifeq      'X'
     C                   eval      KoStNr      = '98'
     C                   END
N02  C     *in34         ifeq      '1'
     C                   move      WDsDT         KOINDT
     C                   move      WDsTID        KOINTI
N02  C                   endif
     C                   eval      KOUSER      = WSUSER
      *
     c     WdsLBH        ifne      *blanks
     c     WdsVKT        orne      *blanks
     c                   exsr      MaalSR
     c                   end
S01  C*                  write     CARGOFR
N01  C   34              write     CARGOFR
N01  C  N34              UPDATE    CARGOFR
     C                   ENDSR
      ************************************************************
     C     MAALSR        BEGSR
      ************************************************************
      * Vekt..
     C                   EVAL      KOMVKT = c2n2(WdsVKT)
      * Mål...
     c     WdsLBH        ifne      *blanks
     c                   move      *zeros        ST2               3 0
     c                   move      *zeros        KOMLEN
     c                   move      *zeros        KOMBRE
     c                   move      *zeros        KOMHØY
     c                   move      *blanks       LENa
     c                   move      *blanks       BREa
     c                   move      *blanks       HØYa
      * dersom mål inneholder ? så er det ikke angitt (kun defaulten som kommer)
     C     '?'           SCAN      WdsLBH        X                 3 0
     C     X             cabgt     0             FeilInp
      *
      * fjern evt slutt-tegn
     C*    '#':' '       XLATE     WdsLBH        WdsLBH
      * f.eks: WDsLBH = '120*70*55'
      * X=4 , Y = 7
     C     '*'           SCAN      WdsLBH        X                 3 0
     c     X             cablt     2             FeilInp
     c     X             cabgt     12            FeilInp
     C                   eval      st2    = X+1                                 *startpkt søk2
     C     '*'           SCAN      WdsLBH:ST2    Y                 3 0
     c     Y             cablt     4             FeilInp
     c     Y             cabgt     14            FeilInp
      * skrell bort ev grums (ikke numerisk) etter 2 stjerne
     c                   move      'N'           Ferdig            1
     c     Y             add       1             Z                 3 0
     c                   movea     WdsLBH        FE
     c     Z             dowle     15
     c     FE(Z)         iflt      '0'
     c     FE(Z)         orgt      '9'
     c                   move      'J'           Ferdig
     c                   end
     c     Ferdig        ifeq      'J'
     c                   move      ' '           FE(Z)
     c                   end
     c                   add       1             Z
     c                   end
     c                   movea     FE            WdsLBH
      * Eksempelet: FRA 1 : lengde 3 (først stjerne minus 1)
     C                   EVAL      LENa   = %SUBST(WdsLBH:1:X-1)
      * Eksempelet: Fra 5 (første stj. + 1 ) : lengde 2 (sist - første - 1 = 7-4-1 = 2)
     C                   EVAL      BREa   = %SUBST(WdsLBH:x+1:Y-X-1)
      * Eksempelet: Fra 8 (andre  stj. + 1 ) : lengde 8 (max-siste stj = 17-7=8)
     C                   EVAL      HØYa   = %SUBST(WdsLBH:Y+1:15-Y)
     C                   EVAL      KOMLEN = c2n2(LENa)
     C                   EVAL      KOMBRE = c2n2(BREa)
     C                   EVAL      KOMHØY = c2n2(HØYa)
      *
     c     FeilInp       tag
      * beregn kubikMETER (resultat bil kubik CM - 100 * 100 *100 = 1.000.000 cm3 = 1 M3 )
      * (hvis en av de er 0 blir jo resultatet 0, men so what...)
     C                   eval(H)   KOMM3= (KOMLEN*KOMBRE*KOMHØY)/1000000
     c                   end
     C                   ENDSR
