********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 slette ettoppdrag
10XXX * 02 sh  PTF10 R= reverser kobling mot SAD
10XXX * 03 sh  rettelse hektav
10XXX * 04 sh  endre søk ref
10XXX * 05 sh  beregn på nytt
10XXX * 06 sh  PTF TIL 5
10XXX * 07 sh  kunne slette status ' '
10XXX * 08 YBC BEREGN STATISTVERDI PÅ LINJE
10286 * 09 YBC Vekt er endret fra 9.0 til 12.3
********************************************************************
      *
      ***********************************************************
      *** PROGRAM for  forenklet prosedyre                    ***
      ***********************************************************
      *
     FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FarkivP    O  A E           K DISK
     FFPSTD     iF   E           K DISK
s01  F*ADH      IF a E           K DISK
n01  FSADH      UF a E           K DISK
s02  F*ADV      iF A E           K DISK
n02  FSADV      UF A E           K DISK
     FSTANDI    UF   E           K DISK
     FFPXREF1   UF   E           K DISK
     F                                     RENAME(FPXREFR:FPXREF1R)
     FFPXREF2   IF   E           K DISK
     F                                     RENAME(FPXREFR:FPXREF2R)
     FFPXREF3   IF   E           K DISK
     F                                     RENAME(FPXREFR:FPXREF3R)
     FFPXREF    UF   E           K DISK
     FFP0002FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                UDS
     D  UDFIRM                 1      2
     D  UDAVD                 15     18  0
     D  UDSG                  19     21
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XDDMM                  7     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  SINAK                  1     30
     D  XSINAK                 1     28
     C                   EXSR      INIT
     C*
     C     START         TAG
     c     sta           ifeq      'A'
     c                   move      'SHU1063'     msgnrh
     c                   else
     c     sta           ifeq      'S'
     c                   move      'SHU1064'     msgnrh
     c                   else
     c     sta           ifeq      'O'
     c                   move      'SHU1065'     msgnrh
     c                   end
     c                   end
     c                   end
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     keysta        SETLL     FPXREF1
     C     START1        TAG
     C     keyequ        READE(N)  FPXREF1                                94
     C  N94              MOVE      fpavd         avdr
     C  N94              MOVE      fpopd         opdr
     C  N94              MOVE      fpavdh        avdrh
     C  N94              MOVE      fpopdh        opdrh
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
s01  C*    KEYSAD        CHAIN     SADH                               33
n01  C     KEYSAD        CHAIN(N)  SADH                               33
     C   33              MOVE      *BLANKS       SINAK
     C   33              MOVEL     '*ERROR*'     SINAK
      *
      * reverse nå søkeref er truffet
      *
     c                   setoff                                       60
     c     kure          ifne      *blanks
     c     kure          andeq     fpkure
     c                   seton                                        60
     c                   end
      *
      * reverse nå løpenr  er truffet
      *
     c                   setoff                                       61
     c     tll           ifne      *zeros
     c     tll           andeq     fptll
     c                   seton                                        61
     c                   end
     C                   WRITE     SUBFIL
     C     keyequ        READE(N)  FPXREF1                                94
     C  N94              MOVE      fpavd         avdr
     C  N94              MOVE      fpopd         opdr
     C  N94              MOVE      fpavdh        avdrh
     C  N94              MOVE      fpopdh        opdrh
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*
     C*** LAG SAD
     C     *IN(10)       IFEQ      '1'
     C                   EXSR      LAGSADH
     C                   GOTO      START
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KEYSTAr       SETLL     FPXREF1
     C  N94KEYEQU        READE(N)  FPXREF1                                94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   30XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C   30              GOTO      START3
     C  N30              GOTO      START
     C*
     C*** SØK FRA
     C     START6        TAG
     c                   move      *zeros        avd
     c                   move      *zeros        opd
     c                   move      *blanks       kure
     c                   move      *zeros        tll
     c     START6B       TAG
     C                   EXFMT     VAREL2
     c                   setoff                                       30
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
      *
      * søke på kunderef
      *
     c     kure          ifne      *blanks
     c     kure          chain     fpxref2                            30
     c     *in(30)       ifeq      '0'
     c     fpstat        ifeq      'O'
s04  c*                  move      fpavdh        avd
s04  c*                  move      fpopdh        opd
n04  c                   move      fpavdh        avdn4
n04  c                   move      fpopdh        opdn5
n04  c                   move      fpavd         avd
n04  c                   move      fpopd         opd
     c                   move      'O'           sta
     c                   else
     c                   move      fpstat        sta
     c                   move      fpavd         avd
     c                   move      fpopd         opd
     c                   end
     c                   else
     c                   move      'SHU1068'     msgnrs
     c                   goto      start6b
     c                   end
     c                   end
      *
      *
      * søke på løpenummer
      *
     c     tll           ifne      *zeros
     c     tll           chain     fpxref3                            30
     c     *in(30)       ifeq      '0'
     c                   move      fpavdh        avd
     c                   move      fpopdh        opd
     c                   move      'O'           sta
     c                   else
     c                   move      'SHU1069'     msgnrs
     c                   goto      start6b
     c                   end
     c                   end
      *
n04  c     kure          ifeq      *blanks
     c     sta           ifeq      'O'
     c                   move      avd           avdn4
     c                   move      opd           opdn5
     c                   move      *zeros        avd
     c                   move      *zeros        opd
     c                   else
     c                   move      *zeros        avdn4
     c                   move      *zeros        opdn5
     c                   end
n04  c                   end
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C                   SETOFF                                       30
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
      * A = endre status til A = Aktiv
      *
     C     WSNR          IFEQ      'A'                                          ENDRE
     c     fpstat        ifeq      'S'
     c                   seton                                        99
     c     keysad        chain     fpxref                             33
     c  n33              move      'A'           fpstat
     c  n33              update    fpxrefr
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     c                   else
     c                   seton                                        30
     c                   move      'SHU1061'     msgnr
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     c                   goto      feil
     C                   END
     C                   END
N01   *
N01   * 4 = slette
N01   *
N01  C     WSNR          IFEQ      '4'                                          ENDRE
N01  c     fpstat        ifeq      'A'
N01  c     fpstat        oreq      'S'
n07  c     fpstat        oreq      ' '
N01  c                   seton                                        99
N01  c     keysad        chain     fpxref                             33
N01  c  n33              delete    fpxrefr
N01  c     keysad        chain     sadh                               33
N01  c  n33              move      ' '           sist
N01  c  n33              update    sadhr
N01  C                   MOVE      ' '           WSNR
N01  C                   UPDATE    SUBFIL
N01  c                   else
N01  c                   seton                                        30
N01  c                   move      'SHU1243'     msgnr
N01  C                   MOVE      ' '           WSNR
N01  C                   UPDATE    SUBFIL
N01  c                   goto      feil
N01  C                   END
N01  C                   END
N02   *
N02   * R = reverser
N02   *
N02  C     WSNR          IFEQ      'R'                                          ENDRE
N02  c     fpstat        ifeq      'O'
N02  c                   seton                                        99
N02  c                   exsr      revers
N02  C                   MOVE      ' '           WSNR
N02  C                   UPDATE    SUBFIL
N02  c                   else
N02  c                   seton                                        30
N02  c                   move      'SHU1255'     msgnr
N02  C                   MOVE      ' '           WSNR
N02  C                   UPDATE    SUBFIL
N02  c                   goto      feil
N02  C                   END
N02  C                   END
N02   *
N02   * H = hektav
N02   *
N02  C     WSNR          IFEQ      'H'                                          ENDRE
N05  C     WSNR          OREQ      'B'                                          ENDRE
N02  c     fpstat        ifeq      'O'
N02  c                   seton                                        99
N02  c                   exsr      hektav
N02  C                   MOVE      ' '           WSNR
N02  C                   UPDATE    SUBFIL
N02  c                   else
N02  c                   seton                                        30
N02  c                   move      'SHU1255'     msgnr
N02  C                   MOVE      ' '           WSNR
N02  C                   UPDATE    SUBFIL
N02  c                   goto      feil
N02  C                   END
N02  C                   END
      *
      * S = endre status til S = Stoppet
      *
     C     WSNR          IFEQ      'S'                                          SLETTE
     c     fpstat        ifeq      'A'
     c                   seton                                        99
     c     keysad        chain     fpxref                             33
     c  n33              move      'S'           fpstat
     c  n33              update    fpxrefr
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     c                   else
     c                   seton                                        30
     c                   move      'SHU1062'     msgnr
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     c                   goto      feil
     C                   END
     C                   END
      *
      * 5 = se dummies
      *
     C     WSNR          ifeq      '5'
     c                   seton                                        99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      DSPSAD
     C                   END
      *
      * P = print dummiliste
      *
     C     WSNR          ifeq      'P'
     c     fpstat        ifeq      'O'
     c                   seton                                        99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      PRTLIS
     c                   else
     c                   seton                                        30
     c                   move      'SHU1072'     msgnr
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     c                   goto      feil
     C                   END
     C                   END
      *
      * 6 = se samlefortolling
      *
     C     WSNR          ifeq      '6'
     c     fpstat        ifeq      'O'
     c                   seton                                        99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     c                   move      fpavdh        fpavd
     c                   move      fpopdh        fpopd
     C                   EXSR      DSPSAD
     c                   else
     c                   seton                                        30
     c                   move      'SHU1066'     msgnr
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     c                   goto      feil
     C                   END
     C                   END
      *
      * 9 = se arkiv
      *
     C     WSNR          ifeq      '8'
     c                   seton                                        99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   CALL      'FAX025B'
     C                   PARM                    fpavd
     C                   PARM                    fpopd
     C                   END
      *
      * 9 = se arkiv
      *
     C     WSNR          ifeq      '9'
     c     fpstat        ifeq      'O'
     c     arkjn         andeq     'J'
     c                   seton                                        99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   CALL      'ARC003'
     C                   PARM                    fpavd
     C                   PARM                    fpopd
     C                   PARM                    URUNDE           10
     c                   else
     c                   seton                                        30
     c                   move      'SHU1067'     msgnr
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     c                   goto      feil
     C                   END
     C                   END
      * end hovedløkke
     C                   END
     C*
     C     SLHENT        TAG
     c   98              move      'DIV0012'     MSGNR
     c   98              seton                                        30
     c     feil          tag
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *entry        plist
     C                   parm                    ustat             1
     c                   parm                    uavd              4 0
     c                   parm                    uopd              7 0
      *
     c                   move      ustat         sta               1
     c                   move      *zeros        avd               4 0
     c                   move      *zeros        opd               7 0
     c                   move      *zeros        avdr              4 0
     c                   move      *zeros        opdr              7 0
      *
     C     keysta        KLIST
     C                   KFLD                    sta
     c                   KFLD                    avdn4             4 0
     c                   kfld                    opdn5             7 0
     C                   KFLD                    avd
     C                   KFLD                    opd
N02  C     KEYREV        KLIST
N02  C                   KFLD                    sta
N02  C                   KFLD                    avdrh             4 0
N02  C                   KFLD                    opdrh             7 0
N02  C     KEYHEKT       KLIST
N02  C                   KFLD                    avdrh             4 0
N02  C                   KFLD                    opdrh             7 0
      *
     C     keystar       KLIST
     C                   KFLD                    sta
     C                   KFLD                    avdrh             4 0
     C                   KFLD                    opdrh             7 0
     C                   KFLD                    avdr
     C                   KFLD                    opdr
      *
     C     keyequ        KLIST
     C                   KFLD                    sta
     C*
     C*
     C     keysad        KLIST
     C                   kfld                    fpavd
     C                   KFLD                    FPOPD
      *
     C*
     C*
     C     keystd        KLIST
     C                   KFLD                    FPSTDA
     C                   KFLD                    FPSTDO
      *
      * SKJEKK OM ARKIVERING ER PÅ
      *
     C                   MOVE      'si'          artype
     C                   READ      FIRM                                   33
     C     FIFIRM        CHAIN     FIRMARC                            34
     C  N34ARTYPE        CHAIN     ARKTXT                             34
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   ENDSR
     C*
     C***********************************************
     C     DSPSAD        BEGSR
     C***********************************************
     C                   CALL      'SAD005A'
     C                   PARM                    FPAVD
     C                   PARM                    FPOPD
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      3
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     LAGSADH       BEGSR
     C***********************************************
      *
      * henter neste nummer for samlefortollingen
      *
N02  C     WSNR          ifne      'H'
N06  C     WSNR          ANDNE     'B'
     C     UDAVD         CHAIN     STANDI                             33
     C                   ADD       1             SITDN
     C                   UPDATE    STANDIR
     C                   MOVE      *BLANKS       SIST
s02  c*    sitdn         sub       1             nytdn             5 0
n02  c     sitdn         sub       1             nytdn             7 0
     c                   z-add     udavd         nyavd             4 0
     c                   z-add     udavd         siavd
 N02 C                   END
      *
      *BEREGNER DETALJER henter SADH/V detaljer
      *
     c                   exsr      sumdet
      *
      * henter standardvardier fra mal-oppdrag
      *
     c     udavd         chain     fpstd                              33
     c   33*loval        setll     fpstd
     c   33              read      fpstd                                  33
s01  c* N33keystd        chain     sadh                               33
n01  c  N33keystd        chain(n)  sadh                               33
     c                   move      nyavd         siavd
     c                   move      nytdn         sitdn
     c                   move      udsg          sisg
     C     FRIBEL        ADD       TOLBEL        SIBEL3
     C     sibel3        ADD       mfrbel        SIBEL3
     C     VKTFRI        ADD       VKTTOL        SIVKB
     C     sivkb         ADD       VKTmfr        SIVKB
     c                   move      *blanks       sipos
     c                   movel     xddmm         sipos
     c                   z-add     antkll        sintk
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           SIDT
     C                   MOVE      WDT           SIFID
     c                   move      *blanks       sift3
     c                   movel     'Forenklet pr'sift3
     c     sift3         cat       'osedyre':0   sift3
     c                   move      antmot        antmota           4
     c                   move      *blanks       sift4
     c                   movel     'Antall motta'sift4
     c     sift4         cat       'gere:':0     sift4
     c     sift4         cat       antmota:1     sift4
     c                   move      *blanks       sist
     c                   write     sadhr
      *LEGGER UT VARELINJER
     C                   EXSR      LAGSADV
     c                   exsr      chgsad
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     sumdet        BEGSR
     C***********************************************
      *
      * summerer på detaljnivå
      *
     c                   move      *zeros        antmot            4 0
     c                   move      *zeros        antkll            7 0
     c                   move      *zeros        anttol            4 0
     c                   move      *zeros        antfri            4 0
     c                   move      *zeros        antmfr            4 0
     c                   setoff                                       33
N02   * spesial ved av hekting
N02  c     wsnr          ifeq      'H'
N06  c     wsnr          OREQ      'B'
N02  c                   move      'O'           sta
N02  c                   move      siavd         avdrh
N02  c                   move      sitdn         opdrh
N02  c                   else
     c                   move      'A'           sta
N02  c                   move      *zeros        avdrh
N02  c                   move      *zeros        opdrh
N02  c                   end
S02  c*    keyequ        setll     fpxref1
N02  c     keyrev        setll     fpxref1
     c     *in(33)       doweq     '0'
s02  c*    keyequ        READE     fpxref1                                33
n02  c     keyrev        READE     fpxref1                                33
     c     *in(33)       IFEQ      '0'
      *
      * hent antall kolli
      *
s01  C*    KEYSAD        CHAIN     SADH                               34
n01  C     KEYSAD        CHAIN(N)  SADH                               34
     c  N34              add       sintk         antkll
     c                   add       1             antmot
     c
     c                   move      'O'           fpstat
     c                   move      nyavd         fpavdh
     c                   move      nytdn         fpopdh
     c                   update    fpxref1r
      *
      * legger til arkiv
      *
     C     ARKJN         IFEQ      'J'
     C                   EXSR      ARKIVSR
     C                   END
      *
      * summerer sadv varelinjer
      *
     C                   EXSR      SUMDET2
     C                   END
     C                   END
      *
      * kaller arkiv til slutt
      *
     c     ARKJN         ifeq      'J'
     c                   call      'ARC002CLB'
     c                   parm                    artype
     c                   parm                    aruser
     c                   end
     C                   ENDSR
     C*
     C***********************************************
     C     sumdet2       BEGSR
     C***********************************************
     c                   move      'N'           kodfri            1
     c                   move      'N'           kodtol            1
     c                   move      'N'           kodmfr            1
N08  C                   IF        SIBEL3 = 0
N08  C                   Z-ADD     1             XTVFAK
N08  C                   ELSE
N08  C     SIBEL4        DIV(H)    SIBEL3        XTVFAK           17 9
N08  C                   END
      *
     c                   setoff                                       34
     c     KEYSAD        setll     SADV
     c     *in(34)       doweq     '0'
s02  c*    KEYSAD        READE     SADV                                   34
S08  c*    KEYSAD        READE(N)  SADV                                   34
N08  C     KEYSAD        READE     SADV                                   34
     c     *in(34)       IFEQ      '0'
N08  C     XTVFAK        MULT(H)   SVBELT        SVBELS
N08  C                   UPDATE    SADVR
     C     SVPVA         IFEQ      'F'
     c     svmfr         ifne      'F'
     c                   move      'J'           KODFRI
     C                   ADD       SVBELS        FRIBEL           11 2
S09  C*                  ADD       SVVKTB        VKTFRI            9 0
S09  C*                  ADD       SVVKTN        VKTFRN            9 0
N09  C                   ADD       SVVKTB        VKTFRI           12 3
N09  C                   ADD       SVVKTN        VKTFRN           12 3
     c                   else
     c                   move      'J'           KODMFR
     C                   ADD       SVBELS        MFRBEL           11 2
S09  C*                  ADD       SVVKTB        VKTMFR            9 0
S09  C*                  ADD       SVVKTN        VKTMFN            9 0
N09  C                   ADD       SVVKTB        VKTMFR           12 3
N09  C                   ADD       SVVKTN        VKTMFN           12 3
     c                   end
     C                   ELSE
     c                   move      'J'           KODTOL
     C                   ADD       SVBELS        TOLBEL           11 2
S09  C*                  ADD       SVVKTB        VKTTOL            9 0
S09  C*                  ADD       SVVKTN        VKTTON            9 0
N09  C                   ADD       SVVKTB        VKTTOL           12 3
N09  C                   ADD       SVVKTN        VKTTON           12 3
     C                   END
     C                   END
     C                   END
     c     kodfri        ifeq      'J'
     c                   add       1             antfri
     c                   end
     c     kodmfr        ifeq      'J'
     c                   add       1             antmfr
     c                   end
     c     kodtol        ifeq      'J'
     c                   add       1             anttol
     c                   end
     C                   ENDSR
     C*
N02  C***********************************************
N02  C     revers        BEGSR
N02  C***********************************************
N02  C                   MOVE      'O'           sta
N02  C                   MOVE      fpavdh        avdrh
N02  C                   MOVE      fpopdh        opdrh
N02   *
N02  c                   setoff                                       33
N02  c*
N02  c     keyrev        setll     fpxref1
N02  c     *in(33)       doweq     '0'
N02  c     keyrev        READE     fpxref1                                33
N02  c     *in(33)       IFEQ      '0'
N02  c
N02  c                   move      'A'           fpstat
N02  c                   move      *zeros        fpavdh
N02  c                   move      *zeros        fpopdh
N02  c                   update    fpxref1r
N02   *
N02  C                   END
N02  C                   END
N02  C                   ENDSR
     C*
N02  C***********************************************
N02  C     hektav        BEGSR
N02  C***********************************************
N03  c                   move      FPAVDH        avdrh
N03  c                   move      FPOPDH        opdrh
N02   *
N02   * summerer på detaljnivå
N02   *
N02  c     keyhekt       chain     sadh                               33
N02  c  N33              delete    sadhr
N02  c                   setoff                                       33
N02  c     keyhekt       setll     sadv
N02  c     *in33         doweq     '0'
N02  c     keyhekt       reade     sadv                                   33
N02  c     *in33         ifeq      '0'
N02  c                   delete    sadvr
N02  c                   end
N02  c                   end
N02  c                   clear                   sadhr
N02  c                   clear                   sadvr
N02  c                   move      fpavdh        siavd
N02  c                   move      fpopdh        sitdn
N02  c                   z-add     fpopdh        nytdn             7 0
N02  c                   z-add     fpavdh        nyavd             4 0
N02   *
N05  c     wsnr          ifeq      'H'
N02  c                   move      'A'           sta
N02  c     keysad        chain     fpxref                             33
N02  c                   move      'A'           fpstat
N02  c                   move      *zeros        fpavdh
N02  c                   move      *zeros        fpopdh
N02  c                   update    fpxrefr
N05  c                   end
N02   *
N02  c                   exsr      lagsadh
N02   *
N02  C                   ENDSR
     C*
     C***********************************************
     C     CHGSAD        BEGSR
     C***********************************************
     c                   move      ' '           umode             1
     C     *LIKE         DEFINE    SIAVD         XAVD
     C     *LIKE         DEFINE    SITDN         XTDN
     C                   MOVE      'CHG'         XTYPE             3
     C                   CALL      'SAD061CL'
     C                   PARM      SIAVD         XAVD
     C                   PARM      SITDN         XTDN
     C                   PARM                    SISG
     C                   PARM                    XTYPE
     C                   PARM                    UMODE
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     LAGSADV       BEGSR
     C***********************************************
     c                   move      antmfr        antmfra           4
     c                   move      antfri        antfria           4
     c                   move      anttol        anttola           4
      *
     C                   Z-ADD     0             LINJE             5 0
     c                   setoff                                       34
     c     KEYSTD        setll     SADV
     c     *in(34)       doweq     '0'
     c     KEYSTD        READE     SADV                                   34
     c     *in(34)       IFEQ      '0'
     C                   MOVE      SIAVD         SVAVD
     C                   MOVE      SITDN         SVTDN
      *
      * tollfri med mva
      *
     C     SVPVA         IFEQ      'F'
     c     svmfr         ifne      'F'
      *
     c     fribel        ifne      0
     C                   EXSR      FRISR
     c                   end
      *
      * tollfri uten mva
      *
     c                   else
     c     mfrbel        ifne      0
     C                   EXSR      MFRSR
     c                   end
     c                   end
      *
      * med toll
      *
     c                   else
     c     tolbel        ifne      0
     c                   exsr      tolsr
     c                   end
      *
      *
     C                   END
     C                   END
     C                   END
     C                   ENDSR
     C*
     C*******************************************************************
     C     ARKIVSR       BEGSR
     C*******************************************************************
      *
      * SKJEKKER OM DET ER MAILKUNDE SOM ER MOTTAKER
      *
     C                   MOVE      SIKNK         ARKUND
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      FIFIRM        ARFIRM
     C                   MOVE      FPAVD         ARAVD
     C                   MOVE      FPOPD         AROPD
     C                   MOVE      ULDT          ARDATE
     C                   MOVE      XULTM         ARTIME
     C                   MOVE      ZUSER         ARUSER
     C                   WRITE     ARKIVR
     C                   endsr
     C*
     C***********************************************
     C     PRTLIS        BEGSR
     C***********************************************
     C     XWIN          IFEQ      ' '
     C                   MOVE      'J'           XWIN              1
     C                   WRITE     WINDOWSZ
     C                   END
     c                   exfmt     printw
     c     *in12         ifeq      '0'
     C                   CALL      'FP0003CL'
     C                   PARM                    FPAVDH
     C                   PARM                    FPOPDH
     c                   PARM                    PRT              10
     c                   end
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     FRISR         BEGSR
     C***********************************************
      *FOR TOLLFRIE LINJER MED MOMS
      *
     C* BLANK LINJE
     c     svvktn        ifeq      0
     c                   write     sadvr
      *
     c                   else
     C                   Z-ADD     FRIBEL        SVBELT
     C                   Z-ADD     FRIBEL        SVBELS
     C                   Z-ADD     VKTFRI        SVVKTB
     C                   Z-ADD     VKTFRN        SVVKTN
     C                   ADD       1             LINJE
     C                   Z-ADD     LINJE         SVLI
     C                   MOVE      *BLANKS       SVTOA
     C                   MOVEL     'Ant tollfri:'SVTOA
     c     svtoa         cat       antfria:1     svtoa
     C                   WRITE     SADVR
      *
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     MFRSR         BEGSR
     C***********************************************
      *FOR TOLLFRIE LINJER UTEN MOMS
      *
      * BLANK LINJE
     c     svvktn        ifeq      0
     c                   write     sadvr
      *
     c                   else
     C                   Z-ADD     MFRBEL        SVBELT
     C                   Z-ADD     MFRBEL        SVBELS
     C                   Z-ADD     VKTMFR        SVVKTB
     C                   Z-ADD     VKTMFN        SVVKTN
     C                   ADD       1             LINJE
     C                   Z-ADD     LINJE         SVLI
     C                   MOVE      *BLANKS       SVTOA
     C                   MOVEL     'Ant t&m fri:'SVTOA
     c     svtoa         cat       antmfra:1     svtoa
     C                   WRITE     SADVR
      *
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     TOLSR         BEGSR
     C***********************************************
      *FOR TOLLpliktige
      *
      * BLANK LINJE
     c     svvktn        ifeq      0
     c                   write     sadvr
     c                   else
     C                   z-add     TOLBEL        SVBELT
     C                   z-add     TOLBEL        SVBELS
     C                   Z-ADD     VKTTOL        SVVKTB
     C                   Z-ADD     VKTTON        SVVKTN
     C                   ADD       1             LINJE
     C                   Z-ADD     LINJE         SVLI
     C                   MOVEL     'Ant m/toll: 'SVTOA
     c     svtoa         cat       anttola:1     svtoa
     C                   WRITE     SADVR
     C                   END
     c                   endsr
