      *
      **************************************************************
      *** PROGRAM SOM OVERFØRER MELLOM KONTI INNH./UTKJ. M.M.    ***
      *** BASERT PÅ REKVISISJONER UTFØRT AV EGNE BILER (TRANSP TYPE 2)
      *** (NÅR F.EKS HOVEDTRANSP. VAR PÅ LEIEBIL...INNH. VAR PÅ EN
      *** EGEN BIL. VIL LEIEBILEN VISE FOR HØYT BIDRAG INNTIL DETTE
      *** PROGRAMMET ER KJØRT FOR Å FLYTTE DET OVER TIL KONTO EGNE..
      **************************************************************
      *
     FTRAN      IF   E           K DISK
     FUNIT      IF   E           K DISK
     FTURER14   IF   E           K DISK
     FKOSBU7    UF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTGI    IF   E           K DISK
     FKOSBL01   UF A E           K DISK
     FKOSAL02   UF A E           K DISK
     FFIRMTEL   UF A E           K DISK
     FNUMML01   UF   E           K DISK                                         X010
     FSYSRL01   UF   E           K DISK
     FKODTÅ     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     D                 DS
     D  UUPERC                 1      2  0
     D  UUPERÅ                 3      4  0
     D  UUPERM                 5      6  0
     D  UUPERI                 1      6  0
     D                 DS
     D  TURACC                 1      2  0
     D  TURAAR                 3      4  0
     D  TURMND                 5      6  0
     D  TUPERI                 1      6  0
     D                 DS
     D  MINPER                 1      6  0
     D  KOÅCC                  1      2  0
     D  KOÅÅR                  3      4  0
     D  KOÅMND                 5      6  0
     C                   EXSR      INIT
     C*
      * FINN ALLE TRANSPORTØRER MED TYPE=2=INTERN/EGEN
     C     *LOVAL        SETLL     TRAN
     C                   READ      TRAN                                   94
     C     *IN94         DOWEQ     '0'
     C*  PROSESSER ALLE BILER FOR AKTUELL TRANSPORTØR..
     C     VMINCR        IFEQ      2
     C     VMTRAN        SETLL     UNIT
     C     VMTRAN        READE     UNIT                                   93
     C     *IN93         DOWEQ     '0'
     C                   EXSR      ENBIL
     C     VMTRAN        READE     UNIT                                   93
     C                   END
     C                   END
     C*
     C                   READ      TRAN                                   94
     C                   END
     C*
      * HVIS POSTER FUNNET (JOURN.NR TILDELT), LEGG UT BUNNTOTAL
     C     FIJOU3        IFNE      *ZEROS
     C                   EXSR      TOTSR
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*
     C***********************************************
     C     ENBIL         BEGSR
     C***********************************************
     C*
     C*------------------------------------------------------*
     C* HVIS REKV. ER FRA ET OPPDRAG/ER PÅ TUR/IKKE FOR HØY PERIODE/
     C*  (GEBYRKODE AV TYPE 'T'..) DA SKAL DE JUSTERES....
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""*
     C*
     C     KOSBUK        SETLL     KOSBU7
     C*
     C     KOSBUK        READE     KOSBU7                                 33
     C     *IN33         DOWEQ     '0'
     C     BUTUNR        CHAIN     TURER14                            32
     C     *IN32         IFEQ      '0'
     C     BUFER         ANDNE     'F'
     C     TUPERI        ANDLT     UUPERI
     C                   EXSR      KONTER
     C                   END
     C     KOSBUK        READE     KOSBU7                                 33
     C                   END
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KONTER        BEGSR
     C***********************************************
     C* Avd. i KEY på debetsida primært fra Oppdrag sek. fra Tur
     C     BUOAVD        IFNE      *ZEROS
     C                   MOVE      BUOAVD        DEBAVD            4 0
     C                   ELSE
     C                   MOVE      TUAVD         DEBAVD
     C                   END
     C*
     C     DEBKE1        CHAIN     KODTGI                             33
     C   33DEBKE2        CHAIN     KODTGI                             33
     C   33DEBKE3        CHAIN     KODTGI                             33
     C   33DEBKE4        CHAIN     KODTGI                             33
     C   33              MOVE      99999         DEBKTO            5 0
     C  N33              MOVE      KGIKOS        DEBKTO
      *(Rekv.side vil aldri ha avd: key med 1./2. prioritet bortfaller
     C     KREKE3        CHAIN     KODTGI                             33
     C   33KREKE4        CHAIN     KODTGI                             33
     C   33              MOVE      99999         KREKTO            5 0
     C  N33              MOVE      KGIINT        KREKTO
      *
     C* SKJEKKER OM PERIODE ER ÅPEN  (TURER ER IKKE! UPDATE - UFARLIG).
     C     KODÅKE        CHAIN     KODTÅ                              33
     C   33              MOVE      MINPE2        TUPERI
     C*
     C                   MOVEL     TURACC        PERAAR
     C                   MOVE      TURAAR        PERAAR
     C                   MOVE      TURAAR        PERAA2            2 0
     C                   Z-ADD     TURMND        PERNR
     C     KEYBI         CHAIN     KOSAL02                            33
     C   33              EXSR      HENTBI
     C                   MOVE      BUBNR         BILTXT
      * BELØP I FICURR...
     C     *LIKE         DEFINE    BUBL          NOKBL
     C     BUVAL         IFEQ      FICURR
     C     BUVAL         OREQ      *BLANKS                                                 I
     C                   Z-ADD     BUBL          NOKBL
     C                   ELSE
     C                   EXSR      VALUT
     C                   END
      * LEGG UT DEBETPOST
     C                   EXSR      GETSUR
     C                   ADD       1             POSNR
     C                   MOVE      DEBKTO        KONTO
     C                   Z-ADD     NOKBL         BBELOP
     C                   WRITE     KOSBR
      * LEGG UT KREDITPOST
     C                   EXSR      GETSUR
     C                   ADD       1             POSNR
     C                   MOVE      KREKTO        KONTO
     C                   Z-SUB     NOKBL         BBELOP
     C                   WRITE     KOSBR
      * SKRIV JOURNAL
     C                   ADD       NOKBL         KOLDEB           11 2
     C                   SUB       NOKBL         KOLKRE           11 2
      * VED FØRSTE TRANS.. HENT JOURNALNR + FRAMPROVOSER "SIDEBRUDD"
     C     FIJOU3        IFEQ      *ZEROS
     C     FIFIRM        CHAIN     FIRMTEL                            33
     C                   ADD       1             FIJOU3
     C  N33              UPDATE    FIRMTR
     C   33              WRITE     FIRMTR
     C                   SETON                                        OF
     C                   END
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C                   EXCEPT    DET
      *
      * OPPDATER STATUS I REKV.FILA...
     C                   MOVE      'I'           BUST
     C                   UPDATE    KOSBUR
      *
     C                   ENDSR
     C******************************************
     C     GETSUR        BEGSR
     C******************************************
     C*
     C* HENT NYTT SURRUGATNR - FRA SYSRF LEGG I "POSLNR"
     C*
     C     SURKEY        CHAIN     SYSRL01                            33
     C  N33              ADD       1             SISSGT
     C  N33              UPDATE    SYSRR
     C                   Z-ADD     SISSGT        POSLNR
     C*
     C                   ENDSR
     C*
     C*
     C******************************************
     C     HENTBI        BEGSR
     C******************************************
     C                   MOVE      'BILS'        NOK1              4
     C                   MOVE      '    '        NOK2              4
     C     NUMMKY        CHAIN     NUMML01                            33
     C     *IN33         IFEQ      '1'
     C                   EXSR      GMLNUM
     C                   END
     C     *IN33         IFEQ      '0'
     C                   ADD       1             SBNRNU
     C                   UPDATE    NUMMR
     C                   MOVE      SBNRNU        BILNR
     C                   END
     C*
     C                   MOVE      *BLANKS       VALKOD
     C                   WRITE     KOSAR
     C*
     C                   ENDSR
     C*
     C******************************************
     C     GMLNUM        BEGSR
     C******************************************
     C     *LIKE         DEFINE    PERAAR        TMPÅR
     C                   MOVE      PERAAR        TMPÅR
      *
     C     PERAAR        DOWGT     2002
     C                   SUB       1             PERAAR
     C     NUMMKY        CHAIN     NUMML01                            33
     C  N33              LEAVE
     C                   END
      *
     C                   MOVE      TMPÅR         PERAAR
     C                   ENDSR
     C*
     C***********************************************
     C     TOTSR         BEGSR
     C***********************************************
      * KOLONNETOTAL
     C                   EXCEPT    KOLTOT
      * AKKUMULER DEB/KRED-TOTAL
     C                   SETOFF                                       33
     C     *LOVAL        SETLL     KOSBL01
     C     TOTTAG        TAG
     C                   READ      KOSBL01                                33
     C  N33              DO
     C     BBELOP        IFGT      *ZEROS
     C                   ADD       BBELOP        DEBTOT           11 2
     C                   ELSE
     C                   ADD       BBELOP        KRETOT           11 2
     C                   END
     C                   GOTO      TOTTAG
     C                   END
     C                   EXCEPT    DEBKRE
      * LIST OPP BRUKTE BILAGSNR..
     C   OF              EXCEPT    PHEAD
     C                   EXCEPT    PNYBIH
     C     *LOVAL        SETLL     KOSAL02
     C     LESA1         TAG
     C                   READ(N)   KOSAL02                                33
     C  N33
     CAN OF              EXCEPT    PHEAD
     C  N33              EXCEPT    PNYBI
     C  N33              GOTO      LESA1
     C                   ENDSR
     C*
     C***********************************************
     C     VALUT         BEGSR
     C***********************************************
     C*
     C     VALU1K        CHAIN     VALUL01                            33
     C     *IN33         IFEQ      '0'
     C     VALKU2        IFNE      0
     C                   MOVE      VALKU2        VALKU1
     C                   END
     C     BUBL          MULT      VALKU1        XWORK            15 4
     C     XWORK         DIV(H)    OMRFAK        NOKBL
     C                   END
     C   33              Z-ADD     BUBL          NOKBL
     C* INITIERE FELT SOM HAR SAMME NAVN I POST.FIL....
     C                   MOVE      *BLANKS       VALKOD
     C                   MOVE      *ZEROS        VALKU1
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C* KJØRER UBETINGET FRAM TIL OG MED FOREGÅENDE!!! PERIODE
     C     UYEAR         IFGT      50
     C                   MOVE      '19'          UUPERC
     C                   ELSE
     C                   MOVE      '20'          UUPERC
     C                   END
     C                   MOVE      UYEAR         UUPERÅ
     C                   MOVE      UMONTH        UUPERM
     C*
     C     SURKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    REGNA
     C                   MOVE      'SYS   '      REGNA             6
     C     KEYBI         KLIST
     C                   KFLD                    FIFIRM
     c                   kfld                    aktkodk
     C                   KFLD                    PERAAR
     C                   KFLD                    PERNR
     c                   move      'A'           aktkodk           1
     C     KOSBUK        KLIST
     C                   KFLD                    YYSTAT
     C                   KFLD                    UNBILN
     C                   MOVE      ' '           YYSTAT            1
      * BILKODE(KONTO) FRA HOVEDOPPDRAG BELASTES...
      * (PÅ DEBETSIDA KAN AVD. (FRA OPPDRAGET) VÆRE AVGJØRENDE)
     C     DEBKE1        KLIST
     C                   KFLD                    TUBILK
     C                   KFLD                    DEBAVD
     C                   KFLD                    TUBILK
     C     DEBKE2        KLIST
     C                   KFLD                    TUBILK
     C                   KFLD                    DEBAVD
     C                   KFLD                    NULBIK
     C     DEBKE3        KLIST
     C                   KFLD                    TUBILK
     C                   KFLD                    NULAVD
     C                   KFLD                    TUBILK
     C     DEBKE4        KLIST
     C                   KFLD                    TUBILK
     C                   KFLD                    NULAVD
     C                   KFLD                    NULBIK
      * BILKODE(KONTO) FRA REKVISISJON KREDITERES (INNTEKTFØRES...)
      * (PÅ KREDITSIDA FINNES IKKE AVDELINGSBEGREP)
     C     KREKE3        KLIST
     C                   KFLD                    UNTRME
     C                   KFLD                    NULAVD
     C                   KFLD                    UNTRME
     C     KREKE4        KLIST
     C                   KFLD                    UNTRME
     C                   KFLD                    NULAVD
     C                   KFLD                    NULBIK
     C                   MOVE      *ZEROS        NULAVD            4 0
     C                   MOVE      *BLANKS       NULBIK            3
     C* VALUTAFIL
     C     VALU1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    BUVAL
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL KODTÅ   ÅPNE PERIODER
     C*--------------------------------------------------------------
     C     KODÅKE        KLIST
     C                   KFLD                    KOÅUNI
     C                   KFLD                    TURACC
     C                   KFLD                    TURAAR
     C                   KFLD                    TURMND
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL NUMML01
     C*--------------------------------------------------------------
     C     NUMMKY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    PERAAR
     C                   KFLD                    NOK1
     C                   KFLD                    NOK2
      * FINN LAVESTE ÅPNE PERIODE (FOR KOSTNADER)
     C*                    MOVE 9999      MINPE2  40
     C                   MOVE      'K'           KOÅUNI
     C                   MOVE      *ZEROS        KOÅMND
     C                   MOVE      *ZEROS        KOÅÅR
     C                   MOVE      *ZEROS        KOÅCC
     C     'K'           SETLL     KODTÅ
     C     'K'           READE     KODTÅ                                  33
     C     *IN33         IFEQ      '1'
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          MINPE2            6 0
     C                   ELSE
     C     UYEAR         ADD       2000          MINPE2
     C                   END
     C                   MULT      100           MINPE2
     C                   MOVE      UMONTH        MINPE2
     C                   ELSE
     C                   MOVE      MINPER        MINPE2
     C                   END
     C* N33                GOTO STMIN
     C* INITIER DATASTRUKTURER
     C                   MOVE      *ZEROS        TUPERI
     C*
     C                   READ      FIRM                                   33
     C*
      * DIVERSE FASTE VERDIER I L.1. POSTERINGSFIL..
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      FIFIRM        FIRMA
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          BILAAR
     C                   ELSE
     C     UYEAR         ADD       2000          BILAAR
     C                   END
     C                   MOVE      UMONTH        BILMND
     C                   MOVE      UDAY          BILDAG
     C                   MOVE      'H'           TYPE
     C                   MOVE      *ZEROS        KSTED
     C                   MOVE      *ZEROS        KBARER
     C                   MOVE      '0'           MOMSK
     C                   MOVE      *ZEROS        PROSNR
     C                   MOVE      ' '           OPDKOD
     C                   MOVEL     'REKVIS.:'    BILTXT
     C*
     C                   ENDSR
     C*
     OLISTE     E            PHEAD            01
     O                       FIFT                45
     OLISTE     E            PHEAD            02
     O                                           24 'INTERNOVERFØRINGS - JOUR'
     O                                           42 'NAL (REKV.)       '
     O                       FIJOU3              44
     O                                          106 'DATO:'
     O                       UDATE         Y    115
     O                                          120 'SIDE:'
     O                       PAGE          Z    126
     OLISTE     E            PHEAD            03
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     O          E            PHEAD            04
     O                                           50 '--- FRA HOVEDOPPDRAG ---'
     O                                           95 '---- TIL UTFØRENDE -----'
     O          E            PHEAD            05
     O                                            8 'BILNR.'
     O                                           13 'AVD.'
     O                                           21 'OPPD.NR'
     O                                           32 'TURNUMMER'
     O                                           37 'BKD'
     O                                           43 'KONTO'
     O                                           46 'MK'
     O                                           61 '"DEBET"-BELØP'
     O                                           80 '"KREDIT"-BELØP'
     O                                           88 'REKV.NR'
     O                                           92 'BKD'
     O                                           98 'KONTO'
     O                                          101 'MK'
     O                                          117 'UTF. AV BIL'
     O                                          122 'REF'
     O                                          131 'PERIODE'
     OLISTE     E            PHEAD          1 06
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            DET            1
     O                       BILNR         Z      8
     O                       BUOAVD        Z     13
     O                       BUOPD         Z     21
     O                       BUTUNR              31
     O                       TUBILK              37
     O                       DEBKTO              43
     O                                           45 '0'
     O                       NOKBL         J     61
     O                       BBELOP        J     80
     O                       BUBNR         Z     88
     O                       UNTRME              92
     O                       KREKTO              98
     O                                          100 '0'
     O                       BUBILN             116
     O                       BUSG               122
     O                       TURMND             127
     O                                          128 '/'
     O                       TURAAR             130
     OLISTE     E            PNYBIH      2  2
     O                                           25 '** BILAGSNUMMER BENYTTET'
     OLISTE     E            PNYBI       0  1
     O                                            9 'Periode:'
     O                       PERNR               13
     O                                           14 '/'
     O                       PERAA2              16
     O                                           18 '='
     O                       BILNR         Z     26
     OLISTE     E            KOLTOT      2
     O                                           25 '** KOLLONNETOTALER... **'
     O                       KOLDEB        J     61
     O                       KOLKRE        J     80
     OLISTE     E            DEBKRE      2
     O                                           25 '** DEB/KRED POSTTOTAL **'
     O                       DEBTOT        J     61
     O                       KRETOT        J     80
