     A*****************************************************************
     A*           UTSKRIFT AV STATISTIKK                              *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=198, OVERFLOW=45      *
     A*****************************************************************
      * INDIKATOR 41 PÅ  -->  TYPE A, AGENT
      *           42 PÅ  -->  TYPE B, AVDELING
      *           40 PÅ  -->  TYPE C, RUNDTUR
      *           59 PÅ  -->  TYPE D, FAKTURAMOTTAGER/OPPDRAGSTYPE
      *           67 PÅ  -->  TYPE E, AGENT/FRANKATUR
      *           64 PÅ  -->  TYPE F, FRANKATUR
      *           65 PÅ  -->  TYPE G, FAKTURAMOTTAGER/FRANKATUR
      *           61 PÅ  -->  TYPE H, FAKTURAMOTTAGER/FRA STED
      *           62 PÅ  -->  TYPE I, FAKTURAMOTTAGER/FRA STED
      *           66 PÅ  -->  TYPE J, KUNDER SOM "PART" PÅ OPPDRAG
      *           43 PÅ  -->  TYPE K, FAKTURAMOTTAGER
      *           44 PÅ  -->  TYPE L, FRA LAND / TIL LAND
      *           58 PÅ  -->  TYPE M, FRA REGION / TIL REGION
      *           55 PÅ  -->  TYPE N, FRA STED / TIL STED
      *           45 PÅ  -->  TYPE O, OPPDRAGSTYPE
      *           53 PÅ  -->  TYPE P, FRA STED
      *           54 PÅ  -->  TYPE Q, TIL STED
      *           56 PÅ  -->  TYPE R, TUR
      *           46 PÅ  -->  TYPE S, SIGNATUR
      *           47 PÅ  -->  TYPE T, TRANSPORTØR
      *           48 PÅ  -->  TYPE U, FAKTURAMOTTAGER/LAND
      *           49 PÅ  -->  TYPE V, FAKTURAMOTTAGER/AGENT
      *           50 PÅ  -->  TYPE W, VAREMOTTAGER
      *           57 PÅ  -->  TYPE X, FAKTURAMOTTAGER/REGION
      *           51 PÅ  -->  TYPE Y, BILNR
      *           52 PÅ  -->  TYPE Z, BILKODE
      *
     A          R LHEAD1
     A            KFMT1         75      1  1
     A            KFMT2         75      1 76
     A  88                              2  1'¤&l1O'
     A  88                              2  7'¤(12U¤(s0p19h0s3b4099T'
     A                                  3  2MSGCON(30 DIV0542 SYMSGF)
     A                                  3132MSGCON(20 YBC0421 SYMSGF)
     A                                  3154DATE EDTCDE(Y)
     A                                  3164TIME
     A                                  3182MSGCON(6 YBC0422 SYMSGF)
     A            XSIDE          5  0   3190
     A                                  4  2MSGCON(12 DIV0559 SYMSGF)
     A  39                              4 15MSGCON(27 DIV2210 SYMSGF)
     A  40                              4 15MSGCON(27 DIV4191 SYMSGF)
     A  41                              4 15MSGCON(27 DIV0454 SYMSGF)
     A  42                              4 15MSGCON(27 DIV0459 SYMSGF)
     A  43                              4 15MSGCON(27 DIV0485 SYMSGF)
     A  44                              4 15MSGCON(27 DIV0460 SYMSGF)
     A  45                              4 15MSGCON(27 DIV0462 SYMSGF)
     A  46                              4 15MSGCON(27 DIV0464 SYMSGF)
     A  47                              4 15MSGCON(27 DIV0466 SYMSGF)
     A  48                              4 15MSGCON(27 DIV0468 SYMSGF)
     A  49                              4 15MSGCON(27 DIV0470 SYMSGF)
     A  50                              4 15MSGCON(27 DIV0471 SYMSGF)
     A  51                              4 15MSGCON(27 DIV4194 SYMSGF)
     A  52                              4 15MSGCON(27 DIV4195 SYMSGF)
     A  53                              4 15MSGCON(27 DIV4184 SYMSGF)
     A  54                              4 15MSGCON(27 DIV4185 SYMSGF)
     A  55                              4 15MSGCON(27 DIV4180 SYMSGF)
     A  56                              4 15MSGCON(27 DIV4181 SYMSGF)
     A  57                              4 15MSGCON(27 DIV4182 SYMSGF)
     A  58                              4 15MSGCON(27 DIV4183 SYMSGF)
     A  59                              4 15MSGCON(27 DIV4192 SYMSGF)
     A  64                              4 15MSGCON(27 YBC0307 SYMSGF)
     A  65                              4 15MSGCON(27 YBC0308 SYMSGF)
     A  66                              4 15MSGCON(27 YBC0309 SYMSGF)
     A  61                              4 15MSGCON(27 YBC0626 SYMSGF)
     A  62                              4 15MSGCON(27 YBC0627 SYMSGF)
     A  67                              4 15MSGCON(27 YBC1908 SYMSGF)
      * IND. 71 PÅ  -->  INVALGTE AVDELINGER
     A  71N42 90                        4 51MSGCON(20 YBC0423 SYMSGF)
     A  71N42 90  WSAVD1         4  0O  4 72EDTCDE(4)
     A  71N42 90  WSAVD2         4  0O  4 77EDTCDE(4)
     A  71N42 90  WSAVD3         4  0O  4 82EDTCDE(4)
     A  71N42 90  WSAVD4         4  0O  4 87EDTCDE(4)
     A  71N42 90  WSAVD5         4  0O  4 92EDTCDE(4)
      * IND. 72 PÅ  -->  INVALGT AVDELINGSGRUPPE
     A  72    90                        4 51MSGCON(20 YBC0424 SYMSGF)
     A  72    90  WSAVDG         7A     4 72
      * IND. 73 PÅ  -->  VISE GRUPPER
     A  73                              4111MSGCON(20 YBC0425 SYMSGF)
     A  73        XINKGR        19A     4132
      *
     A  40                              5  2MSGCON(020 YBC0396 *LIBL/SYMSGF)
     A  41                              5  2MSGCON(020 YBC0390 *LIBL/SYMSGF)
     A  43
     AO 48
     AO 49
     AO 57
     AO 59
     AO 61
     AO 62
     AO 65                              5  2MSGCON(020 YBC0403 *LIBL/SYMSGF)
     A  67                              5  2MSGCON(020 YBC0390 *LIBL/SYMSGF)
     A  44                              5  2MSGCON(020 DIV0545 *LIBL/SYMSGF)
     A  45                              5  2MSGCON(020 YBC0397 *LIBL/SYMSGF)
     A  46                              5  2MSGCON(020 YBC0400 *LIBL/SYMSGF)
     A  47                              5  2MSGCON(020 YBC0401 *LIBL/SYMSGF)
     A  50                              5  2MSGCON(020 YBC0404 *LIBL/SYMSGF)
     A  51                              5  2MSGCON(020 YBC0406 *LIBL/SYMSGF)
     A  52                              5  2MSGCON(020 YBC0407 *LIBL/SYMSGF)
     A  53                              5  2MSGCON(020 DIV4206 *LIBL/SYMSGF)
     A  54                              5  2MSGCON(020 DIV4207 *LIBL/SYMSGF)
     A  55                              5  2MSGCON(020 DIV4200 *LIBL/SYMSGF)
     A  56                              5  2MSGCON(020 YBC0417 *LIBL/SYMSGF)
     A  58                              5  2MSGCON(020 DIV4204 *LIBL/SYMSGF)
     A  64                              5  2MSGCON(020 YBC0399 *LIBL/SYMSGF)
     A  66                              5  2MSGCON(020 YBC0416 *LIBL/SYMSGF)
     A  40        WSRUND         8  0   5 23EDTCDE(Z)
     A  41
     AO 43
     AO 47
     AO 48
     AO 49
     AO 50
     AO 56
     AO 57
     AO 59        XXKN           8  0   5 23EDTCDE(Z)
     A  65
     AO 61
     AO 61
     AO 67
     AO 66        XXKNX          8  0   5 23EDTCDE(Z)
     A  44
     AO 53
     AO 54
     AO 55        XXLK           2A     5 23
     A  53
     AO 54
     AO 55        XXSD           5A     5 27
     A  45        XXOT           2A     5 23
     A  46        XXSG           3A     5 23
     A  51        XXBN          10A     5 23
     A  52        XXBK           3A     5 23
     A  56        WSTUR          8  0   5 23EDTCDE(Z)
     A  58        XXRG           2A     5 23
     A  64        XXFR           3A     5 23
     A            XTXT1         30A     5 34
      *
     A  43 38                           5 71MSGCON(020 YBC0869 *LIBL/SYMSGF)
     A  44                              5 71MSGCON(020 DIV0546 *LIBL/SYMSGF)
     A  48                              5 71MSGCON(020 YBC0402 *LIBL/SYMSGF)
     A  49                              5 71MSGCON(020 YBC0390 *LIBL/SYMSGF)
     A  51                              5 71MSGCON(020 JOV0515 *LIBL/SYMSGF)
     A  55                              5 71MSGCON(020 DIV4201 *LIBL/SYMSGF)
     A  57                              5 71MSGCON(020 YBC0405 *LIBL/SYMSGF)
     A  58                              5 71MSGCON(020 DIV4205 *LIBL/SYMSGF)
     A  59                              5 71MSGCON(020 YBC0397 *LIBL/SYMSGF)
     A  65                              5 71MSGCON(020 YBC0399 *LIBL/SYMSGF)
     A  61                              5 71MSGCON(020 DIV4200 *LIBL/SYMSGF)
     A  62                              5 71MSGCON(020 DIV4201 *LIBL/SYMSGF)
     A  67                              5 71MSGCON(020 YBC0399 *LIBL/SYMSGF)
     A  43 38     W2KG           2A     5 92
     A  44
     AO 61
     AO 62
     AO 55        X2LK           2A     5 92
     A  61
     AO 62
     AO 55        X2SD           5A     5 97
     A  48        X2LKF          2A     5 92
     A  49
     AO 51        X2KN           8  0   5 92EDTCDE(Z)
     A  57        X2RGF          2A     5 92
     A  58        X2RG           2A     5 92
     A  59        X2OT           2A     5 92
     A  65
     AO 67        X2FR           3A     5 92
     A            XTXT2         30A     5104
      *
     A  48                              5141MSGCON(020 YBC0418 *LIBL/SYMSGF)
     A  51                              5141MSGCON(020 JOV0516 *LIBL/SYMSGF)
     A  57                              5141MSGCON(020 YBC0419 *LIBL/SYMSGF)
     A  48        X2LKT          2A     5162
     A  57        X2RGT          2A     5162
     A            XTXT3         30A     5166
      *
     A    N90                           6  2MSGCON(020 YBC0395 *LIBL/SYMSGF)
     A    N90     XXAVD          4  0   6 23EDTCDE(Z)
     A    N90     XTXT4         30A     6 34
     A N39 90                           6  2MSGCON(036 YBC0427 *LIBL/SYMSGF)
     A  39 90                           6  2MSGCON(036 YBC0454 *LIBL/SYMSGF)
     A                                  7  2' '
      *
     A          R LHEAD2                    SPACEB(1)
     A                                     2MSGCON(10 DIV0563 SYMSGF)
     A                                      HIGHLIGHT
     A  88                                 1'¤*c4000A¤*c100B¤*c11G¤*c2P'
     A            SWDTÅ          2  0O    13HIGHLIGHT
     A                                      SPACEA(1)
     A                                    23MSGCON(79 YBC0428 SYMSGF)
     A                                   103MSGCON(92 YBC0429 SYMSGF)
     A                                      SPACEA(1)
     A                                     1'----------------------|--'
     A                                    26'-------------------------'
     A                                    51'-------------------------'
     A                                    76'-------------------------'
     A                                   101'-------------------------'
     A                                   126'-------------------------'
     A                                   151'-------------------------'
     A                                   176'-----|-------------------'
     A                                   197'---------------------'
      *
     A          R LDET1                     SPACEB(1)
     A            SWTXT         20A  O     2
     A                                    23'|'
     A            SWE01         11  0O    25EDTCDE(M)
     A            SWE02         11  0O    38EDTCDE(M)
     A            SWE03         11  0O    51EDTCDE(M)
     A            SWE04         11  0O    64EDTCDE(M)
     A            SWE05         11  0O    77EDTCDE(M)
     A            SWE06         11  0O    90EDTCDE(M)
     A            SWE07         11  0O   103EDTCDE(M)
     A            SWE08         11  0O   116EDTCDE(M)
     A            SWE09         11  0O   129EDTCDE(M)
     A            SWE10         11  0O   142EDTCDE(M)
     A            SWE11         11  0O   155EDTCDE(M)
     A            SWE12         11  0O   168EDTCDE(M)
     A                                   181'|'
     A            SWE13         11  0O   183EDTCDE(M)
      * PRINT VEKTGRUPPE INNDELINGER
     A          R LDET2                     SPACEB(1)
     A                                     2MSGCON(20 YBC0451 SYMSGF)
     A                                    23'|'
     A            KOVK1          5  0O    28EDTCDE(4)
     A                                    34'KG'
     A            KOVK2          5  0O    41EDTCDE(4)
     A                                    47'KG'
     A            KOVK3          5  0O    54EDTCDE(4)
     A                                    60'KG'
     A            KOVK4          5  0O    67EDTCDE(4)
     A                                    73'KG'
     A            KOVK5          5  0O    80EDTCDE(4)
     A                                    86'KG'
     A            KOVK6          5  0O    93EDTCDE(4)
     A                                    99'KG'
     A            KOVK7          5  0O   106EDTCDE(4)
     A                                   112'KG'
     A            KOVK8          5  0O   119EDTCDE(4)
     A                                   125'KG'
     A            KOVK9          5  0O   132EDTCDE(4)
     A                                   138'KG'
     A            KOVK10         5  0O   145EDTCDE(4)
     A                                   151'KG'
     A            KOVK11         5  0O   158EDTCDE(4)
     A                                   164'KG'
     A                                   171'99999'
     A                                   177'KG'
     A                                   181'|'
      * DEKNINSBIDRAG
     A          R LDET3                     SPACEB(1)
     A                                     2MSGCON(20 YBC0431 SYMSGF)
     A                                    23'|'
     A            XDB01          7  2O    28EDTCDE(M)
     A            XDB02          7  2O    41EDTCDE(M)
     A            XDB03          7  2O    54EDTCDE(M)
     A            XDB04          7  2O    67EDTCDE(M)
     A            XDB05          7  2O    80EDTCDE(M)
     A            XDB06          7  2O    93EDTCDE(M)
     A            XDB07          7  2O   106EDTCDE(M)
     A            XDB08          7  2O   119EDTCDE(M)
     A            XDB09          7  2O   132EDTCDE(M)
     A            XDB10          7  2O   145EDTCDE(M)
     A            XDB11          7  2O   158EDTCDE(M)
     A            XDB12          7  2O   171EDTCDE(M)
     A                                   181'|'
     A            XDB13          7  2O   186EDTCDE(M)
      * ÅRSSKILLE
     A          R LDET4                     SPACEB(1)
     A                                     1'*************************'
     A                                    26'*************************'
     A                                    51'*************************'
     A                                    76'*************************'
     A                                   101'*************************'
     A                                   126'*************************'
     A                                   151'*************************'
     A                                   176'*************************'
     A                                   197'*********************'
     A                                      SPACEA(1)
      * KUBIKK
     A          R LDET5                     SPACEB(1)
     A            SWTXT         20A  O     2
     A                                    23'|'
     A            SWM01          9  3O    25EDTCDE(M)
     A            SWM02          9  3O    38EDTCDE(M)
     A            SWM03          9  3O    51EDTCDE(M)
     A            SWM04          9  3O    64EDTCDE(M)
     A            SWM05          9  3O    77EDTCDE(M)
     A            SWM06          9  3O    90EDTCDE(M)
     A            SWM07          9  3O   103EDTCDE(M)
     A            SWM08          9  3O   116EDTCDE(M)
     A            SWM09          9  3O   129EDTCDE(M)
     A            SWM10          9  3O   142EDTCDE(M)
     A            SWM11          9  3O   155EDTCDE(M)
     A            SWM12          9  3O   168EDTCDE(M)
     A                                   181'|'
     A            SWM13          9  3O   183EDTCDE(M)
