      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE002 MODULE(*LIBL/TJGE002 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 mauelle (*LE)tekster = vis både Local+english
12XXX * 02 JOV PTF12 flere felt med i svaret
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
n04  FTRACKL01  IF   E           K DISK    USROPN
n04   *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
N01  D UtText          s            150
N01  D UtTexL          s            150
N02  D TTBREX          s             15
N02  D TTLENX          s             15
n11  D                 DS
N11  D  TTDATE                 1      8  0
N11  D  TTAA                   3      4
N11  D  TTMM                   5      6
N11  D  TTDG                   7      8
n11  D                 DS
N11  D  TTTIME                 1      6  0
N11  D  TTTIM4                 1      4  0
N11  D  TTTIT                  1      2
N11  D  TTTIM                  3      4
N11  D  TTTIS                  5      6
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, '
        +'¬avdeling¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬oppdrag¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"trackTraceEvents" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
     c     Error         cabne     *blanks       Utliste
     C     Key           SETLL     TRACKL01
     C     Key           READE     TRACKL01                               55
     C     *IN55         DOWEQ     '0'
     C                   Add       1             AntLest
N01  C                   EVAL      UTTEXT = TTTEXT
N01  C                   EVAL      UTTEXL = TTTEXL
N01   * manuell tekst / begge linjer uavh. av språk (*LE = Local+English )
N01  c                   if        TTEDEV = '*LE'
N01  C                   EVAL      UTTEXT = %trim(TTTEXL)+' ' + TTTEXT
N01  C                   EVAL      UTTEXL = %trim(TTTEXL)+' ' + TTTEXT
N01  c                   endif
N02   * EDITERING AV LAT/LONG - BLANK HVIS 0
N02  C                   if        TTBRE = *zeros
N02  C                   eval      TTBREX=*blanks
N02  C                   else
N02  C                   eval      TTBREX=%trim(%editw(TTBRE:'  .      '))
N02  C                   endif
N02  C                   if        TTLEN = *zeros
N02  C                   eval      TTLENX=*blanks
N02  C                   else
N02  C                   eval      TTLENX=%trim(%editw(TTLEN:'   .      '))
N02  C                   endif
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬frBrev¬ : ¬'+%trim(%editc(TTFBNR:'Z'))+'¬, '
          +'¬event¬ : ¬'+%trim(TTACTI)+'¬, '
s02       //+'¬date¬ : ¬'+%trim(%editc(TTDATE:'Z'))+'¬, '
s02       //+'¬time¬ : ¬'+%trim(%editc(TTTIM4:'Z'))+'¬, '
S01       //+'¬date¬ : ¬'+%trim(%editc(TTDATE:'Z'))+'¬, '
S01       //+'¬time¬ : ¬'+%trim(%editc(TTTIM4:'Z'))+'¬, '
S01       //+'¬textEng¬ : ¬'+%trim(TTTEXT)+'¬, '
S01       //+'¬textLoc¬ : ¬'+%trim(TTTEXL)+'¬, '
n01       +'¬date¬ : ¬'+%trim(%editc(TTDATE:'X'))+'¬, '
n01       +'¬time¬ : ¬'+%trim(%editc(TTTIM4:'X'))+'¬, '
N01       +'¬textEng¬ : ¬'+%trim(UTTEXT)+'¬, '
N01       +'¬textLoc¬ : ¬'+%trim(UTTEXL)+'¬, '
n02       +'¬tttime¬ : ¬'+%trim(%editc(TTTIME:'X'))+'¬, '
N02       +'¬tttext¬ : ¬'+%trim(TTTEXT)+'¬, '
N02       +'¬tttexl¬ : ¬'+%trim(TTTEXL)+'¬, '
          +'¬ediCode¬ : ¬'+%trim(TTEDEV)+'¬, '
          +'¬ediReason¬ : ¬'+%trim(TTEDRE)+'¬, '
          +'¬depot¬ : ¬'+%trim(TTDEPO)+'¬, '
          +'¬name¬ : ¬'+%trim(TTNAME)+'¬, '
          +'¬status¬ : ¬'+%trim(TTMANU)+'¬, '
N02       +'¬tripNo¬ : ¬'+%trim(%editc(TTTUR:'Z'))+'¬, '
N02       +'¬locCode¬ : ¬'+%trim(TTLOCC)+'¬, '
N02       +'¬locText¬ : ¬'+%trim(TTLOCT)+'¬, '
N02       +'¬latitude¬ : ¬'+%trim(TTBREX)+'¬, '
N02       +'¬longitude¬ : ¬'+%trim(TTLENX)+'¬, '
N02       +'¬eventURL¬ : ¬'+%trim(TTURL)+'¬, '
N02       +'¬manualCode¬ : ¬'+%trim(TTMAN2)+'¬, '
          +'¬user¬ : ¬'+%trim(TTUSER)+'¬, '
          +'¬createDate¬ : ¬'+%trim(%editc(TTDATL:'Z'))+'¬, '
          +'¬createTime¬ : ¬'+%trim(%editc(TTTIML:'Z'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     C     Key           READE     TRACKL01                               55
     C                   ENDDO
     c     Utliste       tag
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     TRACKL01

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD   = zhbgetvar('AVD')
     C                   EVAL      AVDN  = c2n2(AVD)
     C                   EVAL      OPD   = zhbgetvar('OPD')
     C                   EVAL      OPDN  = c2n2(OPD)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      TRACKL01
      *
     C     Key           KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
      *
     c                   endsr
