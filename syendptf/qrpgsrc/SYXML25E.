********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF12 nullstill MVA
12268 * 02 YBC PTF12 Flere vedlegg
********************************************************************
      ***************************************************************************************
      * Mottak av EHF faktura
      * merk at embedded PDF ikke behandles av dette prog men er alt konvertert av XMLRCVEHF
      ***************************************************************************************
     H DATEDIT(*DMY)
     FXMLRCVF   IF   E           K DISK
     FXMLRCAF   IF   E           K DISK
     FKOSTA     O  A E           K DISK
     FKOSTA2    IF   E           K DISK    RENAME(KOSTAR:KOSTAR2)
     F                                     PREFIX(K2_)
     FKOSTB     O  A E           K DISK
     FKOSTT     UF   E           K DISK
     FFIRM      IF   E           K DISK
     FLEVEL10   IF   E           K DISK
     FFIRMNA    IF   E           K DISK
     FNAVLEV    if   e           K DISK
     FHEADF     IF   E           K DISK
     FFRISOL01  IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FKONTL01   IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FARKIVP    O  A E           K DISK
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULÅÅA                  1      4
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  KABDT                  1      8  0
     D    KABDT_ÅR             1      4  0
     D    KABDT_MN             5      6  0
     D                 DS
     D  kbrefc                 1     35
     D  fastko                 1      3
     D  kbrefcko               4      8
     D  fastav                 9     11
     D  kbrefcav              12     15
     D  fastba                16     18
     D  kbrefcba              19     22
     D  fastpr                23     25
     D  kbrefcpr              26     33
     D                 DS
     D  KO                     1     40  0
     D                                     DIM(8)                               I/O. frankatur
     D  NAVKO1                 1      5  0
     D  NAVKO2                 6     10  0
     D  NAVKO3                11     15  0
     D  NAVKO4                16     20  0
     D  NAVKO5                21     25  0
     D  NAVKO6                26     30  0
     D  NAVKO7                31     35  0
     D  NAVKO8                36     40  0
     D  NAVKO28                6     40  0
     D                 DS
     D  AV                     1     32  0
     D                                     DIM(8)                               I/O. frankatur
     D  NAVAV1                 1      4  0
     D  NAVAV2                 5      8  0
     D  NAVAV3                 9     12  0
     D  NAVAV4                13     16  0
     D  NAVAV5                17     20  0
     D  NAVAV6                21     24  0
     D  NAVAV7                25     28  0
     D  NAVAV8                29     32  0
     D  NAVAV28                5     32  0
     D                 DS
     D  BA                     1     32  0
     D                                     DIM(8)                               I/O. frankatur
     D  NAVBA1                 1      4  0
     D  NAVBA2                 5      8  0
     D  NAVBA3                 9     12  0
     D  NAVBA4                13     16  0
     D  NAVBA5                17     20  0
     D  NAVBA6                21     24  0
     D  NAVBA7                25     28  0
     D  NAVBA8                29     32  0
     D  NAVBA28                5     32  0
     D                 DS
     D  PR                     1     64  0
     D                                     DIM(8)                               I/O. frankatur
     D  NAVPR1                 1      8  0
     D  NAVPR2                 9     16  0
     D  NAVPR3                17     24  0
     D  NAVPR4                25     32  0
     D  NAVPR5                33     40  0
     D  NAVPR6                41     48  0
     D  NAVPR7                49     56  0
     D  NAVPR8                57     64  0
     D  NAVPR28                9     64  0
      * workvariabler
     D xxLEVorg        S              9A
     D xxCUSorg        S              9A
     D xxCustID        S             15A
     D xxCustNr        S              8S 0
S02  D*xxDR            S             70A   DIM(10)
N02  D xxDR            S             70A   DIM(100)
     D xxDRNo          S              3S 0
     D xxURI           S            120A
     D xxAtt           S             50A
     D XXOrdRef        S             17A
     D XXSupOrd        S             17A
     D XXInvNo         S             17A
     D XXInvTy         S              3A
     D XXDueDt         S              8A
     D XXInvDt         S              8A
     D XXKidNo         S             30A
     D xxCURR          S              3A
     D xxDesc          S             35A
     D xxSREF          S             20A
     D xxTotBl         S             13S 2
     D xxTotMVA        S             13S 2
     D xxVal           S              3A
     D xxPris          S             11S 2
     D xxBEL           S             11S 2
     D xxMVA           S             11S 2
     D xxstk           S             11S 2
     D xxLastCode      S              3A
     D xxIFBnr         S             20A
     D xxAttach        S              1A
     D xxGEBsy         S              3A
     D xxGEBxx         S             35A
     D NEWDIR          S            100
     D NEWFILE         S            100
     D NEWOBJ          S            200
     D NEWRAN          S             10
      *
     C                   EXSR      INIT
      *
      * Les EN OG EN TAG ("Nøstede tager" kommer nå i "rett rekkefølge" )
     C     XRSN          setll     XMLRCVF
     C     XRSN          reade     XMLRCVF                                94
     C     *in94         doweq     '0'
     C                   EXSR      SRXML2
     C     XRSN          reade     XMLRCVF                                94
     C                   enddo
      * write KOSTA
     c     KABNR         ifne      *zeros
     C                   WRITE     KOSTAR
     C                   EXSR      ARKIV
     c                   endif
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *
      ***********************************************
     C     SRXML2        BEGSR
      ***********************************************
     C                   SELECT
     C                   WHEN      %SUBST(XRPATH:1:5) = '/inv:'
     C                   EVAL      X_XRPATH = '/' + %SUBST(XRPATH:6:251)
     C                   WHEN      %SUBST(XRPATH:1:11) = '/CreditNote'
     C                   EVAL      X_XRPATH = '/Invoice' + %SUBST(XRPATH:12)
     C                   OTHER
     C                   EVAL      X_XRPATH = XRPATH
     C                   ENDSL
      * Handling utfra tag-navn
      *    ( ekstra tester dersom samme tagnavn fins på flere nivå                     )
      *    ( les i filene XMLRCAF (attributt-verdier)+ XMLRCLF ("Extras") om nødvendig )
     C                   SELECT
      *   faktnr
     C                   WHEN      XRNV = 'cbc:ID' and X_XRPATH='/Invoice'
     c                   eval      xxInvNo  = XRVERD
      *   avsenders ID/ORG-nr
     C                   WHEN      XRNV = 'cbc:CompanyID' and
     c                             X_XRPATH='/Invoice'
     c                             +'/cac:AccountingSupplierParty'
     c                             +'/cac:Party/cac:PartyLegalEntity '
     c                   eval      xxLEVORG = XRVERD
      *   mottakes  ID/ORG-nr (ramberg = 987655364  )
     C                   WHEN      XRNV = 'cbc:CompanyID' and
     C                             X_XRPATH='/Invoice'
     C                             +'/cac:AccountingCustomerParty'
     C                             +'/cac:Party/cac:PartyLegalEntity'
     c                   eval      xxCUSORG = XRVERD
      * InvoiceDate
     C                   WHEN      XRNV = 'cbc:IssueDate'
     c                   if        X_XRPATH='/Invoice'
     c                   eval      xxInvDt  = %subst(XRVERD:1:4)
     C                                       +%subst(XRVERD:6:2)
     C                                       +%subst(XRVERD:9:2)
     c                   endif
      * DueDate
     C                   WHEN      XRNV = 'cbc:PaymentDueDate'
     c                   eval      xxDueDt  = %subst(XRVERD:1:4)
     C                                       +%subst(XRVERD:6:2)
     C                                       +%subst(XRVERD:9:2)
      * DocRef
     C                   WHEN      XRNV = 'cbc:ID' and
     c                             X_XRPATH='/Invoice'
     c                             + '/cac:AdditionalDocumentReference'
     c                   eval      xxDRNo   = xxDRNo + 1
     c                   eval      xxDR(xxDRNo) = XRVERD
      * URI
     C                   WHEN      XRNV = 'cbc:URI'
     c                   eval      xxURI    = XRVERD
     c                   eval      xxDR(xxDRNo) = *blanks
     c                   eval      xxDRNo   = xxDRNo - 1
      * Attachment
     C                   WHEN      XRNV = 'cbc:EmbeddedDocumentBinaryObject'
     c                   eval      xxAtt    = xxDR(xxDRNo)
     c                   eval      xxDR(xxDRNo) = *blanks
     c                   eval      xxDRNo   = xxDRNo - 1
      *************
      *************
      *************
      *************
      *  TOTALbeløp
     C                   WHEN      XRNV = 'cbc:PayableAmount'
     c                   eval(H)   xxTotBl= %float(XRVERD)
     C     XMLRCAFK      CHAIN     XMLRCAF                            33
     C  N33              IF        XRATN = 'currencyID'
     c                   eval      xxVal = XRATV
     C                   END
     c
      * KIDnr         = Siste tag i header  - INITIER KOSTA-records(skrives til sist)
     C                   WHEN      XRNV = 'cbc:PaymentID'
     c                   eval      xxKidNo = XRVERD
      * OrderReferanse
     C                   WHEN      XRNV = 'cbc:ID' and
     C                             X_XRPATH='/Invoice/cac:OrderReference'
     c                   eval      xxOrdRef = XRVERD
      * Andre referanse
     C                   WHEN      XRNV = 'cbc:ID' and X_XRPATH='/Invoice'
     C                             +'/cac:AdditionalDocumentReference'
     c                   IF        xxSupOrd = *blanks
     c                   eval      xxSupOrd = XRVERD
     c                   END
     C                   WHEN      XRNV = 'cbc:ID' and X_XRPATH='/Invoice'
     C                             +'/cbc:DocumentType'
     c                   IF        xxSupOrd <> *blanks and XRVERD <> 'SYS'
     c                   eval      xxSupOrd = *blanks
     c                   END
      * Total moms
     C                   WHEN      XRNV = 'cbc:TaxAmount' and
     C                             X_XRPATH='/Invoice/cac:TaxTotal'
     c                   eval(H)   xxTotMVA = %float(XRVERD)
      * Frisøk kode = <Code> - Lite spesifik tag, MEN eneste, så det er OK - kunne testet mot path:
      * /Interchange/Invoice/InvoiceDetails/BaseItemDetails/SubInvoice/Header/Ref
     C                   WHEN      XRNV = 'Code'
     c                   eval      xxLastCode= XRVERD
      * Frisøk text = <Text> - Lite spesifik tag, MEN eneste, så det er OK - kunne testet mot path:
      * /Interchange/Invoice/InvoiceDetails/BaseItemDetails/SubInvoice/Header/Ref
     C                   WHEN      XRNV = 'Text'
     c                   if        xxLastCode='IFB'
     c                   eval      xxIFBnr = XRVERD
     c                   endif
      *
     C                   WHEN      XRNV = 'cbc:InvoicedQuantity'
     c                   eval(H)   xxSTK  = %float(XRVERD)
     c                   if        xxstk = 0
     c                   eval      xxstk = 1
     c                   endif
      * Beløp på linje uten moms
     C                   WHEN      XRNV = 'cbc:LineExtensionAmount' and
     c                             X_XRPATH='/Invoice/cac:InvoiceLine'
     c                   eval      xxBEL   = %float(XRVERD)
      * Moms på linje
     C                   WHEN      XRNV = 'cbc:TaxAmount' and
     c                             X_XRPATH='/Invoice/cac:InvoiceLine'
     c                             + '/cac:TaxTotal'
     c                   eval      xxMVA   = %float(XRVERD)
      * Sjekk momssats hvis moms på linje ikke finnes
     C                   WHEN      XRNV = 'cbc:Percent' and
     c                             X_XRPATH='/Invoice/cac:InvoiceLine'
     c                             + '/cac:Item/cac:ClassifiedTaxCategory'
     c                   IF        xxMVA = 0
s01  c*                  eval      xxMVA = xxBEL * %float(XRVERD) / 100
n01  c                   eval(h)   xxMVA = xxBEL * %float(XRVERD) / 100
     c                   END
      * Description
     C                   WHEN      XRNV = 'Description'
     c                   eval      xxDesc = XRVERD
      *  Beskrivelse
     C                   WHEN      XRNV = 'cbc:Name' and
     c                             X_XRPATH='/Invoice/cac:InvoiceLine/cac:Item'
     c                   eval      xxDesc   = XRVERD
      *  ITEM ID Gebyrkode fra avsender
     C                   WHEN      XRNV = 'cbc:ID' and
     c                             X_XRPATH='/Invoice/cac:InvoiceLine/cac:Item'
     c                             +'/cac:SellersItemIdentification'
     c                   eval      xxGEBsy= XRVERD                              fra annen Syseped-ku
     c                   eval      xxGEBxx= XRVERD                              fra andre ??
     C* 'cbc:PriceAmount' = Siste(betydlige) tag i linje - behandle linje
     C* Beløp
     C                   WHEN      XRNV = 'cbc:PriceAmount'
     c                   if        XRVERD=*blanks
     c                   eval      XRVERD='0'
     c                   endif
     c                   eval(H)   xxPris = %float(XRVERD)
     C                   IF        xxInvTy ='381'
     C                   MULT      -1            xxPris
     C                   END
     c                   eval      xxPris = xxPris * xxstk
     c                   if        xxPris <> 0
     C                   EXSR      SRKB
     c                   endif
      *
     C                   ENDSL
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     SRKB          BEGSR
      *////////////////////////////////////////////////////////////
      * INITIER KOSTA-records(skrives til sist)
     C                   IF        KABNR = 0
     C                   EXSR      SRKAI
     C                   END
      *
     c                   move      *blanks       KBBILT
      *
     C     xxBEL         ADD       XXMVA         KBBLHB
     C     XXMVA         IFNE      *ZEROS
     c     LEVTYP        andne     'I'                                          Ikke fradrag for MVA
     C                   Z-ADD     1             KBKDM
     C                   MOVE      'P'           KBKDPF
     C                   ELSE
     C                   Z-ADD     0             KBKDM
     C                   MOVE      'F'           KBKDPF
     C                   END
     c
N01  c                   move      *zeros        XXMVA
      *
     C                   MOVE      *ZEROS        KBAVD
     C                   MOVE      *ZEROS        KBOPD
     C                   MOVE      *ZEROS        KBKN
      *
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
      *
      * Oppgitt i Buyers Order No
     C                   IF        %SUBST(xxOrdRef:5:1) = '/'
     C                             and %SUBST(xxOrdRef:13:1) = '/'
     C                   eval      HEAVD = %float(%subst(xxOrdRef:1:4))
     C                   eval      HEOPD = %float(%subst(xxOrdRef:6:7))
     C     OPDKEY        CHAIN     HEADF                              33
     C  N33              GOTO      UTOPD
     C                   END
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
      *
      * Ikke treff via BuyersOrderNo - sjekk via IFB..
     c                   IF        xxIFBnr <> *blanks
     c                   eval      FSSOK  = xxIFBnr
     C     IFBKEY        CHAIN     FRISOL01                           33
     C  N33              MOVE      FSAVD         HEAVD
     C  N33              MOVE      FSOPD         HEOPD
     C  N33OPDKEY        CHAIN     HEADF                              33
     C  N33              GOTO      UTOPD
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   END
      *
      *
     C     UTOPD         TAG
      * Legg ut posten i KOSTB
     C                   EXSR      SRKBU
      *
      *
     C                   ENDSR
      *
      *
      *
      *////////////////////////////////////////////////////////////
     C     SRKBU         BEGSR
      *////////////////////////////////////////////////////////////
      *
     c                   eval      KBREFA='OrderRef: ' + xxOrdRef
     c                   eval      KBREFB='Suppl.Ref: ' + xxSupOrd
     c                   eval      KBREFC='IFB/sendn: ' + xxIFBnr
      * MANGLENDE MATCH -  Skriv til liste...
     C                   MOVE      *ZEROS        KBKAVD
     C                   MOVE      *BLANKS       KBKKEY
     C                   MOVE      *BLANKS       MATCHTXT         20
      * HEAVD/HEOPD lik 0 = ref ikke funnet - åpne ferdig-status m.m.
     C* 7/8 av KBTUNR: 7:GEBYR,1=GEB.0=Vkt 8:NØKKEL,1=etter opd 9=IKKE
     C     HEOPD         IFEQ      *ZEROS                                       B1
     c                   eval      MATCHTXT= 'Ingen match !!!'
     C                   MOVE      1             KBGEBY
     C                   MOVE      9             KBNØKK
     C                   ELSE                                                   X1
     C                   MOVE      1             KBGEBY
     C                   MOVE      1             KBNØKK
     C                   Z-ADD     HEAVD         KBKAVD
     C                   MOVEL     HEOPD         KBKKEY
     C                   MOVE      HEAVD         KBAVD
     C                   MOVE      HEOPD         KBOPD
     C                   SELECT
     C     HEUR          WHENEQ    'H'
     C                   MOVE      HEKNSF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNKF        KBKN
     C                   END
     C     HEUR          WHENEQ    'A'
     C     HEUR          OREQ      'C'                                          IMPORT
     C     HEUR          OREQ      'F'
     C                   MOVE      HEKNKF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNSF        KBKN
     C                   END
     C     HEUR          WHENEQ    'B'                                          EKSPORT
     C     HEUR          OREQ      'D'
     C     HEUR          OREQ      'G'
     C                   MOVE      HEKNSF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNKF        KBKN
     C                   END
     C                   ENDSL
      * Ikke funnet kunde - åpne bilaget m.m.
     C     KBKN          IFEQ      *ZEROS                                        B2
     c                   eval      MATCHTXT= 'Knr mangler(oppd)!!!'
     C                   MOVE      9             KBNØKK
     C                   END                                                     E2
      *
     C                   END                                                    E1
      * GEBYRKODE
      * Starter med kun Ramberg  - hardkodet FRA på trafikkfaktura..
     C                   IF        X_DOKU =  'T'
     c                   move      'FRA'         KBVK
      *   her bør vel inn logikk som kan sjekek på varkode/tekst i XML - helst via oppslag
     c*                  if        xxx= yy or aa = bb
     c*                  select
     c*                  when      cccc = xxx
     c*                  move      xxGEBsy       KBVK
     c*                  when      zzzz = vvv
     c*                  move      xxGEBsy       KBVK
     c*                  endsl
     c*                  endif
     c                   endif
     c
      * IKKE frafikkfaktura - avvikende kontering rett på konto..
     C                   IF        X_DOKU <> 'T'
     C                   exsr      konter
     C                   END
      *
     C                   WRITE     KOSTBR
      *
      *
      * MANGLENDE MATCH -  Skriv til liste...
     C     KBNØKK        IFEQ      9
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C                   EXCEPT    DET
     C                   END
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     SRKAI         BEGSR
      *////////////////////////////////////////////////////////////
     c                   move      'N'           Dobbel            1
      *  hent lev.nr via foretaksnr....     Kosta2 = KALNR/KAFNR/KAPÅR
     c                   move      xxLEVorg      FORETN
     c     FirForetK     chain     LEVEL10                            35
     C  N35              eval      KALNR = LEVNR
     C  N35KEYLEV        CHAIN     NAVLEV                             31
     C                   eval      KAFNR = xxInvNo
     C                   MOVE      xxInvDt       KABDT
     C                   MOVE      KABDT_ÅR      KAPÅR
      *
      * SAMME FAKT-NR MOTTATT FØR -FRA GITT LEVERANDØR??  (men ignorere D=slettede/E=Error )
     C     KOSA2K        SETLL     KOSTA2                             33
     C     KOSA2K        READE     KOSTA2                                 33
     C     *IN33         DOWEQ     '0'
     C     K2_kast       IFNE      'D'
     C     K2_kast       andne     'E'
     c                   move      'J'           Dobbel
     C                   LEAVE
     C                   END
     C     KOSA2K        READE     KOSTA2                                 33
     C                   ENDDO
      *
      * SELV om den finnes fra før - skap bilag
      * INITIERING AV KOSTA-FELT
     C                   CLEAR                   KOSTAR
     C  N35              eval      KALNR = LEVNR
     C  N35              MOVE      'T'           X_DOKU            1
     C  N35              IF        LEVTYP <> 'T'
     C                   MOVE      '*L1*'        KATXT
     C                   MOVE      'I'           X_DOKU
     C                   END
     C   35              eval      KATXT = 'Foretaksnr=' + %TRIM(xxLEVorg)
     C   35              MOVE      ' '           X_DOKU
     C                   eval      KAFNR = xxInvNo
     C                   IF        NAVSG = *BLANKS
     C                   MOVE      'EDI'         KASG
     C                   ELSE
     C                   EVAL      KASG = NAVSG
     C                   END
     c
     c     LEVTYP        ifeq      'I'                                          Ikke fradrag for MVA
     c                   eval      xxTotMVA = *zeros
     c                   endif
     C                   IF        xxInvTy ='381'
     C                   EVAL      KABL   = xxTotbl
     C                   EVAL      KABLM  = xxTotMVA
     C                   ELSE
     C                   EVAL      KABL   = xxTotbl * -1
     C                   EVAL      KABLM  = xxTotMVA * -1
     C                   END
     C                   IF        xxTotMVA = 0
     C                   EVAL      KAMVA = 'F'
     C                   ELSE
     C                   EVAL      KAMVA = 'P'
     C                   END
     C                   EVAL      KAVAL  = xxVal
     C                   IF        KAVAL  = *blanks
     C                   EVAL      KAVAL  = FICURR
     C                   endif
     C     VALUL01K      CHAIN     VALUL01                            33
     C  N33              EVAL      KAVKU  = VALKU1
     C  N33              EVAL      KAOMRF = OMRFAK
     C                   MOVE      ZUSER         KAUSER
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      ULDT          KADTE
     C                   MOVE      XULTM         KATME
     C                   MOVE      ULDT          KADTR
     C                   MOVE      XULTM         KATDR
      *
     C                   MOVE      'N'           UKJENT            1
      *
     C* TILDEL PRE.REG.NR (LØPENR FRA FAST TELLER)...
     C     'Ø'           CHAIN     KOSTT                              33
     C                   MOVE      KTNR          KBBNR
     C                   ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
      * Prereg løpenr
     C                   MOVE      KBBNR         KABNR
     C* Bilagsnr til økonomi
     C     KALNR         IFNE      *ZEROS
     C     NASERI        andne     *BLANKS
     C     Dobbel        andne     'J'
     C     NASERI        CHAIN     KOSTT                              33
     C  N33              MOVE      KTNR          KABNR2
     C  N33              ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
     C                   END
      * Bilagsdato / Lev.s fakt.dato/forfallsdato/KID
     C                   MOVE      xxInvDt       KABDT
     C                   MOVE      xxinvDt       KAFDT
     C                   MOVE      xxDueDt       KAFFDT
     C                   MOVEL     xxKidNo       KALKID
      *
     C                   MOVEL     KABDT_ÅR      KAPCC
     C                   MOVE      KABDT_ÅR      KAPÅR
     C                   MOVE      KABDT_MN      KAPMN
      * MOMSSATS
     c                   move      KADTE         MVADT             8 0
     C     MVADT         IFEQ      *zeros
     c                   move      ULDT          MVADT
     c                   endif
     C                   Z-ADD     FITAX         FITAXN            5 4
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     MVADT         ANDLT     FITAXD
     C                   Z-ADD     FITAX2        FITAXN
     C                   END
      *
     C                   if        Dobbel = 'J'
     C* Finnes fra før ?? - meld på lista / sett status E
     C                   EXCEPT    PHEAD
     C                   EXCEPT    DETFIN
     c                   move      'E'           KAST
     c                   movel     '*DOBBEL ?*'  KATXT
     C                   endif
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    USN               9
     C                   MOVE      USN           XRSN
      *
     C     OPDKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
     C     IFBKEY        KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   move      'IFB'         FSKODE
      *
     C     KOSA2K        KLIST
     C                   KFLD                    KALNR
     C                   KFLD                    KAFNR
     C                   KFLD                    KAPÅR
     C     FirForetK     KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FORETN
     C     VALUL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KAVAL
     C     XMLRCAFK      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    XRLEV1
     C     KEYKONT       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    NAVKO1
     C     KEYLEV        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    kalnr
      *
     C     *LIKE         DEFINE    XRPATH        X_XRPATH
      *
     c                   move      *zeros        KABDT
     c                   MOVEA     *ZEROS        KO
     c                   MOVEA     *ZEROS        AV
     c                   MOVEA     *ZEROS        BA
     c                   MOVEA     *ZEROS        PR
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      * Navitro firmarecord
     C     FIFIRM        CHAIN     FIRMNA                             33
      * Trigge print av heading (hvis manglende match)
     C                   SETON                                        OF
      *
     C                   ENDSR
      *
      *
      *
      **********************************************************
     C     KONTER        BEGSR
      **********************************************************
     c                   move      '*K*'         fastko
     c                   move      '*A*'         fastav
     c                   move      '*B*'         fastba
     c                   move      '*P*'         fastpr
      *
      * KONTONUMMER
      *
     C     NAVKO1        IFNE      *zeros
     C     NAVKO28       ANDEQ     *zeros
     C                   MOVEL     NAVKO1        KBREFCKO
     c     keykont       chain     kontl01                            33
     c   33              z-add     9             kbnØkk
     C   33              MOVE      *zeros        KBREFCKO
     c                   else
     c                   z-add     9             kbnØkk
     C                   MOVE      *zeros        KBREFCKO
     c
     C                   END
      *
      *
      * AVDELING
      *
     C     NAVAV1        IFNE      *zeros
     C     NAVAV28       ANDEQ     *zeros
     C                   MOVEL     NAVAV1        KBREFCAV
     c                   else
     C                   MOVE      *zeros        KBREFCAV
     c     stedk         ifeq      'J'
     c                   z-add     9             kbnØkk
     c                   end
     C                   END
      *
      *
      * BARER
      *
     C     NAVBA1        IFNE      *zeros
     C     NAVBA28       ANDEQ     *zeros
     C                   MOVEL     NAVBA1        KBREFCBA
     c                   else
     C                   MOVE      *zeros        KBREFCBA
     c     barerk        ifeq      'J'
     c                   z-add     9             kbnØkk
     c                   end
     C                   END
      *
      *
      * PROSJEKT
      *
     C     NAVPR1        IFNE      *zeros
     C     NAVPR28       ANDEQ     *zeros
     C                   MOVEL     NAVPR1        KBREFCPR
     c                   else
     C                   MOVE      *zeros        KBREFCPR
     c     prosjk        ifeq      'J'
     c                   z-add     9             kbnØkk
     c                   end
     C                   END
      *
     C                   ENDSR
      ************************************************************
     C     arkiv         BEGSR
      ************************************************************
     C     arklagk       KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    arklag
     C     FIFIRM        CHAIN     FIRMARC                            33
     C     *IN33         IFEQ      '0'
      * henter eventuell avvinde bane fra arkext  via Navitro dokumentype
     c     NADTDO        chain     ARKTXT
     C                   if        %found
     C                             and ARKLAG <> *blanks
     c     arklagk       chain     arkext                             54
     c                   end
N02   *
N02  C                   MOVE      *ZEROS        XNR               2 0
N02  C     STARKIV       TAG
N02  C                   ADD       1             XNR
N02  C                   MOVE      XNR           XAN               2
N02  C                   EVAL      U_FIL = '/tmp/EHF_'+USN+'_'+%TRIM(XAN)+'.pdf'
N02  C                   CALL      'CHECKLNK'
N02  C                   PARM                    U_FIL            50
N02  C                   PARM                    U_OK              1
N02  C                   IF        U_OK = 'N'
N02  C                   GOTO      SLARKIV
N02  C                   END
      *  Til-mappe
     C                   EVAL      NEWDIR = '/' + %trimr(ARCANE)
      *  Til-filnavn
     c                   move      KABNR         UND               7
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN
     C                   EVAL      NEWFILE= NADTDO+ULÅÅA
     C                             + UND + NEWRAN +'.pdf'
      * objekt = mappe/fil
     C                   EVAL      NEWOBJ = %trim(NEWDIR) + '/'
     C                             + %trimr(NEWFILE)
     C
     C                   MOVE      'J'           OkMov             1
S02  C*                  EVAL      FRA = '/tmp/EHF_'+USN+'.pdf'
N02  C                   EVAL      FRA = U_FIL
     C                   EVAL      TIL = NewObj
     C                   CALL      'ARCAMOVC'
     C                   PARM                    FRA             120
     C                   PARM                    TIL             120
     C                   PARM                    OkMov
     C                   IF        OkMov = 'J'
     C                   EXSR      ARK2
     C                   end
N02  C                   GOTO      STARKIV
N02  C     SLARKIV       TAG
     C                   end
     C                   endsr
      *////////////////////////////////////////////////////////////
      *  Skriv til ARKIVP                                     ARK2/
      *////////////////////////////////////////////////////////////
     C     ARK2          BEGSR
      * Skriv til ARKIVP
     C                   CLEAR                   ARKIVR

     C                   EVAL      ARSTAT = 'A'
     C                   EVAL      ARTYPE = NADTDO
     C                   EVAL      ARFIRM = FIFIRM
     C                   EVAL      ARAVD  = -1
     C                   EVAL      AROPD  = *zeros
     C                   EVAL      ARUNDE = 'KL:' + UND
     C                   EVAL      ARUSER = 'SY400USR'
     C                   EVAL      ARDATE = KADTE
     C                   EVAL      ARTIME = KATME
     C                   EVAL      ARKUND = 0
     C                   EVAL      ARLINK = NEWFILE

     C                   WRITE     ARKIVR
      *
     C                   ENDSR

     OLISTE     E            PHEAD            01
     O                       FIFT                45
     O                                           49 'Lev:'
     O                       KALNR         Z     57
     O                                           71 'Fakt.nr:'
     O                       XXINVNO             88
     O                                          103 'ForetNr:'
     O                       xxLEVorg           115
     OLISTE     E            PHEAD            02
     O                                           24 '*******     EHF-FAKTURA '
     O                                           48 '. M/REF.  SOM IKKE MATCH'
     O                                           72 'ER SYSPED OPPDRAG ******'
     O                                          100 'DATO:'
     O                       UDATE         Y    110
     O                                          120 'SIDE:'
     O                       PAGE          Z    126

     OLISTE     E            PHEAD            03
     O                                           24 '-----------       ****  '
     O                                           48 'Mottatte referanser for '
     O                                           72 'matching ****      -----'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O                                          132 '------------'
     O          E            PHEAD            04
     O                                           10 'PREREG.NR'
     O                                           35 'SENDINGSNUMMER  '
     O                                           65 'ANNEN REFERANSE     '
     O                                           88 'FEILTYPE            '
     O                                          104 'NETTO BELØP'
     O                                          120 'MOMS'
     OLISTE     E            PHEAD          1 05
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
      *
     OLISTE     E            DET            1
     O                       KABNR         Z     10
     O                       XXIFBnr             35
     O                       XXOrdRef            65
     O                       MATCHTXT            88
     O                       XXBEL         J    105
     O                       XXMVA         J    121
     OLISTE     E            DETFIN         1
     O                       KABNR         Z     10
     O                                           28 '*FINNES FRA FØR*'
     O                                           33 'Lev:'
     O                       K2_KALNR      Z     41
     O                                           47 'F.nr:'
     O                       K2_KAFNR            61
     O                                           75 '(M/Prereg.nr:'
     O                       K2_KABNR      Z     84
     O                                           90 'B.nr:'
     O                       K2_KABNR2     Z     97
     O                                          105 'B.dt:'
     O                       K2_KABDT           116 '    .  .  '
     O                                          118 ')'
