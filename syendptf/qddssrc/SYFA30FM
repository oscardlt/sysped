     A*%%TS  SD  20180821  094223  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P5048A* 01 YBC PTF5 NYE KODER FOR STATISTIKK  (H OG I)
P0602A* 02 JOV PTF6 MOMSKODER STYRT AV KONTOTYPE..
     A*             OGSÅ FLYTTETFELT / OMSTRUKURERT BILDE02
P8XXXA* 03 YBC PTF8 E=AGENT/FRANKATUR
P9XXXA* 04 YBC PTF9 SETT START CURSOR PÅ FIRMANAVN
10XXXA* 05 TH  PTF12 ENDRING I LEDETEKST
P0XXXA* 09 TH  PTF8 ny helpfunksjon kun DDS
12141 * 10 YBC PTF12 SLETTERUTINE
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA30PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FIRMAOPPLY-
     A                                      SNINGER')
     A                                      HELP
     A                                      ALTHELP
     A                                      ENTFLDATR((*COLOR RED) (*DSPATR HI))
     A          R BILDE01
     A                                      CF12(12 'AvBRUDD program')
     A                                      CF10(10 'OPPDATER FAKTNR')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFA30'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(040 DIV1450 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A N31                              3  2MSGCON(075 JOV0305 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A            WSFIRM         2A  B  4 17DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      TEXT('Firmakode')
     A                                      COLOR(YLW)
     A                                  4 24MSGCON(010 BBA0113 *LIBL/SYMSGF)
     A            WSKRTN    R     A  B  4 36REFFLD(FIRMR/FIKRTN *LIBL/FIRM)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2MSGCON(012 DIV1452 *LIBL/SYMSGF)
     A            WSFT          45A  B  5 17DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98
     AON30                                  DSPATR(PC)
     A                                      TEXT('Fritekst')
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(024 CBA0140 *LIBL/SYMSGF)
     A            WSLIBF        10A  B  6 27COLOR(YLW)
     A                                  6 40MSGCON(023 CBA0150 *LIBL/SYMSGF)
     A            WSLIBO        10A  B  6 64COLOR(YLW)
     A                                  7  2MSGCON(012 DIV1453 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WSKDUL         1A  B  7 17DSPATR(UL)
     A                                      TEXT('Utleggskontroll J/N')
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                  7 21'(J/N)'
     A  31                                  DSPATR(ND)
     A                                  7 27MSGCON(044 DIV1454 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A N31                              8  2MSGCON(025 JOV0675 *LIBL/SYMSGF)
     A N31        WSTPNR         1A  B  8 28COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'U')
     A N31                              8 30'J/N/U'
     A N31                              8 49MSGCON(030 JOV1034 *LIBL/SYMSGF)
     A N31                              9  2MSGCON(025 JOV0674 *LIBL/SYMSGF)
     A N31        WSFBNR         1A  B  9 28COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'U')
     A N31                              9 30'J/N/U'
     A N31                              9 36MSGCON(012 SHU0578 *LIBL/SYMSGF)
     A N31        WSSNLA         2Y 0B  9 49EDTCDE(4)
     A                                      COLOR(YLW)
     A N31        WSSNLE         5Y 0B  9 52EDTCDE(4)
     A                                      COLOR(YLW)
     A N31        WSRECN         9Y 0B  9 58EDTCDE(4)
     A                                      COLOR(YLW)
     A N31                              9 68'-'
     A N31        WSRECM         9Y 0B  9 70EDTCDE(4)
     A                                      COLOR(YLW)
     A N31                             10  2MSGCON(022 JOV1099 *LIBL/SYMSGF)
     A N31        WSLFB          1A  B 10 28COLOR(YLW)
     A                                      VALUES(' ' 'S' 'M')
     A N31                             10 30'S/M/ '
     A N31                             10 36MSGCON(010 JOV1035 *LIBL/SYMSGF)
     A N31                             10 47'3'
     A                                      COLOR(YLW)
     A N31        WSIDLA         2Y 0B 10 49EDTCDE(4)
     A                                      COLOR(YLW)
     A N31        WSIDLE         5Y 0B 10 52EDTCDE(4)
     A                                      COLOR(YLW)
     A N31        WSIDNR         9Y 0B 10 58EDTCDE(4)
     A                                      COLOR(YLW)
     A N31                             10 68'-'
     A N31        WSIDMX         9Y 0B 10 70EDTCDE(4)
     A                                      COLOR(YLW)
     A N31                             11  2MSGCON(018 JOV0262 *LIBL/SYMSGF)
     A            WSSTFN         2A  B 11 21
     A  31                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A N31                             11 24MSGCON(008 JOV0676 *LIBL/SYMSGF)
     A            WSSTFE         2A  B 11 33
     A  31                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A N31                             11 36MSGCON(008 JOV0678 *LIBL/SYMSGF)
     A            WSSTFT         2A  B 11 45
     A  31                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A N31                             12  2MSGCON(018 DIV1458 *LIBL/SYMSGF)
     A            WSUPS          4Y 2B 12 21DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      TEXT('Utl.prov. standard')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                 12 27MSGCON(010 DIV1459 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WSFNR          7Y 0B 12 39TEXT('FAKTURANR')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A N31                             12 49MSGCON(005 SHU0630 *LIBL/SYMSGF)
     A            WSKID8         1A  B 12 55COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                      VALUES('J' 'N')
     A                                 12 58MSGCON(012 BBA0047 *LIBL/SYMSGF)
     A            WSLAND    R        B 12 72REFFLD(FIRMR/FILAND *LIBL/FIRM)
     A                                      COLOR(YLW)
     A                                 13  2MSGCON(018 DIV1460 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WSUPM          2Y 0B 13 21DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      TEXT('Utl.prov. min.bel.')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                 13 27MSGCON(011 DIV1468 *LIBL/SYMSGF)
     A            WCURR          3A  B 13 39COLOR(YLW)
     A                                 13 44MSGCON(010 DIV1471 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WDECI          1Y 0B 13 55EDTCDE(4)
     A                                      COLOR(YLW)
     A  31                                  DSPATR(PR)
     A  31                                  DSPATR(ND)
     A                                 13 58MSGCON(011 DIV1470 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WDTFM          1Y 0B 13 72COLOR(YLW)
     A                                      EDTCDE(4)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                 14  2MSGCON(012 DIV1469 *LIBL/SYMSGF)
     A            WTAX           4Y 2B 14 16COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 26MSGCON(012 YBC0099 *LIBL/SYMSGF)
     A            WSTAXD         6Y 0B 14 39COLOR(YLW)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 14 48MSGCON(022 YBC0100 *LIBL/SYMSGF)
     A            WSTAX2         4Y 2B 14 72COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(035 DIV1461 *LIBL/SYMSGF)
     A            WSKDT          1Y 0B 15 38DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      TEXT('KD for Tvinn')
     A                                      COLOR(YLW)
     A                                 15 41MSGCON(015 DIV2139 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WSECON         1A  B 15 58DSPATR(UL)
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                 15 60MSGCON(011 DIV2140 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WSAVTE         1A  B 15 72DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'G')
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                 16  2MSGCON(028 DIV1463 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WSVALK         1X  B 16 32DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('S' 'Ø')
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                 16 35MSGCON(027 DIV1467 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A                                 17  2MSGCON(026 DIV1464 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A                                 17 31'(J/N/blank)'
     A  31                                  DSPATR(ND)
     A N31                             18  2MSGCON(076 DIV1465 *LIBL/SYMSGF)
     A            WSURLI         1A  B 19  7DSPATR(UL)
     A                                      TEXT('Land / import')
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSURLE         1A  B 19 20DSPATR(UL)
     A                                      TEXT('Land / eksport')
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSURFI         1A  B 19 33DSPATR(UL)
     A                                      TEXT('Fly / import')
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSURFE         1A  B 19 45DSPATR(UL)
     A                                      TEXT('Fly / eksport')
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSURFL         1A  B 19 58DSPATR(UL)
     A                                      TEXT('Fly / innland')
     A                                      VALUES(' ' 'J' 'N')
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTRAN         1A  B 19 70
     A  31                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 20  2MSGCON(070 DIV1466 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A                                 20 73MSGCON(006 BBA0048 *LIBL/SYMSGF)
     A  31                                  DSPATR(ND)
     A            WSURSI         1A  B 21  7DSPATR(UL)
     A                                      TEXT('SAD / import')
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSURSE         1A  B 21 20DSPATR(UL)
     A                                      TEXT('SAD / eksport')
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSURBI         1A  B 21 33DSPATR(UL)
     A                                      TEXT('Båt / import')
     A                                      VALUES(' ' 'J' 'N' 'Y')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSURBE         1A  B 21 45DSPATR(UL)
     A                                      TEXT('Båt / eksport')
     A                                      VALUES(' ' 'J' 'N' 'Y')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSURBL         1A  B 21 58DSPATR(UL)
     A                                      TEXT('Båt / innland')
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A  31                                  DSPATR(ND)
     A  31                                  DSPATR(PR)
     A            WSTDVI    R        B 21 76REFFLD(FIRMR/FITDVI *LIBL/FIRM)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  31                                  DSPATR(ND)
     A                                 22  2MSGCON(048 YBC0575 *LIBL/SYMSGF)
     A            WSLE31    R        B 22 51REFFLD(FIRMR/FILE31 *LIBL/FIRM)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'N' 'J')
     A                                 23  2MSGCON(018 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 JOV1456 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 50'(c) Copyright Systema A/S 1990'
     A                                      COLOR(WHT)
     A                                 23 21MSGCON(018 SHU1217 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 18'1/4'
     A                                      COLOR(BLU)
     A          R BILDE02
     A                                      CF12(12 'Avslutt program')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFA30'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(040 DIV1450 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  4  2MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A            WSFIRM         2A  B  4 17DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      TEXT('Firmakode')
     A                                      COLOR(YLW)
     A                                      DSPATR(PR)
     A                                  5  2MSGCON(012 DIV1452 *LIBL/SYMSGF)
     A            WSFT          45A  B  5 17DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      TEXT('Fritekst')
     A                                      DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(040 DIV5086 *LIBL/SYMSGF)
     A                                      DSPATR(ND)
     A            WSKONT         1A  B  6 46VALUES('J' 'N' 'Y')
     A                                      COLOR(YLW)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  7  2MSGCON(037 JOV1143 *LIBL/SYMSGF)
     A            WSSADK         1A  B  7 46COLOR(YLW)
     A                                  7 49MSGCON(030 JOV1145 *LIBL/SYMSGF)
     A                                  8 49MSGCON(030 JOV1146 *LIBL/SYMSGF)
     A                                  9  2MSGCON(037 JOV1142 *LIBL/SYMSGF)
     A            WSLE41    R        B  9 46REFFLD(FIRMR/FILE31 *LIBL/FIRM)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'N' 'J')
     A                                  9 49MSGCON(020 JOV1144 *LIBL/SYMSGF)
     A                                 10  2MSGCON(050 BBA0087 *LIBL/SYMSGF)
     A                                 11  4MSGCON(015 BBA0088 *LIBL/SYMSGF)
     A            WSLE11         1A  B 11 22COLOR(YLW)
     A                                      VALUES('J' 'N' 'Y')
     A                                 11 26MSGCON(005 BBA0090 *LIBL/SYMSGF)
     A                                 12  4MSGCON(015 BBA0089 *LIBL/SYMSGF)
     A            WSLE12         1A  B 12 22COLOR(YLW)
     A                                      VALUES('J' 'N' 'Y')
     A                                 12 26MSGCON(005 BBA0090 *LIBL/SYMSGF)
     A                                 13  2MSGCON(035 SHU1240 *LIBL/SYMSGF)
     A            WSTILL         1A  B 13 46COLOR(YLW)
     A                                      VALUES('J' 'N')
     A                                 13 49'(J/N)'
     A                                 13 55MSGCON(014 SHU1241 *LIBL/SYMSGF)
     A            WSINTE         5Y 0B 13 70EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 14  2MSGCON(040 SHU0527 *LIBL/SYMSGF)
     A            WSIU           1A  B 14 46COLOR(YLW)
     A                                 14 49MSGCON(020 JOV1144 *LIBL/SYMSGF)
     A                                 15  2MSGCON(040 SHU0528 *LIBL/SYMSGF)
     A            WSZIP          1A  B 15 46COLOR(YLW)
     A                                 15 49MSGCON(020 JOV1144 *LIBL/SYMSGF)
     A                                 16  2MSGCON(040 SHU0623 *LIBL/SYMSGF)
     A            WSKUFR         8Y 0B 16 46EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSKUTI         8Y 0B 16 55EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 17  2MSGCON(040 SHU0624 *LIBL/SYMSGF)
     A            WSKUNE         8Y 0B 17 46EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 23  2MSGCON(018 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 JOV1456 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 50'(c) Copyright Systema A/S 1990'
     A                                      COLOR(WHT)
     A                                  1 18'2/4'
     A                                      COLOR(BLU)
     A          R BILDE03
     A*%%TS  SD  20171208  152410  TROND       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Avslutt program')
     A                                  1  2'SYSPED/SYFA30'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(040 DIV1450 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  2 22MSGCON(030 JOV0094 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A            WSFIRM         2A  O  3 17DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSFT          45A  O  3 21DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(033 DIV2215 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'  1     2     3     4     5     6 -
     A                                          7     8     9     10    11    1-
     A                                      2 '
     A            WSVK1          5Y 0B  6  2DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK2          5Y 0B  6  8DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK3          5Y 0B  6 14DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK4          5Y 0B  6 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK5          5Y 0B  6 26EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVK6          5Y 0B  6 32DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK7          5Y 0B  6 38DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK8          5Y 0B  6 44DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK9          5Y 0B  6 50DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK10         5Y 0B  6 56DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVK11         5Y 0B  6 62DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  6 68'99999'
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2MSGCON(070 JOV0095 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(023 JOV0096 *LIBL/SYMSGF)
     A            WSSTA     R        B  8 26REFFLD(FIRSTAR/FISTA *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                  8 29MSGCON(023 JOV0097 *LIBL/SYMSGF)
     A            WSSTB     R        B  8 53REFFLD(FIRSTAR/FISTB *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                  8 56MSGCON(023 JOV0098 *LIBL/SYMSGF)
     A            WSSTC     R        B  8 80REFFLD(FIRSTAR/FISTC *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                  9  2MSGCON(023 JOV0099 *LIBL/SYMSGF)
     A            WSSTD     R        B  9 26REFFLD(FIRSTAR/FISTD *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
S03  A*                                 9 29MSGCON(023 JOV0100 *LIBL/SYMSGF)
     A                                  9 29MSGCON(023 YBC1906 *LIBL/SYMSGF)
     A            WSSTE     R        B  9 53REFFLD(FIRSTAR/FISTE *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                  9 56MSGCON(023 YBC0301 *LIBL/SYMSGF)
     A            WSSTF     R        B  9 80REFFLD(FIRSTAR/FISTF *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 10  2MSGCON(023 YBC0302 *LIBL/SYMSGF)
     A            WSSTG     R        B 10 26REFFLD(FIRSTAR/FISTG *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
S01  A*                                10 29MSGCON(023 JOV0103 *LIBL/SYMSGF)
     A                                 10 29MSGCON(023 YBC0622 *LIBL/SYMSGF)
     A            WSSTH     R        B 10 53REFFLD(FIRSTAR/FISTH *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
S01  A*                                10 56MSGCON(023 JOV0104 *LIBL/SYMSGF)
     A                                 10 56MSGCON(023 YBC0623 *LIBL/SYMSGF)
     A            WSSTI     R        B 10 80REFFLD(FIRSTAR/FISTI *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 11  2MSGCON(023 YBC0303 *LIBL/SYMSGF)
     A            WSSTJ     R        B 11 26REFFLD(FIRSTAR/FISTJ *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 11 56MSGCON(023 JOV0107 *LIBL/SYMSGF)
     A            WSSTL     R        B 11 80REFFLD(FIRSTAR/FISTL *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 11 29MSGCON(023 JOV0106 *LIBL/SYMSGF)
     A            WSSTK     R        B 11 53REFFLD(FIRSTAR/FISTK *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 12  2MSGCON(023 JOV0108 *LIBL/SYMSGF)
     A            WSSTM     R        B 12 26REFFLD(FIRSTAR/FISTM *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 12 29MSGCON(023 JOV0109 *LIBL/SYMSGF)
     A            WSSTN     R        B 12 53REFFLD(FIRSTAR/FISTN *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 12 56MSGCON(023 JOV0110 *LIBL/SYMSGF)
     A            WSSTO     R        B 12 80REFFLD(FIRSTAR/FISTO *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 13  2MSGCON(023 JOV0111 *LIBL/SYMSGF)
     A            WSSTP     R        B 13 26REFFLD(FIRSTAR/FISTP *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 13 29MSGCON(023 JOV0112 *LIBL/SYMSGF)
     A            WSSTQ     R        B 13 53REFFLD(FIRSTAR/FISTQ *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 13 56MSGCON(023 JOV0113 *LIBL/SYMSGF)
     A            WSSTR     R        B 13 80REFFLD(FIRSTAR/FISTR *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 14  2MSGCON(023 JOV0114 *LIBL/SYMSGF)
     A            WSSTS     R        B 14 26REFFLD(FIRSTAR/FISTS *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 14 29MSGCON(023 JOV0115 *LIBL/SYMSGF)
     A            WSSTT     R        B 14 53REFFLD(FIRSTAR/FISTT *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 14 56MSGCON(023 JOV0116 *LIBL/SYMSGF)
     A            WSSTU     R        B 14 80REFFLD(FIRSTAR/FISTU *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 15  2MSGCON(023 JOV0117 *LIBL/SYMSGF)
     A            WSSTV     R        B 15 26REFFLD(FIRSTAR/FISTV *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 15 29MSGCON(023 JOV0118 *LIBL/SYMSGF)
     A            WSSTW     R        B 15 53REFFLD(FIRSTAR/FISTW *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 15 56MSGCON(023 JOV0119 *LIBL/SYMSGF)
     A            WSSTX     R        B 15 80REFFLD(FIRSTAR/FISTX *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 16  2MSGCON(023 JOV0120 *LIBL/SYMSGF)
     A            WSSTY     R        B 16 26REFFLD(FIRSTAR/FISTY *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 16 29MSGCON(023 JOV0121 *LIBL/SYMSGF)
     A            WSSTZ     R        B 16 53REFFLD(FIRSTAR/FISTZ *LIBL/FIRSTA)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 17  2MSGCON(040 JOV0122 *LIBL/SYMSGF)
     A            WSSTNR    R        B 17 44REFFLD(FIRSTAR/FISTNR *LIBL/FIRSTA)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 18  2MSGCON(030 JOV0123 *LIBL/SYMSGF)
     A            WSPRO     R        B 18 34REFFLD(KOVR/KOVPRO *LIBL/KODTV)
     A                                      COLOR(YLW)
     A                                      VALUES('F' 'O' 'B')
     A                                 18 37MSGCON(024 DIV4208 *LIBL/SYMSGF)
     A                                 19 37MSGCON(024 DIV4209 *LIBL/SYMSGF)
     A                                 20 37MSGCON(024 DIV4210 *LIBL/SYMSGF)
     A                                 21  2MSGCON(070 JOV0124 *LIBL/SYMSGF)
     A                                 22  2MSGCON(073 TRH0002 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  3MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 50'(c) Copyright Systema A/S 1990'
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 23 62MSGCON(018 JOV1456 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 18'3/4'
     A                                      COLOR(BLU)
     A          R BILDE04
     A*%%TS  SD  20180821  094223  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Avslutt program')
     A                                      CF14(14 'TILL.PARAMETER')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFA30'
     A                                      COLOR(BLU)
     A                                  1 18'4/4'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(040 DIV1450 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  2 22MSGCON(045 JOV0133 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A            WSFIRM         2A  B  4 17DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      TEXT('Firmakode')
     A                                      COLOR(YLW)
     A                                      DSPATR(PR)
     A                                  5  2MSGCON(012 DIV1452 *LIBL/SYMSGF)
     A            WSFT          45A  B  5 17DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      TEXT('Fritekst')
     A                                      DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(045 JOV0135 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(052 TBR0045 *LIBL/SYMSGF)
     A            AVREGN         1A  B  7 55VALUES('K' 'L')
     A                                      COLOR(YLW)
     A                                  8  2MSGCON(052 JOV0140 *LIBL/SYMSGF)
     A            WSBISE    R        B  8 55REFFLD(FIRTRR/FIBISE *LIBL/FIRTR)
     A                                      COLOR(YLW)
     A                                  9  2MSGCON(052 TBR0046 *LIBL/SYMSGF)
     A            FERJEK         1A  B  9 55VALUES('J' 'N')
     A                                      COLOR(YLW)
S01  A*                                10  2MSGCON(052 JOV0134 *LIBL/SYMSGF)
     A                                 10  2MSGCON(052 JOV0889 *LIBL/SYMSGF)
     A            WSBRUT         1A  B 10 55COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'B')
     A                                 11  2MSGCON(052 TBR0047 *LIBL/SYMSGF)
     A            OPPDRG         1A  B 11 55VALUES(' ' 'J' 'N' 'T')
     A                                      COLOR(YLW)
     A                                 13  2MSGCON(045 JOV0136 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 14  2MSGCON(052 JOV0141 *LIBL/SYMSGF)
     A            WSKONV    R        B 14 55REFFLD(FIRTRR/FIKONV *LIBL/FIRTR)
     A                                      VALUES('N' ' ' 'G')
     A                                      COLOR(YLW)
     A                                 14 58MSGCON(020 JOV0142 *LIBL/SYMSGF)
     A                                 14  2MSGCON(020 SHU1449 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 17  2MSGCON(040 SHU1450 *LIBL/SYMSGF)
     A            WSKJOR         1A  B 17 55COLOR(YLW)
     A                                      VALUES('N' 'J' 'G' 'V')
     A                                 17 58MSGCON(040 SHU1453 *LIBL/SYMSGF)
     A                                 18  2MSGCON(040 SHU1451 *LIBL/SYMSGF)
     A            WSSTDN        10A  B 18 55COLOR(YLW)
     A                                 19  2MSGCON(040 SHU1452 *LIBL/SYMSGF)
     A            WSDATN        10A  B 19 55COLOR(YLW)
     A                                 20  2MSGCON(045 SHU1716 *LIBL/SYMSGF)
     A            WSFOAN         1A  B 20 55COLOR(YLW)
N10  A                                 22  2MSGCON(018 YBC2028 *LIBL/SYMSGF)
N10  A                                 22 22'NO firma:'
N10  A            WSNOYF         2Y 0B 22 32COLOR(YLW)
N10  A  97                                  DSPATR(RI)
N10  A  97                                  DSPATR(PC)
N10  A                                      EDTCDE(Z)
N10  A                                 22 36'NO person:'
N10  A            WSNOYP         2Y 0B 22 47COLOR(YLW)
N10  A  96                                  DSPATR(RI)
N10  A  96                                  DSPATR(PC)
N10  A                                      EDTCDE(Z)
N10  A                                 22 52'SE firma:'
N10  A            WSSEYF         2Y 0B 22 62COLOR(YLW)
N10  A  95                                  DSPATR(RI)
N10  A  95                                  DSPATR(PC)
N10  A                                      EDTCDE(Z)
N10  A                                 22 66'SE person:'
N10  A            WSSEYP         2Y 0B 22 77COLOR(YLW)
N10  A  94                                  DSPATR(RI)
N10  A  94                                  DSPATR(PC)
N10  A                                      EDTCDE(Z)
N10  A                                 22 82'DK firma:'
N10  A            WSDKYF         2Y 0B 22 92COLOR(YLW)
N10  A  93                                  DSPATR(RI)
N10  A  93                                  DSPATR(PC)
N10  A                                      EDTCDE(Z)
N10  A                                 22 96'DK person:'
N10  A            WSDKYP         2Y 0B 22107COLOR(YLW)
N10  A  92                                  DSPATR(RI)
N10  A  92                                  DSPATR(PC)
N10  A                                      EDTCDE(Z)
E10  A                                 26  2MSGCON(015 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E10  A                                 26 24MSGCON(030 JOV1455 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E10  A                                 26 62MSGCON(015 JOV1457 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E10  A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
E10  A N30                             27 50'(c) Copyright Systema A/S 1990'
     A                                      COLOR(WHT)
