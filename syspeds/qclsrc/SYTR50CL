/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 10XXX * 01 JOV PTF10 OFFICE  VISION UTGÅR - SEND CSV-MAIL              */
/* ********************************************************************    */
             PGM
             DCL        VAR(&DTO) TYPE(*DEC) LEN(6 0) /*DATO*/
             DCL        VAR(&A01) TYPE(*DEC) LEN(4 0) /*AVDELINGER*/
             DCL        VAR(&A02) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&A03) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&A04) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&A05) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&A06) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&A07) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&A08) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&A09) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&A10) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&BK1) TYPE(*CHAR) LEN(3) /* BILKODE */
             DCL        VAR(&BK2) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BK3) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BK4) TYPE(*CHAR) LEN(3)
             DCL        VAR(&BK5) TYPE(*CHAR) LEN(3)
             DCL        VAR(&OPD) TYPE(*CHAR) LEN(1)  /*TA MED OPPDRAG*/
             DCL        VAR(&DOC) TYPE(*CHAR) LEN(12) /* DOKUMENTNAVN */
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          AVSLUTT*/
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10) /* ARB.STASJ*/
   /*N01*/   DCL        VAR(&EMNE) TYPE(*CHAR) LEN(44) VALUE('Dagbok +
                          SYSPED TRANSPORTmodul')
   /*N01*/   DCL        VAR(&FLR) TYPE(*CHAR) LEN(10) VALUE(QPGMR)
   /*N01*/   DCL        VAR(&STMFIL) TYPE(*CHAR) LEN(50)
   /*N01*/   DCL        VAR(&EPOST) TYPE(*CHAR) LEN(70)
   /*N01*/   DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SNDNA) TYPE(*CHAR) LEN(8)
             DCL        VAR(&SNDAD) TYPE(*CHAR) LEN(8)
   /*S01     RTVJOBA    JOB(&WS)    Hente arbeidstsjonskode WS */
   /*N01*/   RTVJOBA    JOB(&WS) USER(&QUSER)
   /*N01*/   CALL       PGM(EPOS04E) PARM(&QUSER &SNDNA &SNDAD &EPOST)
   /*N01 - MÅ HA EPOST-ADRESSE */
   /*N01*/   IF         COND(&EPOST = ' ') THEN(DO)
   /*N01*/   SNDPGMMSG  MSG('DET MANGLER EPOST-ADRESSE PÅ DIN +
                          BRUKERPROFIL - Kontakt systemansvarlig ')
   /*N01*/   GOTO SLUTT
   /*N01*/   ENDDO
    /* TØM ARBEIDSFILER                          */
             CLRPFM     FILE(DAYJO) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(DAYJO) +
                          MBR(&WS))
             CLRPFM     FILE(DAYJA) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(DAYJA) +
                          MBR(&WS))
             CLRPFM     FILE(DAYJB) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(DAYJB) +
                          MBR(&WS))
             ADDLFM     FILE(DAYJO1) MBR(&WS) DTAMBRS((DAYJO (&WS)))
             MONMSG     MSGID(CPF7306)
             OVRDBF     FILE(DAYJO) TOFILE(DAYJO) MBR(&WS)
             OVRDBF     FILE(DAYJO1) TOFILE(DAYJO1) MBR(&WS)
             OVRDBF     FILE(DAYJA) TOFILE(DAYJA) MBR(&WS)
             OVRDBF     FILE(DAYJB) TOFILE(DAYJB) MBR(&WS)
                                                          /**/
             CALL       PGM(SYFA00) PARM(&IN01)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* */
    /* OPPDATER FERDIGSTATUS PÅ TURER SOM ER GÅTT*/
 FEINAV:     CALL       PGM(SYTR50) PARM(&DTO &A01 &A02 &A03 &A04 +
                          &A05 &A06 &A07 &A08 &A09 &A10 &BK1 &BK2 +
                          &BK3 &BK4 &BK5 &OPD &DOC &IN01)
    /* AVSLUTT UTEN DAGBOK??                            */
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) +
    /* PLUKK UT DE TURER SOM SKAL VÆRE MED PÅ DAGJOURNAL*/
             CALL       PGM(SYTR50A) PARM(&DTO &A01 &A02 &A03 &A04 +
                          &A05 &A06 &A07 &A08 &A09 &A10 &BK1 &BK2 +
                          &BK3 &BK4 &BK5 &OPD &DOC)
    /* LEGG UTSKRIFT I KØ PÅ SPOOLFILE                  */
             OVRPRTF    FILE(SYTR50PF) PAGESIZE(48 132) OVRFLW(47) +
                          OUTQ(*DEV) HOLD(*YES)
             CALL       PGM(SYTR50B) PARM(&DTO &A01 &A02 &A03 &A04 +
                          &A05 &A06 &A07 &A08 &A09 &A10 &BK1 &BK2 +
                          &BK3 &BK4 &BK5 &OPD &DOC)
    /* KOPIER SPOOLFILE OVER I DATABASEFIL (4 FØRSTE=SKIP/SPACEINFO) */
             CPYSPLF    FILE(SYTR50PF) TOFILE(DAYJA) SPLNBR(*LAST) +
                          TOMBR(&WS) CTLCHAR(*PRTCTL)
             MONMSG     MSGID(CPF3309) EXEC(GOTO INTET)
             DLTSPLF    FILE(SYTR50PF) SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
    /* SETTE INN BLANKLINJER ETTER INFO I SKIPP/SPACEB-FELT*/
             CALL       PGM(SYTR50C)
    /* GENERER DAGBOKSDOKUMENT I TEKSTBEHANDLING*/
    /*S01  OFFICE VISION UTGÅR                                        */
    /*S01    MRGDOC     FROMDOC(DAYJOU) FROMFLR(QPGMR) TODOC(&DOC) +  */
    /*S01                 TOFLR(QPGMR) REPLACE(*YES) MRGTYPE(*FILE) + */
    /*S01                 DTAFILE(*LIBL/DAYJB) DTAMBR(&WS) +          */
    /*S01                 JOBQ(*NO) ADJUST(*NONE) MLTLINRPT(*NO) +    */
    /*S01                 DSPOPT(*EDIT)                               */
    /*S01                 MONMSG MSGID(OFC800B) EXEC(GOTO FEINAV)     */
    /*N01 - SORTER VIA QUERY - OVER I XXDAYJOU  - SEND SOM CSV        */
    /*N01*/  RUNQRY     QRY(DAYJOU) QRYFILE((DAYJO &WS))
    /*N01*/  CHGVAR     VAR(&STMFIL) VALUE('/QDLS/QPGMR/' *CAT &DOC)
    /*N01*/  CPYTOIMPF  FROMFILE(QTEMP/XXDAYJOU) TOSTMF(&STMFIL) +
                          MBROPT(*REPLACE) STMFCODPAG(*PCASCII) +
                          RCDDLM(*CRLF) FLDDLM(';') DECPNT(*COMMA)
             SNDDST     TYPE(*DOC) TOINTNET((&EPOST)) DSTD('FIL') +
                          MSG('Åpning av .CSV - fil:     Dersom +
                          åpning direkte ikke gir ønsket resultat +
                          (problem i eldre versjoner...)     1) +
                          Lagre filen      2)Åpne EXCEL        3) +
                          Åpne filen INNE FRA EXCEL!! (husk filtype +
                          *.csv eller <Alle filer> for å finne den +
                          ved åpning..)   .') DOC(&DOC) FLR(&FLR)
             SNDPGMMSG  MSG('Fil ' *cat &DOC *cat ' er sendt til +
                          ' *cat &EPOST) /*N01*/
             GOTO SLUTT
 INTET:      SNDPGMMSG  MSG('Ingen turer oppfyller utvalgskriteriene.')
 SLUTT:      ENDPGM
