      *__________________________________
      * SYSPED / Tolldekl. Import KURERMANIFEST (CONSIGNMENT HOUSE)
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SADKMCFR
     A            MCST      R               REFFLD(ST)
     A                                      COLHDG('Status')
      * ' ' --> NY PLUKKET, IKKE SENDT. MULIG Å SLETTE FYSISK.
      * 'C' --> ER SENDT TIL TOLLVESENET. KAN SENDES PÅ NYTT ELLER KANSELLERES.
      * 'S' --> KANSELLERT, MEN IKKE SENDT TIL TOLLVESENET ENNÅ.
     A            MCAVD     R               REFFLD(AVD)
     A                                      COLHDG('Avdeling')
     A            MCTDN     R               REFFLD(TDN)
     A                                      COLHDG('Oppdragsnr')
     A            MCCN      R               REFFLD(LI)
     A                                      COLHDG('Consignment nr')
     A            MCPRO     R               REFFLD(PRO)
     A                                      COLHDG('Turnummer')
     A            MCUUID        36A         COLHDG('Unik ID - UUID')
      * Consignee
     A            MCKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Mottaker')
     A            MCNAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Mottaker')
     A            MCRGK         11A         COLHDG('Org.nr Mottaker')
     A            MCADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Mottaker')
     A            MCADK2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Mottaker')
     A            MCPNK          9A         COLHDG('Postnr Mottaker')
     A            MCADK3    R               REFFLD(AD)
     A                                      COLHDG('Poststed Mottaker')
     A            MCLKK          2A         COLHDG('Landkode Mottaker')
      *   Kommunikasjon
     A            MCCMK         50A         COLHDG('Telefon/epost Mott')
     A            MCCMKT         3A         COLHDG('Type tlf/epost Mott')
      *   Kontaktperson
     A            MCCPK         70A         COLHDG('Kontaktperson Mott')
      * Consignor
     A            MCKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Avsender')
     A            MCNAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Avsender')
     A            MCRGS         11A         COLHDG('Org.nr Avsender')
     A            MCADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Avsender')
     A            MCADS2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Avsender')
     A            MCPNS          9A         COLHDG('Postnr Avsender')
     A            MCADS3    R               REFFLD(AD)
     A                                      COLHDG('Poststed Avsender')
     A            MCLKS          2A         COLHDG('Landkode Avsender')
      *   Kommunikasjon
     A            MCCMS         50A         COLHDG('Telefon/epost Avs.')
     A            MCCMST         3A         COLHDG('Type tlf/epost Avs.')
      *   Kontaktperson
     A            MCCPS         70A         COLHDG('Kontaktperson Avs.')
      * Transport Document
     A            MCTRF         70A         COLHDG('Transport Ref.nr')
     A            MCTRFT         4A         COLHDG('Transport Ref. type')
      * Type of goods
     A            MCTYPE         3A         COLHDG('Godstype')
      * Valutakode
     A            MCVAL          3A         COLHDG('Valutakode')
     A            MCLKA          2A         COLHDG('Avsenderland')
     A            MCVOEC        17A         COLHDG('VOEC nr')
