      *
      ***********************************************************
      *** PROGRAM FOR VEDL AGENTS(TRANSPORTØRS) KOMMISJON M.M
      ***********************************************************
      *   FORELØPIG (PR 17.12.2003) KUN BRUKT AV BHR SOM SPESIAL..
     FTRTRAN    UF A E           K DISK
     FFIRM      IF   E           K DISK
     FTRAN      IF   E           K DISK
     FSYTR04FE  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  VMNAVN                 1     30
     D  VMNAV7                 1      7
     C*
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSTRNR
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     TRTRAN
     C                   READ(N)   TRTRAN                                 94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   MOVE      TTTRNR        WSTRNR
     C     TRANKE        CHAIN     TRAN                               33
     C   33              MOVE      *BLANKS       VMNAVN
     C                   WRITE     SUBFIL
     C                   READ(N)   TRTRAN                                 94
     C  N94              END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     1             ZRECNO
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   MOVE      *ZEROS        WSTRNR
     C                   MOVE      *ZEROS        WSKOMS
     C                   MOVE      *ZEROS        WSTERM
     C                   MOVE      *BLANKS       WSIKFB
     C                   GOTO      START5
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       888786
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C* TRANSPORTØR MÅ VÆRE GYLDIG
     C     TRANKE        CHAIN     TRAN                               90
     C   90              MOVE      *BLANKS       VMNAVN
     C   90              MOVE      'SPT0133'     MSGNR
     C   90              Z-ADD     13            POSNBR
     C   90              SETON                                        30
      * *IN40 OFF = NY
     C  N40              DO
     C     WSTRNR        CHAIN     TRTRAN                             33
     C     *IN33         IFEQ      *OFF
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1298'     MSGNR
     C                   Z-ADD     13            POSNBR
     C                   SETON                                        89
     C                   SETON                                        30
     C                   END
     C                   END
      * MINST EN AV VERDIENE MÅ VÆRE OPPGITT.
     C     WSKOMS        IFEQ      *ZEROS
     C     WSTERM        ANDEQ     *ZEROS
     C     WSIKFB        ANDEQ     *BLANKS
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1293'     MSGNR
     C                   Z-ADD     55            POSNBR
     C                   SETON                                        30
     C                   END
      *
      * KODE FOR F.BREVSTREKK ' ' ELLER 'X'
     C     WSIKFB        IFNE      ' '
     C     WSIKFB        ANDNE     'X'
     C     *IN30         ANDNE     *ON
     C                   MOVE      'JOV1294'     MSGNR
     C                   Z-ADD     74            POSNBR
     C                   SETON                                        3087
     C                   END
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSTRNR        CHAIN     TRTRAN                             33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    TRTRANR
     C   33              WRITE     TRTRANR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     VMNAV7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     VMNAV7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C                   MOVE      TTTRNR        WSTRNR
     C     WSTRNR        CHAIN     TRTRAN                             33
     C  N33              DELETE    TRTRANR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       88
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØK           BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
      *
     C     *IN40         IFEQ      '0'                                           KUN V/NY
     C                   CALL      'SYTR04A'
     C                   PARM                    WSTRNR
     C                   Z-ADD     55            POSNBR
     C                   GOTO      UTSØK
     C                   ENDIF
     C*
      *
     C     UTSØK         ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        TTTRNR
     C                   MOVE      *BLANKS       VMNAVN
     C                   MOVEL     '*DELETE'     VMNAV7
     C                   MOVE      *ZEROS        TTKOMS
     C                   MOVE      *ZEROS        TTTERM
     C                   MOVE      *BLANKS       TTIKFB
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSTRNR
     C                   MOVE      *BLANKS       VMNAVN
     C                   MOVE      *ZEROS        WSKOMS
     C                   MOVE      *ZEROS        WSTERM
     C                   MOVE      *BLANKS       WSIKFB
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      TTTRNR        WSTRNR
     C     TRANKE        CHAIN     TRAN                               33
     C   33              MOVE      *BLANKS       VMNAVN
     C                   MOVE      TTKOMS        WSKOMS
     C                   MOVE      TTTERM        WSTERM
     C                   MOVE      TTIKFB        WSIKFB
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     55            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSTRNR        TTTRNR
     C                   END
     C*
     C                   MOVE      WSKOMS        TTKOMS
     C                   MOVE      WSTERM        TTTERM
     C                   MOVE      WSIKFB        TTIKFB
     C*
     C                   ENDSR
     C*
