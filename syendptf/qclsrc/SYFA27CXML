             PGM        PARM(&FNKOP)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&FAKTL) TYPE(*DEC) LEN(3 0) VALUE(019)
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('J')
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('fs')
             DCL        VAR(&FNKOP) TYPE(*DEC) LEN(7 0) /*N02*/
             DCL        VAR(&KOPI) TYPE(*CHAR) LEN(1) VALUE('X') /* +
                          Hvis 'X', sende XML-faktura. */
             DCL        VAR(&MSGTXT) TYPE(*CHAR) LEN(12) /* +
                          Meldingtekst */
             DCL        VAR(&PRT1) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til faktura              */
             DCL        VAR(&PT1) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til spesifikasjon           */
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til vanlig fraktbrev. */
             DCL        VAR(&PT2) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til vanlig fraktbrev. */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(10) /* Unikkode +
                          til register SORT. */
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(9)
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&GIRO) TYPE(*DEC) LEN(3 0) VALUE(027)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&GIRON) TYPE(*CHAR) LEN(10)
             DCL        VAR(&GIROP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LPI) TYPE(*DEC) LEN(2 0) VALUE(06)
             DCL        VAR(&FAKT) TYPE(*DEC) LEN(3 0) VALUE(004)
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&LPP) TYPE(*DEC) LEN(3 0) VALUE(72)
             DCL        VAR(&LPS) TYPE(*DEC) LEN(3 0) VALUE(72)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)
    /* */
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
             RTVDTAARA  DTAARA(*LDA (48 1)) RTNVAR(&TYPE)
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
             CALL       PGM(GETPRT) PARM(&AVD &GIRO &GIROP &GIRON)
             CALL       PGM(GETPRN) PARM(&AVD &FAKT &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)

    /* */
    /* SETTER LPI TIL 08                     */
    /* */
             IF         COND(&LPI2 = 08) THEN(DO)
             CHGVAR     VAR(&LPP) VALUE(096)
             CHGVAR     VAR(&LPI) VALUE(08)
             ENDDO
    /* */
    /* SETTEL ANTALL LINJER TIL 70/93 VED LASER */
    /* */
             IF         COND(&LASER = 'J') THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('070')
             CHGVAR     VAR(&LPS) VALUE('070')
             IF         COND(&LPI = 08) THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('093')
             ENDDO
             ENDDO
             IF         COND(&LASER = 'A') THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('070')
             CHGVAR     VAR(&LPS) VALUE('070')
             IF         COND(&LPI = 08) THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('093')
             ENDDO
             ENDDO
    /*---------------------------------------*/
    /* LASER TYPE F GÅR SOM P             */
    /*---------------------------------------*/
             IF         COND(&LASER *EQ 'F') THEN(CHGVAR VAR(&LASER) +
                          VALUE('P'))
    /*------------------------------------------------------*/
    /* ORDNER PRINTERFILER                                  */
    /*------------------------------------------------------*/
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
             RTVDTAARA  DTAARA(*LDA (681 10)) RTNVAR(&PRT1)
             RTVDTAARA  DTAARA(*LDA (691 10)) RTNVAR(&PT1)
             RTVDTAARA  DTAARA(*LDA (661 10)) RTNVAR(&PRT2)
             RTVDTAARA  DTAARA(*LDA (671 10)) RTNVAR(&PT2)
    /*------------------------------------------------------*/
    /* START FOR IKKE PDF DOKUMENTER                        */
    /*------------------------------------------------------*/
             IF         COND(&LASER *NE 'P') THEN(DO)
    /*------------------------------------------------------*/
    /* FAKTURABLANKETTEN                                    */
    /*------------------------------------------------------*/
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF +
                          FILE(SYFA27PF) DEVTYPE(*IPDS) +
                          PAGESIZE(&LPP 80) LPI(&LPI) CPI(10) +
                          OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FONT(011) CHRID(590 340) +
                          FRONTOVL(&LOGO) OUTQ(&PRT1) +
                          FORMTYPE(&PT1) COPIES(&COPIES))
             IF         COND(&TYPE *NE B) THEN(OVRPRTF +
                          FILE(SYFA27PF) DEVTYPE(*SCS) +
                          PAGESIZE(&LPP 80) LPI(&LPI) CPI(10) +
                          OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT1) FORMTYPE(&PT1) COPIES(&COPIES))
    /*------------------------------------------------------*/
    /* SAMLEFAKTURALISTERR                                  */
    /*------------------------------------------------------*/
             CALL       PGM(GETPRN) PARM(&AVD &FAKTL &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             OVRPRTF    FILE(SYFA27PS) PAGESIZE(&LPS 80) CPI(10) +
                          OVRFLW(54) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(0) FRONTOVL(&LOGO) +
                          OUTQ(&PRT2) FORMTYPE(&PT2) +
                          COPIES(&COPIES) SHARE(*YES) /* Pirnterfil +
                          av samlefaktura liste */
             OVRPRTF    FILE(SYFA27PP) PAGESIZE(70 80) CPI(10) +
                          OVRFLW(66) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT2) FORMTYPE(&PT2) +
                          COPIES(&COPIES) /* PrInterfil av +
                          samlefaktura liste */
    /*------------------------------------------------------*/
    /* FRITTSTÅENDE GIRO                                   */
    /*------------------------------------------------------*/
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF +
                          FILE(SYFA27PG) DEVTYPE(*IPDS) PAGESIZE(24 +
                          80) CPI(10) OVRFLW(24) FONT(011) +
                          CHRID(590 340) OUTQ(&GIROP) FORMTYPE(&GIRON))
             ELSE       CMD(OVRPRTF FILE(SYFA27PG) OUTQ(&GIROP) +
                          DEVTYPE(*SCS) PAGESIZE(24 80) CPI(10) +
                          OVRFLW(24) FORMTYPE(&GIRON))
             ENDDO
    /*                                    */
    /* SLUTT PÅ IKKE PDF                  */
    /*                                    */
    /*                                    */
    /* START FOR PDF DOKUMENTER           */
    /*                                    */
             IF         COND(&LASER = 'P') THEN(DO)
    /*------------------------------------------------------*/
    /* FAKTURABLANKETTEN                                    */
    /*------------------------------------------------------*/
             CALL       PGM(GETPRN) PARM(&AVD &FAKT &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF +
                          FILE(SYFA27PF) DEVTYPE(*AFPDS) +
                          PAGESIZE(&LPP 80) LPI(&LPI) CPI(10) +
                          OVRFLW(&LPP) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) FONT(011) +
                          CHRID(590 340) FRONTOVL(&LOGO) +
                          OUTQ(&PRT1) FORMTYPE(&PT1) COPIES(&COPIES))
             IF         COND(&TYPE *NE B) THEN(OVRPRTF +
                          FILE(SYFA27PF) DEVTYPE(*AFPDS) +
                          PAGESIZE(&LPP 80) LPI(&LPI) CPI(10) +
                          OVRFLW(&LPP) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) +
                          FRONTOVL(&LOGO) OUTQ(&PRT1) +
                          FORMTYPE(&PT1) COPIES(&COPIES))
    /*------------------------------------------------------*/
    /* SAMLEFAKTURALISTERR                                  */
    /*------------------------------------------------------*/
             CALL       PGM(GETPRN) PARM(&AVD &FAKTL &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             OVRPRTF    FILE(SYFA27PS) DEVTYPE(*AFPDS) PAGESIZE(&LPS +
                          80) CPI(10) FRONTMGN(0 0.1) OVRFLW(54) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) PAGRTT(0) +
                          FRONTOVL(&LOGO) OUTQ(&PRT2) +
                          FORMTYPE(&PT2) COPIES(&COPIES) +
                          SHARE(*YES) /* Pirnterfil av samlefaktura +
                          liste */
             OVRPRTF    FILE(SYFA27PP) DEVTYPE(*AFPDS) PAGESIZE(70 +
                          80) CPI(10) OVRFLW(66) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT2) FORMTYPE(&PT2) +
                          COPIES(&COPIES) /* PrInterfil av +
                          samlefaktura liste */
    /*------------------------------------------------------*/
    /* FRITTSTÅENDE GIRO                                   */
    /*------------------------------------------------------*/
             IF         COND(&TYPE *EQ B) THEN(OVRPRTF +
                          FILE(SYFA27PG) DEVTYPE(*AFPDS) +
                          PAGESIZE(24 80) CPI(10) OVRFLW(24) +
                          FONT(011) CHRID(590 340) +
                          FRONTOVL(LOGOFAKT) OUTQ(&GIROP) +
                          FORMTYPE(&GIRON))
             ELSE       CMD(OVRPRTF FILE(SYFA27PG) DEVTYPE(*AFPDS) +
                          PAGESIZE(24 80) CPI(10) OVRFLW(24) +
                          FRONTOVL(LOGOFAKT) OUTQ(&GIROP) +
                          FORMTYPE(&GIRON))
             ENDDO
    /*                        */
    /* OPPRETTE SORTERINGSFIL */
    /*                        */
 RTVTIME:    RTVJOBA    NBR(&TIME)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &TIME)
/* SORT OGSÅ I LØSNING 1 - INLIBL FOR Å SIKRE BIBLIOTEKSLISTE */
             RTVDTAARA  DTAARA(FIRMDT (141 10)) RTNVAR(&LIB)
             OVRDBF     FILE(SORT) TOFILE(&LIB/SORT)

             RMVM       FILE(&LIB/SORT) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(&LIB/SORT) MBR(&UNIK)
             OVRDBF     FILE(SORT) TOFILE(&LIB/SORT) MBR(&UNIK)
             OVRDBF     FILE(FAKT) FRCRATIO(1)
    /*                   */
    /* SKRIVE UT FAKTURA */
    /*                   */
             OVRDBF     FILE(FAKT) FRCRATIO(1)
             OVRDBF     FILE(FAK8) FRCRATIO(1)
    /*                   */
             CALL       PGM(SYFA27) PARM(&PRANT &KOPI &FNKOP)
    /*                   */
    /* SKRIV TIL ARKIV   */
    /*                   */
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &QUSER +
                          &FINS)) JOB(ARKIVFS) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
             RCLRSC
             DLTOVR     FILE(FAKT)
             RMVM       FILE(SORT) MBR(&UNIK)
             CHGVAR     VAR(&MSGTXT) VALUE(&PRANT *CAT &PRT1)
    /* */
             ENDPGM
