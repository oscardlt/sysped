     A*%%TS  SD  20151204  142803  JOVO        REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20151204  142803  JOVO        REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  5  2VALUES(' ' '2' '3' '4' '5')
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A            SYKUNR         8Y 0O  5  4EDTCDE(4)
     A  50                                  DSPATR(ND)
     A            SYPAID         5A  O  5 13
     A  50                                  DSPATR(ND)
     A            SYSORT         3Y 0O  5 19EDTCDE(4)
     A  50                                  DSPATR(ND)
     A            SYVRDN        15Y 5O  5 23EDTCDE(M)
     A  50                                  DSPATR(ND)
     A            SYVRDA        50A  H
     A            SYRECN         7Y 0H
     A            XAVRDA        40A  O  5 41
     A  50                                  COLOR(WHT)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20151204  142803  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0016)
     A  *DS4                                SFLSIZ(0016)
     A  *DS3                                SFLPAG(0015)
     A  *DS4                                SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYPR03R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med paremetre til bruker/ku-
     A                                      nde..'
     A                                      COLOR(WHT)
     A                                  2  2'BRUKER:'
     A            UUSER         10A  O  2 10COLOR(BLU)
     A                                  2 23'KUNDE:'
     A            UKUNR          8S 0O  2 30COLOR(BLU)
     A            UNAVN         30A  O  2 40COLOR(BLU)
     A                                  3  2'Velg opsjon:   2=Endre   3=Kopier -
     A                                        4=Slett   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  1'Op Kundenr. ParID Sort Nummerisk v-
     A                                      erdi Alfabetisk verdi            '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CA12(12 'Avslutt program')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  3  2'F3/F12=Avbryt   F5=Frisk   F6=Lag -
     A                                      ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VARELI
     A*%%TS  SD  20151204  142803  JOVO        REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSRECN         7Y 0H
     A                                  1  2'   Kundenr. ParID Sort Nummerisk v-
     A                                      erdi        '
     A            WSKUNR         8Y 0B  2  5DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPAID         5A  B  2 14DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A            WSSORT         3Y 0B  2 20EDTCDE(4)
     A            WSVRDN        15Y 5B  2 25DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                  3  5'Alfabetisk verdi:'
     A            WSVRDA        50A  B  3 25DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  4  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            KFTXT         50A  O  4 25COLOR(WHT)
     A N30        KFTXTE        50A  O  5 25COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            SYRECN         7Y 0H
     A                                  1  2'   Kundenr. ParID Sort Nummerisk v-
     A                                      erdi              '
     A            SYKUNR         8Y 0O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SYPAID         5A  O  2 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            SYVRDN        15Y 5O  2 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(M)
     A                                  3  5'Alfabetisk verdi:'
     A            SYVRDA        50A  O  3 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            SYSORT         3Y 0O  2 20COLOR(WHT)
     A                                      EDTCDE(4)
     A            KFTXT         50   O  4 25COLOR(WHT)
     A N30        KFTXTE        50   O  5 25COLOR(WHT)
      *
     A          R NYVARE
     A                                      CA03(03 'Avslutt program')
     A                                      CF06(06 'LAGE NY')
     A                                      CA12(12 'Avslutt program')
     A                                  1  2'SYSPED/SYPR03R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med paremetre til bruker/ku-
     A                                      nde..'
     A                                      COLOR(WHT)
     A                                  2  2'BRUKER:'
     A            UUSER         10A  O  2 10COLOR(BLU)
     A                                  2 23'KUNDE:'
     A            UKUNR          8  0O  2 30COLOR(BLU)
     A            UNAVN         30A  O  2 40COLOR(BLU)
     A                                  3  2'Velg opsjon:   2=Endre   3=Kopier -
     A                                        4=Slett   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Kundenr. ParID Sort Nummerisk v-
     A                                      erdi Alfabetisk verdi             '
     A                                      COLOR(WHT)
     A                                 23  2'F3/F12=Avbryt   F6=Lag ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
