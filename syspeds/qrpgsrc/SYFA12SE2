      * ***************************************************************
      *    DETTE PROGRAM SKRIVER ETIKETT PÅ ELTRON SKRIVER
      * ***************************************************************
      *
      * COMMAND Q=LENGDE / MELLOMROM M.M
      * Q p1,p2 (p1=lengde i dots, p2=mellomrom i dots )
      *
      * MÅLES I "DOTS",  PÅ ORION: 8 DOT PR MM
      * TOTALLENGDE MELLOM PERFORERING = 195MM  = 1560 DOTS
      * LENGDE PÅ DEN ENKELT LAPP      = 192MM  = 1536 DOTS
      * LENGDE PÅ MELLOMROM(GAP)       =   3MM  =   24 DOTS
      * (LENGDE FRA PERF.TIL START LAPP= 1,5MM  =   12 DOTS)
      * SE EPL2en.pdf side 244
     FWSNRCLI   UF A E           K DISK
     FFIRFB     UF   E           K DISK
     FSYLA03P   O    F   80        PRINTER OFLIND(*INOF)
     D F1              S             36    DIM(50) CTDATA PERRCD(1)             Kommandoer
      *
     D A4              S              1    DIM(4)                               Input string
     D B4              S              1    DIM(4)                               Output string
     D Æ               S              1    DIM(20)                              Input string
     D Å               S              1    DIM(20)                              Output string
     D                 DS
      * Make "No. of Pieces" Zoned numeric
     D  LANUMA                 1      4  0
     D                 DS
     D  SNDNRDS                1     20
     D  SNDNR_1                1      1
     D  SNDNR_2_20             2     20
     D                 DS
     D  KOID                   1     18
     D  DSIDPK                 1      1
     D  DSIDLA                 2      3
     D  DSIDLE                 4      8
     D  DSIDNR                 9     18
     D                 DS
     D  WCOPN                  1      4  0
     D  WCOPA                  1      4
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
     D                 DS
     D  WscDat                 1      8  0
     D  WscDatÅ                1      4  0
     D  WscDatM                4      6  0
     D  WscDatD                7      8  0
      * Input Parameter
     C     *ENTRY        PLIST
     C                   PARM                    WsUser           10
     C                   PARM                    WFIRM             2
     C                   PARM                    SNDNR            20
     C                   PARM                    ANTKLL            4
     C                   PARM                    POSTNR            4
     C                   PARM                    KOMMENTAR        20
     C                   PARM                    TRANSPORTØR      20
     C                   PARM                    FLAGG             1
      *
     C                   EXSR      SRINIT
      *
      * LOOP FOR NO OF COPIES
     C                   DO        DFNTLA        WCOPN
      * Send field data to printer
     C                   EXSR      SR100
      * END LOOP
     C                   ENDDO
      * End Program
     C                   SETON                                        LR
     C                   RETURN
      *
      *_____________________________________________________
      * Send all data fields
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR100         BEGSR
     C                   MOVE      *BLANKS       KOID
     C     WFIRM         CHAIN     FIRFB                              33                    3
     C                   ADD       1             FIIDNR
     C                   UPDATE    FIRFBR
     C                   MOVE      '3'           DSIDPK
     C                   MOVE      FIIDLA        DSIDLA
     C                   MOVE      FIIDLE        DSIDLE
     C                   MOVEL     FIIDNR        DSIDNR
     C                   MOVE      *ZEROS        UAN22            22
     C                   MOVE      KOID          UAN22
     C                   CALL      'SYGE08'
     C                   PARM                    UAN22
     C                   MOVE      UAN22         KOID
      * WscKOD: U=Umerket gods mottatt/LABEL PRINTET
      *         (M=Merket gods mottatt/koblet)
     C                   MOVE      'U'           WSCKOD
     C                   MOVEL     SNDNR17       WSCSNR
     C                   MOVEL     KOID          WSCCLI
     C                   MOVEL     KOMMENTAR     WSCTXT
     C                   TIME                    XTIME
     C                   MOVE      XDD           WscDatD
     C                   MOVE      XMM           WscDatM
     C                   MOVE      XÅÅ           WscDatÅ
     C                   MOVE      XTID          WscTIM
     C                   MOVE      WSUSER        WscUse
     C                   WRITE     WSNRCLIR
      *
      ******************************************************
      * Her begynner selve produksjon av utdata til merkelapp
      ******************************************************
      * Ved lapp 2,3,4, endres kun variable data ( xx AV YY og kol-ID)
     C     *IN50         CABEQ     '1'           VARI1
      *
      * STREK
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(05)        DATA
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(10)        DATA
     C                   EXCEPT    SYLA01
      *
      *
      * TRANSPORTØR
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(11)        DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(12)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(13)        DATA
     C     DATA          CAT       TRANSPORTØR:0 DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      *
      * TRANSPORT-PRODUKT - KODETEKST
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(15)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C     KOMMENTAR     IFNE      *BLANKS
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(16)        DATA
     C     DATA          CAT       KOMMENTAR:0   DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   END
      *
      * ANTALL ... AV...
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(21)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C     VARI1         TAG
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(22)        DATA
     C                   MOVE      WCOPN         CHAR4             4
     C                   MOVEA     CHAR4         A4
     C                   Z-ADD     1             X
     C                   Z-ADD     1             Y
     C                   MOVEA     *BLANKS       B4
     C     A4(X)         DOWEQ     '0'
     C     X             ANDLT     4
     C                   ADD       1             X
     C                   ENDDO
     C     X             DOWLE     4
     C                   MOVEA     A4(X)         B4(Y)
     C                   ADD       1             X
     C                   ADD       1             Y
     C                   ENDDO
     C                   MOVEA     B4            CHAR4
     C     DATA          CAT       CHAR4:0       DATA
     C     DATA          CAT       'AV':1        DATA
     C                   Z-ADD     DFNTLA        LANUMA
     C                   MOVE      LANUMA        CHAR4             4
     C                   MOVEA     CHAR4         A4
     C                   Z-ADD     1             X
     C                   Z-ADD     1             Y
     C                   MOVEA     *BLANKS       B4
     C     A4(X)         DOWEQ     '0'
     C     X             ANDLT     4
     C                   ADD       1             X
     C                   ENDDO
     C     X             DOWLE     4
     C                   MOVEA     A4(X)         B4(Y)
     C                   ADD       1             X
     C                   ADD       1             Y
     C                   ENDDO
     C                   MOVEA     B4            CHAR4
     C     DATA          CAT       CHAR4:1       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      * Ved lapp 2,3,4, endres kun variable data ( xx AV YY og kol-ID)
     C     *IN50         CABEQ     '1'           VARI2
      *
      * STREK
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(23)        DATA
     C                   EXCEPT    SYLA01
      *
      * FRAKTBREVSNR
      * Ledetekst
     C     SNDNR         IFNE      *BLANKS
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(24)        DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(25)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(26)        DATA
     C     DATA          CAT       SNDNR:0       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   END
      *
      * TIL POSTNR
      * Ledetekst
     C     POSTNR        IFNE      *BLANKS
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(27)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      POSTNR        XXPOUL            4
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(28)        DATA
     C     DATA          CAT       '421':0       DATA
     C                   move      *blanks       xxlkpn           12
     C                   Eval      XXLKPN = '578'+XXPOUL
     C     DATA          CAT       XXLKPN:0      DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   END
      *
      * KOLLIID
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(29)        DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(30)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C     VARI2         TAG
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(31)        DATA
     C     DATA          CAT       '00':0        DATA
     C     DATA          CAT       KOID:0        DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      * Print 1 eksemplar
     C                   MOVEL     *BLANKS       DATA
     C                   MOVEL     'P1'          DATA
     C                   EXCEPT    SYLA01
      *
     C                   MOVE      *ON           *IN50                           FØRSTE FERD
      *
     C                   ENDSR
      *_____________________________________________________
      * Initialise subroutine, set character set
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C                   MOVE      *OFF          *IN50
      *
     C                   Z-ADD     0             ØX                3 0          Index for Ø
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
     C                   MOVE      'N'           ØADEC             1            Decimal Indicator
     C                   Z-ADD     0             ÆX                3 0
     C                   Z-ADD     0             X                 1 0
     C                   Z-ADD     0             Y                 1 0
     C                   MOVE      *BLANKS       MSGVAR            7            Message Variabel
     C* Initiering/ Printparam / label size
     C                   MOVE      *BLANKS       DATA             80
     C                   EXCEPT    SYLA01
     C                   MOVEL     'N       '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'D7      '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'S2      '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'Q1536,24'    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'q760    '    DATA
     C                   EXCEPT    SYLA01
      *
      *
      * Kommer inn uten f.brevsnr... Tildel.
     C                   movel     SNDNR         SNDNRDS
     c     SNDNR_1       IFNE      *Blanks
     c     SNDNR_2_20    ANDEQ     *Blanks
     C                   call      'SYGE15'
     c                   parm                    SndNr
     c                   end
     c                   move      SNDNR         SNDNR17          17
     c                   move      ANTKLL        DFNTLA            4 0
      *
      * Fjern evt gamle cll-ID på dette send.nr
     C     SNDNR         IFNE      *BLANKS
     C     SNDNR17       SETLL     WSNRCLI
     C     SNDNR17       READE     WSNRCLI                                33
     C     *in33         DOWEQ     *OFF
     C                   DELETE    WSNRCLIR
     C     SNDNR17       READE     WSNRCLI                                33
     C                   END
     C                   END
      *
     c                   clear                   WscDat
     c                   clear                   xtime
      *
     C                   ENDSR
      *
     OSYLA03P   E            SYLA01
     O                       DATA                80
      *
      *
      * A=ASCII-TEKST, PARAMETRE...
      *   P1=KOLONNE,P2=LINJE,P3=ROTASJON,P4=FONT,P5=HORIS MULTIPL,
      *   P6=VERTIKAL MULTIPL, P7=N=NORMAL/R=REVERSE,P8=DATA
      * Obs Hvert tab-elem er max 36 long ( t.o.m. kol 44 på skjerm)
**    * KOMMANDO/FAST TEKST         |    DATA SOM SKAL CONCATINERES..
A18,40,0,2,1,1,N,"Fra/From"         | 01
A40,65,0,1,2,2,N,"                  | 02 AVS NAVN
A40,95,0,1,2,2,N,"                  | 03 AVS ADR 1
A40,125,0,1,2,2,N,"                 | 04 AVS ADR 3
LE10,150,666,6                      | 05 (=strek)
A18,170,0,2,1,1,N,"Til/To"          | 06
A40,190,0,2,2,2,N,"                 | 07 MOT NAVN
A40,240,0,2,2,2,N,"                 | 08 MOT ADR 1
A40,290,0,2,2,2,N,"                 | 09 MOT ADR 3
LE10,350,666,6                      | 10 (=strek)
A18,360,0,2,1,1,N,"Transportør/C"   | 11
A170,360,0,2,1,1,N,"arrier"         | 12
A40,385,0,4,1,2,N,"                 | 13 TRANSP. NAVN
A450,385,0,4,2,2,R,"TOLLPASS!!"     | 14 TOLLPASS/BLANK
A18,440,0,2,1,1,N,"Kommentar"       | 15
A40,460,0,2,1,2,N,"                 | 16 Kommentar  (oppr: Produkt..)
A18,505,0,2,1,1,N,"Merke/Marks"     | 17
A40,525,0,2,1,2,N,"                 | 18 MERKE 1 + MERKE2
A18,555,0,2,1,1,N,"Senders ref"     | 19
A40,575,0,2,1,2,N,"                 | 20 AVD/OPD/FBNR/SIGN
A18,610,0,2,1,1,N,"Kolli"           | 21
A110,610,0,3,2,2,N,"                | 22 ANT AV TOT
LE10,650,666,6                      | 23 (=strek)
A18,670,0,2,1,1,N,"Sendings-ID/"    | 24
A160,670,0,2,1,1,N,"Consignment"    | 25
B18,690,0,1E,4,8,220,B,"            | 26 SENDINGSNR
A18,950,0,2,1,1,N,"Til/To"          | 27
B180,970,0,1E,4,8,220,B,"           | 28 POSTNR
A18,1240,0,2,1,1,N,"Kolli-ID/Lic"   | 29
A160,1240,0,2,1,1,N,"ence plate"    | 30
B18,1260,0,1E,4,8,220,B,"           | 31 KOLLIID
A450,555,0,2,1,1,N,"A-ref"          | 32
A450,575,0,2,1,2,N,"                | 33 A-REF
