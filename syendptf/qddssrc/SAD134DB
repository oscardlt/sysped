     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 9 70 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A N49                                  CF04(04 'Søking')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD134RB'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med eksport-id'
     A                                      COLOR(WHT)
     A                                  2  2'Avd.:'
     A            CLAVD          4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Opdnr:'
     A            CLTDN          7Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 32'Linjenr:'
     A            ULI            2Y 0O  2 42TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
      *
     A                                  4  2'Eksp.avd.:'
     A            WSAVDE         4Y 0B  4 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N99                                  COLOR(YLW)
     A  99                                  COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                  4 20'Oppdrag:'
     A            WSTDNE         7Y 0B  4 29TEXT('Oppdragsnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A                                      EDTCDE(Z)
      *
     A                                  6  2'Eksp.type:'
     A            WSETYP         2A  B  6 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
     A            WSETYPT       45A  O  6 21COLOR(WHT)
     A                                  7  2'Eksport Id:'
     A            WSEID         18A  B  7 15TEXT('Eksport Id')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A                                  7 35'Sertifisert:'
     A            WSESER         1A  B  7 48TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                  7 50'(J/N)'
     A N49                              8  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  49                              8  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O  9  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
