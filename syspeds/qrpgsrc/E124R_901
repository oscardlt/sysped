********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11174 * 01 YBC PTF11 TILLEGGSLINJER MED SAMME POST
********************************************************************
     H DATEDIT(*YMD)
     FEUNH      IF   E           K DISK
     FEDIM      UF   E           K DISK
     FEALC901   IF   E           K DISK
     FEALI901   IF   E           K DISK
     FECNT901   IF   E           K DISK
     FECUX901   IF   E           K DISK
     FEFTX901   IF   E           K DISK
     FEIMD901   IF   E           K DISK
     FELIN901   IF   E           K DISK
     FENAD901   IF   E           K DISK
     FEPAI901   IF   E           K DISK
     FEPAT901   IF   E           K DISK
     FEPIA901   IF   E           K DISK
     FERFF901   IF   E           K DISK
     FETDT901   IF   E           K DISK
     FETMA901   IF   E           K DISK
     FETOD901   IF   E           K DISK
     FSTANDI    UF   E           K DISK
     FECMSG1    IF   E           K DISK
     FKUNKO2    IF   E           K DISK
     FSADL      IF   E           K DISK
     FSADH      O  A E             DISK
     FSADV      O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  DSMNN                  1      9  0
     IEDIMR
     I              MMN                         TVIMMN
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N20              EXSR      RTVD
      *___________
      * UPDATE LOG
      *"""""""""""
     C                   MOVE      'C'           MST
     C                   UPDATE    EDIMR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     KEYMS3        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN3
     C                   KFLD                    ZREPD0
     C                   KFLD                    ZREPD1
     C                   KFLD                    ZREPD2
     C     KEYMS4        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN4
     C                   KFLD                    ZREPE0
     C                   KFLD                    ZREPE1
     C                   KFLD                    ZREPE2
     C                   KFLD                    ZREPE3
     C     KEYMS5        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN5
     C                   KFLD                    ZREPF0
     C                   KFLD                    ZREPF1
     C                   KFLD                    ZREPF2
     C                   KFLD                    ZREPF3
     C                   KFLD                    ZREPF4
     C     KEYECM        KLIST
     C                   KFLD                    MSR
     C                   KFLD                    U0065
     C                   KFLD                    U0054
     C                   KFLD                    M0004
     C                   KFLD                    M0010
     C     KEYKUN        KLIST
     C                   KFLD                    ECCNO
     C                   KFLD                    ZUINTN
     C     KEYSAL        KLIST
     C                   KFLD                    SIKNS
     C                   KFLD                    ZLALFA
     C     *LIKE         DEFINE    TVIMMN        ZMMN
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NGN           ZGN3
     C     *LIKE         DEFINE    NGN           ZGN4
     C     *LIKE         DEFINE    NGN           ZGN5
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C     *LIKE         DEFINE    NREP0         ZREPD0
     C     *LIKE         DEFINE    NREP1         ZREPD1
     C     *LIKE         DEFINE    NREP2         ZREPD2
     C     *LIKE         DEFINE    NREP0         ZREPE0
     C     *LIKE         DEFINE    NREP1         ZREPE1
     C     *LIKE         DEFINE    NREP2         ZREPE2
     C     *LIKE         DEFINE    NREP3         ZREPE3
     C     *LIKE         DEFINE    NREP0         ZREPF0
     C     *LIKE         DEFINE    NREP1         ZREPF1
     C     *LIKE         DEFINE    NREP2         ZREPF2
     C     *LIKE         DEFINE    NREP3         ZREPF3
     C     *LIKE         DEFINE    NREP4         ZREPF4
     C     *LIKE         DEFINE    NREP0         ZREPG0
     C     *LIKE         DEFINE    NREP1         ZREPG1
     C     *LIKE         DEFINE    NREP2         ZREPG2
     C     *LIKE         DEFINE    NREP3         ZREPG3
     C     *LIKE         DEFINE    NREP4         ZREPG4
     C     *LIKE         DEFINE    NREP5         ZREPG5
     C     *LIKE         DEFINE    NREP0         ZREPH0
     C     *LIKE         DEFINE    NREP1         ZREPH1
     C     *LIKE         DEFINE    NREP2         ZREPH2
     C     *LIKE         DEFINE    NREP3         ZREPH3
     C     *LIKE         DEFINE    NREP4         ZREPH4
     C     *LIKE         DEFINE    NREP5         ZREPH5
     C     *LIKE         DEFINE    NREP6         ZREPH6
     C     *LIKE         DEFINE    NREP0         ZREPI0
     C     *LIKE         DEFINE    NREP1         ZREPI1
     C     *LIKE         DEFINE    NREP2         ZREPI2
     C     *LIKE         DEFINE    NREP3         ZREPI3
     C     *LIKE         DEFINE    NREP4         ZREPI4
     C     *LIKE         DEFINE    NREP5         ZREPI5
     C     *LIKE         DEFINE    NREP6         ZREPI6
     C     *LIKE         DEFINE    NREP7         ZREPI7
     C     *LIKE         DEFINE    NREP0         ZREPJ0
     C     *LIKE         DEFINE    NREP1         ZREPJ1
     C     *LIKE         DEFINE    NREP2         ZREPJ2
     C     *LIKE         DEFINE    NREP3         ZREPJ3
     C     *LIKE         DEFINE    NREP4         ZREPJ4
     C     *LIKE         DEFINE    NREP5         ZREPJ5
     C     *LIKE         DEFINE    NREP6         ZREPJ6
     C     *LIKE         DEFINE    NREP7         ZREPJ7
     C     *LIKE         DEFINE    NREP8         ZREPJ8
     C     *LIKE         DEFINE    KUINTN        ZUINTN
     C     *LIKE         DEFINE    SLALFA        ZLALFA
     C     *ENTRY        PLIST
     C                   PARM                    DSMNA             9
     C                   MOVE      DSMNA         DSMNN
     C                   Z-ADD     DSMNN         ZMN
     C     ZMN           CHAIN     EUNH                               20
     C  N20              Z-ADD     ZMN           ZMMN
     C  N20ZMMN          CHAIN     EDIM                               20
      *DELAY THIS JOB
     C                   MOVEL(P)  'DLYJOB D'    QCMDR            15
     C                   CAT       'LY(180':0    QCMDR
     C                   CAT       ')':0         QCMDR
     C                   Z-ADD     15            QCMDLE           15 5
     C                   CALL      'QCMDEXC'                            3131
     C                   PARM                    QCMDR
     C                   PARM                    QCMDLE
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *_____________
      * INIT AV SADH
      *"""""""""""""
     C                   CLEAR                   SADHR
      *_______________________
      * HENTE HOVEDKUNDENUMMER
      *"""""""""""""""""""""""
     C     KEYECM        CHAIN     ECMSG1                             20
     C     *IN20         IFEQ      '1'
     C                   Z-ADD     0             ECCNO
     C                   ENDIF
      *_____________________________
      * HENTE TOLLDEKLARASJONSNUMMER
      *"""""""""""""""""""""""""""""
     C                   Z-ADD     1             SIAVD
     C     SIAVD         CHAIN     STANDI                             20
     C     *IN20         IFEQ      '0'
     C                   ADD       1             SITDN
     C                   UPDATE    STANDIR
     C                   ENDIF
      * INIT AV RESTERENDE FELT
     C                   MOVE      'EDI'         SISG
     C                   MOVEL     U1004         SIFIF
     C                   Z-ADD     MDT           SIFID
     C                   Z-ADD     MDT           SIDT
      *___________________
      * READ MAIN SEGMENTS
      *"""""""""""""""""""
      * RFF00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ERFF901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF901                                51
     C     *IN51         DOWEQ     '0'
      *
      *
     C     KEYMS0        READE     ERFF901                                51
     C  N51              ENDDO
     C                   ENDIF
      * NAD01
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     1             ZGN0
     C     KEYMS0        SETLL     ENAD901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     N3039         A
     C                   EXSR      GETNUM
     C                   MOVEL     N3039         ZUINTN
     C     KEYKUN        CHAIN     KUNKO2                             20
     C     *IN20         IFEQ      '0'
     C                   Z-ADD     KUKUN2        ZNUM
     C                   ENDIF
     C                   SELECT
     C     N3035         WHENEQ    'CZ '                                        CONSIGNOR(SELL)
     C                   Z-ADD     ZNUM          SIKNS
     C                   MOVEL     N3036A        SINAS
     C                   MOVEL     N3042A        SIADS1
     C                   MOVEL     N3042B        SIADS2
     C                   MOVEL     N3251         SIADS3                         POSTCODE
     C                   MOVE      N3164         SIADS3                         CITY
     C*                    MOVE N3207     SIADS3           COUNTRY
      *
     C     N3035         WHENEQ    'BY '                                        BUYER
     C                   Z-ADD     ZNUM          SIKNK
     C                   MOVEL     N3036A        SINAK
     C                   MOVEL     N3042A        SIADK1
     C                   MOVEL     N3042B        SIADK2
     C                   MOVEL     N3251         SIADK3                         POSTCODE
     C                   MOVE      N3164         SIADK3                         CITY
     C*                    MOVE N3207     SIADK3           COUNTRY
     C                   ENDSL
      *
     C     KEYMS0        READE     ENAD901                                51
     C  N51              ENDDO
     C                   ENDIF
      * CUX03
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     3             ZGN0
     C     KEYMS0        SETLL     ECUX901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECUX901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     C6345A        SIVAL1
     C                   MOVEL     C6345A        SIVAL3
      *
     C     KEYMS0        READE     ECUX901                                51
     C  N51              ENDDO
     C                   ENDIF
      * ALI00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EALI901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EALI901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     A3239         SILKA
      *
     C     KEYMS0        READE     EALI901                                51
     C  N51              ENDDO
     C                   ENDIF
      * FTX00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EFTX901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EFTX901                                51
     C     *IN51         DOWEQ     '0'
      *
      *
     C     KEYMS0        READE     EFTX901                                51
     C  N51              ENDDO
     C                   ENDIF
      * PAT00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EPAT901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EPAT901                                51
     C     *IN51         DOWEQ     '0'
      *
      *
     C     KEYMS0        READE     EPAT901                                51
     C  N51              ENDDO
     C                   ENDIF
      * PAI00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EPAI901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EPAI901                                51
     C     *IN51         DOWEQ     '0'
      *
      *
     C     KEYMS0        READE     EPAI901                                51
     C  N51              ENDDO
     C                   ENDIF
      * TDT04
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     4             ZGN0
     C     KEYMS0        SETLL     ETDT901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     T8067         SITRM
      *
     C     KEYMS0        READE     ETDT901                                51
     C  N51              ENDDO
     C                   ENDIF
      * TOD00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ETOD901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETOD901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     T4053         SILV
      *
     C     KEYMS0        READE     ETOD901                                51
     C  N51              ENDDO
     C                   ENDIF
      * TMA00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ETMA901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETMA901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     T5356         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SIBEL3
      *
     C     KEYMS0        READE     ETMA901                                51
     C  N51              ENDDO
     C                   ENDIF
      * ALC23
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     23            ZGN0
     C     KEYMS0        SETLL     EALC901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EALC901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     A5422         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SIBEL1
      *
     C     KEYMS0        READE     EALC901                                51
     C  N51              ENDDO
     C                   ENDIF
      * CNT00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     C6066A        A
     C                   EXSR      GETNUM
     C                   SELECT
     C     C6069A        WHENEQ    '11 '                                        NO. OF PACK
     C                   Z-ADD     ZNUM          SINTK
     C     C6069A        WHENEQ    '08 '                                        GROSS WEIGHT
     C                   Z-ADD     ZNUM          SIVKB
     C                   ENDSL
      *
     C     KEYMS0        READE     ECNT901                                51
     C  N51              ENDDO
     C                   ENDIF
      *__________
      * LAGE SADH
      *""""""""""
     C                   WRITE     SADHR
      *_________
      * READ LIN
      *"""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     7             ZGN0
     C     KEYMS0        SETLL     ELIN901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELIN901                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ELIN901                                51
     C  N51              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *_________
      * INZ SADV
      *"""""""""
     C                   CLEAR                   SADVR
     C                   Z-ADD     SIAVD         SVAVD
     C                   Z-ADD     SITDN         SVTDN
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     L1082         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVLI
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     L6060         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVNTM
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     L5110A        A
     C                   EXSR      GETNUM
     C     SVNTM         MULT      ZNUM          SVBELT
     C                   MOVEL     L7020A        SVFT01                         ART.NO ....35AN
     C                   MOVE      '1'           SVVF
     C                   MOVE      'ST  '        SVEH01
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     LREP0         ZREPB0
     C                   EXSR      RFF07
     C                   EXSR      PIA07
     C                   EXSR      IMD07
     C                   EXSR      ALC11
      *_______________________________
      * HENTE FRA KUNDERS VAREREGISTER
      *"""""""""""""""""""""""""""""""
     C                   MOVEL     SVFT01        ZLALFA
     C     KEYSAL        CHAIN     SADL                               20
     C     *IN20         IFEQ      '0'
     C                   MOVEL     SLTXT         SVVT01
     C     SVLK          IFEQ      *BLANKS
     C                   MOVEL     SLOPPL        SVLK
     C                   ENDIF
     C                   Z-ADD     SLVEKT        SVVKTB
     C                   Z-ADD     SLTANR        SVVNT
     C*                    MOVELSLTAR
     C                   MOVEL     SLPVA         SVPVA
     C                   Z-ADD     SLSATS        SVAS
     C                   MOVEL     SLTN          SVTN
     C                   ENDIF
      *______________
      * WRITE TO SADV
      *""""""""""""""
     C                   WRITE     SADVR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * PIA07                                               PIA07 /
      *////////////////////////////////////////////////////////////
     C     PIA07         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     7             ZGN1
     C     KEYMS1        SETLL     EPIA901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EPIA901                                52
     C     *IN52         DOWEQ     '0'
      *
      *
     C     KEYMS1        READE     EPIA901                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF07                                               RFF07 /
      *////////////////////////////////////////////////////////////
     C     RFF07         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     7             ZGN1
     C     KEYMS1        SETLL     ERFF901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF901                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     R1153         WHENEQ    'ABD'
     C                   MOVEL     R1154         SVVNT
     C                   ENDSL
      *
     C     KEYMS1        READE     ERFF901                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * IMD07                                               IMD07 /
      *////////////////////////////////////////////////////////////
     C     IMD07         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     7             ZGN1
     C     KEYMS1        SETLL     EIMD901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EIMD901                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEL     I7008A        SVVT01
      *
     C     KEYMS1        READE     EIMD901                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * ALC11                                               ALC11 /
      *////////////////////////////////////////////////////////////
     C     ALC11         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     11            ZGN1
     C     KEYMS1        SETLL     EALC901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EALC901                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   Z-ADD     AREP0         ZREPC0
     C                   Z-ADD     AREP1         ZREPC1
     C                   EXSR      ALI11
     C     SVLK          IFEQ      *BLANKS
     C                   MOVEL     SILKA         SVLK
     C                   ENDIF
      *
     C     KEYMS1        READE     EALC901                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * ALI11                                               ALI11 /
      *////////////////////////////////////////////////////////////
     C     ALI11         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     11            ZGN2
     C     KEYMS2        SETLL     EALI901                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EALI901                                53
     C     *IN53         DOWEQ     '0'
      *
     C                   MOVEL     A3239         SVLK
      *
     C     KEYMS2        READE     EALI901                                53
     C  N53              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
