     A*%%TS  SD  20181128  103935  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFNRA*SEK SIG VERS  BESKRIVELSE...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXXA* 01 YBC PTF11 OPSJON 4 = SLETT OPPLASTET DOKUMENT
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      HELP
     A*
     A*
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20181128  093601  SVEIN       REL-V7R1M0  5770-WDS
     A*
     A            LOFOAN         1A  O  4  3
     A            LOMND          2Y 0O  4  5 EDTCDE(4)
     A            LOAAR          2Y 0O  4  8 EDTCDE(4)
     A            LODATO    R        O  4 12REFFLD(KOSTLR/LODATO *LIBL/KOSTL)
     A                                      EDTWRD('    -  -  ')
     A            LOKLOK    R        O  4 23REFFLD(KOSTLR/LOKLOK *LIBL/KOSTL)
     A                                      EDTWRD('  :  :  ')
     A            LOUSER    R        O  4 32REFFLD(KOSTLR/LOUSER *LIBL/KOSTL)
     A            LOJOUR    R        O  4 43REFFLD(KOSTLR/LOJOUR *LIBL/KOSTL)
     A            LOSG      R        O  4 51REFFLD(KOSTLR/LOSG *LIBL/KOSTL)
     A            LOTXT     R        O  4 55REFFLD(KOSTLR/LOTXT *LIBL/KOSTL)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20181128  094534  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  3'SYSPED/SYTR33L'
     A                                  1 18MSGCON(026 SHU1908 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 53MSGCON(012 SHU1909 *LIBL/SYMSGF)
     A            LOBILA    R        O  1 67REFFLD(KOSTLR/LOBILA *LIBL/KOSTL)
     A                                  1 76MSGCON(012 SHU2000 *LIBL/SYMSGF)
     A            KAFNR     R        O  1 89REFFLD(KOSTAR/KAFNR *LIBL/KOSTA)
     A                                  1125DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 53MSGCON(012 SHU2001 *LIBL/SYMSGF)
     A            KALNR     R        O  2 66REFFLD(KOSTAR/KALNR *LIBL/KOSTA)
     A                                  2 76MSGCON(012 SHU2002 *LIBL/SYMSGF)
     A            LNAVN         30A  O  2 89
     A  30                                  COLOR(RED)
     A                                  2125TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3126SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(060 SHU1907 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A*%%TS  SD  20181128  092455  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R NYWRK
     A*%%TS  SD  20181128  103935  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                  1  3'SYSPED/SYTR33L'
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(026 SHU1908 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 53MSGCON(012 SHU1909 *LIBL/SYMSGF)
     A            LOBILA    R        O  1 67REFFLD(KOSTLR/LOBILA *LIBL/KOSTL)
     A                                  1 76MSGCON(012 SHU2000 *LIBL/SYMSGF)
     A            KAFNR     R        O  1 89REFFLD(KOSTAR/KAFNR *LIBL/KOSTA)
     A                                  1125DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 53MSGCON(012 SHU2001 *LIBL/SYMSGF)
     A            KALNR     R        O  2 66REFFLD(KOSTAR/KALNR *LIBL/KOSTA)
     A                                  2 76MSGCON(012 SHU2002 *LIBL/SYMSGF)
     A            LNAVN         30A  O  2 89
     A                                  2125TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(060 SHU1907 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  3MSGCON(040 SHU2003 *LIBL/SYMSGF)
     A                                      COLOR(RED)
