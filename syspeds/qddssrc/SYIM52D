     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            HENAKF        30A  H
     A            WSNR           2A  B  6  2VALUES('5 ' '6 ' '9 ' '18' '  ')
     A            HEAVD     R        O  6  5REFFLD(SYIM50FR/HEAVD *LIBL/SYIM50F)
     A            HEOPD     R        O  6 10REFFLD(SYIM50FR/HEOPD *LIBL/SYIM50F)
     A            SISG      R        O  6 18REFFLD(SYIM50FR/SISG *LIBL/SYIM50F)
     A            HEOT      R        O  6 22REFFLD(SYIM50FR/HEOT *LIBL/SYIM50F)
     A            HEGN      R        O  6 25REFFLD(SYIM50FR/HEGN *LIBL/SYIM50F)
     A            KNAVN15       15A  O  6 41
     A            TVIDAF    R        O  6 57REFFLD(SYIM50FR/TVIDAF *LIBL/SYIM50-
     A                                      F)
     A            TVIDAS    R        O  6 66REFFLD(SYIM50FR/TVIDAS *LIBL/SYIM50-
     A                                      F)
     A            FRITXT         1A  O  6 75
     A            FRTTXT        70A  O  7  2COLOR(RED)
     A            TVI077         3   O  6 77
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYIM52'
     A                                      COLOR(BLU)
     A                                  1 17'Arbeide med ufortollet gods'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            TLUSER    R        O  2  2REFFLD(ARKLOGHR/EPUSER *LIBL/ARKLOG-
     A                                      H)
     A                                      COLOR(YLW)
     A            TLTEXT        30A  O  2 17
     A            TLDATE         8S 0O  2 50
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  5  2'Op Avd  Opd.nr  Sig Ot Godsnr     -
     A                                           Fakturamottaker Første T Siste-
     A                                        T S Mnr'
     A                                      COLOR(WHT)
     A                                  3  2'5=oppdrag   6=sad    9=tvinn log  -
     A                                      18=notater '
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF12(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A  30        MSGTXT        70A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYIM52'
     A                                      COLOR(BLU)
     A                                  1 17'Arbeide med ufortollet gods'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            TLUSER    R        O  2 17REFFLD(ARKLOGHR/EPUSER *LIBL/ARKLOG-
     A                                      H)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Avd  Opd   Sig Ot Godsnr       -
     A                                         Fakturamottaker      Første T Si-
     A                                      ste T  M '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
