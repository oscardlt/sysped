      *_________________________________
      * Format for markering av ny side.
      *"""""""""""""""""""""""""""""""""
     A          R KJØP1                     SPACEB(2)
     A  70                                65'** Continues **'
     A N70N79                             65MSGCON(15 DIV1673 SYMSGF)
     A  79                                65'* Fortsetzung *'
      *_____________________________________________
      * Format for hovedopplysninger vanlig faktura.
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R KJØP
     A            KFMT1         75      1  1
     A            KFMT2         75      1  1
     A  46        HOHT1         80      1  1
     A  46        HOHT2         80      2  1
     A  46        HOHT3         80      3  1
     A  46        HOHT4         80      4  1
     A  46        HOHT5         80      5  1
     A  46        HOHT6         80      6  1
     A  46        HOHT7         80      7  1
     A            YNAVN     R          11 11REFFLD(KNAVN CUNDF)
     A N88N51                          11 48'******************************'
     A N88N51                          11 48'******************************'
     A N88
     AA 51N70N79                       11 48MSGCON(30 DIV1674 SYMSGF)
     A N88
     AA 51N70N79                       11 48MSGCON(30 DIV1674 SYMSGF)
     A N88 51 70                       11 48'*** COPY  OF   ***************'
     A N88 51 70                       11 48'*** COPY  OF   ***************'
     A N88 51 79                       11 48'*** KOPI  VON  ***************'
     A N88 51 79                       11 48'*** KOPI  VON  ***************'
     A  70 88                          11 48'********  P A G E '
     A  70 88                          11 48'********  P A G E '
     A N70N79 88                       11 48MSGCON(24 DIV1676 SYMSGF)
     A N70N79 88                       11 48MSGCON(24 DIV1676 SYMSGF)
     A  79 88                          11 48'********  S E I T E'
     A  79 88                          11 48'********  S E I T E'
     A  88        XSIDE          2  0  11 71EDTCDE(Z)
     A  88                             11 75'***'
     A  88                             11 75'***'
     A            YADR1     R          12 11REFFLD(ADR1 CUNDF)
     A                                 12 48'*'
     A                                 12 48'*'
     A                                 12 77'*'
     A                                 12 77'*'
     A N70N79                          12 50MSGCON(26 DIV2368 SYMSGF)
     A N70N79                          12 50MSGCON(26 DIV2368 SYMSGF)
     A  70                             12 48'* FERRY  I N V O I C E '
     A  70                             12 48'* FERRY  I N V O I C E '
     A  79                             12 48'* FEHRE  R E C H N U N G '
     A  79                             12 48'* FEHRE  R E C H N U N G '
     A            YADR2     R          13 11REFFLD(ADR2 CUNDF)
     A N57                             13 48'******************************'
     A N57                             13 48'******************************'
     A            YADR3         35     14 11
     A N70N79                          14 48MSGCON(15 DIV1667 SYMSGF)
     A  70                             14 48'Invoice no....:'
     A  79                             14 48'Rechnung nr...:'
     A N96        XFN            7S 0  14 72EDTCDE(Z)
     A  96                             14 68'** TEST **'
     A            KLKNVN    R          15 11REFFLD(KLKNVN KODTLK)
     A N70N79                          15 48MSGCON(15 DIV1668 SYMSGF)
     A  70                             15 48'Customer no...:'
     A  79                             15 48'Kunden nr.....:'
     A            YKUNDN    R          15 70REFFLD(KUNDNR CUNDF)
     A                                      EDTCDE(Z)
     A N70N79                          16 48MSGCON(15 DIV1666 SYMSGF)
     A  70                             16 48'Date of invoice'
     A  79                             16 48'Rechn.datum...:'
     A            IDAGX          6S 0  16 70EDTCDE(Y)
     A N70N79                          17 48MSGCON(15 DIV1665 SYMSGF)
     A  70                             17 48'Date due......:'
     A  79                             17 48'Verfallstag...:'
     A            XFFDT          6S 0  17 70EDTCDE(Y)
     A N70N79                          18 48MSGCON(15 DIV2367 SYMSGF)
     A  70                             18 48'Inv. periode..:'
     A  79                             18 48'Rechn.periode.:'
     A            W1PERM         2  0  18 73
     A                                 18 75'/'
     A            W1PERÅ         2  0  18 76
     A*
E01  A N70N79                          20  3MSGCON(80 DIV2402 SYMSGF)
E01  A  70                             20  3'Roundtrip Dpt. Trip     Unit      +
     A                                       Ferry Company '
E01  A  79                             20  3'Rundreise      Reise    LKW nummer+
     A                                       Fehre Geselshaft '
E01  A N70N79                          21  3MSGCON(80 DIV2403 SYMSGF)
E01  A  70                             21  3'                        Text  (Fro+
     A                                      m  To    Date  Time                +
     A                                      Amount    '
E01  A  79                             21  3'                        Text  (Von+
     A                                       Nach   Datum  Zeit                +
     A                                      Kosten    '
     A                                 21  2'_______________________'
     A                                 21 25'________________________'
     A                                 21 49'________________________'
     A                                 21 73'________'
     A                                 22  1' '
      *_______________________________
      * Format for faktura-varelinjer.
      *"""""""""""""""""""""""""""""""
     A          R DET                       SPACEB(1)
E01  A N98        TURUND         8  0      3EDTCDE(Z)
E01  A N98        TUAVD          4  0     12EDTCDE(Z)
E01  A N98        TUPRO          8  0     17EDTCDE(Z)
E01  A            BUBILN        10        26
E01  A            LNA20         20        37
E01  A            KAFNR         13        58
     A          R DET2                      SPACEB(1)
E01  A            BUTXT         35        26
     A          R DET3                      SPACEB(1)
E01  A                                    26'Lengde:'
E01  A            FELENG         3        34
E01  A            FECURR         3        40
E01  A            XEPRI2        13  2     44EDTCDE(J)
E01  A            XVAL           3        62
S02  A*           XPFNOK        13  2     62EDTCDE(J)
N02  A            XPFNOKP       13  2     62EDTCDE(J)
     A          R DET4                      SPACEB(1)
     A            BUTXT         35        26
E01  A            XVAL           3        62
S02  A*           XPFNOK        13  2     62EDTCDE(J)
N02  A            XPFNOKP       13  2     62EDTCDE(J)
      *___________________________________
      * Format for faktura-total. IND: N35
      *"""""""""""""""""""""""""""""""""""
     A          R TOTB                      SKIPB(44)
     A N70N79                              2MSGCON(24 DIV1684 SYMSGF)
     A  70N79                              2'Terms of payment'
     A N70N79                             45MSGCON(12 DIV1685 SYMSGF)
     A  70                                45'Total:'
     A  79                                45'Gesamtbetrag'
E01  A            XVAL           3        62
E01  A            XBRUTT        13  2     62EDTCDE(J)
     A                                     2' '
     A                                      SPACEB(1)
     A    N73N79  BETTXT        30         2
     A                                    45'================================='
     A  44        HOBT1         80         1SPACEB(1)
     A  44        HOBT2         80         1SPACEB(1)
     A  44        HOBT3         80         1SPACEB(1)
      *_________________________________________________
      * Format for faktura-total. without Norwegian giro.
      *""""""""""""""""""""""""""""""""""""""""""""""""""
     A          R TOTC                      SPACEB(4)
     A N70N79                              2MSGCON(24 DIV1684 SYMSGF)
     A  70N79                              2'Terms of payment'
     A N70N79                             45MSGCON(12 DIV1685 SYMSGF)
     A  70                                45'Total:'
     A  79                                45'Gesamtbetrag'
E01  A            XVAL           3        62
E01  A            XBRUTT        13  2     62EDTCDE(J)
     A                                     2' '
     A                                      SPACEB(1)
     A N79        BETTXT        30         2
     A                                    45'================================'
     A  44        HOBT1         80         1SPACEB(1)
     A  44        HOBT2         80         1SPACEB(1)
     A  44        HOBT3         80         1SPACEB(1)
      *________________________________
      * Format for Ordinary giro Norway
      *""""""""""""""""""""""""""""""""
     A*** SKRIVER KIDLINJE PÅ SELVE FAKTURABLANKETT
     A          R KIDLIN
     A N60                                 1' '
S39  A*                                     SKIPB(62)
N39  A                                      SKIPB(56)
     A  60                                 1' '
S39  A*                                     SKIPB(86)
N39  A                                      SKIPB(76)
     A  61 63                              1'¤*c2330A¤*c60B¤*c11G¤*c2P'
     A  63                                40'¤(12U¤(s0p10h0s5b24579T'
     A            KIDTXT         4         4
     A                                       SPACEB(1)
     A                                      HIGHLIGHT
     A                                    30'TOTAL:'
     A                                      HIGHLIGHT
     A            XVAL           3        37HIGHLIGHT
     A            IBATXT         8        52
     A                                      HIGHLIGHT
     A            LIN20         20         4HIGHLIGHT
     A                                      SPACEB(2)
     A            XTOTKR        11  0     30EDTCDE(3)
     A                                      HIGHLIGHT
     A                                    41','
     A                                      HIGHLIGHT
     A            XTOTØR         2  0     42
     A                                      HIGHLIGHT
     A            GIRO20        20        52
     A                                      HIGHLIGHT
     A  63                                50'¤(12U¤(s0p10h1s0b4099T'
     A                                      SPACEB(1)
     A          R CVRPA2
     A*                                     BOX(2.7 0.5 4 7.0 0.01)
     A* 2.7 ER LINJE PÅ SIDEN FOR FØRSTE BOKS, 4 ER HØYDEN PÅ BOKSEN 7.0 ER
     A* BREDDEN
S39  A*                                     BOX(10  0.2 11.0 7.5 0.01)
N39  A                                      BOX(9.1  0.2 10.0 7.5 0.02)
     A* 10  ER LINJE PÅ SIDEN FOR ANDRE BOKS,  2.0 ER HØYDEN PÅ BOKSEN 8 ER
     A* BREDDEN  (FUNGERER OGSÅ MED 10.8 OG 7.0)
     A          R BGIR
     A N70N79                          50  7'Faktura nummer:'
     A  70                             50  7'Invoice number:'
     A  79                             50  7'Rechnung nr...:'
     A            XFN            7  0  50 29 EDTCDE(3)
     A N70N79                          51  7'Kundenummer...:'
     A  70                             51  7'Customer no...:'
     A  79                             51  7'Kunden nr.....:'
     A            YKUNDN    R          51 28 REFFLD(KUNDNR CUNDF)
     A                                       EDTCDE(3)
     A N70N79                          52  7'Faktura dato..:'
     A  70                             52  7'Invoice date..:'
     A  79                             52  7'Rechnung datum'
     A            IDAGX          6  0  52 28 EDTCDE(Y)
     A N70N79                          53  7'Forfallsdato..:'
     A  70                             53  7'Date due......:'
     A  79                             53  7'Verfallstag...:'
     A            XFFDT          6  0  53 28 EDTCDE(Y)
     A N70N79                          54  7MSGCON(15 DIV2367 SYMSGF)
     A  70                             54  7'Inv. periode..:'
     A  79                             54  7'Rechn.periode.:'
     A            W1PERM         2  0  54 31
     A                                 54 33'/'
     A            W1PERÅ         2  0  54 34
     A            YNAVN     R          58  7REFFLD(KNAVN CUNDF)
     A            XFNAVN        30     58 46
     A            YADR1     R          59  7REFFLD(ADR1 CUNDF)
     A            XFADR1        30     59 46
     A            YADR2     R          60  7REFFLD(ADR2 CUNDF)
     A            XFADR2        30     60 46
     A            YADR3         35     61  7
     A            XFPONR         4  0  61 46
     A            XFADR3        24     61 52
      *_________________________________________
      * Format for H-line giro, OCR    5224/5225
      *"""""""""""""""""""""""""""""""""""""""""
     A          R BGIR2A
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A                                 67  1'H'
     A            XWKID         19     67 11HIGHLIGHT
     A            XBAK           1  0  67 30HIGHLIGHT
     A            XTOTKR        11  0  67 31EDTCDE(3)
     A                                      HIGHLIGHT
     A                                 67 31'<'
     A            XTOTØR         2  0  67 46HIGHLIGHT
     A            XFBANK        15     67 52HIGHLIGHT
     A          R BGIR2D
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A                                 67  1'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  67 31EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  67 46HIGHLIGHT
     A            XFBANK        15     67 52HIGHLIGHT
      *___________________________________________
      * Format for H-line giro, OCR    4224(*IPDS)
      *"""""""""""""""""""""""""""""""""""""""""""
     A          R BGIR2B
     A                                      FONT(011)
     A                                 67  1'H'
     A                                      HIGHLIGHT
     A            XWKID         19     67 11HIGHLIGHT
     A                                      CHRID
     A            ABAK           1     67 30HIGHLIGHT
     A                                      CHRID
     A            ATOTKR        11     67 31HIGHLIGHT
     A                                      CHRID
     A                                 67 31'<'
     A                                      HIGHLIGHT
     A            ATOTØR         2     67 46HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     67 52HIGHLIGHT
     A                                      CHRID
     A          R BGIR2E
     A                                      FONT(011)
     A                                 67  1'H'
     A                                      HIGHLIGHT
     A            ATOTKR        11     67 31HIGHLIGHT
     A                                      CHRID
     A            ATOTØR         2     67 46HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     67 52HIGHLIGHT
     A                                      CHRID
      *_____________________________________________
      * Format for H-line giro, OCR    BELTESSKRIVER
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R BGIR2C
     A                                 67  1'H'
     A                                      HIGHLIGHT
     A            XWKID         19     67 11HIGHLIGHT
     A                                      CHRID
     A            XBAK           1  0  67 30HIGHLIGHT
     A            XTOTKR        11  0  67 31EDTCDE(3)
     A                                      HIGHLIGHT
     A                                 67 31'<'
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  67 46HIGHLIGHT
     A            XFBANK        15     67 52HIGHLIGHT
     A                                      CHRID
      *________________________________
      * Format for H-line giro, Not OCR
      *""""""""""""""""""""""""""""""""
     A          R BGIR3
     A                                 67  2'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  67 24EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  67 39HIGHLIGHT
     A            XFBANK        15     67 47HIGHLIGHT
