     A*%%TS  SD  20151116  092944  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA78PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FUEL SURCH-
     A                                      ARGE')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4')
     A            KFCFNR         3Y 0O  4  5TEXT('FLYSELSKAP')
     A                                      EDTCDE(4)
     A            KFCAVG         4Y 2O  4  9EDTCDE(4)
     A            KFCVKT         1A  O  4 16
     A            KFCIO1         1A  O  4 20
     A            KFCLK1         2A  O  4 23
     A            KFCLK2         2A  O  4 26
     A            KFCLK3         2A  O  4 29
     A            KFCLK4         2A  O  4 32
     A            KFCLK5         2A  O  4 35
     A            KFCFRA         8Y 0O  4 38TEXT('KUNDENR.')
     A                                      EDTCDE(4)
     A            KFCTIL         8Y 0O  4 47TEXT('KUNDENR.')
     A                                      EDTCDE(4)
     A            KFCSTA         1A  O  4 56
     A            KFCMIN         5Y 0O  4 58EDTCDE(4)
     A            KFCFTX        17A  O  4 67
     A            KFCOTH         2A  O  4 64
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20151116  092944  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA78'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A            MSG102        30A  O  2  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG103        30A  O  2 34MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2'Op Fnr  Avg. C/A I/O Landkoder    -
     A                                        Fra-Til  ≈≈≈≈MMDD F  Min.Kode fri-
     A                                      tekst        '
     A                                      DSPATR(HI)
     A                                  1 20'Vedlikehold av fuel surcharge'
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SÿK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG201        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 41MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A          R VARELI
     A*%%TS  SD  20151116  092944  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF04(04 'S¯k kunde')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSGNR          7A  P
     A                                  1  2'   Fnr  Avg. C/A I/O Landkoder    -
     A                                        Fra-Til  ≈≈≈≈MMDD F  Min.Kode fri-
     A                                      tekst        '
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A            WSFNR          3Y 0B  2  5TEXT('FLYSELSKAP')
     A                                      DSPATR(UL)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSAVG          4Y 2B  2  9EDTCDE(4)
     A                                      DSPATR(UL)
     A  40N30
     AO 89                                  DSPATR(PC)
     A            WSVKT          1A  B  2 16DSPATR(UL)
     A                                      VALUES('A' 'C')
     A            WSIO1          1A  B  2 20DSPATR(UL)
     A                                      VALUES(' ' 'I' 'O')
     A            WSLK1          2A  B  2 23DSPATR(UL)
     A            WSLK2          2A  B  2 26DSPATR(UL)
     A            WSLK3          2A  B  2 29DSPATR(UL)
     A            WSLK4          2A  B  2 32DSPATR(UL)
     A            WSLK5          2A  B  2 35DSPATR(UL)
     A            WSFRA          8Y 0B  2 38TEXT('KUNDENR.')
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSTIL          8Y 0B  2 47TEXT('KUNDENR.')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSSTA          1A  B  2 56
     A            WSMIN          5Y 0B  2 58EDTCDE(4)
     A            WSFTX         17A  B  2 67DSPATR(UL)
     A            WSOTH          2A  B  2 64DSPATR(UL)
     A            MSG302        18A  O  3  2MSGID(DIV 0032 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG303        18A  O  3 22MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG601        12A  O  2  2MSGID(DIV 0026 SYMSGF)
     A            W2NAV         30A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG602        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2'   Fnr  Avg. C/A I/O Landkoder    -
     A                                         Fra-Til (≈≈≈≈MMDD)  Min. Friteks-
     A                                      t'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A            KFCFNR         3Y 0O  2  5TEXT('FLYSELSKAP')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KFCAVG         4Y 2O  2  9EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCVKT         1A  O  2 16DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCIO1         1A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCLK1         2A  O  2 23DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCLK2         2A  O  2 26DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCFRA         8Y 0O  2 39TEXT('KUNDENR.')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            KFCTIL         8Y 0O  2 48TEXT('KUNDENR.')
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            KFCFTX        17A  O  2 67DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCOTH         2A  O  2 64DSPATR(UL)
     A                                      COLOR(WHT)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A            KFCLK3         2A  O  2 29DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCLK4         2A  O  2 32DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCLK5         2A  O  2 35DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFCMIN         5  0O  2 58DSPATR(HI)
     A                                      DSPATR(UL)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SÿK')
     A                                  1  2'SYSPED/SYFA78'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av fuel surcharge'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A            MSG502        30A  O  2  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG503        30A  O  2 34MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2'Op Fnr  Avg. C/A I/O Lk1 Lk2 Fra d-
     A                                      ato Til dato  Min. Fritekst'
     A                                      DSPATR(HI)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG509        18A  O 23 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG510        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SÿK')
     A                                  1  2'SYSPED/SYFA78'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av fuel surcharge'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG510        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2000  '
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(015 SHU1173 *LIBL/SYMSGF)
     A            WSUNIK         2A  B  8 19
     A                                  8 23MSGCON(040 SHU1188 *LIBL/SYMSGF)
