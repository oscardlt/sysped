      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDIG000R MODULE(*LIBL/TDIG000R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 XXX PTF12 XXXXXXXXXXXXXXXXXXXXXXXX
12XXX * 02 XXX PTF12 XXXXXXXXXXXXXXXXXXXXXXXX
12XXX * 03 XXX PTF12 XXXXXXXXXXXXXXXXXXXXXXXX
12356 * 04 CB  PTF12 Send med ekspedisjonsenhet i svaret
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FEDIM2     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FSADMOHF   IF   E             DISK    USROPN
     FSADH      IF   E             DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D DATO            S              8
     D DATON           S              8  0
     D BIL             S             17
     D WSITLE          S              6
     D WEHRG           S             11
     D WEH0068A        S              8  0
     D WEH0068B        S              6  0
     D WEH3039E        S              6  0
     D WSIAVD          S              4S 0
     D WSITDN          S              7S 0
     D WSIKNK          S              8S 0
     D WSIKNS          S              8S 0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d X               S             10  0
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
N02  d wfssok_CR       s                   like(fssok)
N02  d wfssokMRN       s                   like(fssok)
N03  d wfssokMRN2      s                   like(fssok)
N03  d wfssokMRN3      s                   like(fssok)
N03  d wfssokMRN4      s                   like(fssok)
N03  d wfssokMRN5      s                   like(fssok)
N03  d wfssokMRN6      s                   like(fssok)
N03  d wfssokMRN7      s                   like(fssok)
N03  d wfssokMRN8      s                   like(fssok)
N03  d wfssokMRN9      s                   like(fssok)
N02  d wfssokEXP       s                   like(fssok)
N03  d wfssokEXP2      s                   like(fssok)
N03  d wfssokEXP3      s                   like(fssok)
N03  d wfssokEXP4      s                   like(fssok)
N03  d wfssokEXP5      s                   like(fssok)
N03  d wfssokEXP6      s                   like(fssok)
N03  d wfssokEXP7      s                   like(fssok)
N03  d wfssokEXP8      s                   like(fssok)
N03  d wfssokEXP9      s                   like(fssok)
     d avda            S              4
     d avdn            S              4  0
     d tdna            S              7
     d tdnn            S              7  0
     d MyPoDS          ds                  Qualified  dim(1500)
     d  siavd                         4S 0
     d  sitdn                         7S 0
     d  sisg                          3
     d  sidt                          8S 0
     d  sikns                         8S 0
     d  sinas                        30
     d  siads1                       30
     d  siads2                       30
     d  siads3                       30
     d  sintk                         7P 0
     d  sivkb                         9P 0
     d  sikdc                         1
     d  siknk                         8S 0
     d  sirg                          1
     d  sinak                        30
     d  siadk1                       30
     d  siadk2                       30
     d  siadk3                       30
     d  silka                         2
     d  sitlf                        12
     d  sinad                        30
     d  silv                          3
     d  silvt                        17
     d  sign                         15
     d  sitarf                       16
     d  sitrid                       17
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬bil¬ : ¬'+%trim(BIL)+'¬, '
        +'¬dato¬ : ¬'+%trim(%editc(DATON:'Z'))+'¬, '
        +'¬sitle¬ : ¬'+%trim(WSITLE)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬tdn¬ : ¬'+%trim(%editc(TDNN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"list" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

         DATO = %editc(DATON:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' SIDT  >= ' + DATO;

       If    BIL <> *blanks;
         Exsr  AddAnd;
           Eval  WhereC =  %trim(WhereC)  +
           ' UPPER(SITRID) LIKE UPPER(' + cQuote + '%' +
           %trim(BIL) + '%' + cQuote + ')';
       EndIf;

       If    AVDN > 0;
         AVDA = %editc(AVDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' SIAVD  = ' + AVDA;
       EndIf;

       If    TDNN > 0;
         TDNA = %editc(TDNN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' SITDN  = ' + TDNA;
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);
         Eval       SqlStm = 'Select SIAVD, SITDN, SISG, SIDT, SIKNS, ' +
                                    'SINAS, SIADS1, SIADS2, SIADS3, SINTK, ' +
                                    'SIVKB, SIKDC, SIKNK, SIRG, SINAK, ' +
                                    'SIADK1, SIADK2, SIADK3, SILKA, SITLF, ' +
                                    'SINAD, SILV, SILVT, SIGN, SITARF, ' +
                                    'SITRID ' +
                             'from SADH ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'SIDT desc ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
         Exec SQL CLOSE A33;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         bil     = zhbgetvar('BIL');
         dato    = zhbgetvar('DATO');
         daton   = c2n2(DATO);
         wsitle  = zhbgetvar('SITLE');
         avda    = zhbgetvar('AVD');
         avdn    = c2n2(avda);
         tdna = zhbgetvar('TDN');
         tdnn = c2n2(tdna);
        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).sisg <> *blanks;

             wsiavd=mypods(x).SIAVD;
             wsitdn=mypods(x).SITDN;
             Exsr  Lesedim;
             Exsr  Lesedim2;
             fssok=*blanks;
             wfssok_CR=*blanks;
             wfssokMRN=*blanks;
N03          wfssokMRN2=*blanks;
N03          wfssokMRN3=*blanks;
N03          wfssokMRN4=*blanks;
N03          wfssokMRN5=*blanks;
N03          wfssokMRN6=*blanks;
N03          wfssokMRN7=*blanks;
N03          wfssokMRN8=*blanks;
N03          wfssokMRN9=*blanks;
             wfssokEXP=*blanks;
N03          wfssokEXP2=*blanks;
N03          wfssokEXP3=*blanks;
N03          wfssokEXP4=*blanks;
N03          wfssokEXP5=*blanks;
N03          wfssokEXP6=*blanks;
N03          wfssokEXP7=*blanks;
N03          wfssokEXP8=*blanks;
N03          wfssokEXP9=*blanks;
N03          Exsr  LesMRN;
N03          Exsr  Lesexp;
             chain (mypods(x).SIAVD: mypods(x).SITDN: '*CR') FRISOK;
               if %found;
                 wfssok_CR = fssok;
               endif;
             wsiknk=mypods(x).SIKNK;
             wsikns=mypods(x).SIKNS;
             exsr getcundf;

           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;

       JSONstring=%trim(JSONstring)+'{'
          +'¬siavd¬ : ¬'+%trim(%editc(mypods(x).SIAVD:'Z'))+'¬, '
          +'¬sitdn¬ : ¬'+%trim(%editc(mypods(x).SITDN:'Z'))+'¬, '
          +'¬sisg¬ : ¬'+%trim(mypods(x).SISG)+'¬, '
          +'¬sidt¬ : ¬'+%trim(%editc(mypods(x).SIDT:'Z'))+'¬, '
          +'¬sikns¬ : ¬'+%trim(%editc(mypods(x).SIKNS:'Z'))+'¬, '
          +'¬sinas¬ : ¬'+%trim(mypods(x).SINAS)+'¬, '
          +'¬siads1¬ : ¬'+%trim(mypods(x).SIADS1)+'¬, '
          +'¬siads2¬ : ¬'+%trim(mypods(x).SIADS2)+'¬, '
          +'¬siads3¬ : ¬'+%trim(mypods(x).SIADS3)+'¬, '
          +'¬sintk¬ : ¬'+%trim(%editc(mypods(x).SINTK:'Z'))+'¬, '
          +'¬sivkb¬ : ¬'+%trim(%editc(mypods(x).SIVKB:'Z'))+'¬, '
          +'¬sikdc¬ : ¬'+%trim(mypods(x).SIKDC)+'¬, '
          +'¬siknk¬ : ¬'+%trim(%editc(mypods(x).SIKNK:'Z'))+'¬, '
          +'¬sirg¬ : ¬'+%trim(mypods(x).SIRG)+'¬, '
          +'¬sinak¬ : ¬'+%trim(mypods(x).SINAK)+'¬, '
          +'¬siadk1¬ : ¬'+%trim(mypods(x).SIADK1)+'¬, '
          +'¬siadk2¬ : ¬'+%trim(mypods(x).SIADK2)+'¬, '
          +'¬siadk3¬ : ¬'+%trim(mypods(x).SIADK3)+'¬, '
          +'¬silka¬ : ¬'+%trim(mypods(x).SILKA)+'¬, '
          +'¬sitlf¬ : ¬'+%trim(mypods(x).SITLF)+'¬, '
          +'¬sinad¬ : ¬'+%trim(mypods(x).SINAD)+'¬, '
          +'¬silv¬ : ¬'+%trim(mypods(x).SILV)+'¬, '
          +'¬silvt¬ : ¬'+%trim(mypods(x).SILVT)+'¬, '
          +'¬sign¬ : ¬'+%trim(mypods(x).SIGN)+'¬, '
          +'¬sitrid¬ : ¬'+%trim(mypods(x).SITRID)+'¬, '
          +'¬sitle¬ : ¬'+%trim(%editc(WEH3039E:'Z'))+'¬, '
          +'¬wehrg¬ : ¬'+%trim(WEHRG)+'¬, '
          +'¬weh0068a¬ : ¬'+%trim(%editc(WEH0068A:'Z'))+'¬, '
          +'¬weh0068b¬ : ¬'+%trim(%editc(WEH0068B:'Z'))+'¬, '
          +'¬ehrgs¬ : ¬'+%trim(EHRGS)+'¬, '
          +'¬ehnas¬ : ¬'+%trim(EHNAS)+'¬, '
          +'¬ehad1s¬ : ¬'+%trim(EHAD1S)+'¬, '
          +'¬ehpns¬ : ¬'+%trim(EHPNS)+'¬, '
          +'¬ehpss¬ : ¬'+%trim(EHPSS)+'¬, '
          +'¬ehlks¬ : ¬'+%trim(EHLKS)+'¬, '
          +'¬ehems¬ : ¬'+%trim(EHEMS)+'¬, '
          +'¬ehemst¬ : ¬'+%trim(EHEMST)+'¬, '
          +'¬ehrgm¬ : ¬'+%trim(EHRGM)+'¬, '
          +'¬ehnam¬ : ¬'+%trim(EHNAM)+'¬, '
          +'¬ehad1m¬ : ¬'+%trim(EHAD1M)+'¬, '
          +'¬ehpnm¬ : ¬'+%trim(EHPNM)+'¬, '
          +'¬ehpsm¬ : ¬'+%trim(EHPSM)+'¬, '
          +'¬ehlkm¬ : ¬'+%trim(EHLKM)+'¬, '
          +'¬ehemm¬ : ¬'+%trim(EHEMM)+'¬, '
          +'¬ehemmt¬ : ¬'+%trim(EHEMMT)+'¬, '
          +'¬wfssok_cr¬ : ¬'+%trim(WFSSOK_CR)+'¬, '
          +'¬wfssokmrn¬ : ¬'+%trim(WFSSOKMRN)+'¬, '
N03       +'¬wfssokmrn2¬ : ¬'+%trim(WFSSOKMRN2)+'¬, '
N03       +'¬wfssokmrn3¬ : ¬'+%trim(WFSSOKMRN3)+'¬, '
N03       +'¬wfssokmrn4¬ : ¬'+%trim(WFSSOKMRN4)+'¬, '
N03       +'¬wfssokmrn5¬ : ¬'+%trim(WFSSOKMRN5)+'¬, '
N03       +'¬wfssokmrn6¬ : ¬'+%trim(WFSSOKMRN6)+'¬, '
N03       +'¬wfssokmrn7¬ : ¬'+%trim(WFSSOKMRN7)+'¬, '
N03       +'¬wfssokmrn8¬ : ¬'+%trim(WFSSOKMRN8)+'¬, '
N03       +'¬wfssokmrn9¬ : ¬'+%trim(WFSSOKMRN9)+'¬, '
          +'¬wfssokexp¬ : ¬'+%trim(WFSSOKEXP)+'¬, '
N03       +'¬wfssokexp2¬ : ¬'+%trim(WFSSOKEXP2)+'¬, '
N03       +'¬wfssokexp3¬ : ¬'+%trim(WFSSOKEXP3)+'¬, '
N03       +'¬wfssokexp4¬ : ¬'+%trim(WFSSOKEXP4)+'¬, '
N03       +'¬wfssokexp5¬ : ¬'+%trim(WFSSOKEXP5)+'¬, '
N03       +'¬wfssokexp6¬ : ¬'+%trim(WFSSOKEXP6)+'¬, '
N03       +'¬wfssokexp7¬ : ¬'+%trim(WFSSOKEXP7)+'¬, '
N03       +'¬wfssokexp8¬ : ¬'+%trim(WFSSOKEXP8)+'¬, '
N03       +'¬wfssokexp9¬ : ¬'+%trim(WFSSOKEXP9)+'¬, '
          +'¬sitarf¬ : ¬'+%trim(mypods(x).SITARF)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

           endif;
           endif;
        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
         open frisok;
         open brparf;
         open edim2;
         open cundl01;
         open sadmohf;
         open sadh;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close frisok;
         close brparf;
         close edim2;
         close cundl01;
         close sadmohf;
         close sadh;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
      *________________________________________
      * Hent dato og sekvensnr fra meldingslogg
      *"""""""""""""""""""""""""""""""""""""""""
     C     LESEDIM       BEGSR
     C     EDIM2KEY      KLIST
     C                   KFLD                    WSIAVD
     C                   KFLD                    WSITDN
     C                   EVAL      *IN33 = *OFF
     C                   EVAL      WEHRG = *BLANKS
     C                   EVAL      WEH0068A = *ZEROS
     C                   EVAL      WEH0068B = *ZEROS
     C     EDIM2KEY      SETGT     EDIM2
     C     EDIM2KEY      READPE    EDIM2                                  33
     C                   DOW       *IN33 = *OFF
     C                   IF        M1001 = '929'
     C                   EVAL      WEHRG = %SUBST(M0068:1:9)
     C                   EVAL      WEH0068A = M0068A
     C                   EVAL      WEH0068B = M0068B
     C                   LEAVE
     C                   END
     C     EDIM2KEY      READPE    EDIM2                                  33
     C                   ENDDO
     C                   ENDSR
      *________________________________________
      * Hent ekspedisjonsenhet fra meldingslogg
      *"""""""""""""""""""""""""""""""""""""""""
     C     LESEDIM2      BEGSR
     C                   EVAL      ERROR=' '
     C                   EVAL      *IN33 = *OFF
     C                   EVAL      WEH3039E = *ZEROS
     C     EDIM2KEY      SETLL     EDIM2
     C     EDIM2KEY      READE     EDIM2                                  33
     C                   DOW       *IN33 = *OFF
     C                   IF        MSR = 'S'
     C                   IF        M1001 = '929'
     C                   EVAL      WEH3039E = M3039E
     C                   LEAVE
     C                   ENDIF
     C                   ENDIF
     C     EDIM2KEY      READE     EDIM2                                  33
     C                   ENDDO
      * Om Oscar har skickat med ett värde i ekspedisjonsenhet så testa det.
     C     WSITLE        IFNE      *BLANKS
     C                   MOVE      M3039E        WM3039E           6
     C     WSITLE        IFNE      WM3039E
     C                   EVAL      ERROR='X'
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
N03   *____________________________
N03   * Hent EXP fra frie søkeveier
N03   *""""""""""""""""""""""""""""
N03  C     LESEXP        BEGSR
N03  C     FRIKEY        KLIST
N03  C                   KFLD                    WSIAVD
N03  C                   KFLD                    WSITDN
N03  C                   KFLD                    WFSKODE
N03  C     *LIKE         DEFINE    FSKODE        WFSKODE
N03  C                   EVAL      WFSKODE='EXP'
N03
N03  C                   EVAL      *IN33 = *OFF
N03  C     FRIKEY        SETLL     FRISOK
N03  C     FRIKEY        READE     FRISOK                                 33
N03  C                   DOW       *IN33 = *OFF
N03  C                   SELECT
N03  C                   WHEN      WFSSOKEXP=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP
N03  C                   WHEN      WFSSOKEXP2=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP2
N03  C                   WHEN      WFSSOKEXP3=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP3
N03  C                   WHEN      WFSSOKEXP4=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP4
N03  C                   WHEN      WFSSOKEXP5=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP5
N03  C                   WHEN      WFSSOKEXP6=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP6
N03  C                   WHEN      WFSSOKEXP7=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP7
N03  C                   WHEN      WFSSOKEXP8=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP8
N03  C                   WHEN      WFSSOKEXP9=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKEXP9
N03  C                   ENDSL
N03  C     FRIKEY        READE     FRISOK                                 33
N03  C                   ENDDO
N03  C                   ENDSR
N03   *____________________________
N03   * Hent MRN fra frie søkeveier
N03   *""""""""""""""""""""""""""""
N03  C     LESMRN        BEGSR
N03  C                   EVAL      WFSKODE='MRN'
N03
N03  C                   EVAL      *IN33 = *OFF
N03  C     FRIKEY        SETLL     FRISOK
N03  C     FRIKEY        READE     FRISOK                                 33
N03  C                   DOW       *IN33 = *OFF
N03  C                   SELECT
N03  C                   WHEN      WFSSOKMRN=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN
N03  C                   WHEN      WFSSOKMRN2=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN2
N03  C                   WHEN      WFSSOKMRN3=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN3
N03  C                   WHEN      WFSSOKMRN4=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN4
N03  C                   WHEN      WFSSOKMRN5=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN5
N03  C                   WHEN      WFSSOKMRN6=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN6
N03  C                   WHEN      WFSSOKMRN7=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN7
N03  C                   WHEN      WFSSOKMRN8=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN8
N03  C                   WHEN      WFSSOKMRN9=*BLANKS
N03  C                   MOVEL     FSSOK         WFSSOKMRN9
N03  C                   ENDSL
N03  C     FRIKEY        READE     FRISOK                                 33
N03  C                   ENDDO
N03  C                   ENDSR
      *_________________________________________________
      * Hent avs/mot kundeopplysninger fra kundregistret
      *"""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETCUNDF      BEGSR
     C     KEYCUNDS      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WSIKNS
     C     KEYCUNDM      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WSIKNK
     C                   EVAL      EHRGS = *BLANKS
     C                   EVAL      EHNAS = *BLANKS
     C                   EVAL      EHAD1S = *BLANKS
     C                   EVAL      EHPNS = *BLANKS
     C                   EVAL      EHPSS = *BLANKS
     C                   EVAL      EHLKS = *BLANKS
     C                   EVAL      EHEMS = *BLANKS
     C                   EVAL      EHEMST = *BLANKS
     C                   EVAL      EHRGM = *BLANKS
     C                   EVAL      EHNAM = *BLANKS
     C                   EVAL      EHAD1M = *BLANKS
     C                   EVAL      EHPNM = *BLANKS
     C                   EVAL      EHPSM = *BLANKS
     C                   EVAL      EHLKM = *BLANKS
     C                   EVAL      EHEMM = *BLANKS
     C                   EVAL      EHEMMT = *BLANKS
     C     KEYCUNDS      CHAIN     CUNDL01                            33
     C                   IF        *IN33 = *OFF
     C                   EVAL      EHRGS = EORI
     C                   EVAL      EHNAS = KNAVN
     C                   EVAL      EHAD1S = ADR1
     C                   EVAL      EHPNS = SYPOGE
     C                   EVAL      EHPSS = ADR3
     C                   EVAL      EHLKS = SYLAND
     C                   EVAL      EHEMS = SYEPOS
     C                   IF        SYEPOS <> *BLANKS
     C                   EVAL      EHEMST = 'EM'
     C                   ENDIF
     C                   ENDIF
     C     KEYCUNDM      CHAIN     CUNDL01                            33
     C                   IF        *IN33 = *OFF
     C                   EVAL      EHRGM = SYRG
     C                   EVAL      EHNAM = KNAVN
     C                   EVAL      EHAD1M = ADR1
     C                   MOVEL     POSTNR        EHPNM
     C                   EVAL      EHPSM = ADR3
     C                   EVAL      EHLKM = SYLAND
     C                   EVAL      EHEMM = SYEPOS
     C                   IF        SYEPOS <> *BLANKS
     C                   EVAL      EHEMMT = 'EM'
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
