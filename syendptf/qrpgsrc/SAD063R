********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12053 * 01 YBC PTF12 FLERE FELTER ER UTVIDET.
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL ENDRE/SLETT SÆRAVGIFTER I K.VAREREG.   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 05 06 09 11 16 18-22 25 56
      *** LEDIGE INDIKATORER 27 34 37-60 63-67 74 75          ***
      *** ALLE VARIABLER SOM STARTER MED 'X' ER ARBERDSVAR.   ***
      ***   OG DEFINERES I PROGRAM                            ***
      ***********************************************************
      *
     FSADVARE   UF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTS8    IF   E           K DISK
     FSAD063D   CF   E             WORKSTN
     D                 DS
     D  W2AKD1                 1      2
     D  W2AKD2                 3      4
     D  W2AKD3                 5      6
     D  W2AKD4                 7      8
     D  W2AKD5                 9     10
     D  W2AKD6                11     12
     D  W2AKD7                13     14
     D  W2AKD8                15     16
     D  AKD                    1     16    DIM(8)
     D                 DS
     D  W2ASV1                 1      3
     D  W2ASV2                 4      6
     D  W2ASV3                 7      9
     D  W2ASV4                10     12
     D  W2ASV5                13     15
     D  W2ASV6                16     18
     D  W2ASV7                19     21
     D  W2ASV8                22     24
     D  ASV                    1     24    DIM(8)
     D                 DS
     D  W2ASA1                 1      7  2
     D  W2ASA2                 8     14  2
     D  W2ASA3                15     21  2
     D  W2ASA4                22     28  2
     D  W2ASA5                29     35  2
     D  W2ASA6                36     42  2
     D  W2ASA7                43     49  2
     D  W2ASA8                50     56  2
     D  ASA                    1     56  2 DIM(8)
     D                 DS
E01  D  W2AGR1                 1     13  2
E01  D  W2AGR2                14     26  2
E01  D  W2AGR3                27     39  2
E01  D  W2AGR4                40     52  2
E01  D  W2AGR5                53     65  2
E01  D  W2AGR6                66     78  2
E01  D  W2AGR7                79     91  2
E01  D  W2AGR8                92    104  2
E01  D  AGR                    1    104  2 DIM(8)
     D                 DS
E01  D  W2ABL1                 1     13  2
E01  D  W2ABL2                14     26  2
E01  D  W2ABL3                27     39  2
E01  D  W2ABL4                40     52  2
E01  D  W2ABL5                53     65  2
E01  D  W2ABL6                66     78  2
E01  D  W2ABL7                79     91  2
E01  D  W2ABL8                92    104  2
E01  D  ABL                    1    104  2 DIM(8)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   EXFMT     BILDE
     C                   MOVEA     '0000'        *IN(96)
     C                   MOVE      '0'           *IN30
      *
     C   03              GOTO      SLUTT
      *
     C                   EXSR      TESTB1
     C   30              GOTO      START
      *
     C                   EXSR      CHGAVG
      *
     C                   IF        WSKN <> 99999999
     C                   EVAL      WSKN = 0
     C                   EVAL      *IN30 = *ON
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      'YBC1984'     MSGNR
     C                   GOTO      START
     C                   END
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
      *** TEST KUNDENR
     C                   IF        WSKN <> 99999999
     C     CUNDL01K      CHAIN     CUNDL01                            99
     C                   IF        *IN99
     C                   EVAL      *IN30 = *ON
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      'SPK0001'     MSGNR
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *** TEST AVGIFTKODE
     C     WSKDA         CHAIN     KODTS8                             98
     C                   IF        *IN98
     C                   EVAL      *IN30 = *ON
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      'YBC1982'     MSGNR
     C                   GOTO      SLTB1
     C                   END
      *** TEST SEKVENSNR
     C                   IF        WSKDS <> '*'
     C     KODTS8K       CHAIN     KODTS8                             97
     C                   IF        *IN97
     C                   EVAL      *IN30 = *ON
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      'YBC1983'     MSGNR
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *** TEST SATS
     C                   IF        WSSATS1 = 0 AND WSSATS2 <> 0
     C                   EVAL      *IN96 = *ON
     C                   EVAL      *IN30 = *ON
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      'YBC1985'     MSGNR
     C                   GOTO      SLTB1
     C                   END
      *
     C     SLTB1         ENDSR
      *
      ***********************************************
     C     CHGAVG        BEGSR
      ***********************************************
     C                   IF        WSKN = 99999999
     C     *LOVAL        SETLL     SADVARE
     C                   READ      SADVARE                                33
     C                   ELSE
     C     WSKN          SETLL     SADVARE
     C     WSKN          READE     SADVARE                                33
     C                   END
      *
     C                   DOW       *IN33 = *OFF                                 1<-B
      *
     C                   MOVE      'N'           XFJERNET          1
     C     1             DO        8             XN                3 0           2<-B
     C                   IF        WSKDA = AKD(XN)                                3<-B
     C                   IF        WSKDS = '*'                                     4<-B
     C                   EXSR      OPPDAT
     C                   ELSE                                                      4
     C                   IF        WSKDS = ASV(XN)                                  5<-B
     C                   EXSR      OPPDAT
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   ENDDO                                                   2<-E
      *
     C                   IF        XFJERNET = 'J'
     C                   EXSR      ORDNERD
     C                   END
      *
     C                   UPDATE    SADVARER
     C                   IF        WSKN = 99999999
     C                   READ      SADVARE                                33
     C                   ELSE
     C     WSKN          READE     SADVARE                                33
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPPDAT        BEGSR
      ***********************************************
     C                   IF        (WSSATS1 <> 0 AND WSSATS1 = ASA(XN)) OR
     C                             WSSATS1 = 0
     C                   IF        WSSLET  = 'J'
     C                   EVAL      AKD(XN) = *BLANKS
     C                   EVAL      ASV(XN) = *BLANKS
     C                   EVAL      ASA(XN) = 0
     C                   MOVE      'J'           XFJERNET          1
     C                   ELSE
     C                   EVAL      ASA(XN) = WSSATS2
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     ORDNERD       BEGSR
      ***********************************************
     C     1             DO        7             XN
     C                   IF        AKD(XN) = *BLANKS
     C     XN            ADD       1             XN2               3 0
     C                   EVAL      AKD(XN) = AKD(XN2)
     C                   EVAL      ASV(XN) = ASV(XN2)
     C                   EVAL      ASA(XN) = ASA(XN2)
     C                   EVAL      AGR(XN) = AGR(XN2)
     C                   EVAL      ABL(XN) = ABL(XN2)
     C                   EVAL      AKD(XN2) = *BLANKS
     C                   EVAL      ASV(XN2) = *BLANKS
     C                   EVAL      ASA(XN2) = 0
     C                   EVAL      AGR(XN2) = 0
     C                   EVAL      ABL(XN2) = 0
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
     C     KODTS8K       KLIST
     C                   KFLD                    WSKDA
     C                   KFLD                    WSKDS
      *
     C                   EVAL      WSSLET = 'N'
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
