      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 19 80 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  5  2EDTCDE(4)
     A            CSLI           2Y 0O  5  5TEXT('Linjenr')
     A                                      EDTCDE(4)
     A            CSST           1A  O  5  8TEXT('Status')
     A            CSRG          11A  O  5 11TEXT('Org.nr')
     A            CS0068A        8Y 0O  5 23TEXT('Sendingsdato')
     A                                      EDTCDE(4)
     A            CS0068B        6Y 0O  5 32TEXT('Sekvens')
     A                                      EDTCDE(4)
     A            CSPR           2A  O  5 39TEXT('Prosedyre')
     A            CSETYP         2A  O  5 42TEXT('Type')
     A            CSETYPT       30A  H
     A            CSEID         18A  O  5 45TEXT('Eksport Id')
     A            CSNTK          7Y 0O  5 64TEXT('Kolli')
     A                                      EDTCDE(4)
     A            CSVKB          9Y 0O  5 72TEXT('Vekt')
     A                                      EDTCDE(4)
     A            CSESER         1A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Plukk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD132RB'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold E.F. manifest - Ordre'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2'Avd.:'
     A            UAVD           4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Opdnr:'
     A            UTDN           7Y 0O  2 20TEXT('Opdnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(M)
     A  49                              3  2'Velg   5=Vis'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg   2=Endre   4=Slett   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Li St Org.nr      Send.DT Sekve-
     A                                      ns Pr Ty EksportId'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                 18  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             18 42'F6=Lag ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Plukk')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD132RB'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold E.F. manifest - Ordre'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2'Avd.:'
     A            UAVD           4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Opdnr:'
     A            UTDN           7Y 0O  2 20TEXT('Opdnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(M)
     A  49                              3  2'Velg   5=Vis'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg   2=Endre   4=Slett   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Li St Org.nr      Send.DT Sekve-
     A                                      ns Pr Ty EksportId'
     A                                      COLOR(WHT)
     A                                 18  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             18 42'F6=Lag ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
