********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 IMPORT VISER MOTTAKER, EKSPORT VISER AVSENDER
********************************************************************
     A*****************************************************************
     A*           PURRING AV UFAKTURERTE OPPDRAG                      *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=120, OVERFLOW=72      *
     A*****************************************************************
     A          R HEADER
     A            KFMT1         75      1  1
     A            KFMT2         75      2  1
     A  88                              2 90'¤&l'
     A  88        COPY           2  0   2 93
     A  88                              2 95'X'
     A  88                              2 96'¤&a-3R'
     A  88 89                           2102'¤&l1O'
     A  88N89                           3  1'¤(12U¤(s0p15h0s3b4099T'
     A  88 89                           3  1'¤(12U¤(s0p12h0s3b4099T'
     A  88                              4  1'¤*c3000A¤*c220B¤*c11G¤*c2P'
     A                                  5 07MSGCON(70 DIV0534 SYMSGF)
     A                                  5 67'T.O.M'
     A            WUDTD          2  0O  5 74
     A                                  5 76'.'
     A            WUDTM          2  0O  5 77
     A                                  5 79'.'
     A            WUDTÅ          2  0O  5 80
N16  A                                  5 85MSGCON(16 SHU1632 SYMSGF)
N16  A            UPEMMF         2  0O  5101
N16  A            UPEAAF         2  0O  5104
N16  A                                  5106'-'
N16  A            UPEMMT         2  0O  5107
N16  A            UPEAAT         2  0O  5110
     A                                  6 31MSGCON(12 DIV0535 SYMSGF)
     A            HESG           3A  O  6 44
     A                                  6 81MSGCON(12 DIV0536 SYMSGF)
     A  40        HEAVD          4  0O  6 94EDTCDE(4)
     A                                  6104MSGCON(5 DIV0062 SYMSGF)
     A            UTDT           6  0O  6110EDTCDE(Y)
     A                                  8  1MSGCON(120 DIV0537 SYMSGF)
     A N88                              9  1'------------------------'
     A N88                              9 25'------------------------'
     A N88                              9 49'------------------------'
     A N88                              9 73'------------------------'
     A N88                              9 97'------------------------'
     A  88N89                          10  1'¤(12U¤(s0p15h0s0b4099T'
     A  88 89                          10  1'¤(12U¤(s0p12h0s0b4099T'
     A          R DETALJ                    SPACEB(1)
     A            HEAVD          4  0O     2EDTCDE(4)
     A            HEOPD          7  0O     7EDTCDE(4)
     A            HEST           1A  O    15
     A            HEOT           2A  O    19
     A            HEFR           3A  O    22
     A            HEGN          15A  O    26
     A            HEHAWB         9A  O    42
S01  A*           HENAK         30A  O    52
N01  A            XKNV          30A  O    52
S16  A*           HENT           7  0O    82EDTCDE(4)
N16  A            HENT5          5  0O    82EDTCDE(4)
S16  A*           HEVKT          9  0O    90EDTCDE(4)
N16  A            HEVKT5         5  0O    88EDTCDE(4)
S16  A*           UDT13          6  0O   100
N16  A            UDT13          6  0O    94
N16  A            TSTMND         2  0O   101
N16  A            TSTAAR         2  0O   104
     A            XBL            9  0O   109EDTCDE(J)
N17   * KOSTNADSLINJER.
N17  A          R DETALJK                   SPACEB(1)
N17  A            KTYPE          5A  O    15
N17  A            BILNR          7  0O    21EDTCDE(Z)
N17  A            LNAVN         30A  O    29
N17  A            BILTXT        35A  O    60
N17  A            KVK            3A  O    97
N17  A            KVAL           3A  O   102
N17  A            KBL            9  0O   106EDTCDE(J)
     A          R TOTAL                     SPACEB(1)
     A  88N89                              1'¤(12U¤(s0p15h0s3b4099T'
     A  88 89                              1'¤(12U¤(s0p12h0s3b4099T'
     A                                     2MSGCON(18 DIV0538 SYMSGF)
     A            XNTOPD         5  0O    21EDTCDE(4)
     A                                    32MSGCON(18 DIV0539 SYMSGF)
     A            XBLOPD        11  2O    51EDTCDE(J)
     A          R GRAND                     SPACEB(1)
     A  88N89                              1'¤(12U¤(s0p15h0s3b4099T'
     A  88 89                              1'¤(12U¤(s0p12h0s3b4099T'
     A                                     2MSGCON(18 DIV0540 SYMSGF)
     A            XNTTOT         5  0O    21EDTCDE(4)
     A                                    32MSGCON(18 DIV0541 SYMSGF)
     A            XBLTOT        11  2O    51EDTCDE(J)
