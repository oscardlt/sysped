     A*%%TS  SD  20160922  080241  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 03 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)                -
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA11PNL)
     A                                      HLPTITLE('HVEM BETALER HVA')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'PROMT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA11'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV1163 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 66TIME
     A                                      COLOR(BLU)
     A                                  9  2MSGCON(015 DIV1164 *LIBL/SYMSGF)
     A            WSFR01         3A  B  9 20DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                 11  2MSGCON(015 DIV1165 *LIBL/SYMSGF)
     A            WSOT01         2A  B 11 20DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SUBFIL                    SFL
     A*%%TS  SD  20150109  083843  SVEIN       REL-V7R1M0  5770-WDS
     A            PKLNR          4S 0H
     A            WSNR           1A  I  6  3VALUES(' ' '2' '3' '4')
     A            PKOT01         2A  O  6  6
     A            PKFR01         3A  O  6 10
     A            PKAVTN         1A  O  6 15
     A            PKGEBY         3A  O  6 18
     A            KGENOT        20A  O  6 22
     A            PKTNR          1A  O  6 44
     A            XXPROS         5Y 3O  6 46EDTCDE(4)
     A N50                                  DSPATR(ND)
     A                                  6 53'%'
     A N50                                  DSPATR(ND)
     A            PKMVA          1A  O  6 55
     A            PKSAMM         1A  O  6 57
     A            PKKOLL         1   O  6 59
     A            PKFVAL         3A  O  6 61
     A            PKDTWS         6Y 0O  6 65EDTWRD('  .  .  ')
     A            PKURIO         1   O  6 74
     A            PKUR01         1   O  6 76
     A            PKUR02         1   O  6 78
     A            PKLKIO         1   O  6 80
     A            PKLK01         2   O  6 82
     A            PKLK02         2   O  6 85
     A            PKLK03         2   O  6 88
     A            PKLK04         2   O  6 91
     A            PKLK05         2   O  6 94
     A            PTRAIO         1   O  6 97
     A            PTRA01         4   O  6 99
     A            PTRA02         4   O  6104
     A            PTRA03         4   O  6109
     A            PTRA04         4   O  6114
     A            PTRA05         4   O  6119
     A            PTRA06         4   O  6124
     A            PTRA07         4   O  6129
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20160922  075524  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS4                                SFLSIZ(0016)
     A  *DS4                                SFLPAG(0015)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N43N44                            ROLLUP(25)
     A  41N43N44                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYFA11010 SYFA11PNL)
     A  *DS4                                HLPARA(01 001 24 080)
     A            ZRECNO         4  0O  4 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA11'
     A                                      COLOR(BLU)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV1163 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(129 SHU2085 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 19MSGCON(050 SHU1691 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A  60                                  DSPATR(ND)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF08(08 'SEARCH')
     A                                      CF12(12 'CANCEL')
     A                                      CF20(20 'IKKE OPDTY/FRA')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
S03  A*         H                           HLPDOC('SYFA11010' 'SYFA11' 'SY400L-
S03  A*                                     O/MAINT')
N03  A          H                           HLPPNLGRP(SYFA11010 SYFA11PNL)
     A                                      HLPARA(01 001 04 080)
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 61MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 21MSGCON(019 SHU1690 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
S03  A*         H                           HLPDOC('SYFA11010' 'SYFA11' 'SY400L-
S03  A*                                     O/MAINT')
N03  A          H                           HLPPNLGRP(SYFA11010 SYFA11PNL)
     A                                      HLPARA(01 001 04 080)
     A                                  2  2MSGCON(015 DIV1168 *LIBL/SYMSGF)
     A            W2OT01         2   B  2 19DSPATR(PC)
     A            W2FR01         3A  B  2 24DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1 18MSGCON(015 SHU0629 *LIBL/SYMSGF)
     A          R NYWRK
     A*%%TS  SD  20160922  080241  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF08(08 'SEARCH')
     A                                      CF12(12 'CANCEL')
     A                                      CF20(20 'IKKE OPDTY/FRA')
     A          H                           HLPPNLGRP(SYFA11010 SYFA11PNL)
     A  *DS4                                HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/SYFA11'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV1163 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A                                  3 53MSGCON(015 DIV1164 *LIBL/SYMSGF)
     A            WSFR01         3A  O  3 71COLOR(WHT)
     A                                  4 53MSGCON(015 DIV1165 *LIBL/SYMSGF)
     A            WSOT01         2A  O  4 71COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(129 SHU0628 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 61MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(019 SHU1690 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  2 19MSGCON(050 SHU1691 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A  60                                  DSPATR(ND)
