     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HLPDOC('VEDLFRAN' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  5  2VALUES(' ' '2' '4')
     A            PTFNR          8A  O  5  5
     A            PTFDT          8Y 0O  5 14EDTWRD('    .  .  ')
     A            PTFTXT        70   H
     A            WSTXT         55   O  5 25
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYPTF01'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av startdato for PTF'
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Velg opsjon:  2=Endre  4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op PTF-nr.  Dato(Å/M/D) Beskrivels-
     A                                      se av PTF'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1997  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   PTF-nr.  Dato(Å/M/D) Beskrivels-
     A                                      se av PTF'
     A                                      COLOR(WHT)
     A            WSFNR          8A  B  2  5DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WSFDT          8Y 0B  2 14EDTWRD('    .  .  ')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSFTXT        70A  B  2 25DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                  3 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  90                              4  2'PTF-nr. finnes fra før. / PTF-nr. -
     A                                      kan ikke være blank.'
     A                                      COLOR(WHT)
     A  89                              4  2'Tastet dato er ugyldig.'
     A                                      COLOR(WHT)
     A  88                              4  2'Beskrivelse må oppgis.'
     A                                      COLOR(WHT)
     A N90N89N88                        4 40'(C) COPYRIGHT  SYSTEMA A/S  1997  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk fra PTF-nr. --> '
     A            W2FNR          8A  I  2 23DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1997  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'   PTF-nr.  Dato(Å/M/D) Beskrivels-
     A                                      se av PTF'
     A                                      COLOR(WHT)
     A            PTFNR          8A  O  2  5DSPATR(UL)
     A                                      DSPATR(HI)
     A            PTFDT          8Y 0O  2 14EDTWRD('    .  .  ')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            PTFTXT        70A  O  2 25DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1997  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYPTF01'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av startdato for PTF'
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Velg opsjon:  2=Endre  4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op PTF-nr.  Dato(Å/M/D) Beskrivels-
     A                                      se av PTF'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1997  '
     A                                      COLOR(WHT)
