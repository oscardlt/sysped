     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A                                      HLPPNLGRP(HLPSCR SYFA84PNL)
     A                                      HLPTITLE('VEDLIKEHOLD BIC/IBAN-NO')
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '1' '2' '4' '8')
     A            BAVALU         3   O  6  4
     A            BASWIF        11   O  6  9
     A            BAGIRN        25   O  6 22
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1  2'SYFA84 '
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(040 SHU1115 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  1MSGCON(060 SHU1116 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A*%%TS  SD  20040505  072759  SVEIN       REL-V5R2M0  5722-WDS
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1  2'SYFA84 '
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(040 SHU1115 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  1MSGCON(060 SHU1116 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1 17MSGCON(045 SHU0879 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  1MSGCON(075 SHU1116 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSVALU         3A  B  2  4DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      TEXT('LOSSESTEDKODE')
     A            WSSWIF        11A  B  2  9DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      TEXT('LOSSESTED')
     A            WSGIRN        25A  B  2 22DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      TEXT('LOSSESTED')
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  2007  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  1MSGCON(075 SHU1116 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            BAVALU         3   O  2  4COLOR(WHT)
     A                                      DSPATR(PC)
     A            BASWIF        11   O  2  9COLOR(WHT)
     A            BAGIRN        25   O  2 22COLOR(WHT)
     A            ARTXT2        30A  O  2 15COLOR(WHT)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2007  '
     A                                      COLOR(WHT)
