     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R BILDE
     A                                      CF03(12 'AVSLUTT')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SYSJ50A'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A N28                              1 20MSGCON(040 DIV3128 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  28                              1 20MSGCON(040 DIV3458 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(010 DIV3116 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  3 14DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 22MSGCON(012 DIV3117 *LIBL/SYMSGF)
     A            UOPD           7Y 0O  3 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 45MSGCON(010 DIV3118 *LIBL/SYMSGF)
     A            USG            3A  O  3 57DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(030 DIV3129 *LIBL/SYMSGF)
     A            WSBK           1A  B  7 41DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  7 51'(J/N)'
     A                                  9  2MSGCON(030 DIV3130 *LIBL/SYMSGF)
     A            WSBLL          1A  B  9 41DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N' 'K')
     A                                  9 51'(J/K/N)'
     A                                 11  2MSGCON(030 DIV3131 *LIBL/SYMSGF)
     A            WSBLO          1A  B 11 41DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 11 51'(J/N)'
     A                                 13  2MSGCON(030 DIV3132 *LIBL/SYMSGF)
     A            WSCMR          1A  B 13 41DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 13 51'(J/N)'
     A                                 15  2MSGCON(030 DIV3133 *LIBL/SYMSGF)
     A            WSFM           1A  B 15 41DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 15 51'(J/N)'
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 10 51MSGCON(030 JOV1011 *LIBL/SYMSGF)
