      *
      ************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KUNDERS LEVERINGSADRESSE   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24              ***
      *** LEDIGE INDIKATORER 28 29 32 34-39 41-86 95-97        ***
      ************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 adressenr minus 1 (0001-) er avvikende fakturaadresse..
10XXX * 02 JOV PTF10 feil i ptf 1, adresse nr 0 bør ikke være tillatt
********************************************************************
      *
     FKUNKO     UF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FVADR      UF A E           K DISK
     FVADR1     IF   E           K DISK
     F                                     RENAME(VADRR:VADRR1)
     FSYFA05FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D TABSMÅ          S              1    DIM(29) CTDATA PERRCD(1)
     D TABSTO          S              1    DIM(29) ALT(TABSMÅ)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  SONAVN                 1     10
     D  SN                     1     10
     D                                     DIM(10)
     D                 DS
     D  VARG                   1     14
     D  VARG09                 1      9
     D                 DS
     D  VADRNA                 1     30
     D  XDLT                   1      7
     D  VAADNX                 1     25
     D                 DS
     D  VADRN1                 1     30
     D  VAAD1X                 1     25
     D                 DS
     D  VADRN3                 1     30
     D  VAAD3X                 1     18
     D  VAPNR1                 1      1
     D  VAPNR2                 2      2
     D  VAPNR3                 3      3
     D  VAPNR4                 4      4
     D                 DS
     D  YPNR                   1      4
     D  W2PNR1                 1      1
     D  W2PNR2                 2      2
     D  W2PNR3                 3      3
     D  W2PNR4                 4      4
     D                 DS
     D  WSKURE                 1     17
     D  WSKUR2                 2     17
     C*
     C                   EXSR      INIT
     C*
     C     WSKUNR        IFNE      *ZEROS
     C                   GOTO      STARTX
     C                   END
     C*
     C     START0        TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       9930
     C   03              GOTO      SLUTT
     C     WSKUN2        IFNE      *ZEROS
     C     WSDUPK        ANDEQ     ' '
     C                   MOVE      '0'           WSDUPK
     C                   END
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     LINNBR        IFLT      15
     C                   EXSR      SØKUNR
     C     KUND1K        CHAIN     CUNDL01                            99
     C  N99              GOTO      START
     C                   MOVE      *ZEROS        WSKUNR
     C                   SETOFF                                       99
     C                   GOTO      START0
     C                   ELSE
     C                   EXSR      SØKUN2
     C                   MOVE      XXKUNR        WSKUN2
     C                   GOTO      START0
     C                   END
     C                   END
     C     STARTX        TAG
     C     KUND1K        CHAIN     CUNDL01                            99
     C   99              MOVE      'SPK0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START0
     C                   MOVE      '0'           *IN27
     C*
     C     WSKUN2        IFNE      *ZEROS
     C                   MOVE      *ZEROS        XXXADR            5 0
     C                   CALL      'SYFA05BC'
     C                   PARM                    FIFIRM
     C                   PARM                    WSKUNR
     C                   PARM                    WSKUN2
     C                   PARM                    XXXADR
     C                   PARM                    WSDUPK
     C                   MOVE      *ZEROS        WSKUN2
     C                   END
     C                   MOVE      *ZEROS        W4KUNR
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2ADNR        YADNR             5 0
     C                   MOVE      W2ADNA        YADNA            30
     C                   MOVE      W2PNR         YPNR              4
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *IN27         IFEQ      '0'
     C     VADRK1        SETLL     VADR
     C                   ELSE
     C     VAD1K1        SETLL     VADR1
     C                   END
     C     START1        TAG
     C     *IN27         IFEQ      '0'
     C     FIFIRM        READE(N)  VADR                                   94
     C                   ELSE
     C     FIFIRM        READE     VADR1                                  94
     C                   END
     C  N94WSKUNR        COMP      KUNDNR                             9494
     C   94              MOVE      *BLANKS       VADRNR
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
     C                   MOVE      *off          *in71
      *
     C  N94              DO
     C                   DO        7
     C     YPNR          IFNE      *BLANKS
     C                   EXSR      TSTPNR
     C     PNROK         CABEQ     'N'           NEXT
     C                   END
     C                   ADD       1             ZRECNO
     C     AKTKOD        COMP      'I'                                    70
     C   70              MOVE      *on           *in71
     C                   WRITE     SUBFIL
     C     NEXT          TAG
     C     *IN27         IFEQ      '0'
     C     FIFIRM        READE(N)  VADR                                   94
     C                   ELSE
     C     FIFIRM        READE     VADR1                                  94
     C                   END
     C  N94YPNR          IFNE      *BLANKS
     C                   EXSR      TSTPNR
     C     PNROK         CABEQ     'N'           NEXT
     C                   END
     C  N94WSKUNR        COMP      KUNDNR                             9494
     C  N94              MOVE      VADRNR        YADNR
     C  N94              END
     C                   END
     C*
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY VARE
     C   06              GOTO      START5
     C*** SØK FRA VARE
     C   08              GOTO      START6
     C*** TIL BILDE SOM HENTER KUNDENR
     C   12              GOTO      START0
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94VADRK3        CHAIN(N)  VADR                               94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     VADRK2        CHAIN(N)  VADR                               33
     C     AKTKOD        COMP      'I'                                    70
     C                   EXFMT     VAREL3
     C  N12              DO
     C     VADRK2        CHAIN     VADR                               33
     C     *in10         IFEQ      *ON
     C  N70              move      'I'           AKTKOD
     C   70              move      'A'           AKTKOD
     C  N33              update    VADRR
     C                   else
     C  N33              DELETE    VADRR
     C     KUDRK2        CHAIN     KUNKO                              33
     C  N33              DELETE    KUNKOR
     C                   EXSR      MOVB1
     C                   MOVE      '*DELETE'     XDLT
     C                   end
     C                   END
     C     AKTKOD        COMP      'I'                                    70
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
      *
     C*** KOPIER TIL ANNEN KUNDE
     C     WSNR          IFEQ      '9'
     C                   MOVE      ' '           WSNR
     C     TAG4          TAG
     C                   EXFMT     VAREL4
     C                   SETOFF                                       30
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKUN2
     C                   MOVE      XXKUNR        W4KUNR
     C                   GOTO      TAG4
     C                   END
     C  N12              DO
     C     KUND4K        CHAIN     CUNDL01                            30
     C   30              MOVE      'SPK0001'     MSGNR
     C   30              GOTO      TAG4
     C     KUND1K        CHAIN     CUNDL01                            99
     C                   EXSR      KOPI
     C     FRAADR        COMP      *ZEROS                                 30
     C   30              MOVE      'JOV1139'     MSGNR
     C   30              GOTO      TAG4
     C                   END
     C     AKTKOD        COMP      'I'                                    70
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ENDRE VARE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       908988
     C                   SETOFF                                       87
     C                   SETOFF                                       30
     C   12              DO
     C   40AKTKOD        COMP      'I'                                    70
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C   092             DO        99998         XN                5 0
     C     VASØKK        CHAIN(N)  VADR                               33
     C     *IN33         IFEQ      '1'
     C                   MOVE      XN            WSADNR
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
     C*
     C*** SØK FRA UTSKRIFTTYPE
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C     W2ADNA        COMP      *BLANKS                            2727
     C                   GOTO      START
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TSTPNR        BEGSR
     C***********************************************
     C* TEST KUN NÅR W2PNR/YPNR ULIK BALNK
     C                   MOVE      'N'           PNROK             1
     C     W2PNR1        IFNE      ' '
     C     W2PNR1        ANDNE     VAPNR1
     C                   GOTO      UTPNR
     C                   END
     C     W2PNR2        IFNE      ' '
     C     W2PNR2        ANDNE     VAPNR2
     C                   GOTO      UTPNR
     C                   END
     C     W2PNR3        IFNE      ' '
     C     W2PNR3        ANDNE     VAPNR3
     C                   GOTO      UTPNR
     C                   END
     C     W2PNR4        IFNE      ' '
     C     W2PNR4        ANDNE     VAPNR4
     C                   GOTO      UTPNR
     C                   END
      * ALLE TESTER BESTÅTT..
     C                   MOVE      'J'           PNROK
     C*
     C     UTPNR         ENDSR
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSADNR        COMP      0                                    9090
N01  C*  90WSADNR        COMP      -1                                   90
N01  C   90WSADNR        COMP      -1                                 9090
     C  N90VADRK         CHAIN(N)  VADR                               33
     C  N90
     CANN33              SETON                                        90
     C   90              MOVE      'SPA0018'     MSGNR
     C   90              SETON                                        30
     C                   END
     C*
     C     WSADNA        COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SPN0014'     MSGNR
     C   89              SETON                                        30
     C*
     C     WSAD3         COMP      *BLANKS                                88
     C   88
     CANN30              MOVE      'SPA0019'     MSGNR
     C   88              SETON                                        30
     C   30              GOTO      SLTB1
      *
     C     WSKURE        COMP      *BLANKS                                87
     C   87
     CANN30              MOVE      'JOV1319'     MSGNR
     C     *IN87         IFEQ      '1'
     C                   SETON                                        30
     C                   GOTO      SLTB1
     C                   ELSE
     C     WSKUR2        IFEQ      *BLANKS
     C                   MOVE      *BLANKS       WSKURE
     C                   END
     C                   END
     C*
     C     VADRK         CHAIN     VADR                               33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWVA
     C  N33              UPDATE    VADRR
     C   33              WRITE     VADRR
     C                   EXSR      KRYREF
     C     AKTKOD        COMP      'I'                                    70
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     KOPI          BEGSR
     C***********************************************
     C                   MOVE      WSKUNR        FRAKNR            8 0
     C                   MOVE      VADRNR        FRAADR            5 0
     C                   CALL      'SYFA05BC'
     C                   PARM                    FIFIRM
     C                   PARM                    W4KUNR                           TIL
     C                   PARM                    FRAKNR                           FRA
     C                   PARM                    FRAADR                           FRA
     C                   PARM                    W4DUPK                           DUPKODE
     C                   ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVVAW
     C                   MOVE      ' '           WSNR
     C                   SETOFF                                       908988
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     WSNR          OREQ      '9'
     C     XDLT          COMP      '*DELETE'                              98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWVA        BEGSR
     C***********************************************
     C                   MOVEL     WSADNA        SONAVN
     C                   Z-ADD     1             NR                2 0
     C     NR            DOWLE     10
     C                   SETOFF                                       31
     C     SN(NR)        LOOKUP    TABSMÅ        TABSTO                   31
     C   31              MOVE      TABSTO        SN(NR)
     C                   ADD       1             NR
     C                   END
     C                   MOVE      'A '          AKTKOD
     C                   MOVE      '00000'       JOURNR
     C                   MOVE      FIFIRM        FIRMA
     C                   MOVE      WSKUNR        KUNDNR
     C                   MOVE      WSADNR        VADRNR
     C                   MOVE      WSADNA        VADRNA
     C                   MOVE      WSAD1         VADRN1
     C                   MOVE      WSLAND        VALAND
     C                   MOVE      WSAD2         VADRN2
     C                   MOVE      WSAD3         VADRN3
     C                   MOVEL     WSRG09        VARG09
     C                   MOVE      WSKON1        VAKON1
     C                   MOVE      WSKON2        VAKON2
     C                   MOVE      WSKURE        VAKURE
     C                   MOVE      WSTLF         VATLF
     C                   MOVE      WSFAX         VAFAX
     C                   MOVE      WSMAIL        VAMAIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVVAW        BEGSR
     C***********************************************
     C                   MOVE      VADRNR        WSADNR
     C                   MOVE      VADRNA        WSADNA
     C                   MOVE      VADRN1        WSAD1
     C                   MOVE      VALAND        WSLAND
     C                   MOVE      VADRN2        WSAD2
     C                   MOVE      VADRN3        WSAD3
     C     VARG09        IFNE      *BLANKS
     C                   MOVE      VARG09        WSRG09
     C                   ELSE
     C                   MOVE      *ZEROS        WSRG09
     C                   END
     C                   MOVE      VAKON1        WSKON1
     C                   MOVE      VAKON2        WSKON2
     C                   MOVE      VAKURE        WSKURE
     C                   MOVE      VATLF         WSTLF
     C                   MOVE      VAFAX         WSFAX
     C                   MOVE      VAMAIL        WSMAIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        VADRNR
     C                   MOVE      *BLANKS       VADRNA
     C                   MOVE      *BLANKS       VADRN1
     C                   MOVE      *BLANKS       VALAND
     C                   MOVE      *BLANKS       VADRN2
     C                   MOVE      *BLANKS       VADRN3
     C                   MOVE      *ZEROS        VARG09
     C                   MOVE      *ZEROS        VAKON1
     C                   MOVE      *ZEROS        VAKON2
     C                   MOVE      *BLANKS       VAKURE
     C                   MOVE      *BLANKS       VATLF
     C                   MOVE      *BLANKS       VAFAX
     C                   MOVE      *BLANKS       VAMAIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSADNR
     C                   MOVE      *BLANKS       WSADNA
     C                   MOVE      *BLANKS       WSAD1
     C                   MOVE      *BLANKS       WSLAND
     C                   MOVE      *BLANKS       WSAD2
     C                   MOVE      *BLANKS       WSAD3
     C                   MOVE      *ZEROS        WSRG09
     C                   MOVE      *ZEROS        WSKON1
     C                   MOVE      *ZEROS        WSKON2
     C                   MOVE      *BLANKS       WSKURE
     C                   MOVE      *BLANKS       WSTLF
     C                   MOVE      *BLANKS       WSFAX
     C                   MOVE      *BLANKS       WSMAIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    WSKUNR
      *
     C     VASØKK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    XN
     C     VADRK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSADNR
     C     KUDRK         KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSADNR
     C     VADRK1        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2ADNR
N01  C                   z-sub     1             W2ADNR
     C     VADRK2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    VADRNR
     C     KUDRK2        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSKUNR
     C                   KFLD                    VADRNR
     C     VADRK3        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    YADNR
     C     VAD1K1        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2ADNA
     C     VAD1K3        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    YADNA
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     KUND4K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    W4KUNR
     C*
     C                   MOVE      '0'           WSDUPK
     C                   MOVE      '0'           W4DUPK
     C*
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKUN2        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    XXKUNR            8 0
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C***********************************************
     C     KRYREF        BEGSR
     C***********************************************
     C     KUDRK         CHAIN     KUNKO                              44
     C  N44              DO
     C     VAKURE        IFNE      *BLANKS
     C                   MOVE      SONAVN        KUALFA
     C                   MOVE      VAKURE        KUINTN
     C                   UPDATE    KUNKOR
     C                   ELSE
     C                   DELETE    KUNKOR
     C                   END
     C                   END
     C*
     C   44VAKURE        IFNE      *BLANKS
     C                   MOVE      WSKUNR        KUKUN1
     C                   MOVE      WSKUNR        KUKUN2
     C                   MOVE      VAKURE        KUINTN
     C                   MOVE      VADRNR        KUVADR
     C                   MOVE      SONAVN        KUALFA
     C                   WRITE     KUNKOR
     C                   END
     C                   ENDSR
     C*
**  TABSMÅ/TABSTO
æÆ
øØ
åÅ
aA
bB
cC
dD
eE
fF
gG
hH
iI
jJ
kK
lL
mM
nN
oO
pP
qQ
rR
sS
tT
uU
vV
wW
xX
yY
zZ
