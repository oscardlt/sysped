/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 12099 * 01 CB  PTF12 2 EXTRA REORGPROGRAM                               */
/* ********************************************************************    */
             PGM
          /*___________________*/
          /* Declare variables */
          /*"""""""""""""""""""*/
             DCLF       FILE(QTEMP/Z500F)
             DCL        VAR(&DATO) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DAYS) TYPE(*DEC) LEN(3 0) VALUE(30)
             DCL        VAR(&ODOBNM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ODOBTP) TYPE(*CHAR) LEN(8)
             DCL        VAR(&ODOBAT) TYPE(*CHAR) LEN(10)
          /*____________________________________*/
          /* Finner dato x antall dager tilbake */
          /*""""""""""""""""""""""""""""""""""""*/
             CALL       PGM(Z510C) PARM(&DAYS &DATO)
          /*____________________________________*/
          /* Slett poster i Z-filer             */
          /*""""""""""""""""""""""""""""""""""""*/
             CALL       PGM(Z501R) PARM(&DATO)
/*N01*/      CALL       PGM(Z502R)
/*N01*/      CALL       PGM(Z503R)
             RCLRSC
          /*___________________________________________________*/
          /* Retreive Physical files in *libl who start with Z*/
          /*"""""""""""""""""""""""""""""""""""""""""""""""""""*/
             DSPOBJD    OBJ(Z*) OBJTYPE(*FILE) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/Z500F)
          /*_______________*/
          /* Read workfile */
          /*"""""""""""""""*/
 READ:       RCVF       RCDFMT(QLIDOBJD)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))
          /*_________________*/
          /* Reorganize file */
          /*"""""""""""""""""*/
             IF         COND(&ODOBTP *NE '*FILE   ') THEN(GOTO +
                          CMDLBL(READ))
             IF         COND(&ODOBAT *NE 'PF        ') THEN(GOTO +
                          CMDLBL(READ))
             RGZPFM     FILE(&ODOBNM)
             MONMSG     MSGID(CPF0000)
             GOTO READ
 EXIT:       ENDPGM
