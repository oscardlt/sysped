      *********************************************************************
CRTPG+*  CRTPGM SYSPED/TDKE052R MODULE(*LIBL/TDKE052R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKEH      UF   E           K DISK    USROPN
     FEDIC      UF   E             DISK    USROPN
     FEDIM      O  A E             DISK    USROPN
     FDKTFI     IF   E             DISK    USROPN
     FDKEA      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4S 0
     D OPD             S              7
     D OPDN            S              7S 0
     D DTM2            S             12
     D DTM2N           S             12S 0
     D JSONstring      S          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(AVD)+'¬, '
        +'¬opd¬ : ¬'+%trim(OPD)+'¬, '
        +'¬dtm2¬ : ¬'+%trim(DTM2)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"dtmupdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

      * Quit
     C                   exsr      Exit

      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKEH
     C  N50              CLOSE     EDIC
     C  N50              CLOSE     EDIM
     C  N50              CLOSE     DKTFI
     C  N50              CLOSE     DKEA

     C                   callp     wrtsection('*fini')

     C                   MOVE      *ON           *INLR
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   EVAL      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   EVAL      OPDN    = c2n2(OPD)
     C                   EVAL      DTM2    = zhbgetvar('DTM2')
     C                   EVAL      DTM2N   = c2n2(DTM2)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     DKEHKey       klist
     C                   kfld                    AVDN
     C                   kfld                    OPDN
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKEH
     C  N50              OPEN      EDIC
     C  N50              OPEN      EDIM
     C  N50              OPEN      DKTFI
     C  N50              OPEN      DKEA
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6

     C                   ENDSR
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hent angivelse
     C     DKEHKey       CHAIN     DKEH                               55
      * Tvungen testindikator ?
     C     DKEH_0035     IFEQ      *BLANKS
     C     DKEH_SYAV     CHAIN     DKEA
     C     DKEA_0035     IFNE      *BLANKS
     C                   EVAL      DKEH_0035=DKEA_0035
     C                   ENDIF
     C                   ENDIF
      * Ændra variablar
     C                   EVAL      DKEH_DTM2 = DTM2N
     C                   EVAL      DKEH_AJOU = '9'
     C                   EVAL      DKEH_SYST='A '
     C                   UPDATE    DKEHR
      * Hæmta firmaupplysningar
     C     1             CHAIN     DKTFI                              51
      * Hæmta næsta meddelandenummer
     C     1             CHAIN     EDIC                               51
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      * Hæmta dagens datum och klockslag
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      * Skriv til EDIM
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004=DKTF_0004T
     C                   EVAL      M0010=DKTF_0010T
     C                   EVAL      M0035=DKEH_0035
     C                   MOVEL     DKEH_SYAV     W0062            11
     C                   MOVE      DKEH_SYOP     W0062
     C                   EVAL      M0062=W0062
     C                   EVAL      M0065='CUSDEC'
     C                   EVAL      M1001=DKEH_AART
     C                   EVAL      M1004=DKEH_1004
     C                   EVAL      M1225=DKEH_AART
     C                   EVAL      M1N07='DKE'
     C                   EVAL      MMN=CMN
     C                   EVAL      MSR='S'
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   EVAL      MAVD=DKEH_SYAV
     C                   EVAL      MTDN=DKEH_SYOP

     C                   WRITE     EDIMR
     C                   ENDSR
