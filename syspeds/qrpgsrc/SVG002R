     FSVIHE     IF   E           K DISK
     D                 DS
     D  KOMP                   1     18
     D  KOMPA                  1      3S 0
     D  KOMPB                  4      9S 0
     D  KOMPC                 10     15S 0
     D  KOMPD                 16     18S 0

     D                 DS
     D  RES                    1      7S 0
     D  RESA                   2      7
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EVAL      *INLR=*ON
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVIHE_SYA     UVIHE_SYA
     C     *LIKE         DEFINE    SVIHE_SYO     UVIHE_SYO
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVIHE_SYA
     C                   KFLD                    UVIHE_SYO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Hovedrutine for sigillering                          SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      * Vent på neste record i dataq
     C                   EXSR      RCV
      * Sigillering
     C                   EXSR      SIG
      * Komprimer sigillet från 18 till 6 tecken
     C                   EXSR      KOMPR
      * Send svar på sigillering ut i dataq
     C                   EXSR      SND
     C*                  GOTO      SRB1

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Sigillering                                          SIG /
      *////////////////////////////////////////////////////////////
     C     SIG           BEGSR
      * Øppna sigillatorn
     C                   EVAL      SKEY='12345678901234567890123456789012345'
     C                   CALL      'SEAL0'
     C                   PARM      'E'           SMODE             1
     C                   PARM      35            SLENGT            3 0
     C                   PARM                    SKEY             35
      * Hur många records ?
     C                   EVAL      *IN51=*OFF
     C     KEYA          SETLL     SVIHE
     C     KEYA          READE     SVIHE                                  51
     C     *IN51         DOWEQ     *OFF
     C                   ADD       1             WREC1             5 0
     C     KEYA          READE     SVIHE                                  51
     C  N51              ENDDO
      * Skicka alla records till sigillatorn
     C                   EVAL      *IN51=*OFF
     C     KEYA          SETLL     SVIHE
     C     KEYA          READE     SVIHE                                  51
     C     *IN51         DOWEQ     *OFF
     C                   ADD       1             WREC2             5 0
     C     WREC2         IFEQ      WREC1
     C     ' '           CHECKR(E) SVIHE_DTA     SLENGT
     C                   ELSE
     C                   EVAL      SLENGT=80
     C                   ENDIF
     C                   CALL      'SEAL0'
     C                   PARM      ' '           SMODE
     C                   PARM                    SLENGT
     C                   PARM      SVIHE_DTA     DATA             80
     C     KEYA          READE     SVIHE                                  51
     C  N51              ENDDO
      * Øppna sigillatorn, svaret ligger i SRES
     C                   CALL      'SEAL0'
     C                   PARM      'C'           SMODE
     C                   PARM      18            SLENGT
     C                   PARM                    SRES             18

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Les dataq                                            RCV /
      *////////////////////////////////////////////////////////////
     C     RCV           BEGSR
      * Les dataq
     C                   CALL      'QRCVDTAQ'
     C                   PARM      'SVGSRQ'      QRNAME           10
     C                   PARM      '*LIBL'       QRLIB            10
     C                   PARM      11            QRLEN             5 0
     C                   PARM                    QRFIEL           11
     C                   PARM      -1            QWAIT             5 0
      * Timeout
     C     QRLEN         IFEQ      0
     C                   ENDIF
     C                   MOVEL     QRFIEL        UVIHE_SYA
     C                   MOVE      QRFIEL        UVIHE_SYO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Send dataq                                           SND /
      *////////////////////////////////////////////////////////////
     C     SND           BEGSR
      * Send svar
     C                   EVAL      QTFIEL=RESA
     C                   MOVEL     UVIHE_SYA     QTKEY
     C                   MOVE      UVIHE_SYO     QTKEY
     C                   CALL      'QSNDDTAQ'
     C                   PARM      'SVGSSQ'      QTNAME           10
     C                   PARM      '*LIBL'       QTLIB            10
     C                   PARM      6             QTLEN             5 0
     C                   PARM                    QTFIEL            6
     C                   PARM      11            QTKILEN           3 0
     C                   PARM                    QTKEY            11
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Komprimera sigillet från 18 till 6 tecken          KOMPR /
      *////////////////////////////////////////////////////////////
     C     KOMPR         BEGSR
     C                   EVAL      KOMP=SRES
     C                   EVAL      RES=0
     C                   EVAL      RES = KOMPA + KOMPB
     C                   IF        RES > 999999
     C                   SUB       1000000       RES
     C                   ENDIF
     C                   ADD       KOMPC         RES
     C                   IF        RES > 999999
     C                   SUB       1000000       RES
     C                   ENDIF
     C                   ADD       KOMPD         RES
     C                   IF        RES > 999999
     C                   SUB       1000000       RES
     C                   ENDIF
      * Har nu ett resultat (6 tecken) i fæltet: RESA
     C                   ENDSR
