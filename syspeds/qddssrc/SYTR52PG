      *_________________________________
      * FILE FOR SEPARAT GIROPRINT     .
      *"""""""""""""""""""""""""""""""""
      *________________________________
      * Format for Ordinary giro Norway
      *""""""""""""""""""""""""""""""""
     A          R SGIR
     A N70N79                           2  7'Faktura nummer:'
     A  70                              2  7'Invoice number:'
     A  79                              2  7'Rechnung nr...:'
     A            XFN            7  0   2 29 EDTCDE(3)
     A N70N79                           3  7'Kundenummer...:'
     A  70                              3  7'Customer no...:'
     A  79                              3  7'Kunden nr.....:'
     A            YKUNDN    R           3 28 REFFLD(KUNDNR CUNDF)
     A                                       EDTCDE(3)
     A N70N79                           4  7'Faktura dato..:'
     A  70                              4  7'Invoice date..:'
     A  79                              4  7'Rechnung datum'
     A            IDAGX          6  0   4 28 EDTCDE(Y)
     A N70N79                           5  7'Forfallsdato..:'
     A  70                              5  7'Date due......:'
     A  79                              5  7'Verfallstag...:'
     A            XFFDT          6  0   5 28 EDTCDE(Y)
     A N70N79                           6  7MSGCON(15 DIV2367 SYMSGF)
     A  70                              6  7'Inv. periode..:'
     A  79                              6  7'Rechn.periode.:'
     A            W1PERM         2  0   6 31
     A                                  6 33'/'
     A            W1PERÅ         2  0   6 34
     A            YNAVN     R          10  7REFFLD(KNAVN CUNDF)
     A            XFNAVN        30     10 46
     A            YADR1     R          11  7REFFLD(ADR1 CUNDF)
     A            XFADR1        30     11 46
     A            YADR2     R          12  7REFFLD(ADR2 CUNDF)
     A            XFADR2        30     12 46
     A            YADR3         35     13  7
     A            XFPONR         4  0  13 46
     A            XFADR3        24     13 52
      *_________________________________________
      * Format for H-line giro, OCR    5224/5225
      *"""""""""""""""""""""""""""""""""""""""""
     A          R SGIR2A
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A            XWKID         19     19 11HIGHLIGHT
     A            XBAK           1  0  19 30HIGHLIGHT
     A            XTOTKR        11  0  19 31EDTCDE(3)
     A                                      HIGHLIGHT
     A                                 19 31'<'
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  19 46HIGHLIGHT
     A            XFBANK        15     19 52HIGHLIGHT
     A          R SGIR2D
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  19 31EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  19 46HIGHLIGHT
     A            XFBANK        15     19 52HIGHLIGHT
      *___________________________________________
      * Format for H-line giro, OCR    4224(*IPDS)
      *"""""""""""""""""""""""""""""""""""""""""""
     A          R SGIR2B
     A                                      FONT(011)
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A            XWKID         19     19 11HIGHLIGHT
     A                                      CHRID
     A            ABAK           1     19 30HIGHLIGHT
     A                                      CHRID
     A            ATOTKR        11     19 31HIGHLIGHT
     A                                      CHRID
     A                                 19 31'<'
     A                                      HIGHLIGHT
     A            ATOTØR         2     19 46HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     19 52HIGHLIGHT
     A                                      CHRID
     A          R SGIR2E
     A                                      FONT(011)
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A            ATOTKR        11     19 31HIGHLIGHT
     A                                      CHRID
     A            ATOTØR         2     19 46HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     19 52HIGHLIGHT
     A                                      CHRID
      *_____________________________________________
      * Format for H-line giro, OCR    BELTESSKRIVER
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R SGIR2C
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A            XWKID         19     19 11HIGHLIGHT
     A                                      CHRID
     A            XBAK           1  0  19 30HIGHLIGHT
     A            XTOTKR        11  0  19 31EDTCDE(3)
     A                                      HIGHLIGHT
     A                                 19 31'<'
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  19 46HIGHLIGHT
     A            XFBANK        15     19 52HIGHLIGHT
     A                                      CHRID
      *________________________________
      * Format for H-line giro, Not OCR
      *""""""""""""""""""""""""""""""""
     A          R SGIR3
     A                                 19  2'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  19 24EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  19 39HIGHLIGHT
     A            XFBANK        15     19 47HIGHLIGHT
