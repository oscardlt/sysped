*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(SYPO09210 SYPO09PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV KUNDERS EG-
     A                                      NE FRAKTTABELLER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  8  3
     A  27                                  DSPATR(PR)
     A            AFPRIA    R        O  8  6REFFLD(AVFRDR/AFPRIA *LIBL/AVFRD)
     A                                      EDTCDE(M)
S01  A*           AFCOBE    R        O  8 21REFFLD(AVFRDR/AFCOBE *LIBL/AVFRD)
     A            AFCOBE    R        O  8 22REFFLD(AVFRDR/AFCOBE *LIBL/AVFRD)
     A            AFVALK    R        O  8 24REFFLD(AVFRHR/AFVALK *LIBL/AVFRH)
     A            AFVEKT    R        O  8 31REFFLD(AVFRDR/AFVEKT *LIBL/AVFRD)
     A                                      EDTCDE(4)
     A            AFPRPR    R        O  8 45REFFLD(AVFRDR/AFPRPR *LIBL/AVFRD)
     A  70                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A            AFADJU    R        O  8 54REFFLD(AVFRDR/AFADJU *LIBL/AVFRD)
     A  70                                  DSPATR(ND)
     A                                      EDTCDE(L)
     A            AMIMAP    R        O  8 64REFFLD(AVFRDR/AMIMAP *LIBL/AVFRD)
     A  70                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0080)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYPO10'
     A                                      COLOR(BLU)
     A N27                              1 19MSGCON(050 SHU0103 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 19MSGCON(040 SHU0449 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(010 DIV0018 *LIBL/SYMSGF)
     A            AFKUND    R        O  2 19REFFLD(AVFRHR/AFKUND *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 29COLOR(WHT)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A  67                              3  2MSGCON(015 SHU0105 *LIBL/SYMSGF)
     A  67                                  COLOR(RED)
     A  68                              3  2MSGCON(015 SHU0106 *LIBL/SYMSGF)
     A  68                                  COLOR(RED)
     A  69                              3  2MSGCON(015 SHU0107 *LIBL/SYMSGF)
     A  69                                  COLOR(RED)
     A            AFCDE1    R        O  3 26REFFLD(AVFRDR/AFCDE1 *LIBL/AVFRD)
     A                                      COLOR(WHT)
     A            POSTNR         4S 0O  3 29COLOR(WHT)
     A            ADR3          24A  O  3 35COLOR(WHT)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(078 YBC0060 *LIBL/SYMSGF)
     A                                      COLOR(GRN)
     A            AFLKFR    R        O  5  5REFFLD(AVFRHR/AFLKFR *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFPNF1    R        O  5  8REFFLD(AVFRHR/AFPNF1 *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFPNF2    R        O  5 14REFFLD(AVFRHR/AFPNF2 *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A                                  5 20'->'
     A            AFLKTI    R        O  5 23REFFLD(AVFRHR/AFLKTI *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFPNT1    R        O  5 26REFFLD(AVFRHR/AFPNT1 *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFPNT2    R        O  5 32REFFLD(AVFRHR/AFPNT2 *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFFRAN    R        O  5 38REFFLD(AVFRHR/AFFRAN *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFGEBY    R        O  5 42REFFLD(AVFRHR/AFGEBY *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFVALK    R        O  5 46REFFLD(AVFRHR/AFVALK *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFPROD    R        O  5 50REFFLD(AVFRHR/AFPROD *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFAVDE    R        O  5 52REFFLD(AVFRHR/AFAVDE *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFBFR1    R        O  5 58REFFLD(AVFRHR/AFBFR1 *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFBFRA    R        O  5 62REFFLD(AVFRHR/AFBFRA *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFBFR2    R        O  5 66REFFLD(AVFRHR/AFBFR2 *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            AFTYPE    R        O  5 69REFFLD(AVFRHR/AFTYPE *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N70                              7  2MSGCON(078 SHU0065 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A  70                              7  2MSGCON(078 BBA0058 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(03 'avslutt')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A          H                           HLPPNLGRP(SYPO09220 SYPO09PNL)
     A                                      HLPARA(02 001 02 008)
     A          H                           HLPPNLGRP(SYPO09230 SYPO09PNL)
     A                                      HLPARA(02 009 02 025)
     A          H                           HLPPNLGRP(SYPO09240 SYPO09PNL)
     A                                      HLPARA(02 026 02 032)
     A          H                           HLPPNLGRP(SYPO09250 SYPO09PNL)
     A                                      HLPARA(02 033 02 046)
     A          H                           HLPPNLGRP(SYPO09260 SYPO09PNL)
     A                                      HLPARA(02 047 02 055)
     A          H                           HLPPNLGRP(SYPO09270 SYPO09PNL)
     A                                      HLPARA(02 056 02 064)
     A          H                           HLPPNLGRP(SYPO09280 SYPO09PNL)
     A                                      HLPARA(02 065 02 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N70                              1  2MSGCON(078 SHU0065 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  70                              1  2MSGCON(078 BBA0058 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            WSCOBE    R        B  2  6REFFLD(AVFRDR/AFCOBE *LIBL/AVFRD)
     A  88                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A            WSPRIA    R        B  2  9REFFLD(AVFRDR/AFPRIA *LIBL/AVFRD)
     A  90                                  DSPATR(HI)
     A                                      EDTCDE(M)
     A            AFVALK    R        O  2 27REFFLD(AVFRHR/AFVALK *LIBL/AVFRH)
     A            WSVEKT    R        B  2 34REFFLD(AVFRDR/AFVEKT *LIBL/AVFRD)
     A  89                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A N70        WSPRPR    R        B  2 48REFFLD(AVFRDR/AFPRPR *LIBL/AVFRD)
     A                                      EDTCDE(4)
     A N70        WSADJU    R        B  2 57REFFLD(AVFRDR/AFADJU *LIBL/AVFRD)
     A                                      EDTCDE(L)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2VEKT    R        I  2 17REFFLD(AVFRDR/AFVEKT *LIBL/AVFRD)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2MSGCON(070 SHU0101 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            AFCOBE    R        O  2  6REFFLD(AVFRDR/AFCOBE *LIBL/AVFRD)
     A            AFPRIA    R        O  2  9REFFLD(AVFRDR/AFPRIA *LIBL/AVFRD)
     A                                      EDTCDE(4)
     A            AFVEKT    R        O  2 34REFFLD(AVFRDR/AFVEKT *LIBL/AVFRD)
     A            AFPRPR    R        O  2 48REFFLD(AVFRDR/AFPRPR *LIBL/AVFRD)
     A            AFADJU    R        O  2 57REFFLD(AVFRDR/AFADJU *LIBL/AVFRD)
     A            AFVALK    R        O  2 26REFFLD(AVFRHR/AFVALK *LIBL/AVFRH)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SYPO10'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(012 SHU0035 *LIBL/SYMSGF)
     A                                  2 44MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
