      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVG016R MODULE(*LIBL/TSVG016R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVTX03F   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D UIE             S              1
     D uvtx15_01       S             10
     D ULK             S              2
     D uvtx15_33       S              4
     D STOPP           S             10
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D ZB              S              1    DIM(10)
     D WXTRA           S              1

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬ie¬ : ¬'+%trim(UIE)+'¬, '
        +'¬kod¬ : ¬'+%trim(uvtx15_01)+'¬, '
        +'¬lk¬ : ¬'+%trim(ulk)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"xtramangdenhet" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta varukod/varukoder
      *
     c     Error         ifeq      *blanks
     C                   EXSR      XTRA

     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬svtx15_33¬ : ¬'+%trim(uvtx15_33)+'¬, '
          +'¬svtx03_04¬ : ¬'+%trim(svtx03_04)+'¬, '
          +'¬xtra¬ : ¬'+%trim(WXTRA)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')


     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C                   CLOSE     SVTX03F

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      uie = zhbgetvar('IE')
     C                   EVAL      uvtx15_01 = zhbgetvar('KOD')
     C                   EVAL      ulk = zhbgetvar('LK')
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
     C                   OPEN      SVTX03F
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     *LIKE         DEFINE    SVTX03_03     W_03

     C   50              eval      Error = 'Invalid userID'

     C                   ENDSR
      *********************************************************
     C     XTRA          Begsr
      *********************************************************
     C                   CALL      'SVT010R'
     C                   PARM                    uie
     C                   PARM                    uvtx15_01
     C                   PARM                    ulk
     C                   PARM                    uvtx15_33

     C     uvtx15_33     IFEQ      *BLANKS
     C                   EVAL      WXTRA='N'
     C                   ELSE
     C                   EVAL      WXTRA='Y'
      /free
           w_03 = uvtx15_33;
           chain ('A': 'MEM': w_03) SVTX03F;
      /end-free
     C                   ENDIF
     C                   ENDSR
