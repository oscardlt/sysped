********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL SPØRRE PÅ TOLLDEKLARASJONER (EKSPORT)  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32 34-40 50-89 95          ***
      ***********************************************************
     FSADTRH    IF   E           K DISK
     FSADTRHUP  IF   E           K DISK    RENAME(SADTRHR:SADTRHRU)
     FSADTRH01  IF   E           K DISK    RENAME(SADTRHR:SADTRHR1)
     FSADTRH02  IF   E           K DISK    RENAME(SADTRHR:SADTRHR2)
     FSADTRH03  IF   E           K DISK    RENAME(SADTRHR:SADTRHR3)
     FSADTRH04  IF   E           K DISK    RENAME(SADTRHR:SADTRHR4)
     FSADTRH05  IF   E           K DISK    RENAME(SADTRHR:SADTRHR5)
     FSADTRH06  IF   E           K DISK    RENAME(SADTRHR:SADTRHR6)
     FSADTRH10  IF   E           K DISK    RENAME(SADTRHR:SADTRHRA)
     FSADTRH12  IF   E           K DISK    RENAME(SADTRHR:SADTRHRC)
     FSADTRH13  IF   E           K DISK    RENAME(SADTRHR:SADTRHRD)
     FSTANDT    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSAD040FM  CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
N01  F                                     INFDS(FBAREA)
N01  D FBAREA          DS
N01  D  WSCLOC               370    371B 0
N01  D  WSSUBN               378    379B 0
     D                 DS
     D  XDTDD                  1      2  0
     D  XDTMM                  3      4  0
     D  XDTÅÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  XDTÅÅ2                 1      4  0
     D  XDTMM2                 5      6  0
     D  XDTDD2                 7      8  0
     D  XDT                    1      8  0
     D                 DS
     D  XDTSD                  1      2  0
     D  XDTSM                  3      4  0
     D  XDTSÅ                  5      6  0
     D  WSDTS                  1      6  0
     D                 DS
     D  XDTSÅ2                 1      4  0
     D  XDTSM2                 5      6  0
     D  XDTSD2                 7      8  0
     D  XDTS                   1      8  0
     D                 DS
     D  XDTKD                  1      2  0
     D  XDTKM                  3      4  0
     D  XDTKÅ                  5      6  0
     D  WSDTK                  1      6  0
     D                 DS
     D  XDTKÅ2                 1      4  0
     D  XDTKM2                 5      6  0
     D  XDTKD2                 7      8  0
     D  XDTK                   1      8  0
     D                 DS
     D  XDTTD                  1      2  0
     D  XDTTM                  3      4  0
     D  XDTTÅ                  5      6  0
     D  WSDTT                  1      6  0
     D                 DS
     D  XDTTÅ2                 1      4  0
     D  XDTTM2                 5      6  0
     D  XDTTD2                 7      8  0
     D  XDTT                   1      8  0
     D                 DS
     D  SEDTÅ                  1      4  0
     D  SEDTM                  5      6  0
     D  SEDTD                  7      8  0
     D  SEDT                   1      8  0
     D                 DS
     D  WSDTD2                 1      2  0
     D  WSDTM2                 3      4  0
     D  WSDTÅ2                 5      6  0
     D  WSDT2                  1      6  0
     D                 DS
     D  SEVKB                  1      9  0
     D  WSVKB                  3      9  0
     D                 DS
     D  SENAK                  1     30
     D  WSNAK                  1     10
     D                 DS
     D  SETLL                  1     10
     D  XSETLL                 1      8
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *
     C                   EXSR      INIT
     C     *IN28         IFEQ      '0'
     C                   GOTO      START
     C                   ELSE
     C                   MOVE      '1'           *IN27
     C                   MOVE      '1'           *IN41
     C                   END
      *
     C     START0        TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '0000'        *IN(96)
     C                   SETOFF                                       30
      *
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKKUN
     C                   GOTO      START0
     C                   END
      *
     C                   EXSR      TESTB1
     C   30              GOTO      START0
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C     WSAAVD        IFEQ      0
     C                   EXSR      LESRAL
     C                   ELSE
     C                   EXSR      LESREC
     C                   END
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        16
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   MOVE      ZRECNO        XRECN2            5 0
     C     WSAAVD        IFEQ      0
     C                   EXSR      LESRAL
     C                   ELSE
     C                   EXSR      LESREC
     C                   END
     C  N94              END
     C                   END
      *
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C  N27              DO
     C                   MOVE      '1'           *IN27
     C                   GOTO      START0
     C                   END
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C     START4        TAG
     C   06              DO
     C                   EXSR      CRTSAD
     C                   GOTO      START3
     C                   END
N01  C   08              EXSR      SETCUR
     C   08              GOTO      START0
     C   15              DO
     C                   CALL      'SAD41P01'
     C                   GOTO      START3
     C                   END
     C   16              DO
     C                   EXSR      CRTSA2
     C                   GOTO      START
     C                   END
      *
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFNE      0                                            1<-B
     C     W2TDN         ORNE      0
     C     W2SG          ORNE      *BLANKS
     C     W2DTT         ORNE      0
     C     W2TLL         ORNE      *BLANKS
     C     W2CTN         ORNE      0
     C     W2ST          ORNE      *BLANKS
     C     W2NR          IFEQ      0                                             2<-B
     C                   EXSR      NYSØK
     C                   GOTO      START
     C                   ELSE                                                    2
     C     *IN28         IFEQ      '0'                                            3<-B
     C     W2NR          COMP      1                                      06
     C     W2NR          COMP      11                                     16
     C   06
     COR 16              DO
     C     W2AVD         IFNE      0                                               4<-B
     C                   MOVE      W2AVD         WSAAVD
     C                   END
     C                   MOVE      *ZEROS        W2NR
     C                   MOVE      *ZEROS        W2AVD
     C                   GOTO      START4
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C     W2TDN         IFEQ      0                                              3<-B
     C                   SETON                                        9830
     C                   MOVE      'YBC0046'     MSGNR
     C                   ELSE                                                     3
     C                   EXSR      NYTDN
     C                   END                                                      3<-E
     C                   MOVE      *BLANKS       W2SG
     C                   MOVE      *ZEROS        W2DTT
     C                   MOVE      *BLANKS       W2TLL
     C                   MOVE      *ZEROS        W2CTN
     C                   MOVE      *BLANKS       W2ST
     C                   GOTO      START3
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C     WSAAVD        IFNE      0
     C                   MOVE      WSAAVD        UAVD
     C                   END
     C     WSSG          IFNE      *BLANKS
     C                   MOVE      WSSG          USG
     C                   END
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     NYSØK         BEGSR
     C***********************************************
     C                   MOVE      *IN28         XIN28             1
     C                   CLEAR                   BILDE01
     C                   MOVE      XIN28         *IN28
     C     W2AVD         IFEQ      0
     C                   MOVE      UAVD          WSAAVD
     C                   ELSE
     C                   MOVE      W2AVD         WSAAVD
     C                   END
     C                   SELECT
     C     W2TDN         WHENNE    0
     C                   MOVE      W2TDN         WSTDN
     C     W2SG          WHENNE    *BLANKS
     C                   MOVE      W2SG          WSSG
     C                   MOVE      W2DTT         WSDTT
     C     W2DTT         WHENNE    0
     C                   MOVE      W2DTT         WSDT
     C     W2TLL         WHENNE    *BLANKS
     C                   MOVE      W2TLL         WSTLL
     C     W2CTN         WHENNE    0
     C                   MOVE      W2CTN         WSCTN
     C     W2ST          WHENNE    *BLANKS
     C                   MOVE      W2ST          WSST
     C                   ENDSL
      *
     C                   MOVE      *ZEROS        W2NR
     C                   MOVE      *ZEROS        W2AVD
     C                   MOVE      *ZEROS        W2TDN
     C                   MOVE      *BLANKS       W2SG
     C                   MOVE      *ZEROS        W2DTT
     C                   MOVE      *BLANKS       W2TLL
     C                   MOVE      *ZEROS        W2CTN
     C                   MOVE      *BLANKS       W2ST
      *
     C                   MOVEA     '0000'        *IN(96)
     C                   EXSR      TESTB1
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYTDN         BEGSR
     C***********************************************
     C     W2AVD         IFEQ      0
     C                   MOVE      UAVD          W2AVD
     C                   END
     C     WRK2K         CHAIN     SADTRHUP                           98
     C   98              DO
     C                   MOVE      'YBC0047'     MSGNR
     C                   GOTO      SLTDN
     C                   END
     C                   MOVE      '1'           *IN90
      *
     C                   SELECT
     C     W2NR          WHENEQ    2
     C     *IN28         ANDEQ     '0'
     C                   EXSR      CHGSAD
     C     W2NR          WHENEQ    3
     C     *IN28         ANDEQ     '0'
     C                   EXSR      CPYSAD
     C     W2NR          WHENEQ    5
     C                   EXSR      DSPSAD
     C     W2NR          WHENEQ    6
     C                   EXSR      PRTSAD
     C                   ENDSL
      *
     C     SLTDN         TAG
     C   98              MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN90
     C                   MOVE      *ZEROS        W2NR
     C  N98              MOVE      *ZEROS        W2AVD
     C  N98              MOVE      *ZEROS        W2TDN
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '0'                                           2<-B
     C     *IN28         IFEQ      '0'                                            3<-B
     C                   SELECT
     C     WSNR          WHENEQ    2
     C                   MOVE      '1'           *IN99
     C                   EXSR      CHGSAD
     C     WSNR          WHENEQ    3
     C                   MOVE      '1'           *IN99
     C                   EXSR      CPYSAD
     C     WSNR          WHENEQ    5
     C                   MOVE      '1'           *IN99
     C                   EXSR      DSPSAD
     C     WSNR          WHENEQ    6
     C                   MOVE      '1'           *IN99
     C                   EXSR      PRTSAD
     C                   ENDSL
     C                   ELSE                                                     3
     C                   SELECT
     C     WSNR          WHENEQ    5
     C                   MOVE      '1'           *IN99
     C                   EXSR      DSPSAD
     C     WSNR          WHENEQ    6
     C                   MOVE      '1'           *IN99
     C                   EXSR      PRTSAD
     C                   ENDSL
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
S01  C*                  CALL      'SOEKUN'
S01  C*                  PARM                    FIFIRM
S01  C*                  PARM                    SOANTK
S01  C*                  PARM                    SOTXT1
S01  C*                  PARM                    WSKNK
S01  C*                  PARM                    SOTXT2
S01  C*                  PARM                    WSKNS
S01  C*                  PARM                    SOTXT3
S01  C*                  PARM                    SOKNR3
S01  C*                  PARM                    SOTXT4
S01  C*                  PARM                    SOKNR4
S01  C*                  PARM                    SOTXT5
S01  C*                  PARM                    SOKNR5
S01  C*                  PARM                    SOTXT6
S01  C*                  PARM                    SOKNR6
S01  C*                  PARM                    SOTXT7
S01  C*                  PARM                    SOKNR7
S01  C*                  PARM                    SOTXT8
S01  C*                  PARM                    SOKNR8
N01  C     WSCLOC        DIV       256           LINNBR
N01  C                   MVR                     POSNBR
N01   *
N01  C     LINNBR        IFEQ      11
N01  C     POSNBR        ANDGE     35
N01  C     POSNBR        ANDLE     42
N01  C                   MOVE      *BLANKS       UKNAVN           30
N01  C                   CALL      'SOEKKUN'
N01  C                   PARM                    WSKNK
N01  C                   PARM                    FIFIRM
N01  C                   PARM                    UKNAVN
N01  C                   Z-ADD     55            POSNBR
N01  C                   END
N01   *
N01  C     LINNBR        IFEQ      12
N01  C     POSNBR        ANDGE     35
N01  C     POSNBR        ANDLE     42
N01  C                   MOVE      *BLANKS       UKNAVN           30
N01  C                   CALL      'SOEKKUN'
N01  C                   PARM                    WSKNS
N01  C                   PARM                    FIFIRM
N01  C                   PARM                    UKNAVN
N01  C                   Z-ADD     55            POSNBR
N01  C                   END
      *
     C                   ENDSR
      *
N01   ***********************************************
N01  C     SETCUR        BEGSR
N01   ***********************************************
N01  C                   Z-ADD     35            POSNBR
N01  C                   SELECT
N01  C                   WHEN      WSDT   <> 0
N01  C                   Z-ADD     9             LINNBR
N01  C                   WHEN      WSTDN  <> 0
N01  C                   Z-ADD     10            LINNBR
N01  C                   WHEN      WSKNK  <> 0
N01  C                   Z-ADD     11            LINNBR
N01  C                   WHEN      WSKNS  <> 0
N01  C                   Z-ADD     12            LINNBR
N01  C                   WHEN      WSSG   <> *BLANKS
N01  C                   Z-ADD     13            LINNBR
N01  C                   WHEN      WSTLL  <> *BLANKS
N01  C                   Z-ADD     14            LINNBR
N01  C                   WHEN      WSCTN  <> 0
N01  C                   Z-ADD     15            LINNBR
N01  C                   WHEN      WSST   <> *BLANKS
N01  C                   Z-ADD     16            LINNBR
N01  C                   OTHER
N01  C                   Z-ADD     9             LINNBR
N01  C                   ENDSL
N01   *
N01  C                   ENDSR
N01   *
     C***********************************************
     C     DSPSAD        BEGSR
     C***********************************************
     C                   CALL      'SAD040A'
     C                   PARM                    SEAVD
     C                   PARM                    SETDN
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      3
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C  N90              MOVE      ' '           WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGSAD        BEGSR
     C***********************************************
     C                   MOVE      'CHG'         XTYPE             3
     C                   CALL      'SAD041CL'
     C                   PARM      SEAVD         XAVD
     C                   PARM      SETDN         XTDN
     C                   PARM                    SESG
     C                   PARM                    UKODE
     C                   PARM                    XTYPE
     C     SATHKO        CHAIN     SADTRHUP                           33
     C  N90              MOVE      ' '           WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYSAD        BEGSR
     C***********************************************
     C                   MOVE      'CPY'         XTYPE             3
     C     *LIKE         DEFINE    SEAVD         XAVD
     C     *LIKE         DEFINE    SETDN         XTDN
     C                   MOVE      SEAVD         XAVD
     C                   MOVE      SETDN         XTDN
     C                   CALL      'SAD041CL'
     C                   PARM                    XAVD
     C                   PARM                    XTDN
     C                   PARM                    USG
     C                   PARM                    UKODE
     C                   PARM                    XTYPE
     C  N90              MOVE      ' '           WSNR
     C  N90              UPDATE    SUBFIL
     C     XTDN          IFNE      0
     C     XAVD          IFNE      SEAVD
     C     XTDN          ORNE      SETDN
     C     SATHKC        CHAIN     SADTRHUP                           33
     C                   MOVE      SEDTD         WSDTD2
     C                   MOVE      SEDTM         WSDTM2
     C                   MOVE      SEDTÅ         WSDTÅ2
     C                   ADD       1             XRECN2
     C                   MOVE      XRECN2        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTSAD        BEGSR
     C***********************************************
     C                   MOVE      'CRT'         XTYPE             3
     C     WSAAVD        IFEQ      0
     C                   MOVE      UAVD          XAVD
     C                   ELSE
     C                   MOVE      WSAAVD        XAVD
     C                   END
     C                   MOVE      *ZEROS        XTDN
     C                   CALL      'SAD041CL'
     C                   PARM                    XAVD
     C                   PARM                    XTDN
     C                   PARM                    USG
     C                   PARM                    UKODE
     C                   PARM                    XTYPE
     C     XAVD          IFNE      0
     C     XTDN          ANDNE     0
     C     SATHKC        CHAIN     SADTRHUP                           33
     C  N33              DO
     C     WSAAVD        IFEQ      0
     C                   MOVE      XAVD          UAVD
     C                   ELSE
     C                   MOVE      XAVD          WSAAVD
     C                   END
     C                   MOVE      SEDTD         WSDTD2
     C                   MOVE      SEDTM         WSDTM2
     C                   MOVE      SEDTÅ         WSDTÅ2
     C                   ADD       1             XRECN2
     C                   MOVE      XRECN2        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTSA2        BEGSR
     C***********************************************
     C                   MOVE      'CR2'         XTYPE             3
     C     WSAAVD        IFEQ      0
     C                   MOVE      UAVD          XAVD
     C                   ELSE
     C                   MOVE      WSAAVD        XAVD
     C                   END
     C                   MOVE      *ZEROS        XTDN
     C                   CALL      'SAD041CL'
     C                   PARM                    XAVD
     C                   PARM                    XTDN
     C                   PARM                    USG
     C                   PARM                    UKODE
     C                   PARM                    XTYPE
      *
     C     WSAAVD        IFEQ      0
     C                   MOVE      XAVD          UAVD
     C                   ELSE
     C                   MOVE      XAVD          WSAAVD
     C                   END
     C*
     C                   ENDSR
      *
     C***********************************************
     C     PRTSAD        BEGSR
     C***********************************************
     C     SEST          IFEQ      ' '
     C     SEST          OREQ      'P'
     C                   MOVE      'PRT'         XTYPE             3
     C     *IN28         IFEQ      '1'
     C                   CALL      'SAD041CO'
     C                   PARM                    SEAVD
     C                   PARM                    SETDN
     C                   PARM                    USG
     C                   PARM                    UKODE
     C                   PARM                    XTYPE
     C                   MOVE      'H'           PRTTYP            1
     C                   CALL      'SAD41P'
     C                   PARM                    PRTTYP            1
     C                   PARM                    PRTAVD            4
     C                   PARM                    PRTTDN            5
     C     SATHKO        CHAIN     SADTRHUP                           33
     C                   ELSE
     C                   CALL      'SAD041CL'
     C                   PARM                    SEAVD
     C                   PARM                    SETDN
     C                   PARM                    USG
     C                   PARM                    UKODE
     C                   PARM                    XTYPE
     C     SATHKO        CHAIN     SADTRHUP                           33
     C                   CALL      'SAD41P01'
     C                   END
     C                   END
     C  N90              MOVE      ' '           WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KOANVN
     C     WSAAVD        IFNE      0
     C     WSTDN         ORNE      0
     C     WSAAVD        CHAIN     STANDT                             99
     C  N99KODTAK        CHAIN     KODTA                              99
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      SLTB1
     C                   MOVE      WSAAVD        UAVD
     C                   END
      *
     C                   MOVEA     '00000000'    *IN(42)
      *
     C     WSDT          COMP      *ZEROS                             4141
     C   41              GOTO      STTB1
     C     WSTDN         COMP      *ZEROS                             4242
     C   42              GOTO      STTB1
     C     WSKNK         COMP      *ZEROS                             4343
     C   43              GOTO      STTB1
     C     WSKNS         COMP      *ZEROS                             4444
     C   44              GOTO      STTB1
     C     WSSG          COMP      *BLANKS                            4545
     C   45              GOTO      STTB1
     C     WSTLL         COMP      *BLANKS                            4646
     C   46              GOTO      STTB1
     C     WSCTN         COMP      *ZEROS                             4747
     C   47              GOTO      STTB1
     C     WSST          COMP      *BLANKS                            4949
     C   49              GOTO      STTB1
      *
     C                   MOVE      'DIV0064'     MSGNR
     C                   SETON                                        3098
     C                   GOTO      SLTB1
      *
     C     STTB1         TAG
     C   43              MOVE      WSKNK         KUNDNR
     C   43KUNDK         CHAIN     CUNDL01                            97
     C   97              MOVE      'SPK0035'     MSGNR
     C   44              MOVE      WSKNS         KUNDNR
     C   44KUNDK         CHAIN     CUNDL01                            96
     C   96
     CANN97              MOVE      'SPK0035'     MSGNR
     C   97
     COR 96              SETON                                        30
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     SETKEY        BEGSR
     C***********************************************
     C   41              DO
     C     WSDT          IFEQ      0
     C                   MOVE      *ZEROS        XDT
     C                   ELSE
     C                   MOVE      XDTDD         XDTDD2
     C                   MOVE      XDTMM         XDTMM2
     C     XDTÅÅ         IFGT      50
     C     XDTÅÅ         ADD       1900          XDTÅÅ2
     C                   ELSE
     C     XDTÅÅ         ADD       2000          XDTÅÅ2
     C                   END
     C                   END
     C                   END
     C   43              DO
     C                   SELECT
     C     WSDTK         WHENEQ    0
     C                   MOVE      *ZEROS        XDTK
     C                   OTHER
     C                   MOVE      XDTKD         XDTKD2
     C                   MOVE      XDTKM         XDTKM2
     C     XDTKÅ         IFGT      50
     C     XDTKÅ         ADD       1900          XDTKÅ2
     C                   ELSE
     C     XDTKÅ         ADD       2000          XDTKÅ2
     C                   END
     C                   ENDSL
     C                   END
     C   44              DO
     C     WSDTS         IFEQ      0
     C                   MOVE      *ZEROS        XDTS
     C                   ELSE
     C                   MOVE      XDTSD         XDTSD2
     C                   MOVE      XDTSM         XDTSM2
     C     XDTSÅ         IFGT      50
     C     XDTSÅ         ADD       1900          XDTSÅ2
     C                   ELSE
     C     XDTSÅ         ADD       2000          XDTSÅ2
     C                   END
     C                   END
     C                   END
     C   45              DO
     C     WSDTT         IFEQ      0
     C                   MOVE      *ZEROS        XDTT
     C                   ELSE
     C     WSDTT         IFEQ      999999
     C                   MOVE      99999999      XDTT
     C                   ELSE
     C                   MOVE      XDTTD         XDTTD2
     C                   MOVE      XDTTM         XDTTM2
     C     XDTTÅ         IFGT      50
     C     XDTTÅ         ADD       1900          XDTTÅ2
     C                   ELSE
     C     XDTTÅ         ADD       2000          XDTTÅ2
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C     WSAAVD        IFEQ      0
     C                   Z-ADD     1             YAVD              4 0
     C                   EXSR      MINSAL
     C                   ELSE
     C                   EXSR      MINSAT
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MINSAT        BEGSR
     C***********************************************
     C   41SATH1K        SETLL     SADTRH01
     C   42SATHK         SETLL     SADTRH
     C   43SATH2K        SETLL     SADTRH02
     C   44SATH3K        SETLL     SADTRH03
     C   45WSDTT         IFEQ      999999
     C     SATH4K        SETGT     SADTRH04
     C                   ELSE
     C     SATH4K        SETLL     SADTRH04
     C                   END
     C   46SATH5K        SETLL     SADTRH05
     C   47SATH6K        SETLL     SADTRH06
     C   49SATH0K        SETLL     SADTRH10
      *
     C                   ENDSR
      *
     C***********************************************
     C     MINSAL        BEGSR
     C***********************************************
     C   41SATA1K        SETLL     SADTRH01
     C   43SATA2K        SETLL     SADTRH02
     C   44SATA3K        SETLL     SADTRH03
     C   45WSDTT         IFEQ      999999
     C     SATA4K        SETGT     SADTRH04
     C                   ELSE
     C     SATA4K        SETLL     SADTRH04
     C                   END
     C   46WSTLL         SETLL     SADTRH13
     C   47SATA6K        SETLL     SADTRH12
     C   49SATA0K        SETLL     SADTRH10
      *
     C                   ENDSR
      *
     C***********************************************
     C     LESREC        BEGSR
     C***********************************************
     C     STLREC        TAG
     C   41WSAAVD        READE     SADTRH01                               94
     C   42WSAAVD        READE     SADTRH                                 94
     C   43WSAAVD        READE     SADTRH02                               94
     C   43
     CANN94WSKNK         COMP      SEKNK                              9494
     C   44WSAAVD        READE     SADTRH03                               94
     C   44
     CANN94WSKNS         COMP      SEKNS                              9494
     C   45WSDTT         IFEQ      999999
     C     WSAAVD        READPE    SADTRH04                               94
     C  N94WSSG          COMP      SESG                               9494
     C                   ELSE
     C     WSAAVD        READE     SADTRH04                               94
     C  N94WSSG          COMP      SESG                               9494
     C                   END
     C   46WSAAVD        READE     SADTRH05                               94
     C   47WSAAVD        READE     SADTRH06                               94
     C   49SATH0K        READE     SADTRH10                               94
     C  N94              DO
     C                   MOVE      SEDTD         WSDTD2
     C                   MOVE      SEDTM         WSDTM2
     C                   MOVE      SEDTÅ         WSDTÅ2
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     LESRAL        BEGSR
     C***********************************************
     C     STLRAL        TAG
     C   41YAVD          READE     SADTRH01                               94
     C   43YAVD          READE     SADTRH02                               94
     C   43
     CANN94WSKNK         COMP      SEKNK                              9494
     C   44YAVD          READE     SADTRH03                               94
     C   44
     CANN94WSKNS         COMP      SEKNS                              9494
     C   45WSDTT         IFEQ      999999
     C     YAVD          READPE    SADTRH04                               94
     C  N94WSSG          COMP      SESG                               9494
     C                   ELSE
     C     YAVD          READE     SADTRH04                               94
     C  N94WSSG          COMP      SESG                               9494
     C                   END
     C   46              READ      SADTRH13                               94
     C   47              READ      SADTRH12                               94
     C   49SATA0K        READE     SADTRH10                               94
     C  N94              DO
     C                   MOVE      SEDTD         WSDTD2
     C                   MOVE      SEDTM         WSDTM2
     C                   MOVE      SEDTÅ         WSDTÅ2
     C                   END
     C   94YAVD          IFLT      9999
     C     *IN46         ANDNE     '1'
     C     *IN47         ANDNE     '1'
     C                   ADD       1             YAVD
     C     YAVD          SETLL     STANDT
     C                   SETOFF                                       33
     C                   READ      STANDT                                 33
     C  N33              DO
     C                   MOVE      SEAVD         YAVD
     C                   EXSR      MINSAL
     C                   SETOFF                                       94
     C                   GOTO      STLRAL
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SATHK         KLIST
     C                   KFLD                    WSAAVD
     C                   KFLD                    WSTDN
     C     SATHKO        KLIST
     C                   KFLD                    SEAVD
     C                   KFLD                    SETDN
     C     WRK2K         KLIST
     C                   KFLD                    W2AVD
     C                   KFLD                    W2TDN
     C     SATHKC        KLIST
     C                   KFLD                    XAVD
     C                   KFLD                    XTDN
     C     SATH1K        KLIST
     C                   KFLD                    WSAAVD
     C                   KFLD                    XDT
     C     SATH2K        KLIST
     C                   KFLD                    WSAAVD
     C                   KFLD                    WSKNK
     C                   KFLD                    XDTK
     C     SATH3K        KLIST
     C                   KFLD                    WSAAVD
     C                   KFLD                    WSKNS
     C                   KFLD                    XDTS
     C     SATH4K        KLIST
     C                   KFLD                    WSAAVD
     C                   KFLD                    WSSG
     C                   KFLD                    XDTT
     C     SATH5K        KLIST
     C                   KFLD                    WSAAVD
     C                   KFLD                    WSTLL
     C     SATH6K        KLIST
     C                   KFLD                    WSAAVD
     C                   KFLD                    WSCTN
     C     SATH0K        KLIST
     C                   KFLD                    WSAAVD
     C                   KFLD                    WSST
     C     KUNDK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     KODTAK        KLIST
     C                   KFLD                    XUNIK
     C                   KFLD                    WSAAVD
     C     SATA1K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    XDT
     C     SATA2K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    WSKNK
     C                   KFLD                    XDTK
     C     SATA3K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    WSKNS
     C                   KFLD                    XDTS
     C     SATA4K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    WSSG
     C                   KFLD                    XDTT
     C     SATA5K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    WSTLL
     C     SATA6K        KLIST
     C                   KFLD                    WSCTN
     C     SATA0K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    WSST
      *
     C     *ENTRY        PLIST
      * UKODE = S  -->  SPØRRING
      * UKODE = T  -->  TRANSITT
     C                   PARM                    UKODE             1
     C                   PARM                    UTDN              5 0
      *
     C     UKODE         COMP      'S'                                    28
     C                   MOVE      'A'           XUNIK             1
      *
S01  C*                  Z-ADD     2             SOANTK            1 0
S01  C*                  MOVE      'AVSENDER'    SOTXT1            8
S01  C*                  MOVE      'MOTTAKER'    SOTXT2            8
S01  C*                  MOVE      *BLANKS       SOTXT3            8
S01  C*                  MOVE      *BLANKS       SOTXT4            8
S01  C*                  MOVE      *BLANKS       SOTXT5            8
S01  C*                  MOVE      *BLANKS       SOTXT6            8
S01  C*                  MOVE      *BLANKS       SOTXT7            8
S01  C*                  MOVE      *BLANKS       SOTXT8            8
S01  C*                  MOVE      *ZEROS        SOKNR3            8 0
S01  C*                  MOVE      *ZEROS        SOKNR4            8 0
S01  C*                  MOVE      *ZEROS        SOKNR5            8 0
S01  C*                  MOVE      *ZEROS        SOKNR6            8 0
S01  C*                  MOVE      *ZEROS        SOKNR7            8 0
S01  C*                  MOVE      *ZEROS        SOKNR8            8 0
      *
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSDTS
     C                   MOVE      *ZEROS        WSDTK
     C                   MOVE      *ZEROS        WSDTT
     C                   MOVE      *ZEROS        XDT
     C                   MOVE      *ZEROS        XDTS
     C                   MOVE      *ZEROS        XDTK
     C                   MOVE      *ZEROS        XDTT
     C                   MOVE      *ZEROS        SEDT
     C                   MOVE      *ZEROS        WSDT2
      *
     C                   MOVE      UAVD          WSAAVD
     C     KODTAK        CHAIN     KODTA                              30
     C     *IN28         IFEQ      '0'
     C                   MOVE      USG           WSSG
     C                   MOVE      '999999'      WSDTT
     C                   SETON                                        45
     C                   END
      *
     C     FIFIRM        SETLL     FIRM                               30
     C                   READ      FIRM                                   30
      *
     C                   ENDSR
      *
