********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 CURSOR PÅ FEIL LINJE
11XXX * 02 SH  PTF11 VISER IKKE SIDE 1
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KUNDE - SYSPED INTERNFAKTURA*
      ***********************************************************
      *
     FCUIFAK    UF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FEDII      IF   E           K DISK
     FSYKS36FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KNAVN                  1     30
     D  ZZKNAV                 1     20
     D  ZZSEI7                 1      7
     D                 DS
     D  CISEID                 1     35
     D  ZZSEID                 1     20
     D                 DS
     D  CIMOID                 1     35
     D  ZZMOID                 1     20
     C*
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     CUIFAK
     C                   READ(N)   CUIFAK                                 94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   MOVE      CIKUNR        WSKUNR
     C     KUNKEY        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C                   WRITE     SUBFIL
     C                   READ(N)   CUIFAK                                 94
     C  N94              END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
N02  C                   IF        ZRECNO > 1
N02  C                   Z-ADD     1             ZRECNO
N02  C                   END
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
S01  C*                  Z-ADD     22            LINNBR
N01  C                   Z-ADD     21            LINNBR
     C                   Z-ADD     1             POSNBR
     C                   MOVE      *ZEROS        WSKUNR
     C                   GOTO      START5
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       88
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     KUNKEY        CHAIN     CUNDL01                            90
     C   90              MOVE      'KUN0001'     MSGNR
     C  N90WSKUNR        CHAIN(N)  CUIFAK                             33
     C  N90
     CANN33              DO
     C                   SETON                                        90
     C                   MOVE      'JOV1215'     MSGNR
     C                   END
     C   90              Z-ADD     1             POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C  N30WSSEID        CHAIN     EDII                               89
     C   89
     CANN30              MOVE      'SPN0001'     MSGNR
     C   89
     CANN30              Z-ADD     10            POSNBR
     C   89              SETON                                        30
      *
     C  N30WSMOID        CHAIN     EDII                               88
     C   88
     CANN30              MOVE      'SPN0001'     MSGNR
     C   88
     CANN30              Z-ADD     46            POSNBR
     C   88              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSKUNR        CHAIN     CUIFAK                             33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    CIFAKR
     C   33              WRITE     CIFAKR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     ZZSEI7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     ZZSEI7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      CIKUNR        WSKUNR
     C                   MOVE      CISEID        WSSEID
     C                   MOVE      CIMOID        WSMOID
     C                   MOVE      CIPRT         WSPRT
     C                   EXFMT     VAREL3
     C  N12              DO
     C     WSKUNR        CHAIN     CUIFAK                             33
     C  N33              DELETE    CIFAKR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       88
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØK           BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C     POSNBR        IFLE      8
     C     *IN40         ANDEQ     '0'                                           KUN V/NY
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   ENDIF
     C*
     C     POSNBR        IFGE      10
     C     POSNBR        ANDLE     44
     C                   CALL      'EDI52R'
     C                   PARM                    WSSEID
     C                   GOTO      UTSØK
     C                   ENDIF
     C*
     C     POSNBR        IFGE      46
     C                   CALL      'EDI52R'
     C                   PARM                    WSMOID
     C                   GOTO      UTSØK
     C                   ENDIF
      *
     C     UTSØK         ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        CIKUNR
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       CISEID
     C                   MOVE      *BLANKS       CIMOID
     C                   MOVE      *BLANKS       CIPRT
     C                   MOVEL     '*DELETE'     ZZSEI7
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSKUNR
     C                   MOVE      *BLANKS       WSSEID
     C                   MOVE      *BLANKS       WSMOID
     C                   MOVE      *BLANKS       WSPRT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      CIKUNR        WSKUNR
     C                   MOVE      CISEID        WSSEID
     C                   MOVE      CIMOID        WSMOID
     C                   MOVE      CIPRT         WSPRT
     C                   SETON                                        40
S01  C*                  Z-ADD     22            LINNBR
N01  C                   Z-ADD     21            LINNBR
     C                   Z-ADD     10            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSKUNR        CIKUNR
     C                   END
     C*
     C                   MOVE      WSSEID        CISEID
     C                   MOVE      WSMOID        CIMOID
     C                   MOVE      WSPRT         CIPRT
     C*
     C                   ENDSR
     C*
