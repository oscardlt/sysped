      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/SYSTI3 MODULE(*LIBL/SYSTI3 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FBRSTP     UF A E           K DISK    USROPN
     FBRSTP01   IF   E           K DISK    USROPN
     F                                     RENAME(BRSTPR:BRSTPR01)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D OddEven         s             10
      * Client input variables
     D Sort            s              1
     D user            s             10
     D MALID           s             10
     D PNRFRA          s              4
     D PNRTIL          s              4
     D SONENR          s              2
     D SONENAVN        s             20
     D PNRFN           s              4  0
     D PNRTN           s              4  0
     D SONRN           s              2  0
     D PNRFa           s              4
     D PNRTa           s              4
     D SONRa           s              2
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D UTxt            s             80
     D ItemCount       s             10i 0
     D i               s             10i 0
      * Vektgrupper
     D                 DS
     D  KOVK1                  1      5  0
     D  KOVK2                  6     10  0
     D  KOVK3                 11     15  0
     D  KOVK4                 16     20  0
     D  KOVK5                 21     25  0
     D  KOVK6                 26     30  0
     D  KOVK7                 31     35  0
     D  KOVK8                 36     40  0
     D  KOVK9                 41     45  0
     D  KOVK10                46     50  0
     D  KOVK11                51     55  0
     D  KOVK12                56     60  0
     D  FF                     1     60  0
     D                                     DIM(12) ASCEND
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
     c     BIKUNR        setll     BRSTP
     c     BIKUNR        Reade     BRSTP                                  35
     c     *IN35         doweq     '0'
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c     BIKUNR        Reade     BRSTP                                  35
     c                   end
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      Sort     = zhbgetvar('Sort')
     C                   eval      MALID    = zhbgetvar('MALID')
     C                   eval      PNRFRA   = zhbgetvar('PNRFRA')
     C                   eval      PNRFN   = c2n2(PNRFRA)
     C                   eval      PNRTIL   = zhbgetvar('PNRTIL')
     C                   eval      PNRTN   = c2n2(PNRTIL)
     C                   eval      SONENR   = zhbgetvar('SONENR')
     C                   eval      SONRN   = c2n2(SONENR)
     C                   eval      SONENAVN = zhbgetvar('SONENAVN')
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     CUNDL01
     C                   CLOSE     BRSTP
     C                   CLOSE     BRSTP01
      *
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYSTI3':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('Sort':Sort)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
     C                   callp     updHTMLvar('MALID':MALID)
      *
     c     MALID         ifNE      *blanks
     c     PNRFN         andne     *zeros
     c     PNRTN         andne     *zeros
     C                   eval      RIKUNR  = BIKUNR
     C                   eval      RIMAID  = MALID
      * hvis sonenr er brukt før hent navn fra denne
     C     SonNrKey      CHAIN     BRSTP01                            33
     C  N33              eval      SONENAVN  = RISONA
     C                   eval      RIPNRF    = PNRFN
     C     OmrKey        CHAIN     BRSTP                              33
     C                   eval      RIPNRT    = PNRTN
     C                   eval      RISONR    = SONRN
     C                   eval      RISONA    = SONENAVN
     C     *IN33         IFEQ      *ON
     c                   write     BRSTPR
     c                   eval      Utxt = 'Nytt postnr område ' + PNRFRA +
     c                                    ' - '+PNRTIL+' er lagt til i ' +
     c                                    'sone '+SONENR+' '+SONENAVN
     c                   else
     c                   update    BRSTPR
     c                   eval      Utxt = 'Eksisterende postnr område ' +
     c                                    PNRFRA + ' - '+PNRTIL+
     c                                    ' er oppdatert med ' +
     c                                    'sone '+SONENR+' '+SONENAVN
     c                   end
      * Malid - Pnr fra/til er ikke tastet.
     c                   else
     c                   eval      Utxt = 'Tast inn MALID, Postnr fra/til,'+
     c                                    ' sonenr og navn, og send'
     c                   end
      * Hvis vellykket skal INPUT blankes
     c                   move      *zeros        pnrfn
     c                   move      *zeros        pnrtn
     c                   move      *BLANKS       sonenavn
     C                   callp     updHTMLvar('PNRFRA':
     C                             %trim(%editc(PNRFN:'Z')))
     C                   callp     updHTMLvar('PNRTIL':
     C                             %trim(%editc(PNRTN:'Z')))
     C                   callp     updHTMLvar('SONENR':
     C                             %trim(%editc(SONRN:'Z')))
     C                   callp     updHTMLvar('SONENAVN':SONENAVN)
      *
     C                   callp     updHTMLvar('UpdateTxt':Utxt)
      *
      * Slett evt merkede poster...
     C                   eval      ItemCount = ZhbGetVarCnt('MaidPnrf')
     C                   eval      i = 1
     C     i             DOUGT     ItemCount
     C                   eval      MaidPnrf = ZhbGetVar('MaidPnrf':i)
     C                   movel     MaidPnrf      RIMAID
     C                   move      MaidPnrf      PNRFA
     C                   eval      RIPNRF  = c2n2(PNRFA)
     C                   eval      RIKUNR  = BIKUNR
     C     OmrKey        CHAIN     BRSTP                              33
     C  N33              Delete    BRSTPR
     C                   eval      i = i +1
     C                   ENDDO
     C
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     c                   move      RIPNRF        PNRFa
     c                   move      RIPNRT        PNRTa
     c                   move      RISONR        SONRa
      *MaidPnrf er HTML-variabel for å kunne utføre delete i neste runde..
     c                   movel     RIMAID        MaidPnrf         14
     c                   move      RIPNRF        MaidPnrf
     C                   callp     updHTMLvar('MaidPnrf':MaidPnrf)
     C                   callp     updHTMLvar('MAID':RIMAID)
     C                   callp     updHTMLvar('PNRF':PNRFa)
     C                   callp     updHTMLvar('PNRT':PNRTa)
     C                   callp     updHTMLvar('SONR':SONRa)
     C                   callp     updHTMLvar('SONA':RISONA)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      CUNDL01
     C                   OPEN      BRSTP
     C                   OPEN      BRSTP01
      * Key for POSTNROMRÅDE
     C     OmrKey        KLIST
     C                   KFLD                    RIKUNR
     C                   KFLD                    RIMAID
     C                   KFLD                    RIPNRF
      * Key for å hente Sonenavn vi tidligere brukt på nummer
     C     SonNrKey      KLIST
     C                   KFLD                    RIKUNR
     C                   KFLD                    RIMAID
     C                   KFLD                    SONRN
      * Key for CUNDL01
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
      *
     C     KunKey        CHAIN     Cundl01                            33
bhr   * Spesialvri for å kunne deme uten å avsløre kundenavn
bhr  c                   MOVEL     USER          tstjov            4
bhr  c     tstjov        ifeq      'JOVO'
bhr  c                   eval      KNAVN  = 'Jan Ottar Valderhaug'
bhr  c                   end
      *
      *
     C                   endsr
      *
