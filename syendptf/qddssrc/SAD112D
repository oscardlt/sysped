      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            MCAVD          4Y 0O  6  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A            MCTDN          7Y 0O  6 10TEXT('Oppdrag')
     A                                      EDTCDE(4)
     A            MCCN           2Y 0O  6 18TEXT('Consignment nr')
     A                                      EDTCDE(4)
     A            MCST           1A  O  6 21TEXT('Signatur')
     A            MCNAK         30A  O  6 24TEXT('Consignee')
     A            MCNAS         30A  O  6 55TEXT('Consignor')
     A            MCTRF_35      35A  O  6 86TEXT('Transport Ref.nr')
     A            MCTRF         70A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Lag ny consignment')
     A                                      CF08(08 'Søk')
     A N49                                  CF16(16 'Plukk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD112R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold kurermanifest - Liste'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            UAVD           4Y 0O  2 12TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 18'Turnr:'
     A            UPRO           8Y 0O  2 25TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 35'UUID:'
     A            UUUID         36A  O  2 41TEXT('Turnr')
     A                                      COLOR(BLU)
     A  49                              3  2'Velg   5=Se oppdrag   15=Se SAD'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg   2=Endre   3=Kopier   4=Slet-
     A                                      t   5=Se oppdrag   9=Angre sletting-
     A                                         15=Se SAD'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag CN St Mottaker    -
     A                                                         Avsender        -
     A                                                     Transport ref.'
     A                                      COLOR(WHT)
     A                                  5  2' '
      *
     A          R BUNN
     A                                 26  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             26 42'F6=Lag consignment   F16=Plukk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Lag ny consignment')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A N49                                  CF16(16 'Plukk')
     A                                  1  2'SYSPED/SAD112R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold kurermanifest - Liste'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            UAVD           4Y 0O  2 12TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 18'Turnr:'
     A            UPRO           8Y 0O  2 25TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 35'UUID:'
     A            UUUID         36A  O  2 41TEXT('Turnr')
     A                                      COLOR(BLU)
     A  49                              3  2'Velg   5=Se oppdrag   15=Se SAD'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg   2=Endre   3=Kopier   4=Slet-
     A                                      t   5=Se oppdrag   9=Angre sletting-
     A                                         15=Se SAD'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag CN St Mottaker    -
     A                                                         Avsender        -
     A                                                     Transport ref.'
     A                                      COLOR(WHT)
     A                                  5  2' '
     A                                 26  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             26 42'F6=Lag consignment   F16=Plukk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
