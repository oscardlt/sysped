********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 RETTELSE ETTER UTVIDELSE AV OPPDRAGSNR
      * 02 jov PTF10 Feil ved søk via transp/periode
********************************************************************
      *
      ****************************************************************
      *** PROGRAM SKAL SPØRRE PÅ RUNDTURER                         ***
      *** INDIKATORER 01 - 25 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 01    04 07 09-11 13-24               ***
      *** LEDIGE INDIKATORER 37-90 95-97                           ***
      ****************************************************************
      *
     FTELL      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FTURER12   IF   E           K DISK
     FTURER2    IF   E           K DISK
     F                                     RENAME(TURERR:TURE2R)
     FTURER4    IF   E           K DISK
     F                                     RENAME(TURERR:TURE5R)
     FTURER5    IF   E           K DISK
     F                                     RENAME(TURERR:TURE4R)
     FTURER6    IF   E           K DISK
     F                                     RENAME(TURERR:TURE6R)
     FTURER8    IF   E           K DISK
     F                                     RENAME(TURERR:TURE8R)
     FTURER9    IF   E           K DISK
     F                                     RENAME(TURERR:TURE9R)
     FSYTR90FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  YSONF2                 1      5
     D  AL                     1      5
     D                                     DIM(5)
     D                 DS
     D  YSONT2                 1      5
     D  AL2                    1      5
     D                                     DIM(5)
     D                 DS
     D  WSBDAT                 1      6  0
     D  WSDAG                  1      2  0
     D  WSMND                  3      4  0
     D  WSAAR                  5      6  0
     D                 DS
     D  WSDATF                 1      6  0
     D  WSDTFD                 1      2  0
     D  WSDTFM                 3      4  0
     D  WSDTFÅ                 5      6  0
     D                 DS
     D  TUDT                   1      8  0
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D                 DS
     D  WSDATT                 1      6  0
     D  WSDTTD                 1      2  0
     D  WSDTTM                 3      4  0
     D  WSDTTÅ                 5      6  0
     D                 DS
     D  TUDTT                  1      8  0
     D  TUDTTÅ                 1      4  0
     D  TUDTTM                 5      6  0
     D  TUDTTD                 7      8  0
     D                 DS
     D  TUNAT                  1     30
     D  TRNA17                 1     17
     C*
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   SETOFF                                           98
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   26WSBDAT        IFEQ      0
     C                   MOVE      *ZEROS        WSBDT8            8 0
     C                   ELSE
     C     WSBDAT        IFEQ      999999
     C                   MOVE      99999999      WSBDT8
     C                   ELSE
     C                   MOVE      WSBDAT        WSBDT6            6 0
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C                   PARM                    WSBDT6
     C     WSBDT6        IFGT      501231
     C     WSBDT6        ADD       19000000      WSBDT8
     C                   ELSE
     C     WSBDT6        ADD       20000000      WSBDT8
     C                   END
     C                   END
     C                   END
     C   26TURE2K        SETGT     TURER2
     C   27TUR12K        SETGT     TURER12
     C   28TURE5K        SETLL     TURER5
     C   29TURE6K        SETLL     TURER6
     C   31TURE8K        SETLL     TURER8
     C   32TURE9K        SETLL     TURER9
     C   44TURE4K        SETLL     TURER4
     C     START1        TAG
     C                   EXSR      LESFIL
      *
     C     START2        TAG
     C*
     C* START PÅ INDIKATOR 94 FOR LESING PÅ SUBFIL
     C*
     C  N94              DO
     C                   DO        12
     C                   ADD       1             ZRECNO
     C                   MOVE      TUDTD         WSDTFD
     C                   MOVE      TUDTM         WSDTFM
     C                   MOVE      TUDTÅ         WSDTFÅ
     C                   MOVE      TUDTTD        WSDTTD
     C                   MOVE      TUDTTM        WSDTTM
     C                   MOVE      TUDTTÅ        WSDTTÅ
     C                   WRITE     SUBFIL
     C                   EXSR      LESFIL
     C  N94              END
     C                   END
     C*
     C* EKSEKVERER SKJERMBILDET
     C*
     C                   MOVE      ZRECNO        XRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C   03              GOTO      SLUTT
     C   12              GOTO      START
     C*
     C* F05 OPPDATER BILDET
     C*
     C   05              DO
     C                   GOTO      START
     C                   END
     C*
     C* F08 = VELG SØKEBEGREP
     C*
     C   08              DO
     C                   GOTO      START6
     C                   END
     C*
     C*** BLA VIDERE
     C*
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C* ENTER ER TASTET TEST HVA SOM ER TASTET
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENTSR
     C   03              GOTO      SLUTT
     C                   GOTO      START3
     C                   END
     C*
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     2             POSNBR
     C     STAR6B        TAG
     C                   EXFMT     SØKTUR
     C                   SETOFF                                       9930
     C   03              GOTO      SLUTT
     C   12              GOTO      START3
     C*
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     LINNBR        IFEQ      21
     C                   CALL      'SYTR19'
     C                   PARM                    WSBILN
     C                   ELSE
     C     LINNBR        IFEQ      22
     C                   CALL      'SYTR04A'
     C                   PARM                    WSKNT2
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   WRITE     SUBCTL
     C                   GOTO      STAR6B
     C                   END
     C                   END
     C                   EXSR      TSTSØK
     C   30              GOTO      START6
     C     WSMNDT        IFEQ      0
     C     WSAART        ANDEQ     0
     C                   MOVE      *ZEROS        WSACCT            2 0
     C                   ELSE
     C     WSAART        IFGT      50
     C                   MOVE      19            WSACCT
     C                   ELSE
     C                   MOVE      20            WSACCT
     C                   END
     C                   END
     C                   GOTO      START
     C                   END
     C*
     C                   EXSR      TSTSØK
N02  C     WSMNDT        IFEQ      0
N02  C     WSAART        ANDEQ     0
N02  C                   MOVE      *ZEROS        WSACCT
N02  C                   ELSE
N02  C     WSAART        IFGT      50
N02  C                   MOVE      19            WSACCT
N02  C                   ELSE
N02  C                   MOVE      20            WSACCT
N02  C                   END
N02  C                   END
     C   30              GOTO      START6
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TILTUR        BEGSR
     C***********************************************
     C                   MOVE      'J'           USPØR
     C                   MOVE      *ZEROS        NULL4             4 0
     C                   MOVE      *ZEROS        NULL5             5 0
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYTR88A'
     C                   PARM                    FIFIRM
     C                   PARM                    TUAVD
     C                   PARM                    TUPRO
     C                   PARM                    TURUND
     C                   PARM                    NULL5
     C                   PARM                    NULL4
     C                   PARM                    USPØR             1
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      12
     C     UFLAGG        COMP      3                                      03
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   SETOFF                                           98    30
     C                   Z-ADD     0             ANTLES            2 0
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C*
     C* VED 0 LESTE GI FEILMELDING
     C*
     C   98ANTLES        IFEQ      0
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   GOTO      SLHENT
     C                   ELSE
     C                   GOTO      SLHENT
     C                   END
     C*
     C                   ADD       1             ANTLES
     C*
     C* 5 = SPØRRING RUNDTUR    9=UTSKRIFT
     C*
     C     WSNR          IFEQ      '5'
     C                   CALL      'SYTR91'
     C                   PARM                    TURUND
     C                   PARM                    USKJER
     C                   PARM                    WSNR
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C*
     C*   9=UTSKRIFT
     C*
     C     WSNR          IFEQ      '9'
     C                   CALL      'SYTR91CL'
     C                   PARM                    TUAVD
     C                   PARM                    TURUND
     C                   PARM                    USKJER
     C                   PARM                    WSNR
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C*
     C* 6= SPØRRING TUR
     C     WSNR          IFEQ      '6'
     C                   EXSR      TILTUR
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C*
     C     WSNR          IFEQ      '8'
     C                   MOVE      'J'           XXVIS             1
     C                   CALL      'SYTR12A'
     C                   PARM                    TUAVD
     C                   PARM                    TUPRO
     C                   PARM                    XXVIS
     C                   PARM                    N4                4 0
S01  C*                  PARM                    N5                5 0
N01  C                   PARM                    N7                7 0
     C                   PARM                    N8                8 0
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C*
     C* END  98
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     TSTSØK        BEGSR
     C***********************************************
     C                   SETOFF                                       703099
     C                   SETOFF                                       3132
     C                   MOVEA     '0000'        *IN(26)
     C                   SETOFF                                       44
     C*
     C                   EXSR      MAX
     C* TEST
     C     WSSKOD        IFNE      ' '
     C     WSSKOD        ANDNE     '0'
     C     WSSKOD        ANDNE     'A'
     C     WSSKOD        ANDNE     'B'
     C     WSSKOD        ANDNE     'C'
     C     WSSKOD        ANDNE     'D'
     C     WSSKOD        ANDNE     'P'
     C                   MOVE      'DIV4040'     MSGNR
     C                   SETON                                        3070
     C                   END
     C*
     C     WSAVD         IFNE      0
     C     WSAVD         CHAIN     TELL                               99
     C   99AVDZER        CHAIN     TELL                               99
     C   99              MOVE      'SPA0009'     MSGNR
     C   99              SETON                                        30
     C  N99WSRUND        IFEQ      *ZEROS
     C                   MOVE      99999999      WSRUND
     C                   END
     C  N99              MOVEA     '0100'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSRUND        IFNE      *ZEROS
     C                   MOVEA     '0100'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C*
     C     WSKNT2        IFNE      *ZEROS
     C                   SETON                                        44
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSBILN        IFNE      *BLANKS
     C     WSBDAT        IFEQ      *ZEROS
     C                   MOVE      999999        WSBDAT
     C                   END
     C                   MOVEA     '1000'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C*
     C     WSSTEF        IFNE      *BLANKS
     C                   MOVEA     '0010'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSSONF        IFNE      *BLANKS
     C                   MOVEA     '0001'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C*
     C     WSSTET        IFNE      *BLANKS
     C                   SETON                                        31
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSSONT        IFNE      *BLANKS
     C                   SETON                                        32
     C                   GOTO      SLTST
     C                   END
     C*
     C                   MOVE      'DIV0983'     MSGNR
     C                   SETON                                        9930
     C*
     C     SLTST         ENDSR
     C*
     C***********************************************
     C     MAX           BEGSR
     C***********************************************
     C                   MOVE      WSSTEF        YSTEF             5
     C                   MOVE      WSSTEF        YSTEF2            5
     C                   MOVE      WSSTET        YSTET             5
     C                   MOVE      WSSTET        YSTET2            5
     C                   MOVE      WSSONF        YSONF             5
     C                   MOVE      WSSONF        YSONF2            5
     C                   MOVE      WSSONT        YSONT             5
     C                   MOVE      WSSONT        YSONT2            5
     C*
     C     YSTEF2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSTEF2
     C                   END
     C*
     C     YSTET2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSTET2
     C                   END
     C*
     C                   Z-ADD     5             XN                2 0
     C     YSONF2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSONF2
     C                   ELSE
     C     STMBIL        TAG
     C     AL(XN)        IFEQ      ' '
     C                   MOVE      '9'           AL(XN)
     C                   SUB       1             XN
     C                   GOTO      STMBIL
     C                   END
     C                   END
     C*
     C                   Z-ADD     5             XN                2 0
     C     YSONT2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSONT2
     C                   ELSE
     C     STMBI2        TAG
     C     AL2(XN)       IFEQ      ' '
     C                   MOVE      '9'           AL2(XN)
     C                   SUB       1             XN
     C                   GOTO      STMBI2
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     LESFIL        BEGSR
     C***********************************************
     C     STLES         TAG
     C   26              READP     TURER2                                 94
     C   27              READP     TURER12                                94
     C   28              READ      TURER5                                 94
     C   29              READ      TURER6                                 94
     C   31              READ      TURER8                                 94
     C   32              READ      TURER9                                 94
     C   44              READ      TURER4                                 94
     C*
     C* ALLE "LEST FOR LANGT"-TESTER FLYTTET HIT --> LETTLEST LOGIKK.
     C* TESTER VED SØKING TRANSPORTØRNUMMER
     C   44
     CANN94WSKNT2        COMP      TUKNT2                             9494
     C* TESTER VED SØKING BILNUMMER
     C   26
     CANN94WSBILN        COMP      TUBILN                             9494
     C*
     C* SØKING VED STED FRA UTFYLLT
     C   28
     CANN94TUSTEF        COMP      YSTEF2                             94
     C*
     C* SØKING VED SONE FRA UTFYLLT
     C   29
     CANN94TUSONF        COMP      YSONF2                             94
     C*
     C* SØKING VED STED TIL UTFYLLT
     C   31
     CANN94TUSTET        COMP      YSTET2                             94
     C*
     C* SØKING VED SONE TIL UTFYLLT
     C   32
     CANN94TUSONT        COMP      YSONT2                             94
     C*
     C   94              GOTO      UTLES
     C* (TEST N94 ETTER DETTE TATT BORT - IF..IF.. --> IF..AND..)
     C*
     C* GENERELLE TESTER - NÅR IKKE KEYBEGREP-BARE BEGRENSNING...:
     C* TESTER STATUSKODE
     C*
     C     WSSKOD        IFNE      *BLANKS
     C     WSSKOD        IFEQ      '0'
     C     TUST          IFNE      ' '
     C                   GOTO      STLES
     C                   END
     C                   ELSE
     C     WSSKOD        IFNE      TUST
     C                   GOTO      STLES
     C                   END
     C                   END
     C                   ELSE
     C     TUST          IFEQ      'D'
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C* GENERELL TEST HVIS FRA STED ER TASTET
     C*
     C     YSTEF2        IFNE      '99999'
     C     TUSTEF        ANDNE     YSTEF2
     C                   GOTO      STLES
     C                   END
     C*
     C* GENERELL TEST HVIS TIL STED ER TASTET
     C*
     C  N31YSTET2        IFNE      '99999'
     C                   GOTO      STLES
     C                   END
     C*
     C* GENERELL TEST HVIS FRA SONE ER TASTET
     C*
     C     YSONF2        IFNE      '99999'
     C     TUSONF        IFGT      YSONF2
     C                   GOTO      STLES
     C                   END
     C     TUSONF        IFLT      WSSONF
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C* GENERELL TEST HVIS AVDELING ER SELEKTERT
     C*
     C     WSAVD         IFNE      *ZEROS
     C     TUAVD         ANDNE     WSAVD
     C                   GOTO      STLES
     C                   END
     C*
     C*
     C* GENERELL TEST HVIS TIL SONE ER TASTET
     C*
     C  N32YSONT2        IFNE      '99999'
     C     TUSONT        ANDGT     YSONT2
     C                   GOTO      STLES
     C                   END
     C*
     C     UTLES         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TUR12K        KLIST
     C                   KFLD                    WSRUND
     C     TURE2K        KLIST
     C                   KFLD                    WSBILN
     C                   KFLD                    WSBDT8
     C     TURE4K        KLIST
     C                   KFLD                    WSKNT2
     C                   KFLD                    WSACCT
     C                   KFLD                    WSAART
     C                   KFLD                    WSMNDT
     C     TURE5K        KLIST
     C                   KFLD                    YSTEF
     C                   KFLD                    YSTET
     C     TURE6K        KLIST
     C                   KFLD                    YSONF
     C                   KFLD                    YSONT
     C     TURE8K        KLIST
     C                   KFLD                    YSTET
     C     TURE9K        KLIST
     C                   KFLD                    YSONT
     C                   Z-ADD     99999999      WSRUND
     C                   MOVE      ' '           YYST              1
     C*
     C     *ENTRY        PLIST
     C     URUND         PARM      URUND         URUND             8 0
     C     USKJER        PARM      USKJER        USKJER           10
     C*
     C*
     C     *LIKE         DEFINE    WSAVD         AVDZER
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *BLANKS       YSONF
     C                   MOVE      *BLANKS       YSONF2
     C                   MOVE      *BLANKS       YSONT
     C                   MOVE      *BLANKS       YSONT2
     C                   MOVE      *BLANKS       YSTEF
     C                   MOVE      *BLANKS       YSTEF2
     C                   MOVE      *BLANKS       YSTET
     C                   MOVE      *BLANKS       YSTET2
     C                   MOVE      ' '           WSSKOD
     C                   MOVE      *ZEROS        WSBDAT
     C*
     C* SETTER INDIKATOR PÅ FOR SØKING PÅ AVDELING INITIELLT
     C*
     C                   READ      FIRM                                   33
     C                   SETON                                        27
     C                   EXSR      TSTSØK
     C*
     C*
     C                   ENDSR
     C*
