     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R B10
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'SØK')
     A  89                                  CF11(11 'DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(01)
     A                                      CLRL(24)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYGB01R'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 TBR0100 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 23MSGCON(030 TBR0219 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            WMODE          7A  O  4 32MSGID(&WMODNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  3MSGCON(010 TBR0231 *LIBL/SYMSGF)
     A            VTEKST         3   B  8 27
     A                                 10  3MSGCON(010 TBR0232 *LIBL/SYMSGF)
     A            VAVD           4Y 0B 10 27EDTCDE(4)
     A                                 12  3MSGCON(010 TBR0233 *LIBL/SYMSGF)
     A            VKF            1   B 12 27VALUES('X' ' ')
     A                                 12 35MSGCON(050 TBR0237 *LIBL/SYMSGF)
     A                                 14  3MSGCON(020 TBR0235 *LIBL/SYMSGF)
     A            VAVIK          2Y 0B 14 27EDTCDE(4)
     A                                 16  3MSGCON(020 TBR0236 *LIBL/SYMSGF)
     A            VRED           2Y 0B 16 27EDTCDE(4)
     A                                 23  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 70MSGCON(010 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            WMODNR         7A  P
     A          R B20
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYGB01R'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 TBR0100 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 23MSGCON(030 TBR0219 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  4 40'  2=Endre  4=Slette'
     A                                      COLOR(BLU)
     A                                  6  3'Valg'
     A                                      COLOR(WHT)
     A                                  6  9MSGCON(010 TBR0234 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            FTEKST         3   B  7  9
     A                                 10 10'(Ingen poster i filen)   '
     A  30        MSGTXT        78A  O 24  2TEXT('FEIL I INNTASTEDE DATA')
     A                                      COLOR(WHT)
      *
     A          R B30
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'SØK')
     A  89                                  CF11(11 'DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(01)
     A                                      CLRL(24)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYGB01R'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 TBR0100 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 23MSGCON(030 TBR0219 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            WMODE          7A  O  4 32MSGID(&WMODNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  3MSGCON(010 TBR0231 *LIBL/SYMSGF)
     A            VTEKST         3A  O  7 27
     A                                  9  3MSGCON(010 TBR0232 *LIBL/SYMSGF)
     A N80        VAVD           4Y 0B  9 27EDTCDE(4)
     A  80        BAVD           4S 0O  9 27
     A                                 11  3MSGCON(015 TBR0233 *LIBL/SYMSGF)
     A N80        VKF            1   B 11 27
     A  80        BKF            1A  O 11 27
     A                                 13  3MSGCON(020 TBR0235 *LIBL/SYMSGF)
     A N80        VAVIK          2Y 0B 13 27EDTCDE(4)
     A  80        BAVIK          2S 0O 13 27
     A                                 15  3MSGCON(020 TBR0236 *LIBL/SYMSGF)
     A N80        VRED           2Y 0B 15 27EDTCDE(4)
     A  80        BRED           2S 0O 15 27
     A  80                             20  8MSGCON(040 TBR0110 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 70MSGCON(010 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2TEXT('FEIL I INNTASTEDE DATA')
     A                                      COLOR(WHT)
     A            WMODNR         7A  P
     A          R B40
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A N31                                  CF05(05 'Forny')
     A                                      CF06(06 'Ny linje')
     A  31                                  CF12(12 'Avbryt')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(010 TBR0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(010 TBR0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2TEXT('FEIL I INNTASTEDE DATA')
      *
     A          R H50                       SFL
     A            SFRRN         10S 0H
     A            WSNR           2A  I  9  3VALUES('  ' '2 ' '02' ' 2' '4 ' '04-
     A                                      ' ' 4')
     A            STEKST         3A  O  9  9
     A            SAVD           4S 0O  9 18
     A            SKF            1   O  9 31
     A            SAVIK          2  0O  9 45
     A            SRED           2  0O  9 68
      *
     A          R K60                       SFLCTL(H50)
     A                                      SFLSIZ(2000)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  3'SYSPED/SYGB01R'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 TBR0100 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 23MSGCON(030 TBR0219 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  4 40'  2=Endre  4=Slette'
     A                                      COLOR(BLU)
     A                                  6  9MSGCON(006 TBR0220 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 17MSGCON(008 TBR0221 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 28MSGCON(008 TBR0228 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 39MSGCON(015 TBR0222 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 60MSGCON(020 TBR0226 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  2'Valg'
     A                                      COLOR(WHT)
     A                                  7  9MSGCON(004 TBR0225 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 28MSGCON(006 TBR0229 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 39MSGCON(015 TBR0223 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 60MSGCON(020 TBR0227 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            FTEKST         3A  B  8  9
     A                                  8 39MSGCON(018 TBR0224 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 28MSGCON(008 TBR0230 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
