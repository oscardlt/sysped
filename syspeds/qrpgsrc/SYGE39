     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
     FSYPA01F   IF   E           K DISK
      *__________
      * Variabler
      *""""""""""
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
     D Idx             s              5u 0
     D                SDS
     D  ZUSER                254    263
      *___________
      * Konstanter
      *"""""""""""
     D LF              c                   x'25'
     D progkall        C                   CONST('RunDll32.exe url.dll -
     D                                     ,FileProtocolHandler %p1%')
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfgets            Pr              *   ExtProc( '_C_IFS_fgets' )
     D                                 *   Value
     D                               10i 0 Value
     D                                 *   Value

     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value

      *______________________________________________
      * inn parameter filmappe finnes i dette program
      *""""""""""""""""""""""""""""""""""""""""""""""
     c     *entry        plist
     c                   parm                    upara1          512
     c                   parm                    filmappe         42
     c                   parm                    jn                1
      *
      *______________________________________________
      * Legger på parameterinfor på inngående streng
      *""""""""""""""""""""""""""""""""""""""""""""""
     c                   eval      uparany='set p1="'
     c     uparany       cat       upara1:0      uparany         512
     c     uparany       cat       '"':0         uparany
      *______________________________________________
      * Henter nødvendige parametre fra parameterfil
      *""""""""""""""""""""""""""""""""""""""""""""""
     c     userk         klist
     c                   kfld                    uuser            10
     c                   eval      uuser=zuser
     c     userk         chain     sypa01f                            33
     c   33              move      'N'           jn
     c  n33pastas        ifeq      ' '
     c                   move      'N'           jn
     c                   end
     c     *in(33)       ifeq      '1'
     c                   clear                   uuser
     c     userk         chain     sypa01f                            33
     c  N33              move      ' '           jn
     c  n33pastas        ifeq      ' '
     c                   move      'N'           jn
     c                   goto      slutt
     c                   end
     c   33              move      'N'           jn
     c     *in(33)       cabeq     '1'           slutt
     c                   end
      *_________________________________________________
      * flytter inn bane og nettverskstasjon til workfil
      *"""""""""""""""""""""""""""""""""""""""""""""""""
     c                   eval      filmappe=pastas
     c     filmappe      cat       ':/':0        filmappe
     c     filmappe      cat       pamapp:0      filmappe
     c     filmappe      cat       '/':0         filmappe
     c     filmappe      cat       zuser:0       filmappe
     c     filmappe      cat       '.bat':0      filmappe
      *
      * filnavn uten stasjonsnavn
      *
     c                   eval      Filnam='/'
     c     filnam        cat       pamapp:0      filnam           30
     c     filnam        cat       '/':0         filnam
     c     filnam        cat       zuser:0       filnam
     c     filnam        cat       '.bat':0      filnam
      *_____________________________
      * Lag fil og set codepage (pc)
      *"""""""""""""""""""""""""""""
     C                   EVAL      FILE_o = open( %Trim( FilNam )
     C                             : 'w, codepage=850' )

     C                   EVAL      rc = close( FILE_o )
      *________________________________________
      * Åpne fil, med tegnkonvertering for jobb
      *""""""""""""""""""""""""""""""""""""""""
     C                   EVAL      FILE_o = open( %Trim( FilNam )
     C                             : 'w' )

     C                   IF        FILE_o <> *Null

     C                   EVAL      rc = fputs( %trim(uparany) + LF : FILE_o )
     C                   EVAL      rc = fputs( %trim(progkall) + LF : FILE_o )
      *
     C                   EVAL      rc = close( FILE_o )
     C                   ENDIF
      *
     C     SLUTT         TAG
     C                   EVAL      *INLR = *On
     C                   RETURN
