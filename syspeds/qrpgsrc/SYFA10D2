      *
      ***********************************************************
      *** PROGRAM SKAL PLUKKE UnNummer                  ***
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 returner også evt index (v fler like UNnr/emb-grp)
*********************************************************1**********
      * BRINGTMS-prosjekt.
     FADRUNNRF  IF   E           K DISK
     FSYFA10D2D CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D W2filter        s             30    VARYING
     D                 DS
     D ADTEXT                  1    150
     D  UNBESKA                1     40
     D  UNBESKB               41    125
N01  D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
N01  D                                     UVWXYZÆØÅ')
N01  D LO              C                   CONST('abcdefghijklmnopqrst-
N01  D                                     uvwxyzæøå')
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     c                   move      *blanks       W2Max             4
     c                   if        W2Kod <> *blanks
     c                   eval      W2Max = %trim(W2KOD)+'999'
     c                   endif
     C                   eval      W2filter = %trim(W2SØK)
     C     ADRKey        SETLL     ADRUNNRF
     C                   READ      ADRUNNRF                               94
     C     *in94         Doweq     '0'
     c                   if        W2Max <> *blanks
     c                             and ADUNNR > W2Max
     c                   leave
     c                   endif
     c                   if        W2filter <> *blanks
     C     Lo:Up         xlate     ADTEXT        TSTText         150
     C     W2filter      SCAN      TSTText                                33
     c     *IN33         CaBEQ     *OFF          NextRec
     c                   endif
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     c     NextRec       tag
     C                   READ      ADRUNNRF                               94
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     *IN29         IFEQ      '0'
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      '1'           *IN29
     C                   ELSE
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     c                   z-add     1             ZRECNO
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *
     C     W2KOD         IFNE      *blanks
     C                   GOTO      START
     C                   END
     C     W2SØK         IFNE      *BLANKS
     C                   GOTO      START
     C                   END
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   GOTO      SLUTT
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       9192
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'
     C                   MOVE      ADUNNR        UUNNR
     C                   MOVE      ADEMBG        UEMBG
N01  C                   MOVE      ADINDX        UINDX
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C     *IN98         IFEQ      '1'
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     ADRKey        KLIST
     C                   KFLD                    W2KOD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUNNR             4
     C                   PARM                    UEMBG             3
N01  C                   PARM                    UINDX             1
      *
     C                   move      UUNNR         W2KOD
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
     C*
