     A*%%TS  SD  20131028  123924  SVEIN       REL-V7R1M0  5770-WDS
N01  A* NYE FILTER / FJERNET CHECK(RB)
N02  A* NYE FILTER  32 ON = PROTECT/NONDISPLAY EKSTRAFILTER
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      HLPPNLGRP(SYOP010000 SYOP01PNL)
     A                                      HLPTITLE('REGISTRERING AV OPPDRAG')
     A                                      ALTHELP
     A                                      HELP
     A          R BILDE02
N02  A                                      CF02(02 'PROTECT FILTER')
     A                                      CF04(04 'Søking')
     A                                      CF20(20 'Direkte status forespørsel-
     A                                      ')
     A                                      CF21(21 'Status forespørsel')
     A                                      CF22(22 'TdV IS loggfunksjon')
     A                                      CF23(23 'Availability forespørsel')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            MSGHNR         7A  P
     A            MSGF11         7A  P
     A            MSGF12         7A  P
     A            MSGF13         7A  P
     A                                  1  2'SYSPED/SYOP01R'
     A                                      COLOR(BLU)
     A            MSGH01        35A  O  1 20MSGID(&MSGHNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  4  2MSGCON(023 DIV1497 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
S03  A*                                 4 38MSGCON(024 DIV0457 *LIBL/SYMSGF)
     A                                  4 34MSGCON(010 JOV1525 *LIBL/SYMSGF)
N02  A                                  4 50MSGCON(020 JOV1536 *LIBL/SYMSGF)
N02  A                                      COLOR(WHT)
N02  A  32                              4 71MSGCON(010 JOV1556 *LIBL/SYMSGF)
N02  A                                      COLOR(BLU)
N02  A N32                              4 71MSGCON(010 JOV1557 *LIBL/SYMSGF)
N02  A                                      COLOR(BLU)
     A            KOANVN        33A  O  5  2TEXT('AVDELINGSNAVN')
     A                                      COLOR(WHT)
     A                                  5 50MSGCON(011 JOV1537 *LIBL/SYMSGF)
N02  A  32                                  DSPATR(ND)
     A            WSVKTO         5Y 0B  5 63DSPATR(UL)
N02  A  32                                  DSPATR(PR)
N02  A  32                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                  5 69MSGCON(006 JOV1538 *LIBL/SYMSGF)
N02  A  32                                  DSPATR(ND)
     A            WSVKTU         5Y 0B  5 76DSPATR(UL)
N02  A  32                                  DSPATR(PR)
N02  A  32                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                  6 10MSGCON(035 DIV0051 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 50MSGCON(031 JOV1539 *LIBL/SYMSGF)
N02  A  32                                  DSPATR(ND)
     A                                  7  2MSGCON(023 SHU1098 *LIBL/SYMSGF)
     A            WSDT9W         6Y 0B  7 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      EDTCDE(4)
     A                                  7 50MSGCON(002 JOV1540 *LIBL/SYMSGF)
N02  A  32                                  DSPATR(ND)
     A            WSHNAV        12A  B  7 53
N02  A  32                                  DSPATR(PR)
N02  A  32                                  DSPATR(ND)
     A                                  7 66MSGCON(002 JOV1541 *LIBL/SYMSGF)
N02  A  32                                  DSPATR(ND)
     A            WSLNAV        12A  B  7 69
N02  A  32                                  DSPATR(PR)
N02  A  32                                  DSPATR(ND)
     A                                  8  2MSGCON(023 DIV0052 *LIBL/SYMSGF)
     A            WSFN           7Y 0B  8 28DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A            WSKOST         1A  B  8 36
     A                                  8 38MSGCON(011 JOV1526 *LIBL/SYMSGF)
     A                                  9  2MSGCON(023 DIV0053 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B  9 28DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 10  2MSGCON(023 DIV0054 *LIBL/SYMSGF)
     A            WSSG           3A  B 10 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 10 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT1W         6Y 0B 10 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 11  2MSGCON(023 DIV0055 *LIBL/SYMSGF)
     A            WSGNA          4A  B 11 28DSPATR(UL)
     A                                      COLOR(GRN)
     A            WSGNB         11A  B 11 33DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 11 46MSGCON(004 DIV3167 *LIBL/SYMSGF)
     A            WSGNÅR         2Y 0B 11 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 12  2MSGCON(023 DIV0060 *LIBL/SYMSGF)
     A            WSKN           8Y 0B 12 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 12 37'Type:'
     A            WSTYPE         2A  B 12 43DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      VALUES('  ' 'A ' 'K ' 'S ' 'KF' 'SF-
     A                                      ')
     A                                 12 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT2W         6Y 0B 12 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 12 61'(A /K /S /KF/SF)'
     A                                 13  2MSGCON(023 SHU1271 *LIBL/SYMSGF)
     A            WSGM1         15A  B 13 28
     A                                 14  2MSGCON(023 DIV0056 *LIBL/SYMSGF)
     A            WSOT           2A  B 14 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 14 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT4W         6Y 0B 14 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 15  2MSGCON(023 DIV0057 *LIBL/SYMSGF)
     A            WSRFA         15A  B 15 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 15 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDTAW         6Y 0B 15 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 16  2MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B 16 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 17  2MSGCON(023 DIV5090 *LIBL/SYMSGF)
     A            WSHAWB         9A  B 17 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 17 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT5W         6Y 0B 17 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 18  2MSGCON(023 SHU0016 *LIBL/SYMSGF)
     A            WSCONT        17A  B 18 28DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 18 46MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT6W         6Y 0B 18 53DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 19  2MSGCON(023 BBA0037 *LIBL/SYMSGF)
     A            WAIRLI         3Y 0B 19 28TEXT('Airline Prefix')
     A                                      EDTCDE(4)
     A            WAWBNO         8Y 0B 19 33TEXT('AWB Serial Number')
     A                                      EDTCDE(4)
     A            WAIRCD         3A  B 19 43TEXT('Airline code')
     A                                 20  2MSGCON(023 SHU0128 *LIBL/SYMSGF)
     A            WSFRI1         3A  B 20 28
     A            WSFRI2        35A  B 20 33
     A                                 20 69MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT8W         6Y 0B 20 75DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 21  2MSGCON(023 JOV0768 *LIBL/SYMSGF)
     A            WSSAML         6A  B 21 28
     A                                 21 36MSGCON(014 SHU0622 *LIBL/SYMSGF)
     A            WSSDF          5A  B 21 51
     A            WSSDT          5A  B 21 58
     A                                 21 64MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT7W         6Y 0B 21 70DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                 22  2MSGCON(023 YBC0551 *LIBL/SYMSGF)
     A            WSUTL1         2A  B 22 28
     A            WSUTL2         2A  B 22 32
     A            WSUTL3         2A  B 22 36
     A            WSUTL4         2A  B 22 40
     A            WSUTL5         2A  B 22 44
     A                                 22 53MSGCON(012 YBC0552 *LIBL/SYMSGF)
     A            MSGB11        18A  O 23  2MSGID(&MSGF11 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB12        18A  O 23 22MSGID(&MSGF12 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB13        18A  O 23 42MSGID(&MSGF13 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20131028  123924  SVEIN       REL-V7R1M0  5770-WDS
     A            HEDTOP         8S 0H
     A            HEGN          15A  H
     A            HEPOS1         7A  H
     A            HESGM         10A  H
     A            WSNR           2Y 0B  7  2EDTCDE(4)
     A            HEAVD          4Y 0O  7  5EDTCDE(4)
     A  89                                  COLOR(RED)
     A            HEOPD          7Y 0O  7 10EDTCDE(4)
     A  42                                  COLOR(WHT)
     A  88                                  DSPATR(RI)
     A            HESG           3A  O  7 18
     A  43                                  COLOR(WHT)
     A            WSDTOP         6Y 0O  7 22EDTCDE(4)
     A  39                                  COLOR(WHT)
     A            HEST           1A  O  7 29
     A            HEUR           1A  O  7 32COLOR(YLW)
     A            HEPRO          8Y 0O  7 34EDTCDE(4)
     A  49                                  COLOR(WHT)
     A            HEALT         15A  O  7 43
     A  48
     AO 53
     AO 67
     AO 51                                  COLOR(WHT)
     A            XFNFM          7Y 0O  7 59EDTCDE(4)
     A  41                                  COLOR(WHT)
     A            XNAFM          7A  O  7 66
     A  41                                  COLOR(WHT)
     A            HEOT           2A  O  7 74
     A  47                                  COLOR(WHT)
     A            QHF            1A  O  7 77COLOR(RED)
     A            QFSU           1A  O  7 79COLOR(RED)
     A            WSSELG        20A  O  7 82
     A            WSKJØP        20A  O  7103
     A            HEGN11        11A  O  8  2DSPATR(UL)
     A  44                                  COLOR(WHT)
     A            WSPOS1         4A  O  8 14DSPATR(UL)
     A            WSNT           5Y 0O  8 19EDTCDE(4)
     A                                      DSPATR(UL)
     A  27                                  COLOR(RED)
     A            HEVS1         25A  O  8 25DSPATR(UL)
     A            WSVKT          7Y 0O  8 51EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSM3           4Y 2O  8 59EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSLM           4Y 2O  8 65EDTCDE(4)
     A                                      DSPATR(UL)
     A            HEFR           3A  O  8 74DSPATR(UL)
     A            HESDF          5A  O  8 82DSPATR(UL)
     A  58                                  COLOR(WHT)
     A            HESDFT        14A  O  8 88DSPATR(UL)
     A            HESDT          5A  O  8103DSPATR(UL)
     A  58                                  COLOR(WHT)
     A            HESDTT        14A  O  8109DSPATR(UL)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20131028  122026  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE EN NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(08)
     A  28                                  CF10(10 'SØK TUR')
     A  49                                  CF11(11 'ARB. M/UTL.FORB.')
     A  35                                  CA14(14 'TIL BL')
     A  35                                  CA15(15 'TIL BOOKING')
     A                                      CF16(16 'LAGE FLERE NYE')
     A                                      CF22(22 'HURTIGBOOKING')
     A                                      CF23(23 'FLERE OPSJON')
     A                                      CF24(24 'FLERE TASTER')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  63
     AO 64
     AO 65
     AO 66                                  CF20(20 'Plukke ingående')
     A  63                                  CF21(21 'PLUKKE INN FLY')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPPNLGRP(SYOP010050 SYOP01PNL)
     A                                      HLPARA(02 001 02 130)
     A          H                           HLPPNLGRP(SYOP010100 SYOP01PNL)
     A                                      HLPARA(03 001 03 130)
     A          H                           HLPPNLGRP(SYOP010200 SYOP01PNL)
     A                                      HLPARA(04 001 04 028)
     A          H                           HLPPNLGRP(SYOP010400 SYOP01PNL)
     A                                      HLPARA(04 029 04 031)
     A          H                           HLPPNLGRP(SYOP010450 SYOP01PNL)
     A                                      HLPARA(04 032 04 033)
     A          H                           HLPPNLGRP(SYOP010200 SYOP01PNL)
     A                                      HLPARA(04 034 04 130)
     A          H                           HLPPNLGRP(SYOP010200 SYOP01PNL)
     A                                      HLPARA(05 001 05 130)
     A          H                           HLPPNLGRP(SYOP010300 SYOP01PNL)
     A                                      HLPARA(06 001 06 130)
     A          H                           HLPPNLGRP(SYOP010100 SYOP01PNL)
     A                                      HLPARA(07 001 07 003)
     A          H                           HLPPNLGRP(SYOP010400 SYOP01PNL)
     A                                      HLPARA(07 004 07 031)
     A          H                           HLPPNLGRP(SYOP010450 SYOP01PNL)
     A                                      HLPARA(07 032 07 033)
     A          H                           HLPPNLGRP(SYOP010400 SYOP01PNL)
     A                                      HLPARA(07 034 07 130)
     A          H                           HLPPNLGRP(SYOP010400 SYOP01PNL)
     A                                      HLPARA(08 001 08 130)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGF21         7A  P
     A            MSGHNR         7A  P
     A                                  1  2'SYSPED/SYOP01R'
     A                                      COLOR(BLU)
     A            MSGH01        35A  O  1 20MSGID(&MSGHNR SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A            WSAVDA         4Y 0B  2 13EDTCDE(4)
     A  96                                  DSPATR(HI)
     A            WSSGA          3A  B  2 19DSPATR(UL)
     A  95                                  DSPATR(HI)
     A                                  2 25MSGCON(008 YBC0066 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 34EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A  41                              2 39MSGCON(023 DIV0052 *LIBL/SYMSGF)
     A  42                              2 39MSGCON(023 DIV0053 *LIBL/SYMSGF)
     A  43                              2 39MSGCON(023 DIV0054 *LIBL/SYMSGF)
     A  44                              2 39MSGCON(023 DIV0055 *LIBL/SYMSGF)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55                              2 39MSGCON(023 DIV0060 *LIBL/SYMSGF)
     A  47                              2 39MSGCON(023 DIV0056 *LIBL/SYMSGF)
     A  48                              2 39MSGCON(023 DIV0057 *LIBL/SYMSGF)
     A  49                              2 39MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A  56                              2 39MSGCON(023 JOV0768 *LIBL/SYMSGF)
     A  41        WSFN           7Y 0O  2 63EDTCDE(4)
     A                                      COLOR(WHT)
     A  42        WSOPD          7Y 0O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  43        WSSG           3A  O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A  44        WSGN          15A  O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55        WSKN           8Y 0O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  47        WSOT           2A  O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A  48        WSRFA         15A  O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A  49        WSPRO          8Y 0O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  56        WSSAML         6A  O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A  43        WSDT1W         6S 0O  2 72DSPATR(UL)
     A                                      COLOR(WHT)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55        WSDT2W         6D 0O  2 72DSPATR(UL)
     A                                      COLOR(WHT)
     A  47        WSDT4W         6D 0O  2 72DSPATR(UL)
     A                                      COLOR(WHT)
     A            UUR            1A  O  2 79DSPATR(UL)
     A                                      COLOR(YLW)
S22  A*           MSGB21        78A  O  3  2MSGID(&MSGF21 SYMSGF)
N22  A            MSGB21       130A  O  3  2MSGID(&MSGF21 SYMSGF)
     A                                      COLOR(BLU)
     A            W2NR           2Y 0B  6  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  6  5EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2OPD          7Y 0B  6 10EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2SG           3A  B  6 18DSPATR(UL)
SV5  A*           W2DTR          6Y 0B  6 22EDTCDE(4)
     A            W2DTOP         6Y 0B  6 22EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2PRO          8Y 0B  6 34EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2RFA         15A  B  6 43DSPATR(UL)
     A            W2FN           7Y 0B  6 59EDTCDE(4)
     A                                      DSPATR(UL)
     A                                  4  2MSGCON(130 SHU1416 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(130 SHU1417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYOP010500 SYOP01PNL)
     A                                      HLPARA(01 001 02 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGB31        18A  O  1  2MSGID(&MSGF31 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB32        18A  O  1 22MSGID(&MSGF32 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB33        18A  O  1 42MSGID(&MSGF33 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            MSGF31         7A  P
     A            MSGF32         7A  P
     A            MSGF33         7A  P
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE EN NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(08)
     A  28                                  CF10(10 'SØK TUR')
     A  49                                  CF11(11 'ARB. M/UTL.FORB.')
     A  35                                  CA14(14 'TIL BL')
     A  35                                  CA15(15 'TIL BOOKING')
     A                                      CF16(16 'LAGE FLERE NYE')
     A  63
     AO 64
     AO 65
     AO 66                                  CF20(20 'Plukke ingående')
     A  63                                  CF21(21 'PLUKKE INN FLY')
     A                                      CF23(23 'FLERE OPSJON')
     A                                      CF24(24 'FLERE TASTER')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYOP010050 SYOP01PNL)
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPPNLGRP(SYOP010100 SYOP01PNL)
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPPNLGRP(SYOP010200 SYOP01PNL)
     A                                      HLPARA(04 001 04 028)
     A          H                           HLPPNLGRP(SYOP010400 SYOP01PNL)
     A                                      HLPARA(04 029 04 031)
     A          H                           HLPPNLGRP(SYOP010450 SYOP01PNL)
     A                                      HLPARA(04 032 04 033)
     A          H                           HLPPNLGRP(SYOP010200 SYOP01PNL)
     A                                      HLPARA(04 034 04 080)
     A          H                           HLPPNLGRP(SYOP010200 SYOP01PNL)
     A                                      HLPARA(05 001 05 080)
     A          H                           HLPPNLGRP(SYOP010300 SYOP01PNL)
     A                                      HLPARA(06 001 06 080)
     A          H                           HLPPNLGRP(SYOP010100 SYOP01PNL)
     A                                      HLPARA(07 001 07 003)
     A          H                           HLPPNLGRP(SYOP010400 SYOP01PNL)
     A                                      HLPARA(07 004 07 031)
     A          H                           HLPPNLGRP(SYOP010450 SYOP01PNL)
     A                                      HLPARA(07 032 07 033)
     A          H                           HLPPNLGRP(SYOP010400 SYOP01PNL)
     A                                      HLPARA(07 034 07 080)
     A          H                           HLPPNLGRP(SYOP010400 SYOP01PNL)
     A                                      HLPARA(08 001 08 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYOP01R'
     A                                      COLOR(BLU)
     A            MSGH01        35A  O  1 20MSGID(&MSGHNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A            WSAVDA         4Y 0B  2 13EDTCDE(4)
     A  96                                  DSPATR(HI)
     A            WSSGA          3A  B  2 19DSPATR(UL)
     A  95                                  DSPATR(HI)
     A                                  2 25MSGCON(008 YBC0066 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 34EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A  41                              2 39MSGCON(023 DIV0052 *LIBL/SYMSGF)
     A  42                              2 39MSGCON(023 DIV0053 *LIBL/SYMSGF)
     A  43                              2 39MSGCON(023 DIV0054 *LIBL/SYMSGF)
     A  44                              2 39MSGCON(023 DIV0055 *LIBL/SYMSGF)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55                              2 39MSGCON(023 DIV0060 *LIBL/SYMSGF)
     A  47                              2 39MSGCON(023 DIV0056 *LIBL/SYMSGF)
     A  48                              2 39MSGCON(023 DIV0057 *LIBL/SYMSGF)
     A  49                              2 39MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A  41        WSFN           7Y 0O  2 63EDTCDE(4)
     A                                      COLOR(WHT)
     A  42        WSOPD          7Y 0O  2 63DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  43        WSSG           3A  O  2 63DSPATR(UL)
     A                                      COLOR(BLU)
     A  44        WSGN          15A  O  2 63DSPATR(UL)
     A                                      COLOR(BLU)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55        WSKN           8Y 0O  2 63DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  47        WSOT           2A  O  2 63DSPATR(UL)
     A                                      COLOR(BLU)
     A  48        WSRFA         15A  O  2 63DSPATR(UL)
     A                                      COLOR(BLU)
     A  49        WSPRO          8Y 0O  2 63DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  43        WSDT1W         6S 0O  2 72DSPATR(UL)
     A                                      COLOR(WHT)
     A  45
     AO 46
     AO 50
     AO 54
     AO 55        WSDT2W         6D 0O  2 72DSPATR(UL)
     A                                      COLOR(BLU)
     A  47        WSDT4W         6D 0O  2 72DSPATR(UL)
     A                                      COLOR(BLU)
     A            UUR            1A  O  2 79DSPATR(UL)
     A                                      COLOR(YLW)
S22  A*           MSGB21        78A  O  3  2MSGID(&MSGF21 SYMSGF)
N22  A            MSGB21       130A  O  3  2MSGID(&MSGF21 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 YBC0104 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(078 YBC0038 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  6  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  6  5EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2OPD          7Y 0B  6 10EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2SG           3A  B  6 18DSPATR(UL)
SV5  A*           W2DTR          6Y 0B  6 22EDTCDE(4)
     A            W2DTOP         6Y 0B  6 22EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2PRO          8Y 0B  6 34EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2RFA         15A  B  6 43DSPATR(UL)
     A            W2FN           7Y 0B  6 59EDTCDE(4)
     A                                      DSPATR(UL)
     A            MSGB31        18A  O 23  2MSGID(&MSGF31 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB32        18A  O 23 22MSGID(&MSGF32 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGB33        18A  O 23 42MSGID(&MSGF33 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            MSGHNR         7A  P
     A            MSGF31         7A  P
     A            MSGF32         7A  P
     A            MSGF33         7A  P
     A            MSGF21         7A  P
     A          R VISLINK
     A*%%TS  SD  20111116  105743  JOVO        REL-V7R1M0  5770-WDS
     A                                  1  3'SYOP01R'
     A                                      COLOR(BLU)
     A                                  1 20'Klikk linken for å vise i browser.-
     A                                                '
     A                                      COLOR(WHT)
     A            HFLINK       100A  O  2  1COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  5  1'For å komme tilbake til SAD tast F-
     A                                      3(el Enter) med SAD-vinduet i fokus-
     A                                      (blå topp) (IKKE fokus på browser)'
     A                                  6  1'OBS! Dersom du åpner svært mange d-
     A                                      okument, husk å lukke - elles spise-
     A                                      s PCens memory opp.'
