     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                  1  3'SYSPED/SYEF94'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(050 DIV2201 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 21MSGCON(050 DIV2202 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 72TIME
     A                                 10  3MSGCON(018 DIV0599 *LIBL/SYMSGF)
     A            WSSG           3A  B 10 26DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 10 47MSGCON(030 DIV1447 *LIBL/SYMSGF)
     A                                 12  3MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 12 26DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 14  3MSGCON(018 DIV0601 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 14 26DSPATR(UL)
     A N99N98N96
     AO 97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 16  3MSGCON(018 DIV2078 *LIBL/SYMSGF)
     A            WSSD           3A  B 16 26DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A            WSLNR          3Y 0B 16 30DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  3MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(078 SPS0001 *LIBL/SYMSGF)
     A  98                             24  2MSGCON(078 SPA0001 *LIBL/SYMSGF)
     A  97                             24  2MSGCON(078 SPO0001 *LIBL/SYMSGF)
     A  96                             24  2MSGCON(078 SPS0038 *LIBL/SYMSGF)
     A          R BILDE02
     A                                      CF05(05 'Bekrefte at oppdraget skal-
     A                                       slettes fra samlast')
     A                                  1  3'SYSPED/SYEF94'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(040 DIV2203 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  7 12MSGCON(012 DIV2081 *LIBL/SYMSGF)
     A            SMSD           3A  O  7 25TEXT('Sted 3 LANG')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  7 32MSGCON(008 DIV2082 *LIBL/SYMSGF)
     A            WSDT           6Y 0O  7 41DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                  7 53MSGCON(007 DIV2083 *LIBL/SYMSGF)
     A            SMKL           4Y 0O  7 61DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                  9 12MSGCON(012 DIV2084 *LIBL/SYMSGF)
     A            SMKNF          3S 0O  9 25TEXT('Kundenr - 3 LANG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 32MSGCON(008 DIV2085 *LIBL/SYMSGF)
     A            SMKNS          8Y 0O  9 41TEXT('Kundenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 12MSGCON(012 DIV2086 *LIBL/SYMSGF)
     A            SMROA1         3A  O 11 25TEXT('ROUTING A')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SMROB1         6A  O 11 30TEXT('ROUTING B')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SMROC1         2S 0O 11 38TEXT('ROUTING C')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 12MSGCON(012 DIV2087 *LIBL/SYMSGF)
     A            SMROA2         3A  O 13 25TEXT('ROUTING A')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SMROB2         6A  O 13 30TEXT('ROUTING B')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SMROC2         2S 0O 13 38TEXT('ROUTING C')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 12MSGCON(012 DIV2088 *LIBL/SYMSGF)
     A            SMROA3         3A  O 15 25TEXT('ROUTING A')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SMROB3         6A  O 15 30TEXT('ROUTING B')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SMROC3         2S 0O 15 38TEXT('ROUTING C')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 12MSGCON(012 DIV2089 *LIBL/SYMSGF)
     A            HEAVD          4Y 0O 17 25DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 17 31'/'
     A            HEOPD          7Y 0O 17 34DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 17 43'/'
     A            HESG           3A  O 17 46DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 19 12MSGCON(012 DIV2090 *LIBL/SYMSGF)
     A            HENAS         30A  O 19 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20 12MSGCON(012 DIV2091 *LIBL/SYMSGF)
     A            HENAK         30A  O 20 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 23  3MSGCON(075 DIV2204 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
