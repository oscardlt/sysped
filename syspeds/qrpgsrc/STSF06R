N01   * blanke div felt når oppd ikke finnes
     FKUFAST    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FSTSOPD    IF   E           K DISK
     FSTSCLI    IF   E           K DISK
     FTRACKK03  IF   E           K DISK
     FSTSF06P   O    E             PRINTER OFLIND(*IN27)
     D                 DS
     D  HENT                   1      7  0
     D  HENT4                  4      7  0
     D  TOT                    4      7  0
     D                 DS
     D  TKAVD                  1      4  0
     D  TKOPD                  5     11  0
     D  TKAvdOpd               1     11
     D                 DS
     D  TKText                 1     70
     D  TKText_B              23     70
      *
     C                   EXSR      INIT
      *
     C     KUFASTK       CHAIN     KUFAST                             33
     C     USTAT         COMP      'M'                                5050
     C                   WRITE     STSF06P1
      * 50 ON = status er IKKE MANKO - skriv alt ok / avslutt
     C                   IF        *IN50
     C                   WRITE     STSF06P4
     c                   goto      UtRapp
     c                   end
      *
      * Skriv alle oppdrag med status blank i skytefil
     C     STSopdK       SETLL     STSOPD
     C     STSopdK       READE     STSOPD                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        SOSTAT = ' '
     C                   exsr      Ettopd
     C                   END
     C     STSopdK       READE     STSOPD                                 33
     C                   ENDDO
     c
      *
     c     UtRapp        Tag
     c                   EXSR      FREMMED
     C                   WRITE     STSF06P3
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *=====================================================================******
      *  EttOpd
      *=====================================================================******
     C     ETTOPD        BEGSR
     C     HEADFK        CHAIN     HEADF                              33
      * 51 on = første kolli-linje (skriv avd/opd mm + kllid) / 51 off=skriv kunn kolliid
      * 52 on = kollilinjer fantes ikke (skriv avd/oppd basert på stsopd)
     c                   move      *ON           *IN51
     c                   move      *ON           *IN52
     c                   move      *Blanks       Merknad          39
     c                   move      *ZEROS        LNR
     C     STSCLIK       SETLL     STSCLI
     C     STSCLIK       READE     STSCLI                                 33
     C                   DOW       *IN33 = *OFF
     c                   move      *OFF          *IN52
     C                   ADD       1             LNR
     C                   IF        SCSTAT = ' '
     C   27              WRITE     STSF06P1                                     Page Overflow
     C   27              MOVE      *OFF          *IN27
     C                   WRITE     STSF06P2
     c                   move      *OFF          *IN51
     C                   END
     C     STSCLIK       READE     STSCLI                                 33
     C                   ENDDO
      *
      * INGEN KOLLI FUNNET - PRINT PÅ SENDINGSNIVÅ
     C     *IN52         IFEQ      *ON
     c     SOAMKK        ifeq      *zeros
     c                   eval      Merknad = SOMERK
     c                   else
     c                   eval      Merknad = %trim(%editc(SOAMKK:'Z'))
     c                             + ' OK  '+ SOMERK
     c                   end
     C                   WRITE     STSF06P2
     C                   END
     C                   ENDSR
      *=====================================================================******
      *  Fremmed
      *=====================================================================******
     C     FREMMED       BEGSR
      * 51 on = første kolli-linje (skriv avd/opd mm + kllid) / 51 off=skriv kunn kolliid
      * 52 off= skriv kolli ID
      * 53 on = Fremmed (ikke skriv xxx av yy )
     c                   move      *ON           *IN51
     c                   move      *OFF          *IN52
     c                   move      *ON           *IN53
     c                   move      *ZEROS        LNR
      * Skriv alle fremmede på denne feltkontroll
     C     STSopdK       SETLL     TRACKK03
     C     STSopdK       READE     TRACKK03                               33
     C                   DOW       *IN33 = *OFF
     C   27              WRITE     STSF06P1                                     Page Overflow
     C   27              MOVE      *OFF          *IN27
     c     LNR           ifeq      *zeros
     C                   WRITE     STSF06P5
     c                   end
     C                   ADD       1             LNR
     c                   move      TKAvd         SOavd
     c                   move      TKOpd         SOopd
     C     HEADFK        CHAIN     HEADF                              33
n01  c   33              clear                   HENT
n01  c   33              clear                   HXSNDN
     c                   move      TKCLID        SCCLID
     c     TKAvdOpd      comp      GMAvdOpd                           5151
     C                   WRITE     STSF06P2
     c                   movel     TKAvdOpd      GMAvdOpd         11
     C     STSopdK       READE     TRACKK03                               33
     C                   ENDDO
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C     *ENTRY        PLIST
     C                   PARM                    USTAT             1
     C                   PARM                    UTYPE             5
     C                   PARM                    UPRO              8
     C                   PARM                    PDAUSR           10
      *
     C     STSOPDK       KLIST
     C                   KFLD                    UTYPE
     C                   KFLD                    XPRO
     C     STSCLIK       KLIST
     C                   KFLD                    UTYPE
     C                   KFLD                    XPRO
     C                   KFLD                    SOAVD
     C                   KFLD                    SOOPD
     C     KUFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    UTYPE
     C     HEADFK        KLIST
     C                   KFLD                    SOAVD
     C                   KFLD                    SOOPD
      *
     C                   MOVE      *zeros        HENT
     C                   MOVE      UPRO          XPRO
     C                   EVAL      KFTYP = 'FELTKONTR'
      *
     C                   ENDSR
      *
