********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 04 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S04  A*                                     HLPDOC('SYFA0801' 'SYFA08 ' 'SY400-
S04  A*                                     LO/MAINT')
N04  A                                      HLPPNLGRP(HLPSCR SYFA08PNL)
N04  A                                      HLPTITLE('VEDLIKEHOLD AV FLYFRAKTRA-
N04  A                                      TER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  3VALUES(' ' '1' '2' '4')
     A            KFMKD          5A  O  5  6
     A            KFMSD          1  0O  5 12EDTCDE(4)
     A            KFMTXT        30A  O  5 14
     A            KFMTX         35A  O  5 45
     A            KFMT1         75A  H
     A            KFMT2         75A  H
     A            KFMT3         75A  H
     A            KFMOCR         5A  H
     A            KFMPOS         2S 0H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA08'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A N27                              1 19MSGCON(040 YBC0016 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  27                              1 19MSGCON(040 SHU0718 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(030 SHU0717 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 YBC0017 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              1 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(006 YBC0018 *LIBL/SYMSGF)
     A            WSKD           5A  B  1  9DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                  1 17MSGCON(006 YBC0019 *LIBL/SYMSGF)
     A            WSSD           1Y 0B  1 24DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  1 27MSGCON(015 YBC0020 *LIBL/SYMSGF)
     A            WSTXT         30A  B  1 43DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WST1          75A  B  2  2DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      CHECK(LC)
     A            WST2          75A  B  3  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WST3          75A  B  4  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOS          2Y 0B  5 17DSPATR(UL)
     A            WSOCR          5A  B  5 37DSPATR(UL)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              6  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  5  2MSGCON(014 SHU0616 *LIBL/SYMSGF)
     A                                  5 21MSGCON(015 SHU0617 *LIBL/SYMSGF)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  2  2MSGCON(24 YBC0021 SYMSGF)
     A            W2TXT          5A  I  2 27DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  1  2MSGCON(6 YBC0018 SYMSGF)
     A            KFMKD          5   O  1  9DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 17MSGCON(6 YBC0019 SYMSGF)
     A            KFMSD          1Y 0O  1 24DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  1 27MSGCON(15 YBC0020 SYMSGF)
     A            KFMTXT        30   O  1 43DSPATR(UL)
     A                                      DSPATR(HI)
     A            KFMT1         75   O  2  2DSPATR(UL)
     A                                      DSPATR(HI)
     A            KFMT2         75   O  3  2DSPATR(UL)
     A                                      DSPATR(HI)
     A            KFMT3         75   O  3  2DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA08'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A N27                              1 19MSGCON(40 YBC0016 SYMSGF)
     A                                      DSPATR(HI)
     A  27                              1 19MSGCON(40 SHU0718 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0017 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
