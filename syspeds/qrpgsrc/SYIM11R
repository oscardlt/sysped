********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 ta med sader uten headf oppdrag
10XXX * 02 SH  PTF10 ikke fortollinger med hegnn
10XXX * 03 SH  PTF10 ikke sorter
10XXX * 04 SH  PTF10 ta bort punktum
10XXX * 05 SH  PTF10 inkluder utelat
10XXX * 06 SH  PTF10 ikke fortollinger med hegnn
10XXX * 07 YBC PTF10 CLEAR TØMMER IKKE ALLE FELTER
10XXX * 08 YBC PTF11 emnerubrikk  70
10XXX * 09 SH  PTF11 nye felt
********************************************************************
      **************************************************************
      *** PROGRAM for utskrift av tollagerregnskap               ***
      **************************************************************
N09  FTURER14   IF   E           K DISK
     FSAEH6B    IF   E           K DISK
N01  FSADH6B    IF   E           K DISK
N01  F                                     RENAME(SADHR:SADHR6)
     FCUNDL01   IF   E           K DISK
     FHEAD18    IF   E           K DISK
N01  FHEADF     IF   E           K DISK
N01  F                                     RENAME(HEADFR:HEADFRF)
     FSADH      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYIM11P   O    E             PRINTER OFLIND(*IN02)
     FSYIM11D   CF   E             WORKSTN
     FSYIM11F   O  a E             DISK
     D                 DS
     D  WSGNFR                 1     15
     D  WSGNFA                 1      4
     D  WSGNFB                 5      9
     D  WSGNFC                10     12
     D  WSGNFD                13     15
     D                 DS
     D  WSGNTI                 1     15
     D  WSGNTA                 1      4
     D  WSGNTB                 5      9
     D  WSGNTC                10     12
     D  WSGNTD                13     15
     D                 DS
     D  HEGN                   1     15
     D  AAR01                  1      1
     D  AAR02                  2      2
     D  AAR03                  3      3
     D  AAR04                  4      4
     D  AARH                   1      4
     D  DAG01                 10     10
     D  DAG02                 11     11
     D  DAG03                 12     12
     D  DAGNRH                10     12
     D  hegn16                 1     16
N01  D                 DS
N01  D  sirg                   1     11
N01  D  sirg1011              10     11
N01  D                 DS
N01  D  syrg                   1     14
N01  D  syrg1011              10     11
     D                 DS
     D  DSTID                  1     14  0
     D  DSCENT                11     14  0
     D                 DS
S05  D* Z                      1     20
S05  D*                                    DIM(10)
N05  D  Z                      1     28
N05  D                                     DIM(14)
     D  ZOT01                  1      2
     D  ZOT02                  3      4
     D  ZOT03                  5      6
     D  ZOT04                  7      8
     D  ZOT05                  9     10
     D  ZOT06                 11     12
     D  ZOT07                 13     14
     D  ZOT08                 15     16
     D  ZOT09                 17     18
     D  ZOT10                 19     20
N05  D  ZOT11                 21     22
N05  D  ZOT12                 23     24
N05  D  ZOT13                 25     26
N05  D  ZOT14                 27     28
      *_____
      * Init
      *"""""
     C*
     C     *ENTRY        PLIST
     C                   PARM                    EPOST            70
     C                   PARM                    UDAGNR            3
     C                   PARM                    UIN01             1
s09  C*                  PARM                    COV01            40
n09  C                   PARM                    COV01            70
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    WSPRIN           10
     C                   PARM                    WSSDV             1
     C*
     C                   MOVE      UDAGNR        DAGNR             3 0
s04  c*                  move      '.'           hegn16
     C*
     C                   TIME                    DSTID            14 0
     C                   MOVE      DSCENT        WSGNFA
     C                   MOVE      DSCENT        WSGNTA
     C*
     C                   READ      FIRM                                   34
     C     START         TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       303536
     C                   SETOFF                                       37
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKUNR
     C                   GOTO      START
     C                   END
     C     WSKUND        IFNE      *ZEROS
     C                   MOVE      WSKUND        KUNDNR
     C     KUNKEY        CHAIN     CUNDL01                            35
     C                   MOVE      KNAVN         KNAVNS
     C   35              MOVE      'SPK0001'     MSGNR
     C   35              SETON                                        30
     C   35              GOTO      START
     C                   END
      *
      * EPOSTADRESSE MÅ TASTES
      *
     C     EPOST         IFEQ      *BLANKS
     C     WSPRIN        ANDEQ     *BLANKS
     C                   SETON                                        36
     C                   MOVE      'SHU0723'     MSGNR
     C                   SETON                                        30
     C                   GOTO      START
     C                   END
      *
      * TESTER GODSNUMMER
      *
     C     WSGNFR        IFGT      WSGNTI
     C                   SETON                                        37
     C                   MOVE      'SHU0724'     MSGNR
     C                   SETON                                        30
     C                   GOTO      START
     C                   END
     C*
     C  N10              GOTO      START
     C*
     C     EPOST         IFNE      *BLANKS
     C                   MOVE      ' '           FUNK
     C                   CALL      'EPOCVP'
s09  C*                  PARM                    COV01            40
n09  C                   PARM                    COV01            70
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C     FUNK          CABEQ     'A'           START
     C                   END
     C*
     C*
     c     wssdv         ifne      'J'
     C                   WRITE     HEAD
     c                   end
     C                   EXSR      PRINT
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *********************************************************
     C     PRINT         BEGSR
      *********************************************************
     C     FRAKEY        KLIST
     C                   KFLD                    WSGNFR
     C     SADKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
s03  C*                  SORTA     Z
     C     FRAKEY        SETLL     HEAD18
      *
      * START HOVEDLØKKE
      *
     C     *IN33         DOWEQ     '0'
     C     NESTE         TAG
     C                   READ      HEAD18                                 33
     C  N33WSGNTI        COMP      HEGN                                 33
      *
      *
      * brudd godsnummer
      *
N01  C   33              EVAL      HEGN='999999999999999'
N01  C     FORIGE        IFNE      *BLANKS
N01  C     FORIGE        ANDNE     HEGN
N01  C                   EXSR      PRINT2
N01  C                   MOVE      *BLANKS       FORIGE
N01  C                   READP     HEAD18                                 33
N01  C                   READ      HEAD18                                 33
E05  C  N33WSGNTI        COMP      HEGN                                 33
N01  C                   END
      *
     C     *IN33         IFEQ      '0'
N06  C     hegnn         ifne      *blanks
N06  C                   goto      neste
N06  c                   end
      *
      * SKJEKKER OPPDRAGSTYPE
      *
     C     ZOT01         IFNE      '  '
     C     HEOT          LOOKUP    Z                                      44
N05  C     INKUTE        IFEQ      'U'
     C   44              GOTO      NESTE
N05  C                   ELSE
N05  C     INKUTE        IFEQ      'I'
N05  C  N44              GOTO      NESTE
N05  C                   END
N05  C                   END
     C                   END
      *
      * HENT SAD DATA
      *
N01  C                   CLEAR                   sadhr
N01  C                   MOVE      *blanks       sirg9
     C     SADKEY        CHAIN     SADH                               34
      *
     C     *IN34         IFEQ      '1'
N07  C                   MOVE      HENAS         SINAS
N07  C                   MOVE      HENAK         SINAK
N07  C                   MOVE      HEADK3        SIADK3
N07  C                   MOVE      HEDTOP        SIDT
     C                   MOVE      HETLL         SITLL
     C                   MOVE      HETLE         SITLE
     C                   MOVE      *BLANKS       SIDTG
N05  C                   MOVE      HEGN          SIGN
      *
      * SKJEKKER OM IMPORT / EKSPORT HVIS IKKE SAD
      *
     C     HEUR          IFEQ      'A'
     C     HEUR          OREQ      'C'
     C     HEUR          OREQ      'F'
     C                   MOVE      HEKNK         KUNDNR
     C                   ELSE
     C                   MOVE      HEKNS         KUNDNR
     C                   END
      * TRANSP-MODUL
     C     HEUR          IFEQ      'H'
      * Import (fraland ulik NO)
     C     HELKA         IFNE      FILAND
     C                   MOVE      HEKNK         KUNDNR
     C                   ELSE
      * Eksport(fraland  lik NO)
     C                   MOVE      HEKNS         KUNDNR
     C                   END
     C                   END
      *
     C                   Z-ADD     HENT          ANT               5 0
     C                   Z-ADD     HEVKT         VEKT              7 0
N09  C                   MOVE      *BLANKS       SITRID
N09  C                   MOVEL     HEHAWB        SITRID
N09  C                   MOVE      'N'           XTUR              1
N09  C     HEUR          IFEQ      'H'
N09  C     HEPRO         andne     *zeros
N09  C     HEPRO         CHAIN     TURER14                            97
N09  C                   move      TUTRMA        HETRM
N09  c                   endif
N09  C     HETRM         IFNE      0
N09  C                   Z-ADD     HETRM         SITRM
N09  C* VED TRANSPORTMÅTE 16 HENTES TOLLSTED FRA TURER
N09  C                   MOVE      'N'           XTUR              1
N09  C     HETRM         IFEQ      16
N09  C     HEPRO         CHAIN     TURER14                            97
N09  C  N97              MOVE      'J'           XTUR
N09  C                   ELSE
N09  C     HEPRO         IFNE      0
N09  C     HEPRO         CHAIN     TURER14                            33
N09  C  N33              MOVE      'J'           XTUR
N09  C                   END
N09  C                   END
N09  C                   END
N09  C                   MOVE      HETRC         SIKDC
N09  C                   IF        XTUR = 'N'
N09  C                   MOVE      HETRI         SILKT
N09  C                   ELSE
N09  C     TULK          IFNE      *BLANKS
N09  C                   EVAL      SILKT  = TULK
N09  C                   ELSE
N09  C                   MOVE      HETRI         SILKT
N09  C                   ENDIF
N09  C     TUBILN        IFNE      *BLANKS
N09  C                   EVAL      SITRID = TUBILN
N09  C                   ENDIF
N09  C                   END
      *
      * SADH EKSISTERER
      *
     C                   ELSE
     C                   MOVE      SIKNK         KUNDNR
     C                   Z-ADD     SINTK         ANT
     C                   Z-ADD     SIVKB         VEKT
     C                   END
      *
     C     KUNKEY        CHAIN     CUNDL01                            35
      *
      * flytt hvis har ikke sad
      *
     C     *IN34         IFEQ      '1'
     C  N35              MOVE      KNAVN         SINAK
     C  N35              MOVE      *BLANKS       SIADK3
     C  N35              MOVEL     POSTNR        SIADK3
     C  N35              MOVE      ADR3          SIADK3
     C   35              MOVE      HENAK         SINAK
     C   35              MOVE      HEADK3        SIADK3
     C                   END
      * obs personnummer ikke til print
     C     *IN(34)       ifeq      '0'
     C     SIRG1011      IFEQ      *BLANKS
     C                   MOVEL     SIRG          SIRG9
     C                   else
     C  N35SYRG1011      IFEQ      *blanks
     C                   MOVEL     SYRG          SIRG9
     C                   END
     C                   END
     C                   END
      *
      *
      * SKJEKKER SELEKTERT KUNDE
      *
     C     WSKUND        IFNE      KUNDNR
     C     WSKUND        ANDNE     *ZEROS
     C                   GOTO      NESTE
     C                   END
      *
      * SKRIVER KUN HVIS IKKE FORTOLLET
      *
     C     SITLL         IFEQ      *BLANKS
N02  c     hetll         andeq     *blanks
     C*
     C                   EXSR      SRDAG
      *
      * TESTER OM REEKSPORTERT ANT/VEKT ER NULLET UT
      *
     C                   EXSR      EXPOT
      *
      * HVIS ANT VEKT STEMMER SKRIVES IKKE OPPDRAGET
      *
     C     ANTE          IFNE      *ZEROS
     C     ANTE          ANDEQ     ANT
     C     VEKTE         ANDEQ     VEKT
     C                   GOTO      NESTE
     C                   END
      *
      * OVERFLOW
      *
     C     *IN02         IFEQ      '1'
     C     WSSDV         ANDNE     'J'
     C                   WRITE     HEAD2
     C                   SETOFF                                       02
     C                   END
      *
     c     wssdv         ifne      'J'
     C                   WRITE     DET
     c                   else
     c                   write     syim11fr
     c                   end
      *
      * SKRIVER REEXPORT
      *
     C     ANTE          IFNE      *ZEROS
     C                   EXSR      EXPOP
     C                   END
N01  C                   MOVE      SIGN          FORIGE           15
      * SLUTT SITLL
     C                   END
      * SLUTT IN33
     C                   END
      * SLUTT DO-LØKKE
     C                   END
     C                   ENDSR
N01   *********************************************************
N01  C     PRINT2        BEGSR
N01   *********************************************************
N01  C     HEAKEY        KLIST
N01  C                   KFLD                    SIAVD
N01  C                   KFLD                    SITDN
     C                   MOVE      *BLANKS       HEOT
N01  C                   SETOFF                                       33
S05  C*                  SORTA     Z
N01  C     FORIGE        SETLL     SADH6B
N01   *
N01   * START HOVEDLØKKE
N01   *
N01  C     *IN33         DOWEQ     '0'
N01  C     NESTE2        TAG
N01  C     FORIGE        READE     SADH6B                                 33
N01  C  N33WSGNTI        COMP      SIGN                                 33
N01   *
N01  C     *IN33         IFEQ      '0'
N01   *
N01   * SKJEKKER OPPDRAGSTYPE
N01   *
N01   *
N01   * KUN DE UTEN HEADF record
N01   *
N05  C                   CLEAR                   headfr
N01  C     HEAKEY        CHAIN     HEADF                              34
N01  C  N34              GOTO      NESTE2
N01   *
N01   * obs personnummer ikke til print
N01  C                   MOVE      *blanks       SIRG9
N01  C     SIRG1011      IFEQ      *BLANKS
N01  C                   MOVEL     SIRG          SIRG9
N01  C                   end
N01   *
N01  C                   MOVE      SIKNK         KUNDNR
N01  C                   Z-ADD     SINTK         ANT
N01  C                   Z-ADD     SIVKB         VEKT
N01   *
N01  C     KUNKEY        CHAIN     CUNDL01                            35
N01  C  N35              MOVE      KNAVN         SINAK
N01  C   35              MOVE      HENAK         SINAK
N01   *
N01   * SKJEKKER SELEKTERT KUNDE
N01   *
N01  C     WSKUND        IFNE      KUNDNR
N01  C     WSKUND        ANDNE     *ZEROS
N01  C                   GOTO      NESTE2
N01  C                   END
N01   *
N01   * SKRIVER KUN HVIS IKKE FORTOLLET
N01   *
N01  C     SITLL         IFEQ      *BLANKS
N01  C*
N01  C                   EXSR      SRDAG
N01   *
N01   * OVERFLOW
N01   *
N01  C     *IN02         IFEQ      '1'
N01  C     WSSDV         ANDNE     'J'
N01  C                   WRITE     HEAD2
N01  C                   SETOFF                                       02
N01  C                   END
N01   *
N01  c     wssdv         ifne      'J'
N01  C                   WRITE     DET
N01  c                   else
N01  c                   write     syim11fr
N01  c                   end
N01   *
N01   * SLUTT SITLL
N01  C                   END
N01   * SLUTT IN33
N01  C                   END
N01   * SLUTT DO-LØKKE
N01  C                   END
N01  C                   ENDSR
      *********************************************************
     C     SRDAG         BEGSR
      *********************************************************
     C                   MOVE      *ZEROS        DIFF              9 0
     C     DAG01         IFGE      '0'
     C     DAG01         ANDLE     '9'
     C     DAG02         ANDGE     '0'
     C     DAG02         ANDLE     '9'
     C     DAG03         ANDGE     '0'
     C     DAG03         ANDLE     '9'
     C                   MOVE      DAGNRH        DAGNRF            3 0
     C     AAR01         IFGE      '0'
     C     AAR01         ANDLE     '9'
     C     AAR02         ANDGE     '0'
     C     AAR02         ANDLE     '9'
     C     AAR03         ANDGE     '0'
     C     AAR03         ANDLE     '9'
     C     AAR04         ANDGE     '0'
     C     AAR04         ANDLE     '9'
     C                   MOVE      AARH          AARF              4 0
     C     DAGNR         SUB       DAGNRF        DIFF
     C     DSCENT        IFGT      AARF
     C     DSCENT        SUB       AARF          DIFAAR            4 0
     C     DIFAAR        MULT      366           DIFFB             9 0
     C                   ADD       DIFFB         DIFF
     C                   END
     C                   END
     C                   END
     C                   ENDSR
     C*******************************************************
     C     EXPOT         BEGSR
     C*******************************************************
     C                   MOVE      *ZEROS        ANTE              5 0
     C                   MOVE      *ZEROS        VEKTE             9 0
     C                   SETOFF                                       34
     C     HEGN          SETLL     SAEH6B
     C     *IN34         DOWEQ     '0'
     C     HEGN          READE     SAEH6B                                 34
     C     *IN34         IFEQ      '0'
      * Overflow ?
     C     *IN02         IFEQ      '1'
     c     wssdv         andne     'J'
     C                   WRITE     HEAD2
     C                   MOVE      '0'           *IN02
     C                   END
     C                   ADD       SENTK         ANTE
     C                   ADD       SEVKB         VEKTE
     C                   END
     C                   END
     C                   ENDSR
     C*******************************************************
     C     EXPOP         BEGSR
     C*******************************************************
     C                   SETOFF                                       34
     C     HEGN          SETLL     SAEH6B
     C     *IN34         DOWEQ     '0'
     C     HEGN          READE     SAEH6B                                 34
     C     *IN34         IFEQ      '0'
     C                   MOVEL     '*REKSPOR'    HEGNN
     C                   MOVE      'TERT*  '     HEGNN
      * Overflow ?
     C     *IN02         IFEQ      '1'
     c     wssdv         andne     'J'
     C                   WRITE     HEAD2
     C                   MOVE      '0'           *IN02
     C                   END
     C                   MOVE      SENAK         SINAK
     C                   MOVE      SETLE         SITLE
     C                   MOVE      SETLL         SITLL
     C                   MOVE      SEDTG         SIDTG
     C                   Z-ADD     SENTK         ANT
     C                   Z-ADD     SEVKB         VEKT
     C                   MOVE      SEKNS         KUNDNR
     C                   Z-ADD     SEAVD         HEAVD
     C                   Z-ADD     SETDN         HEOPD
     C                   MOVE      SESG          HESG
     C     KUNKEY        CHAIN     CUNDL01                            35
     C  N35              MOVE      KNAVN         SINAK
     C   35              MOVE      HENAK         SINAK
     c     wssdv         ifne      'J'
     C                   WRITE     DET
     c                   else
     c                   write     syim11fr
     c                   end
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUND
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C*
