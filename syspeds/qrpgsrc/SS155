      *********************************************************************
      * Endre artikkel
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS155 MODULE(*LIBL/SS155
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSORF    IF   E           k DISK    USROPN
     FGSSLGL01  IF   E           k DISK    USROPN
     FTRACKL01  IF   E           k DISK    USROPN
     FWRKCGISS  UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSANR           S             15
     D WSONO           s              7
     D W2ONO           s              7  0
     D WSTXT           s             30
     D WKEY            S            150
     D WINT            s              6
     D WINTEG          s              6  0
     D lstbrd          s              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D WKEY2                   1    256
     D KEY                     1    256    DIM(256)
     D                 DS
     D WDATA                   1   1024
     D DATA                    1   1024    DIM(1024)
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C                   exsr      GetData
     C                   IF        XFOUND = 'N'
     C                   callp     wrtsection('0found')
     C                   ELSE
     C                   exsr      SetData
     C                   callp     wrtsection('foundsome')
     C                   END
      *
     C                   exsr      SetEnd
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      WKEY    = zhbgetvar('WKEY')
     C                   EVAL      WSONO   = zhbgetvar('ORONO')
     C                   eval      W2ONO   = c2n2(WSONO)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   callp     updhtmlvar('WKEY':WKEY)
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(W2ONO:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS155'
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  Hent data til arbeidsfil
      *=====================================================================******
     C     GetData       begsr
      *
      * Slett gamle data
     C     WINTEG        SETLL     WRKCGISS
     C     WINTEG        READE     WRKCGISS                               33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    WRKCGISR
     C     WINTEG        READE     WRKCGISS                               33
     C  N33              ENDDO
     C                   MOVE      *BLANKS       WKEY2
     C                   MOVE      *BLANKS       WDATA
      *
      * Oppdater ny data
     C                   MOVE      'N'           XFOUND            1
      *
     C     W2ONO         SETLL     GSSLGL01
     C     W2ONO         READE     GSSLGL01                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        LGNIVÅ <= 3
     C                   MOVEL     ORONO         XAN07             7
     C                   MOVEA     XAN07         KEY(1)
     C                   MOVEL     LGHDT         XAN08             8
     C                   MOVEA     XAN08         KEY(8)
     C                   MOVEL     LGHTID        XAN06             6
     C                   MOVEA     XAN06         KEY(16)
      *
     C                   MOVEA     LGTXT         DATA(1)
      *
     C                   WRITE     WRKCGISR
     C                   MOVE      'J'           XFOUND            1
     C                   END
     C     W2ONO         READE     GSSLGL01                               33
     C  N33              ENDDO
      *
     C                   GOTO      SLGETDATA
     C                   CLEAR                   WDATA
     C                   IF        ORTNO  <> 0
     C                   MOVEL     ORTNO         TTAVD
     C                   MOVE      ORTNO         TTOPD
     C     TRACKL01K     SETLL     TRACKL01
     C     TRACKL01K     READE     TRACKL01                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVEL     ORONO         XAN07             7
     C                   MOVEA     XAN07         KEY(1)
     C                   MOVEL     TTDATE        XAN08             8
     C                   MOVEA     XAN08         KEY(8)
     C                   MOVEL     TTTIME        XAN06             6
     C                   MOVEA     XAN06         KEY(16)
      *
     C                   MOVEA     TTTEXT        DATA(1)
      *
     C                   WRITE     WRKCGISR
     C                   MOVE      'J'           XFOUND            1
     C     W2ONO         READE     GSSLGL01                               33
     C  N33              ENDDO
     C                   END
      *
     C                   IF        ORTNO2 <> 0
     C                   MOVEL     ORTNO2        TTAVD
     C                   MOVE      ORTNO2        TTOPD
     C     TRACKL01K     SETLL     TRACKL01
     C     TRACKL01K     READE     TRACKL01                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVEL     ORONO         XAN07             7
     C                   MOVEA     XAN07         KEY(1)
     C                   MOVEL     TTDATE        XAN08             8
     C                   MOVEA     XAN08         KEY(8)
     C                   MOVEL     TTTIME        XAN06             6
     C                   MOVEA     XAN06         KEY(16)
      *
     C                   MOVEA     TTTEXT        DATA(1)
      *
     C                   WRITE     WRKCGISR
     C                   MOVE      'J'           XFOUND            1
     C     TRACKL01K     READE     TRACKL01                               33
     C  N33              ENDDO
     C                   END
      *
     C                   IF        ORTNO3 <> 0
     C                   MOVEL     ORTNO3        TTAVD
     C                   MOVE      ORTNO3        TTOPD
     C     TRACKL01K     SETLL     TRACKL01
     C     TRACKL01K     READE     TRACKL01                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVEL     ORONO         XAN07             7
     C                   MOVEA     XAN07         KEY(1)
     C                   MOVEL     TTDATE        XAN08             8
     C                   MOVEA     XAN08         KEY(8)
     C                   MOVEL     TTTIME        XAN06             6
     C                   MOVEA     XAN06         KEY(16)
      *
     C                   MOVEA     TTTEXT        DATA(1)
      *
     C                   WRITE     WRKCGISR
     C                   MOVE      'J'           XFOUND            1
     C     TRACKL01K     READE     TRACKL01                               33
     C  N33              ENDDO
     C                   END
     C     SLGETDATA     TAG
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section
      *  (search criteria)
      *=====================================================================******
     C     SetData       begsr
      *
     C                   callp     wrtsection('tablestr')
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   MOVE      *ZEROS        XGHDT             8 0
     C                   MOVE      *ZEROS        XGHTID            6 0
      *
     C     WINTEG        SETLL     WRKCGISS
     C     WINTEG        READE     WRKCGISS                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVEA     KEY(8)        XAN08
     C                   MOVE      XAN08         LGHDT
     C                   MOVEA     KEY(16)       XAN06
     C                   MOVE      XAN06         LGHTID
     C                   IF        ((LGHDT = XGHDT) AND (LGHTID = XGHTID))
     C                   callp     updhtmlvar('LGHDT':' ')
     C                   callp     updhtmlvar('LGHTID':' ')
     C                   ELSE
     C                   callp     updhtmlvar('LGHDT':
     C                             %trim(%editw(LGHDT:'    .  .  ')))
     C                   callp     updhtmlvar('LGHTID':
     C                             %trim(%editw(LGHTID:'  .  .  ')))
     C                   MOVE      LGHDT         XGHDT
     C                   MOVE      LGHTID        XGHTID
     C                   END
     C                   MOVEA     DATA(1)       TTTEXT
     C                   callp     updhtmlvar('LGTXT':TTTEXT)
      *
     C                   callp     wrtsection('tablerow')
      *
     C                   DELETE    WRKCGISR
     C     WINTEG        READE     WRKCGISS                               33
     C  N33              ENDDO
      *
     C                   callp     wrtsection('tableend')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSORF
     C                   OPEN      GSSLGL01
     C                   OPEN      TRACKL01
     C                   OPEN      WRKCGISS
      *
     C     TRACKL01K     klist
     C                   kfld                    TTAVD
     C                   kfld                    TTOPD
      *
     c                   eval      WINTEG   = random(1:999999)
     c                   move      WINTEG        WINT
     C     W2ONO         CHAIN     GSSORF                             33
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSLGL01
     C                   close     TRACKL01
     C                   close     WRKCGISS
     C                   eval      *inlr = *on
      *
     C                   endsr
