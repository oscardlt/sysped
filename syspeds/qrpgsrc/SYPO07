      *
      ***********************************************************
      *** PROGRAM FOR INTERVALLSTYRING AV POSTNUMMER          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 42-85 95-97 99
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 avvikende inn/utkj-sone v over xxx kg
********************************************************************
      *  41    =     VEDLIKEHOLD AV KUNDERS POSTNUMMERSTYRING
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FSTED2D    IF   E           K DISK
     FSONESL02  IF   E           K DISK
     FSTEDI     UF A E           K DISK
     FSTEDIL03  IF   E           K DISK
     F                                     RENAME(STEDIR:STEDIR3)
     FSYPO07FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D DOMTXT          C                   CONST('Domestic, Avvik ved -
     D                                     ren innland')
     D POSTXT          C                   CONST('Postens soneinndelin-
     D                                     ger')
     C*
     C                   EXSR      INIT
     C*
     C     START0        TAG
     C*
     C                   Z-ADD     28            POSNBR
     C                   Z-ADD     5             LINNBR
     C   41              Z-ADD     7             LINNBR
     C                   MOVE      *OFF          *IN50
     C                   MOVE      ' '           WSSU
     C     STAR0B        TAG
     C                   EXFMT     BILDE01
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C* SEARCH
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START0
     C                   END
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C*
     C* TEST CUSTOMER
     C*
     C  N41              DO
     C     KUNKEY        CHAIN     CUNDL01                            30
     C  N30STICUS        COMP      0                                      30
     C   30              MOVE      'SPK0001'     MSGNR
     C   30              GOTO      START0
     C                   END
     C*
     C* TEST COUNTRY
     C*
     C     KODLKK        CHAIN     KODTLK                             30
     C   30              Z-ADD     28            POSNBR
     C   30              Z-ADD     7             LINNBR
     C   30              MOVE      'SPL0001'     MSGNR
     C   30              GOTO      STAR0B
     C*
     C                   MOVE      *BLANKS       SPETAB
     C     STINLK        IFEQ      'NO'
     C     WSSU          ANDNE     'S'
     C     WSSU          ANDNE     'D'
     C     WSSU          ANDNE     'P'
     C                   MOVE      *ON           *IN50
     C                   GOTO      STAR0B
     C                   END
      * HVIS NO: SPESIAL KUN V/DOMESTIC / POSTEN - FIKTIV KUNDE...
     C                   SELECT
     C     STINLK        WHENEQ    'NO'
     C     WSSU          ANDEQ     'D'
     C                   MOVE      *ON           *IN50
     C                   Z-ADD     99999999      STICUS
     C                   MOVEL     DOMTXT        SPETAB
     C     STINLK        WHENEQ    'NO'
     C     WSSU          ANDEQ     'P'
     C                   MOVE      *ON           *IN50
     C                   Z-ADD     99999998      STICUS
     C                   MOVEL     POSTXT        SPETAB
     C                   OTHER
     C                   MOVE      *OFF          *IN50
     C                   MOVE      *ZEROS        STICUS
     C                   END
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2INBA        YTINBA            5
     C                   MOVE      W2INPF        YTINPF            5
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2INBA        IFNE      *BLANKS                                        BASE/PNR F
     C                   MOVE      'J'           BASORT            1
     C     BASKEY        SETLL     STEDIL03
     C                   ELSE                                                     PNR FRA
     C                   MOVE      'N'           BASORT
     C     W2INPF        IFEQ      *BLANKS
     C     PRIKU         SETLL     STEDI
     C                   ELSE
     C     PRISK1        SETLL     STEDI
     C                   MOVE      *BLANKS       W2INPF
     C                   END
     C                   END
     C     START1        TAG
     C     BASORT        IFEQ      'J'
     C     PRIKU         READE     STEDIL03                               94
     C                   ELSE
     C     PRIKU         READE(N)  STEDI                                  94
     C                   END
     C   94              MOVE      *BLANKS       STINPF
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     BASORT        IFEQ      'J'
     C     PRIKU         READE     STEDIL03                               94
     C                   ELSE
     C     PRIKU         READE(N)  STEDI                                  94
     C                   END
     C  N94              MOVE      STINBA        YTINBA
     C  N94              MOVE      STINPF        YTINPF
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C   12              GOTO      START0
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C     BASORT        IFEQ      'J'
     C  N94BASKEY        SETLL     STEDIL03
     C  N94PRIKU         READE     STEDIL03                               94
     C                   ELSE
     C  N94PRISK3        SETLL     STEDI
     C  N94PRIKU         READE(N)  STEDI                                  94
     C                   END
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     PRISK2        CHAIN     STEDI                              33
     C  N33              DELETE    STEDIR
     C                   EXSR      MOVB1
     C                   MOVE      '*DEL*'       STINPF
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
      *
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     17            POSNBR
     C*
     C*** LAGE/ENDRE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000'       *IN(86)
N01  C                   MOVEA     '000'         *IN(83)
     C                   SETOFF                                       30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDT2
     C                   GOTO      START5
     C                   END
      *
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C     NY            IFEQ      'J'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      START5
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     15            POSNBR
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     *IN40         IFEQ      '0'
     C     WSINPF        COMP      *BLANKS                                30
     C     *IN30         IFEQ      '1'
     C                   MOVE      'JOV0163'     MSGNR
     C                   ELSE
     C     PRISK         CHAIN(N)  STEDI                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'YBC0351'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
     C     *IN30         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   MOVE      '1'           *IN90
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
     C     WSINPT        IFLT      WSINPF
     C     WSINPT        ANDNE     *BLANKS
     C                   MOVE      'YBC0352'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     17            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN89
     C                   GOTO      SLTB1
     C                   END
      *
      *
N01   * Basested flyttet foran innh/utkj-sone...
     C     WSINBA        IFEQ      *BLANKS
     C                   MOVE      WSINPF        WSINBA
     C                   END
     C     STE2KD        CHAIN     STED2D                             86
     C     *IN86         IFEQ      '1'
S01  C*                  MOVE      'YBC0352'     MSGNR
N01  C                   MOVE      'YBC0353'     MSGNR
     C                   Z-ADD     22            LINNBR
s01  C*                  Z-ADD     61            POSNBR
N01  C                   Z-ADD     25            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSINS1        IFNE      *BLANKS
     C     WSINS1        SETLL     SONESL02
     C     WSINS1        READE     SONESL02                               88
     C     *IN88         IFEQ      '1'
     C                   MOVE      'YBC0346'     MSGNR
     C                   Z-ADD     22            LINNBR
S01  C*                  Z-ADD     30            POSNBR
N01  C                   Z-ADD     36            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
     C     WSINS2        IFNE      *BLANKS
     C     WSINS2        SETLL     SONESL02
     C     WSINS2        READE     SONESL02                               87
     C     *IN87         IFEQ      '1'
     C                   MOVE      'YBC0346'     MSGNR
     C                   Z-ADD     22            LINNBR
S01  C*                  Z-ADD     46            POSNBR
N01  C                   Z-ADD     43            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
     C                   END
N01   * hvis avvikende sone git MÅ vektgrense angis
N01  c     WSINS3        ifne      *blanks
N01  c     WSINS4        orne      *blanks
N01  c     WSINSL        ifeq      *zeros
N01  C                   Z-ADD     22            LINNBR
N01  C                   Z-ADD     55            POSNBR
N01  C                   MOVE      'JOV1522'     MSGNR
N01  C                   MOVE      '1'           *IN83
N01  C                   MOVE      '1'           *IN30
N01  C                   GOTO      SLTB1
N01  C                   endif
N01  c                   endif
N01   *
N01   * avvikende innh..
N01  C     WSINS3        IFNE      *BLANKS
N01  C     WSINS3        SETLL     SONESL02
N01  C     WSINS3        READE     SONESL02                               84
N01  C     *IN84         IFEQ      '1'
N01  C                   MOVE      'YBC0346'     MSGNR
N01  C                   Z-ADD     22            LINNBR
N01  C                   Z-ADD     62            POSNBR
N01  C                   MOVE      '1'           *IN30
N01  C                   GOTO      SLTB1
N01  C                   END
N01  C                   END
N01   * avvikende utkj..
N01  C     WSINS4        IFNE      *BLANKS
N01  C     WSINS4        SETLL     SONESL02
N01  C     WSINS4        READE     SONESL02                               85
N01  C     *IN85         IFEQ      '1'
N01  C                   MOVE      'YBC0346'     MSGNR
N01  C                   Z-ADD     22            LINNBR
N01  C                   Z-ADD     69            POSNBR
N01  C                   MOVE      '1'           *IN30
N01  C                   GOTO      SLTB1
N01  C                   END
N01  C                   END
     C*
     C     PRISK         CHAIN     STEDI                              33
     C     *IN40         IFEQ      '0'                                          *F6-MODUS
     C     *IN33         ANDEQ     '0'                                          *FINNES
     C                   MOVE      'JOV0164'     MSGNR
     C                   MOVE      '1'           *IN90
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
     C*
     C                   EXSR      MOVWPR
     C  N33              UPDATE    STEDIR
     C   33              WRITE     STEDIR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C   40              MOVE      'N'           NY                1
     C  N40              MOVE      'J'           NY
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     STINPF        COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVPRW
     C                   MOVE      ' '           WSNR
     C                   SETOFF                                       9089
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     STINPF        COMP      '*DEL*'                                98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWPR        BEGSR
     C***********************************************
     C                   MOVE      WSINPF        STINPF
     C                   MOVE      WSINPT        STINPT
     C     STINPT        IFEQ      *BLANKS
     C                   MOVE      STINPF        STINPT
     C                   END
     C                   MOVE      WSINS1        STINS1
     C                   MOVE      WSINS2        STINS2
     C     STINS2        IFEQ      *BLANKS
     C                   MOVE      STINS1        STINS2
     C                   END
     C                   MOVE      WSINBA        STINBA
     C     STINBA        IFEQ      *BLANKS
     C                   MOVE      STINPF        STINBA
     C                   END
N01  C                   MOVE      WSINSL        STINSL
N01  C                   MOVE      WSINS3        STINS3
N01  C                   MOVE      WSINS4        STINS4
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVPRW        BEGSR
     C***********************************************
     C                   MOVE      STINPF        WSINPF
     C                   MOVE      STINPT        WSINPT
     C                   MOVE      STINS1        WSINS1
     C                   MOVE      STINS2        WSINS2
     C                   MOVE      STINBA        WSINBA
N01  C                   MOVE      STINSL        WSINSL
N01  C                   MOVE      STINS3        WSINS3
N01  C                   MOVE      STINS4        WSINS4
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       STINPF
     C                   MOVE      *BLANKS       STINPT
     C                   MOVE      *BLANKS       STINS1
     C                   MOVE      *BLANKS       STINS2
     C                   MOVE      *BLANKS       STINBA
N01  C                   MOVE      *ZEROS        STINSL
N01  C                   MOVE      *BLANKS       STINS3
N01  C                   MOVE      *BLANKS       STINS4
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSINPF
     C                   MOVE      *BLANKS       WSINPT
     C                   MOVE      *BLANKS       WSINS1
     C                   MOVE      *BLANKS       WSINS2
     C                   MOVE      *BLANKS       WSINBA
N01  C                   MOVE      *ZEROS        WSINSL
N01  C                   MOVE      *BLANKS       WSINS3
N01  C                   MOVE      *BLANKS       WSINS4
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C* B = BRUTTO
     C* C = CUSTOMER-RELATED
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UCODE             1
     C                   PARM                    UCUSTN            8 0
     C*
     C     UCODE         COMP      'B'                                    41
     C*
     C*
     C     KODLKK        KLIST
     C                   KFLD                    LKUNIK
     C                   KFLD                    STINLK
     C     PRIKU         KLIST
     C                   KFLD                    STICUS
     C                   KFLD                    STINLK
     C     PRISK         KLIST
     C                   KFLD                    STICUS
     C                   KFLD                    STINLK
     C                   KFLD                    WSINPF
     C     PRISK1        KLIST
     C                   KFLD                    STICUS
     C                   KFLD                    STINLK
     C                   KFLD                    W2INPF
     C     PRISK2        KLIST
     C                   KFLD                    STICUS
     C                   KFLD                    STINLK
     C                   KFLD                    STINPF
     C     PRISK3        KLIST
     C                   KFLD                    STICUS
     C                   KFLD                    STINLK
     C                   KFLD                    YTINPF
     C     BASKEY        KLIST
     C                   KFLD                    STICUS
     C                   KFLD                    STINLK
     C                   KFLD                    YTINBA
     C                   KFLD                    YTINPF
     C*
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    STICUS
     C     STE2KD        KLIST
     C                   KFLD                    STINLK
     C                   KFLD                    WSINBA
     C*
     C                   MOVE      'LK '         LKUNIK            3
     C*
     C*
     C                   Z-ADD     UCUSTN        STICUS
     C                   READ      FIRM                                   33
     C     UCUSTN        IFNE      *ZEROS
     C     KUNKEY        CHAIN     CUNDL01                            33
     C                   END
     C*
     C                   Z-ADD     28            POSNBR
     C                   Z-ADD     5             LINNBR
     C   41              Z-ADD     7             LINNBR
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   SETOFF                                       30
     C                   Z-ADD     28            POSNBR
     C*
     C     LINNBR        IFEQ      5
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    STICUS
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C     STICUS        IFNE      *ZEROS
     C                   Z-ADD     7             LINNBR
     C                   END
     C                   GOTO      SLSØK
     C                   END
     C*
     C*
     C     LINNBR        IFEQ      7
     C                   MOVE      'J'           USPED             1
     C                   CALL      'SYFI10L'
     C                   PARM                    STINLK
     C                   PARM                    USPED
     C     STINLK        IFNE      '  '
     C                   Z-ADD     9             LINNBR
     C                   END
     C                   GOTO      SLSØK
     C                   END
     C*
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     SØKDT2        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      22
      *
     C     POSNBR        IFGE      8
     C     POSNBR        ANDLE     12
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSINPF
     C                   PARM                    UNVN             30
     C                   PARM      STINLK        ULK               2
     C                   Z-ADD     17            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C     POSNBR        IFGE      17
     C     POSNBR        ANDLE     21
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSINPT
     C                   PARM                    UNVN             30
     C                   PARM      STINLK        ULK               2
     C                   Z-ADD     30            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      * bsase flyttet foran innh/utkj-soner uten merking
s01  C*    POSNBR        IFGE      61
s01  C*    POSNBR        ANDLE     65
N01  C     POSNBR        IFGE      25
N01  C     POSNBR        ANDLE     29
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSINBA
     C                   PARM                    UNVN             30
     C                   PARM      STINLK        ULK               2
N01  C                   Z-ADD     36            POSNBR
     C                   GOTO      SLSØK2
     C                   END
S01  C*    POSNBR        IFGE      30
S01  C*    POSNBR        ANDLE     34
N01  C     POSNBR        IFGE      36
N01  C     POSNBR        ANDLE     40
     C                   CALL      'SYPO02X'
     C                   PARM                    WSINS1
s01  C*                  Z-ADD     46            POSNBR
N01  C                   Z-ADD     43            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
S01  C*    POSNBR        IFGE      46
S01  C*    POSNBR        ANDLE     50
N01  C     POSNBR        IFGE      43
N01  C     POSNBR        ANDLE     47
     C                   CALL      'SYPO02X'
     C                   PARM                    WSINS2
s01  C*                  Z-ADD     61            POSNBR
n01  C                   Z-ADD     55            POSNBR
     C                   GOTO      SLSØK2
     C                   END
N01  C     POSNBR        IFGE      62
N01  C     POSNBR        ANDLE     66
N01  C                   CALL      'SYPO02X'
N01  C                   PARM                    WSINS3
N01  C                   Z-ADD     69            POSNBR
N01  C                   GOTO      SLSØK2
N01  C                   END
N01  C     POSNBR        IFGE      69
N01  C     POSNBR        ANDLE     73
N01  C                   CALL      'SYPO02X'
N01  C                   PARM                    WSINS4
N01  C                   Z-ADD     69            POSNBR
N01  C                   GOTO      SLSØK2
N01  C                   END
      *
      *
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK2        ENDSR
      *
