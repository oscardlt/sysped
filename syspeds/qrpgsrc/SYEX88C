********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK/kolli I TURER ER ENDRET TIL 9.0, 7.3 ot 7.0
********************************************************************
      *
      ****************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TUR (PLUKKE OPPDRAG)           ***
      ****************************************************************
      *
     FTURER     UF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD9     IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD9R)
     FKODTA     IF   E           K DISK
     FSYEX88FC  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  DSNAAD                 1     30
     D  DSNAVN                 1     15
     D  DSSK                  16     16
     D  DSSTPO                17     30
     D                 DS
     D  HEVKT                  1      9  0
     D  W2VKT                  5      9  0
     D                 DS
     D  HEM3                   1      7  3
     D  W2M3                   3      6  2
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
S01  C*                  MOVE      *ZEROS        WSNT              5 0
S01  C*                  MOVE      *ZEROS        WSVKT             5 0
S01  C*                  MOVE      *ZEROS        WSM3              5 2
N01  C                   MOVE      *ZEROS        WSNT
N01  C                   MOVE      *ZEROS        WSVKT
N01  C                   MOVE      *ZEROS        WSM3
     C                   ADD       *ZEROS        WSLM              5 2
     C                   MOVE      *ZEROS        WSNTO             5 0
      * FYLL SUBFILE ER ENDRET HELT, IKKE MARKERT ALLE ENDRINGER.
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     HEAD9K        SETLL     HEAD9
     C     START1        TAG
     C     WSAVD         READE     HEAD9                                  94
     C  N94HEPRO         COMP      0                                  9494
      *
     C     START2        TAG
      *
     C     *IN94         DOWEQ     '0'
      *
     C     HEST          IFNE      ' '
     C                   GOTO      ST21
     C                   END
     C   31HEUR          IFNE      'A'
     C                   GOTO      START1
     C                   END
     C   31HEPLAN        IFNE      'X'
     C                   GOTO      START1
     C                   END
     C  N31HEUR          IFNE      'B'
     C                   GOTO      START1
     C                   END
     C     WSKNA         IFNE      *ZEROS
     C     WSKNA         ANDNE     HEKNA
     C                   GOTO      UTLES                                          DEL AV KEY
     C                   END
     C     WSKNK         IFNE      *ZEROS
     C     WSKNK         ANDNE     HEKNK
     C                   GOTO      START1
     C                   END
     C     WSKNS         IFNE      *ZEROS
     C     WSKNS         ANDNE     HEKNS
     C                   GOTO      START1
     C                   END
     C     WSFRA         IFNE      *BLANKS
     C     WSFRA         ANDNE     HESDF
     C     WSFRA         ANDNE     HESDFF
     C                   GOTO      START1
     C                   END
     C     WSTIL         IFNE      *BLANKS
     C     WSTIL         ANDNE     HESDT
     C     WSTIL         ANDNE     HESDVT
     C                   GOTO      START1
     C                   END
     C     WSOT          IFNE      *BLANKS
     C     WSOT          ANDNE     HEOT
     C                   GOTO      START1
     C                   END
     C     WSKDPL        IFNE      *BLANKS
     C     WSKDPL        ANDNE     HEKDPL
     C                   GOTO      START1
     C                   END
      *
      * AKKUMULER ANT VEKT MM I PLUKKLISTA..
     C                   ADD       HENT          WSNT
     C                   ADD       HEVKT         WSVKT
     C                   ADD(H)    HEM3          WSM3
     C                   ADD       HELM          WSLM
     C                   ADD       1             WSNTO
      *
      *  SETT SAMMEN NAVN/ POSTNR STED
     C                   MOVE      *BLANKS       DSNAAD
     C                   MOVEL     HENAK         DSNAVN
     C                   MOVE      '/'           DSSK
     C                   MOVEL     HEADK3        DSSTPO
     C                   MOVEL     DSNAAD        SFNAVN
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
      *
     C     ST21          TAG
      *
     C     WSAVD         READE     HEAD9                                  94
     C  N94HEPRO         COMP      0                                  9494
     C  N94              END
     C     UTLES         TAG
      *
     C                   MOVE      ZRECNO        XRECNO            5 0
     C                   Z-ADD     1             ZRECNO
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     XRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
     C                   SETOFF                                       9830
     C   03              Z-ADD     1             UFLAGG
     C   03              GOTO      SLUTT
     C   12              Z-ADD     9             UFLAGG
     C   12              GOTO      SLUTT
     C   08              GOTO      START6
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     *IN16         IFEQ      *ON
     C     *IN34         ANDEQ     *OFF
     C                   EXSR      PLUKKA
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      TESTIN
     C                   END
     C     NOESLE        IFEQ      'J'
     C                   GOTO      START
     C                   END
     C                   GOTO      START3
      *
     C     START6        TAG
     C                   EXFMT     SØKOPD
     C                   SETOFF                                       9930
     C   12              GOTO      START3
     C     KODTAK        CHAIN     KODTA                              99
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START6
     C                   MOVE      WSKNA         YKNA
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTIN        BEGSR
     C***********************************************
     C                   MOVE      'N'           NOESLE            1
     C                   SETON                                        32
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'                                           2<-B
     C     *IN34         ANDEQ     *OFF
     C     HENAS         IFEQ      XPLUKK                                         3<-B
     C                   MOVE      'SPO0010'     MSGNR
     C                   SETON                                        983032
     C                   END                                                      3<-E
     C  N98              EXSR      PLUKK
     C                   UPDATE    SUBFIL
     C  N98              SETOFF                                       32
     C   98              GOTO      SLTST
     C                   END                                                     2<-E
      *
     C  N98WSNR          IFEQ      '5'                                           2<-B
     C     HENAS         IFEQ      XPLUKK                                         3<-B
     C                   MOVE      'SPO0010'     MSGNR
     C                   SETON                                        983032
     C                   END                                                      3<-E
     C                   UPDATE    SUBFIL
     C  N98              EXSR      VISOPD
     C  N98              SETOFF                                       32
     C   98              GOTO      SLTST
     C                   END                                                     2<-E
      *
      * SLETT
      *
     C  N98WSNR          IFEQ      '4'                                           2<-B
     C     HENAS         IFEQ      XPLUKK                                         3<-B
     C                   MOVE      'SPO0010'     MSGNR
     C                   SETON                                        983032
     C                   END                                                      3<-E
     C                   MOVE      'N'           SLETTE
     C  N98              CALL      'SYFA93'
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    SLETTE            1
     C     SLETTE        IFEQ      'J'
     C                   MOVE      *BLANKS       HENAS
     C                   MOVEL     '*DELETE*'    HENAS
     C                   MOVE      'J'           NOESLE            1
     C                   END
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C  N98              SETOFF                                       32
     C   98              GOTO      SLTST
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
     C  N32              SETOFF                                       98
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C     SLTST         ENDSR
      *
      ***********************************************
     C     PLUKKA        BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        CHREC             4 0
     C                   MOVEA     '00'          *IN(91)
     C     PLST          TAG
     C                   ADD       1             CHREC
     C     CHREC         CHAIN     SUBFIL                             98
     C     *IN98         IFEQ      *OFF
     C     WSNR          IFEQ      'X'
     C     HENAS         OREQ      XPLUKK                                         3<-B
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      PLST
     C                   ENDIF                                                  <WSNR
     C                   EXSR      PLUKK
     C                   UPDATE    SUBFIL
     C                   GOTO      PLST
     C                   ENDIF                                                  <*IN98
     C                   ENDSR
     C*
     C***********************************************
     C     PLUKK         BEGSR
     C***********************************************
     C     HEADFK        CHAIN     HEADF                              33
     C     HEPRO         IFEQ      0
     C                   Z-ADD     UPRO          HEPRO
     C     HEKNA         IFEQ      *ZEROS
     C                   MOVE      TUKNA         HEKNA
     C                   END
     C                   END
     C                   MOVEL     TUBILN        HEHAWB
     C                   UPDATE    HEADFR
     C     HEPRO         IFEQ      UPRO
     C     TURERK        CHAIN     TURER                              33
     C                   ADD       HENT          TUTS
     C                   ADD       HEVKT         TUTVKT
     C                   ADD(H)    HEM3          TUTM3
     C                   ADD       HELM          TUTLM
     C                   ADD       1             TUAO
     C                   UPDATE    TURERR
     C                   MOVE      XPLUKK        HENAS
     C                   MOVE      *BLANKS       HENAK
     C                   MOVE      *BLANKS       SFNAVN
     C                   MOVE      *ZEROS        HENT
     C                   MOVE      *BLANKS       HEVS1
     C                   MOVE      *BLANKS       HESDF
     C                   MOVE      *BLANKS       HESDT
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        HEM3
     C                   MOVE      *ZEROS        HELM
     C                   MOVE      *BLANKS       HEOT
     C                   MOVE      *BLANKS       HEFR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     VISOPD        BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    UFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
     C                   MOVE      *ZEROS        UFLAGG
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     TURERK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UPRO
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEAD9K        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    YPRO
     C                   KFLD                    YKNA
     C     KODTAK        KLIST
     C                   KFLD                    YUNI
     C                   KFLD                    WSAVD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    UIMP              1
     C                   PARM                    UFLAGG            1 0
     C     UIMP          COMP      'J'                                    31
     C                   MOVE      *ZEROS        UFLAGG
      *
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      *ZEROS        YPRO              8 0
     C                   MOVE      *ZEROS        YKNA              8 0
     C                   MOVE      'A'           YUNI              1
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        HEM3
      *
     C                   MOVE      'DIV0702'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     30            XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XPLUKK           30
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
      *
     C     TURERK        CHAIN(N)  TURER                              33
      * 34 ON= KUN INNE PÅ AVD.OVERSIKT - KAN IKKE PLUKKE
     C     UPRO          COMP      0                                      34
      *
     C                   ENDSR
      *
