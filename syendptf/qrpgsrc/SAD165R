      *
      ***********************************************************
      *** PROGRAM SKAL E.F. MANIFEST - LISTE GoodsItem        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FTRACKF    O  A E             DISK
     FSADEXIL1  UF   E           K DISK
     FSAD165D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'SAD166R'
     d SAD166R         pr                  extpgm('SAD166R')
     d upro_                               like(upro)
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d u_li_                               like(u_li)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for SAD165R
     d sad165r         pr
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d upro_                               like(upro)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  *ENTRY Interface for Main Procedure
     d sad165r         pi
     d uavd                           4  0
     d utdn                           7  0
     d upro                           8  0
     d utype                          3a
     d uflagg                         1  0
      *
     d scatyp          s              2a
     d xtype           s              3a
     d yrecno          s                   like(zrecno)
     d u_avd           s              4  0
     d u_tdn           s              7  0
     d u_li            s              5  0
     d u_pro           s              8  0
     d u_sg            s              3
     d u_type          s              3
     d u_ie            s              1
     d u_runde         s             10
     d wdt             s              8a
     d wtm             s              6a
      *
      /free

         exsr init;

         *in93 = *on;
         write subctl;
         *in93 = *off;
         *in94 = *off;
         zrecno = 0;

         dow *in03 = *off;            // START                                   // 1<b

           setll (upro: uavd: utdn) sadexil1;
           reade(n) (upro: uavd: utdn) sadexil1;

           dow not %eof;                                                         // 2<b
             zrecno = zrecno + 1;
             write subfil;
             reade(n) (upro: uavd: utdn) sadexil1;
           enddo;                                                                // 2<e
           yrecno = zrecno;
           if zrecno > 12;                                                       // 2<b
             zrecno = 1;
           endif;                                                                // 2<e

           dow *in03 = *off;               // START3                             // 2<b
           // vis subfil og få input
             *in91 = *on;
             *in92 = *on;
             write bunn;
             if zrecno = 0;                                                      // 3<b
               exfmt nyvare;
             else;                                                               // 3
               exfmt subctl;
             endif;                                                              // 3<e

             *in30 = *off;
             *in98 = *off;
             *in99 = *off;
             select;                                                             // 3<b
             when *in03;                                                         // 3
               uflagg = 3;
               *inlr = *on;
               return;
             when *in05;                                                         // 3
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in06;                                                         // 3
               exsr lagopd;
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in12;                                                         // 3
               uflagg = 1;
               *inlr = *on;
               return;
             when zrecno <> 0;                                                   // 3
               exsr hent;
             endsl;                                                              // 3<e

           enddo;                                                                // 2<e

         enddo;                                                                  // 1<e

         *inlr = *on;
         return;

      //*********************************************
       begsr hent;

       msgnr = *blanks;
       *in90 = *off;
       *in91 = *off;
       *in92 = *off;
       *in98 = *off;
       *in99 = *off;

       readc subfil;
       dow not %eof;
         if eist <> 'S';
           select;
           when wsnr = 2 and *in49 = *off and eivt <> '*SLETTET';
             *in99 = *off;
             exsr chgopd;
           when wsnr = 4 and *in49 = *off;
             *in99 = *off;
             exsr sltopd;
           when wsnr = 5;
             *in99 = *off;
             exsr dspopd;
           endsl;
         endif;

         readc subfil;
       enddo;

       if *in99 = *off;
         msgnr = 'DIV0012';
         *in30 = *on;
       endif;
       *in99 = *off;

       endsr;

      //*********************************************
       begsr dspopd;

       xtype = 'DSP';            // SPØRRING
       u_avd = uavd;
       u_tdn = utdn;
       u_li  = eili;
       sad166r (upro: u_avd: u_tdn: u_li: xtype: uflagg);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr chgopd;

       xtype = 'CHG';            // ENDRING
       u_avd = uavd;
       u_tdn = utdn;
       u_li  = eili;
       sad166r (upro: u_avd: u_tdn: u_li: xtype: uflagg);

       chain(n) (upro: u_avd: u_tdn: u_li) sadexil1;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr sltopd;

       chain (upro: eiavd: eitdn: eili) sadexil1;
       if %found;
         //eist  = 'S';
         //update sadexifr;
         eivt = '*SLETTET';
         delete sadexifr;
         exsr ttsr;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr lagopd;

       xtype = 'CRT';
       u_avd = uavd;
       u_tdn = utdn;
       u_li  = 0;
       uflagg = 0;
       sad166r (upro: u_avd: u_tdn: u_li: xtype: uflagg);

       endsr;

      //*********************************************
       begsr ttsr;

       syge15c (wdt: wtm);

       ttavd  = u_avd;
       ttopd  = u_tdn;
       tttur  = 0;
       ttacti = 'EFC';
       ttdate = %dec(wdt:8:0);
       tttime = %dec(wtm:6:0);
       ttdatl = ttdate;
       tttiml = tttime;
       ttuser = uusr;
       tttext = 'Linje ' + %trim(%editc(eili:'Z')) + ' er fjernet fra '
               + 'fra E.F. manifest('
               + %TRIM(%EDITC(UPRO:'Z')) + ').';
       tttexl = tttext;
       ttedev = 'RMV';

       write trackfr;

       endsr;

      //*********************************************
       begsr init;

       syge15c (wdt: wtm);
       if utype = 'DSP';
         *in49 = *on;
       else;
       endif;
       write windowsz;

       endsr;

      /end-free
