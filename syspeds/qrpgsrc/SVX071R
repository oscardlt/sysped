      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED NY MERKE & NR              ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FSVNLMN    UF A E           K DISK
     FSVXKODL01 IF   E           K DISK
     FSVX071D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UTMN          IFNE      *BLANKS
     C     UNMN          ORNE      *BLANKS
     C                   EVAL      NVMN = UNMN
     C                   MOVE      UNMNSK        NVMNSK
     C                   MOVE      UTMN          TVMN
     C                   MOVE      UTMNSK        TVMNSK
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SVLMNK        SETLL     SVNLMN
     C     SVLMNK        READE(N)  SVNLMN                                 94
     C  N94TVLI2         IFGT      0
     C     SVLMNK        READE(N)  SVNLMN                                 94
     C                   END
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SVLMNK        READE(N)  SVNLMN                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C   03
     COR 12              GOTO      SLUTT
      *
     C   04              DO
     C                   MOVEL     W2MNSK        UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         W2MNSK
     C                   GOTO      START3
     C                   END
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C  N27W2NR          IFEQ      '1'
     C                   EXSR      CRTMN
     C                   GOTO      START3
     C                   END
      *
     C  N27ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     SVLMNK        SETLL     SVNLMN
     C     SVLMNK        READE     SVNLMN                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SVNLMNR
     C     SVLMNK        READE     SVNLMN                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UTMN
     C                   MOVE      *BLANKS       UTMNSK
     C                   MOVE      *BLANKS       UNMN
     C                   MOVE      *BLANKS       UNMNSK
     C                   MOVE      *ZEROS        XANTL             3 0
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     NVMN          IFNE      '*DELETE'
     C                   EVAL      UNMN = NVMN
     C                   MOVE      NVMNSK        UNMNSK
     C                   MOVE      TVMN          UTMN
     C                   MOVE      TVMNSK        UTMNSK
     C                   ADD       1             XANTL
     C     TVLI2         IFGT      0
     C                   WRITE     SVNLMNR
     C                   END
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     NVMN          IFNE      '*DELETE'
     C     XANTL         IFEQ      0
     C                   MOVE      TVMN          UTMN
     C                   MOVE      TVMNSK        UTMNSK
     C                   EVAL      UNMN = NVMN
     C                   MOVE      NVMNSK        UNMNSK
     C                   ADD       1             XANTL
     C     TVLI2         IFGT      0
     C                   WRITE     SVNLMNR
     C                   END
     C                   ELSE
     C                   ADD       1             XANTL
     C                   WRITE     SVNLMNR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     NVMN          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGMN
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     NVMN          COMP      '*DELETE'                              98
     C  N98TVMN          COMP      *BLANKS                            9898
     C   98              GOTO      SLHENT
     C                   EXSR      DLTMN
     C                   END
     C  N98WSNR          IFEQ      '9'
     C     NVMN          COMP      '*DELETE'                              98
     C  N98TVMN          COMP      *BLANKS                                98
     C   98              GOTO      SLHENT
     C                   EXSR      ANGMN
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTMN         BEGSR
     C***********************************************
     C     W2MN          IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'SVX0826'     MSGNR
     C                   GOTO      SLCRT
     C                   END
      *
     C     W2MNSK        IFNE      *BLANKS
     C                   MOVEL     W2MNSK        UKODE
     C     SVK01K        CHAIN     SVXKODL01                          89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLCRT
     C                   END
     C                   END
      *
     C                   EVAL      NVMN = W2MN
     C                   MOVE      W2MNSK        NVMNSK
     C                   MOVE      *BLANKS       TVMN
     C                   MOVE      *BLANKS       TVMNSK
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2MN
     C                   MOVE      *BLANKS       W2MNSK
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGMN         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   EVAL      WSMN = NVMN
     C                   MOVE      NVMNSK        WSMNSK
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   MOVEL     WSMNSK        UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         WSMNSK
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C     WSMN          IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'SVX0826'     MSGNR
     C                   GOTO      STCHG
     C                   END
      *
     C     WSMNSK        IFNE      *BLANKS
     C                   MOVEL     WSMNSK        UKODE
     C     SVK01K        CHAIN     SVXKODL01                          89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
     C                   END
      *
     C                   EVAL      NVMN = WSMN
     C                   MOVE      WSMNSK        NVMNSK
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTMN         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       NVMN
     C                   MOVEL     '*DELETE'     NVMN
     C                   MOVE      *BLANKS       NVMNSK
     C                   MOVE      *BLANKS       TVMN
     C                   MOVE      *BLANKS       TVMNSK
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     ANGMN         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
      * KUN SLETTE NY LINJE
     C                   MOVE      TVMN          NVMN
     C                   MOVE      TVMNSK        NVMNSK
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SVLMNK        KLIST
     C                   KFLD                    TVAVD
     C                   KFLD                    TVTDN
     C                   KFLD                    TVLI
     C                   KFLD                    TVLI2
     C     SVK01K        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    UKODE
      *
     C     *ENTRY        PLIST
     C                   PARM                    TVAVD
     C                   PARM                    TVTDN
     C                   PARM                    TVLI
     C                   PARM                    TVLI2
     C                   PARM                    UNMN             35
     C                   PARM                    UNMNSK            2
     C                   PARM                    UTMN             42
     C                   PARM                    UTMNSK            2
     C     *IN27         PARM                    UIN27             1
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      '012'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
      *
     C                   ENDSR
      *
