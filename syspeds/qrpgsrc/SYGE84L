********************************************************************
      * Sjekke om det er farlig gods som ikke kan samlastes
******* KUN HOVEDREGEL :
      * Det er IKKE tillatt å samlaste Eksplosiver=Faresedler 1, 1.4  (unntatt 1.4S), 1.5 og 1.6
      *                                med IKKE eksplosiver  (= andre faresedler ...)
      *+Det er ikke lov samlaste gods som har både  4.1 og 1 (i fareseddel) med annet gods
      *+Det er ikke lov samlaste gods som har både  5.2 og 1 (i fareseddel) med annet gods
      * OBS !!
      * Forutsetter at det er testing FØR linjen legges til(slipper å testet mot ikke les seg selv)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 endret gåndtering av 1.4S
********************************************************************
     FDOKUFVF   IF   E           K DISK
      *
     c     *ENTRY        PLIST
     c                   parm                    Uavd              4 0
     c                   parm                    UOpd              7 0
     c                   parm                    UNyKlasse         4
     c                   parm                    UNySeddel        10
     c                   parm                    usvar             1
     c                   move      '0'           usvar
     c     HeaKey        KLIST
     c                   kfld                    Uavd
     c                   kfld                    UOpd
      *
      * kun nødvendig å teste dersom det finnes annen ADR fra før på oppdraget
     C     Heakey        chain     DOKUFVF                            35
     c     *in35         cabeq     '1'           Slutt
     c                   movel     FFKLAS        LinKlasse         4
     c                   movel     FFSEDD        LinSeddel        10
     c                   exsr      TstAdr
      *
     c     Slutt         tag
     c                   seton                                        lr
     c                   return
     C***********************************************
     c     TstAdr        BEGSR
     C***********************************************
      * 1. Hovedregel:
      * Det er IKKE tillatt å samlaste Eksplosiver=Faresedler 1, 1.4  (unntatt 1.4S), 1.5 og 1.6
      *                                med IKKE eksplosiver
      * (Slik det ser ut i ADR-tabellene er alle hvor fareklasse begynner på 1..(og unntatt 1.4S)
      *  i de aktuelle fareseddel-gruppene, derfor tiltrekkelig å teste mot klasse= 1... )
      * (mange har klass=blank men seddel<>blank (det betyr jo et de definitivt IKKE er ekplosiver))
      *
N01   * 1.4S er i særstilling - kan blandes med det meste, men ikke 4.1 ( 1 ) / 5.2 ( 1 )
N01  c                   if        UnyKlasse='1.4S' or LinKlasse='1.4S'
N01  c                   goto      Regel2
N01  c                   endif
      *
      * Oppdraget Eksplosjonsfare ??
     c                   If        %subst(UnyKlasse:1:1)='1'
S01  c*                            and UnyKlasse<>'1.4S'                        (1.4S= ikke ...)
     c                   move      'E'           UnyEksplo         1            Eksplosiv
     c                   else
     c                   move      'I'           UnyEksplo                      IKKE Eksplosiv
     C                   endif
      * Linja -   Eksplosjonsfare ??
     c                   If        %subst(LinKlasse:1:1)='1'
     c                             and LinKlasse<>'1.4S'                        (1.4S= ikke ...)
     c                   move      'E'           LinEksplo         1            Eksplosiv
     c                   else
     c                   move      'I'           LiNEksplo                      IKKE Eksplosiv
     C                   endif
      *
      * Dersom NY linje ulik eksisterende  m.h.t eksplosjonsfare så kan de ikke blandes
     C                   if        UnyEksplo <> LinEksplo
     C                   move      '1'           Usvar
     c                   goto      UtTst
     C                   endif
      *
      *
      * 2. Hovedregel:
      * Det er ikke lov samlaste gods som har både  4.1 og 1 (i fareseddel) med annet gods
N01  C     Regel2        tag
     C                   if        UnySeddel = '4.1 ( 1 )' and
     C                             LinSeddel <>'4.1 ( 1 )'
     C                   move      '2'           Usvar
     c                   goto      UtTst
     C                   endif
     C                   if        UnySeddel <>'4.1 ( 1 )' and
     C                             LinSeddel = '4.1 ( 1 )'
     C                   move      '2'           Usvar
     c                   goto      UtTst
     C                   endif
      * 3. Hovedregel:
      * Det er ikke lov samlaste gods som har både  5.2 og 1 (i fareseddel) med annet gods
     C                   if        UnySeddel = '5.2 ( 1 )' and
     C                             LinSeddel <>'5.2 ( 1 )'
     C                   move      '3'           Usvar
     c                   goto      UtTst
     C                   endif
     C                   if        UnySeddel <>'5.2 ( 1 )' and
     C                             LinSeddel = '5.2 ( 1 )'
     C                   move      '3'           Usvar
     c                   goto      UtTst
     C                   endif
      *
     C     UtTst         ENDSR
