********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10117 * 01 CB  PTF10 1 Routing til
     H DATEDIT(*YMD)
     FHEADF     IF   E           K DISK
     FDOKUI     UF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFAK2      IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTDIS   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCNFF      UF   E             DISK    USROPN
     FLOCL01    IF   E           K DISK    USROPN
     FLOGF      IF A E             DISK    USROPN
     FFWB00F    IF A E             DISK    USROPN
     FFWB12F    IF A E             DISK    USROPN
     FFWB13F    IF A E             DISK    USROPN
     FFWB05F    IF A E             DISK    USROPN
     FFWB06F    IF A E             DISK    USROPN
     D MND             S              2  0 DIM(12) CTDATA PERRCD(1)
     D MNA             S              3    DIM(12) ALT(MND)
     D WR1             S              1    DIM(9)
     D WR2             S              1    DIM(17)
     D WR3             S              1    DIM(30)
     D                 DS
     D  DIAWB                  1     11  0
     D  XAWBW                  9     11  0
     D  DIAWB3                 1      3  0
     D  DIAWB8                 4     11  0
     D                 DS
     D  HEHAWB                 1      9
     D  XHAWB                  4      9  0
     D                 DS
     D  DIXXX1                 1      7
     D  DIXXV1                 1      3
     D  DIXXV2                 4      6
     D GRUNPR          C                   CONST('GRUNNPRIS')
     D                 DS
     D  DATT1                  1      2
     D  DATT2                  3      4
     D  DATT3                  5      6
     D  DATT                   1      6
     D                 DS
     D  IDAG1                  1      4  0
     D  IDAG2                  5      6  0
     D  IDAG3                  7      8  0
     D  IDAG                   1      8  0
     D                 DS
     D  W12DS                  1      1
     D  W12DS1                 1      3
     D  W12DS2                 4     20
     D  W12115                 1     20
     D                 DS
     D  WICAR1                 1      2
     D  WIFLT1                 3      6
     D  DIRUNR                 1      6
     D                 DS
     D  WICAR2                 1      2
     D  WIFLT2                 3      6
     D  DIRUN2                 1      6
     D                 DS
     D  WICAR3                 1      2
     D  WIFLT3                 3      6
     D  DIRUN3                 1      6
     D                 DS
     D  AWBDES                 1     14
     D  WHESDT                 4      6
     D                 DS
     D  ROUTIN                 1     15
     D  WWA                    1      3
     D  WWB                    5     10
     D  WWC                   11     11
     D  WWD                   12     13
     D                 DS
     D  WFRO3                  1     17
     D  DFROA3                 1      3
     D  DFROB3                 5     11
     D  DFROC3                13     14  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D                SDS
     D  WUSER                254    263
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C     WAVD          IFNE      *ZEROS
     C     WOPD          ANDNE     *ZEROS
     C     DOKUIK        CHAIN     DOKUI                              90
     C     *IN90         CABEQ     *ON           SLUTT
     C                   ELSE
     C     WAWB          IFNE      *BLANKS
      * Bruk AWB no. via HEADF for å få slått opp i DOKEF. Fjern så GOTO SLUTT
     C                   GOTO      SLUTT
     C                   ELSE
     C                   GOTO      SLUTT
     C                   ENDIF
     C                   ENDIF
     C     HEADFK        CHAIN     HEADF                              34
     C   34              GOTO      SLUTT
      *
     C     DIAWB         IFEQ      0
     C                   GOTO      SLUTT
     C                   END
      *
     C     DOKUIK        CHAIN     DOKUI                              33
      *
     C                   EXSR      PRINT
      *
     C                   MOVE      *OFF          *IN90
     C                   EXSR      OPNFIL                                       <Åpne filer
     C     *IN90         IFEQ      '0'
     C                   EXSR      WRTINH                                       <Write inhouse
     C     *IN90         IFEQ      '0'
     C                   EXSR      WRTLOG                                       <Write log
     C                   EXSR      SNDTAQ                                       <Send dataq
     C     DOKUIK        CHAIN     DOKUI                              90
     C     *IN90         IFEQ      '0'
     C                   MOVE      'T'           DICMN
     C                   EXCEPT    OPDDO2
     C                   ENDIF                                                  <*IN90
     C                   ENDIF                                                  <*IN90
     C                   ENDIF                                                  <*IN90
     C                   EXSR      CLOFIL                                       <Lukk filer
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     PRINT         BEGSR
      ***********************************************
      * BEREGNER GRUNNPRIS
      *
     C                   Z-ADD     DIFRAK        XTSTP1            8 0
     C     DIFVKT        MULT(H)   DIPRIS        XTSTP2            8 0
     C     XTSTP1        SUB       XTSTP2        XGRUNN            5 066
      *
     C                   Z-ADD     0             XCCFR             7 2
     C     DICCPP        COMP      'PP'                                   50
     C  N50DICCPP        COMP      'CP'                                   50
     C                   EXSR      BEREGN
      *
     C     DICCPP        IFEQ      'CP'
     C                   Z-ADD     XTOTK         XCCFR
     C                   Z-ADD     0             DIVGEB
     C                   Z-ADD     0             XMOMS             8 2
     C                   END
      *
     C     DICCPP        IFEQ      'PP'
     C     DICCPP        OREQ      'PC'
     C     HEPK9         IFNE      'J'
     C                   Z-ADD     KDSBL5        DIVGEB
      *          KDSBL5    MULT 0.2       XMOMS
      * VARIABLE VAT
     C                   MOVE      FITAX         FITAXB            4 4
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     IDAG          ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAXB            4 4
     C                   END
      *
     C     KDSBL5        MULT      FITAX         XMOMS
     C                   ELSE
     C                   Z-ADD     0             DIVGEB
     C                   Z-ADD     0             XMOMS
     C                   END
     C                   END
      *
     C     DICCPP        IFEQ      'CC'
     C                   Z-ADD     XTOTK         DIVGEB
     C                   END
      *
     C     DIFRA         CHAIN     STED2                              34
     C  N34              MOVEL     ST2NVN        XALUFT           17
     C   34              MOVE      *BLANKS       XALUFT
     C     DITIL         CHAIN     STED2                              34
     C  N34              MOVEL     ST2NVN        XBLUFT           17
     C   34              MOVE      *BLANKS       XBLUFT
      *
     C     HEPK2         COMP      'N'                                7171
     C   71HEPK2         COMP      ' '                                7171
      *
     C     DIFRAK        ADD       DIFS          DISUM
     C                   ADD       DIFPRE        DISUM
     C  N71DISUM         MULT      FITAX         DIMVA
     C                   ADD       XMOMS         DIMVA
     C                   Z-ADD(H)  DIMVA         XMVAHH            7 0
     C                   Z-ADD     XMVAHH        DIMVA
     C   71              MOVE      *ZEROS        DIMVA
      *
     C     DITIL         IFEQ      'LYR'
     C                   MOVE      *ZEROS        DIMVA
     C                   END
     C                   ADD       DIMVA         DISUM
     C                   ADD       1             XPRANT
     C                   ADD       DIVGEB        DISUM
     C     DIMVA         COMP      0                                  72
     C     XCCFR         COMP      0                                  73
     C     XAWBW         COMP      154                                    74
     C     DIRUN2        COMP      *BLANKS                            8282
     C     DIRUN3        COMP      *BLANKS                            8383
      *
     C     HEPK2         COMP      ' '                                    44
     C   44XHAWB         COMP      0                                  44
      *
     C     DIFS          COMP      *ZEROS                             47
      *
     C                   ENDSR
      ***********************************************
     C     BEREGN        BEGSR
      ***********************************************
     C                   SETOFF                                       34
     C     FAK2K         SETLL     FAK2
     C                   EXSR      SUM2
     C                   Z-ADD     XBRUTT        XTOTK             9 2
      *
     C                   ENDSR
      ***********************************************
     C     SUM2          BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        XTOTAL            9 2
     C                   MOVE      *ZEROS        XMVAGR            9 2
     C     STVALI        TAG
     C     FAK2K         READE     FAK2                                   34
     C  N34FAVK          IFEQ      'FFN'
     C                   GOTO      STVALI
     C                   END
      *
     C  N34FAKDM         IFEQ      'J'
     C                   ADD       FABELN        XMVAGR
     C                   END
     C  N34              ADD       FABELN        XTOTAL
     C  N34              GOTO      STVALI
      *
     C     XMVAGR        MULT(H)   FITAX         XMOMS
     C                   Z-ADD     XTOTAL        XBRUTT            9 2
      *
     C                   ENDSR
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     HEADFK        KLIST
     C                   KFLD                    DIAVD
     C                   KFLD                    DIOPD
     C     DOKUIK        KLIST
     C                   KFLD                    XRI
     C                   KFLD                    WAVD
     C                   KFLD                    WOPD
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KOAKNR
     C     FAK2K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XSK
     C     KODTAK        KLIST
     C                   KFLD                    XAUNI
     C                   KFLD                    UAVD
      *
     C     *ENTRY        PLIST
     C                   PARM                    AVD
     C                   PARM                    OPD
     C                   PARM                    AWB              11
     C                   PARM                    PGID
     C                   PARM                    PGIDNE
     C                   PARM                    PGPACP
     C                   PARM                    PGPACA
     C                   PARM                    PGCPAI
     C                   PARM                    PGCPAA
     C                   PARM                    PGMTM1
     C                   PARM                    PGMTM2
     C                   PARM                    PGMTM3
     C     *LIKE         DEFINE    HEAVD         AVD
     C     *LIKE         DEFINE    HEOPD         OPD
     C     *LIKE         DEFINE    HEAVD         WAVD
     C     *LIKE         DEFINE    HEOPD         WOPD
     C     *LIKE         DEFINE    LGID          PGID
     C     *LIKE         DEFINE    LGIDNE        PGIDNE
     C     *LIKE         DEFINE    LGPACP        PGPACP
     C     *LIKE         DEFINE    LGPACA        PGPACA
     C     *LIKE         DEFINE    LGCPAI        PGCPAI
     C     *LIKE         DEFINE    LGCPAA        PGCPAA
     C     *LIKE         DEFINE    LGMTM1        PGMTM1
     C     *LIKE         DEFINE    LGMTM2        PGMTM2
     C     *LIKE         DEFINE    LGMTM3        PGMTM3
      *
     C                   Z-ADD     AVD           WAVD
     C                   Z-ADD     OPD           WOPD
     C                   MOVEL     AWB           WAWB             11
      *
     C                   MOVE      'I'           XRI               1
     C                   MOVE      'F'           XSK               1
     C                   MOVE      'A'           XAUNI             1
     C                   MOVE      'DISPU'       XDISPU            5
     C                   MOVE      *ZEROS        XPRANT            2 0
     C                   MOVE      *BLANKS       WKOPFM            5
      *
     C                   READ      FIRM                                   33
     C                   MOVE      UDAY          IDAG3
     C                   MOVE      UMONTH        IDAG2
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          IDAG1
     C                   ELSE
     C     UYEAR         ADD       2000          IDAG1
     C                   END
      *
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     IDAG          ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAX
     C                   END
      *
     C     KODTAK        CHAIN     KODTA                              35
     C     XDISPU        CHAIN     KODTDIS                            33
     C     KUND1K        CHAIN     CUNDL01                            33
     C                   MOVEL     KNAVN         XKNA24           24
      *
     C                   MOVEL     WUSER         WWUSER           17
      *
     C                   ENDSR
      ***********************************************
     C     OPNFIL        BEGSR
      ***********************************************
      * DUMMY READ AV FILER
     C  N90
     CAN 90              READ      LOGF                                   90
     C  N90
     CAN 90              READ      FWB00F                                 90
     C  N90
     CAN 90              READ      FWB12F                                 90
     C  N90
     CAN 90              READ      FWB13F                                 90
     C  N90
     CAN 90              READ      FWB05F                                 90
     C  N90
     CAN 90              READ      FWB06F                                 90
      * ÅPNER FILER
     C                   OPEN      CNFF                                 90
     C  N90              OPEN      LOCL01                               90
     C  N90              OPEN      LOGF                                 90
     C  N90              OPEN      FWB00F                               90
     C  N90              OPEN      FWB12F                               90
     C  N90              OPEN      FWB13F                               90
     C  N90              OPEN      FWB05F                               90
     C  N90              OPEN      FWB06F                               90
      * HVIS ÅPNING AV FILER GIKK GALT KAN IKKE RUTINEN UTFØRES
     C     *IN90         CABEQ     '1'           OPNF99
     C     WWUSER        CHAIN     LOCL01                             9090
      * HVIS POST IKKE FINNES KAN IKKE RUTINEN UTFØRES
     C     *IN90         CABEQ     '1'           OPNF99
     C     1             CHAIN     CNFF                               9090
      * HVIS POST IKKE FINNES KAN IKKE RUTINEN UTFØRES
     C     *IN90         CABEQ     '1'           OPNF99
      * HENTER NESTE LEDIGE TRANSAKSJONS ID
     C                   ADD       1             CNRECN
     C                   UPDATE    CNFR
      * HVIS ALT GIKK GREIT KAN RUTINEN UTFØRES
     C     OPNF99        ENDSR
      ***********************************************
     C     WRTINH        BEGSR
      ***********************************************
      * SKRIVER POST TIL FWB00F
     C                   CLEAR                   FWB00R
     C                   Z-ADD     CNRECN        W00REC
     C                   Z-ADD     DIAWB3        W0211
     C                   Z-ADD     DIAWB8        W0213
     C                   MOVEL     DIFRA         W0221
     C                   MOVEL     DITIL         W0222
     C                   Z-ADD     DIANT         W0233
     C                   MOVEL     'K'           W0234
     C                   Z-ADD     DIVEKT        W0235
     C                   MOVEL     WICAR1        W0322A
     C                   MOVEL     WIFLT1        W0323A
     C                   Z-ADD     DIDAG1        W0325A
     C     W0323A        IFEQ      *BLANKS
     C     W0325A        OREQ      *ZEROS
     C                   MOVE      *BLANKS       W0322A
     C                   ENDIF
     C                   MOVEL     WICAR2        W0322B
     C                   MOVEL     WIFLT2        W0323B
     C                   Z-ADD     DIDAG2        W0325B
     C     W0323B        IFEQ      *BLANKS
     C     W0325B        OREQ      *ZEROS
     C                   MOVE      *BLANKS       W0322B
     C                   ENDIF
N01  C                   MOVEL     WICAR3        W0322C
N01  C                   MOVEL     WIFLT3        W0323C
N01  C                   Z-ADD     DIDAG3        W0325C
N01  C     W0323C        IFEQ      *BLANKS
N01  C     W0325C        OREQ      *ZEROS
N01  C                   MOVE      *BLANKS       W0322C
N01  C                   ENDIF
      * Hvis bare en flight
     C     DIRUN2        IFEQ      *BLANKS
     C     DIRUN3        ANDEQ     *BLANKS
     C     DIRUNR        ANDNE     *BLANKS
     C                   MOVEL     DITIL         W0422
     C                   MOVEL     WICAR1        W0423
     C                   ENDIF
      * Hvis 2 flights
     C     DIRUN3        IFEQ      *BLANKS
     C     DIRUN2        ANDNE     *BLANKS
     C     DIRUNR        ANDNE     *BLANKS
     C                   MOVEL     DIXXV1        W0422
     C                   MOVEL     WICAR1        W0423
     C                   MOVEL     DITIL         W0432A
     C                   MOVEL     WICAR2        W0433A
     C                   ENDIF
      * Hvis 3 flights
     C     DIRUN3        IFNE      *BLANKS
     C     DIRUN2        ANDNE     *BLANKS
     C     DIRUNR        ANDNE     *BLANKS
     C                   MOVEL     DIXXV1        W0422
     C                   MOVEL     WICAR1        W0423
     C                   MOVEL     DIXXV2        W0432A
     C                   MOVEL     WICAR2        W0433A
     C                   MOVEL     DITIL         W0432B
     C                   MOVEL     WICAR3        W0433B
     C                   ENDIF
     C                   MOVEL     HEKNS         W0522
     C                   MOVEL     *BLANKS       W0542
     C                   MOVEL     DISNAV        W0542
     C                   MOVEL     *BLANKS       W0552
     C                   MOVEL     DISAD1        W0552
     C                   Z-ADD     1             XW                3 0
     C                   Z-ADD     0             MY                3 0
     C                   MOVEA     *BLANKS       WR3
     C                   MOVEA     *BLANKS       WR2
     C                   MOVEA     *BLANKS       WR1
     C                   MOVEA     DISAD3        WR3
     C     WR3(XW)       DOWNE     *BLANK
     C     XW            ANDLE     9
     C                   MOVEA     WR3(XW)       WR1(XW)
     C                   ADD       1             XW
     C                   ENDDO
     C                   DO        17
     C                   ADD       1             XW
     C                   ADD       1             MY
     C                   MOVEA     WR3(XW)       WR2(MY)
     C                   ENDDO
     C                   MOVEA     WR2           W0562
     C                   MOVEL     *BLANKS       W0564
     C                   MOVEL     'NO'          W0572
     C     W0572         IFEQ      *BLANKS
     C                   MOVEL     'NO'          W0572
     C                   ENDIF
     C                   MOVEA     WR1           W0574
     C     DISTLF        IFNE      *BLANKS
     C                   Z-ADD     CNRECN        W05REC
     C                   MOVEL     'TE'          W0582
     C                   MOVEL     DISTLF        W0584
     C                   WRITE     FWB05R
     C                   ENDIF
     C                   MOVEL     *BLANKS       W0622
     C                   MOVEL     *BLANKS       W0642
     C                   MOVEL     DIKNAV        W0642
     C                   MOVEL     *BLANKS       W0652
     C                   MOVEL     DIKAD1        W0652
     C     W0652         IFEQ      *BLANKS
     C                   MOVEL     'X'           W0652
     C                   ENDIF
     C                   Z-ADD     1             XW                3 0
     C                   Z-ADD     0             MY
     C                   MOVEA     *BLANKS       WR3
     C                   MOVEA     *BLANKS       WR2
     C                   MOVEA     *BLANKS       WR1
     C                   MOVEA     HEADK3        WR3
     C     WR3(XW)       DOWNE     *BLANK
     C     XW            ANDLE     9
     C                   MOVEA     WR3(XW)       WR1(XW)
     C                   ADD       1             XW
     C                   ENDDO
     C                   DO        17
     C                   ADD       1             XW
     C                   ADD       1             MY
     C                   MOVEA     WR3(XW)       WR2(MY)
     C                   ENDDO
     C                   MOVEA     WR2           W0662
     C                   MOVEL     *BLANKS       W0664
     C                   MOVEL     'NO'          W0672
     C     W0672         IFEQ      *BLANKS
     C                   MOVEL     'NO'          W0672
     C                   ENDIF
     C                   MOVEA     WR1           W0674
     C     DIKTLF        IFNE      *BLANKS
     C                   Z-ADD     CNRECN        W06REC
     C                   MOVEL     'TE'          W0682
     C                   MOVEL     DIKTLF        W0684
     C                   WRITE     FWB06R
     C                   ENDIF
     C                   MOVEL     *BLANKS       W0722
     C                   Z-ADD     KOAIAT        W0724
     C     W0724         IFNE      0
     C                   Z-ADD     KOAIA2        W0726
     C                   ELSE
     C                   Z-ADD     0             W0726
     C                   ENDIF
     C                   MOVEL     *BLANKS       W0728
     C                   MOVEL     KNAVN         W0742
     C                   MOVEL     ADR3          W0752
     C                   MOVEL     'NOK'         W113
     C                   MOVEL     DICCPP        W115
     C                   MOVEL     DICCPP        W1171
     C                   MOVE      DICCPP        W1172
     C                   Z-ADD     0             W1182
     C                   Z-ADD     0             W11101
     C                   Z-ADD     DIFORS        W11112
     C     *IN50         IFEQ      '1'
     C                   Z-ADD     DIFRAK        W1423
     C                   Z-ADD     0             W1433
     C                   Z-ADD     DIMVA         W1443
     C                   Z-ADD     DIVGEB        W1463
     C                   Z-ADD     DIFPRE        W1473
     C                   ADD       DIFS          W1473
     C                   Z-ADD     DISUM         W1483
     C                   ELSE
     C                   Z-ADD     DIFRAK        W1523
     C                   Z-ADD     0             W1533
     C                   Z-ADD     DIMVA         W1543
     C                   Z-ADD     DIVGEB        W1563
     C                   Z-ADD     DIFPRE        W1573
     C                   ADD       DIFS          W1573
     C                   Z-ADD     DISUM         W1583
     C                   ENDIF
     C                   MOVEL     XKNA24        W163
     C                   MOVE      UDAY          W1722
     C                   Z-ADD     1             MX                3 0
     C     UMONTH        LOOKUP    MND(MX)                                42
     C   42              MOVE      MNA(MX)       W1723
     C                   MOVE      UYEAR         W1724
     C                   MOVEL     XALUFT        W1726
     C                   MOVEL     USG           W1732
     C                   MOVEL     DIMERK        W183A
     C     DIRUN3        IFNE      *BLANKS
     C                   MOVEL     W183A         W183B
     C                   MOVEL     *BLANKS       W183A
     C                   MOVEL     DITIL         DFROA3
     C                   MOVEL     DIRUN3        DFROB3
     C                   Z-ADD     DIDAG3        DFROC3
     C                   MOVEL     WFRO3         W183A
     C                   ENDIF
     C                   WRITE     FWB00R
      * SKRIVER POSTER TIL FWB12F (RATE DESCRIPTION)
      * Endret slik at basic raten evt. skrives først
      * Hvis grunnpris er utfylt lages det en egen linje
     C     XGRUNN        IFGT      0
     C                   CLEAR                   FWB12R
     C                   Z-ADD     CNRECN        W12REC                         <FELLES
     C                   MOVE      'B'           W1253
     C                   Z-ADD     XGRUNN        W1293
     C                   MOVEL     *BLANKS       W12115
      *                    MOVELGRUNPR    W12115
     C                   WRITE     FWB12R
     C                   ENDIF                                                  <XGRUNN
     C                   CLEAR                   FWB12R
     C                   Z-ADD     CNRECN        W12REC                         <FELLES
      * Hvis HEPK9 = 'J' ---> JETPAK
     C     HEPK9         IFEQ      'J'
     C                   MOVE      'P'           W12192
     C                   ENDIF
     C                   Z-ADD     DIANT         W1233A
     C                   MOVEL     *BLANKS       W1233B
     C                   Z-ADD     DIVEKT        W1243
     C     W1243         IFGT      0
     C                   MOVE      'K'           W1242
     C                   ELSE
     C                   MOVE      *BLANK        W1242
     C                   ENDIF
     C                   Z-ADD     DIFVKT        W1273
     C     HEPK9         IFNE      'J'
     C                   Z-ADD     DIPRIS        W1283
     C     DIFVKT        MULT      DIPRIS        W1293
     C                   ELSE
     C                   Z-ADD     DIFRAK        W1293
     C                   ENDIF
     C                   MOVEL     *BLANKS       W12115
     C                   MOVEL     DISLAG        W12115
     C     W12DS         IFGE      '0'
     C     W12DS         ANDLE     '9'                                          <DIM. INFO
     C                   MOVEL     *BLANKS       W12115
     C                   MOVE      'DIM'         W12DS1
     C                   MOVEL     DISLAG        W12DS2
     C                   ENDIF                                                  <W12DS
     C                   WRITE     FWB12R
      * Hvis tekstlinje 2 er utfylt lages det en egen linje
     C     DIVSL2        IFNE      *BLANKS
     C                   CLEAR                   FWB12R
     C                   Z-ADD     CNRECN        W12REC                         <FELLES
     C                   MOVEL     *BLANKS       W12115
     C                   MOVEL     DIVSL2        W12115
     C                   WRITE     FWB12R
     C                   ENDIF                                                  <DIVSL2
      * SKRIVER POSTER TIL FWB13F (OTHER CHARGES)
     C     DIFPRE        IFGT      *ZEROS
     C     DIFS          ORGT      *ZEROS
     C     *IN50         IFEQ      '1'
     C     DIFPRE        IFNE      *ZEROS
     C     DIFS          ANDNE     *ZEROS
     C                   CLEAR                   FWB13R
     C                   Z-ADD     CNRECN        W13REC                         <FELLES
     C                   MOVE      'P'           W1322
     C                   MOVE      'IN'          W1331A
     C                   MOVE      'C'           W1332A
     C                   Z-ADD     DIFPRE        W1333A
     C                   MOVE      'DF'          W1331B
     C                   MOVE      'C'           W1332B
     C                   Z-ADD     DIFS          W1333B
     C                   ENDIF
     C     DIFPRE        IFNE      *ZEROS
     C     DIFS          ANDEQ     *ZEROS
     C                   CLEAR                   FWB13R
     C                   Z-ADD     CNRECN        W13REC                         <FELLES
     C                   MOVE      'P'           W1322
     C                   MOVE      'IN'          W1331A
     C                   MOVE      'C'           W1332A
     C                   Z-ADD     DIFPRE        W1333A
     C                   ENDIF
     C     DIFPRE        IFEQ      *ZEROS
     C     DIFS          ANDNE     *ZEROS
     C                   CLEAR                   FWB13R
     C                   Z-ADD     CNRECN        W13REC                         <FELLES
     C                   MOVE      'P'           W1322
     C                   MOVE      'DF'          W1331A
     C                   MOVE      'C'           W1332A
     C                   Z-ADD     DIFS          W1333A
     C                   ENDIF
     C                   ELSE                                                   <*IN50
     C     DIFPRE        IFNE      *ZEROS
     C     DIFS          ANDNE     *ZEROS
     C                   CLEAR                   FWB13R
     C                   Z-ADD     CNRECN        W13REC                         <FELLES
     C                   MOVE      'C'           W1322
     C                   MOVE      'IN'          W1331A
     C                   MOVE      'C'           W1332A
     C                   Z-ADD     DIFPRE        W1333A
     C                   MOVE      'DF'          W1331B
     C                   MOVE      'C'           W1332B
     C                   Z-ADD     DIFS          W1333B
     C                   ENDIF
     C     DIFPRE        IFNE      *ZEROS
     C     DIFS          ANDEQ     *ZEROS
     C                   CLEAR                   FWB13R
     C                   Z-ADD     CNRECN        W13REC                         <FELLES
     C                   MOVE      'C'           W1322
     C                   MOVE      'IN'          W1331A
     C                   MOVE      'C'           W1332A
     C                   Z-ADD     DIFPRE        W1333A
     C                   ENDIF
     C     DIFPRE        IFEQ      *ZEROS
     C     DIFS          ANDNE     *ZEROS
     C                   CLEAR                   FWB13R
     C                   Z-ADD     CNRECN        W13REC                         <FELLES
     C                   MOVE      'C'           W1322
     C                   MOVE      'DF'          W1331A
     C                   MOVE      'C'           W1332A
     C                   Z-ADD     DIFS          W1333A
     C                   ENDIF
     C                   ENDIF
      *
     C                   WRITE     FWB13R
     C                   ENDIF
      *
     C     WRT99         ENDSR
      ***********************************************
     C     WRTLOG        BEGSR
      ***********************************************
     C                   CLEAR                   LOGR
     C                   MOVE      PGID          LGID
     C                   MOVE      PGIDNE        LGIDNE
     C                   MOVE      PGPACP        LGPACP
     C                   MOVE      PGPACA        LGPACA
     C                   MOVE      PGCPAI        LGCPAI
     C                   MOVE      PGCPAA        LGCPAA
     C                   MOVE      PGMTM1        LGMTM1
     C                   MOVE      PGMTM2        LGMTM2
     C                   MOVE      PGMTM3        LGMTM3
     C                   Z-ADD     CNRECN        LGRECN
     C                   Z-ADD     CNRECN        LGTRID
     C                   MOVEL     LOID          LGSID
     C                   MOVEL     'FWB'         LGMSID
     C                   MOVEL     'T'           LGTR
     C                   MOVEL     'A'           LGSTAT
     C                   MOVEL     UDATE         LGDATE
     C                   TIME                    LGTIME
     C                   MOVEL     'HAGF'        LGHAAG
     C     CNCOD1        IFEQ      'Y'
     C                   MOVEL     *BLANKS       LGHAAG
     C                   ENDIF
     C     LGMTM1        IFNE      *BLANKS
     C                   MOVEL     *BLANKS       LGHAAG
     C                   ENDIF
      *
     C                   WRITE     LOGR
      *
     C                   ENDSR
      ***********************************************
     C     SNDTAQ        BEGSR
      ***********************************************
     C                   MOVEL     'SMADTATQ'    QTNAME           10
     C                   MOVEL     '*LIBL'       QTLIB            10
     C                   Z-ADD     9             QTLEN             5 0
     C                   MOVEL     *BLANKS       QTFIEL            9
     C                   MOVEL     LGRECN        QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM                    QTNAME
     C                   PARM                    QTLIB
     C                   PARM                    QTLEN
     C                   PARM                    QTFIEL
      *
     C                   ENDSR
      ***********************************************
     C     CLOFIL        BEGSR
      ***********************************************
      * CLOSER FILER
     C                   CLOSE     CNFF                                 90
     C                   CLOSE     LOCL01                               90
     C                   CLOSE     LOGF                                 90
     C                   CLOSE     FWB00F                               90
     C                   CLOSE     FWB12F                               90
     C                   CLOSE     FWB13F                               90
     C                   CLOSE     FWB05F                               90
     C                   CLOSE     FWB06F                               90
      *
     C                   ENDSR
     ODOKUIR    E            OPDDO2
     O                       DICMN
**
01JAN
02FEB
03MAR
04APR
05MAY
06JUN
07JUL
08AUG
09SEP
10OCT
11NOV
12DEC
