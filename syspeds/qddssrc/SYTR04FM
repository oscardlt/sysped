     A*%%TS  SD  20140515  123215  JOVO        REL-V7R1M0  5770-WDS
N01  A* IKKE SLETTE HVIS BILER..../ OPT 6=BILER
N02  A* DU = INTERNT DUP-OPPDRAG
N03  A* MC = IFTMCS
N04  A* P4 = POSTEN IFTMIN D04 A
N05  A* P5 = BRING IFTMIN D04 A
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/TRAN)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A                                      HLPPNLGRP(HLPSCR SYTR04PNL)
     A                                      HLPTITLE('VEDLIKEHOLD TRANSPORTØRER-
     A                                      ')
     A          R SUBFIL                    SFL
     A            VM0004        35   H
     A            VM0010        35   H
     A            VMINTP    R        H
     A            VMFIRM    R        H
     A            VM0035    R        H
     A            VMPREF    R        H
     A            VMRECN    R        H
     A            VMSNLA    R        H
     A            VMSNLE    R        H
     A            VMRECM    R        H
     A            VMLAYO    R        H
     A            VMLMOM    R        H
     A            VMM3OM    R        H
     A            VMRAB     R        H
     A            VMALFA    R        H
     A            VMTKAT    R        H
     A            VMTEXT    R        H
     A            VMSTOP    R        H
     A            VMSTDA    R        H
E06  A            WSNR           1A  B  6  2VALUES(' ' '1' '2' '4' '6' '8')
     A            VMTRAN    R        O  6  4EDTCDE(4)
     A            VMTRKU    R        O  6 13EDTCDE(4)
     A            VMTRLE    R        O  6 22EDTCDE(4)
     A            VMINCR    R        O  6 31
     A            VMNAVN    R        O  6 33
     A  55                                  COLOR(RED)
     A  56                                  COLOR(WHT)
     A            WW0004         8A  O  6 64TEXT('NODI Id Avsender')
     A            WW0010         8A  O  6 73TEXT('NODI Id Mottaker')
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20010423  155652  JOVO        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYTR04'
     A                                      COLOR(BLU)
     A N50                              1 19MSGCON(045 DIV3304 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  50                              1 19MSGCON(045 DIV3791 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 SHU1757 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50                              4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  50                              4  2MSGCON(030 DIV3792 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 33MSGCON(015 JOV1151 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 65'Interchange ID'
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(079 DIV3305 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A*
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(079 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
N01  A            MSGNR          7A  P
N01  A N98 30     MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R VARELI
     A*%%TS  SD  20140515  123215  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      CF04(04 'Forrige')
NXX  A                                      CF14(14 'FLERE PARAM')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTR04'
     A                                      COLOR(BLU)
     A N50                              1 19MSGCON(045 DIV3304 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  50                              1 19MSGCON(045 DIV3791 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(018 DIV5049 *LIBL/SYMSGF)
     A            WSTRAN    R        B  3 21REFFLD(VMTRAN)
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  40                                  COLOR(BLU)
     A  40                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                      COMP(NE 0)
     A            WSNAVN    R        B  3 32REFFLD(VMNAVN)
     A                                      DSPATR(UL)
     A  40                                  COLOR(BLU)
     A  40                                  DSPATR(PR)
     A                                  4  2MSGCON(018 DIV5050 *LIBL/SYMSGF)
     A            WSTRKU    R        B  4 21REFFLD(VMTRKU)
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  4 32COLOR(WHT)
     A                                  5  2MSGCON(018 DIV5051 *LIBL/SYMSGF)
     A            WSTRLE    R        B  5 21REFFLD(VMTRLE)
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            LNAVN         30A  O  5 32COLOR(WHT)
     A                                  6  2MSGCON(018 DIV5052 *LIBL/SYMSGF)
     A            WSINCR    R     D  B  6 21REFFLD(VMINCR)
     A                                      DSPATR(UL)
     A                                  6 32'(0,1,2,3,4,5)'
     A                                  7  2MSGCON(018 DIV5053 *LIBL/SYMSGF)
     A            WS0004    R        B  7 21REFFLD(VM0004)
     A                                      DSPATR(UL)
     A                                  8  2MSGCON(018 DIV5054 *LIBL/SYMSGF)
     A            WS0010    R        B  8 21REFFLD(VM0010)
     A                                      DSPATR(UL)
     A                                  9  2MSGCON(018 DIV5055 *LIBL/SYMSGF)
     A            WSPREF    R        B  9 21REFFLD(VMPREF)
     A                                      DSPATR(UL)
     A                                 10  2MSGCON(018 DIV5056 *LIBL/SYMSGF)
     A            WSSNLA         2Y 0B 10 21EDTCDE(4)
     A            WSSNLE         5Y 0B 10 24EDTCDE(4)
     A            WSRECN    R        B 10 30REFFLD(VMRECN)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(018 DIV5057 *LIBL/SYMSGF)
     A            WSRECM    R        B 11 30REFFLD(VMRECN)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 11 41MSGCON(040 JOV1410 *LIBL/SYMSGF)
     A                                 12  2MSGCON(018 DIV5058 *LIBL/SYMSGF)
     A            WS0035    R        B 12 21REFFLD(VM0035)
     A                                      DSPATR(UL)
     A                                      VALUES(' ' '1' '2' '3')
     A                                 13  2MSGCON(018 DIV5059 *LIBL/SYMSGF)
     A            WSLAYO    R        B 13 21REFFLD(VMLAYO)
     A                                      DSPATR(UL)
     A                                      VALUES('  ' 'LG' 'TG' 'PO' 'DU' 'MC-
     A                                      ' 'P4' 'DF' 'BR')
S05  A*                                     ' 'P4' 'DF')
S04  A*                                     ')
S02  A*                                13 32MSGCON(030 DIV5060 *LIBL/SYMSGF)
     A                                 13 28MSGCON(051 JOV1342 *LIBL/SYMSGF)
     A                                 14  2MSGCON(018 DIV5061 *LIBL/SYMSGF)
     A            WSLMOM    R        B 14 21REFFLD(VMLMOM)
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 14 28MSGCON(051 SHU1077 *LIBL/SYMSGF)
     A                                 15  2MSGCON(018 DIV5062 *LIBL/SYMSGF)
     A            WSM3OM    R        B 15 21REFFLD(VMM3OM)
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 16  2MSGCON(018 DIV5063 *LIBL/SYMSGF)
     A            WSRAB     R        B 16 21REFFLD(VMRAB)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 17  2MSGCON(018 TBR0048 *LIBL/SYMSGF)
     A            WSTKAT         1A  B 17 21DSPATR(UL)
     A                                 18  2MSGCON(018 JOV0250 *LIBL/SYMSGF)
     A            WSTEXT        60A  B 18 21
     A  75                                  DSPATR(RI)
     A                                 19  2MSGCON(018 JOV0251 *LIBL/SYMSGF)
     A            WSSTOP         1A  B 19 21
     A                                 19 32MSGCON(040 JOV0252 *LIBL/SYMSGF)
     A            WSSTDA         6S 0O 19 73
     A*                                20  2MSGCON(018 SHU0788 *LIBL/SYMSGF)
     A                                 20  2MSGCON(018 JOV1690 *LIBL/SYMSGF)
     A            WSINTP         7A  B 20 21CHECK(LC)
     A            WSALF         10A  B 22 22DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 63MSGCON(017 JOV1632 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(017 JOV1632 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2TRAN    R        B  2  4REFFLD(VMTRAN)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSALFA        10A  B  2 33COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL9
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSALFA        10A  B  2  4COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYTR04'
     A                                      COLOR(BLU)
     A N50                              1 19MSGCON(045 DIV3304 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  50                              1 19MSGCON(045 DIV3791 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(018 DIV5049 *LIBL/SYMSGF)
     A            VMTRAN    R        O  3 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            VMNAVN    R        O  3 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(018 DIV5050 *LIBL/SYMSGF)
     A            VMTRKU    R        O  4 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  4 32COLOR(WHT)
     A                                  5  2MSGCON(018 DIV5051 *LIBL/SYMSGF)
     A            VMTRLE    R        O  5 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            LNAVN         30A  O  5 32COLOR(WHT)
     A                                  6  2MSGCON(018 DIV5052 *LIBL/SYMSGF)
     A            VMINCR    R        O  6 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 32'(0,1,2)'
     A                                  7  2MSGCON(018 DIV5053 *LIBL/SYMSGF)
     A            VM0004    R        O  7 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(018 DIV5054 *LIBL/SYMSGF)
     A            VM0010    R        O  8 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(018 DIV5055 *LIBL/SYMSGF)
     A            VMPREF    R        O  9 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(018 DIV5056 *LIBL/SYMSGF)
     A            VMRECN    R        O 10 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(018 DIV5057 *LIBL/SYMSGF)
     A            VMRECM    R        O 11 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(018 DIV5058 *LIBL/SYMSGF)
     A            VM0035    R        O 12 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2MSGCON(018 DIV5059 *LIBL/SYMSGF)
     A            VMLAYO    R        O 13 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13 32MSGCON(030 DIV5060 *LIBL/SYMSGF)
     A                                 14  2MSGCON(018 DIV5061 *LIBL/SYMSGF)
     A            VMLMOM    R        O 14 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(018 DIV5062 *LIBL/SYMSGF)
     A            VMM3OM    R        O 15 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16  2MSGCON(018 DIV5063 *LIBL/SYMSGF)
     A            VMRAB     R        O 16 22DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 17  2MSGCON(018 TBR0048 *LIBL/SYMSGF)
     A            VMTKAT         1A  O 17 22DSPATR(UL)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1 19MSGCON(045 DIV3304 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1  2'SYSPED/SYTR04'
     A                                      COLOR(BLU)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(079 DIV3305 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
