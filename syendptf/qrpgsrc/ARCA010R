**********************************************************************************************
      *  Arkiver dokument knyttet KUN TIL L1 bilagsnr(ikke oppdrag / tur/Kbilag mm)
**********************************************************************************************
      * OBS - FØR KJØRING må typen være definert i fil ARKTXT
      *       og denne bør gjerne peke på "eksternt" arkivlager med
      *       avvikende bane (f.eks /pdf/scan ) (fil ARKEXT)
**********************************************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 ignorer evt timestamp bakerst ...xlsx_2009-11-27
P8087 * 02 sh  PTF9 år i strekkode
10XXX * 03 SH  PTF11 finne extension
10XXX * 04 SH  PTF12 test også levresk
      * 05 JOV PTF12 Hvis lagring i google cloud send rett dit (den rutinen sletter lokalt)
********************************************************************
      *""""""""""""""""
     H DFTACTGRP(*NO)
      *""""""""""""""""
     FFIRMARC   IF   E           K DISK
N05  FFIRMGOOG  IF   E           K DISK
     FARKIVP    O    E             DISK
     FFIRM      IF   E             DISK
s02  F*POSL03   IF   E           K DISK
n02  FHPOHL02   IF   E           K DISK
n04  FLREHL08   IF   E           K DISK
n04  FKREHL05   IF   E           K DISK
     FARKTXT    IF   E           K DISK    PREFIX(X_)
     FARKEXT    IF   E           K DISK    PREFIX(Y_)
      *""""""""""""""""
     D DirName         S            100A
     D EntryName       S            120A
     D EntryPath       S            256A
      *________________
      * Andre variabler
      *""""""""""""""""
n02  D AAR             S              2
n02  D AAR4            S              4
     D UND             S             10
     D UND7            S              7
     D TYPE            S              2
     D NULL            S              1A   INZ(X'00')
     D CMDLINE         S            512
     D CMDLEN          S             15  5
     D NEWDIR          S            100
     D NEWFILE         S            100
     D NEWOBJ          S            200
s01  D*NEWEND          S              4
s03  D*NEWEND          S             10
     D NEWRAN          S             10

     C                   EXSR      INIT
     C                   EXSR      MOV
     C     OkMov         IFEQ      'J'
     C                   EXSR      ARK
     C                   end
     C                   EVAL      *inlr = *on
     C                   RETURN

      *////////////////////////////////////////////////////////////
      *  Init                                                INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     *entry        PLIST
     C                   PARM                    DirName
     C                   PARM                    EntryName
     C                   PARM                    EntryPath
N02  c                   z-add     2000          bilaar
      * Type er 2 første av filnavn
     C                   EVAL      TYPE = %subst(EntryName:1:2)
N02  C                   EVAL      AAR = %subst(EntryName:3:2)
N02  C                   Movel     AAR           AAR2              2
N02  C                   TESTN                   AAR2                 30
N02  C                   IF        *IN30=*OFF
N02  C                   EVAL      *INLR=*ON
N02  C                   RETURN
N02  C                   ENDIF
N02  c                   move      aar           bilaar
N02  c                   move      bilaar        aar4
      * Kundenr  er 7 neste av filnavn (3-9)
s02  C*                  EVAL      UND = %subst(EntryName:3:7)
N02  C                   EVAL      UND = %subst(EntryName:5:7)
     C                   Movel     UND           UND7
     C                   TESTN                   UND7                 30
     C                   IF        *IN30=*OFF
     C                   EVAL      *INLR=*ON
     C                   RETURN
     C                   ENDIF
     C                   Movel     UND           BILNR
      * Les firmafil for å få tak i firmakode.
     C                   READ      FIRM
s02  c*    BilKey        Chain     HPOSL03                            33
n02  c     BilKey        Chain     HPOHL02                            33
n04  c   33BilKey        Chain     LREHL08                            33
n05  c   33BilKey        Chain     KREHL05                            33
     C     *IN33         IFEQ      *ON
     C                   EVAL      *inlr = *on
     C                   RETURN
     C                   end
      * Definiere parameterliste
     C     QCMDEXC       PLIST
     C                   PARM                    CMDLINE
     C                   PARM                    CMDLEN
     C                   READ      FIRMARC                                41

     C     EXKEY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    X_ARKLAG
      * kan IKKE krysse årstall (bilaget må være for inneværende år)
     C     BilKey        KLIST
     C                   KFLD                    FIFIRM
N02  C                   KFLD                    BILAAR
     C                   KFLD                    BILNR
      *
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Flytt og endre navn på fil                           MOV /
      *////////////////////////////////////////////////////////////
     C     MOV           BEGSR
      * Hent dato og klokkeslett
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      * Hent slutt på filnavn (ex. .pdf) fra frafil
s03  C*    '.'           SCAN      EntryName     X                 3 0
s01  C*                  EVAL      NEWEND = %SUBST(EntryName:X:4)
s03  C*    '_'           SCAN      EntryName:X   Y                 3 0
s03  c*    Y             ifeq      *zeros
s03  C*    ' '           SCAN      EntryName:X   Y                 3 0
s03  c*                  end
s03  C*                  EVAL      NEWEND = %SUBST(EntryName:X:y-x)
n03  c                   eval      par512=EntryName
n03  c                   call      'SYGE112'
n03  c                   parm                    par512          512
n03  c                   parm                    NEWEND           10
      * Generer random number
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN
      * Definier ny katalog Hent fra standard
     C                   EVAL      NEWDIR = '/' + %trimr(ARCANE)
      * Er det på dokumenttypenivå angitt avvikende katalog?(via EXT lager)
     C                   EVAL      *IN51 = *OFF
     C     TYPE          CHAIN     ARKTXT                             51
     C                   IF        *IN51 = *OFF
     C     X_ARKLAG      IFNE      *BLANKS
     C     EXKEY         CHAIN     ARKEXT                             51
     C                   IF        *IN51 = *OFF
     C                   EVAL      NEWDIR = '/' + %trimr(Y_ARCANE)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      * Definier nytt filnavn
     C                   EVAL      NEWFILE= %trim(TYPE)
N02  C*                            + %SUBST(WDATO:1:4)
N02  C                             + %SUBST(AAR4:1:4)
     C                             + %trimr(UND)
     C                             + NEWRAN
     C                             + NEWEND
      * Lag nytt objektnavn
     C                   EVAL      NEWOBJ = %trim(NEWDIR) + '/'
     C                             + %trimr(NEWFILE)
      * Flytte objekt
     C                   EVAL      CmdLine = 'MOV OBJ(' + X'7D'
     C                             + %trimr(EntryPath) + X'7D'
     C                             + ') TOOBJ(' +X'7D'
     C                             + %trimr(NEWOBJ) + X'7D' + ')'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   MOVE      'J'           OkMov             1
     C                   MOVEl     EntryPath     FRA             120
     C                   MOVEl     NewObj        Til             120
     C                   CALL      'ARCAMOVC'
     C                   PARM                    FRA
     C                   PARM                    TIL
     C                   PARM                    OkMov
N05   *Hvis google cloud benyttes - send det i skyen med en gang
N05  C                   IF        OkMov = 'J'
N05  C     FIFIRM        chain     FIRMGOOG
N05  C                   if        %found and GOBUID <> *blanks
N05  c                   eval      Companyid = GOBUID
N05  c                   eval      UlocalPath= %trim(NEWDIR)+'/'
N05  c                   eval      USrcFile  = NEWFILE
N05  c                   eval      UNewFile  = *blanks
N05  c                   eval      Udelete   = 'J'
N05  c                   call      'ARC99WSU1'
N05  c                   parm                    Companyid        10
N05  c                   parm                    UlocalPath      100
N05  c                   parm                    uSrcFile        120
N05  c                   parm                    udelete           1
N05  c                   parm                    uNewFile         60
N05  c                   parm                    Uok               1
N05  c                   endif
N05  c                   endif
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv til ARKIVP                                     ARK /
      *////////////////////////////////////////////////////////////
     C     ARK           BEGSR
      * Skriv til ARKIVP
     C                   CLEAR                   ARKIVR

     C                   EVAL      ARSTAT = 'A'
     C                   EVAL      ARTYPE = TYPE
     C                   EVAL      ARFIRM = FIFIRM
     C
     C                   EVAL      ARUNDE = 'L1:' + UND
     C                   EVAL      ARUSER = 'SY400USR'
     C                   move      WDATO         ARDATE
     C                   movel     WTID          ARTIME
     C                   EVAL      ARLINK = NEWFILE
     C                   WRITE     ARKIVR
      *
     C                   ENDSR
