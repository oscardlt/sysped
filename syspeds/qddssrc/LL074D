     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 60 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            LIMN           7Y 0H
     A            LILN           3Y 0H
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4' '9')
     A            LIKD           3A  O  5  5
     A            LITXT         35A  O  5  9
     A            LIREFI         3A  O  5 45
     A            LIREFE         3A  O  5 49
     A            LIANR          5  0O  5 53EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAG NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL074R'
     A                                      COLOR(BLU)
     A                                  1 17'Arbeid med lisens'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0B  1 42SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Artikkel:'
     A            UANR          15A  O  2 12COLOR(WHT)
     A            XVB           25A  O  2 28COLOR(WHT)
     A                                  3  2'Opsjon:  2=Endre  4=Slett  9=Kopie-
     A                                      r til alle'
     A                                      COLOR(BLU)
     A                                  4  2'Op Kod Tekst                      -
     A                                               S-I S-E Antal'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(02)
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAG NY')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A                                  1  2'F3/F12=Avbryt   F5=Frisk   F6=Lage-
     A                                       ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL074R'
     A                                      COLOR(BLU)
     A                                  1 17'Arbeid med lisens'
     A                                      COLOR(WHT)
     A                                  2  2'Artikkel:'
     A            UANR          15A  O  2 12COLOR(WHT)
     A            XVB           25A  O  2 28COLOR(WHT)
     A                                  3  2'Opsjon:  2=Endre  4=Slett  9=Kopie-
     A                                      r til alle'
     A                                      COLOR(BLU)
     A                                  4  2'Op Kod Tekst                      -
     A                                               S-I S-E Antal'
     A                                      COLOR(WHT)
     A                                 15  2'F3/F12=Avbryt   F5=Frisk   F6=Lage-
     A                                       ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 16  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL074R'
     A                                      COLOR(BLU)
     A                                  1 17'Arbeid med lisens'
     A                                      COLOR(WHT)
     A                                  2  2'Artikkel:'
     A            UANR          15A  O  2 12COLOR(WHT)
     A            XVB           25A  O  2 28COLOR(WHT)
     A                                  5  2'Kode  :'
     A            WSKD           3A  B  5 10DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  COLOR(WHT)
     A                                  6  2'Tekst :'
     A            WSTXT         35A  B  6 10DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  COLOR(WHT)
     A                                  7  2'SAD-I :'
     A            WSREFI         3A  B  7 10DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  COLOR(WHT)
     A                                  8  2'SAD-E :'
     A            WSREFE         3A  B  8 10DSPATR(UL)
     A  87                                  DSPATR(PC)
     A  87                                  COLOR(WHT)
     A                                  9  2'Antall:'
     A            WSANT          5  0B  9 10DSPATR(UL)
     A  86                                  DSPATR(PC)
     A  86                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 16  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
