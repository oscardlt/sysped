      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS131 MODULE(*LIBL/SS131
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FGSSORL09  IF   E           K DISK    USROPN
     FGSSORF    UF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFRU)
     FGSSOLL01  IF   E           K DISK    USROPN
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSRFF    IF   E           K DISK    USROPN
     FGSSLGF    O    E             DISK    USROPN
     FGSSTRL01  if   e           k disk    usropn
     FKUFAST    IF   E           K DISK    USROPN
     FCDBOAT    IF   E           K DISK    USROPN
     FVadr      IF   E           k disk    usropn
     FCUNDL01   IF   E           K DISK    USROPN
     FDOKUF     IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D CURTUR          S             11
     D CURTURN         S             11  0
     D KunITur         S              1
     D KUNDE           S              8
     D KUNDEN          S              8  0
     D ORNO            S              7
     D ORNON           S              7  0
     D STATUS          S              1
     D AREA            S              2
     D POCO            S             10    VARYING
     D TotCol          S              5  0
     D TotVkt          S              7  1
     D TotM3           S              7  2
     D TotLM           S              7  2
     D ENDCUS          s             30
     D POSTCODE        s             10
     D GrpNr           s              8
     D TRIPCOL         s              8
     D ItemCount       s             10i 0
     D i               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D XXTXT01         C                   CONST('Stauts is changed at')
     D XXTXT02         C                   CONST('Picked up to trip at')
     D XXTXT03         C                   CONST('Removed from trip at')
      * Number of records matching search criteria
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * MIDLERTIDIG - LÅNER FRITEKST 4 FOR AREA
     D                 DS
     D  ORFT4                  1     30
     D  ORAREA                 1      2
     D  ORPÅR                  4      7
     D  ORPUKE                 8      9
     D  ORÅRUKE                4      9
     D  ORLMA                 11     14
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * Hvis arbeide med gitt tur, oppdater select/remove
     c     CURTURN       ifne      *zeros
     c                   exsr      SelRem
     c                   end
      *------------------                                                   ******
      * 3-Section /$tablestr
     C                   callp     wrtsection('tablestr1')
     C     curturn       ifne      *zeros
     C                   callp     wrtsection('tablestr2')
     c                   end
     C                   callp     wrtsection('tablestr3')
      * 4-Sections /$tablerow
     C                   z-add     0             round             3 0
     C                   z-add     0             bseq              3 0
     C                   z-add     0             TotCol
     C                   z-add     0             TotVkt
     C                   z-add     0             TotM3
     C                   z-add     0             TotLM
      *
     c     strles        tag
     c                   add       1             round
      *
     C     GSSOR9K       SETLL     GSSORL09
     C     GSSOR9K       READE     GSSORL09                               50
      *
     C     *IN50         DOWEQ     '0'
      * Når turnr gitt: først kun de som ER på turen, så "udisponerte"
     c     CURTURN       IFNE      *ZEROS
     c     Round         Ifeq      1
     c     ORTNO         CABNE     CURTURN       NextRec
     c                   else
     c     ORTNO         CABNE     *zeros        NextRec
     c                   end
     c                   end
      * Når Kun de som ikke er på tur er valgt ...
     c     KunITur       IFEQ      'Y'
     c     ORTNO         CABNE     *zeros        NextRec
     c                   end
      *
      * Når Kun kundenr xxx er valgt ...
     c     KUNDEN        IFNE      *ZEROS
     c     KUNDEN        CABNE     ORCNO         NextRec
     c                   end
      *
      * filter på areakode skal kun utføres på udisponerte.
     c     CURTURN       IFEQ      *zeros
     c     Round         OREQ      2
     c     AREA          IFNE      *BLANKS
     c     AREA          CABNE     ORAREA        NextRec
     c                   end
     c                   end
      *
     c     ORLMA         IFEQ      *BLANKS
     c                   MOVE      *ZEROS        ORLM              4 2
     c                   ELSE
     c                   MOVE      ORLMA         ORLM
     c                   END
      * Hent Clients PO
     C     ORONO         SETLL     GSSPOF
     C     ORONO         READE     GSSPOF                                 33
     C   33              MOVE      *BLANKS       POCONO
      * Hent ACK NR
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 33
     C     *IN33         DOWEQ     *OFF
     C                   IF        RFKD = 'CPO'
     C                   LEAVE
     C                   END
     C     ORONO         READE     GSSRFF                                 33
     C  N33              ENDDO
     C   33              MOVE      *BLANKS       RFREF
      * Hent sluttkunde / postnr
     C     ORBCD         CHAIN     CDBOAT                             33
     C                   MOVE      *BLANKS       ENDCUS
     C                   MOVE      *BLANKS       ENDCUS
     C  N33              MOVEL     CBNAB         ENDCUS
     C                   MOVE      ORBCD         TMPADRN           4
     C                   TESTN                   TMPADRN              34
     C     *IN34         IFEQ      '1'
     c                   MOVE      TMPADRN       VADRNR
     C     VADRKEY       CHAIN     VADR                               33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     VADRNA        ENDCUS
     C                   MOVEL     VADRN3        POSTCODE
     c                   end
     c                   end
      *
      * filter på POSTCODE skal kun utføres på udisponerte.
     c     CURTURN       IFEQ      *zeros
     c     Round         OREQ      2
     c     POCO          ifne      *BLANKS
     C     POCO          SCAN      POSTCODE                               33
     c     *IN33         CaBEQ     *OFF          NextRec
     C                   end
     c                   end
      * Hent kundenavn
     C     CUNDL1K       CHAIN     CUNDL01                            33
     C   33              EVAL      KNAVN = '*UNKNOW'
      *
     C                   eval      bseq=bseq + 1
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow1')
     C     curturn       ifne      *zeros
     C                   eval      Totcol=totcol + ORCLI
     C                   eval      TotVkt=totvkt + ORWT
     C                   eval      TotM3 =totm3  + ORVOL
     C                   eval      TotLM =totLM  + ORLM
     c     Orcli         ifeq      *zeros
     c     OrWt          oreq      *zeros
     c                   end
     C                   callp     wrtsection('tablerow2')
     c                   end
     C                   callp     wrtsection('tablerow3')
     c     NextRec       tag
     C     GSSOR9K       READE     GSSORL09                               50
     C  N50              ENDDO
      *
      * Ferdig med første runde ved valgt tur, vis buntotaler/buttons..
      * Gå så til toppen og les udisponerte
     c     CURTURN       IFNE      *zeros
     c     Round         andeq     1
     C                   callp     updhtmlvar('TOTCOL':
     C                             %trim(%editc(TOTCOL:'Z')))
     C                   callp     updhtmlvar('TOTVKT':
     C                             %trim(%editc(TOTVKT:'M')))
     C                   callp     updhtmlvar('TOTM3':
     C                             %trim(%editc(TOTM3:'M')))
     C                   callp     updhtmlvar('TOTLM':
     C                             %trim(%editc(TOTLM:'M')))
      * minst en post som mangler BOX-nr, ikke vis knapper for packing.list
     C     Box0Key       chain     GSSOLL01                           33
     C  N33              MOVE      'N'           BOXOK             1
     C   33              MOVE      'J'           BOXOK
     C                   callp     updhtmlvar('BOXOK':BOXOK)
     C                   callp     wrtsection('TripBot')
     C     BOXOK         Ifeq      'J'
     C                   callp     wrtsection('TripBot2')
     C                   else
     C                   callp     wrtsection('TripBot3')
     c                   end
     C                   callp     wrtsection('TripBotE')
      *
     c                   goto      strles
     c                   end
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      *
     C     SLUTT         tag
     C                   EXSR      SetEnd
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Select to trip / remove from trip
      *=====================================================================******
     C     SelRem        begsr
      * Slett evt merkede poster...
     C                   eval      ItemCount = ZhbGetVarCnt('Select')
     C                   eval      i = 1
     C     i             DOUGT     ItemCount
     C                   eval      ORNO    = ZhbGetVar('Select':i)
     C                   eval      ORNON   = c2n2(ORNO)
      *
     C     ORNON         CHAIN     GSSORF                             33
     C     *in33         IFEQ      *OFF
     C     ORST          IFEQ      '0'
     C     ORST          OREQ      '1'
     C                   EXSR      OPDLOGG
      * er IKKE på tur, Sett på aktuell tur
     C     ORTNO         IFEQ      *ZEROS
     c                   move      '1'           ORST
     c     ORST2         ifeq      'IPR'
     c                   movel     'RFP'         ORST2
     c                   end
     c                   move      CURTURN       ORTNO
     c                   else
      * er PÅ aktuell tur, Fjern
     C     ORTNO         IFEQ      CURTURN
     c                   move      '0'           ORST
     c                   move      *zeros        ORTNO
     c                   end
     c                   end
     c                   end
     C                   Update    GSSORFRU
     C                   CALL      'GSS024RB'
     C                   parm                    ORONO
     C                   parm                    ORTNO
     C                   end
      *
     C                   eval      i = i +1
     C                   ENDDO
      *
     C                   endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG       begsr
      *
     C                   MOVE      WSUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'SS131'       LGPGM
     C                   Z-ADD     5             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      ORNON         LGONO
     C                   MOVE      *ZEROS        LGLNO
      *
     C     ORTNO         IFEQ      *ZEROS
     c     ORST2         ifeq      'IPR'
     C                   EVAL      LGTXT = XXTXT01
     C                   CAT       ORLC:1        LGTXT
     C                   EVAL      LGAGM = 'IPR'
     C     KUFASTKGM     CHAIN     KUFAST                             33
     C                   CAT       KFTXT:2       LGAGM
     C                   EVAL      LGANY = 'RFP'
     C     KUFASTKNY     CHAIN     KUFAST                             33
     C                   CAT       KFTXT:2       LGANY
     C                   WRITE     GSSLGFR
     c                   end
     C                   EVAL      LGTXT = XXTXT02
     C                   CAT       ORLC:1        LGTXT
     C                   EVAL      LGANY = 'New trip No.'
     C                   CAT       CURTUR:2      LGANY
     c                   MOVE      *BLANKS       LGAGM
     C                   WRITE     GSSLGFR
     c                   else
     C                   EVAL      LGTXT = XXTXT03
     C                   CAT       ORLC:1        LGTXT
     C                   EVAL      LGAGM = 'Old trip No.'
     C                   MOVE      ORTNO         XAN11            11
     C                   CAT       XAN11:1       LGAGM
     C                   MOVE      *BLANKS       LGANY
     C                   WRITE     GSSLGFR
     C                   end
      *
     C                   endsr
      *= ===================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   MOVE      'N'           XFEIL             1
     C                   eval      lng     = zhbgetvar('lng')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      CURTUR  = zhbgetvar('CURTUR')
     C                   eval      CURTURN = c2n2(CURTUR)
     C                   EVAL      KunITur = zhbgetvar('KunITur')
     c     curturn       ifeq      99999999999
     c                   eval      KunITur = 'Y'
     c                   eval      CurTur  = ' '
     c                   eval      CurTurn = 0
     c                   end
     C                   EVAL      AREA    = zhbgetvar('AREA')
     C                   eval      AREA = uppify(AREA)
     C                   EVAL      POCO    = zhbgetvar('POCO')
     C                   eval      POCO = uppify(POCO)
     C                   eval      KUNDE   = zhbgetvar('KUNDE')
     C                   eval      KUNDEN  = c2n2(KUNDE)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     c     curturn       ifne      *zeros
     c                   eval      TRIPCOL = '#DDDDDD'
     c                   else
     c                   eval      TRIPCOL = BIFARG
     c                   end
     C                   callp     updhtmlvar('TRIPCOL':TRIPCOL)
      *
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('CURTUR':
     C                             %trim(%editc(CURTURN:'Z')))
     c                   move      curturN       curtura          11
     C                   callp     updhtmlvar('CURTURA':CURTURA)
     C                   callp     updhtmlvar('LABELTYP':LabelTyp)
     C                   callp     updhtmlvar('KunITur':KunITur)
     C                   callp     updhtmlvar('KUNDE':
     C                             %trim(%editc(KUNDEN:'Z')))
     c     KUNDEN        ifne      *zeros
     c                   move      kunden        ORCNO
     C     CUNDL1K       CHAIN     CUNDL01                            33
     C   33              EVAL      KNAVN = '*UNKNOW'
     C                   callp     updhtmlvar('KUNDENA':KNAVN)
     c                   else
     C                   callp     updhtmlvar('KUNDENA':'All')
     c                   end
     C     CURTURN       IFNE      *ZEROS
     C                   callp     updhtmlvar('REMOVE':'REMOVE')
     c                   else
     C                   callp     updhtmlvar('REMOVE':'      ')
     c                   end
     C                   callp     updhtmlvar('AREA':AREA)
     C                   callp     updhtmlvar('POCO':POCO)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
     c* Sequence of this Recd (numeric, converted to char)
     C                   move      bseq          bseqchar          3
     C                   callp     updhtmlvar('SEQ':bseqchar)
     C                   callp     updhtmlvar('KNAVN':KNAVN)
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   callp     updhtmlvar('RFREF':RFREF)
     C                   callp     updhtmlvar('ENDCUS':ENDCUS)
     C                   callp     updhtmlvar('POSTCODE':POSTCODE)
     C                   callp     updhtmlvar('STAT2':ORST2)
     C                   callp     updhtmlvar('ORAREA':ORAREA)
     C                   callp     updhtmlvar('ORPÅR':ORPÅR)
     C                   callp     updhtmlvar('ORPUKE':ORPUKE)
     C                   callp     updhtmlvar('KLL':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   callp     updhtmlvar('VKT':
     C                             %trim(%editc(ORWT:'M')))
     C                   callp     updhtmlvar('M3':
     C                             %trim(%editc(ORVOL:'M')))
     C                   callp     updhtmlvar('LM':
     C                             %trim(%editc(ORLM:'M')))
     C                   callp     updhtmlvar('TRIP':
     C                             %trim(%editc(ORTNO:'Z')))
     C                   callp     updhtmlvar('DATE':
     C                             %trim(%editw(ORDTWL:'    .  .  ')))
     c     curturn       ifne      *zeros
     c     curturn       andeq     ORTNO
     c                   eval      TRIPCOL = '#DDDDDD'
     c                   else
     c                   eval      TRIPCOL = BIFARG
     c                   end
     C                   callp     updhtmlvar('TRIPCOL':TRIPCOL)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS131'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSORF                               50
     C                   open      GSSORL09                             50
     C                   open      GSSOLL01                             50
     C                   open      GSSPOF                               50
     C                   open      GSSRFF                               50
     C                   open      GSSLGF                               50
     C                   open      KUFAST                               50
     C                   open      CDBOAT                               50
     C                   open      Vadr
     C                   open      CUNDL01                              50
     C                   open      Dokuf
     C                   open      BRPARF
     C                   open      GSSTRL01
      * arbeider med turnr som ikke lenger har status ' '/'1'...
     c     CURTURN       ifne      *zeros
     c     curturn       chain     gsstrl01                           33
     c   33              move      *zeros        curturn
     c   33              move      *blanks       curtur
     c                   end

      *
     C     CUNDL1K       klist
     C                   kfld                    BIFIRM
     C                   kfld                    ORCNO
     C     DokufKey      klist
     C                   kfld                    DFRI
     C                   kfld                    DFAVD
     C                   kfld                    DFOPD
     C                   kfld                    DFFBNR
     c                   move      'F'           DFRI
     c                   z-add     1             DFFBNR
      *
     C     GSSOR9K       klist
     C                   kfld                    SHIPFROM
     C                   kfld                    XXST2
     C                   MOVEL     'ONH'         XXST2             5
     C     VadrKey       klist
     C                   kfld                    BIFIRM
     C                   kfld                    ORCNO
     C                   kfld                    VADRNR
     C     Box0Key       klist
     C                   kfld                    CURTURN
     C                   kfld                    Box0
     C                   move      *zeros        Box0              3 0
     C     KUFASTKGM     KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    XXST2GM           5
     C                   MOVEL     'SSSTATUS'    KFTYP
     C                   MOVEL     'IPR'         XXST2GM
     C     KUFASTKNY     KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    XXST2NY           5
     C                   MOVEL     'RFP'         XXST2NY
      *
      *
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Hent fabrikk/HUb-kode  / Navn
     c                   Move      *blanks       SHIPFROM          5
     C                   MOVE      'SSFAB'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        SHIPFROM
     c  n33              MoveL     PAVRDA        WSNA
      * Hent labeltye (zebra/eltron) dersom det er behandling av tur
     c                   Move      *blanks       LabelTyp          1
     c     Curturn       ifne      *zeros
     C                   MOVE      'LABTY'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        LabelTyp
     c                   end
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSORL09
     C                   close     GSSOLL01
     C                   close     GSSPOF
     C                   close     GSSRFF
     C                   CLOSE     GSSLGF
     C                   CLOSE     GSSTRL01
     C                   close     CDBOAT
     C                   close     VADR
     C                   close     CUNDL01
     C                   close     dokuf
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
