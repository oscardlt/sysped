      *
      * OBS! FELTER SOM VEDRØRER STATISTIKK, MÅ OGSÅ FINNES I STATVF.
     A*__________________________
     A* SYSPED / Oppdragsregister
     A*""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R HEADFR
     A            HEST      R               REFFLD(ST)
     A                                      COLHDG('Statuskode')
     A            HEUR      R               REFFLD(UR)
     A                                      COLHDG('Delsystem-kode')
     A            HEAVD     R               REFFLD(AVD)
     A                                      COLHDG('Avdeling')
     A            HEOPD     R               REFFLD(OPD)
     A                                      COLHDG('Oppdragsnr')
     A            HESG      R               REFFLD(SG)
     A                                      COLHDG('Signatur')
     A            HEDTOP    R               REFFLD(DT)
     A                                      COLHDG('Oppdragsdato')
     A            HEDTR     R               REFFLD(DT)
     A                                      COLHDG('Registreringsdato')
     A            HESDFF    R               REFFLD(SD)
     A                                      COLHDG('Forfrakt fra')
     A            HESDF     R               REFFLD(SD)
     A                                      COLHDG('Frakt fra')
     A            HESDT     R               REFFLD(SD)
     A                                      COLHDG('Frakt til')
     A            HESDVT    R               REFFLD(SD)
     A                                      COLHDG('Viderefrakt til')
     A            HEGN      R               REFFLD(GN)
     A                                      COLHDG('Godsnr')
     A            HEGNN     R               REFFLD(GN)
     A                                      COLHDG('Nytt godsnr')
     A            HEPRO     R               REFFLD(PRO)
     A                                      COLHDG('Turnummer')
     A            HEPLAN    R               REFFLD(PLAN)
     A                                      COLHDG('Turnummer kode')
     A            TRRUND    R               REFFLD(PRO)
     A                                      EDTCDE(Z)
     A                                      COLHDG('Rundturnr')
     A            HEPOS1         7A         COLHDG('Posisjon-1')
     A            HEPOS2    R               REFFLD(POS)
     A                                      COLHDG('Posisjon-2')
N01  A            TRKNFA    R               REFFLD(KN)
N01  A                                      COLHDG('K.nr oppdragsgiver.')
N01  A            HENAO     R               REFFLD(NA)
N01  A                                      COLHDG('Navn oppdragsgiver')
     A            HEKNA     R               REFFLD(KN)
     A                                      COLHDG('Agent kundenr')
     A            HENAA     R               REFFLD(NA)
     A                                      COLHDG('Agent navn')
     A*           HERFA     R               REFFLD(RF)
     A*                                     COLHDG('Agent referanse')
N01  A            HERFA         35          COLHDG('Agent referanse')
     A            HESAML         6          COLHDG('Samlastnr fly')
     A            HEHAWB    R               REFFLD(HAWB)
     A                                      COLHDG('Bil/Hawbnr')
S01  A*           HESDLK         1          COLHDG('Lossestedskode')
N01  A            HESDLA    R               REFFLD(SDL)
N01  A                                      COLHDG('Lastested')
     A            HESDL     R               REFFLD(SDL)
     A                                      COLHDG('Lossested')
     A            HEVALT    R               REFFLD(VK)
     A                                      COLHDG('Frakttillegg kode')
     A            HEVALP    R               REFFLD(VALP)
     A                                      COLHDG('Frakttillegg  %')
     A            HEKNT     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Transportør')
     A            HELKA     R               REFFLD(LKA)
     A                                      COLHDG('Landkode')
      * Selger/Avsender
     A            HEKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            HENAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            HEADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            HEADS2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Selger')
N01  A            HEPNS          9          COLHDG('Postnr Selger')
     A            HEADS3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Selger')
     A            HELKS     R               REFFLD(LKA)
     A                                      COLHDG('Landkode selger')
      * HENTEADRESSE
N01  A            HEKNH     R               REFFLD(KN)
N01  A                                      COLHDG('Kundenr Hen.adr')
N01  A            HENAH     R               REFFLD(NA)
N01  A                                      COLHDG('Navn Hen.adr')
N01  A            HEADH1    R               REFFLD(AD)
N01  A                                      COLHDG('Adresse-1 Hen.adr')
N01  A            HEADH2    R               REFFLD(AD)
N01  A                                      COLHDG('Adresse-2 Hen.adr')
N01  A            HEPNH          9          COLHDG('Postnr Hen.adr')
N01  A            HEADH3    R               REFFLD(AD)
N01  A                                      COLHDG('Adresse-3 Hen.adr')
N01  A            HELKH     R               REFFLD(LKA)
N01  A                                      COLHDG('Landkode Hen.adr')
N01  A            HEHBRE         8S 6       COLHDG('HENTING, BREDDEGRAD')
N01  A            HEHLEN         9S 6       COLHDG('HENTING, LENGDEGRAD')
N01  A            HEHBLK         1          COLHDG('HENT. BRE/LEN-KODE')
      * Selgers fmot
     A            HEKDFS    R               REFFLD(KDF)
     A                                      COLHDG('Fakturakode Selger')
     A            HEVALS    R               REFFLD(VAL)
     A                                      COLHDG('Valutakode Selger')
     A            HEKNSF    R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selg.fakm')
     A            HEANS     R               REFFLD(AN)
     A                                      COLHDG('Avtalenr Selger')
     A            HENASF    R               REFFLD(NA)
     A                                      COLHDG('Navn Selg.fakm')
      * Kjøper/Mottaker
     A            HEKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            HENAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            HEADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            HEADK2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Kjøper')
N01  A            HEPNK          9          COLHDG('Postnr Kjøper')
     A            HEADK3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Kjøper')
     A            HELKK     R               REFFLD(LKA)
     A                                      COLHDG('Landkode kjøper')
      * LeveringsAdresse
N01  A            HEKNL     R               REFFLD(KN)
N01  A                                      COLHDG('Kundenr Lev.adr')
N01  A            HENAL     R               REFFLD(NA)
N01  A                                      COLHDG('Navn Lev.adr')
N01  A            HEADL1    R               REFFLD(AD)
N01  A                                      COLHDG('Adresse-1 Lev.adr')
N01  A            HEADL2    R               REFFLD(AD)
N01  A                                      COLHDG('Adresse-2 Lev.adr')
N01  A            HEPNL          9          COLHDG('Postnr Lev.adr')
N01  A            HEADL3    R               REFFLD(AD)
N01  A                                      COLHDG('Adresse-3 Lev.adr')
N01  A            HELKL     R               REFFLD(LKA)
N01  A                                      COLHDG('Landkode Lev.adr')
N01  A            HELBRE         8S 6       COLHDG('LEVERING, BREDDEGRAD')
N01  A            HELLEN         9S 6       COLHDG('LEVERING, LENGDEGRAD')
N01  A            HELBLK         1          COLHDG('LEVE. BRE/LEN-KODE')
      * Kjøpers fmot
     A            HEKDFK    R               REFFLD(KDF)
     A                                      COLHDG('Fakturakode Kjøper')
     A            HEVALK    R               REFFLD(VAL)
     A                                      COLHDG('Valutakode Kjøper')
     A            HEKNKF    R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøp.fakm')
     A            HEANK     R               REFFLD(AN)
     A                                      COLHDG('Avtalenr Kjøper')
     A            HENAKF    R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøp.fakm')
      *
     A            HEOT      R               REFFLD(OT)
     A                                      COLHDG('Oppdragstype')
     A            HEKDPL    R               REFFLD(KDPL)
     A                                      COLHDG('Produktkode')
     A            HEFR      R               REFFLD(FR)
     A                                      COLHDG('Frankatur')
     A            HENT      R               REFFLD(NTS)
     A                                      COLHDG('Antall kolli')
     A            HEVS1     R               REFFLD(VS)
     A                                      COLHDG('Vareslag-1')
     A            HEVS2     R               REFFLD(VS)
     A                                      COLHDG('Vareslag-2')
     A            HEGM1     R               REFFLD(GM)
     A                                      COLHDG('Godsmerking-1')
     A            HEGM2     R               REFFLD(GM)
     A                                      COLHDG('Godsmerking-2')
     A            HEVKT     R               REFFLD(VKT9)
     A                                      COLHDG('Bruttovekt')
     A            HELM      R               REFFLD(LM)
     A                                      COLHDG('Lastemeter faktura')
     A            HEM3           7P 3       COLHDG('KUBIKKMETER')
     A            HEFBV     R               REFFLD(VKT9)
     A                                      COLHDG('Fraktb.vekt')
      *
     A            HEKF      R               REFFLD(KF)
     A                                      COLHDG('Utl forb.kode')
     A            HEKFT1    R               REFFLD(KFT)
     A                                      COLHDG('Utl forb status')
     A            HEFT1     R               REFFLD(FT)
     A                                      COLHDG('Utl forb tekst')
     A            HEDTCP    R               REFFLD(DTC)
     A                                      COLHDG('Regnskapsårhundre')
     A            HEDTÅP    R               REFFLD(DTÅ)
     A                                      COLHDG('Regnskapsår')
     A            HEDTMP    R               REFFLD(DTM)
     A                                      COLHDG('Regnskapsperiode')
     A            HEKDKK    R               REFFLD(KDK)
     A                                      COLHDG('Kreditkode Kjøper')
     A            HEFNGK    R               REFFLD(FN)
     A                                      COLHDG('Kred. faknr Kjøper')
     A            HEFNGS    R               REFFLD(FN)
     A                                      COLHDG('Kred. faknr Selger')
     A            HEKDTM    R               REFFLD(KDTM)
     A                                      COLHDG('Sad transp.måte')
     A            HETRM     R               REFFLD(TRM)
     A                                      COLHDG('SAD Trans.middel')
     A            HETRI     R               REFFLD(LKA)
     A                                      COLHDG('SAD TR.midd land')
     A            HETRC     R               REFFLD(TRC)
     A                                      COLHDG('SAD Container kode')
     A            HETRCN    R               REFFLD(TRCN)
     A                                      COLHDG('SAD Container')
     A            HEFBK     R               REFFLD(BEL7S)
     A                                      COLHDG('SAD Fraktbeløp')
     A            HETLE     R               REFFLD(TLE)
     A                                      COLHDG('SAD Eksp.enhet')
     A            HETLL     R               REFFLD(TLL)
     A                                      COLHDG('SAD Løpenr')
     A            HETLKU    R               REFFLD(TLKU)
     A                                      COLHDG('SAD kan utføres')
     A            HENTVS    R               REFFLD(NTVS)
     A                                      COLHDG('FORP SAD ANT  LINJER')
     A            HEVN      R               REFFLD(VN)
     A                                      COLHDG('LEDIG...      ')
     A            HENTF     R               REFFLD(NTF)
     A                                      COLHDG('SAD ant.linjer')
     A            HEBELT    R               REFFLD(BEL9)
     A                                      COLHDG('SAD Tollbeløp')
     A            HEBELM    R               REFFLD(BEL9)
     A                                      COLHDG('SAD Momsbeløp')
     A            HESGM     R               REFFLD(SGM)
     A                                      COLHDG('Mottatt signatur')
     A            HEDTMO    R               REFFLD(DTMO)
     A                                      COLHDG('Mottatt dato')
     A            HEKLMO         4S 0       COLHDG('Mottatt klokka')
     A            HEPK1     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-1')
     A            HEPK2     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-2')
     A            HEPK3     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-3')
     A            HEPK4     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-4')
     A            HEPK5     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-5')
     A            HEPK6     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-6')
     A            HEPK7     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-7')
     A            HEPK8     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-8')
     A            HEPK9     R               REFFLD(PK)
     A                                      COLHDG('Printerkode-9')
     A            HEKDL     R               REFFLD(KDL)
     A                                      COLHDG('Lagerpos')
     A            HELB           5          COLHDG('Lokalbåt')
     A            HELKFR         2          COLHDG('Frankatur land')
     A            HESDFR         5          COLHDG('Frankatur stedskode')
     A            HESTFR        30          COLHDG('Frankatur stedsnavn')
     A            HERFK         35          COLHDG('Oppdragsgivers ref')
     A* Transportdisponering
     A* AKTUELT OPPDRAG er av type: HO=hovedoppdrag
     A*                           : FF=Forfrakt (DUP-oppd. innhenting)
     A*                           : VF=Viderefrakt (DUP-oppd. utkjør.)
     A            TRSLAG         2          COLHDG('Type oppdrag')
     A* Henvisning til oppdrag OPPOVER i hierarkiet
     A* (Aktuelt oppdrag er "dup-oppdrag"innh./utkjøring FOR .....:)
     A            TRAVD0    R               REFFLD(AVD)
     A                                      COLHDG('Avdeling opphav')
     A                                      EDTCDE(Z)
     A            TROPD0    R               REFFLD(OPD)
     A                                      COLHDG('Oppdragsnr opphav')
     A                                      EDTCDE(Z)
     A* Henvisning til oppdrag NEDOVER i hierarkiet
     A* (Aktuelt oppdrag har følgende "dup-oppdrag" under seg.....:)
     A            TRAVD1    R               REFFLD(AVD)
     A                                      COLHDG('Avdeling forfrakt')
     A                                      EDTCDE(Z)
     A            TROPD1    R               REFFLD(OPD)
     A                                      COLHDG('Oppdragsnr forfrakt')
     A                                      EDTCDE(Z)
     A            TRAVD2    R               REFFLD(AVD)
     A                                      COLHDG('Avdeling Etterfrakt')
     A                                      EDTCDE(Z)
     A            TROPD2    R               REFFLD(OPD)
     A                                      COLHDG('Oppdragsnr Etterfra')
     A                                      EDTCDE(Z)
     A            TRSFFD         8S 0       COLHDG('Forfrakt dato')
     A            TRSFFK         4S 0       COLHDG('Forfrakt klokka')
     A            TRSDFD         8S 0       COLHDG('Fra dato')
     A            TRSDFK         4S 0       COLHDG('Fra klokka')
     A            TRSDTD         8S 0       COLHDG('Til dato')
     A            TRSDTK         4S 0       COLHDG('Til klokka')
     A            TRSVTD         8S 0       COLHDG('Viderefr. dato')
     A            TRSVTK         4S 0       COLHDG('Viderefr. klokka')
     A            TRKDAK         1          COLHDG('Oppdragsg.kode')
     A            TRKDFF         1          COLHDG('Fakturere S/K')
     A            TRVERV         3          COLHDG('Valkod vareverdi')
     A            TRVERB        13P 2       COLHDG('Vareverdi')
     A            TRETTV         3          COLHDG('Valkod etterkrav')
     A            TRETTB        13P 2       COLHDG('Etterkrav')
     A            TRFRAK         1          COLHDG('Kode frakt')
     A            TRFRAV         3          COLHDG('Valkod frakt')
     A            TRFRAB        13P 2       COLHDG('Fraktbeløp')
     A            TRMVA          1A         COLHDG('Mvakode frakt')
     A            TRFA11         7A         COLHDG('Farlig gods 11')
     A            TRFA12         4A         COLHDG('Farlig gods 12')
     A            TRFLP1         3P 0       COLHDG('FLashpoint 1')
     A            TRFA21         7A         COLHDG('Farlig gods 21')
     A            TRFA22         4A         COLHDG('Farlig gods 22')
     A            TRFLP2         3P 0       COLHDG('Flashpoint 2')
     A            TRTSTA         1A         COLHDG('T-papirstatus')
S01  A*           TRKNFA    R               REFFLD(KN)
S01  A*                                     COLHDG('K.nr oppdragsgiver.')
S01  A*                                     EDTCDE(Z)
     A            TRSTAF         1          COLHDG('Dispstat forfrakt')
     A            TRSTAE         1          COLHDG('Dispstat etterfrakt')
     A            TRSTA1         1          COLHDG('Disponeringsstatus')
     A            TRSTA2         1          COLHDG('LEDIG')
     A            TRSTA3         1          COLHDG('LEDIG')
     A            TRSTA4         1          COLHDG('Produkt fly')
     A            TRKM           5P 0       COLHDG('Kjørte Kilometer')
     A            HE01           1          COLHDG('Oppdat stat')
     A            HESTD     R               REFFLD(STD)
     A                                      COLHDG('Avvikende bærer')
      * FELT SOM T.O.M V.08 LÅ I DS I FELT HERFK
     A            HXSNDN        20          COLHDG('EAN SEND.NR')
     A            HXPALL         3S 0       COLHDG('ANT PALLER')
     A            HXBLGK         3          COLHDG('BELØP GODK. SIGN')
     A            HXLKOD         5          COLHDG('LAGERSTED/INNLAND')
     A            HXLLST         1          COLHDG('LS.STAT(ER PRINTET)')
     A            HXEDIK         1          COLHDG('EDIKODE,E=EDI,K=KWEB')
     A            HXTERI         1          COLHDG('TERMIN.INN')
S01   * FELT FOR Å KUNNE OPPDTTERE ADRESSE MED BREDDE/LENGDE-GRAD
S01  A            HEHAKN         8S 0       COLHDG('HEN.ADR.KUNDENR')
S01  A            HEHAAN         3S 0       COLHDG('HEN.ADR.ADR.NR ')
S01  A            HELAKN         8S 0       COLHDG('LEV.ADR.KUNDENR')
S01  A            HELAAN         3S 0       COLHDG('LEV.ADR.ADR.NR ')
      * TMS PROSJEKT
TMS  A            HELMLA    R               REFFLD(LM)
TMS  A                                      COLHDG('Lastemeter lasting')
TMS  A            HEPOEN         5S 0       COLHDG('ADR-POENG')
TMS  A            HESTL1         1          COLHDG('VLINJE 1 STATUS')
TMS  A            HESTL2         1          COLHDG('VLINJE 2 STATUS')
TMS  A            HESTL3         1          COLHDG('VLINJE 3 STATUS')
TMS  A            HESTL4         1          COLHDG('VLINJE 4 STATUS')
TMS  A            HESTN1         1          COLHDG('TILLEGGPROD. 1')
TMS  A            HESTN2         1          COLHDG('Tilleggprod. 2')
TMS  A            HESTN3         1          COLHDG('Tilleggprod. 3')
TMS  A            HESTN4         1          COLHDG('Tilleggprod. 4')
TMS  A            HESTN5         1          COLHDG('Tilleggprod. 5')
TMS  A            HESTN6         1          COLHDG('Låsing tillegg')
TMS  A            HESTN7         1          COLHDG('Prebook-Flagg(P)')
TMS  A            HESTN8         1          COLHDG('X=Ikke ADR saml.ktr')
TMS  A            HESTN9         1          COLHDG('STATUS NY 9')
TMS  A            HERFED        35          COLHDG('EDI ekstern ref')
     A* OVERFØRT FRA Z-FIL
     A            HELIB         10          COLHDG('BIBLIOTEK')
     A            HEFILE        10          COLHDG('FIL')
     A            HEMBR         10          COLHDG('MBR')
     A            HETYPE         6          COLHDG('TYPE MELDING')
     A            HELVL          3          COLHDG('MELDINGSNIVÅ')
     A            HESNN          9  0       COLHDG('MELDINGSNR')
     A            HEUNIK         9  0       COLHDG('RECORDNR')
     A            HEREFF        17          COLHDG('REFERANSE')
     A          K HEAVD
     A          K HEOPD
