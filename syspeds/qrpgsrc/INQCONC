      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/inqconc MODULE(*LIBL/inqconc *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
ODDEV *ODDEV   PTF9 ODDEVEN-logikk
P9XXX * 02 YBC PTF9 WSVARNV: Variable navn som mottar verdi
      *             WSFORMS: Formnavn som mottar verdi
      *             WSREFR : J/Y betyr program for mottak av verdi skal refreshes
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCDCON4    IF   E           K DISK    USROPN
     FCDCON4LF  IF   E           K DISK    USROPN
     F                                     RENAME(CDCON4R:CDCON4RL)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
OddEvD OddEven         s             10
      * Client input variables
N02  D WSVARNV         s             20
N02  D WSFORMS         s             20
N02  D WSREFR          s              1
N02  D REFRESH         s             50
     D SOKKOD          s              2
     D SOKTXT          s             10    varying
     D SOKTXT2         s             30
     D WSuser          s             10
     D MaxV            s              5
     D MaxVis          s              5  0
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      SOKKOD  = zhbgetvar('SOKKOD')
     C                   eval      SOKTXT  = zhbgetvar('SOKTXT')
     C                   eval      SOKTXT  = uppify(SOKTXT)
N02  C                   eval      WSVARNV = zhbgetvar('WSVARNV')
N02  C                   IF        WSVARNV = *BLANKS
N02  C                   EVAL      WSVARNV = 'WS4KD'
N02  C                   END
N02  C                   eval      WSFORMS = zhbgetvar('WSFORMS')
N02  C                   IF        WSFORMS = *BLANKS
N02  C                   EVAL      WSFORMS = 'forms[0]'
N02  C                   END
N02  C                   eval      WSREFR  = zhbgetvar('WSREFR')
N02  C                   IF        ((((WSREFR = 'J') OR
N02  C                                (WSREFR = 'j')) OR
N02  C                                (WSREFR = 'Y')) OR
N02  C                                (WSREFR = 'y'))
N02  C                   eval      REFRESH = 'self.opener.document.'
N02  C                             + %trim(WSFORMS)
N02  C                             + '.submit();'
N02  C                   END
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      WSuser = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     CDCON4
     C                   CLOSE     CDCON4LF
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'INQCONC'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':WSUSER)
N02  C                   callp     updHTMLvar('wsvarnv':WSVARNV)
N02  C                   callp     updHTMLvar('wsforms':WSFORMS)
N02  C                   callp     updHTMLvar('wsrefr':WSREFR)
N02  C                   callp     updHTMLvar('refresh':REFRESH)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
      * fyll skjermen med alle ved første inngang (normalt få)
     C     SOKKOD        IFEQ      *Blanks
     C     SOKTXT        ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   END
      *
     c                   eval      SOKTXT2   = SOKTXT
     c                   Move      *zeros        AntVist           5 0
      *
     C     SOKKOD        IFNE      *blanks
N01  C     KNRKEY        setll     CDCON4
     C                   read      CDCON4                                 33
     C                   ELSE
N01  C     NVNKEY        setll     CDCON4LF
     C                   read      CDCON4LF                               33
N01  C     SOKTXT        SCAN      CC4FT                                  34
     c     *IN34         CaBEQ     *OFF          UtLes
     C                   END
     C     *in33         DOWEQ     *OFF
     C                   exsr      exclude
     c     EXCL          IFEQ      'N'
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('BKOD':CC4KD)
     C                   callp     updHTMLvar('BTXT':CC4FT)
     C                   callp     updHTMLvar('BTEU':
     C                             %trim(%editc(CC4NT:'3')))
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C                   END
     C     SOKKOD        IFNE      *blanks
     C                   read      CDCON4                                 33
     C                   ELSE
     C                   read      CDCON4LF                               33
N01  C     SOKTXT        SCAN      CC4FT                                  34
     c     *IN34         CaBEQ     *OFF          UtLes
     C                   END
     C                   end
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   Move      'J'           EXCL              1
bhr   * diverse tester - om ikke bestått hopp ut (med J i EXCL)
      * Posten skal med på liste
     C                   Move      'N'           EXCL              1
      *
     C     UTEXCL        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      CDCON4
     C                   OPEN      CDCON4LF
      * Key for CDCON4
     C     KNRKEY        KLIST
     C                   KFLD                    SOKKOD
      * Key for CDCON4
     C     NVNKEY        KLIST
     C                   KFLD                    SOKTXT2
      *
      *
     C                   endsr
