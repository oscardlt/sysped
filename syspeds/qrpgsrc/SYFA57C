     FSYFA57FC  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     FSYFLD     IF   E           K DISK
     FCUFLD1    UF A E           K DISK
     FWFLD      UF A E             DISK
     FWFLD1     IF   E           K DISK
     F                                     RENAME(WFLDR:WFLD1R)
     FWFLD2     UF   E           K DISK
     F                                     RENAME(WFLDR:WFLD2R)
     F                                     INFDS(WFLDDS)
     FWFLD3     UF   E           K DISK
     F                                     RENAME(WFLDR:WFLD3R)
     FCUSAPR    UF   E           K DISK
     FKOFAST    IF   E           K DISK
     FKODFRI    IF   E           K DISK
      *ALLE TENKELIGE FELT SOM KAN INNGÅ I UTSKRIFTEN (MAX FELTLEN. 79)
     D FL              S             79    DIM(100)
      *ARBEIDSFELT FOR MOVEA OPERASJON INN TIL LINJA
      *ARBEIDSFELT FOR EDITERING AV FELT
      *ARBEIDSFELT MOTTA DEN FERDIGE EDITERTE STRENGEN
     D WN              S              1    DIM(79)                              WORKFIELD EDIT
      *ARBEIDSFELT FOR Å FINNE LENGDE PÅ TEKSTKONSTANT
     D TK              S              1    DIM(20)                              WORKFIELD EDIT
      *FERDIG KOMPONERTE LINJER 1,2,3
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D WFLDDS          DS
     D  WRECNO               397    400B 0
     D                 DS
     D  WL                     1     79
     D                                     DIM(79)                              WORK LINE
     D  WORKL                  1     79
     D                 DS
     D  WH                     1     79
     D                                     DIM(79)                              WORK LINE HEAD
     D  WORKH                  1     79
     D                 DS
     D  WE                     1     79
     D                                     DIM(79)                              WORKFIELD EDIT
     D  WORKE                  1     79
     D                 DS
     D  HE                     1    237
     D                                     DIM(3)                               Output string
     D  HE1                    1     79
     D  HE2                   80    158
     D  HE3                  159    237
     D                 DS
     D  LI                     1    237
     D                                     DIM(3)                               Output string
     D  LI1                    1     79
     D  LI2                   80    158
     D  LI3                  159    237
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   EXSR      OPPDAT
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UKUNDE
     C                   PARM                    UDOKUM
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    WFL           GML
     C     *LIKE         DEFINE    WFR           GMR
     C     *LIKE         DEFINE    WFS           GMS
     C     *LIKE         DEFINE    WFL           XTOPL
     C     *LIKE         DEFINE    TOPL          WWTOPL
     C     *LIKE         DEFINE    CFR           XTOPR
     C     *LIKE         DEFINE    CFKEY1        UKUNDE
     C     *LIKE         DEFINE    CFKEY2        UDOKUM
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C     *LIKE         DEFINE    ZRECNO        XR
     C     *LIKE         DEFINE    ZRECNO        YRECNO
      *
      *___________________
      * Definiere nøkler
      *"""""""""""""""""""
     C     CUFLDK        KLIST
     C                   KFLD                    UKUNDE
     C                   KFLD                    UDOKUM
     C     CUFLDN        KLIST
     C                   KFLD                    UKUNDE
     C                   KFLD                    UDOKUM
     C                   KFLD                    WFN
     C     WFLDK         KLIST
     C                   KFLD                    UKUNDE
     C                   KFLD                    UDOKUM
     C                   KFLD                    XTOPL
     C                   KFLD                    XTOPR
     C     WFLDE         KLIST
     C                   KFLD                    UKUNDE
     C                   KFLD                    UDOKUM
     C     EDTKEY        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   MOVEL     'SFEDIT'      KFTYP
      * Hent kundens profil (frisøkkode 1,2,3,4,5)
     C                   MOVE      *BLANKS       FSTX01           20
     C                   MOVE      *BLANKS       FSTX02           20
     C                   MOVE      *BLANKS       FSTX03           20
     C                   MOVE      *BLANKS       FSTX04           20
     C                   MOVE      *BLANKS       FSTX05           20
     C     UKUNDE        CHAIN(N)  CUSAPR                             33
     C     CPFS01        IFNE      *BLANKS
     C     CPFS01        CHAIN     KODFRI                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFSOTX        FSTX01
     C                   END
     C                   END
     C     CPFS02        IFNE      *BLANKS
     C     CPFS02        CHAIN     KODFRI                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFSOTX        FSTX02
     C                   END
     C                   END
     C     CPFS03        IFNE      *BLANKS
     C     CPFS03        CHAIN     KODFRI                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFSOTX        FSTX03
     C                   END
     C                   END
     C     CPFS04        IFNE      *BLANKS
     C     CPFS04        CHAIN     KODFRI                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFSOTX        FSTX04
     C                   END
     C                   END
     C     CPFS05        IFNE      *BLANKS
     C     CPFS05        CHAIN     KODFRI                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFSOTX        FSTX05
     C                   END
     C                   END
      *_____________
      * TØM WOKFILE
      *"""""""""""""
     C                   EXSR      TØMWRK
      *
      *__________________
      * Fyll wokfile....
      *""""""""""""""""""
      * Felt som er innlagt i kundens profil....
     C     CUFLDK        SETLL     CUFLD1
     C     CUFLDK        READE(N)  CUFLD1                                 50
     C     *IN50         DOWEQ     '0'
     C                   EXSR      MOVCW
     C                   WRITE     WFLDR
     C     CUFLDK        READE(N)  CUFLD1                                 50
     C                   END
      *
      * Øvrige UBRUKTE felt legges ut med linjenr '9' (->' ' på subfil)
     C                   MOVE      '0'           *IN50
     C                   MOVE      '1'           *IN99
     C     *LOVAL        SETLL     SYFLD
     C                   READ      SYFLD                                  50
     C     *IN50         DOWEQ     '0'
     C                   MOVE      '0'           *IN99                           Minst 1..
     C                   MOVE      SFNR          WFN
      * Spesialfelt for tekstkonstant kan forekomme mange ganger..
     C     SFNR          IFEQ      0
     C                   MOVE      '1'           *IN33
     C                   ELSE
     C     CUFLDN        CHAIN     WFLD1                              33
     C                   END
     C     *IN33         IFEQ      '1'
     C                   EXSR      MOVSW
     C                   WRITE     WFLDR
     C                   END
     C                   READ      SYFLD                                  50
     C                   END
      * Det fantes ingen records i SYFLD - Modul ikke installert
     C     *IN99         IFEQ      '1'
     C                   MOVE      'JOV0499'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     MSGREC
     C                   EXFMT     NOREC
     C                   SETON                                        LR
     C                   RETURN
     C                   END
      *Initier array med simulerte feltverdier for alle mulige SYFLD
      *De fleste felt er ren Alfa = AAAAAAA..
     C                   MOVEA     *ALL'A'       FL                             *HELE (ALFA)
      *En del felt er numerisk men uten fortegn = 99999999
     C                   MOVEA     *ALL'9'       WE
     C                   MOVE      WORKE         FL(2)                          HEAVD
     C                   MOVE      WORKE         FL(4)                          HEOPD
     C                   MOVE      WORKE         FL(8)                          HEPRO
     C                   MOVE      WORKE         FL(14)                         HEPOS2
     C                   MOVE      WORKE         FL(18)                         OPPDR.DATO
     C                   MOVE      WORKE         FL(24)                         HEVKT
     C                   MOVE      WORKE         FL(26)                         HEFBV
     C                   MOVE      WORKE         FL(28)                         HEM3
     C                   MOVE      WORKE         FL(30)                         HELM
     C                   MOVE      WORKE         FL(32)                         HENT
     C                   MOVE      WORKE         FL(72)                         HEDTR
      *Noen få felt er numerisk MED fortegn = 9999999999999-
     C* (alltid 13 lang... Minus i posisjon 14)
     C                   MOVE      '-'           WE(14)
     C                   MOVE      WORKE         FL(84)                         FAKT. FRI
     C                   MOVE      WORKE         FL(86)                         FAKT. PLIKT.
     C                   MOVE      WORKE         FL(88)                         FAKT. FRI/PL
     C                   MOVE      WORKE         FL(90)                         FAKT. MVA
     C                   MOVE      WORKE         FL(92)                         FAKT. F/P/MV
     C                   MOVE      WORKE         FL(93)                         FAKT. F/P/MV
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TØMWRK        BEGSR
      *////////////////////////////////////////////////////////////
      *_____________
      * TØM WOKFILE
      *"""""""""""""
     C     WFLDE         SETLL     WFLD2
     C     WFLDE         READE     WFLD2                                  94
     C     *IN94         DOWEQ     '0'
     C                   DELETE    WFLD2R
     C     WFLDE         READE     WFLD2                                  94
     C  N94              END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     OPPDAT        BEGSR
      *////////////////////////////////////////////////////////////
      *_____________
      * TØM GAMLE POSTER I CUFLD
      *"""""""""""""
     C     WFLDE         SETLL     CUFLD1
     C     WFLDE         READE     CUFLD1                                 94
     C     *IN94         DOWEQ     '0'
     C                   DELETE    CUFLDR
     C     WFLDE         READE     CUFLD1                                 94
     C  N94              END
      *_____________
      * LEGG AKTIVE(1/3) POSTER OVER I KUNDENS CUFLD                   9)
      * Felt som vil skrive utover linjelengde droppes
      *"""""""""""""
     C                   MOVE      ' '           MINST1            1
     C     1             DO        XRECNO        XR
     C     XR            CHAIN     SUBFIL                             94
     C     WSFL          IFNE      ' '
     C     WFS           ANDLE     79
     C                   EXSR      MOVWC
     C                   WRITE     CUFLDR
     C                   MOVE      'X'           MINST1
     C                   END
     C                   END
      *
      * TØM WOKFILE
     C                   EXSR      TØMWRK
      *
      * OPPDATER HEADER PÅ SAMLEFAKTPROFILEN
     C     UKUNDE        CHAIN     CUSAPR                             33
     C     *IN33         IFEQ      '0'
     C     MINST1        IFEQ      ' '
     C                   MOVE      *BLANKS       CPSPH1
     C                   MOVE      *BLANKS       CPSPH2
     C                   MOVE      *BLANKS       CPSPH3
     C                   ELSE
     C* Varsel/valg ved endret heading
     C     HE(1)         IFNE      CPSPH1
     C     HE(2)         ORNE      CPSPH2
     C     HE(3)         ORNE      CPSPH3
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0501'     ZMSGNO
     C                   WRITE     MSGREC
     C                   MOVEA     '11'          *IN(91)
     C                   Z-ADD     1             ZRECNO
     C                   EXFMT     SUBCTL
     C     *IN12         IFEQ      '0'
     C                   MOVE      HE(1)         CPSPH1
     C                   MOVE      HE(2)         CPSPH2
     C                   MOVE      HE(3)         CPSPH3
     C                   END
     C                   END
     C                   END
     C                   UPDATE    CUSAPRR
     C                   END
      *
     C                   ENDSR
      *
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      TOPL          WWTOPL
     C                   Z-ADD     TOPL          XTOPL
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   MOVE      *ZEROS        GML
     C                   MOVE      *ZEROS        GMR
     C                   MOVE      *ZEROS        GMS
     C                   MOVEA     *BLANKS       HE
     C                   MOVEA     *BLANKS       LI
     C                   MOVEA     *BLANKS       WH
     C                   MOVEA     *BLANKS       WL
      *
      *
      *
      * Ind 31 ON - Forsøk på å skrive utover linjelengde...
      * Ind 32 ON - Feil editkode
     C                   MOVEA     '000'         *IN(30)
      *
     C     WFLDK         SETLL     WFLD2
      *_____________
      * Fyll Subfile  - Renumerer(i subfila) aktive linjer
      * Ubrukte linjer (li.nr. 9 i WFLD / ' ' i subfil) legges alltid
      * med SFNR som rekkefølgenr slik at de har fast plass i rekka...
      *"""""""""""""
     C     WFLDE         READE     WFLD2                                  94
     C     *IN94         DOWEQ     '0'
      * Felt fra SYFLD brukes også i SKREKS - derfor chain allerede her
     C     WFN           CHAIN     SYFLD                              33
     C     WFL           IFNE      GML
     C     GML           ANDNE     0
      * Ny linje.. skriv forige til eksempelarrayet
     C                   MOVE      WORKH         HE(GML)
     C                   MOVE      WORKL         LI(GML)
     C                   MOVEA     *BLANKS       WH
     C                   MOVEA     *BLANKS       WL
     C                   MOVE      *ZEROS        GMR
     C                   MOVE      *ZEROS        GMS
     C                   END
     C     GMR           ADD       10            WFRNY
     C                   MOVE      WFL           GML
     C                   MOVE      WFRNY         GMR
     C     WFL           IFNE      9
      * Skriv til eksempelarrayet (oppdater del av linja)..
     C                   EXSR      SKREKS
     C                   END
     C                   MOVE      WFL           WSFL
     C     WFL           IFEQ      9
     C                   MOVE      ' '           WSFL
     C                   Z-ADD     2             WFB
     C                   MOVE      SFNR          WFRNY
     C                   MOVE      *ZEROS        WSFR
     C                   ELSE
     C                   MOVE      WFRNY         WSFR
     C                   END
      *
     C                   UPDATE    WFLD2R
      *
     C                   MOVE      WFB           WSFB
     C                   MOVE      WFOVER        WSOVER
     C                   MOVE      WFLEN         WSFLEN
     C                   MOVE      WFDEC         WSFDEC
     C                   MOVE      WFEDIT        WSEDIT
     C                   MOVE      WRECNO        WFRECN
      *
     C                   MOVEA     '000000'      *IN(61)
      * Rev image felt som droppes pga linjeoverskridelse
     C     WFS           COMP      79                                 60
     C* Protect  editkode/desimaler ved alfafelt/Tekstkonstant
     C     SFEDIT        IFEQ      ' '
     C     SFEDIT        OREQ      'T'
     C                   MOVEA     '1'           *IN(65)
     C                   END
     C* Protect  lengde ved tekstkonstanter
     C     SFEDIT        IFEQ      'T'
     C                   MOVEA     '1'           *IN(66)
     C                   END
      * Highlight avvikende definisjoner i.forh. til standard..
     C     *IN66         IFEQ      '0'
     C     WSFLEN        COMP      SFLEN                              6161
     C     *IN65         IFEQ      '0'
     C     WSFDEC        COMP      SFDEC                              6262
     C     WSEDIT        COMP      SFEDIT                             6363
     C* Avvikende editkode - gyldighet (64=Rev image / 32=minst 1 feil)
     C     *IN63         IFEQ      '1'
     C                   MOVEL     WSEDIT        KFKOD
     C     EDTKEY        CHAIN     KOFAST                             64
     C     *IN64         IFEQ      '1'
     C                   MOVE      '1'           *IN32
     C                   END
     C                   END
     C                   END
     C                   END
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     WFLDE         READE     WFLD2                                  94
     C  N94              END
      *
      *
      *_____________
      * Renumerer aktive linjer på workfile
      * (Kan desverre IKKE gjøres samtidig som den legger ut i subfil)
      *  får da loop på lesingen...)
      *"""""""""""""
     C                   MOVE      *ZEROS        GMR
      *
     C     WFLDK         SETLL     WFLD3
     C     WFLDE         READE     WFLD3                                  33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      WFRNY         WFR
     C                   MOVE      *ZEROS        WFRNY
      * oppdater workfile med evt. endret rekkefølge
     C                   UPDATE    WFLD3R
     C     WFLDE         READE     WFLD3                                  33
     C  N33              END
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV0499'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     MSGREC
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *
      *_______________
      * Bevar antall records
      *_______________
     C                   MOVE      ZRECNO        XRECNO
      *_______________
      * Vis subfila fra post begynnelsen
      *_______________
      *
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C     *IN31         IFEQ      '1'
     C                   MOVE      'JOV0502'     ZMSGNO
     C                   ELSE
     C     *IN32         IFEQ      '1'
     C                   MOVE      'JOV0506'     ZMSGNO
     C                   END
     C                   END
     C                   WRITE     MSGREC
     C                   WRITE     BOTREC
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
      *
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         IFEQ      '1'
     C                   MOVE      *ZEROS        TOPL
     C                   GOTO      START
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     TOPL          IFNE      WWTOPL
     C                   GOTO      START
     C                   END
      *_______
      * F4 = Søk
      *"""""""
     C     *IN04         IFEQ      '1'                                          B1
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * WSSUBN inneholder relativt rec.nr (i subfila) for 1 post på
      * aktuell side. Legger en til aktuelt linjenr - 5 (1.subf.linje)
      * skal en ha recno for den posten en tar F4 fra.
     C     WSSUBN        ADD       LINNBR        YRECNO
     C                   SUB       5             YRECNO
     C     YRECNO        CHAIN     SUBFIL                             30
     C     *IN30         IFEQ      '0'                                           B2
     C     SFDEC         COMP      ' '                                    30
     C                   END                                                     E2
     C     *IN30         IFEQ      '1'                                           B2
     C                   UPDATE    SUBFIL
     C                   MOVE      'JOV0504'     ZMSGNO
     C                   GOTO      STBILD
     C                   END                                                     E2
     C                   MOVEL     'SFEDIT'      KDTYPE           10
     C                   MOVE      'N'           KDUPD             1
     C                   MOVE      *BLANKS       KDKODE            5
     C                   CALL      'SYFAKDA'
     C                   PARM                    KDTYPE
     C                   PARM                    KDUPD
     C                   PARM                    KDKODE
     C     KDKODE        IFEQ      *BLANKS                                       B2
     C     KDKODE        OREQ      'T    '                                       B2
     C                   UPDATE    SUBFIL
     C                   GOTO      STBILD
     C                   ELSE
     C                   MOVEL     KDKODE        WSEDIT
     C                   UPDATE    SUBFIL
     C     WFRECN        CHAIN     WFLD                               33
     C                   MOVE      WSEDIT        WFEDIT
     C  N33              UPDATE    WFLDR
     C                   GOTO      START
     C                   END                                                     E2
     C                   END                                                    E1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C                   EXSR      OPTION
     C                   MOVE      '1'           *IN95
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
      *
      * ' ' I WSFL (SUBFIL) TILSVARER 9 I WFL (WORKFILA) = "LEDIG"
      *
     C                   MOVE      ' '           TXTKON            1
     C     WFRECN        CHAIN     WFLD                               33
     C     WSFL          IFEQ      ' '
      * Sett flagg dersom en konstant "fjernes"
     C     SFEDIT        IFEQ      'T'
     C     WFL           ANDNE     9
     C                   MOVE      'S'           TXTKON
     C                   END
     C                   MOVE      9             WFL
     C                   Z-ADD     WFN           WSFR
     C                   ELSE
      * Sett flagg dersom en konstant "aktiveres"
     C     SFEDIT        IFEQ      'T'
     C     WFL           ANDEQ     9
     C                   MOVE      'N'           TXTKON
     C                   END
     C                   MOVE      WSFL          WFL
     C                   END
      *
     C     WSFR          IFEQ      *ZEROS
     C                   Z-ADD     999           WSFR
     C                   END
      * Ved numeriske..
     C     SFDEC         IFGT      ' '
      * Desim.ant kan IKKE settes større enn standard/IKKE blank
     C     WSFDEC        IFGT      SFDEC
     C     WSFDEC        OREQ      ' '
     C                   MOVE      SFDEC         WSFDEC
     C                   END
      * Heltallsdelen kan IKKE være større enn standard
     C                   MOVE      SFDEC         SFDECN            1 0
     C                   MOVE      WSFDEC        WFDECN            1 0
     C     WSFLEN        SUB       WFDECN        WHEL              3 0
     C     SFLEN         SUB       SFDECN        SHEL              3 0
     C     WHEL          SUB       SHEL          LENJUS            3 0
     C     LENJUS        IFGT      0
     C                   SUB       LENJUS        WSFLEN
     C                   END
     C                   END
      *
      * Finn feltlengde på tekstkonstant.
     C     SFEDIT        IFEQ      'T'
     C                   MOVEA     WSOVER        TK(1)
     C                   Z-ADD     20            TL                3 0
     C     TL            DOWGT     0
     C     TK(TL)        IFNE      ' '
     C                   GOTO      TLUT
     C                   END
     C                   SUB       1             TL
     C                   END
     C     TLUT          TAG
     C                   MOVE      TL            WSFLEN
     C                   END
      *
     C                   MOVE      WSFR          WFR
     C                   MOVE      WSFB          WFB
     C                   MOVE      WSOVER        WFOVER
     C                   MOVE      WSFLEN        WFLEN
     C                   MOVE      WSFDEC        WFDEC
     C                   MOVE      WSEDIT        WFEDIT
     C                   UPDATE    SUBFIL
      *
     C     *IN33         IFEQ      '0'
     C     TXTKON        IFEQ      'S'
     C                   DELETE    WFLDR
     C                   ELSE
     C                   UPDATE    WFLDR
     C                   END
      * Spesialfelt for tekstkonstant kan forekomme mange ganger..
      * legg ut en ny ledig straks en er aktivert
     C     TXTKON        IFEQ      'N'
     C     *LOVAL        SETLL     SYFLD
     C                   READ      SYFLD                                  50
     C                   EXSR      MOVSW
     C                   WRITE     WFLDR
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     MOVSW         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      UKUNDE        WFKEY1
     C                   MOVE      UDOKUM        WFKEY2
     C                   Z-ADD     9             WFL
     C                   MOVE      SFNR          WFR
     C                   Z-ADD     2             WFB
     C                   MOVE      *ZEROS        WFP
     C                   MOVE      *ZEROS        WFS
     C                   MOVE      SFNR          WFN
     C                   SELECT
     C     SFNR          WHENEQ    95
     C                   MOVE      FSTX01        WFOVER
     C     SFNR          WHENEQ    96
     C                   MOVE      FSTX02        WFOVER
     C     SFNR          WHENEQ    97
     C                   MOVE      FSTX03        WFOVER
     C     SFNR          WHENEQ    98
     C                   MOVE      FSTX04        WFOVER
     C     SFNR          WHENEQ    99
     C                   MOVE      FSTX05        WFOVER
     C                   OTHER
     C                   MOVE      SFOVER        WFOVER
     C                   ENDSL
     C                   MOVE      SFLEN         WFLEN
     C                   MOVE      SFDEC         WFDEC
     C                   MOVE      SFEDIT        WFEDIT
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     MOVCW         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      CFKEY1        WFKEY1
     C                   MOVE      CFKEY2        WFKEY2
     C                   MOVE      CFL           WFL
     C                   MOVE      CFR           WFR
     C                   MOVE      CFB           WFB
     C                   MOVE      *ZEROS        WFP
     C                   MOVE      *ZEROS        WFS
     C                   MOVE      CFN           WFN
     C                   MOVE      CFOVER        WFOVER
     C                   MOVE      CFLEN         WFLEN
     C                   MOVE      CFDEC         WFDEC
     C                   MOVE      CFEDIT        WFEDIT
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     MOVWC         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      UKUNDE        CFKEY1
     C                   MOVE      UDOKUM        CFKEY2
     C                   MOVE      WSFL          CFL
     C                   MOVE      WSFR          CFR
     C                   MOVE      WSFB          CFB
     C                   MOVE      WFP           CFP
     C                   MOVE      WFS           CFS
     C                   MOVE      WFN           CFN
     C                   MOVE      WSOVER        CFOVER
     C                   MOVE      WSFLEN        CFLEN
     C                   MOVE      WSFDEC        CFDEC
     C                   MOVE      WSEDIT        CFEDIT
     C                   ENDSR
      *
     C*-------------------------------------------
     C     SKREKS        BEGSR
     C*"""""""""""""""""""""""""""""""""""""""""""
      * OBS! Hvis element/felt-lengden i array FL er MINDRE enn lengden
      *      på feltet/(array WL) den flyttes inn i, vil NESTE arr-elem
      *      fra FL bli med over i MOVEA. (Når faktor2 i MOVEA er
      *      indexert betyr det jo fra og med.. Resfelt indexsert: legg
      *      inn begynnende fra og med.. ->pøs på alle data (også neste
      *      element hvis det er plass i mottakende array)
      * HVIS DET ER ØNSKELIG Å HA MINDRE FELTLENGDE ENN MAX LINJELENGDE
      * (FOR Å SPARE PLASS HVIS ARRAYET BLIR SVÆRT!) MÅ EN FØRST FLYTTE
      * FL,XX INN I EN VARIABEL, SÅ FLYTTE VARIABELEN TIL WL,YY!!!!!!!!
      *
      * KOMPONER (MAX 3) LINJER A 79 TEGN...
      * Hent felt for felt i kundens profil.
      * Sjekk om lengde skal justeres i forhold til standard.
      * Hvis numerisk - ta hensyn til editkode (og lengdejustering)
      * Hvis alfa - ta hensyn til lengdejustering
      *
      *Gammel sluttpos + "blank foran" + 1 = Ny startposisjon
     C     GMS           ADD       WFB           WFP
     C                   ADD       1             WFP
      *
     C     WFLEN         SUB       1             PLUSLU            3 0
     C     SFDEC         IFGE      '0'
     C     SFDEC         ANDLE     '9'
     C                   EXSR      NUMER
     C                   ELSE
     C                   EXSR      ALFA
     C                   END
     C* Posisjon start + plassbehov (-1) = Sluttposisjon
     C     WFP           ADD       PLUSLU        WFS
     C                   MOVE      WFS           GMS
      * Legg ut det evt. editerte/forkodtede feltet i worlinje eksempel
      * (men kun hvis sluttposisjon innenfor linjelengden)
     C     WFS           IFLE      79
     C                   MOVEA     WORKE         WL(WFP)
      * Legg ut overskriften i worklinje for heading(IKKE v/tekstkonst)
     C     WFEDIT        IFNE      'T'
     C                   MOVE      *BLANKS       HLPHE            79
     C                   MOVEL     WFOVER        HLPHE
     C                   MOVEA     HLPHE         WH(WFP)
     C                   END
     C                   ELSE
      * (sluttposisjon overskrider linjelengden - varsel)
     C                   MOVEA     '11'          *IN(30)
     C                   END
     C                   ENDSR
      *
     C*-------------------------------------------
     C     NUMER         BEGSR
     C*"""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      SFDEC         SFDECN            1 0
     C                   MOVE      SFDEC         DECANT            1 0
     C                   MOVE      WFDEC         WFDECN            1 0
     C     WFLEN         SUB       WFDECN        WHEL              3 0
     C     SFLEN         SUB       SFDECN        SHEL              3 0
      * lengdejustering fra venstre (Neg forteg=reduser)
     C     WHEL          SUB       SHEL          LENJUS            3 0
     C                   MOVE      FL(WFN)       WORKE
     C                   Z-ADD     SFLEN         EE                3 0
     C                   Z-ADD     79            NN                3 0
     C                   MOVE      *ZEROS        TUSDEL            1 0
     C                   MOVE      *ZEROS        DATTID            1 0
     C*
     C* Fortegn?? (ligger i så fall klar i posisjonen bak feltet)
     C     WFEDIT        IFEQ      '2'
     C     WFEDIT        OREQ      '4'
     C                   ADD       1             EE
     C                   MOVE      WE(EE)        WN(NN)
     C                   SUB       1             EE
     C                   SUB       1             NN
     C                   END
     C* Desimaler angitt ??
     C* I FL (->WE) er komma plassert i.h.t. SYFLD desimaldef. Hvis det
      * er definert ferre desim. i CUFLD ikke legg ut de "bakerste"...
      *(Mindre ant desim enn std. reduseres antall AKTIVE siff. bakfra
      * Lengdejustering også i forkant (LENJUS)  vil gil for få siffer
      * Eks 13,2 -> 11,2 = Juster 2 i forkant, men...
      *     13,2 -> 11,0 = 2 justeres i bakkant, IKKE også i forkant)
     C     SFDECN        IFNE      0
     C                   DO        SFDECN
     C     DECANT        IFLE      WFDECN
     C                   MOVE      WE(EE)        WN(NN)
     C                   SUB       1             NN
     C                   END
     C                   SUB       1             EE
     C                   END
     C* Legg inn desimal.komma (men IKKE hvis overstyrt til 0 i CUFLD)
     C     WFDECN        IFNE      0
     C                   MOVE      ','           WN(NN)
     C                   SUB       1             NN
     C                   END
     C                   END
     C*
     C* Resten av strengen takles i denne loopen......
     C     EE            DOWNE     0
     C*     tusendeling???
     C     WFEDIT        IFEQ      '3'
     C     WFEDIT        OREQ      '4'
     C     TUSDEL        IFEQ      3
     C                   MOVE      '.'           WN(NN)
     C                   SUB       1             NN
     C                   MOVE      *ZEROS        TUSDEL
     C                   END
     C                   ADD       1             TUSDEL
     C                   ELSE
     C*     dato/tid-editering???
     C     WFEDIT        IFEQ      '6'
     C     DATTID        IFEQ      2
     C                   MOVE      '.'           WN(NN)
     C                   SUB       1             NN
     C                   MOVE      *ZEROS        DATTID
     C                   END
     C                   ADD       1             DATTID
     C                   END
     C                   END
     C* ... evt editeringstegn er nå gjort - flytt selve tegnet/tallet
     C                   MOVE      WE(EE)        WN(NN)
     C                   SUB       1             EE
     C                   SUB       1             NN
     C                   END
     C*
     C* NN står nå en foran feltet..
     C                   ADD       1             NN
     C*
     C* Feltet ligger nå i WN fra posisjon NN -> 79
      * Skal lengden justeres (-4 i justering vil si øke NN med 4)
      * (EFFEKTIV sifferlengde - editeringskode m.m. telles ikke med)
     C     LENJUS        IFLT      0
     C     LENJUS        DOWLT     0
     C     WN(NN)        IFGE      '0'
     C     WN(NN)        ANDLE     '9'
     C                   ADD       1             LENJUS
     C                   END
     C                   ADD       1             NN
     C                   END
     C                   END
      * Står en etter lengdejustering med tusendelingstegn som første?
     C     WN(NN)        IFEQ      '.'
     C                   ADD       1             NN
     C                   END
     C* Skal foranstilte 0 fjernes??
     C     WFEDIT        IFGE      '1'
     C     WFEDIT        ANDLE     '4'
     C                   MOVE      NN            N2                3 0
      *
     C     STNULL        TAG
     C     WN(N2)        IFEQ      '0'
     C     WN(N2)        OREQ      '.'
     C                   MOVE      ' '           WN(N2)
     C                   ADD       1             N2
     C     N2            IFLT      79
     C                   GOTO      STNULL
     C                   END
     C                   END
      *
     C* Gjenstår  ,00 (eller ,50....) eller 0 med "fortegn" (= blank)
     C     WN(N2)        IFEQ      ','
     C     WN(N2)        OREQ      ' '
     C                   SUB       1             N2
     C                   MOVE      '0'           WN(N2)
     C                   END
      *
     C                   END
     C*
     C* Flytt det editerte feltet tilbake til array FL
     C                   MOVE      *BLANKS       HLPFLT           79
     C                   MOVEA     *BLANKS       WE
     C                   MOVEA     WN(NN)        WORKE
     C*
     C* Editeringen kan ha øket plassbehovet
     C     79            SUB       NN            PLUSLU
     C*
     C                   ENDSR
     C*
     C*-------------------------------------------
     C     ALFA          BEGSR
     C*"""""""""""""""""""""""""""""""""""""""""""
      *Legg tekstkonstant eller valgt felt i arbeidsarray
     C     SFEDIT        IFEQ      'T'
     C                   MOVE      *BLANKS       WORKE
     C                   MOVEL     WFOVER        WORKE
     C                   ELSE
     C                   MOVE      FL(WFN)       WORKE
     C                   END
      *Tegnene utover kundens ønskede lengde blankes
     C     WFLEN         ADD       1             EE
     C                   MOVEA     *BLANKS       WE(EE)
     C*
     C                   ENDSR
     C*
