     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYKS52PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV KOSTNADSBI-
     A                                      LAG FRITEKSTER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  5VALUES(' ' '1' '2' '4')
     A            KOKSKO         3A  O  6 10TEXT('KODE')
     A            KOKSTX        35A  O  6 15TEXT('FRITEKST')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYKS52'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  1 19MSGCON(050 JOV0388 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N55                              4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55                              4  2MSGCON(040 JOV0394 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 JOV0389 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 10MSGCON(060 JOV0390 *LIBL/SYMSGF)
     A            WSKSKO         3A  B  2 10DSPATR(UL)
     A                                      CHECK(LC)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      TEXT('Fritekstkode')
     A            WSKSTX        35A  B  2 15DSPATR(UL)
     A                                      CHECK(LC)
     A  89                                  DSPATR(RI)
     A                                      TEXT('Fritekst')
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            W2KSKO         3A  I  2 16DSPATR(UL)
     A                                      CHECK(LC)
     A                                      DSPATR(PC)
     A            W2KSTX        35A  I  2 20DSPATR(UL)
     A                                      CHECK(LC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 10MSGCON(060 JOV0390 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KOKSKO         3A  O  2 10DSPATR(PC)
     A                                      COLOR(WHT)
     A            KOKSTX        35A  O  2 15COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYKS52'
     A                                      COLOR(BLU)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  1 19MSGCON(045 JOV0388 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 66TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N55                              4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55                              4  2MSGCON(040 JOV0394 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 JOV0389 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
