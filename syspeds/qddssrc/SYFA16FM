     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R HEADER
     A                                  1 18'Vedlikehold av varekoders kostnads-
     A                                      prosent'
     A                                      DSPATR(HI)
     A                                  2 15'Avdeling:'
     A            KOAAVD         4  0O  2 28TEXT('AVDELINGSNR')
     A                                      EDTCDE(4)
     A                                      COLOR(BLU)
     A            KOANVN        33   O  2 35TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3 23'Varekode                Prosent'
     A                                      DSPATR(HI)
     A          R SUBFIL
     A                                      SLNO(*VAR)
     A                                      CLRL(01)
     A            SYVKOD         3   O  1 23TEXT('VAREKODE')
     A            SYPROS         5  2O  1 48TEXT('PROSENT')
     A                                      EDTCDE(1)
     A          R VARELI
     A                                      CF03(03 'Avslutt program')
     A                                      CF10(10 'Slette post')
     A                                      ROLLUP(25 'Bla frem')
     A                                      ROLLDOWN(26 'Bla tilbake')
     A                                      CF01(01 'Bytte avdeling')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2 10'Varekode:'
     A                                  2 36'Prosent:'
     A            WSVKOD         3A  B  2 20TEXT('VAREKODE')
     A  99                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  99                                  ERRMSG('Tastet varekode er ikke reg-
     A                                      istrert.' 99)
     A  97                                  ERRMSG('Tastet varekode for slettin-
     A                                      g er feil.' 97)
     A            WSPROS         5Y 2B  2 46TEXT('PROSENT')
     A  98                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  ERRMSG('Prosent kan ikke være 0.' 9-
     A                                      8)
     A                                  3 29'F3 = Avslutt'
     A                                      COLOR(BLU)
     A                                  3 42'F10 = Slett'
     A                                      COLOR(BLU)
     A                                  3 54'Bla frem/tilbake'
     A                                      COLOR(BLU)
     A                                  3  7'F1 = Bytte avdelinger'
     A                                      COLOR(BLU)
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                  2 18'Vedlikehold av varekoders kostnads-
     A                                      prosent'
     A                                      DSPATR(HI)
     A                                  9 18'Tast avdeling:'
     A            WSAAVD         4Y 0B  9 36
     A  99                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('Tastet avdeling finnes ikke-
     A                                      .' 99)
     A                                 23 18'F3 = Avslutt'
     A                                      COLOR(BLU)
