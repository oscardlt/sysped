      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 19 115 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '5' '8')
     A            ELDATE         8Y 0O  5  5EDTWRD('    .  .  ')
     A            ELTIME         6Y 0O  5 16EDTWRD('  .  .  ')
     A            ELTYP          5A  O  5 25
     A            ELLTXT      1024A  H
     A            ELLTXT80      80A  O  5 31
     A            ELIFSF       100A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF05(05 'FRIST')
     A                                      CF12(12 'AVBRYT')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD167R'
     A                                      COLOR(BLU)
     A                                  1 20'Vis sendingslogg'
     A                                      COLOR(WHT)
     A  41                              1 37'- Master'
     A                                      COLOR(WHT)
     A  42                              1 37'- House'
     A                                      COLOR(WHT)
     A                                  1 90DATE
     A                                      EDTCDE(Y)
     A                                  2 90TIME
     A  41                              2  2'Turnr:'
     A  41        UPRO           8Y 0O  2  9COLOR(BLU)
     A                                      EDTCDE(4)
     A  42                              2  2'Avd. :'
     A  42        UAVD           4Y 0O  2  9COLOR(BLU)
     A                                      EDTCDE(4)
     A  42                              2 15'Opdnr:'
     A  42        UTDN           7Y 0O  2 22COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  2'Velg:  5=Se hele tekst'
     A                                      COLOR(BLU)
     A                                  4  2'Op Send.dato  Send.tid Type  Tekst'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      WINDOW(WINDOWSZ)
     A                                      CF05(05 'FRIST')
     A                                      CF12(12 'AVBRYT')
     A                                      SLNO(18)
     A                                      CLRL(02)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      CF05(05 'FRIST')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD167R'
     A                                      COLOR(BLU)
     A                                  1 20'Vis sendingslogg'
     A                                      COLOR(WHT)
     A  41                              1 37'- Master'
     A                                      COLOR(WHT)
     A  42                              1 37'- House'
     A                                      COLOR(WHT)
     A                                  1 90DATE
     A                                      EDTCDE(Y)
     A                                  2 90TIME
     A  41                              2  2'Turnr:'
     A  41        UPRO           8Y 0O  2  9COLOR(BLU)
     A                                      EDTCDE(4)
     A  42                              2  2'Avd. :'
     A  42        UAVD           4Y 0O  2  9COLOR(BLU)
     A                                      EDTCDE(4)
     A  42                              2 15'Opdnr:'
     A  42        UTDN           7Y 0O  2 22COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  2'Velg:  5=Se hele tekst'
     A                                      COLOR(BLU)
     A                                  4  2'Op Send.dato  Send.tid Type  Tekst'
     A                                      COLOR(WHT)
     A                                 18  2'F5=Frisk   F12=Avbryt'
     A  30        MSGTXT        68A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE
     A                                      WINDOW(*DFT 13 105 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SAD167R'
     A                                      COLOR(BLU)
     A                                  1 20'Vis sendingslogg'
     A                                      COLOR(WHT)
     A  41                              1 37'- Master'
     A                                      COLOR(WHT)
     A  42                              1 37'- House'
     A                                      COLOR(WHT)
     A                                  2  2'Loggdato:'
     A            ELDATE         8Y 0O  2 12EDTWRD('    .  .  ')
     A                                  2 24'Loggtid:'
     A            ELTIME         6Y 0O  2 34EDTWRD('  .  .  ')
     A                                  2 45'Type:'
     A            ELTYP          5A  O  2 51
     A            ELLTXT      1024A  H
     A            ELLTXT01     100A  O  3  2
     A            ELLTXT02     100A  O  4  2
     A            ELLTXT03     100A  O  5  2
     A            ELLTXT04     100A  O  6  2
     A            ELLTXT05     100A  O  7  2
     A            ELLTXT06     100A  O  8  2
     A            ELLTXT07     100A  O  9  2
     A            ELLTXT08     100A  O 10  2
     A            ELLTXT09     100A  O 11  2
     A            ELLTXT10     100A  O 12  2
     A                                 13  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
