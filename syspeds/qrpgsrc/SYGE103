********hente økonomitall på ett oppdrag
     FFIRM      IF   E           K DISK
     FKOSBU9    IF   E           K DISK
     FKOSBU18   IF   E           K DISK
     F                                     RENAME(KOSBUR:KOSEBUR18R)
     FVALUL01   IF   E           K DISK
     FSYGE103O  UF a e           K DISK
     FSYGE103F  uF a e           K DISK
     FFAKT      IF   e           K DISK
     FHEADF     IF   e           K DISK
     FHEAD11    IF   e           K DISK
     F                                     RENAME(HEADFR:HEAD11R)
     FTURER14   IF   e           K DISK
      * førte inntekter
     D                 DS
     D  OMF                    1     78  2 DIM(6)
     D  omsgr1                 1     13  2
     D  omsgr2                14     26  2
     D  omsgr3                27     39  2
     D  omsgr4                40     52  2
     D  omsgr5                53     65  2
     D  omsgra                66     78  2
      * førte kostnader
     D                 DS
     D  KOF                    1     78  2 DIM(6)
     D  kosgr1                 1     13  2
     D  kosgr2                14     26  2
     D  kosgr3                27     39  2
     D  kosgr4                40     52  2
     D  kosgr5                53     65  2
     D  kosgra                66     78  2
      * budsj inntekter
     D                 DS
     D  OMB                    1     78  2 DIM(6)
     D  ombgr1                 1     13  2
     D  ombgr2                14     26  2
     D  ombgr3                27     39  2
     D  ombgr4                40     52  2
     D  ombgr5                53     65  2
     D  ombgra                66     78  2
     D                 DS
      * budsj kostnader
     D  KOB                    1     78  2 DIM(6)
     D  kobgr1                 1     13  2
     D  kobgr2                14     26  2
     D  kobgr3                27     39  2
     D  kobgr4                40     52  2
     D  kobgr5                53     65  2
     D  kobgra                66     78  2
     D                 DS
     D  gio                    1     30    DIM(10)
     D                 DS
     D  tabgeb                 1     30
     D  tg                     1     30    DIM(10)
     D                 DS
     D  P1DS                   1    256
     D  P1FLAG                 1      1
     D  P1BNR                  2      8  0
     D  P1AVD                  9     12  0
     D  P1TUNR                13     20  0
     D  P1OAVD                21     24  0
     D  P1OPD                 25     31  0
     D  P1BL                  32     44  2
     D                 DS
     D  XULTID                 1     14  0
     D  TID                    1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  DATO                   1      8  0
     C                   exsr      init
     c                   exsr      gebkosra
     c                   exsr      faksr
      * budsjett direkte på oppdrag
      *
      * kun førte     ?
      *
     c     utype         ifne      'O'
     c     ustat         ifne      'F'
     c                   exsr      budoppdrag
      * budsjett direkte på tur uten oppdrag
     c     hepro         ifne      0
     c                   exsr      budturdir
     c                   end
     c                   end
      *
      * I linjer på "Underordnert oppdrag" som ennå ikke er overført!
      * evt hopp ut ved interne
     c     UTYPEI        ifne      'N'
     C     TrOpd1        IFNE      *zeros
     C                   move      travd1        dupavd
     C                   move      tropd1        dupopd
     C                   exsr      DupKost
     C                   end
     C     TrOpd2        IFNE      *zeros
     C                   move      travd2        dupavd
     C                   move      tropd2        dupopd
     C                   exsr      DupKost
     C                   end
     C                   end
     C                   end
      *
     c                   exsr      udssr
     C     SLUTT         TAG
     C                   WRITE     SYGE103OR
     c                   seton                                        lr
     C                   RETURN
     c******************************************************************************
     c     init          begsr
     c******************************************************************************
     c     *entry        plist
     c                   parm                    uavd              4 0
     c                   parm                    uopd              7 0
     c                   parm                    utypeu            1
     c                   parm                    utypeiu           1
     c                   parm                    ustatu            1
     c                   parm                    gebioku           1
     c                   parm                    gebiou           30
     c                   parm                    gebio1u          30
     c                   parm                    gebio2u          30
     c                   parm                    gebio3u          30
     c                   parm                    gebio4u          30
     c                   parm                    gebio5u          30
      * FØRTE BELØP
     c                   parm                    omsgr1u          13 2
     c                   parm                    kosgr1u          13 2
     c                   parm                    omsgr2u          13 2
     c                   parm                    kosgr2u          13 2
     c                   parm                    omsgr3u          13 2
     c                   parm                    kosgr3u          13 2
     c                   parm                    omsgr4u          13 2
     c                   parm                    kosgr4u          13 2
     c                   parm                    omsgr5u          13 2
     c                   parm                    kosgr5u          13 2
     c                   parm                    omsgrau          13 2
     c                   parm                    kosgrau          13 2
      * BUDSJETTERTE BELØP
     c                   parm                    ombgr1u          13 2
     c                   parm                    kobgr1u          13 2
     c                   parm                    ombgr2u          13 2
     c                   parm                    kobgr2u          13 2
     c                   parm                    ombgr3u          13 2
     c                   parm                    kobgr3u          13 2
     c                   parm                    ombgr4u          13 2
     c                   parm                    kobgr4u          13 2
     c                   parm                    ombgr5u          13 2
     c                   parm                    kobgr5u          13 2
     c                   parm                    ombgrau          13 2
     c                   parm                    kobgrau          13 2
     c                   parm                    omstot           13 2
     c                   parm                    kostot           13 2
     c                   parm                    ombtot           13 2
     c                   parm                    kobtot           13 2
     c                   parm                    resbel           13 2
     c                   parm                    respro           13 2
      *
      *
      * tømmer wrkfil ordre
      *
     c                   move      uavd          faavd
     c                   move      uopd          faopd
     c     keyfak        chain     syge103o                           33
     c  N33              delete    syge103or
      *
     c                   move      *zeros        omf
     c                   move      *zeros        kof
     c                   move      *zeros        omb
     c                   move      *zeros        kob
      *
      * tømnmer workfil gebyr
      *
     c                   exsr      tommgeb
      *
     c                   move      utypeu        utype
     c                   move      utypeiu       utypei
     c                   move      ustatu        ustat
     c                   move      gebioku       gebiok
     c                   move      gebiou        gebio
     c                   move      gebio1u       gebio1
     c                   move      gebio2u       gebio2
     c                   move      gebio3u       gebio3
     c                   move      gebio4u       gebio4
     c                   move      gebio5u       gebio5
      *
     c     keyfak        klist
     c                   kfld                    faavd
     c                   kfld                    faopd
     C     KOSB9K        KLIST
     C                   KFLD                    YYST
     C                   KFLD                    faavd
     C                   KFLD                    faopd
     C                   MOVE      ' '           YYST              1
      *
     c     keybut        klist
     C                   KFLD                    YYST              1
     c                   kfld                    hepro
     c                   kfld                    avd0              4 0
     c                   kfld                    opd0              7 0
      *
     C*
     C     VALU1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    buval
      *
     c     turkey        klist
     c                   kfld                    tupro
      *
     C     DUPKEY        KLIST
     C                   KFLD                    DupAvd            4 0
     C                   KFLD                    DupOpd            7 0
      *
     c     keyfak        chain     headf                              33
     c  N33hepro         ifne      0
     c                   z-add     hefbv         ordfbv            9 0
     c     hepro         chain     turer14                            33
      * summer fraktvekt
     c                   exsr      sumturvkt
     c     keyfak        chain     headf                              33
     c                   end
     c                   read      firm                                   33
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     c                   endsr
     c******************************************************************************
     c     gebkosra      begsr
     c******************************************************************************
     c                   move      gebio1        tabgeb
     c                   z-add     1             gruppe
     c                   exsr      gebkosrb
     c                   move      gebio2        tabgeb
     c                   z-add     2             gruppe
     c                   exsr      gebkosrb
     c                   move      gebio3        tabgeb
     c                   z-add     3             gruppe
     c                   exsr      gebkosrb
     c                   move      gebio4        tabgeb
     c                   z-add     4             gruppe
     c                   exsr      gebkosrb
     c                   move      gebio5        tabgeb
     c                   z-add     5             gruppe
     c                   exsr      gebkosrb
     c                   endsr
     c******************************************************************************
     c     gebkosrb      begsr
     c******************************************************************************
     c     1             do        10            nr                2 0
     c     tg(nr)        ifne      *blanks
     c                   move      tg(nr)        gebko
     c                   write     syge103fr
     c                   endif
     c                   enddo
     c                   endsr
     C*
     C***********************************************
     C     FAKSR         BEGSR
     C***********************************************
      * henter tall fra fakturafil
      *
     c                   move      'J'           gebjn
     C                   SETOFF                                       34
     C     KEYFAK        SETLL     FAKT
     C     *IN34         DOWEQ     '0'
     c     nestefa       tag
     C     KEYFAK        READE     FAKT                                   34
     C     *IN34         IFEQ      '0'
      * test gebyr J/N
     c     gebio         ifne      *blanks
     c                   exsr      gebjnsr
     c                   end
     c     gebjn         ifeq      'J'
     C     FAOPKO        IFNE      'D'
     C     FASK          IFEQ      'S'
     C     FASK          OREQ      'K'
     C     FASK          OREQ      'X'
     C     FASK          OREQ      'I'
     C     FASK          OREQ      'F'
      * evt hopp ut ved interne
     c     UTYPEI        ifeq      'N'
     C     FASK          andeq     'I'
     c                   goto      nestefa
     c                   end
      *
      * finn aktuell gruppe
      *
     c     FAVK          chain     SYGE103F                           35
     c   35              z-add     6             gruppe
     C     FAKDA         IFEQ      'K'
     c     UTYPE         andne     'O'
     C                   ADD       FABELN        KOF(gruppe)
     C                   ELSE
     C     FASK          ifne      'F'
     C     FAOPKO        ifge      'E'
     C                   ADD       FABELN        OMF(gruppe)
     C                   ELSE
     c     ustat         ifne      'F'
     C                   ADD       FABELN        OMB(gruppe)
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C                   ENDDO
     C                   ENDSR
      ***********************************************
     C     BUDAVR        BEGSR
      ***********************************************
     c                   move      *zeros        kosta
      * henter budsjett fra avregning
     C                   MOVE      'O'           P1FLAG
     C                   MOVE      *ZEROS        P1BNR
     C                   MOVE      *ZEROS        P1AVD
     C                   MOVE      *ZEROS        P1TUNR
     C                   MOVE      HEAVD         P1OAVD
     C                   MOVE      HEOPD         P1OPD
     C                   MOVE      *ZEROS        P1BL
     C                   MOVE      P1DS          PARM1           256
     C                   CALL      'SYKS12'
     C                   PARM                    PARM1
     C                   MOVE      P1BL          KOSTA            13 2
     c                   endsr
      ***********************************************
     C     GEBJNSR       BEGSR
      ***********************************************
     c     FAVK          LOOKUP    gio                                    35
     c     gebio         ifeq      'I'
     c   35              move      'J'           gebjn             1
     c  N35              move      'N'           gebjn
     c                   end
     c     gebio         ifeq      'O'
     c   35              move      'N'           gebjn
     c  N35              move      'J'           gebjn
     c                   end
     c                   endsr
      ***********************************************
     C     udssr         BEGSR
      ***********************************************
      *
     c                   move      utype         utypeu
     c                   move      utypei        utypeiu
     c                   move      ustat         ustatu
     c                   move      gebiok        gebioku
     c                   move      gebio         gebiou
     c                   move      gebio1        gebio1u
     c                   move      gebio2        gebio2u
     c                   move      gebio3        gebio3u
     c                   move      gebio4        gebio4u
     c                   move      gebio5        gebio5u
      *
     c                   MOVE      omsgr1        omsgr1u          13 2
     c                   MOVE      kosgr1        kosgr1u          13 2
     c                   MOVE      omsgr2        omsgr2u          13 2
     c                   MOVE      kosgr2        kosgr2u          13 2
     c                   MOVE      omsgr3        omsgr3u          13 2
     c                   MOVE      kosgr3        kosgr3u          13 2
     c                   MOVE      omsgr4        omsgr4u          13 2
     c                   MOVE      kosgr4        kosgr4u          13 2
     c                   MOVE      omsgr5        omsgr5u          13 2
     c                   MOVE      kosgr5        kosgr5u          13 2
     c                   MOVE      omsgra        omsgrau          13 2
     c                   MOVE      kosgra        kosgrau          13 2
      * BUDSJETTERTE BELØP
     c                   MOVE      ombgr1        ombgr1u          13 2
     c                   MOVE      kobgr1        kobgr1u          13 2
     c                   MOVE      ombgr2        ombgr2u          13 2
     c                   MOVE      kobgr2        kobgr2u          13 2
     c                   MOVE      ombgr3        ombgr3u          13 2
     c                   MOVE      kobgr3        kobgr3u          13 2
     c                   MOVE      ombgr4        ombgr4u          13 2
     c                   MOVE      kobgr4        kobgr4u          13 2
     c                   MOVE      ombgr5        ombgr5u          13 2
     c                   MOVE      kobgr5        kobgr5u          13 2
     c                   MOVE      ombgra        ombgrau          13 2
     c                   MOVE      kobgra        kobgrau          13 2
     c                   xfoot     OMF           OMSTOT
     c                   xfoot     KOF           KOSTOT
     c                   xfoot     OMB           OMBTOT
     c                   xfoot     KOB           KOBTOT
     C     OMSTOT        ADD       KOSTOT        RESBEL
     C                   ADD       OMBTOT        RESBEL
     C                   ADD       KOBTOT        RESBEL
     c     OMSTOT        ADD       OMBTOT        BEL              13 2
     c     resbel        ifne      0
     c     bel           ifne      0
     c     resbel        div       bel           RESPRO
     c                   mult      100           respro
     c                   end
     c                   end
     c                   endsr
     C***************************************************************************
     C     budoppdrag    BEGSR
     C***************************************************************************
      *
     C* HENT EVT ÅPNE FORVENTEDE (BUDSJETTERTE) KOSTNADER
      *
     c                   setoff                                       33
     C     KOSB9K        SETLL     KOSBU9
     C     *IN33         DOWEQ     '0'
     C     KOSB9K        READE     KOSBU9                                 33
     c     *in33         ifeq      '0'
     c                   move      BUVK          favk
      * test gebyr J/N
     c     gebio         ifne      *blanks
     c                   exsr      gebjnsr
     c                   end
     c     gebjn         ifeq      'J'
      *
     C* Omregn valuta
      *
     C     BUVAL         IFNE      FICURR
     C     VALU1K        CHAIN     VALUL01                            34
     C  N34VALKU1        DIV       OMRFAK        XFAK              9 6
     C  N34              MULT(H)   XFAK          BUBL
     C                   END
      *
      *
      * finn aktuell gruppe
      *
     c     FAVK          chain     SYGE103F                           35
     c   35              z-add     6             gruppe
     C                   sub       BUBL          KOB(gruppe)
     C                   ENDif
     C                   ENDif
     C                   ENDdo
     C                   ENDSR
      *********************************************************************
     C     sumturvkt     begsr
      *********************************************************************
     c                   setoff                                       33
     C     turkey        setll     head11
     C     *IN33         doweq     '0'
     C     turkey        reade     head11                                 33
     c  N33              add       hefbv         turfbv            9 0
     c                   enddo
     c     ordfbv        div       turfbv        andeltur         13 4
     c                   endsr
      *
      *
     C***************************************************************************
     C     BUDTURDIR     BEGSR
     C***************************************************************************
      *
     C* HENT EVT ÅPNE FORVENTEDE (BUDSJETTERTE) KOSTNADER
      *
     c                   setoff                                       33
     C     KEYBUT        SETLL     KOSBU18
     C     *IN33         DOWEQ     '0'
     C     KEYBUT        READE     KOSBU18                                33
     c     *in33         ifeq      '0'
     c                   move      BUVK          favk
      * test gebyr J/N
     c     gebio         ifne      *blanks
     c                   exsr      gebjnsr
     c                   end
     c     gebjn         ifeq      'J'
      *
     C* Omregn valuta
      *
     C     BUVAL         IFNE      FICURR
     C     VALU1K        CHAIN     VALUL01                            34
     C  N34VALKU1        DIV       OMRFAK        XFAK              9 6
     C  N34              MULT(H)   XFAK          BUBL
     C                   END
      *
      *
      * finn aktuell gruppe
      *
     c     FAVK          chain     SYGE103F                           35
     c   35              z-add     6             gruppe
      *
      * ta andeltur
      *
     c     bubl          mult      andeltur      work132          13 2
     C                   sub       work132       KOB(gruppe)
     C                   ENDif
     C                   ENDif
     C                   ENDdo
     C                   ENDSR
     C***********************************************
     C     DupKost       BEGSR
     C***********************************************
     c                   setoff                                       34
     c                   move      'J'           gebjn
      * Ikke førte kostnadert på duppoppdrag
     C     DUPKEY        SETLL     FAKT
     C     *IN34         DOWEQ     '0'
     C     DUPKEY        READE     FAKT                                   34
     c     *in34         ifeq      '0'
      * test gebyr J/N
     c     gebio         ifne      *blanks
     c                   exsr      gebjnsr
     c                   end
     c     gebjn         ifeq      'J'
      * Kun åpne I-linjer skal hentes fra dup-oppdrag.
     c     FASK          IFEQ      'I'
     c     FAOPKO        andeq     ' '
     C                   SUB       FABELN        KOB(gruppe)
     C                   END
     C                   END
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      *
     C*
     C***********************************************
     C     TOMMGEB       BEGSR
     C***********************************************
      * tømmer workfil gebyr
      *
     c                   setoff                                       34
     C     *LOVAL        SETLL     SYGE103F
     C     *IN34         DOWEQ     '0'
     C                   READ      SYGE103F                               34
     C     *IN34         IFEQ      '0'
     c                   DELETE    SYGE103FR
     C                   END
     C                   ENDDO
     C                   ENDSR
