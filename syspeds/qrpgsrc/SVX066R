      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED NY FORSEGLINGS-ID          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FSVNVFS    UF A E           K DISK
     FSVXKODL01 IF   E           K DISK
     FSVX066D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UDFKD         IFNE      *BLANKS
     C     UDFSK         ORNE      *BLANKS
     C                   MOVE      UDFKD         TVDFKD
     C                   MOVE      UDFSK         TVDFSK
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SVVFSK        SETLL     SVNVFS
     C     SVVFSK        READE(N)  SVNVFS                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SVVFSK        READE(N)  SVNVFS                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C   03
     COR 12              GOTO      SLUTT
      *
     C   04              DO
     C                   MOVEL     W2DFSK        UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         W2DFSK
     C                   GOTO      START3
     C                   END
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C  N27W2NR          IFEQ      '1'
     C                   EXSR      CRTFS
     C                   GOTO      START3
     C                   END
      *
     C  N27ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     SVVFSK        SETLL     SVNVFS
     C     SVVFSK        READE     SVNVFS                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SVNVFSR
     C     SVVFSK        READE     SVNVFS                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UDFKD
     C                   MOVE      *BLANKS       UDFSK
     C                   MOVE      *ZEROS        XANTL             3 0
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     TVDFKD        IFNE      '*DELETE'
     C                   MOVE      TVDFKD        UDFKD
     C                   MOVE      TVDFSK        UDFSK
     C                   ADD       1             XANTL
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     TVDFKD        IFNE      '*DELETE'
     C     XANTL         IFEQ      0
     C                   MOVE      TVDFKD        UDFKD
     C                   MOVE      TVDFSK        UDFSK
     C                   ADD       1             XANTL
     C                   ELSE
     C                   ADD       1             XANTL
     C                   WRITE     SVNVFSR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     TVDFKD        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGFS
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     TVDFKD        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTFS
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTFS         BEGSR
     C***********************************************
     C     W2DFKD        IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'SVX0821'     MSGNR
     C                   GOTO      SLCRT
     C                   END
      *
     C     W2DFSK        IFNE      *BLANKS
     C                   MOVEL     W2DFSK        UKODE
     C     SVK01K        CHAIN     SVXKODL01                          89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLCRT
     C                   END
     C                   END
      *
     C                   MOVE      W2DFKD        TVDFKD
     C                   MOVE      W2DFSK        TVDFSK
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2DFKD
     C                   MOVE      *BLANKS       W2DFSK
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGFS         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      TVDFKD        WSDFKD
     C                   MOVE      TVDFSK        WSDFSK
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   MOVEL     W2DFSK        UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         W2DFSK
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C     WSDFKD        IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'SVX0821'     MSGNR
     C                   GOTO      STCHG
     C                   END
      *
     C     WSDFSK        IFNE      *BLANKS
     C                   MOVEL     WSDFSK        UKODE
     C     SVK01K        CHAIN     SVXKODL01                          89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
     C                   END
      *
     C                   MOVE      WSDFKD        TVDFKD
     C                   MOVE      WSDFSK        TVDFSK
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTFS         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       TVDFKD
     C                   MOVEL     '*DELETE'     TVDFKD
     C                   MOVE      *BLANKS       TVDFSK
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SVVFSK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    ULI
     C     SVK01K        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    UKODE
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
     C                   PARM                    UDFKD            20
     C                   PARM                    UDFSK             2
     C     *IN27         PARM                    UIN27             1
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      '012'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVE      UAVD          TVAVD
     C                   MOVE      UTDN          TVTDN
     C                   MOVE      ULI           TVLI
      *
     C                   ENDSR
      *
