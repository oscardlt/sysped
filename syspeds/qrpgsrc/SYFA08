      *
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE UTSKRIFTFORMULAR          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 28-29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     FKODTFM    UF A E           K DISK
     FKODTFML2  IF   E           K DISK
     F                                     RENAME(KODTFMR:KODTFMRL)
     FSYFA08D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KFMT1                  1     75
     D  KFMTX                  1     35
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2TXT         SETLL     KODTFML2
     C     START1        TAG
     C                   READ      KODTFML2                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        13
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      KODTFML2                               94
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   Z-ADD     1             WSSD
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                           30
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO
     C*
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     27            POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSKD          COMP      *BLANKS                                90
     C   90              MOVE      'YBC0022'     MSGNR
     C   90              SETON                                        30
     C   90              Z-ADD     19            LINNBR
     C   90              Z-ADD     9             POSNBR
     C  N30WSSD          COMP      0                                    8989
     C   89              MOVE      'YBC0023'     MSGNR
     C   89              SETON                                        30
     C   89
     CANN90              Z-ADD     19            LINNBR
     C   89
     CANN90              Z-ADD     24            POSNBR
     C  N30KODFMK        CHAIN(N)  KODTFM                             33
     C  N30
     CANN33              DO
     C                   MOVE      'YBC0026'     MSGNR
     C                   SETON                                        3090
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   END
     C                   END
     C*
     C     WSTXT         COMP      *BLANKS                                88
     C  N30
     CAN 88              DO
     C                   MOVE      'YBC0024'     MSGNR
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     43            POSNBR
     C                   SETON                                        30
     C                   END
     C*
     C*
     C   30              GOTO      SLTB1
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODFMK        CHAIN     KODTFM                             33
     C                   EXSR      MOVWFM
     C  N33              UPDATE    KODTFMR
     C  N33              UPDATE    SUBFIL
     C   33              WRITE     KODTFMR
     C   33              ADD       1             YRECNO
     C   33              Z-ADD     YRECNO        ZRECNO
     C   33              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C   27WSNR          IFEQ      '1'
     C                   MOVE      KFMKD         STRI
     C                   SETON                                        LR
     C                   RETURN
     C                   ELSE
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     KFMKD         COMP      '*DEL '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGKFM
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     KFMKD         COMP      '*DEL '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTKFM
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGKFM        BEGSR
     C***********************************************
     C                   EXSR      MOVFMW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                         30
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO
     C*
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTKFM        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODF2K        CHAIN     KODTFM                             33
     C  N33              DELETE    KODTFMR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWFM        BEGSR
     C***********************************************
     C                   MOVE      WSKD          KFMKD
     C                   MOVE      WSSD          KFMSD
     C                   MOVE      WSTXT         KFMTXT
     C                   MOVE      WST1          KFMT1
     C                   MOVE      WST2          KFMT2
     C                   MOVE      WST3          KFMT3
     C                   MOVE      WSPOS         KFMPOS
     C                   MOVE      WSOCR         KFMOCR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVFMW        BEGSR
     C***********************************************
     C                   MOVE      KFMKD         WSKD
     C                   MOVE      KFMSD         WSSD
     C                   MOVE      KFMTXT        WSTXT
     C                   MOVE      KFMT1         WST1
     C                   MOVE      KFMT2         WST2
     C                   MOVE      KFMT3         WST3
     C                   MOVE      KFMPOS        WSPOS
     C                   MOVE      KFMOCR        WSOCR
     C                   SETON                                        40
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     43            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      '*DEL '       KFMKD
     C                   MOVE      *ZEROS        KFMSD
     C                   MOVE      *BLANKS       KFMTXT
     C                   MOVE      *BLANKS       KFMT1
     C                   MOVE      *BLANKS       KFMT2
     C                   MOVE      *BLANKS       KFMT3
     C                   MOVE      *ZEROS        KFMPOS
     C                   MOVE      *BLANKS       KFMOCR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSKD
     C                   MOVE      *ZEROS        WSSD
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      *BLANKS       WST1
     C                   MOVE      *BLANKS       WST2
     C                   MOVE      *BLANKS       WST3
     C                   MOVE      *ZEROS        WSPOS
     C                   MOVE      *BLANKS       WSOCR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    MODE              1
     C                   PARM                    STRI              5
     C     KODFMK        KLIST
     C                   KFLD                    WSKD
     C                   KFLD                    WSSD
     C     KODF2K        KLIST
     C                   KFLD                    KFMKD
     C                   KFLD                    KFMSD
     C     MODE          COMP      'S'                                    27
     C*
     C                   MOVE      STRI          W2TXT
     C     MODE          IFEQ      'V'
     C                   WRITE     NYVARE
     C                   EXFMT     VAREL2
     C                   MOVE      *BLANKS       KFMT1
     C                   END
     C*
     C                   ENDSR
     C*
