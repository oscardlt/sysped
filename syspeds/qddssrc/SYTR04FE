     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  5VALUES(' ' '2' '4')
     A            TTTRNR         8Y 0O  6 13TEXT('KUNDE')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            VMNAVN        30A  O  6 23TEXT('NAVN')
     A            TTKOMS    R        O  6 55REFFLD(TRTRANR/TTKOMS *LIBL/TRTRAN)
     A                                      EDTCDE(4)
     A            TTTERM    R        O  6 63REFFLD(TRTRANR/TTTERM *LIBL/TRTRAN)
     A                                      EDTCDE(4)
     A            TTIKFB    R        O  6 74REFFLD(TRTRANR/TTIKFB *LIBL/TRTRAN)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYTR04E'
     A                                      COLOR(BLU)
     A                                  1 19'Vedl. Agents/transportørs komm.sat-
     A                                      s mm (innland) '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2' Opt       Trans.nr  Navn         -
     A                                                     Komm.sats  pr.tonn  -
     A                                       X=fritatt'
     A                                      COLOR(WHT)
     A                                  4 62'Term.geb  Fb.trekk'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'           Trans.nr  Navn         -
     A                                                     Komm.sats  Term.geb -
     A                                       Fb.trekk.'
     A                                      COLOR(WHT)
     A            WSTRNR         8Y 0B  2 13TEXT('TRANSPORTØRNR')
     A  90N40                               DSPATR(RI)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            VMNAVN        30A  O  2 23TEXT('NAVN')
     A            WSKOMS    R        B  2 55REFFLD(TRTRANR/TTKOMS *LIBL/TRTRAN)
     A                                      EDTCDE(4)
     A            WSTERM    R        B  2 63REFFLD(TRTRANR/TTTERM *LIBL/TRTRAN)
     A                                      EDTCDE(4)
     A            WSIKFB    R        B  2 74REFFLD(TRTRANR/TTIKFB *LIBL/TRTRAN)
     A  87                                  DSPATR(HI)
     A N40                              3  4'F4=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'           Trans.nr  Navn         -
     A                                                     Komm.sats  Term.geb -
     A                                       Fb.trekk '
     A                                      COLOR(WHT)
     A            TTTRNR         8Y 0O  2 13TEXT('KUNDE')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            VMNAVN        30A  O  2 23TEXT('NAVN')
     A                                      COLOR(WHT)
     A            TTKOMS    R        O  2 55REFFLD(TRTRANR/TTKOMS *LIBL/TRTRAN)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TTTERM    R        O  2 63REFFLD(TRTRANR/TTTERM *LIBL/TRTRAN)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TTIKFB    R        O  2 74REFFLD(TRTRANR/TTIKFB *LIBL/TRTRAN)
     A                                      COLOR(WHT)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3 21'Tast Enter for å slette eller F12 -
     A                                      for å avbryte'
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                  1  2'SYSPED/SYTR04E'
     A                                      COLOR(BLU)
     A                                  1 19'Vedl. Agents/transportørs komm.sat-
     A                                      s mm (innland) '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2' Opt       Trans.nr  Navn         -
     A                                                     Komm.sats  pr.tonn  -
     A                                       X=fritatt'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  4 62'Term.geb  Fb.trekk'
     A                                      COLOR(WHT)
