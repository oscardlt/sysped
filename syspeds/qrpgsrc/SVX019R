********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10311 * 01 YBC PTF10 DOKUMENTKODE ER ENDRET FRA 3A TIL 4A
********************************************************************
      ************************************************************
      *** PROGRAM SKAL PLUKKE OPPDRAG TIL TRANSITTERING        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04 06 07 09-11 13-24        ***
      *** LEDIGE INDIKATORER 29 32-89 95-97                    ***
      ************************************************************
      *
     FSVXV      O    E             DISK
     FSVXVAM    O    E             DISK
     FSVXHL     UF A E           K DISK
     FTURER14   IF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD14    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR14)
     FSVEH      IF   E           K DISK
     FSVEV      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHESTED    IF   E           K DISK
     FSVX019D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSID                 197    206
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HESV1                  1     25
     D  WSSV1                  1     10
     D                 DS
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D  TUDT                   1      8  0
     D                 DS
     D  XUDTD                  1      2  0
     D  XUDTM                  3      4  0
     D  XUDTÅ                  5      6  0
     D  XUDT                   1      6  0
     D                 DS
     D  W1TYPE                 1      2
     D  W1TYP2                 2      2
     D                 DS
     D  HENAS                  1     30
     D  WSNAS                  1     21
     D                 DS
     D  HENAK                  1     30
     D  WSNAK                  1     21
     D                 DS
     D  HEVS1                  1     25
     D  WSVS1                  1      8
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
      *
     C                   EXSR      INIT
      *
      ***  VIS BILDE FOR LISTETYPE
     C     CHOISE        TAG
     C                   EXFMT     BILDE01
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
      * SØK ETTER KUNDENR
     C   04              EXSR      SØKDTA
      *
     C                   EXSR      TESTB1
     C   30              GOTO      CHOISE
      *
     C     W1KNFA        COMP      *ZEROS                             8989
      *
      ***  KOMMER HIT KUN VED F5 - FRISK OPP = HENT FRA TUR IGJEN
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UPRO          SETLL     HEAD14
     C     UPRO          READE     HEAD14                                 94
      *
     C     *IN94         DOWEQ     '0'                                          1<-B
     C                   SETOFF                                       908586
     C     X_HEADFK      CHAIN(N)  SVXHL                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      TLTXT         WSTXT
     C                   MOVE      TLTLKU        WSTLKU
     C                   MOVE      'J'           WSMED
     C                   ELSE
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      HETLKU        WSTLKU
     C                   MOVE      ' '           WSMED
     C                   END
     C     WSTXT         IFEQ      *BLANKS
     C                   MOVE      ' '           WSFT
     C                   ELSE
     C                   MOVE      'J'           WSFT
     C                   END
     C                   MOVE      *BLANKS       WSNAFA
     C                   MOVE      HETLE         WSTLE
     C                   MOVE      HETLL         WSTLL
     C     HEATRK        CHAIN     HESTEDR                            31
     C     *IN31         IFEQ      '0'                                           2<-B
     C                   MOVEL     HSFROM        WSSDF
     C                   MOVEL     HSTO          WSSDT
     C                   ELSE                                                    2
     C                   SELECT
     C                   WHEN      HEADS3 <> *BLANKS
     C                   MOVEL     HEADS3        WSSDF
     C                   WHEN      HEADS2 <> *BLANKS
     C                   MOVEL     HEADS2        WSSDF
     C                   OTHER
     C                   MOVEL     HEADS1        WSSDF
     C                   ENDSL
     C                   SELECT
     C                   WHEN      HEADK3 <> *BLANKS
     C                   MOVEL     HEADK3        WSSDF
     C                   WHEN      HEADK2 <> *BLANKS
     C                   MOVEL     HEADK2        WSSDF
     C                   OTHER
     C                   MOVEL     HEADK1        WSSDF
     C                   ENDSL
     C                   END                                                     2<-E
      * HAR DENNE POSIJON STATUS T-DOK UTSKREVET ?? UTELATES OM IKKE Y/J I W1ALL
     C     W1ALL         IFEQ      ' '                                           2<-B
     C     W1ALL         OREQ      'N'
     C     TRTSTA        IFEQ      'A'                                            3<-B
     C     TRTSTA        OREQ      'B'
     C     TRTSTA        OREQ      'C'
     C     TRTSTA        OREQ      'D'
     C                   GOTO      ST21
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C     W1TYP2        IFEQ      ' '                                           2<-B
     C     TRTSTA        COMP      '3'                                    85    *WSTSTA WHITE
     C  N85TRTSTA        COMP      '4'                                    85
     C                   ELSE                                                    2
     C     W1TYP2        COMP      TRTSTA                                 85
     C                   END                                                     2<-E
     C   89W1KNFA        COMP      HEKNA                                  86    *WSNAFA WHITE
     C     HEKNA         IFNE      *ZEROS                                        2<-B
     C                   MOVE      HEKNA         YKN
     C     KUND1K        CHAIN     CUNDL01                            33
     C  N33              MOVEL     KNAVN         WSNAFA
     C   33              MOVE      *BLANKS       WSNAFA
     C                   END                                                     2<-E
     C     TRTSTA        IFEQ      W1TYP2                                        2<-B
     C     W1KNFA        IFEQ      *ZEROS                                         3<-B
     C     W1KNFA        ORNE      *ZEROS
     C     W1KNFA        ANDEQ     HEKNA
     C   28              MOVE      'J'           WSMED
     C                   SETON                                        90
     C                   ADD       1             XANTO
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * POSTEN SKAL MED - ØK TELLER SUBFILREC.NR
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C     UPRO          READE     HEAD14                                 94
     C  N94              ENDDO                                                  1<-E
      *
     C                   MOVE      ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
      * STARTPUNKT FOR NORMAL SYKLUS - RULLING/HENT OPTION....
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            5 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO
     C                   END
      ***
      *
     C                   SETOFF                                       9830
      *** FRISK OPP
     C   05              GOTO      START
      *** UTFØR PLUKK
     C   07              DO
     C     ULSTL         IFEQ      0
     C                   EXSR      UTFØRP
     C                   ELSE
     C                   EXSR      UTFØR2
     C                   END
     C                   GOTO      SLUTT
     C                   END
      *** FORRIGE
     C   12              GOTO      CHOISE
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      VLHENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     UTFØRP        BEGSR
     C***********************************************
     C     1             DO        YRECNO        ZRECNO                         1<-B
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     XANTO         IFEQ      0                                             2<-B
     C     WSMED         OREQ      'J'
     C                   CLEAR                   SVXVR
     C                   CLEAR                   SVEVR
     C                   MOVE      UAVD          TVAVD
     C                   MOVE      UTDN          TVTDN
     C     HEATRK        CHAIN     HEADF                              34
      * OPPDATER STATUS FOR Å VISE AT LISTE ER SKREVET
     C  N34W1TYPE        IFEQ      'T1'
     C                   MOVE      'A'           TRTSTA
     C                   ELSE
     C                   MOVE      'B'           TRTSTA
     C                   END
     C  N34              UPDATE    HEADFR
     C                   MOVE      HEAVD         TVAVD2
     C                   MOVE      HEOPD         TVTDN2
     C     X_HEADFK      CHAIN     SVEH                               34
     C     *IN34         IFEQ      '1'                                            3<-B
      * IKKE FINNE SAD-EKSPORT, BRUK OPPDRAGSOPPLYSNINGER
     C                   ADD       1             ULI
     C                   MOVE      ULI           TVLI
     C                   MOVE      HEVKT         TVVKTB
     C                   MOVEL     HEGM1         TVMN
     C                   MOVE      HENT          TVNT
     C                   MOVE      HENT          TVNTEH
     C                   MOVE      *BLANKS       TVDTY
     C                   MOVE      *BLANKS       TVDREF
     C     HETLL         IFNE      *BLANKS
S01  C*                  MOVE      '830'         TVDTY
N01  C                   EVAL      TVDTY = '830'
     C                   MOVEL     HETLE         TVDREF
     C                   CAT       HETLL:1       TVDREF
     C                   END
     C                   EXSR      HADR1
     C                   WRITE     SVXVR
     C                   ELSE                                                     3
     C                   MOVE      SVEH_AVLK     TVALK
     C                   MOVE      SVEH_MOLK     TVBLK
S01  C*                  MOVE      '830'         TVDTY
N01  C                   EVAL      TVDTY = '830'
     C                   EVAL      TVDREF = SVEH_TUID
     C                   EXSR      HADR2
     C                   MOVE      *OFF          *IN33
     C     X_HEADFK      SETLL     SVEV
     C     X_HEADFK      READE     SVEV                                   34
     C  N34              DOW       *IN33 = *OFF                                    4<-B
     C     X_HEADFK      READE     SVEV                                   33
     C  N33SVEV_NETO     IFNE      0                                                5<-B
     C                   LEAVE
     C                   END                                                        5<-E
     C                   ENDDO                                                     4<-E
      *
      * IKKE FINN SAD-EKSPORT ELLER FLERE VARELINJER
     C                   IF        (*IN34 OR (*IN33 = *OFF))                       4<-B
     C   34              CLEAR                   SVEVR
     C                   ADD       1             ULI
     C                   MOVE      ULI           TVLI
     C                   MOVE      SVEH_BRUT     TVVKTB
     C                   EVAL      TVVKTN = 0
     C                   IF        (*IN34 OR (SVEV_SUTX = *BLANKS))                 5<-B
     C                   MOVEL     HEGM1         TVMN
     C                   ELSE                                                       5
     C                   EVAL      TVMN   = SVEV_SUTX
     C                   END                                                        5<-E
     C                   EVAL      TVVT   = SVEV_VASL
     C                   EVAL      TVEH   = SVEV_KOSL
     C                   IF        *IN34 = *OFF                                     5<-B
     C     X_HEADFK      SETLL     SVEV
     C     X_HEADFK      READE     SVEV                                   33
     C  N33              DOW       *IN33 = *OFF                                      6<-B
     C                   EVAL      TVVKTN = TVVKTN + SVEV_NETO
     C     X_HEADFK      READE     SVEV                                   33
     C                   ENDDO                                                       6<-E
     C                   END                                                        5<-E
     C                   WRITE     SVXVR
     C                   ELSE                                                      4
      * FINN KUN EN SAD-EKSPORT LINJE
     C                   ADD       1             ULI
     C                   MOVE      ULI           TVLI
     C                   IF        SVEV_VATA <> *BLANKS
     C                   MOVEL     SVEV_VATA     TVVNT
     C                   ELSE
     C                   MOVE      *ZEROS        TVVNT
     C                   END
     C                   MOVE      SVEV_BRUT     TVVKTB
     C                   MOVE      SVEV_NETO     TVVKTN
     C                   MOVE      SVEV_ANKV     TVNTEH
     C                   MOVEL     SVEV_SUTX     TVMN
     C                   IF        SVEV_KOTA = 0
     C                   EVAL      TVNT   = SVEH_KOTA
     C                   ELSE
     C                   EVAL      TVNT   = SVEV_KOTA
     C                   END
     C                   EVAL      TVEH   = SVEV_KOSL
     C                   EVAL      TVVT   = SVEV_VASL
     C                   WRITE     SVXVR
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C                   MOVE      *ZEROS        UFLAGG
      *
     C                   ENDSR
      *
     C***********************************************
     C     HADR1         BEGSR
     C***********************************************
     C                   MOVE      HEKNS         TVKNS
     C                   MOVE      HENAS         TVNAS
     C                   MOVE      HEADS1        TVADS1
     C                   MOVEL     HEADS3        TVPNS
     C                   MOVEL     HEADS3        TVPSS
     C                   MOVEL     'SV'          TVSKS
     C     HEUR          IFEQ      'B'                                          1<-B
     C     HEUR          OREQ      'D'
     C     HEUR          OREQ      'G'
     C                   MOVE      'SE'          TVLKS
     C                   ELSE                                                   1
     C                   MOVE      HELKA         TVLKS
     C                   END                                                    1<-E
     C     HEKNS         IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TVTINS
     C                   ELSE                                                   1
     C                   MOVE      HEKNS         YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C     SYFOR9        IFEQ      *ZEROS                                        2<-B
     C                   MOVE      *BLANKS       TVTINS
     C                   ELSE                                                    2
     C                   MOVEL     SYFORE        TVTINS
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C                   MOVE      HEKNK         TVKNK
     C                   MOVE      HENAK         TVNAK
     C                   MOVE      HEADK1        TVADK1
     C                   MOVEL     HEADK3        TVPNK
     C                   MOVEL     HEADK3        TVPSK
     C                   MOVEL     'SV'          TVSKK
     C     HEUR          IFEQ      'A'                                          1<-B
     C     HEUR          OREQ      'C'
     C     HEUR          OREQ      'F'
     C                   MOVEL     'SE'          TVLKK
     C                   ELSE                                                   1
     C                   MOVEL     HELKA         TVLKK
     C                   END                                                    1<-E
     C     HEKNK         IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TVTINK
     C                   ELSE                                                   1
     C                   MOVE      HEKNK         YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C     SYFOR9        IFEQ      *ZEROS                                        2<-B
     C                   MOVE      *BLANKS       TVTINK
     C                   ELSE                                                    2
     C                   MOVEL     SYFORE        TVTINK
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C     TVADS1        IFEQ      *BLANKS                                      1<-B
     C                   MOVEL     '.'           TVADS1
     C                   END                                                    1<-E
     C     TVADK1        IFEQ      *BLANKS                                      1<-B
     C                   MOVEL     '.'           TVADK1
     C                   END                                                    1<-E
      *
     C                   WRITE     SVXVAMR
      *
     C                   ENDSR
      *
     C***********************************************
     C     HADR2         BEGSR
     C***********************************************
     C                   MOVE      SVEH_AVKN     TVKNS
     C                   MOVEL     SVEH_AVNA     TVNAS
     C                   MOVEL     SVEH_AVA1     TVADS1
     C                   MOVEL     SVEH_AVPN     TVPNS
     C                   MOVEL     SVEH_AVPA     TVPSS
     C                   MOVEL     'SV'          TVSKS
     C                   MOVEL     SVEH_AVLK     TVLKS
     C     SVEH_AVKN     IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TVTINS
     C                   ELSE                                                   1
     C                   MOVE      SVEH_AVKN     YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C     SYFOR9        IFEQ      *ZEROS                                        2<-B
     C                   MOVE      *BLANKS       TVTINS
     C                   ELSE                                                    2
     C                   MOVEL     SYFORE        TVTINS
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C                   MOVE      SVEH_MOKN     TVKNK
     C                   MOVE      SVEH_MONA     TVNAK
     C                   MOVE      SVEH_MOA1     TVADK1
     C                   MOVEL     SVEH_MOPN     TVPNK
     C                   MOVEL     SVEH_MOPA     TVPSK
     C                   MOVEL     'SV'          TVSKK
     C                   MOVEL     SVEH_MOLK     TVLKK
     C     SVEH_MOKN     IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TVTINK
     C                   ELSE                                                   1
     C                   MOVE      SVEH_MOKN     YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C     SYFOR9        IFEQ      *ZEROS                                        2<-B
     C                   MOVE      *BLANKS       TVTINK
     C                   ELSE                                                    2
     C                   MOVEL     SYFORE        TVTINK
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C     TVADS1        IFEQ      *BLANKS                                      1<-B
     C                   MOVEL     '.'           TVADS1
     C                   END                                                    1<-E
     C     TVADK1        IFEQ      *BLANKS                                      1<-B
     C                   MOVEL     '.'           TVADK1
     C                   END                                                    1<-E
      *
     C                   WRITE     SVXVAMR
      *
     C                   ENDSR
      *
     C***********************************************
     C     UTFØR2        BEGSR
     C***********************************************
     C     SVHK          SETLL     SVXHL
     C     SVHK          READE     SVXHL                                  33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SVXHLR
     C     SVHK          READE     SVXHL                                  33
     C  N33              ENDDO
      *
     C     1             DO        YRECNO        ZRECNO                         1<-B
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     XANTO         IFEQ      0                                             2<-B
     C     WSMED         OREQ      'J'
     C     HEATRK        CHAIN     HEADF                              34
      * OPPDATER STATUS FOR Å VISE AT LISTE ER SKREVET
     C  N34W1TYPE        IFEQ      'T1'
     C                   MOVE      'A'           TRTSTA
     C                   ELSE
     C                   MOVE      'B'           TRTSTA
     C                   END
     C  N34              UPDATE    HEADFR
     C                   MOVE      UAVD          TLAVD
     C                   MOVE      UTDN          TLTDN
     C                   MOVE      HEAVD         TLAVD2
     C                   MOVE      HEOPD         TLTDN2
     C                   MOVE      WSTXT         TLTXT
     C                   MOVE      WSTLKU        TLTLKU
     C                   WRITE     SVXHLR
     C                   ADD       HENT          TVNT
     C                   ADD       HENT          TVNTEH
     C                   ADD       HEVKT         TVVKTB
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C                   MOVE      UAVD          TVAVD
     C                   MOVE      UTDN          TVTDN
     C                   ADD       1             ULI
     C                   MOVE      ULI           TVLI
     C                   WRITE     SVXVR
      *
     C                   MOVE      *ZEROS        UFLAGG
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     W1KNFA        IFNE      *ZEROS
     C                   MOVE      W1KNFA        YKN
     C     KUND1K        CHAIN     CUNDL01                            87
     C  N87              MOVE      KNAVN         W1NAFA
     C   87              MOVE      'SPK0001'     MSGNR
     C   87              SETON                                        30
     C                   ELSE
     C                   MOVE      *BLANKS       W1NAFA
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W1KNFA
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
      *
     C                   ENDSR
      *
     C***********************************************
     C     VLHENT        BEGSR
     C***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
      *
      * INGEN GYLDIGE OPTIONS FORETATT
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     2             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
      * OPPDATER CURSORPOSISJON PÅ SKJERMBILDE
     C     ZRECNO        DIV       7             LINNBR
     C                   MVR                     LINNBR
     C     LINNBR        MULT      2             XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   SETON                                        99
      *
      * PLUKKE TIL DENNE LISTE
     C     WSNR          IFEQ      '1'
     C     WSMED         ANDNE     'J'
     C                   ADD       1             XANTO             5 0
     C                   MOVE      'J'           WSMED
     C                   SETON                                        90
      * VED PLUKKING OPPDATERES TRSTAT TIL AKTUELL TYPE
      *     DERSOM EN HAR VALGT FORTOLLINGSAGENT OPPDATERES OPD MED DENNE
     C     TRTSTA        IFNE      W1TYP2
     C     WSNAFA        ORNE      W1NAFA
     C     HEATRK        CHAIN     HEADF                              33
     C  N33              DO
     C     W1KNFA        IFNE      *ZEROS
     C                   Z-ADD     W1KNFA        HEKNA
     C                   MOVEL     W1NAFA        WSNAFA
     C                   END
     C                   MOVE      W1TYP2        TRTSTA
     C                   UPDATE    HEADFR
     C                   END
     C                   END
     C                   END
      *
      * FJERNE FRA DENNE LISTE
     C     WSNR          IFEQ      '4'
     C     WSMED         ANDEQ     'J'
     C                   SUB       1             XANTO             5 0
     C                   MOVE      ' '           WSMED
     C                   SETOFF                                       90
      * VED FJERNING OPPDATERES TRSTAT MED VERDI FRA OPTFELT 2
     C     HEATRK        CHAIN     HEADF                              33
     C  N33              MOVE      WSTSTA        TRTSTA
     C  N33              UPDATE    HEADFR
     C     W1TYP2        COMP      TRTSTA                                 85
     C                   END
      *
      * SE PÅ OPPDRAG
     C     WSNR          IFEQ      '5'
     C                   EXSR      VISE
     C                   END
      *
      * SE PÅ FRITEKST TIL ETT OPPDRAG
     C     WSNR          IFEQ      '9'
     C                   EXFMT     BILDE02
     C     WSTXT         IFNE      *BLANKS
     C                   MOVE      'J'           WSFT
     C                   END
     C                   END
      *
      *   OPTIONS UTFØRT - BLANK INPUT - OPPDAT- SUBFIL
     C                   MOVE      ' '           WSNR
     C                   MOVE      ' '           WSTSTA
     C                   UPDATE    SUBFIL
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       6             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISE          BEGSR
     C***********************************************
     C                   Z-ADD     2             UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SVHK          KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     X_HEADFK      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEATRK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YKN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
     C                   PARM                    UDK               5
     C                   PARM                    UPRO              8 0
     C                   PARM                    ULSTL             4 0
     C                   PARM                    UFLAGG            1 0
      *
     C                   Z-ADD     0             YKN               8 0
     C                   Z-ADD     0             HEOPD
     C                   MOVE      *ZEROS        W1KNFA
     C                   MOVEL     UDK           W1TYPE
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     UPRO          CHAIN     TURER14                            33
     C                   MOVE      TUDTD         XUDTD
     C                   MOVE      TUDTM         XUDTM
     C                   MOVE      TUDTÅ         XUDTÅ
     C     SVHK          SETLL     SVXHL
     C     SVHK          READE     SVXHL                                  28
      *
     C                   ENDSR
      *
