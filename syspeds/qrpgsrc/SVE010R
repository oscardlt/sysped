********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PKI sækerhetskoncept ikke merket
10XXX * 02 CB  Les alle for status og signatur
11XXX * 03 CB  Testindikator
     FSVE010D   CF   E             WORKSTN USROPN
     F                                     SFILE(SVE010DA:ZRECNO)
     FSVEH      UF   E           K DISK
S02  F*VEH2     IF   E           K DISK
N02  FSVEH12    IF   E           K DISK
     F                                     RENAME(SVEHR:SVEHR2)
     FSVTFI     IF   E             DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     D                UDS
     D  UAVD                  15     18  0
     D  USG                   19     21
      *   SYEDI data area
     D EDIDTA          DS           256
     D  UELIBF               171    180
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST

     D RcvDtaQ         PR                  EXTPGM('QRCVDTAQ')
     D  dqname                       10A   CONST
     D  dqLib                        10A   CONST
     D  dqLen                         5P 0
     D  dqData                       80A
     D  dqWait                        5P 0 CONST

     D DQLen           S              5P 0
     D TimeOut         S              5P 0
     D DQData          S             80A
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     c                   CLOSE     SVE010D
     c                   CALLP     RUNCMD('DLTOVR FILE(SVE010D)':200)
     c                   CALLP     RUNCMD('DLTDTAQ DTAQ(QTEMP/SVE010DQ)':200)
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN12         WIN12
     C     *LIKE         DEFINE    SVEH_SYST     ZVEH_SYST
     C                   EVAL      ZVEH_SYAV=UAVD
     C                   EVAL      ZVEH_SYSG=USG
     C                   EVAL      ZVEH_SYST='S'
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    ZVEH_SYST
S02  C*                  KFLD                    ZVEH_SYAV
     C                   KFLD                    ZVEH_SYSG
     C     KEYB          KLIST
     C                   KFLD                    SVEH_SYAV
     C                   KFLD                    SVEH_SYOP
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
N01  C                   CALL      'SYGE15C'
N01  C                   PARM                    WDT               8
N01  C                   PARM                    WTM               6
      *________________________
      * Hæmta firmaupplysningar
      *""""""""""""""""""""""""
N01  C     1             CHAIN     SVTFI                              51
      * Åpne dataarea
     C     *DTAARA       DEFINE                  EDIDTA
     C                   IN        EDIDTA
      * Lage dataq i QTEMP
     c                   CALLP     RUNCMD('CRTDTAQ DTAQ(QTEMP/SVE010DQ) -
     c                             MAXLEN(80) SEQ(*FIFO)': 200)
      * Overstyr skjermbildet til å bruke dataq
     c                   CALLP     RUNCMD('OVRDSPF FILE(SVE010D) DTAQ(' +
     c                              'QTEMP/SVE010DQ)': 200)
      * Åpne skjermbildet
     c                   OPEN      SVE010D
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN92=*ON
     C                   EVAL      *IN93=*ON
     C                   WRITE     SVE010DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
S02  C*    KEYA          SETLL     SVEH2
N02  C     KEYA          SETLL     SVEH12
S02  C*    KEYA          READE     SVEH2                                  94
N02  C     KEYA          READE     SVEH12                                 94

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SVE010DA
S02  C*    KEYA          READE     SVEH2                                  94
N02  C     KEYA          READE     SVEH12                                 94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     SVE010DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVE010DØ
     C                   WRITE     SVE010DC
     C                   WRITE     SVE010DB
      * Vent på en av to hendelser
      *    1) En post i dataq
      *    2) Et tidsavbrudd i vente på dataq
     c                   EVAL      DQLEN = 5
     c                   EVAL      TIMEOUT = 1800
     c*                  EVAL      TIMEOUT = 5
     c                   CALLP     RCVDTAQ('SVE010DQ':'QTEMP': DQLen: DQData:
     c                                 Timeout)
      * Vis resultatet  (hvis Len=0, ingen data mottat)
     c     DQLEN         IFEQ      0
     C                   EXFMT     SVE010DE
     C                   EVAL      *IN03 = *ON
     c                   ELSE
     c                   READ      SVE010DB                             9999
     c                   ENDIF
     C                   EVAL      *IN30=*OFF

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     SVE010DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '4'           SRBD                           Återkalla
     C     ZOP           CASEQ     '5'           SRBE                           Visa edifact
     C     ZOP           CASEQ     'S'           SRBS                           Skicka till tullverk
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Återkalla                                           SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *_____________
      * Ændra status
      *"""""""""""""
     C     KEYB          CHAIN     SVEH                               52
     C                   EVAL      SVEH_SYST=' '
     C                   UPDATE    SVEHR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   SVE010DA
     C                   UPDATE    SVE010DA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      *BLANKS       WFILNAM         256
     C                   MOVE      SVEH_SYAV     WAVD              4
     C                   MOVE      SVEH_SYOP     WOPD              7
     C                   EVAL      WfilNam = '/' + %trimr(UELIBF)
     C                             + '/EDISE/TEMP/'
     C                             + 'SVEU'
     C                             + WAVD
     C                             + '-'
     C                             + WOPD
      * Vise fil
     C                   MOVE      *BLANKS       QCMDA           256
     C                   EVAL      QCMDA = 'DSPF STMF(' + X'7D'
     C                             + %trimr(WFILNAM)
     C                             + X'7D'
     C                             + ')'
     c                   CALLP     RUNCMD(QCMDA:256)
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   SVE010DA
     C                   UPDATE    SVE010DA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skicka till tullverket                              SRBS /
      *////////////////////////////////////////////////////////////
     C     SRBS          BEGSR
      *_________________________
      * Bekræfta genom egen bild.
      *"""""""""""""""""""""""""
     C                   EXFMT     SVE010DF
     C   12              GOTO      SRBS9
      *_____________
      * Ændra status
      *"""""""""""""
     C     KEYB          CHAIN     SVEH                               52
     C                   EVAL      SVEH_SYST='A'
     C                   UPDATE    SVEHR
      *_____________________________
      * Hæmta næsta meddelandenummer
      *"""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *_______________
      * Skriv til EDIM
      *"""""""""""""""
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004=SVTF_0004
     C                   EVAL      M0010=SVTF_0010
S03  C*                  EVAL      M0035=SVTF_0035
N03  C                   EVAL      M0035=SVEH_0035
     C                   MOVEL     SVEH_SYAV     W0062            11
     C                   MOVE      SVEH_SYOP     W0062
     C                   EVAL      M0062=W0062
     C                   EVAL      M0065='CUSDEC'
     C                   EVAL      M1001=SVEH_MTYP
     C                   EVAL      M1004=SVEH_TUID
     C                   EVAL      M1225=SVEH_MTYP
     C                   EVAL      M1N07='SVE'
     C                   EVAL      MMN=CMN
     C                   EVAL      MSR='S'
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   EVAL      MAVD=SVEH_SYAV
     C                   EVAL      MTDN=SVEH_SYOP

     C                   WRITE     EDIMR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBS9         TAG
     C                   CLEAR                   SVE010DA
     C                   UPDATE    SVE010DA
     C                   ENDSR
