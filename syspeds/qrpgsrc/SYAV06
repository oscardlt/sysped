********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 ny parameter syge03
********************************************************************
      **************************************************************
      *** PROGRAM SKAL SLÅ SAMMEN OPPDRAG                        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER                                     ***
      **************************************************************
      *
     FTELL      UF   E           K DISK
     FHEADF     UF A E           K DISK    USROPN
     FFAKT      UF A E           K DISK
     FKODTV     IF   E           K DISK
     FFRISOK    O    E             DISK
     FKODFRI    IF   E           K DISK
     FSYAV06W   IF   E             DISK
     D                 DS
     D  XAVD                   1      4  0
     D  XOPDNY                 5     11  0
     D  XOPDNR                 1     11  0
     D                 DS
     D  HEAVD                  1      4  0
     D  HEOPD                  5     11  0
     D  XOPDGN                 1     11  0
     D                SDS
     D  UJOBNM               244    253
      *
     C                   EXSR      INIT
      *
     C                   OPEN      HEADF
     C                   READ      SYAV06W                                33
     C                   MOVE      SFAVD         XAVD
     C                   MOVE      SFOPD         XOPD
     C                   MOVE      SFLI          XLI
     C                   MOVE      SFKDPL        XKDPL
     C     SFAVD         CHAIN     TELL                               33
     C                   MOVE      TEOPDN        XOPDNY
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
      *
     C     *IN33         DOWEQ     '0'
      * OPPDATER GAMMELT OPPDRAG
     C     HEADFK        CHAIN     HEADF                              34
     C                   ADD       1             XKM
     C                   MOVE      XKM           TRKM
     C     HEM3          IFNE      0
     C     KODV2K        CHAIN     KODTV                              34
     C     HEM3          MULT(H)   KOVKKG        XFBV2
     C     HEFBV         IFLT      XFBV2
     C                   MOVE      XFBV2         HEFBV
     C                   END
     C                   END
     C                   UPDATE    HEADFR
     C                   MOVE      HEAVD         FSAVD
     C                   MOVE      HEOPD         FSOPD
      * OPPDATER GAMMEL FAKTLINJE
     C     FAKTK         CHAIN     FAKT                               34
     C                   MOVE      *ZEROS        FABELN
     C                   MOVE      *ZEROS        FABELV
     C                   MOVE      'E'           FAKDA
     C                   UPDATE    FAKTR
      * OPPDATER KRYSSEREFERANSE FOR GAMMELT OPPDRAG
     C                   MOVEL     'SAA'         FSKODE
     C                   MOVEL     XOPDNR        FSSOK
     C                   MOVE      HEDTOP        FSDTOP
     C     FSKODE        CHAIN     KODFRI                             34
     C  N33              MOVE      KFSODK        FSDOKK
     C   33              MOVE      *BLANKS       FSDOKK
     C                   WRITE     FRISOKR
      * OPPDATER KRYSSEREFERANSE FOR NYTT OPPDRAG
     C                   MOVE      XAVD          FSAVD
     C                   MOVE      XOPDNY        FSOPD
     C                   MOVEL     'SAB'         FSKODE
     C                   MOVEL     XOPDGN        FSSOK
     C                   MOVE      HEDTOP        FSDTOP
     C                   WRITE     FRISOKR
      * SUMMER ANTALL, VEKT, KUBIKK OG LASTMETER
     C                   ADD       HENT          XNT
     C                   ADD       HEVKT         XVKT
     C                   ADD       HEFBV         XFBV
     C                   ADD       HEM3          XM3
     C                   ADD       HELM          XLM
     C                   READ      SYAV06W                                33
     C  N33              ENDDO
      *
      * OPPDATER NYTT OPPDRAG
     C     HKEYS         CHAIN(N)  HEADF                              33
     C                   MOVE      XAVD          HEAVD
     C                   MOVE      XOPDNY        HEOPD
     C                   MOVE      'IB'          HEOT
     C                   MOVE      XNT           HENT
     C                   MOVE      XVKT          HEVKT
     C     XM3           IFEQ      0
     C                   MOVE      *ZEROS        HEM3
     C                   ELSE
     C     KODTVK        CHAIN     KODTV                              33
     C     XFBV          DIV(H)    KOVKKG        HEM3
     C                   END
     C                   MOVE      XFBV          HEFBV
     C                   MOVE      XLM           HELM
     C                   MOVE      XKDPL         HEKDPL
     C                   ADD       1             XKM
     C                   MOVE      XKM           TRKM
     C                   WRITE     HEADFR
      * TVUNG OPPDATERING PÅ PLATELAGER
     C                   CLOSE     HEADF
      *
      * OPPDATER NY FAKTURALINJE
     C     FKEYS         CHAIN(N)  FAKT                               33
     C                   MOVE      XAVD          FAAVD
     C                   MOVE      XOPDNY        FAOPD
     C                   MOVE      XLI           FALI
     C                   MOVE      'M'           FAKDA
     C                   EXSR      BERAVR
     C                   Z-SUB     AVRBEL        FABELN
     C                   Z-SUB     AVRBEL        FABELV
     C                   WRITE     FAKTR
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *****************************************************************
     C     BERAVR        BEGSR
      *****************************************************************
     C                   MOVE      'FRA'         UGEBTY            3
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      *ZEROS        AVRBEL           13 2
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      FAKUNR        UKUNDE
     C                   Z-ADD     HEAVD         UAVD
     C                   Z-ADD     HEOPD         UOPD
     C                   CALL      'SYGE03'
     C                   PARM                    UGEBTY
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UUVAL
     C                   PARM                    AVRBEL
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UKUNDE            8 0
     C                   PARM                    UAVNR             8 0
n01  C                   PARM                    atdum             1
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    HENT          XNT
     C     *LIKE         DEFINE    HEVKT         XVKT
     C     *LIKE         DEFINE    HEFBV         XFBV
     C     *LIKE         DEFINE    HEFBV         XFBV2
     C     *LIKE         DEFINE    HEM3          XM3
     C     *LIKE         DEFINE    HELM          XLM
     C     *LIKE         DEFINE    HEOPD         XOPD
     C     *LIKE         DEFINE    HEKDPL        XKDPL
     C     *LIKE         DEFINE    FALI          XLI
      *--------------------------------------------------------------
      * DEFINERER NØKKEL TIL FAKT
      *--------------------------------------------------------------
     C     FAKTK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    SFLI
     C     FKEYS         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XLI
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL HEADF
     C*--------------------------------------------------------------
     C     HEADFK        KLIST
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPD
     C     HKEYS         KLIST
     C                   KFLD                    XAVD
     C                   KFLD                    XOPD
     C     KODTVK        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    XAVD
     C     KODV2K        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    HEAVD
     C                   MOVE      'V'           KOVUNI
      *
     C     *ENTRY        PLIST
     C                   PARM                    XKM               5 0
      *
     C                   ENDSR
      *
