     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('FELLES' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A          H                           HLPDOC('FELLES' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 001 24 080)
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFAPRT  '
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV1511 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  2 20'D O K U M E N T E R - TRANSPORTMOD-
     A                                      ULEN  '
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  6 29DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                  7  2MSGCON(024 DIV1498 *LIBL/SYMSGF)
     A            WSSG           3A  B  7 29DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                  7 38MSGCON(012 DIV1513 *LIBL/SYMSGF)
     A                                  8  2MSGCON(024 DIV1499 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  8 29DSPATR(UL)
     A N98N99                               DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  8 38MSGCON(010 DIV1518 *LIBL/SYMSGF)
     A                                  9  2'Godsliste/turkonvolutt..'
     A            WSTUKO         1   B  9 29
     A                                 10  2'Tr.modul int/ekst ordre.'
     A            WSTROF         1   B 10 29
     A                                 10 38'(I/E/B)'
     A                                 11  2MSGCON(024 DIV1501 *LIBL/SYMSGF)
     A            WSOF           1A  B 11 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A            WSDT2          6Y 0B 11 31DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 11 38MSGCON(016 DIV1514 *LIBL/SYMSGF)
     A                                 12  2MSGCON(024 DIV1502 *LIBL/SYMSGF)
     A            WSVF           1A  B 12 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 12 38'Oridnær'
     A            WSVBST         1A  B 12 46DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A                                 12 49MSGCON(008 DIV1517 *LIBL/SYMSGF)
     A            WSVBEK         1A  B 12 58DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A                                 13  2MSGCON(024 DIV1503 *LIBL/SYMSGF)
     A            WSTPFB         1A  B 13 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 14  2MSGCON(024 DIV1504 *LIBL/SYMSGF)
     A            WSFP           1A  B 14 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 15  2'CMR-fraktbrev...........'
     A            WSCMR          1   B 15 29
     A                                 16  2'Enkel B/L, Waybill......'
     A            WSBL           1   B 16 29
     A                                 17  2MSGCON(024 DIV1507 *LIBL/SYMSGF)
     A            WSFFAK         1A  B 17 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 20  2MSGCON(024 DIV1509 *LIBL/SYMSGF)
     A            WSJBK          1A  B 20 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 20 50MSGCON(010 DIV0259 *LIBL/SYMSGF)
     A                                 21  2MSGCON(024 DIV1510 *LIBL/SYMSGF)
     A            WSPRT         10A  B 21 29DSPATR(UL)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
