********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 sh  PTF12 EVENTUELT ARKIVERER DOKUMENTET
********************************************************************
n01  FFIRMARC   IF   E           K DISK
N01  FARKTXT    IF   E           K DISK
n01  FARKIVL02  UF A E           K DISK
N01  FFIRM      IF   E           K DISK
     FSVIH      IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FSVI041P   O    E             PRINTER OFLIND(*IN27) USROPN
N01  D                SDS
N01  D  ZUSER                254    263
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
n01  c* EVENTUELT ARKIVER
n01  C     FIFIRM        CHAIN     FIRMARC                            33
n01  c                   move      'in'          artype
n01  C  N33ARTYPE        chain     arktxt                             33
n01  C     *IN33         IFEQ      '0'
n01  C     arkjn         andeq     'J'
n01  C                   EXSR      ARKIVSR
n01  C                   END
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVIH_SYAV     UVIH_SYAV
     C     *LIKE         DEFINE    SVIH_SYOP     UVIH_SYOP
     C     *LIKE         DEFINE    KOPUNI        UOPUNI
     C     *LIKE         DEFINE    KOPAVD        UOPAVD
     C     *LIKE         DEFINE    KOPLNR        UOPLNR
     C     *LIKE         DEFINE    KFMSD         UFMSD
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVIH_SYAV
     C                   KFLD                    UVIH_SYOP
     C     KEYP          KLIST
     C                   KFLD                    UOPUNI
     C                   KFLD                    UOPAVD
     C                   KFLD                    UOPLNR
     C     KEYF          KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD
N01  C     ARKKEY        KLIST
N01  C                   KFLD                    ARSTAT
N01  C                   KFLD                    ARAVD
N01  C                   KFLD                    AROPD
N01  C                   KFLD                    ARTYPE
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C                   EVAL      UOPUNI='P'
     C                   EVAL      UOPAVD=UVIH_SYAV
     C                   EVAL      UOPLNR=253
     C                   EVAL      UFMSD=1
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD           1024
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVIH_SYAV
     C                   PARM                    UVIH_SYOP
      *_____________________
      * Hente printerstyring
      *"""""""""""""""""""""
     C     KEYP          CHAIN     KODTP
     C     KEYF          CHAIN     KODTFM
      *_______________
      * Printerstyring
      *"""""""""""""""
     C                   EVAL      UCMD='OVRPRTF FILE(SVI041P) OUTQ('
     C     UCMD          CAT       KOPNVN:0      UCMD
     C     UCMD          CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   OPEN      SVI041P
n01  C                   READ      FIRM                                   33
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Læs fakturor                                         SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     SVIH
     C                   EVAL      WVIH_SYDT=SVIH_SYDT
     C                   EVAL      WVIH_VAKD=SVIH_VAKD
     C                   EVAL      WVIH_FABL=SVIH_FABL
     C                   WRITE     SVI041PA

     C                   ENDSR
N01  C****************************************************************
N01  C     ARKIVSR       BEGSR
N01  C****************************************************************
N01  C                   MOVE      FIFIRM        ARFIRM
N01  C                   MOVE      UVIH_SYAV     ARAVD
N01  C                   MOVE      UVIH_SYOP     AROPD
N01  C                   MOVE      'A'           ARSTAT
N01  C     ARKKEY        CHAIN     arkivl02                           33
N01  C   33              MOVE      ' '           ARSTAT
N01  C   33ARKKEY        CHAIN     arkivl02                           33
N01  c     *in33         ifeq      '1'
N01  C                   MOVE      ' '           ARSTAT
N01  C                   MOVE      *blanks       ARLINK
N01  C                   MOVE      ZUSER         ARUSER
N01  C                   MOVE      WDT           ARDATE
N01  C                   MOVE      WTM           ARTIME
N01  C                   move      SVIH_AVKN     arkund
N01  C                   WRITE     ARKIVR
N01  c                   move      'N'           nei               1
N01  c                   call      'ARC002CL'
N01  c                   parm                    artype
N01  c                   parm                    aruser
N01  c                   parm                    nei
n01  c                   end
N01  c                   endsr
