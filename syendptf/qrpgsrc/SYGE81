********************************************************************
      *  PROGRAM SOM FLYTTER ALLE DATAFILER FRA ETT OPPDRAG TIL ETT ANNET
********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 MÅ TA HENSUN TIL AT OPPD KAN VÆRE DUP ELLER HA DUP UNDER SEG
11XXX * 02 SH  PTF11 ARKIVET
11XXX * 03 JOV PTF11 obs dokufvf
11XXX * 04 SH  PTF11 TILDELER NY REKVISISJONSNUMMER
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%
12038 * 05 JOV PTF12 rekomp pga DOKUFVF
12041 * 06 JOV PTF12 T&T-Event MOV uheldig/sammenfall med Bring EDI - buttet til MFR=Moved from..
12041 * 07 SH  PTF12 Opprinnelig oppdrag i FRI søkevei
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12053 * 08 YBC PTF12 SVBELT I SADV ER ENDRET TIL 13.2
12053 * 09 YBC PTF12 også fil UTLLOGF
12053 * 10 JOV PTF12 også fil DOKUFE
12053 * 11 JOV PTF12 også ruckp purrelogg
12248 * 12 YBC PTF12 VEKT, KOLLI ER UTVIDET.
********************************************************************
N04  FKOSTT     UF   E           K DISK
N02  FARKIVL15  UF   E           K DISK
N02  FARKLOGH5  UF   E           K DISK
     FSADH      UF   E           K DISK
     FSADV      UF   E           K DISK
     FSADF      UF   E           K DISK
     FSAEH      UF   E           K DISK
S12  F*SAEV      UF   E           K DISK
N12  FSAEV      UF   E           K DISK    PREFIX(E_)
     FSAEF      UF   E           K DISK    RENAME(SADFR:SAEFR)
     FTELL      UF   E           K DISK
     FHEADF     UF   E           K DISK
     FHESTED    UF   E           K DISK
     FHETRIU    UF   E           K DISK
     FKOSBU12   UF   E           K DISK
     FDISP      UF   E           K DISK
     FTRACKF    UF A E           K DISK
     FTRACKT    UF   E           K DISK
     FDOKUF     UF   E           K DISK
     FDOKUFV    UF   E           K DISK
N03  FDOKUFVF   UF   E           K DISK
N10  FDOKUFE    UF   E           K DISK
N11  FRUCKP     UF   E           K DISK
     FDOKUFM    UF   E           K DISK
     FDOKEB     UF   E           K DISK
     FDOKEF     UF   E           K DISK
     FDOKEC     UF   E           K DISK
     FDOKUT     UF   E           K DISK
     FFAKT      UF   E           K DISK
s07  F*RISOK    UF   E           K DISK
N07  FFRISOK    UF A E           K DISK
     FFREETXT   UF   E           K DISK
     FCARGOF3   UF   E           K DISK
     FCARGOO    UF   E           K DISK
N09  FUTLLOGF   UF   E           K DISK
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     ISADVR
     I              SVBELT                      I_SVBELT
      *
     c     *ENTRY        PLIST
     c                   parm                    uavd              4 0
     c                   parm                    uopd              7 0
     c                   parm                    tilavd            4 0
     c                   parm                    tilopd            7 0
      *
     c                   move      uavd          fraavd            4 0
     c                   move      uopd          fraopd            7 0
     c                   move      uavd          fraavda           4
     c                   move      uopd          fraopda           7
     c                   move      tilavd        tilavda           4
      *
     c     KOPD          klist
     c                   kfld                    fraavd
     c                   kfld                    fraopd
N01  c     MORKEY        klist
N01  c                   kfld                    GMAVD0
N01  c                   kfld                    GMOPD0
N01  c     KMORDOKUF     klist
N01  C                   KFLD                    DFRI
N01  c                   kfld                    GMAVD0
N01  c                   kfld                    GMOPD0
     c                   move      'F'           DFRI
N01  c     FFKEY         klist
N01  c                   kfld                    GMAVD1
N01  c                   kfld                    GMOPD1
N01  c     VFKEY         klist
N01  c                   kfld                    GMAVD2
N01  c                   kfld                    GMOPD2
      *
     c     KDOKUF        klist
     C                   KFLD                    DFRI              1
     c                   kfld                    fraavd
     c                   kfld                    fraopd
     C     KDOKUT        KLIST
     C                   KFLD                    XDTSTA
     C                   KFLD                    FRAAVD
     C                   KFLD                    FRAOPD
     C                   MOVE      'T'           XDTSTA            1
      *
      * hent ordrenummer til
      *
     C     tilavd        CHAIN     TELL                               33
     c     *in33         ifeq      '0'
     C                   MOVE      TEOPDN        tilopd
     c                   move      TEOPDN        tilopda           7
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   END
      *
      * headf
      *
     c     KOPD          chain     headf                              33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        heavd
     c                   move      tilopd        heopd
     c                   update    headfr
     C                   END
N01  c                   move      TRAVD0        GMAVD0            4 0
N01  c                   move      TROPD0        GMOPD0            7 0
N01  c                   move      TRAVD1        GMAVD1            4 0
N01  c                   move      TROPD1        GMOPD1            7 0
N01  c                   move      TRAVD2        GMAVD2            4 0
N01  c                   move      TROPD2        GMOPD2            7 0
N01   * Flyttet oppdrag er DUP ??  =  Hadde MOR over seg: Endre pekere fra mor til dup
N01  c     GMOPD0        ifne      *zeros
N01  c     MORKEY        chain     HEADF                              33
N01  C     *IN33         IFEQ      '0'
N01  C                   select
N01  C                   when      TRAVD1=FraAvd and TROPD1=FraOpd
N01  C                   eval      TRAVD1=TilAvd
N01  C                   eval      TROPD1=TilOpd
N01  C                   when      TRAVD2=FraAvd and TROPD2=FraOpd
N01  C                   eval      TRAVD2=TilAvd
N01  C                   eval      TROPD2=TilOpd
N01  c                   other
N01   *   Det var hverken FF eller VF. Kan det være DUP via Fr.brev -  a al: U.op:0080/0201546
N01  C                   SETOFF                                       33
N01  c     KMORDOKUF     SETLL     DOKUF
N01  C     *IN33         DOWEQ     '0'
N01  c     KMORDOKUF     READE     DOKUF                                  33
N01  C     *IN33         IFEQ      '0'
N01  c                   if        %subst(DFBREF:1:5)='U.op:'
N01  c                             and  %subst(DFBREF:6:4) =fraavda
N01  c                             and  %subst(DFBREF:11:7)=fraopda
N01  c                   eval      DFBREF = 'U.op:'+tilavda+'/'+tilopda
N01  c                   endif
N01  c                   update    DOKUFR
N01  C                   ENDIF
N01  C                   ENDDO
N01  c
N01  c                   endsl
N01  c                   update    headfr
N01  c                   endif
N01  c                   endif
N01   *
N01   * Flyttet oppdrag DUP under seg??  =  Hadde barn unver seg: Endre pekere fra dup/barn til Mor
N01  c     GMOPD1        ifne      *zeros
N01  c     FFKEY         chain     HEADF                              33
N01  C     *IN33         IFEQ      '0'
N01  C                   eval      TRAVD0=TilAvd
N01  C                   eval      TROPD0=TilOpd
N01  c                   update    headfr
N01  c                   endif
N01  c                   endif
N01  c     GMOPD2        ifne      *zeros
N01  c     VFKEY         chain     HEADF                              33
N01  C     *IN33         IFEQ      '0'
N01  C                   eval      TRAVD0=TilAvd
N01  C                   eval      TROPD0=TilOpd
N01  c                   update    headfr
N01  c                   endif
N01  c                   endif
      *
      * disp
      *
     c     KOPD          chain     disp                               33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        DIAVD
     c                   move      tilopd        DIOPD
     c                   update    dispr
     C                   END
      *
      * SADH
      *
     c     KOPD          chain     SADH                               33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        SIAVD
     c                   move      tilopd        SITDN
     c                   update    sadhr
     C                   END
      *
      * SAEH
      *
     c     KOPD          chain     SAEH                               33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        SEAVD
     c                   move      tilopd        SETDN
     c                   update    SAEHR
     C                   END
      *
      * SADV
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     SADV
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     SADV                                   33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        SVAVD
     c                   move      tilopd        SVTDN
     c                   update    SADVR
     C                   ENDIF
     C                   ENDDO
      *
      *
      * SAEV
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     SAEV
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     SAEV                                   33
     C     *IN33         IFEQ      '0'
S12  c*                  move      tilavd        SVAVD
S12  c*                  move      tilopd        SVTDN
N12  c                   move      tilavd        E_SVAVD
N12  c                   move      tilopd        E_SVTDN
     c                   update    SAEVR
     C                   ENDIF
     C                   ENDDO
      *
      *
      * SADF
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     SADF
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     SADF                                   33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        SFAVD
     c                   move      tilopd        SFOPDN
     c                   update    SADFR
     C                   ENDIF
     C                   ENDDO
      *
      *
N02   * ARKIVL15
      *
N02  C                   SETOFF                                       33
N02  c     KOPD          SETLL     ARKIVL15
N02  C     *IN33         DOWEQ     '0'
N02  c     KOPD          READE     ARKIVL15                               33
N02  C     *IN33         IFEQ      '0'
N02  c                   move      tilavd        ARAVD
N02  c                   move      tilopd        AROPD
N02  c                   update    ARKIVR
N02  C                   ENDIF
N02  C                   ENDDO
      *
      *
N02   * ARKLOGH5
      *
N02  C                   SETOFF                                       33
N02  c     KOPD          SETLL     ARKLOGH5
N02  C     *IN33         DOWEQ     '0'
N02  c     KOPD          READE     ARKLOGH5                               33
N02  C     *IN33         IFEQ      '0'
N02  c                   move      tilavd        EPAVD
N02  c                   move      tilopd        EPOPD
N02  c                   update    ARKLOGHR
N02  C                   ENDIF
N02  C                   ENDDO
      *
      *
      * SAEF
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     SAEF
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     SAEF                                   33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        SFAVD
     c                   move      tilopd        SFOPDN
     c                   update    SAEFR
     C                   ENDIF
     C                   ENDDO
      *
      * HESTED
      *
     c     KOPD          chain     HESTED                             33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        HSAVD
     c                   move      tilopd        HSOPD
     c                   update    HESTEDR
     C                   END
      *
      * DOKEB
      *
     c     KOPD          chain     DOKEB                              33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        DBAVD
     c                   move      tilopd        DBOPD
     c                   update    DOKEBR
     C                   END
      *
      * HETRIU
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     HETRIU
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     HETRIU                                 33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        TIAVD
     c                   move      tilopd        TIOPD
     c                   update    HETRIUR
     C                   ENDIF
     C                   ENDDO
      *
      *
      * DOKUT
      *
     C                   SETOFF                                       33
     c     KDOKUT        SETLL     DOKUT
     C     *IN33         DOWEQ     '0'
     c     KDOKUT        READE     DOKUT                                  33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        DTAVD
     c                   move      tilopd        DTOPD
     c                   update    DOKUTR
     C                   ENDIF
     C                   ENDDO
      *
      *
      * DOKEC
      *
     c     KOPD          chain     DOKEC                              33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        DCAVD
     c                   move      tilopd        DCOPD
     c                   update    DOKECR
     C                   END
      *
      * DOKUF
      *
     C                   SETOFF                                       33
     c     KDOKUF        SETLL     DOKUF
     C     *IN33         DOWEQ     '0'
     c     KDOKUF        READE     DOKUF                                  33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        DFAVD
     c                   move      tilopd        DFOPD
     c                   update    DOKUFR
N01   * Dersom Frbrev peker på DUP - a la: U.op:0080/0201546 - må DUPend oppadgående ref fikses
N01  c                   if        %subst(DFBREF:1:5)='U.op:'
N01  c                             and  %subst(DFBREF:10:1) = '/'
N01  c                   move      *zeros        GMAVD2A           4
N01  c                   move      *zeros        GMOPD2A           7
N01  c                   eval      GMAVD2A = %subst(DFBREF:6:4)
N01  c                   eval      GMOPD2A = %subst(DFBREF:11:7)
N01  c                   move      GMAVD2A       GMAVD2
N01  c                   move      GMOPD2A       GMOPD2
N01  c     VFKEY         chain     HEADF                              34
N01  C     *IN34         ifeq      '0'
N01  C                   eval      TRAVD0=TilAvd
N01  C                   eval      TROPD0=TilOpd
N01  c                   update    headfr
N01  c                   endif
N01  c                   endif
     C                   ENDIF
     C                   ENDDO
      *
      *
      * DOKUFV
      *
     C                   SETOFF                                       33
     c     KDOKUF        SETLL     DOKUFV
     C     *IN33         DOWEQ     '0'
     c     KDOKUF        READE     DOKUFV                                 33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        FVAVD
     c                   move      tilopd        FVOPD
     c                   update    DOKUFVR
     C                   ENDIF
     C                   ENDDO
      *
      *
      * FAKT
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     FAKT
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     FAKT                                   33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        FAAVD
     c                   move      tilopd        FAOPD
     c                   update    FAKTR
     C                   ENDIF
     C                   ENDDO
      *
      *
      *
      * FRISOK
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     FRISOK
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     FRISOK                                 33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        FSAVD
     c                   move      tilopd        FSOPD
     c                   update    FRISOKR
     C                   ENDIF
     C                   ENDDO
      *
N07   * legg peker tilbake til gammlet oppdrag i Frisok slik at den blir søkbar..
N07  c                   move      tilavd        FSAVD
N07  c                   move      tilopd        FSOPD
N07  c                   move      'MFR'         FSKODE
N07  c                   move      HEDTOP        FSDTOP
N07  c                   move      *blanks       FSDOKK
N07  c                   eval      FSSOK=fraavda +'/'+fraopda
N07  c                   write     FRISOKR
      *
      * TRACKF
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     TRACKF
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     TRACKF                                 33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        TTAVD
     c                   move      tilopd        TTOPD
     c                   update    TRACKFR
     C                   ENDIF
     C                   ENDDO
      *
      *
      *
      * TRACKT
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     TRACKT
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     TRACKT                                 33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        TXAVD
     c                   move      tilopd        TXOPD
     c                   update    TRACKTR
     C                   ENDIF
     C                   ENDDO
      *
      *
      *
      * FREETXT
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     FREETXT
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     FREETXT                                33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        FRTAVD
     c                   move      tilopd        FRTOPD
     c                   update    FREETXTR
     C                   ENDIF
     C                   ENDDO
      *
      *
      *
      * DOKUFM
      *
     C                   SETOFF                                       33
     c     KDOKUF        SETLL     DOKUFM
     C     *IN33         DOWEQ     '0'
     c     KDOKUF        READE     DOKUFM                                 33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        FMAVD
     c                   move      tilopd        FMOPD
     c                   update    DOKUFMR
     C                   ENDIF
     C                   ENDDO
      *
N03   * DOKUFVF
N03   *
N03  C                   SETOFF                                       33
N03  c     KOPD          SETLL     DOKUFVF
N03  C     *IN33         DOWEQ     '0'
N03  c     KOPD          READE     DOKUFVF                                33
N03  C     *IN33         IFEQ      '0'
N03  c                   move      tilavd        FFAVD
N03  c                   move      tilopd        FFOPD
N03  c                   update    DOKUFVFR
N03  C                   ENDIF
N03  C                   ENDDO
      *
N10   * DOKUFE
N10   *
N10  C                   SETOFF                                       33
N10  c     KOPD          SETLL     DOKUFE
N10  C     *IN33         DOWEQ     '0'
N10  c     KOPD          READE     DOKUFE                                 33
N10  C     *IN33         IFEQ      '0'
N10  c                   move      tilavd        FE_DFAVD
N10  c                   move      tilopd        FE_DFOPD
N10  c                   update    DOKUFER
N10  C                   ENDIF
N10  C                   ENDDO
N11   * RUCKP
N11   *
N11  C                   SETOFF                                       33
N11  c     KOPD          SETLL     RUCKP
N11  C     *IN33         DOWEQ     '0'
N11  c     KOPD          READE     RUCKP                                  33
N11  C     *IN33         IFEQ      '0'
N11  c                   move      tilavd        RUAVD
N11  c                   move      tilopd        RUOPD
N11  c                   update    RUCKPR
N11  C                   ENDIF
N11  C                   ENDDO
N01   *
N01   * CARGOF
N01   *
N01  C                   SETOFF                                       33
N01  c     KOPD          SETLL     CARGOF3
N01  C     *IN33         DOWEQ     '0'
N01  c     KOPD          READE     CARGOF3                                33
N01  C     *IN33         IFEQ      '0'
N01  c                   move      tilavd        KOAVD
N01  c                   move      tilopd        KOOPD
N01  c                   update    CARGOFR
N01  C                   ENDIF
N01  C                   ENDDO
N01   *
N01   * CARGOO
N01   *
N01  C                   SETOFF                                       33
N01  c     KOPD          SETLL     CARGOO
N01  C     *IN33         DOWEQ     '0'
N01  c     KOPD          READE     CARGOO                                 33
N01  C     *IN33         IFEQ      '0'
N01  c                   move      tilavd        COAVD
N01  c                   move      tilopd        COOPD
N01  c                   update    CARGOOR
N01  C                   ENDIF
N01  C                   ENDDO
      *
N09  C                   SETOFF                                       33
N09  c     KOPD          SETLL     UTLLOGF
N09  C     *IN33         DOWEQ     '0'
N09  c     KOPD          READE     UTLLOGF                                33
N09  C     *IN33         IFEQ      '0'
N09  c                   move      tilavd        ULAVD
N09  c                   move      tilopd        ULOPD
N09  c                   update    UTLLOGR
N09  C                   ENDIF
N09  C                   ENDDO
      *
      *
      * DOKEF
      *
      *
     C                   SETOFF                                       33
     c     KDOKUF        SETLL     DOKEF
     C     *IN33         DOWEQ     '0'
     c     KDOKUF        READE     DOKEF                                  33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        DFAVD
     c                   move      tilopd        DFOPD
     c                   update    DOKEFR
     C                   ENDIF
     C                   ENDDO
      *
      * KOSBU12
      *
     C                   SETOFF                                       33
     c     KOPD          SETLL     KOSBU12
     C     *IN33         DOWEQ     '0'
     c     KOPD          READE     KOSBU12                                33
     C     *IN33         IFEQ      '0'
     c                   move      tilavd        BUOAVD
     c                   move      tilopd        BUOPD
N04  C                   MOVE      'Å'           YBNRT             1
N04  C     YBNRT         CHAIN     KOSTT                              34
N04  C  N34              ADD       1             KTNR
N04  C  N34              UPDATE    KOSTTR
N04  C  N34              MOVE      KTNR          BUBNR
     C*
     c                   update    KOSBUR
     C                   ENDIF
     C                   ENDDO
      *
     c                   exsr      ttsr
      *
     c                   seton                                        lr
     c                   return
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
     C                   MOVE      TILAVD        TTAVD
     C                   MOVE      TILOPD        TTOPD
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
S06  C*                  MOVE      'MOV'         TTACTI
N06  C                   MOVE      'MFR'         TTACTI
     C                   MOVE      ULDT          TTDATE
     C                   MOVE      XULTM         TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      ZUSER         TTUSER
     C                   MOVE      *blanks       TTTEXT
n21  C                   eval      TTTEXT = 'Order:'
n21  C     TTTEXT        CAT       FRAAVDA:1     TTTEXT
n21  C     TTTEXT        CAT       '/':0         TTTEXT
n21  C     TTTEXT        CAT       FRAOPDA:0     TTTEXT
n21  C     TTTEXT        CAT       'Moved to':1  TTTEXT
n21  C     TTTEXT        CAT       TILAVDA:1     TTTEXT
n21  C     TTTEXT        CAT       '/':0         TTTEXT
n21  C     TTTEXT        CAT       TILOPDA:0     TTTEXT
n24  C                   eval      TTTEXL = 'Ordre:'
n21  C     TTTEXL        CAT       FRAAVDA:1     TTTEXL
n21  C     TTTEXL        CAT       '/':0         TTTEXL
n21  C     TTTEXL        CAT       FRAOPDA:0     TTTEXL
n21  C     TTTEXL        CAT       'Flyttet':1   TTTEXL
n21  C     TTTEXL        CAT       'til':1       TTTEXL
n21  C     TTTEXL        CAT       TILAVDA:1     TTTEXL
n21  C     TTTEXL        CAT       '/':0         TTTEXL
n21  C     TTTEXL        CAT       TILOPDA:0     TTTEXL
     C                   WRITE     TRACKFR
     C                   ENDSR
     C*
