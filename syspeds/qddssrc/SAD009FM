********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068F* 02 TH  PTF8 ny helpfunksjon
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S02  A*                                     HLPDOC('SAD00901' 'SAD009' 'SY400-
S02  A*                                     LO/SADTVINN')
S02  A*                                     HELP
S02  A*                                     ALTHELP
N02  A                                      HELP(01)
N02  A                                      CF01(01)
     A                                      CF03(03 'Avslutt program')
      *
     A          R BILDE01
     A                                  1  2'SYSPED/SAD009'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(40 DIV1798 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  9 25MSGCON(18 DIV1799 SYMSGF)
     A            WSVALG         1D 0B  9 48DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2)
     A                                 10 25MSGCON(18 DIV1800 SYMSGF)
     A                                      COLOR(BLU)
     A                                 11 25MSGCON(18 DIV1801 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 25MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(78 SPV0010 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE02
     A                                      CF12(12 'Gå til sorteringsmåtevalgs-
     A                                       bilde')
     A                                  1  2'SYSPED/SAD009'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(40 DIV1798 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  7 16MSGCON(18 DIV1802 SYMSGF)
     A            WSDTF          6D 0B  7 37DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  9 16MSGCON(18 DIV1803 SYMSGF)
     A            WSDTT          6D 0B  9 37DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11 16MSGCON(18 DIV1804 SYMSGF)
     A            WSKKUN         1A  B 11 37DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'Y')
     A                                 11 44MSGCON(35 DIV1805 SYMSGF)
     A                                      COLOR(BLU)
     A                                 13 16MSGCON(18 DIV1806 SYMSGF)
     A            WSKUTS         1A  B 13 37DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'Y')
     A                                 13 44MSGCON(35 DIV1807 SYMSGF)
     A                                      COLOR(BLU)
     A                                 15 16MSGCON(18 DIV1808 SYMSGF)
     A            WSMI           1A  B 15 37DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'Y')
     A                                 15 44MSGCON(35 DIV1809 SYMSGF)
     A                                      COLOR(BLU)
     A                                 17 16MSGCON(18 DIV1810 SYMSGF)
     A            WSAVDF         4Y 0B 17 37DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 19 16MSGCON(18 DIV1811 SYMSGF)
     A            WSAVDT         4Y 0B 19 37DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 23 16MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 36MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE03
     A                                      CF12(12 'Gå til sorteringsmåtevalgs-
     A                                       bilde')
     A                                  1  2'SYSPED/SAD009'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 DIV1798 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  6 16MSGCON(020 DIV1812 *LIBL/SYMSGF)
     A            WSGNF         15A  B  6 37DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  7 16MSGCON(020 DIV1813 *LIBL/SYMSGF)
     A            WSGNT         15A  B  7 37DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 48MSGCON(030 DIV2369 *LIBL/SYMSGF)
     A                                  9 16MSGCON(020 DIV2370 *LIBL/SYMSGF)
     A            WSDTGF         6Y 0B  9 37CHECK(RB)
     A                                      EDTCDE(4)
     A  97                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  9 48MSGCON(030 DIV2371 *LIBL/SYMSGF)
     A                                 11 16MSGCON(018 DIV1804 *LIBL/SYMSGF)
     A            WSKKUN         1A  B 11 37DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'N' 'J' 'Y')
     A                                 11 44MSGCON(035 DIV1805 *LIBL/SYMSGF)
     A                                 13 16MSGCON(018 DIV1806 *LIBL/SYMSGF)
     A            WSKUTS         1A  B 13 37DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'N' 'J' 'Y')
     A                                 13 44MSGCON(035 DIV1807 *LIBL/SYMSGF)
     A                                 15 16MSGCON(020 DIV2372 *LIBL/SYMSGF)
     A            WSKUEX         1A  B 15 37COLOR(YLW)
     A                                      VALUES(' ' 'N' 'J' 'Y')
     A                                 15 44MSGCON(035 DIV2373 *LIBL/SYMSGF)
     A                                 17 16MSGCON(040 DIV2379 *LIBL/SYMSGF)
     A                                 18 16MSGCON(018 DIV1810 *LIBL/SYMSGF)
     A            WSAVDF         4Y 0B 18 37DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18 44MSGCON(030 DIV2371 *LIBL/SYMSGF)
     A                                 19 16MSGCON(018 DIV1811 *LIBL/SYMSGF)
     A            WSAVDT         4Y 0B 19 37DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 19 44MSGCON(030 DIV2369 *LIBL/SYMSGF)
     A                                 23 16MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 36MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
