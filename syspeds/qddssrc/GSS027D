     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 13 55 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2')
     A            KALNOA         3  0H
     A            AKANR         15A  O  5  5
     A            AKCONO        20A  O  5 21
     A            KASTKM         5  0O  5 42EDTCDE(4)
     A            XXSTKM         5  0O  5 48EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'GSS027R'
     A                                      COLOR(BLU)
     A                                  1 10'Aribed med kolli innhold'
     A                                      COLOR(WHT)
     A                                  2  2'Ordrenr.'
     A            UONO           7  0O  2 12EDTCDE(4)
     A                                  2 22'Kollinr.'
     A            ULNO           2  0O  2 32EDTCDE(4)
     A N27                              3  2'Velg option:  2=Endre'
     A                                      COLOR(BLU)
     A                                  4  2'Op Artikkelnr      P.O.nr         -
     A                                            Stykk  Rest'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(13)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30N27                           1  2'F3/F12=Avsl F5=Frisk'
     A                                      COLOR(BLU)
     A N30 27                           1  2'F3/F12=Avsl'
     A                                      COLOR(BLU)
     A  30        MSGTXT        33A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'GSS027R'
     A                                      COLOR(BLU)
     A                                  1 10'Aribed med kolli innhold'
     A                                      COLOR(WHT)
     A                                  2  2'Ordrenr.'
     A            UONO           7  0O  2 12EDTCDE(4)
     A                                  2 22'Kollinr.'
     A            ULNO           2  0O  2 32EDTCDE(4)
     A                                  4  2'Op Artikkelnr      P.O.nr         -
     A                                            Stykk  Rest'
     A                                      COLOR(WHT)
     A N27                             13  2'F3/F12=Avsl F5=Frisk'
     A                                      COLOR(BLU)
     A  27                             13  2'F3/F12=Avsl'
     A                                      COLOR(BLU)
      *
     A          R BILDE01
     A                                      WINDOW(*DFT 07 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                  1  2'GSS027R'
     A                                      COLOR(BLU)
     A                                  1 10'Aribed med kolli innhold'
     A                                      COLOR(WHT)
     A                                  2  2'Ordrenr.'
     A            UONO           7  0O  2 12EDTCDE(4)
     A                                  2 22'Kollinr.'
     A            ULNO           2  0O  2 32EDTCDE(4)
     A                                  3  2'Art.nr:'
     A            AKANR         15A  O  3 12DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4  2'P.O.nr:'
     A            AKCONO        20A  O  4 12DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  5  2'Mott.stk:'
     A            WSSTKM         5  0B  5 12DSPATR(UL)
     A                                      DSPATR(PC)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        48A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
