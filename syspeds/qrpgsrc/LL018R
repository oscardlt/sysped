      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED SERIENR (INNLEGG)          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 Sikre mot update without prior read ..
11XXX * 02 YBC PTF11 OPSJON M = OPPDATER SERIENR + SETT STATUS FOR MOTTATT
********************************************************************
      *
     FLLSERL01  UF A E           K DISK
     FLLSERL03  IF   E           K DISK
     F                                     RENAME(LLSERR:LLSERR3)
     FLL018D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   Z-ADD     *ZEROS        XRECNO            4 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SER3K         SETLL     LLSERL03
     C     1             DO        UANT          XN                5 0
     C                   ADD       1             ZRECNO
     C  N33SER3K         READE     LLSERL03                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      SNSN          WSSN
     C                   ELSE
     C                   MOVE      *BLANKS       WSSN
     C                   MOVE      ' '           SNST
     C                   MOVE      *ZEROS        SNMNU
     C     XRECNO        IFEQ      0
     C                   MOVE      ZRECNO        XRECNO
     C                   END
     C                   END
     C     WSSN          COMP      *BLANKS                            2828
     C     SNST          COMP      ' '                                2929
     C                   WRITE     SUBFIL
     C                   ENDDO
     C                   MOVE      '1'           *IN94
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     1             ZRECNO
     C     XRECNO        IFEQ      0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   ELSE
     C                   DIV       10            XRECNO
     C                   MVR                     XRECNO
     C     XRECNO        IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C     XRECNO        ADD       5             LINNBR
     C                   END
     C                   Z-ADD     8             POSNBR
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
      *
     C                   SETOFF                                       9998
     C                   SETOFF                                       30
      *** AVSLUTT
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      WRKSER
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     WRKSER        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLWRK
     C                   END
      *
     C     ZRECNO        DIV       10            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   SELECT
     C     WSNR          WHENEQ    '4'
     C     SNMNU         IFEQ      0
     C     SNVNI         ANDEQ     0
     C                   EXSR      SLETT
     C                   ELSE
     C                   MOVE      'YBC1905'     MSGNR
     C                   SETON                                            30
     C                   GOTO      SLWRK
     C                   END
     C     WSNR          WHENEQ    'M'
     C     SNST          IFEQ      ' '
     C                   EXSR      MOTTA
     C                   END
     C     WSNR          WHENEQ    'N'
     C     SNST          IFEQ      'I'
     C                   EXSR      NMOTTA
     C                   END
     C                   OTHER
     C                   EXSR      NYSER
     C   30              GOTO      SLWRK
     C                   END
      *
     C                   END
      *
     C     SLWRK         TAG
     C                   ADD       5             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C  N30              MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SLETT         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C     SER1K         SETLL     LLSERL01
     C     STSLT1        TAG
     C     SER1K         READE     LLSERL01                               33
     C  N33              IF        SNMNI <> UMN
     C                   UPDATE    LLSERR
     C                   GOTO      STSLT1
     C                   END
     C     SNTYPE        IFEQ      'I'
     C     SNMNU         ANDEQ     0
     C     SNVNU         ANDEQ     0
S01  C*                  DELETE    LLSERR
S01  C*                  MOVE      *BLANKS       WSSN
N01  C  N33              DELETE    LLSERR
N01  C  N33              MOVE      *BLANKS       WSSN
     C                   ELSE
S01  C*                  UPDATE    LLSERR
N01  C  N33              UPDATE    LLSERR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0862'     MSGNR
     C                   END
     C     WSSN          COMP      *BLANKS                            2828
     C     SNST          COMP      ' '                                2929
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOTTA         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
N02   *
N02  C     SER1K         CHAIN     LLSERL01                           33
N02  C                   IF        *IN33
N02  C                   MOVE      ' '           SNST
N02  C                   MOVE      UGRP          SNGRP
N02  C                   MOVE      UPNR          SNPNR
N02  C                   MOVE      UANR          SNANR
N02  C                   MOVE      WSSN          SNSN
N02  C                   MOVE      'I'           SNTYPE
N02  C                   MOVE      *ZEROS        SNMNU
N02  C                   MOVE      *ZEROS        SNVNU
N02  C                   MOVE      UMN           SNMNI
N02  C                   MOVE      *ZEROS        SNVNI
N02  C                   WRITE     LLSERR
N02  C                   END
N02   *
     C     SER1K         SETLL     LLSERL01
     C     STMOT1        TAG
     C     SER1K         READE     LLSERL01                               33
     C  N33              IF        SNMNI <> UMN
     C                   UPDATE    LLSERR
     C                   GOTO      STMOT1
     C                   END
S01  C*                  MOVE      'I'           SNST
S01  C*                  UPDATE    LLSERR
S01  C  N33              MOVE      'I'           SNST
S01  C  N33              UPDATE    LLSERR
     C     WSSN          COMP      *BLANKS                            2828
     C     SNST          COMP      ' '                                2929
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     NMOTTA        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C     SER1K         SETLL     LLSERL01
     C     STNMT1        TAG
     C     SER1K         READE     LLSERL01                               33
     C  N33              IF        SNMNI <> UMN
     C                   UPDATE    LLSERR
     C                   GOTO      STNMT1
     C                   END
S01  C*                  MOVE      ' '           SNST
S01  C*                  UPDATE    LLSERR
S01  C  N33              MOVE      ' '           SNST
S01  C  N33              UPDATE    LLSERR
     C     WSSN          COMP      *BLANKS                            2828
     C     SNST          COMP      ' '                                2929
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYSER         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C     WSSN          IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0859'     MSGNR
     C                   GOTO      SLNYS
     C                   END
      *
     C     SER1K         SETLL     LLSERL01
     C     STNYS1        TAG
     C     SER1K         READE(N)  LLSERL01                               33
     C  N33SNMNU         CABNE     0             STNYS1
     C     *IN33         IFEQ      '0'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0860'     MSGNR
     C                   GOTO      SLNYS
     C                   END
     C                   MOVE      ' '           SNST
     C                   MOVE      UGRP          SNGRP
     C                   MOVE      UPNR          SNPNR
     C                   MOVE      UANR          SNANR
     C                   MOVE      WSSN          SNSN
     C                   MOVE      'I'           SNTYPE
     C                   MOVE      *ZEROS        SNMNU
     C                   MOVE      *ZEROS        SNVNU
     C                   MOVE      UMN           SNMNI
     C                   MOVE      *ZEROS        SNVNI
     C                   WRITE     LLSERR
     C     WSSN          COMP      *BLANKS                            2828
     C     SNST          COMP      ' '                                2929
     C                   UPDATE    SUBFIL
      *
     C     SLNYS         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UGRP              8 0
     C                   PARM                    UPNR              8 0
     C                   PARM                    UANR
     C                   PARM                    ULOK
     C                   PARM                    UMN               7 0
     C                   PARM                    UANT
      *
     C     SER1K         KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    UANR
     C                   KFLD                    WSSN
     C     SER3K         KLIST
     C                   KFLD                    UMN
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
