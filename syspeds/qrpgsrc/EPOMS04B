********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 OPPDATER UTSKRIFT DATO/TID OG STATUS PRINTET PÅ EPOL
********************************************************************
      *****************************************************************
      *** PROGRAM SKAL OPPDATERE POMS-KOLLI-ID                      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26
      *** LEDIGE INDIKATORE0 27-32 33-99
      *****************************************************************
     FFIRM      IF   E           K DISK
     FFIRFB     UF   E           K DISK
S01  F*EPOL      IF   E           K DISK
N01  FEPOL      UF   E           K DISK
     FEPOK      UF A E           K DISK
     FEPOK01    IF   E           K DISK    RENAME(EPOKR:EPOKR01)
     F                                     PREFIX(K1_)
     FEPOKLOG   O  A E             DISK
     D                SDS
     D  UUSR                 254    263
     D                 DS
     D  ELNTKL                 1      9  3
     D  ELNTKL_6               1      6  0
     D                 DS
     D  KOID                   1     18
     D  DSIDPK                 1      1
     D  DSIDLA                 2      3
     D  DSIDLE                 4      8
     D  DSIDNR                 9     18
      *
     C                   EXSR      INIT
      *
     C     EPOLK         CHAIN     EPOL                               33
     C                   IF        *IN33 = *OFF
N01  C                   SELECT
N01  C                   WHEN      ELSTA < 'B' AND ELNTKL > 0
N01  C                   EVAL      ELSTA = 'B'
N01  C                   WHEN      ELSTA = 'B' AND ELNTKL = 0
N01  C                   EVAL      ELSTA = 'A'
N01  C                   ENDSL
N01  C                   UPDATE    EPOLR
      * TELL ANTALL MERKELAPPER
     C     EPOLK         SETLL     EPOK
     C     EPOLK         READE(N)  EPOK                                   33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      XANTM = XANTM + 1
     C     EPOLK         READE(N)  EPOK                                   33
     C                   ENDDO
     C                   END
      *
     C                   EVAL      EGKUND = ELKUND
     C                   EVAL      EGBES  = ELBES
     C                   EVAL      EGLIN  = ELLIN
     C                   EVAL      EGULIN = ELULIN
     C                   EVAL      EGUSR  = UUSR
     C                   MOVE      WDT           EGDTLG
     C                   MOVE      WTM           EGTMLG
     C                   EVAL      XKLIN = 0
     C     EPOLK         SETLL     EPOK
     C     EPOLK         READE     EPOK                                   33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XKLIN
     C                   IF        XKLIN > ELNTKL_6
      * SLETT FOR MANGE MERKELAPPER VED ENDRING AV KOLLI
     C                   DELETE    EPOKR
     C                   EVAL      EGTYPE = 'DLT'
     C                   EXSR      OPDLOG
     C                   ELSE
     C                   EVAL      EKKLIN = XKLIN
     C                   IF        XANTM <> ELNTKL_6
      * VED ENDRING AV ANTALL KOLLI, BLANKE STATUS OG UTSKRIFT DATO
     C                   EVAL      EKST   = *BLANKS
     C                   EVAL      EKDTPR = 0
     C                   END
     C                   UPDATE    EPOKR
     C                   END
     C     EPOLK         READE     EPOK                                   33
     C                   ENDDO
      *
     C                   EVAL      EKKUND = ELKUND
     C                   EVAL      EKBES  = ELBES
     C                   EVAL      EKLIN  = ELLIN
     C                   EVAL      EKULIN = ELULIN
     C                   EVAL      EKST   = *BLANKS
     C                   EVAL      EKUSR  = UUSR
     C                   EVAL      EKDTPR = 0
     C                   EVAL      EKDTIN = 0
     C                   EVAL      EKDTUT = 0
     C                   EVAL      EKBILN = *BLANKS
N01  C                   MOVE      WDT           EKDTPR
     C                   ADD       1             XKLIN
      *
     C     XKLIN         DO        ELNTKL_6
      * LAG NYE MERKELAPP
     C                   EVAL      EKKLIN = XKLIN
     C     X_NESTENR     TAG
     C     FIFIRM        CHAIN     FIRFB                              33
     C                   ADD       1             FIIDNR
     C                   UPDATE    FIRFBR
     C                   MOVE      '3'           DSIDPK
     C                   MOVE      FIIDLA        DSIDLA
     C                   MOVE      FIIDLE        DSIDLE
     C                   MOVEL     FIIDNR        DSIDNR
     C                   MOVE      *ZEROS        UAN22            22
     C                   MOVE      KOID          UAN22
     C                   CALL      'SYGE08'
     C                   PARM                    UAN22
     C                   MOVE      UAN22         KOID
     C                   EVAL      EKMRK     =   KOID
     C     EKMRK         CHAIN     EPOK01                             33
     C  N33              GOTO      X_NESTENR
     C                   WRITE     EPOKR
     C                   EVAL      EGTYPE = 'CRT'
     C                   EXSR      OPDLOG
     C                   ADD       1             XKLIN
     C                   ENDDO
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *=====================================================================******
      *  OPPDATER LOGGEN
      *=====================================================================******
     C     OPDLOG        begsr
      *
     C                   EVAL      EGKLIN  = EKKLIN
     C                   SELECT
     C                   WHEN      EGTYPE = 'SLT'
     C                   EVAL      EG1ST   = EKST
     C                   EVAL      EG1MRK  = EKMRK
     C                   EVAL      EG1DTPR = EKDTPR
     C                   EVAL      EG1USR  = EKUSR
     C                   EVAL      EG1DTIN = EKDTIN
     C                   EVAL      EG1DTUT = EKDTUT
     C                   EVAL      EG1BILN = EKBILN
     C                   EVAL      EG2ST   = *BLANKS
     C                   EVAL      EG2MRK  = *BLANKS
     C                   EVAL      EG2DTPR = *ZEROS
     C                   EVAL      EG2USR  = *BLANKS
     C                   EVAL      EG2DTIN = *ZEROS
     C                   EVAL      EG2DTUT = *ZEROS
     C                   EVAL      EG2BILN = *BLANKS
     C                   WHEN      EGTYPE = 'CRT'
     C                   EVAL      EG1ST   = *BLANKS
     C                   EVAL      EG1MRK  = *BLANKS
     C                   EVAL      EG1DTPR = *ZEROS
     C                   EVAL      EG1USR  = *BLANKS
     C                   EVAL      EG1DTIN = *ZEROS
     C                   EVAL      EG1DTUT = *ZEROS
     C                   EVAL      EG1BILN = *BLANKS
     C                   EVAL      EG2ST   = EKST
     C                   EVAL      EG2MRK  = EKMRK
     C                   EVAL      EG2DTPR = EKDTPR
     C                   EVAL      EG2USR  = EKUSR
     C                   EVAL      EG2DTIN = EKDTIN
     C                   EVAL      EG2DTUT = EKDTUT
     C                   EVAL      EG2BILN = EKBILN
     C                   ENDSL
     C                   WRITE     EPOKLOGR
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
      *
     C     *ENTRY        PLIST
     c                   PARM                    ELKUND
     c                   PARM                    ELBES
     c                   PARM                    ELLIN
     c                   PARM                    ELULIN
      *
     C     EPOLK         klist
     C                   kfld                    ELKUND
     C                   kfld                    ELBES
     C                   kfld                    ELLIN
     C                   kfld                    ELULIN
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C     *LIKE         DEFINE    EKKLIN        XKLIN
     C     *LIKE         DEFINE    EKKLIN        XANTM
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   endsr
      *
