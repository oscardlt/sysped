     A*%%TS  SD  20131108  144849  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX88PNL)
     A                                      HLPTITLE('ARBEIDE MED TURER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20131108  144849  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  6  3
     A  98                                  DSPATR(RI)
     A                                      VALUES(' ' 'G' '5' 'T' '6')
     A            AVREGN         1A  O  6  6
     A            TUAVD          4Y 0O  6  8EDTCDE(4)
     A            TUPRO          8Y 0O  6 13EDTCDE(4)
     A            WSINNT         7Y 0O  6 23EDTCDE(4)
     A            WSKOST         7Y 0O  6 31EDTCDE(4)
     A            WSRESU         7Y 0O  6 39EDTCDE(4)
     A  25                                  COLOR(RED)
     A            WSINNI         7Y 0O  6 47EDTCDE(4)
     A            WSKOSI         7Y 0O  6 55EDTCDE(4)
     A            WSINNU         7Y 0O  6 63EDTCDE(4)
     A            WSKOSU         7Y 0O  6 71EDTCDE(4)
     A            WSREST         7Y 0O  6 79EDTCDE(4)
     A  26                                  COLOR(RED)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20131108  144849  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYKS96'
     A                                      COLOR(BLU)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  27                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(040 SHU1420 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(130 SHU1421 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N32                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØKE TUR')
     A                                      CF12(12 'AVSLUT')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A N32                              4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                              4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 SHU0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SØKTUR
     A*%%TS  SD  20131108  144849  SVEIN       REL-V7R1M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'FORRIGE')
     A                                      CF14(14 'legge inn tillatelser')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            WSAVD          4Y 0B  1  2DSPATR(UL)
     A                                      DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPRO          8Y 0B  1  8DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSSTAT         1A  B  2  2
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  5MSGCON(015 SHU1422 *LIBL/SYMSGF)
     A          R NYWRK
     A N32                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A  27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØKE TUR')
     A  32                                  CF12(12 'TIL SYEX10')
     A                                  1  2'SYSPED/SYKS91'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  27                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 61TIME
     A                                      COLOR(BLU)
     A N32                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                             23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 SHU0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  5  2MSGCON(077 SHU0567 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 19MSGCON(040 SHU0659 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
