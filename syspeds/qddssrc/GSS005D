     A*%%TS  SD  20140519  215443  TROND       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10004A* 01 YBC PTF10 LEVERANDØRSNR ER ØKT TIL 5.0
     A*              INGEN MERKING
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R GSS005DA                  SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '2' '4')
     A            SFSNO          5Y 0O  5  5EDTCDE(4)
     A            SFNM          30A  H
     A            SFNM28        28A  O  5 11
     A            SFAD1         30A  H
     A            SFAD2         30A  H
     A            SFAD3         30A  H
     A            SFCNO          8Y 0O  5 40EDTCDE(4)
     A            SFTEL         15A  O  5 49
     A            SFFAX         15A  O  5 65
      *
     A          R GSS005DB                  SFLCTL(GSS005DA)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS005D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A N27                              1 21'Work with suppliers'
     A                                      DSPATR(HI)
     A  27                              1 21'Inquiry suppliers'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Choose option, press ENTER:'
     A                                      COLOR(BLU)
     A N27                              3 32'2=Change   4=Delete'
     A                                      COLOR(BLU)
     A  27                              3 32'1=Pick 2=change'
     A                                      COLOR(BLU)
     A                                  4  2'Op SupNo Name of supplier         -
     A                                          ClientNo Telephone       Telefa-
     A                                      x'
     A                                      DSPATR(HI)
      *
     A          R GSS005DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N27                                  CF03(03 'End')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'New entry')
     A                                      CF08(08 'Search')
     A  27                                  CF12(12 'Previous')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              1  2'F3=End   F5=Refresh   F6=Create -
     A                                        F8=Search'
     A                                      COLOR(BLU)
     A  27                              1  2'F5=Refresh   F6=Create   F8=Search-
     A                                       F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS005DD
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Search')
     A                                      CF12(12 'Previous')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SupNo Name of supplier'
     A                                      DSPATR(HI)
     A            WSSNO          5Y 0B  2  3DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNM          30A  B  2  9DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSAD1         30A  B  2 40DSPATR(UL)
     A            WSAD2         30A  B  3  9DSPATR(UL)
     A            WSAD3         30A  B  3 40DSPATR(UL)
     A                                  4  5'ClientNo'
     A            WSCNO          8Y 0B  4 14DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  4 24'Telephone:'
     A            WSTEL         15A  B  4 35DSPATR(UL)
     A                                  4 52'Telefax:'
     A            WSFAX         15A  B  4 61DSPATR(UL)
     A                                  5  2'F4=Search  F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS005DE
     A                                      CF12(12 'Previous')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  3  2'Search from --> '
     A            W2NM          30A  I  3 19DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R GSS005DF
     A                                      CF12(12 'Previous')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  1  3'SupNo Name of supplier'
     A                                      DSPATR(HI)
     A            SFSNO          5Y 0O  2  3DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            SFNM          30A  O  2  9DSPATR(UL)
     A                                      DSPATR(HI)
     A            SFAD1         30A  O  2 40DSPATR(UL)
     A                                      DSPATR(HI)
     A            SFAD2         30A  O  3  9DSPATR(UL)
     A                                      DSPATR(HI)
     A            SFAD3         30A  O  3 40DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4  5'ClientNo'
     A            SFCNO          8Y 0O  4 14DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  4 24'Telephone:'
     A            SFTEL         15A  O  4 35DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 52'Telefax:'
     A            SFFAX         15A  O  4 61DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R GSS005DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A N27                                  CF03(03 'End')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'New entry')
     A                                      CF08(08 'Search')
     A  27                                  CF12(12 'Previous')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS005D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A N27                              1 21'Work with suppliers'
     A                                      DSPATR(HI)
     A  27                              1 21'Inquiry suppliers'
     A                                      DSPATR(HI)
     A                                  3  2'Choose option, press ENTER:'
     A                                      COLOR(BLU)
     A N27                              3 32'2=Change   4=Delete'
     A                                      COLOR(BLU)
     A  27                              3 32'1=Pick up   2=Change'
     A                                      COLOR(BLU)
     A                                  4  2'Op SupNo Name of supplier         -
     A                                          ClientNo Telephone       Telefa-
     A                                      x'
     A                                      DSPATR(HI)
     A                                  6  6'No supplier found'
     A                                      COLOR(RED)
     A N27                             23  2'F3=End   F5=Refresh   F6=Creste -
     A                                        F8=Search'
     A                                      COLOR(BLU)
     A  27                             23  2'F5=Refresh   F6=Create   F8=Search-
     A                                      F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
