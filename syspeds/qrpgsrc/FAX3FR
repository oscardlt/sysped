      ************************************************************
      *** PROGRAM SKAL LESE VALGBEGREPENE FOR Å SKRIVE         ***
      ***                   MANIFEST                           ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04-26                       ***
      *** LEDIGE INDIKATORER 27-89 91-95                       ***
      ************************************************************
     FCUNDC     IF   E             DISK
     FFAXLK     IF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FKUNDL01   UF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR2)
     FFAXTELL   UF A E           K DISK
     FSAMLB     IF   E           K DISK
     FDOKEM     IF   E           K DISK
     FDOKUM     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTW     IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FFAX3FRFM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  USØKKN                 1     30
     D  USØKKO                 1      2
     D  URRN                   3     12
     D  epofax                13     13
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UPRO                  22     29  0
     D  USD                   36     38
     D  ULNR                  39     41  0
     D  UFAK                  42     42
     D  UHOD                  47     47
     D  UBBS                  48     48
     D*
     D* XBLA1/XBLA2 ER IKKE I BRUK. HVIS TAR NOEN POSISJONER TIL BRUK
     D* HUSK Å SYNKE STØRRELSE PÅ XBLA1/XBL2
     D  XBLA1                 70    256
     D  XBLA2                257    510
     D  UPRT18               681    690
     D  UPT18                691    700
     D  UPRT73               961    970
     D  UPT73                971    980
     D                 DS
     D  F1                     1     21
     D                                     DIM(21)
     D  FAXNRP                 1     21
     D                 DS
     D  F2                     1     15
     D                                     DIM(15)
     D  FAXNR                  1     15
     D  FAX05A                 1      4
     D                 DS
     D  F3                     1      6
     D                                     DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyz')
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     I*
     C*
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     SAMLBK        KLIST
     C                   KFLD                    WSSD
     C                   KFLD                    WSLNR
     C     DOKEMK        KLIST
     C                   KFLD                    YRI
     C                   KFLD                    WSSD
     C                   KFLD                    WSLNR
     C     DOKUMK        KLIST
     C                   KFLD                    WSSD
     C                   KFLD                    WSLNR
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPLNR
     C     KODTVK        KLIST
     C                   KFLD                    WSVUNI
     C                   KFLD                    WSAVD
     C     KODTWK        KLIST
     C                   KFLD                    WSWUNI
     C                   KFLD                    WSAVD
     C     KODSFK        KLIST
     C                   KFLD                    WSFUNI
     C                   KFLD                    WSSG
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UJBKØ             1
     C                   PARM                    UIN01             1
     C                   PARM                    UIN07             1
     C                   PARM                    UIN08             1
     C                   PARM                    UFAKT             1
     C                   PARM                    FAXNRU           21
     C                   PARM                    AAAVD             4
     C                   PARM                    AATUR             8
     C                   PARM                    KNAVN            30
     C                   PARM                    PDATE
     C                   PARM                    PTIME
     C                   PARM                    WSEDIT            1
     C                   PARM                    UUDOK             8
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    COVP              1
     C                   PARM                    EPOST
     C                   PARM                    EPOSTJ            1
     C     UIN07         COMP      '0'                                9090
     C  N90UIN08         COMP      '0'                                9090
     C                   MOVE      '0'           UIN01
     C                   MOVE      'N'           WSEDIT
     C                   MOVE      'J'           UJBKØ
     C                   MOVE      'J'           WSJBKØ            1
     C*
     C                   MOVE      'M'           YRI               1
     C                   MOVE      'P'           WSPUNI            1
     C                   MOVE      'V'           WSVUNI            1
     C                   MOVE      'W'           WSWUNI            1
     C                   MOVE      'SF'          WSFUNI            2
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      USG           WSSG
     C* BLANKE DE FELTENE SOM IKKE ER I BRUK
     C                   MOVE      *BLANKS       XBLA1
     C                   MOVE      *BLANKS       XBLA2
     C*
     C                   READ      FIRM                                   99
     C                   MOVE      FIFIRM        UFIRM
     C*
     C     START         TAG
     C                   EXFMT     BILDE
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   SETOFF                                       309998
     C                   SETOFF                                       309796
     C*
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C* 04 0 SØK KUND
     C   04              DO
     C     LINNBR        IFeq      9
     C     LO:UP         XLATE     EPOST         usok
     C                   CALL      'VCUNDC2R'
     C                   PARM                    FIFIRM
     C                   PARM                    USOK             30
     C                   PARM                    URRN2            10 0
     c                   parm                    epofax            1
     C     URRN2         IFNE      *ZEROS
     C                   MOVE      URRN2         URRN
     C                   MOVE      'KP'          USØKKO
     c                   else
     c                   movel     usok          epost
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    KUNDNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     c                   end
     C* RETUR MED KONTAKTPERS
     C     USØKKO        IFEQ      'KP'
     C                   MOVE      URRN          RRN              10 0
     C     RRN           CHAIN     CUNDC                              31
     c     epofax        ifne      'E'
     C  N31              MOVEL     CFAX          FAXNR
     c                   else
     c                   move      *blanks       faxnr
     c                   end
     c     epofax        ifne      'F'
     C  N31              MOVEL     CEMAIL        EPOST
     c                   else
     c                   move      *blank        epost
     c                   end
     C  N31              MOVE      CCOMPN        KUNDNR
     C                   END
     C                   END
     C*
     C     KODTVK        CHAIN     KODTV                              99
     C  N99KODTWK        CHAIN     KODTW                              99
     C  N99              Z-ADD     18            WSPLNR            3 0
     C  N99KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT18
     C  N99              MOVE      KOPTY         UPT18
     C  N99              Z-ADD     73            WSPLNR            3 0
     C  N99KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT73
     C  N99              MOVE      KOPTY         UPT73
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START
     C     KODSFK        CHAIN     KODTSF                             98
     C   98              MOVE      'SPS0001'     MSGNR
     C   98              SETON                                        30
     C   98              GOTO      START
     C     SAMLBK        CHAIN     SAMLB                              97
     C     KUNDNR        IFEQ      *ZEROS
     C     KNAVN         ANDEQ     *BLANKS
     C  N97              MOVE      SMKNS         KUNDNR
     C                   END
      *
     C  N97              DO
     C     DOKEMK        CHAIN     DOKEM                              97
     C   97DOKUMK        CHAIN     DOKUM                              97
     C                   END
     C   97              MOVE      'SPS0035'     MSGNR
     C   97              SETON                                        30
     C   97              GOTO      START
     C     KUNKEY        CHAIN(N)  CUNDL01                            33
     C*
     C* HENTER LANDKODE
     C*
     C  N33WSLAND        IFEQ      *BLANKS
     C                   MOVE      SYLAND        WSLAND
     C                   END
     C*
     C     FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     C                   GOTO      START
     C                   END
      * IKKE FAX ELLER EPOST-ADRESSE LAGT INN
     C     FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   SETON                                        30
     C   30              MOVE      'SHU0165'     MSGNR
     C   30              GOTO      START
     C                   END
      *
      * BÅDE FAX OG EPOST-ADRESSE LAGT INN
     C     FAXNR         IFNE      *BLANKS
     C     EPOST         ANDNE     *BLANKS
     C                   SETON                                        30
     C   30              MOVE      'SHU0538'     MSGNR
     C   30              GOTO      START
     C                   END
     C*
     C  N10              GOTO      START
     C*
     C* BEHANDLING VED COVERPAGE
     C*
     C     EPOST         IFne      *BLANKS
     c                   move      'J'           wscvrp
     c                   end
     C     WSCVRP        IFEQ      'J'
     C     WSCVRP        OREQ      'Y'
     C                   MOVE      'J'           COVP
     C*FAX COVERPAGE
     C     EPOST         IFEQ      *BLANKS
     C                   MOVEL     FIFT          COV05
     C                   MOVE      *BLANKS       COV01
     C                   MOVE      *BLANKS       COV03
     C                   MOVE      *BLANKS       COV04
     C                   MOVE      *BLANKS       COV07
     C                   MOVE      *BLANKS       COV08
     C                   CALL      'SYGE12CL'
     C                   PARM                    USE              10
     C                   PARM                    NVN              50
     C                   MOVEL     NVN           COV06
     C                   MOVEL     KNAVN         COV02
     C                   MOVEL     ADR1          COV03
     C                   CALL      'FAXCVP'
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C*EPOST COVERPAGE
     C                   ELSE
     C                   CALL      'EPOCVP'
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C                   END
     C     FUNK          IFEQ      'A'
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C* OPPDATERER KUNDEREGISTER
     C*
     C     KUNDNR        IFNE      *ZEROS
     C     FAXNR         IFNE      *BLANKS
     C     TFAXNR        IFEQ      *BLANKS
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD
     C     KUNKEY        CHAIN     KUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD2
     C                   END
     C                   END
     C                   END
     C*
     C     WSLAND        IFNE      *BLANKS
     C     WSLAND        CHAIN     FAXLK                              33
     C     FAX05A        IFEQ      FAX05B
     C                   MOVE      *BLANKS       FAXRET
     C                   END
     C                   END
     C*
     C                   MOVE      WSAVD         UAVD
     C                   MOVE      WSSG          USG
     C                   MOVE      *ZEROS        UPRO
     C                   MOVE      WSSD          USD
     C                   MOVE      WSLNR         ULNR
     C                   MOVE      'N'           UFAK
     C                   MOVE      'N'           UFAKT
     C                   MOVE      'N'           UJBKØ
     C                   MOVE      KOWHOD        UHOD
     C                   MOVE      KOWBBS        UBBS
     C     UPRT18        IFEQ      *BLANKS
     C                   MOVEL     'QPRINT '     UPRT18
     C                   END
     C     UPT18         IFEQ      *BLANKS
     C                   MOVEL     '*STD'        UPT18
     C                   END
     C     UPRT73        IFEQ      *BLANKS
     C                   MOVEL     'QPRINT '     UPRT73
     C                   END
     C     UPT73         IFEQ      *BLANKS
     C                   MOVEL     '*STD'        UPT73
     C                   END
     C*
     C     SLUTT         TAG
     C                   MOVE      WSAVD         AAAVD
     C                   MOVE      SMPRO         AATUR
     C*
     C* ORDNER DATO
     C*
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        IFNE      *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVEL     *BLANKS       PDATE
     C                   MOVEL     WSTIME        PTIME
     C                   MOVEL     WSDATE        PDATE
     C                   END
     C                   END
     C*
     C* HENTER DOKUMENTNUMMER HVIS WSEDIT=J
     C*
     C     WSEDIT        IFEQ      'J'
     C*
     C     FIFIRM        CHAIN     FAXTELL                            33
     C   33              Z-ADD     1             COUNTN
     C   33              WRITE     FAXTELLR
     C  N33              ADD       1             COUNTN
     C  N33              UPDATE    FAXTELLR
     C* CREERER DOKUMENT
     C                   MOVEL     'F'           UUDOK             8
     C                   MOVE      COUNTN        UUDOK
     C                   END
     C*
     C*** FIXER FAXNR
     C*
     C     FAXRET        IFNE      *BLANKS
     C                   EXSR      FAXFIX
     C                   ELSE
     C                   MOVEL     FAXNR         FAXNRP
     C                   END
     C*
     C                   MOVE      FAXNRP        FAXNRU
     C                   MOVE      'N'           EPOSTJ            1
     C     EPOST         IFNE      *BLANKS
     C                   MOVE      'J'           EPOSTJ
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   Z-ADD     1             A1                2 0
     C                   Z-ADD     1             A2                2 0
     C                   Z-ADD     1             A3                2 0
     C                   DO        6
     C     F3(A3)        IFNE      *BLANKS
     C                   MOVEL     F3(A3)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A3
     C                   END
     C                   DO        15
     C     F2(A2)        IFNE      *BLANKS
     C                   MOVEL     F2(A2)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A2
     C                   END
     C                   ENDSR
     C*
     OKUNDR     E            UPD
     O                       TFAXNR
     OKUNDR2    E            UPD2
     O                       TFAXNR
