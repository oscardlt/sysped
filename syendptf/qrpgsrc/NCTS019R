********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12322 * 01 OR  PTF12 Løpenr fra HEADF legges ut to ganger, rettet
12322 * 01 OR  PTF12 til ekspedisjonssted + løpenr
12342 * 03 CB  PTF12 Nye pgm i PT for å plukke exportdeklarsjoner
********************************************************************
      *
      ************************************************************
      *** PROGRAM SKAL PLUKKE OPPDRAG TIL TRANSITTERING        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04 06 07 09-11 13-24        ***
      *** LEDIGE INDIKATORER 29 32-89 95-97                    ***
      ************************************************************
      *
     FSYPTFF    IF   E           K DISK
     FNCTSEC    O    E             DISK
     FNCTSECAM  O    E             DISK
     FNCTSEI    O    E             DISK
     FTRUHL     UF A E           K DISK
     FTURER14   IF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD14    IF   E           K DISK    RENAME(HEADFR:HEADFR14)
     FSAEH      IF   E           K DISK
     FSAEV      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHESTED    IF   E           K DISK
N03  FFRISOK    IF   E           K DISK
     FNCTS019D  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSID                 197    206
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HESV1                  1     25
     D  WSSV1                  1     10
     D                 DS
     D  TUDTA                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D  TUDT                   1      8  0
     D                 DS
     D  XUDTD                  1      2  0
     D  XUDTM                  3      4  0
     D  XUDTA                  5      6  0
     D  XUDT                   1      6  0
     D                 DS
     D  W1TYPE                 1      2
     D  W1TYP2                 2      2
     D                 DS
     D  HENAS                  1     30
     D  WSNAS                  1     19
     D                 DS
     D  HENAK                  1     30
     D  WSNAK                  1     21
     D                 DS
     D  HEVS1                  1     25
     D  WSVS1                  1      8
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
      *
     C                   EXSR      INIT
     C     XFRAOPD       CABEQ     'J'           START
      *
      ***  VIS BILDE FOR LISTETYPE
     C     CHOISE        TAG
     C                   EXFMT     BILDE01
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
      * SØK ETTER KUNDENR
     C   04              EXSR      SOKDTA
      *
     C                   EXSR      TESTB1
     C   30              GOTO      CHOISE
      *
     C     W1KNFA        COMP      *ZEROS                             8989
      *
      ***  KOMMER HIT KUN VED F5 - FRISK OPP = HENT FRA TUR IGJEN
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UPRO          SETLL     HEAD14
     C     UPRO          READE     HEAD14                                 94
      *
     C     *IN94         DOWEQ     '0'                                          1<-B
     C                   SETOFF                                       908586
     C     HEADFK        CHAIN(N)  TRUHL                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      TLTXT         WSTXT
     C                   MOVE      TLTLKU        WSTLKU
     C                   MOVE      'J'           WSMED
     C                   ELSE
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      HETLKU        WSTLKU
     C                   MOVE      ' '           WSMED
     C                   END
     C     WSTXT         IFEQ      *BLANKS
     C                   MOVE      ' '           WSFT
     C                   ELSE
     C                   MOVE      'J'           WSFT
     C                   END
     C                   MOVE      *BLANKS       WSNAFA
     C                   MOVE      HETLE         WSTLE
     C                   MOVE      HETLL         WSTLL
     C     HEATRK        CHAIN     HESTEDR                            31
     C     *IN31         IFEQ      '0'                                           2<-B
     C                   MOVEL     HSFROM        WSSDF
     C                   MOVEL     HSTO          WSSDT
     C                   ELSE                                                    2
     C                   SELECT
     C                   WHEN      HEADS3 <> *BLANKS
     C                   MOVEL     HEADS3        WSSDF
     C                   WHEN      HEADS2 <> *BLANKS
     C                   MOVEL     HEADS2        WSSDF
     C                   OTHER
     C                   MOVEL     HEADS1        WSSDF
     C                   ENDSL
     C                   SELECT
     C                   WHEN      HEADK3 <> *BLANKS
     C                   MOVEL     HEADK3        WSSDF
     C                   WHEN      HEADK2 <> *BLANKS
     C                   MOVEL     HEADK2        WSSDF
     C                   OTHER
     C                   MOVEL     HEADK1        WSSDF
     C                   ENDSL
     C                   END                                                     2<-E
      * HAR DENNE POSIJON STATUS T-DOK UTSKREVET ?? UTELATES OM IKKE Y/J I W1ALL
     C     W1ALL         IFEQ      ' '                                           2<-B
     C     W1ALL         OREQ      'N'
     C     TRTSTA        IFEQ      'A'                                            3<-B
     C     TRTSTA        OREQ      'B'
     C     TRTSTA        OREQ      'C'
     C     TRTSTA        OREQ      'D'
     C                   GOTO      ST21
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C     W1TYP2        IFEQ      ' '                                           2<-B
     C     TRTSTA        COMP      '3'                                    85    *WSTSTA WHITE
     C  N85TRTSTA        COMP      '4'                                    85
     C                   ELSE                                                    2
     C     W1TYP2        COMP      TRTSTA                                 85
     C                   END                                                     2<-E
     C   89W1KNFA        COMP      HEKNA                                  86    *WSNAFA WHITE
     C     HEKNA         IFNE      *ZEROS                                        2<-B
     C                   MOVE      HEKNA         YKN
     C     KUND1K        CHAIN     CUNDL01                            33
     C  N33              MOVEL     KNAVN         WSNAFA
     C   33              MOVE      *BLANKS       WSNAFA
     C                   END                                                     2<-E
     C     TRTSTA        IFEQ      W1TYP2                                        2<-B
     C     W1KNFA        IFEQ      *ZEROS                                         3<-B
     C     W1KNFA        ORNE      *ZEROS
     C     W1KNFA        ANDEQ     HEKNA
     C   28              MOVE      'J'           WSMED
     C                   SETON                                        90
     C                   ADD       1             XANTO
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * POSTEN SKAL MED - ØK TELLER SUBFILREC.NR
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C     UPRO          READE     HEAD14                                 94
     C  N94              ENDDO                                                  1<-E
      *
     C                   MOVE      ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
      * STARTPUNKT FOR NORMAL SYKLUS - RULLING/HENT OPTION....
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            5 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO
     C                   END
      ***
      *
     C                   SETOFF                                       9830
      *** FRISK OPP
     C   05              GOTO      START
      *** UTFØR PLUKK
     C   07              DO
     C     ULSTL         IFEQ      0
     C                   EXSR      UTFORP
     C                   ELSE
     C                   EXSR      UTFOR2
     C                   END
     C                   GOTO      SLUTT
     C                   END
      *** FORRIGE
     C   12XFRAOPD       CABEQ     'J'           SLUTT
     C   12              GOTO      CHOISE
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      VLHENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     UTFORP        BEGSR
      ***********************************************
     C                   Z-ADD     0             XLIT              5 0
     C                   MOVE      'J'           XCRTHOUSE         1
      *
     C     1             DO        YRECNO        ZRECNO                         1<-B
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     XANTO         IFEQ      0                                             2<-B
     C     WSMED         OREQ      'J'
     C                   IF        XCRTHOUSE = 'J'                                3<-B
     C                   CLEAR                   NCTSECR
     C                   CLEAR                   NCTSEIR
     C                   END                                                      3<-E
     C                   CLEAR                   SAEVR
     C                   MOVE      UAVD          TCAVD
     C                   MOVE      UTDN          TCTDN
     C     HEATRK        CHAIN     HEADF                              34
      * OPPDATER STATUS FOR Å VISE AT LISTE ER SKREVET
     C  N34W1TYPE        IFEQ      'T1'
     C                   MOVE      'A'           TRTSTA
     C                   ELSE
     C                   MOVE      'B'           TRTSTA
     C                   END
     C  N34              UPDATE    HEADFR
     C                   IF        XIDAG >= X_FASE5                               3<-B
     C                   MOVE      HEAVD         TCAVD2
     C                   MOVE      HEOPD         TCTDN2
     C                   END                                                      3<-E
     C     HEADFK        CHAIN     SAEH                               34
     C     *IN34         IFEQ      '1'                                            3<-B
      * IKKE FINNE SAD-EKSPORT, BRUK OPPDRAGSOPPLYSNINGER
     C                   ADD       1             ULI
     C                   MOVE      ULI           TCLI
     C                   Z-ADD     HEVKT         TCVKTB
     C                   MOVE      *BLANKS       TCPDTY
     C                   MOVE      *BLANKS       TCPDRF
     C                   IF        XIDAG >= X_FASE5 AND HETLL <> *BLANKS           4<-B
S03  C*                  EVAL      TCPDTY = 'N830'
S01  C*                  EVAL      TCPDRF = %TRIM(HETLL) + ' ' + %TRIM(HETLL)
S03  C*                  EVAL      TCPDRF = %TRIM(HETLE) + ' ' + %TRIM(HETLL)
N03  C     FRISOKK       CHAIN     FRISOK
N03  C                   IF        %FOUND
N03  C                   MOVEL     FSSOK         TCUCR
N03  C                   MOVEL     FSSOK         TCXEXT
N03  C                   ENDIF
     C                   END                                                       4<-E
      *
     C                   IF        XIDAG >= X_FASE5                                4<-B
      * IN-TP KUN EN HOUSE
     C                   EXSR      HADR1
     C                   END                                                       4<-E
     C                   IF        XCRTHOUSE = 'J'                                 4<-B
      * ENTEN FØRST HOUSE ELLER POST-TP
     C                   WRITE     NCTSECR
     C                   Z-ADD     0             TILI2
     C                   END                                                       4<-E
      *
     C                   IF        XIDAG < X_FASE5                                 4<-B
      * IN-TP -> IKKE LAGE FLERE HOUSE
     C                   MOVE      'N'           XCRTHOUSE         1
     C                   ELSE                                                      4
     C                   Z-ADD     0             TILI2
     C                   END                                                       4<-E
      *
     C                   MOVE      UAVD          TIAVD
     C                   MOVE      UTDN          TITDN
     C                   Z-ADD     TCLI          TILI
     C                   ADD       1             TILI2
     C                   ADD       1             XLIT
     C                   Z-ADD     XLIT          TILIT
     C                   Z-ADD     0             TICCH
     C                   MOVE      *BLANKS       TICCCD
     C                   MOVE      *BLANKS       TICTX
     C                   Z-ADD     HEVKT         TIVKB
     C                   EVAL      TIVKN = HEVKT * 0,9
     C                   EVAL      TIPTY = 'PK'
     C                   Z-ADD     HENT          TIPNT
     C                   EVAL      TIPMRK = HEGM1
     C                   IF        XIDAG < X_FASE5 AND HETLL <> *BLANKS             5<-B
S03  C*                  EVAL      TIPDTY = 'N830'
S01  C*                  EVAL      TIPDRF = %TRIM(HETLL) + ' ' + %TRIM(HETLL)
S03  C*                  EVAL      TIPDRF = %TRIM(HETLE) + ' ' + %TRIM(HETLL)
     C                   END                                                        5<-E
     C                   WRITE     NCTSEIR
      *
     C                   ELSE                                                     3
      *
     C                   IF        XCRTHOUSE = 'J'                                 4<-B
     C                   ADD       1             ULI
     C                   MOVE      ULI           TCLI
     C                   END                                                       4<-E
     C                   IF        XIDAG >= X_FASE5                                4<-B
     C                   MOVE      SELKA         TCALK
     C                   MOVE      SELKB         TCBLK
     C                   END                                                       4<-E
     C                   EVAL      TCVKTB = SEVKB
     C                   MOVE      *BLANKS       TCPDTY
     C                   MOVE      *BLANKS       TCPDRF
     C     SETLL         IFNE      *BLANKS                                         4<-B
     C     XIDAG         ANDGE     X_FASE5
S03  C*                  EVAL      TCPDTY = 'N830'
S03  C*                  EVAL      TCPDRF = %TRIM(SETLL) + ' ' + %TRIM(SETLL)
N03  C     FRISOKK       CHAIN     FRISOK
N03  C                   IF        %FOUND
N03  C                   MOVEL     FSSOK         TCUCR
N03  C                   MOVEL     FSSOK         TCXEXT
N03  C                   ENDIF
     C                   END                                                       4<-E
      *
     C                   IF        XIDAG >= X_FASE5                                4<-B
     C                   EXSR      HADR2
     C                   END                                                       4<-E
     C                   IF        XCRTHOUSE = 'J'                                 4<-B
     C                   WRITE     NCTSECR
     C                   Z-ADD     0             TILI2
     C                   END                                                       4<-E
     C                   IF        XIDAG < X_FASE5                                 4<-B
      * IN-TP -> IKKE LAGE FLERE HOUSE
     C                   MOVE      'N'           XCRTHOUSE         1
     C                   ELSE                                                      4
     C                   Z-ADD     0             TILI2
     C                   END                                                       4<-E
      *
     C     HEADFK        SETLL     SAEV
     C     HEADFK        READE     SAEV                                   34
     C                   MOVE      *OFF          *IN33
     C  N34              DOW       *IN33 = *OFF                                    4<-B
     C     HEADFK        READE     SAEV                                   33
     C  N33SVVKTB        IFNE      0                                                5<-B
     C                   LEAVE
     C                   END                                                        5<-E
     C                   ENDDO                                                     4<-E
     C                   IF        XIDAG >= X_FASE5                                4<-B
     C                   MOVE      *ZEROS        TILI2
     C                   END                                                       4<-E
      *
      * IKKE FINN SAD-EKSPORT (*IN34 PÅ) ELLER FLERE VARELINJER (*IN33 AV)
     C                   IF        (*IN34 OR (*IN33 = *OFF))                       4<-B
     C   34              CLEAR                   SAEVR
     C                   MOVE      UAVD          TIAVD
     C                   MOVE      UTDN          TITDN
     C                   Z-ADD     TCLI          TILI
     C                   ADD       1             TILI2
     C                   ADD       1             XLIT
     C                   Z-ADD     XLIT          TILIT
     C                   Z-ADD     0             TICCH
     C                   MOVE      *BLANKS       TICCCD
     C                   MOVE      *BLANKS       TICTX
     C                   Z-ADD     SEVKB         TIVKB
     C                   EVAL      TIVKN = SEVKB * 0,9
     C                   IF        (*IN34 OR (SVFT01 = *BLANKS))                    5<-B
     C                   EVAL      TIPMRK = HEGM1
     C                   ELSE                                                       5
     C                   EVAL      TIPMRK = SVFT01
     C                   END                                                        5<-E
     C                   EVAL      TIPTY  = 'PK'
     C                   MOVE      SENTK         TIPNT
     C                   IF        XIDAG < X_FASE5                                  5<-B
S03  C*                  EVAL      TIPDTY = 'N830'
S03  C*                  EVAL      TIPDRF = %TRIM(SETLL) + ' ' + %TRIM(SETLL)
N03  C                   EVAL      TISDTY = 'NZZZ'
N03  C                   EVAL      TISDRF = %TRIM(SETLE) + ' ' + %TRIM(SETLL)
     C                   END                                                        5<-E
     C                   WRITE     NCTSEIR
     C                   ELSE                                                      4
      * FINN KUN EN SAD-EKSPORT LINJE
     C     HEADFK        SETLL     SAEV
     C     HEADFK        READE     SAEV                                   34
     C                   Z-ADD     TCLI          TILI
     C                   ADD       1             TILI2
     C                   ADD       1             XLIT
     C                   Z-ADD     XLIT          TILIT
     C                   MOVEL     SVVNT         TICCH
     C                   MOVE      *BLANKS       TICCCD
     C                   Z-ADD     SVVKTB        TIVKB
     C                   Z-ADD     SVVKTN        TIVKN
     C                   MOVEL     SVFT01        TIPMRK
     C                   MOVE      SVNT01        TIPNT
     C                   MOVEL     SVEH01        TIPTY
     C                   MOVEL     SVVT01        TICTX
     C                   IF        XIDAG < X_FASE5                                  5<-B
S03  C*                  EVAL      TIPDTY = 'N830'
S03  C*                  EVAL      TIPDRF = %TRIM(SETLE) + ' ' + %TRIM(SETLL)
N03  C                   EVAL      TISDTY = 'NZZZ'
N03  C                   EVAL      TISDRF = %TRIM(SETLE) + ' ' + %TRIM(SETLL)
     C                   END                                                        5<-E
     C                   WRITE     NCTSEIR
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C                   MOVE      *ZEROS        UFLAGG
      *
     C                   ENDSR
      *
      ***********************************************
     C     HADR1         BEGSR
      ***********************************************
     C                   MOVE      HEKNS         TCKNS
     C                   MOVE      HENAS         TCNAS
     C                   MOVE      HEADS1        TCAD1S
     C                   MOVEL     HEADS3        TCPNS
     C                   MOVEL     HEADS3        TCPSS
     C     HEUR          IFEQ      'B'                                          1<-B
     C     HEUR          OREQ      'D'
     C     HEUR          OREQ      'G'
     C                   MOVE      'NO'          TCLKS
     C                   ELSE                                                   1
     C                   MOVE      HELKA         TCLKS
     C                   END                                                    1<-E
     C     HEKNS         IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TCTINS
     C                   ELSE                                                   1
     C                   MOVE      HEKNS         YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C     SYFOR9        IFEQ      *ZEROS                                        2<-B
     C                   MOVE      *BLANKS       TCTINS
     C                   ELSE                                                    2
     C                   MOVEL     SYFORE        TCTINS
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C                   MOVE      HEKNK         TCKNK
     C                   MOVE      HENAK         TCNAK
     C                   MOVE      HEADK1        TCAD1K
     C                   MOVEL     HEADK3        TCPNK
     C                   MOVEL     HEADK3        TCPSK
     C     HEUR          IFEQ      'A'                                          1<-B
     C     HEUR          OREQ      'C'
     C     HEUR          OREQ      'F'
     C                   MOVEL     'NO'          TCLKK
     C                   ELSE                                                   1
     C                   MOVEL     HELKA         TCLKK
     C                   END                                                    1<-E
     C     HEKNK         IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TCTINK
     C                   ELSE                                                   1
     C                   MOVE      HEKNK         YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C     SYFOR9        IFEQ      *ZEROS                                        2<-B
     C                   MOVE      *BLANKS       TCTINK
     C                   ELSE                                                    2
     C                   MOVEL     SYFORE        TCTINK
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C     TCAD1S        IFEQ      *BLANKS                                      1<-B
     C                   MOVEL     '.'           TCAD1S
     C                   END                                                    1<-E
     C     TCAD1K        IFEQ      *BLANKS                                      1<-B
     C                   MOVEL     '.'           TCAD1K
     C                   END                                                    1<-E
      *
     C                   WRITE     NCTSECAMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     HADR2         BEGSR
      ***********************************************
     C                   MOVE      SEKNK         TCKNS
     C                   MOVE      SENAK         TCNAS
     C                   MOVE      SEADK1        TCAD1S
     C                   MOVEL     SEADK3        TCPNS
     C                   MOVEL     SEADK3        TCPSS
     C                   MOVEL     'NO'          TCLKS
     C     SEKNK         IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TCTINS
     C                   ELSE                                                   1
     C                   MOVE      SEKNK         YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C     SYFOR9        IFEQ      *ZEROS                                        2<-B
     C                   MOVE      *BLANKS       TCTINS
     C                   ELSE                                                    2
     C                   MOVEL     SYFORE        TCTINS
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C                   MOVE      SEKNS         TCKNK
     C                   MOVE      SENAS         TCNAK
     C                   MOVE      SEADS1        TCAD1K
     C                   MOVEL     SEADS3        TCPNK
     C                   MOVEL     SEADS3        TCPSK
     C                   MOVEL     SELKA         TCLKK
     C     SEKNS         IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TCTINK
     C                   ELSE                                                   1
     C                   MOVE      SEKNS         YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C     SYFOR9        IFEQ      *ZEROS                                        2<-B
     C                   MOVE      *BLANKS       TCTINK
     C                   ELSE
     C                   MOVEL     SYFORE        TCTINK
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C     TCAD1S        IFEQ      *BLANKS                                      1<-B
     C                   MOVEL     '.'           TCAD1S
     C                   END                                                    1<-E
     C     TCAD1K        IFEQ      *BLANKS                                      1<-B
     C                   MOVEL     '.'           TCAD1K
     C                   END                                                    1<-E
      *
     C                   WRITE     NCTSECAMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     UTFOR2        BEGSR
      ***********************************************
     C     TRHK          SETLL     TRUHL
     C     TRHK          READE     TRUHL                                  33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    TRUHLR
     C     TRHK          READE     TRUHL                                  33
     C  N33              ENDDO
      *
     C     1             DO        YRECNO        ZRECNO                         1<-B
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     XANTO         IFEQ      0                                             2<-B
     C     WSMED         OREQ      'J'
     C     HEATRK        CHAIN     HEADF                              34
      * OPPDATER STATUS FOR Å VISE AT LISTE ER SKREVET
     C  N34W1TYPE        IFEQ      'T1'
     C                   MOVE      'A'           TRTSTA
     C                   ELSE
     C                   MOVE      'B'           TRTSTA
     C                   END
     C  N34              UPDATE    HEADFR
     C                   MOVE      UAVD          TLAVD
     C                   MOVE      UTDN          TLTDN
     C                   MOVE      HEAVD         TLAVD2
     C                   MOVE      HEOPD         TLTDN2
     C                   MOVE      WSTXT         TLTXT
     C                   MOVE      WSTLKU        TLTLKU
     C                   WRITE     TRUHLR
     C                   ADD       HENT          TIPNT
     C                   ADD       HEVKT         TIVKB
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C                   MOVE      UAVD          TCAVD
     C                   MOVE      UTDN          TCTDN
     C                   ADD       1             ULI
     C                   MOVE      ULI           TCLI
     C                   Z-ADD     TIVKB         TCVKTB
     C                   WRITE     NCTSECR
      *
     C                   MOVE      UAVD          TIAVD
     C                   MOVE      UTDN          TITDN
     C                   MOVE      TCLI          TILI
     C                   Z-ADD     1             TILI2
     C                   Z-ADD     1             TILIT
     C                   EVAL      TICCH  = 0
     C                   EVAL      TICCCD = *BLANKS
     C                   EVAL      TIVKN  = TIVKB * 0,9
     C                   EVAL      TIPTY  = 'PK'
     C                   WRITE     NCTSEIR
      *
     C                   MOVE      *ZEROS        UFLAGG
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     W1KNFA        IFNE      *ZEROS
     C                   MOVE      W1KNFA        YKN
     C     KUND1K        CHAIN     CUNDL01                            87
     C  N87              MOVE      KNAVN         W1NAFA
     C   87              MOVE      'SPK0001'     MSGNR
     C   87              SETON                                        30
     C                   ELSE
     C                   MOVE      *BLANKS       W1NAFA
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SOKDTA        BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W1KNFA
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
      *
     C                   ENDSR
      *
      ***********************************************
     C     VLHENT        BEGSR
      ***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
      *
      * INGEN GYLDIGE OPTIONS FORETATT
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     2             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
      * OPPDATER CURSORPOSISJON PÅ SKJERMBILDE
     C     ZRECNO        DIV       7             LINNBR
     C                   MVR                     LINNBR
     C     LINNBR        MULT      2             XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   SETON                                        99
      *
      * PLUKKE TIL DENNE LISTE
     C     WSNR          IFEQ      '1'
     C     WSMED         ANDNE     'J'
     C                   ADD       1             XANTO             5 0
     C                   MOVE      'J'           WSMED
     C                   SETON                                        90
      * VED PLUKKING OPPDATERES TRSTAT TIL AKTUELL TYPE
      *     DERSOM EN HAR VALGT FORTOLLINGSAGENT OPPDATERES OPD MED DENNE
     C     TRTSTA        IFNE      W1TYP2
     C     WSNAFA        ORNE      W1NAFA
     C     HEATRK        CHAIN     HEADF                              33
     C  N33              DO
     C     W1KNFA        IFNE      *ZEROS
     C                   Z-ADD     W1KNFA        HEKNA
     C                   MOVEL     W1NAFA        WSNAFA
     C                   END
     C                   MOVE      W1TYP2        TRTSTA
     C                   UPDATE    HEADFR
     C                   END
     C                   END
     C                   END
      *
      * FJERNE FRA DENNE LISTE
     C     WSNR          IFEQ      '4'
     C     WSMED         ANDEQ     'J'
     C                   SUB       1             XANTO             5 0
     C                   MOVE      ' '           WSMED
     C                   SETOFF                                       90
      * VED FJERNING OPPDATERES TRSTAT MED VERDI FRA OPTFELT 2
     C     HEATRK        CHAIN     HEADF                              33
     C  N33              MOVE      WSTSTA        TRTSTA
     C  N33              UPDATE    HEADFR
     C     W1TYP2        COMP      TRTSTA                                 85
     C                   END
      *
      * SE PÅ OPPDRAG
     C     WSNR          IFEQ      '5'
     C                   EXSR      VISE
     C                   END
      *
      * SE PÅ FRITEKST TIL ETT OPPDRAG
     C     WSNR          IFEQ      '9'
     C                   EXFMT     BILDE02
     C     WSTXT         IFNE      *BLANKS
     C                   MOVE      'J'           WSFT
     C                   END
     C                   END
      *
      *   OPTIONS UTFØRT - BLANK INPUT - OPPDAT- SUBFIL
     C                   MOVE      ' '           WSNR
     C                   MOVE      ' '           WSTSTA
     C                   UPDATE    SUBFIL
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       6             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     VISE          BEGSR
      ***********************************************
     C                   Z-ADD     2             UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     TRHK          KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEATRK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YKN
N03  C     FRISOKK       KLIST
N03  C                   KFLD                    UAVD
N03  C                   KFLD                    UTDN
N03  C                   KFLD                    WFSKODE
N03  C     *LIKE         DEFINE    FSKODE        WFSKODE
N03  C                   EVAL      WFSKODE = '*CE'
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
     C                   PARM                    UDK               5
     C                   PARM                    UPRO              8 0
     C                   PARM                    ULSTL             4 0
     C                   PARM                    UFLAGG            1 0
      *
     C     'NCTS0100'    CHAIN     SYPTFF                             33
     C                   IF        *IN33
     C                   MOVE      99999999      X_FASE5           8 0
     C                   ELSE
     C                   MOVE      PTFDT         X_FASE5
     C                   END
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XIDAG             8 0
      *
     C                   Z-ADD     0             YKN               8 0
     C                   MOVE      *ZEROS        W1KNFA
     C                   MOVEL     UDK           W1TYPE
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     UPRO          CHAIN     TURER14                            33
     C                   IF        *IN33
     C                   MOVE      'J'           XFRAOPD           1
     C                   MOVE      'J'           W1ALL
     C                   MOVE      *BLANKS       XUDT
     C                   ELSE
     C                   MOVE      'N'           XFRAOPD
     C                   MOVE      TUDTD         XUDTD
     C                   MOVE      TUDTM         XUDTM
     C                   MOVE      TUDTA         XUDTA
     C                   END
     C     TRHK          SETLL     TRUHL
     C*    TRHK          READE     TRUHL                                  28
     C     TRHK          READE(N)  TRUHL                                  28
      *
     C                   ENDSR
      *
