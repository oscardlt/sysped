********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11048 * 01 CB  PTF11 Testindikator
12XXX * 02 CB  PTF12 Parameter på om varuposter ska kopieras eller inte.
********************************************************************
     FSVEH      UF A E           K DISK
     FSVEV1     IF   E           K DISK
     F                                     RENAME(SVEVR:SVEVR1)
     FSVEVA1    IF   E           K DISK
     F                                     RENAME(SVEVAR:SVEVAR1)
     FSVEF1     IF   E           K DISK
     F                                     RENAME(SVEFR:SVEFR1)
     FSVEV      O  A E             DISK
     FSVEVA     O  A E             DISK
     FSVEF      O  A E             DISK
     FSVEA      UF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVEH_SYAV
     C                   PARM                    UVEH_SYOP
     C                   PARM                    UVEH_SYSG
N02  C                   PARM                    UKOPFA            1
N02  C                   PARM                    UKOPVP            1
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVEH_SYAV     UVEH_SYAV
     C     *LIKE         DEFINE    SVEH_SYOP     UVEH_SYOP
     C     *LIKE         DEFINE    SVEH_SYSG     UVEH_SYSG
     C     *LIKE         DEFINE    SVEH_TUID     UVEH_TUID
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     UKEY          KLIST
     C                   KFLD                    UVEH_SYAV
     C                   KFLD                    UVEH_SYOP
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Hent tolldeklarasjon for kopiering
     C     UKEY          CHAIN(N)  SVEH                               51
      * Hent neste tolldeklarasjonsnummer
     C     UVEH_SYAV     CHAIN     SVEA                               52
     C                   ADD       1             SVEA_SYOP
     C                   UPDATE    SVEAR
     C                   EVAL      SVEH_SYOP=SVEA_SYOP
      * Lag tullid
     C                   EVAL      SVEH_TUID=*BLANKS
     C                   CALL      'SVG001R'
     C                   PARM                    SVEH_TUID
      * Andre variabler
     C                   EVAL      SVEH_SYST=*BLANKS
     C                   MOVE      WDT           SVEH_SYDT
     C                   MOVEL     SVEH_SYAV     SVEH_RFAB
     C                   MOVE      SVEH_SYOP     SVEH_RFAB
     C                   MOVE      UVEH_SYSG     SVEH_SYSG
N01  C                   MOVE      SVEA_0035     SVEH_0035
      * Skriv post
     C                   WRITE     SVEHR
      * Varuposter
N02  C                   IF        UKOPVP='J'
     C     UKEY          SETLL     SVEV1
     C     UKEY          READE     SVEV1                                  53
     C     *IN53         DOWEQ     *OFF
     C                   EVAL      SVEV_SYOP=SVEH_SYOP
     C                   WRITE     SVEVR
     C     UKEY          READE     SVEV1                                  53
     C                   ENDDO
N02  C                   ENDIF
      * Fakturor
N02  C                   IF        UKOPFA='J'
     C                   EVAL      *IN53=*OFF
     C     UKEY          SETLL     SVEF1
     C     UKEY          READE     SVEF1                                  53
     C     *IN53         DOWEQ     *OFF
     C                   EVAL      SVEF_SYOP=SVEH_SYOP
     C                   WRITE     SVEFR
     C     UKEY          READE     SVEF1                                  53
     C                   ENDDO
N02  C                   ENDIF
      * Avgifter
N02  C                   IF        UKOPVP='J'
     C                   EVAL      *IN53=*OFF
     C     UKEY          SETLL     SVEVA1
     C     UKEY          READE     SVEVA1                                 53
     C     *IN53         DOWEQ     *OFF
     C                   EVAL      SVEVA_SOP=SVEH_SYOP
     C                   WRITE     SVEVAR
     C     UKEY          READE     SVEVA1                                 53
     C                   ENDDO
N02  C                   ENDIF
      * Endre *entry for oppdragsnummer
     C                   EVAL      UVEH_SYOP=SVEA_SYOP
     C                   ENDSR
