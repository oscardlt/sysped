      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE23R MODULE(*LIBL/TJGE23R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
11XXX * 01 JOV PTF11 Feil på klasse mm .... / key
11XXX * 02 JOV PTF11 DELETE av linje OG reorg skilt ut i eget prog
11XXX * 03 JOV PTF11 Nytt felt FFINDX
11XXX * 04 JOV PTF11 feilmeld dersom add av ugyldig linjenr
11XXX * 05 JOV PTF11 Kontroll av max ADR-poeng (i så fall --> 99999 )
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDOKUFV    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FDOKUFVF   UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FDOKUFM    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
N01  FADRUNNRF  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D AVD             S              4S 0
     D OPD             S              7S 0
     D FBN             S              3S 0
     D LIN             S              2S 0
     D FMMRK1          S             35
     D FVANT           S              7P 0
     D FVPAKN          S              7
     D FVVT            S             35
     D FVVKT           S              9P 0
     D FVVOL           S              7P 3
     D FVLM            S              4P 2
     D FVLM2           S              4P 2
     D FVLEN           S              4P 0
     D FVBRD           S              4P 0
     D FVHOY           S              4P 0
     D FFUNNR          S              4
     D FFEMBG          S              3
N01  D FFINDX          S              1
     D FFKLAS          S              4
     D FFSEDD          S             10
     D FFTRES          S             10
     D FFFAKT          S              4S 0
     D FFANTK          S              5S 0
     D FFANTE          S              7S 0
     D FFENH           S              3
     D FFPOEN          S              5S 0

N05  D BigNum          S             19S 5

     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
          +'¬avd¬ : ¬'+%trim(%editc(avd:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(opd:'Z'))+'¬, '
          +'¬fbn¬ : ¬'+%trim(%editc(fbn:'Z'))+'¬, '
          +'¬lin¬ : ¬'+%trim(%editc(lin:'Z'))+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"awblineUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DOKUFV
     C  N50              CLOSE     DOKUFVF
N01  C  N50              CLOSE     ADRUNNRF
     C  N50              CLOSE     DOKUFM

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      AVD        = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      OPD        = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('FBN')
     C                   EVAL      FBN        = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('LIN')
     C                   EVAL      LIN        = c2n2(WA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      FMMRK1  = zhbgetvar('FMMRK1')
     C     LO:UPC        XLATE     FMMRK1        FMMRK1
     C                   EVAL      WA      = zhbgetvar('FVANT')
     C                   EVAL      FVANT   = c2n2(WA)
     C                   EVAL      FVPAKN     = zhbgetvar('FVPAKN')
     C     LO:UPC        XLATE     FVPAKN        FVPAKN
     C                   EVAL      FVVT       = zhbgetvar('FVVT')
     C     LO:UPC        XLATE     FVVT          FVVT
     C                   EVAL      WA      = zhbgetvar('FVVKT')
     C                   EVAL      FVVKT   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVVOL')
     C                   EVAL      FVVOL   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVLM')
     C                   EVAL      FVLM    = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVLM2')
     C                   EVAL      FVLM2   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVLEN')
     C                   EVAL      FVLEN   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVBRD')
     C                   EVAL      FVBRD   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FVHOY')
     C                   EVAL      FVHOY   = c2n2(WA)
     C                   EVAL      FFUNNR  = zhbgetvar('FFUNNR')
     C                   EVAL      FFEMBG  = zhbgetvar('FFEMBG')
N01  C                   EVAL      FFINDX  = zhbgetvar('FFINDX')
     C                   EVAL      FFKLAS  = zhbgetvar('FFKLAS')
     C                   EVAL      FFSEDD  = zhbgetvar('FFSEDD')
     C                   EVAL      FFTRES  = zhbgetvar('FFTRES')
     C                   EVAL      WA      = zhbgetvar('FFFAKT')
     C                   EVAL      FFFAKT  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FFANTK')
     C                   EVAL      FFANTK  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FFANTE')
     C                   EVAL      FFANTE  = c2n2(WA)
s01  C*                  EVAL      FFENH   = zhbgetvar('FENH')
N01  C                   EVAL      FFENH   = zhbgetvar('FFENH')
     C                   EVAL      WA      = zhbgetvar('FFPOEN')
     C                   EVAL      FFPOEN  = c2n2(WA)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DOKUFVK       KLIST
     C                   KFLD                    WFVRI
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FBN
     C                   KFLD                    LIN
     C     DOKUFVFK      KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FBN
     C                   KFLD                    LIN
     C     DOKUFMK       KLIST
     C                   KFLD                    WFVRI
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FBN
     C                   KFLD                    LIN
N01  C                   KFLD                    WFMMKNR
N01  C                   KFLD                    WFMLONR
N01  C                   EVAL      WFMMKNR=1
N01  C                   EVAL      WFMLONR=1
     C     *LIKE         DEFINE    XFMMKNR       WFMMKNR
     C     *LIKE         DEFINE    XFMLONR       WFMLONR
     C     *LIKE         DEFINE    XFVRI         WFVRI
     C                   EVAL      WFVRI='F'
N01  C     UnNrKey       KLIST
N01  C                   KFLD                    FFUNNR
N01  C                   KFLD                    FFEMBG
N03  C                   KFLD                    FFINDX
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DOKUFV
     C  N50              OPEN      DOKUFVF
N01  C  N50              OPEN      ADRUNNRF
     C  N50              OPEN      DOKUFM

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
S02  C*    WMODE         IFNE      'A'
S02  C*    DOKUFVK       CHAIN     DOKUFV                             55
S02  C*                  ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
N04  C     DOKUFVK       CHAIN(N)  DOKUFV                             55
N04  C                   if        *in55=*off or LIN = 0
N04  C                   eval      Error = 'Ugyldig linjenummer '
N04  C                             + '(tips gå ut /inn igjen)'
N04  C                   else
     C                   eval      Error = *blanks
     C                   EXSR      SRA
N04  C                   endif
     C     WMODE         WHENEQ    'D'
N02  C     DOKUFVK       CHAIN(N)  DOKUFV                             55
     C                   IF        *IN55=*ON
N02  C*                  eval      Error = 'AwbLine not found'
N02  C                   eval      Error = 'OrderLine not found'
     C                   ELSE
     C                   eval      Error = *blanks
S02  C*                  EXSR      SRD
N02  c                   call      'SYGE85'
N02  C                   parm                    XFVAVD
N02  C                   parm                    XFVOPD
N02  C                   parm                    XFVFBNR
N02  C                   parm                    XFVLINR
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
N02  C     DOKUFVK       CHAIN     DOKUFV                             55
     C                   IF        *IN55=*ON
S02  C*                  eval      Error = 'AwbLine not found'
N02  C                   eval      Error = 'OrderLine not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   CLEAR                   DOKUFVR
     C                   EVAL      XFVRI='F'
     C                   EVAL      XFVAVD=AVD
     C                   EVAL      XFVOPD=OPD
     C                   EVAL      XFVFBNR=FBN
     C                   EVAL      XFVLINR=LIN

     C                   EVAL      XFVANT=FVANT
     C                   EVAL      XFVPAKN=FVPAKN
     C                   EVAL      XFVVT=FVVT
     C                   EVAL      XFVVKT=FVVKT
     C                   EVAL      XFVVOL=FVVOL
     C                   EVAL      XFVLM=FVLM
TMS  C                   EVAL      XFVLM2=FVLM2
TMS  C                   EVAL      XFVLEN=FVLEN
TMS  C                   EVAL      XFVBRD=FVBRD
TMS  C                   EVAL      XFVHOY=FVHOY

     C                   WRITE     DOKUFVR
      * DOKUFM
     C     DOKUFMK       CHAIN     DOKUFM                             56
     C                   IF        *IN56
     C                   CLEAR                   DOKUFMR
     C                   EVAL      XFMRI='F'
N01  C                   EVAL      XFMMKNR=1
N01  C                   EVAL      XFMLONR=1
     C                   EVAL      XFMAVD=AVD
     C                   EVAL      XFMOPD=OPD
     C                   EVAL      XFMFBNR=FBN
     C                   EVAL      XFMLINR=LIN
     C                   EVAL      XFMMRK1=FMMRK1
     C                   WRITE     DOKUFMR
     C                   ELSE
     C                   EVAL      XFMMRK1=FMMRK1
     C                   UPDATE    DOKUFMR
     C                   ENDIF
      * DOKUFVF
      *  - farlig gods - skapes/endres dersom FFUNR ulik blank / slettes hvis lik
N01  C                   IF        FFUNNR <> *blanks
N01  C     UnNrKey       chain     ADRUNNRF                           33
N01  C                   ENDIF
     C     DOKUFVFK      CHAIN     DOKUFVF                            56
     C                   IF        *IN56
N01  C                   IF        FFUNNR <> *blanks
     C                   CLEAR                   DOKUFVFR
     C                   EVAL      XFFAVD=AVD
     C                   EVAL      XFFOPD=OPD
     C                   EVAL      XFFFBNR=FBN
     C                   EVAL      XFFLINR=LIN
N01  C                   EVAL      XFFLIN2=1
     C                   EVAL      XFFUNNR=FFUNNR
     C                   EVAL      XFFEMBG=FFEMBG
N03  C                   EVAL      XFFINDX=FFINDX
s01  C                   EVAL      XFFKLAS=ADKLAS
s01  C                   EVAL      XFFSEDD=ADSEDD
s01  C                   EVAL      XFFTRES=ADTRES
s01  C                   EVAL      XFFFAKT=ADFAKT
     C                   EVAL      XFFANTK=FFANTK
     C                   EVAL      XFFANTE=FFANTE
     C                   EVAL      XFFENH=FFENH
S05  C*                  EVAL      XFFPOEN=ADFAKT*FFANTE
N05  C                   EVAL      BigNum =ADFAKT*FFANTE
N05  C                   IF        BigNum < 99999
N05  C                   EVAL      XFFPOEN=BigNum
N05  C                   else
N05  C                   EVAL      XFFPOEN=99999
N05  C                   endif
     C                   WRITE     DOKUFVFR
N01  C                   ENDIF
     C                   ELSE
N01  C                   IF        FFUNNR <> *blanks
     C                   EVAL      XFFUNNR=FFUNNR
     C                   EVAL      XFFEMBG=FFEMBG
N03  C                   EVAL      XFFINDX=FFINDX
s01  C                   EVAL      XFFKLAS=ADKLAS
s01  C                   EVAL      XFFSEDD=ADSEDD
s01  C                   EVAL      XFFTRES=ADTRES
s01  C                   EVAL      XFFFAKT=ADFAKT
     C                   EVAL      XFFANTK=FFANTK
     C                   EVAL      XFFANTE=FFANTE
     C                   EVAL      XFFENH=FFENH
S05  C*                  EVAL      XFFPOEN=ADFAKT*FFANTE
N05  C                   EVAL      BigNum =ADFAKT*FFANTE
N05  C                   IF        BigNum < 99999
N05  C                   EVAL      XFFPOEN=BigNum
N05  C                   else
N05  C                   EVAL      XFFPOEN=99999
N05  C                   endif
     C                   UPDATE    DOKUFVFR
N01  C                   ELSE
N01  C                   DELETE    DOKUFVFR
N01  C                   ENDIF
     C                   ENDIF
     C                   ENDSR
S02   *
S02   *______________________________________________________
S02   * Delete                                            SRD
S02   *""""""""""""""""""""""""""""""""""""""""""""""""""""""
S02  C*    SRD           BEGSR
S02  C*                  DELETE    DOKUFVR
S02   * DOKUFM
S02  C*    DOKUFMK       CHAIN     DOKUFM                             56
S02  C*                  IF        *IN56=*OFF
S02  C*                  DELETE    DOKUFMR
S02  C*                  ENDIF
S02   * DOKUFVF
S02  C*    DOKUFVFK      CHAIN     DOKUFVF                            56
S02  C*                  IF        *IN56=*OFF
S02  C*                  DELETE    DOKUFVFR
S02  C*                  ENDIF
S02  C*                  ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XFVANT=FVANT
     C                   EVAL      XFVPAKN=FVPAKN
     C                   EVAL      XFVVT=FVVT
     C                   EVAL      XFVVKT=FVVKT
     C                   EVAL      XFVVOL=FVVOL
     C                   EVAL      XFVLM=FVLM
TMS  C                   EVAL      XFVLM2=FVLM2
TMS  C                   EVAL      XFVLEN=FVLEN
TMS  C                   EVAL      XFVBRD=FVBRD
TMS  C                   EVAL      XFVHOY=FVHOY
     C                   UPDATE    DOKUFVR
      * DOKUFM
     C     DOKUFMK       CHAIN     DOKUFM                             56
     C                   IF        *IN56
     C                   CLEAR                   DOKUFMR
     C                   EVAL      XFMRI='F'
     C                   EVAL      XFMAVD=AVD
     C                   EVAL      XFMOPD=OPD
     C                   EVAL      XFMFBNR=FBN
     C                   EVAL      XFMLINR=LIN
N01  C                   EVAL      XFMMKNR=1
N01  C                   EVAL      XFMLONR=1
     C                   EVAL      XFMMRK1=FMMRK1
     C                   WRITE     DOKUFMR
     C                   ELSE
     C                   EVAL      XFMMRK1=FMMRK1
     C                   UPDATE    DOKUFMR
     C                   ENDIF
      * DOKUFVF
      *  - farlig gods - skapes/endres dersom FFUNR ulik blank / slettes hvis lik
N01  C                   IF        FFUNNR <> *blanks
N01  C     UnNrKey       chain     ADRUNNRF                           33
N01  C                   ENDIF
     C     DOKUFVFK      CHAIN     DOKUFVF                            56
     C                   IF        *IN56
N01  C                   IF        FFUNNR <> *blanks
     C                   CLEAR                   DOKUFVFR
     C                   EVAL      XFFAVD=AVD
     C                   EVAL      XFFOPD=OPD
     C                   EVAL      XFFFBNR=FBN
     C                   EVAL      XFFLINR=LIN
N01  C                   EVAL      XFFLIN2=1
     C                   EVAL      XFFUNNR=FFUNNR
     C                   EVAL      XFFEMBG=FFEMBG
N03  C                   EVAL      XFFINDX=FFINDX
s01  C                   EVAL      XFFKLAS=ADKLAS
s01  C                   EVAL      XFFSEDD=ADSEDD
s01  C                   EVAL      XFFTRES=ADTRES
s01  C                   EVAL      XFFFAKT=ADFAKT
     C                   EVAL      XFFANTK=FFANTK
     C                   EVAL      XFFANTE=FFANTE
     C                   EVAL      XFFENH=FFENH
S05  C*                  EVAL      XFFPOEN=ADFAKT*FFANTE
N05  C                   EVAL      BigNum =ADFAKT*FFANTE
N05  C                   IF        BigNum < 99999
N05  C                   EVAL      XFFPOEN=BigNum
N05  C                   else
N05  C                   EVAL      XFFPOEN=99999
N05  C                   endif
     C                   WRITE     DOKUFVFR
N01  C                   ENDIF
     C                   ELSE
N01  C                   IF        FFUNNR <> *blanks
     C                   EVAL      XFFUNNR=FFUNNR
     C                   EVAL      XFFEMBG=FFEMBG
N03  C                   EVAL      XFFINDX=FFINDX
s01  C                   EVAL      XFFKLAS=ADKLAS
s01  C                   EVAL      XFFSEDD=ADSEDD
s01  C                   EVAL      XFFTRES=ADTRES
s01  C                   EVAL      XFFFAKT=ADFAKT
     C                   EVAL      XFFANTK=FFANTK
     C                   EVAL      XFFANTE=FFANTE
     C                   EVAL      XFFENH=FFENH
S05  C*                  EVAL      XFFPOEN=ADFAKT*FFANTE
N05  C                   EVAL      BigNum =ADFAKT*FFANTE
N05  C                   IF        BigNum < 99999
N05  C                   EVAL      XFFPOEN=BigNum
N05  C                   else
N05  C                   EVAL      XFFPOEN=99999
N05  C                   endif
     C                   UPDATE    DOKUFVFR
N01  C                   ELSE
N01  C                   DELETE    DOKUFVFR
N01  C                   ENDIF
     C                   ENDIF
     C                   ENDSR
