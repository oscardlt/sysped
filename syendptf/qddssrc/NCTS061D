     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R NC061D01
     A                                      CSRLOC(LINNBR     POSNBR)
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF10(10 'Slette ferdigmeldt TVINN')
     A                                      CA12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/NCTS061R'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  1 20'Registrering av NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  5 18'Avdeling:'
     A            UAVD           4Y 0B  5 34EDTCDE(4)
     A  95                                  DSPATR(PC)
     A  31                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A                                  9 18'Signatur:'
     A            WSSG           3A  B  9 34DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 18'Tolldek.nr.'
     A            WSTDN          7Y 0O 10 34DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 42'Tast oppdragsnr.'
     A                                      COLOR(BLU)
     A                                 13 18'Sendes NCTS'
     A            WSTOLK         1A  B 13 34VALUES(' ' 'J' 'N')
     A  96                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 42'(J/N/blank)'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A N36                             23  2'F7=Søke oppdrag'
     A                                      COLOR(BLU)
     A                                 23 20'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NC061D02
     A N27                                  CF04(04 'Søking')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF10(10 'Se TVINN logg')
     A                                      CA12(12 'Forrige')
     A                                      CF14(14 'KUNDEINFO')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/NCTS061R'
     A                                      COLOR(BLU)
     A N27                              1 18'Vedl. av NCTS ankomst'
     A                                      COLOR(WHT)
     A  27                              1 18'Spørring på NCTS ank.'
     A                                      COLOR(WHT)
     A                                  1 42'Ref.'
     A            UAVD           4Y 0O  1 47COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  1 52COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSSG           3A  O  1 60COLOR(WHT)
     A            WSTARF        16A  O  1 64TEXT('Tariffer')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'Godsnr.'
     A            WSGN          35A  B  3 13DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  4  2'MRN-nr.:'
     A            WSTRNR        18A  B  4 13TEXT('MRN-nr')
     A  98                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  4 42'Ankomst dato/tid:'
     A            WSDTA          6Y 0B  4 63TEXT('Ankomst dato')
     A  97                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTMA          4Y 0B  4 70TEXT('Ankomst tid')
     A  96                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5  2'Forenklet:'
     A            WSSPR          1Y 0B  5 13TEXT('FORENKLET PROSEDYRE')
     A  27                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                  5 16'(1=Ja, 0=Nei)'
     A                                  5 32'Hendelse:'
     A            WSIFLG         1Y 0B  5 43TEXT('Hendelsesflagg')
     A  27                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                  5 46'(1=Ja, 0=Nei)'
     A                                  6  2'Autorisasjon:'
     A                                  6 22'Type:'
     A            WSATYP         4A  B  6 28TEXT('Autorisasjonstype')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  6 33'Ref:'
     A            WSAREF        35A  B  6 38TEXT('Autorisasjons ref.nr')
     A  94                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'Destinasj:'
     A            WSTSBA         8A  B  7 13TEXT('Destinasjons tollsted')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8  2'Trader:'
     A            WSKNTA         8Y 0B  8 13TEXT('Trade kundenr')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNATA        30A  B  8 22TEXT('Trader navn')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTINTA       17A  B  8 53TEXT('Trader org.nr')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 71'Språk'
     A            WSSKTA         2A  B  8 77TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9  2'Lokasjon:'
     A                                 10  4'Type:'
     A            WSLCTY         1A  B 10 13TEXT('Lokasjon type')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10 15'Kvalif:'
     A            WSQID          1A  B 10 23TEXT('Qualifier')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10 25'Autorisasjon:'
     A            WSANR         35A  B 10 39TEXT('Autorisasjonsnr')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11  4'Till:'
     A            WSAID          4A  B 11 13TEXT('Tilleggs identifier')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 18'Lok:'
     A            WSSD          17A  B 11 23TEXT('Lokasjons UN kode')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12  2'Tollsted:'
     A            WSTS           8A  B 12 13TEXT('Tolldted')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 13  2'Økonomi:'
     A            WSKNE          8Y 0B 13 13TEXT('Økonomi operatørsnr')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINE        17A  B 13 22TEXT('Økonomi operatørs org.nr')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14  2'Addresse:'
     A            WSADE1        30A  B 14 13TEXT('Gateadresse')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 15  2'PN/PS/LK:'
     A            WSPNE         17A  B 15 13TEXT('Postnr')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPSE         24A  B 15 31TEXT('Poststed')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSLKE          2A  B 15 56TEXT('Landkode')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 16  2'Post adr.:'
     A            WSADE2        17A  B 16 13TEXT('Husnr')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNE2        17A  B 16 31TEXT('Postnr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSLKE2         2A  B 16 49TEXT('Landkode')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 17  2'KontaktP:'
     A            WSCPE         30A  B 17 13TEXT('Kontaktperson')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2'Tlf.:'
     A            WSTLE         20A  B 18 13TEXT('Tlf')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 19  2'Epost:'
     A            WSEME         50A  B 19 13TEXT('Epostadresse')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A N27                             23  1'F4=Søking'
     A                                      COLOR(BLU)
     A N36                             23 11'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A                                 23 25'F10=NCTS'
     A                                      COLOR(BLU)
     A                                 23 35'F12=Forrige'
     A                                      COLOR(BLU)
     A                                 23 47'F14=Se kundeoppl'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
