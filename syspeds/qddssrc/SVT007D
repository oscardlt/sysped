     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 11 52 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '1')
     A            SVVK_KD        3A  O  4  5
     A            SVVK_DTS       8S 0O  4 10
     A            SVVK_DTE       8S 0O  4 20
     A            SVVK_OMR       7Y 0O  4 30EDTCDE(4)
     A            SVVK_KRS      10Y 6O  4 39EDTCDE(K)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVT007R'
     A                                      COLOR(BLU)
     A                                  2  2'Välj option:'
     A                                      COLOR(BLU)
     A                                  2 16'1=Plocka'
     A                                      COLOR(BLU)
     A                                  3  2'Op Kod  Från      Till      Faktor-
     A                                               Kurs'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 12'Selektion av valutakod'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(11)
     A                                      CLRL(01)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N30                              1  2'F3/F12=Avbryt   F5=Förnya'
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVT003R'
     A                                      COLOR(BLU)
     A            XFTXT         30A     1 10COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  3  2'Op Kode       Beskrivelse'
     A                                      COLOR(WHT)
     A            W2SØK         20A  I  4 16DSPATR(UL)
     A                                 11  2'F3/F12=Avbryt   F5=Frisk'
     A                                      COLOR(BLU)
