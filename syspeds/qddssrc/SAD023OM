********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
      *
     A          R BILDE01
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde/leverandør')
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A                                  2 30'Spørring på SAD-eksport (historikk-
     A                                      )'
     A                                      COLOR(WHT)
     A                                  4 13'Avdeling:'
     A            WSAAVD         4Y 0B  4 23TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('Tastet avdeling er ikke reg-
     A                                      istrert.' 99)
     A            KOANVN        33A  O  4 30TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  9 20'Velg søkevei med startpunkt'
     A                                      COLOR(WHT)
     A                                 12 20'Dato:'
     A            WSDT           6D 0B 12 35TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  41
     AO 98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 20'Tolldek.nr.'
     A            WSTDN          7Y 0B 13 35TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A  42                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 20'Avsendersnr.'
     A            WSKNK          8Y 0B 14 35TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  43
     AO 97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 48'Dato:'
     A            WSDTK          6D 0B 14 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 20'Mottakersnr.'
     A            WSKNS          8Y 0B 15 35TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  44
     AO 96                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 15 48'Dato:'
     A            WSDTS          6D 0B 15 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16 20'Tariffør:'
     A            WSSG           3A  B 16 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  45                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 16 48'Dato:'
     A            WSDTT          6D 0B 16 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 20'Løpenummer:'
     A            WSTLL         10A  B 17 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 18 20'Godsnummer:'
     A            WSGN          15A  B 18 35TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A  47                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 23 20'F3 = Avslutt'
     A                                      COLOR(BLU)
     A                                 23 36'F4 = Søk kunde/leverandør'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '5')
     A            SEAVD          4Y 0O  5  4TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A            SETDN          7Y 0O  5  9TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  42                                  COLOR(TRQ)
     A            SEKNK          8Y 0O  5 17TEXT('Kundenr Kjøper')
     A                                      EDTCDE(4)
     A  43                                  COLOR(TRQ)
     A            WSDT2          6S 0O  5 26TEXT('Registreringsdato')
     A  41                                  COLOR(TRQ)
     A            SETLL         10A  O  5 33TEXT('Tollnr Løpenr')
     A  46                                  COLOR(TRQ)
     A            SETLE          6A  O  5 44TEXT('Tollnr Ekspsted')
     A  46                                  COLOR(TRQ)
     A            SEVKB          9Y 0O  5 52TEXT('Bruttovekt')
     A                                      EDTCDE(4)
     A            SESG           3A  O  5 62TEXT('Signatur')
     A  45                                  COLOR(TRQ)
     A            SEGN          15A  O  5 66TEXT('Godsnummer')
     A  47                                  COLOR(TRQ)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 10'SPØRRING PÅ SAD-eksport (historikk-
     A                                      )'
     A                                  2 10'Avdeling:'
     A            WSAAVD         4Y 0O  2 20COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 25TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(045 DIV1913 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1'Op Avd. Tdeknr. Mottaker  Dato  Lø-
     A                                      penr.. eksp.enh ...Vekt.. Tar Godsn-
     A                                      r...'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2MSGCON(18 DIV1914 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF01(01 'BYTT AVDELNGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                  1 10'SPØRRING PÅ SAD-eksport +
     A                                      (historikk)'
     A                                  2 10'Avdeling:'
     A            WSAAVD         4Y 0O  2 20COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 25TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(45 DIV1913 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1'Op Avd. Tdeknr. Mottaker  Dato  Lø-
     A                                      penr.. eksp.enh ...Vekt.. Tar Godsn-
     A                                      r...'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV1914 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
