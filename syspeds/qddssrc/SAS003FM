     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R SUBFIL                    SFL
     A            SFNR           1A  B  8  3
     A  51                                  DSPATR(PR)
     A                                      VALUES(' ' '1' '2' '4' '5')
     A            FLKODE         3   O  8  9
     A            FLNAVN    R        O  8 17REFFLD(KUNDR/KNAVN *LIBL/CUNDF)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      COLOR(BLU)
     A                                  1  2'SYSPED/SAS003'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(012 DIV0688 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(040 SHU0656 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(060 SHU0657 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF12(12 'tilbake')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BUNNN
     A                                      CF12(12 'tilbake')
     A                                      CF04(04 'søking')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            FLKODE         3   B  1  6
     A                                  2  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNNS
     A                                      CF12(12 'tilbake')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            FLKODE         3   O  1  6
     A            FLNAVN    R        O  1 15REFFLD(KUNDR/KNAVN *LIBL/CUNDF)
     A                                  1 48MSGCON(015 SHU0267 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
