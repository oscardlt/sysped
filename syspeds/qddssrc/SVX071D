     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 55 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '2' '4' '9')
     A  27                                  DSPATR(PR)
     A            NVMN          42A  O  5  5
     A            NVMNSK         2A  O  5 48
     A            TVMN          42A  O  6  5
     A            TVMNSK         2A  O  6 48
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 48SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SVX071R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll nytt mærke&nr   '
     A                                      COLOR(WHT)
     A N27                              2  2'1=Ny 2=Ændra 4=Radera 9=Ångra'
     A                                      COLOR(BLU)
     A                                  3  2'Op Nytt mærke&nr                  -
     A                                                  Språk'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A  27                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2MN          35A  B  4  5DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A            W2MNSK         2A  B  4 48DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A N27                             17  2'F3/12=Avbryt F4=Søk F5=Uppdatera'
     A                                      COLOR(BLU)
     A  27                             17  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
     A  30        MSGTXT        48A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(15)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'Op Nytt mærke & nr                -
     A                                                   Språk'
     A                                      COLOR(WHT)
     A            WSMN          35A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90
     AON30                                  DSPATR(PC)
     A            WSMNSK         2A  B  2 48DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A            TVMN          42A  O  3  5
     A            TVMNSK         2A  O  3 48
     A N30                              4  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        48A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX071R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll nytt mærke&nr   '
     A                                      COLOR(WHT)
     A N27                              2  2'1=Lag'
     A                                      COLOR(BLU)
     A                                  3  2'Op Nytt mærke&nr                  -
     A                                                   Språk'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A            W2MN          35A  B  4  5DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A            W2MNSK         2A  B  4 48DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A N30N27                          17  2'F3/12=Avbryt F4=Søk F5=Uppdatera'
     A                                      COLOR(BLU)
     A N30 27                          17  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
