      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS137A MODULE(*LIBL/SS137A *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FGSSTRL01  UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D UsrSpcName      s             10
     D PDF             S              1
     D CURTUR          S             11
     D CURTURN         S             11  0
     D IFSFIL          S             30
     D STEDTIL         S             30
     D TRANSP          S             30
     D ATDDT           S             10
     D ETADT           S             10
     D UNIK            S              7
     D UNIK6           S              6  0
     D LTyp            S              1
     D Dialog          S              1
     D FMELDNR         S              2
     D FEILMELDING     S             80
     D i               s             10i 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  WSFT4                  1     70
     D  WSPOACK               70     70
     D  WSKLDET               69     69
     D  WSM3LM                68     68
     D                 DS
     D  ATDDT2                 1      8  0
     D  ATDDT8                 1      8
     D  ATDDTÅ                 1      4  0
     D  ATDDTM                 5      6  0
     D  ATDDTD                 7      8  0
     D                 DS
     D  ETADT2                 1      8  0
     D  ETADT8                 1      8
     D  ETADTÅ                 1      4  0
     D  ETADTM                 5      6  0
     D  ETADTD                 7      8  0
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      ** Test inputdata -  Ved feil settes Dialog= J -> Ny visning !!
     c                   exsr      tstfeil
      *
     c     FEILMELDING   IFNE      *BLANKS
     C                   exsr      SetSklDial
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('CURTUR':
     C                             %trim(%editc(CURTURN:'Z')))
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('ltyp':ltyp)
     C                   callp     updhtmlvar('pdf':pdf)
     C                   callp     updHtmlVar('STEDTIL':STEDTIL)
     C                   callp     updHtmlVar('TRANSP':TRANSP)
     C                   callp     updhtmlvar('ATDDT':
     C                             %trim(%editw(TRDTS:'    .  .  ')))
     C                   callp     updhtmlvar('ETADT':
     C                             %trim(%editw(TRDTL:'    .  .  ')))
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
     C                   ELSE
     C     curturn       chain     gsstrl01                           33
     C                   IF        *IN33 = *OFF
     C                   EVAL      TRMFT1 = STEDTIL
     C                   EVAL      TRMFT2 = TRANSP
     C                   EVAL      TRDTL  = ETADT2
     C                   EVAL      TRDTS  = ATDDT2
     C                   EVAL      TRFT4  = WSFT4
     C                   UPDATE    GSSTRFR
     C                   END
      * ALT OK - aktuell printjobb skal kjøres...
      *  Kall prog for å generere html-fil i IFS.
     C                   MOVE      CURTURN       TUR              11
     c     PDF           IFEQ      'J'
     C                   eval      IFSFil = 'PackLst'+TUR+LTYP+'.pdf'
     c                   else
     C                   eval      IFSFil = 'PackLst'+TUR+LTYP+'.txt'
     c                   end
     c                   eval      Unik = UsrSpcName
     c     Unik          ifeq      *blanks
     c                   eval      Unik6    = random(1:999999)
     c                   move      Unik6         Unik
     c                   movel     'W'           Unik
     c                   end
     C                   call      'SS137C'
     c                   parm                    IFSFIL
     c                   parm                    PDF
     c                   parm                    TUR
     c                   parm                    LTyp
     c                   parm                    WSUSER
     C                   parm                    WSPOACK
     C                   parm                    WSKLDET
     C                   parm                    WSM3LM
     c                   parm                    UNIK
      * Set output skeleton html member name
      *-dummy html for å få kjørt prog                                      ******
      * "bytter ut seg selv" med Html-fil i IFS umiddelbart                 ******
     C                   exsr      SetSklSpool
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   callp     updhtmlvar('IFSFIL':IFSFIL)
     C                   callp     wrtsection('top')
     c                   end
      *------------------                                                   ******
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C*
     C                   close     BRIDF
     C                   close     GSSTRL01
     C                   eval      *inlr = *on
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   EVAL      WSFT4   = *BLANKS
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      PDF    = zhbgetvar('PDF')
     C                   EVAL      CURTUR  = zhbgetvar('CURTUR')
     C                   eval      CURTURN = c2n2(CURTUR)
     C                   EVAL      STEDTIL = zhbgetvar('STEDTIL')
     C                   EVAL      TRANSP  = zhbgetvar('TRANSP')
     C                   EVAL      ATDDT   = zhbgetvar('ATDDT')
     C                   eval      ATDDT2  = c2n2(ATDDT)
     C                   EVAL      ETADT   = zhbgetvar('ETADT')
     C                   eval      ETADT2  = c2n2(ETADT)
     C                   eval      WSPOACK = zhbgetvar('WSPOACK')
     C                   eval      WSKLDET = zhbgetvar('WSKLDET')
     C                   eval      WSM3LM  = zhbgetvar('WSM3LM')
     C                   eval      Dialog  = zhbgetvar('Dialog')
     C                   eval      LTyp    = zhbgetvar('LTyp')
     c     LTyp          Ifeq      ' '
     c                   move      'P'           LTyp
     c                   end
     c     PDF           IfNE      'J'
     c                   move      'N'           PDF
     c                   end
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklDial    begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SS137A'
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklSpool   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SPLHTMDUM'
     C                   endsr
      *=====================================================================******
      * Test data -  ulike tester ved box (001-900) og bil (901-998)
      *=====================================================================******
     C     TstFeil       begsr
      * Test om bruker tester STEDTIL
     c     STEDTIL       Ifeq      *BLANKS
     C                   eval      FEILMELDING='Destination can not be blanks.'
     c                   eval      Fmeldnr='01'
     C                   Move      'J'           Dialog
     C                   goto      UtTest
     c                   end
      * Test om bruker har gitt transportmåte
     c     TRANSP        Ifeq      *BLANKS
     C                   eval      FEILMELDING='"SENT BY" can not be blanks.'
     c                   eval      Fmeldnr='02'
     C                   Move      'J'           Dialog
     C                   goto      UtTest
     c                   end
      * Test avgangsdato
     c                   MOVE      ATDDTD        UDD               2 0
     c                   MOVE      ATDDTM        UMM               2 0
     c                   MOVE      ATDDTÅ        UÅÅ               2 0
     c                   CALL      'TESTDATO'
     c                   PARM                    UDD
     c                   PARM                    UMM
     c                   PARM                    UÅÅ
     c                   PARM                    UFLAGG            1 0
     c                   IF        UFLAGG = 1
     C                   eval      FEILMELDING='ATD date is not valid.'
     c                   eval      Fmeldnr='03'
     C                   Move      'J'           Dialog
     C                   goto      UtTest
     c                   end
      * Test ankomstdato
     c                   MOVE      ETADTD        UDD               2 0
     c                   MOVE      ETADTM        UMM               2 0
     c                   MOVE      ETADTÅ        UÅÅ               2 0
     c                   CALL      'TESTDATO'
     c                   PARM                    UDD
     c                   PARM                    UMM
     c                   PARM                    UÅÅ
     c                   PARM                    UFLAGG            1 0
     c                   IF        UFLAGG = 1
     C                   eval      FEILMELDING='ETA date is not valid.'
     c                   eval      Fmeldnr='03'
     C                   Move      'J'           Dialog
     C                   goto      UtTest
     c                   end
      *
     C     Uttest        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSTRL01
      * arbeider med turnr som ikke lenger har status ' '/'1'(når ikke KOPI)
      * Sett flagg slik at en kommer inn i feilmeld-rutine og refresher
     c                   move      'N'           FeilStatus        1
     c     Dialog        ifne      'K'
     c     curturn       chain(N)  gsstrl01                           33
     C     *in33         Ifeq      *on
     c                   move      'Y'           FeilStatus
     c                   move      'F'           Dialog
     c                   move      'P'           Ltyp
     C                   end
     C                   end
     C                   endsr
