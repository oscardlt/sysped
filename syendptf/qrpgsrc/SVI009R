********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11013 * 01 CB  PTF11 URSPRUNGSLAND fra SAD EKSPORT
11XXX * 02 CB  PTF11 Varubeskrivning från TARIEU
11XXX * 03 CB  PTF11 Inga decimaler på 1 eller øver 1 Kg
12XXX * 04 CB  PTF12 Godsmerking
********************************************************************
KING  * Totalantall fra export til linje på import
KING FSAEH      IF   E           K DISK
     FSAEV      IF   E           K DISK
     FTARIEU    IF   E           K DISK
     FSVIV      O  A E             DISK
     D                 DS
     D  SVVNT                  1      8S 0
     D  SVVNT6                 1      6
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVIV_SYAV
     C                   PARM                    UVIV_SYOP
     C                   PARM                    UVIH_EUP1
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVIV_SYAV     UVIV_SYAV
     C     *LIKE         DEFINE    SVIV_SYOP     UVIV_SYOP
     C     *LIKE         DEFINE    SVIV_SYOP     UVIV_SYOP
     C     *LIKE         DEFINE    SVAVD         USVAVD
     C     *LIKE         DEFINE    SVTDN         USVTDN
     C     *LIKE         DEFINE    SVIV_EUP1     UVIH_EUP1
     C                   EVAL      USVAVD=UVIV_SYAV
     C                   EVAL      USVTDN=UVIV_SYOP
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVKEY         KLIST
     C                   KFLD                    USVAVD
     C                   KFLD                    USVTDN
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
KING  * Hoved
KING C     SVKEY         CHAIN     SAEH
      * Varuposter
     C     SVKEY         SETLL     SAEV
     C     SVKEY         READE     SAEV                                   53
     C     *IN53         DOWEQ     *OFF
     C                   CLEAR                   SVIVR
      *
     C                   EVAL      SVIV_SYAV=SVAVD
     C                   EVAL      SVIV_SYOP=SVTDN
     C                   EVAL      SVIV_SYLI=SVLI
     C                   EVAL      SVIV_VASL=SVVT01
     C                   EVAL      SVIV_VANO=SVLI
      * De 6 första tecknen är lika i EU
     C                   MOVEL     SVVNT6        SVIV_VATA
      * Finne x-ref ?
     C     SVVNT         CHAIN     TARIEU                             54
     C     *IN54         IFEQ      *OFF
     C     TARIEUI       ANDNE     *BLANKS
     C                   EVAL      SVIV_VATA=TARIEUI
N02  C     TARIEUV       IFNE      *BLANKS
N02  C                   EVAL      SVIV_VASL=TARIEUV
N02  C                   ENDIF
     C                   ENDIF
     C                   EVAL      SVIV_ULKD='NO'
N01  C     SVFYL         IFEQ      '91'
N01  C     SVLK          ANDNE     *BLANKS
N01  C                   EVAL      SVIV_ULKD=SVLK
N01  C                   ENDIF
     C                   EVAL      SVIV_BRUT=SVVKTB
N03  C     SVIV_BRUT     IFGE      1
N03  C                   Z-ADD     SVVKTB        W9N               9 0
N03  C                   Z-ADD     W9N           SVIV_BRUT
N03  C                   ENDIF
     C                   EVAL      SVIV_NETO=SVVKTN
N03  C     SVIV_NETO     IFGE      1
N03  C                   Z-ADD     SVVKTN        W9N
N03  C                   Z-ADD     W9N           SVIV_NETO
N03  C                   ENDIF
     C                   EVAL      SVIV_KOTA=SENTK
     C                   EVAL      SVIV_FOKD='300'
     C                   EVAL      SVIV_EUP1=UVIH_EUP1
     C                   EVAL      SVIV_FABL=SVBELT
     C                   MOVEL     SVEH01        SVIV_KOSL
     C                   EVAL      SVIV_KOTA=SVNT01
N04  C                   MOVEL     SVFT01        SVIV_GODM

     C                   WRITE     SVIVR
     C     SVKEY         READE     SAEV                                   53
     C                   ENDDO
     C                   ENDSR
