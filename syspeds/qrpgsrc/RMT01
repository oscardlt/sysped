     H DATEDIT(*YMD)
      ***********************************************************
      *** VEDLIKEHOLD AV REMOTEREGISTER/STRPASTHR             ***
      ***                                                     ***
      *** INDIKATORER  01 - 26 FUNCTION  / ROLL KEYS          ***
      *** BRUKTE INDIKATORER                                  ***
      ***                                                     ***
      ***  03 - GÅ UT                                         ***
      ***  05 - FRISK OPP                                     ***
      ***  06 - LAG NY                                        ***
      ***  11 - SLETT                                         ***
      ***  12 - ANNULLER                                      ***
      ***  25 - ROLL                                          ***
      ***                                                     ***
      ***  30 - GENERELL FEIL                                 ***
      ***  66 - READ INDICATOR FOR SUBCTL                     ***
      ***  91 - SFLDSP                                        ***
      ***  92 - SFLDSPCTL                                     ***
      ***  93 - SFLCLR                                        ***
      ***  94 - SFLEND                                        ***
      ***  98 - END OF READC SUBFILE                          ***
      ***********************************************************
      *
     FREMOTEF   UF A E           K DISK
     FRMT01FM   CF   E             WORKSTN
     F                                     SFILE(RMT01H50:ZRECNO)
      ***********************************************************
      *    Tabeller
      *
     D TXT             S             75    DIM(10) CTDATA PERRCD(1)
      ***********************************************************
     D                 DS
     D  QCMD1                  1    239
     D  QCM00                  1     10
     D* RMTLOCNMAE
     D  QCM01A                11     21
     D  QCM01P                22     29
     D  QCM01B                30     31
     D* VRTCTL
     D  QCM02A                32     38
     D  QCM02P                39     48
     D  QCM02B                49     50
     D* VRTDEV
     D  QCM03A                51     57
     D  QCM03P                58     67
     D  QCM03B                68     69
     D* MODE
     D  QCM04A                70     74
     D  QCM04P                75     82
     D  QCM04B                83     84
     D* LCLLOCNAME
     D  QCM05A                85     95
     D  QCM05P                96    103
     D  QCM05B               104    105
     D* RMTNETID
     D  QCM06A               106    114
     D  QCM06P               115    122
     D  QCM06B               123    124
     D* SRQ10PGM
     D  QCM07A               125    133
     D  QCM08P               134    143
     D  QCM07C               144    144
     D  QCM07P               145    154
     D  QCM07B               155    156
     D* RMTUSER
     D  QCM09A               157    164
     D  QCM09P               165    174
     D  QCM09B               175    176
     D* RMTPWD
     D  QCM10A               177    183
     D  QCM10P               184    193
     D  QCM10B               194    195
     D* RMTINLPGM
     D  QCM11A               196    205
     D  QCM11P               206    215
     D  QCM11B               216    217
     D* RMTINLMNU
     D  QCM12A               218    227
     D  QCM12P               228    237
     D  QCM12B               238    239
     D                 DS
     D  QCMD2                  1    256
     D* RMTCURLIB
     D  QCM13A                 1     10
     D  QCM13P                11     20
     D  QCM13B                21     22
     D* PASTHRSCN
     D  QCM14A                23     32
     D  QCM14P                33     36
     D  QCM14B                37     37
      *______________________
      * Initiering
      *""""""""""""""""""""""
     C                   EXSR      INIT
      *___________________________
      * Hovedlogikk
      *"""""""""""""""""""""""""""
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVE      *ON           *IN93
     C                   WRITE     RMT01K60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C                   MOVE      *BLANKS       WSTEXT
     C     RMTKE2        SETLL     REMOTEF                            77
      *
     C     START1        TAG
     C                   DO        14
     C     NEXT          TAG
     C                   READ(N)   REMOTEF                                94    <IKKE LOCK
     C     *IN94         IFEQ      *OFF
     C                   MOVEL     RMTEXT        ZSTEXT
     C                   ADD       1             ZRECNO
     C                   WRITE     RMT01H50
     C                   END                                                    <*IN94
     C  N94              ENDDO                                                  <DO 14
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C     ZRECNO        SUB       1             CURS1
     C     CURS1         DIV       14            CURS1
     C     CURS1         MULT      14            CURS1
     C                   ADD       1             CURS1
     C                   ENDIF
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C     VISB30        TAG
     C                   EXFMT     RMT01B30
     C     *IN03         CABEQ     *ON           ENDPGM
     C     *IN06         IFEQ      *ON
     C                   EXSR      LAGNY
     C     *IN03         CABEQ     *ON           ENDPGM
     C     *IN12         CABEQ     *ON           VISB30
     C                   GOTO      START
     C                   END                                                    <*IN06
      *
     C                   ELSE                                                   <ZRECNO
     C                   WRITE     RMT01B40
     C                   EXFMT     RMT01K60
     C                   END                                                    <ZRECNO
      *
     C                   MOVEA     '0000000'     *IN(30)
      *
     C                   SETOFF                                       9998
      *** F3=Gå ut
     C     *IN03         IFEQ      *ON
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F5=Frisk opp
     C     *IN05         IFEQ      *ON
     C                   GOTO      START
     C                   END                                                    <*IN05
      *** F6=Lag ny
     C     *IN06         IFEQ      *ON
     C                   EXSR      LAGNY
     C                   GOTO      START
     C                   END                                                    <*IN06
      *** Roll
     C     *IN25         IFEQ      *ON
     C     *IN94         ANDEQ     *OFF                                          EOF
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      *OFF          *IN25
     C                   GOTO      START1
     C                   END                                                    <*IN25
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
      *** FUNK = 'EN' --> det er valgt en post fra filen
     C     FUNK          IFEQ      'EN'
     C     WSNR          ANDEQ     '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F3=Gå ut
     C     *IN03         IFEQ      *ON
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F12=Annuller
     C     *IN12         IFEQ      *ON
     C                   GOTO      START3
     C                   END                                                    <*IN12
     C                   END                                                    <ZRECNO
      *
     C                   GOTO      START
      *
     C     ENDPGM        TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     GETOPT        BEGSR
      ***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     *OFF
     C                   READC     RMT01H50                               98
     C     *IN98         IFEQ      *ON
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
     C                   MOVEA     '000000'      *IN(30)
     C                   MOVEL     *BLANKS       FUNK
      ***  BLANK
     C     WSNR          IFEQ      ' '
     C                   UPDATE    RMT01H50
     C                   ELSE                                                   <WSNR
     C                   Z-ADD     ZRECNO        CURS1
      *** 1=Starte
     C     WSNR          IFEQ      '1'
     C     *IN57         IFEQ      *OFF
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN31
     C                   UPDATE    RMT01H50
     C                   GOTO      ENDGET
     C                   ELSE                                                   <*IN57
     C                   EXSR      STARTE
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    RMT01H50
     C                   END                                                    <*IN57
     C                   END                                                    <WSNR
      *** 2=Endre
     C     WSNR          IFEQ      '2'
     C     *IN56         IFEQ      *OFF
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN31
     C                   UPDATE    RMT01H50
     C                   GOTO      ENDGET
     C                   ELSE                                                   <*IN56
     C                   EXSR      ENDRE
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    RMT01H50
     C                   END                                                    <*IN56
     C                   END                                                    <WSNR
      *** 4=Slette
     C     WSNR          IFEQ      '4'
     C     *IN56         IFEQ      *OFF
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN31
     C                   UPDATE    RMT01H50
     C                   GOTO      ENDGET
     C                   ELSE                                                   <*IN56
     C                   EXSR      SLETT
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    RMT01H50
     C                   END                                                    <*IN56
     C                   END                                                    <WSNR
      *** 5=Vis
     C     WSNR          IFEQ      '5'
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    RMT01H50
     C                   MOVEL     ZSTEXT        WSTEXT
     C                   MOVE      *ON           *IN60
     C                   EXFMT     RMT01B20
     C                   MOVE      *OFF          *IN60
     C                   END                                                    <WSNR
     C                   END                                                    <WSNR
      *
     C     *IN12         CABEQ     *ON           START3
     C     *IN03         IFEQ      *ON
     C                   GOTO      ENDGET
     C                   END                                                    <*IN03
      *
     C                   ENDDO
      *
     C     ENDGET        TAG
     C                   MOVE      *OFF          *IN54
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UNIK              3
     C                   PARM                    BRUK              1
     C                   PARM                    FUNK              2
      *
     C                   MOVEL     *BLANKS       FUNK
     C                   Z-ADD     0             ZRECNO            4 0
      *
     C     RMTKEY        KLIST
     C                   KFLD                    WSTEXT
      *
     C     RMTKE2        KLIST
     C                   KFLD                    WSTEXT
      *
     C     RMTKE3        KLIST
     C                   KFLD                    ZSTEXT
     C     UNIK          IFEQ      'KKO'
     C                   MOVE      *OFF          *IN67
     C                   MOVEL     TXT(10)       SKODET
     C                   ENDIF                                                  <UNIK
     C     UNIK          IFEQ      'KÅR'
     C                   MOVE      *OFF          *IN67
     C                   MOVEL     TXT(7)        SKODET
     C                   ENDIF                                                  <UNIK
     C     UNIK          IFEQ      'KHE'
     C                   MOVE      *OFF          *IN67
     C                   MOVEL     TXT(8)        SKODET
     C                   ENDIF                                                  <UNIK
     C     UNIK          IFEQ      'RÅR'
     C                   MOVE      *ON           *IN67
     C                   MOVEL     TXT(9)        SKODET
     C                   ENDIF                                                  <UNIK
      *
      * Setter indikatorer avhengig av inngansparametere
      *
      *
     C                   SELECT
     C     BRUK          WHENEQ    '1'                                          <Spørring
     C                   MOVE      *ON           *IN55
     C     BRUK          WHENEQ    '3'                                          <Plukking
     C                   MOVE      *ON           *IN57
     C                   OTHER                                                  <Vedlikehold
     C                   MOVE      *ON           *IN56
     C                   ENDSL
      *
     C                   ENDSR
      ***********************************************
     C     STARTE        BEGSR
      ***********************************************
     C     ZSTEXT        CHAIN     REMOTEF                            90
     C     *IN90         IFEQ      *OFF
      *
     C* STRPASTHR
     C                   MOVEL     'STRPASTH'    QCM00
     C                   MOVE      'R '          QCM00
     C                   MOVEL     RMP01         QCM01P
     C                   MOVEL     RMP02         QCM02P
     C                   MOVEL     RMP03         QCM03P
     C                   MOVEL     RMP04         QCM04P
     C                   MOVEL     RMP05         QCM05P
     C                   MOVEL     RMP06         QCM06P
     C                   MOVEL     RMP07         QCM07P
     C                   MOVEL     RMP08         QCM08P
     C                   MOVEL     RMP09         QCM09P
     C                   MOVEL     RMP10         QCM10P
     C                   MOVEL     RMP11         QCM11P
     C                   MOVEL     RMP12         QCM12P
     C                   MOVEL     RMP13         QCM13P
     C                   MOVEL     RMP14         QCM14P
     C* RMTLOCNAME
     C                   MOVEL     'RMTLOCNA'    QCM01A
     C                   MOVE      'ME('         QCM01A
     C                   MOVEL     ') '          QCM01B
     C* VRTCTL
     C                   MOVEL     'VRTCTL('     QCM02A
     C                   MOVEL     ') '          QCM02B
     C* VRTDEV
     C                   MOVEL     'VRTDEV('     QCM03A
     C                   MOVEL     ') '          QCM03B
     C* MODE
     C                   MOVEL     'MODE('       QCM04A
     C                   MOVEL     ') '          QCM04B
     C* LCLLOCNAME
     C                   MOVEL     'LCLLOCNA'    QCM05A
     C                   MOVE      'ME('         QCM05A
     C                   MOVEL     ') '          QCM05B
     C* RMTNETID
     C                   MOVEL     'RMTNETID'    QCM06A
     C                   MOVE      '('           QCM06A
     C                   MOVEL     ') '          QCM06B
     C* SRQ10PGM
     C                   MOVEL     'SRQ10PGM'    QCM07A
     C                   MOVE      '('           QCM07A
     C                   MOVE      '/'           QCM07C
     C                   MOVEL     ') '          QCM07B
     C* RMTUSER
     C                   MOVEL     'RMTUSER('    QCM09A
     C                   MOVEL     ') '          QCM09B
     C* RMTPWD
     C                   MOVEL     'RMTPWD('     QCM10A
     C                   MOVEL     ') '          QCM10B
     C* RMTINLPGM
     C                   MOVEL     'RMTINLPG'    QCM11A
     C                   MOVE      'M('          QCM11A
     C                   MOVEL     ') '          QCM11B
     C* RMTINLMNU
     C                   MOVEL     'RMTINLMN'    QCM12A
     C                   MOVE      'U('          QCM12A
     C                   MOVEL     ') '          QCM12B
     C* RMTCURLIB
     C                   MOVEL     'RMTCURLI'    QCM13A
     C                   MOVE      'B('          QCM13A
     C                   MOVEL     ') '          QCM13B
     C* PASTHRSCN
     C                   MOVEL     'PASTHRSC'    QCM14A
     C                   MOVE      'N('          QCM14A
     C                   MOVEL     ')'           QCM14B
     C* EXECUTE COMMAND
     C                   Z-ADD     239           QCMDL            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMD1
     C                   PARM                    QCMDL
     C                   ENDIF                                                  <*IN90
     C                   ENDSR
      ***********************************************
     C     ENDRE         BEGSR
      ***********************************************
     C                   MOVE      *ON           *IN62
     C                   MOVEL     ZSTEXT        WSTEXT
     C     RMTKE3        CHAIN(N)  REMOTEF                            90
     C                   MOVEL     RMP01         WSP01
     C                   MOVEL     RMP02         WSP02
     C                   MOVEL     RMP03         WSP03
     C                   MOVEL     RMP04         WSP04
     C                   MOVEL     RMP05         WSP05
     C                   MOVEL     RMP06         WSP06
     C                   MOVEL     RMP07         WSP07
     C                   MOVEL     RMP08         WSP08
     C                   MOVEL     RMP09         WSP09
     C                   MOVEL     RMP10         WSP10
     C                   MOVEL     RMP11         WSP11
     C                   MOVEL     RMP12         WSP12
     C                   MOVEL     RMP13         WSP13
     C                   MOVEL     RMP14         WSP14
     C     END01         TAG
     C                   EXFMT     RMT01B20
     C                   MOVEA     '000000'      *IN(30)
     C     *IN03         CABEQ     *ON           END99
     C     *IN12         CABEQ     *ON           END99
      *
      * Sjekker at koden det endres til ikke finnes fra før
      *
     C     WSTEXT        IFNE      ZSTEXT
     C     RMTKEY        SETLL     REMOTEF                                90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN32
     C                   GOTO      END01
     C                   ENDIF                                                  <*IN90
     C                   ENDIF                                                  <WSTEXT
      *
      * Oppdaterer posten
      *
     C     RMTKE3        CHAIN     REMOTEF                            90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     WSTEXT        RMTEXT
     C                   MOVEL     WSP01         RMP01
     C                   MOVEL     WSP02         RMP02
     C                   MOVEL     WSP03         RMP03
     C                   MOVEL     WSP04         RMP04
     C                   MOVEL     WSP05         RMP05
     C                   MOVEL     WSP06         RMP06
     C                   MOVEL     WSP07         RMP07
     C                   MOVEL     WSP08         RMP08
     C                   MOVEL     WSP09         RMP09
     C                   MOVEL     WSP10         RMP10
     C                   MOVEL     WSP11         RMP11
     C                   MOVEL     WSP12         RMP12
     C                   MOVEL     WSP13         RMP13
     C                   MOVEL     WSP14         RMP14
     C                   UPDATE    REMOTER
     C                   ENDIF
      *
     C     END99         TAG
     C                   MOVE      *OFF          *IN62
     C                   ENDSR
      ***********************************************
     C     LAGNY         BEGSR
      ***********************************************
     C                   MOVE      *ON           *IN63
     C                   MOVEL     *BLANKS       WSTEXT
     C                   MOVEL     *BLANKS       WSP01
     C                   MOVEL     '*NONE   '    WSP02
     C                   MOVEL     '*NONE   '    WSP03
     C                   MOVEL     '*NETATR '    WSP04
     C                   MOVEL     '*LOC    '    WSP05
     C                   MOVEL     '*LOC    '    WSP06
     C                   MOVEL     '*SRQMNU '    WSP07
     C                   MOVEL     *BLANKS       WSP08
     C                   MOVEL     '*NONE   '    WSP09
     C                   MOVEL     '*NONE   '    WSP10
     C                   MOVEL     'RMTUSRPR'    WSP11
     C                   MOVE      'F'           WSP11
     C                   MOVEL     'RMTUSRPR'    WSP12
     C                   MOVE      'F'           WSP12
     C                   MOVEL     'RMTUSRPR'    WSP13
     C                   MOVE      'F'           WSP13
     C                   MOVEL     '*YES    '    WSP14
     C     LAG01         TAG
     C                   EXFMT     RMT01B20
     C                   MOVEA     '000000'      *IN(30)
     C     *IN03         CABEQ     *ON           LAG99
     C     *IN12         CABEQ     *ON           LAG99
      *
      * Sjekker at koden ikke finnes fra før eller er blank
      *
     C     WSTEXT        IFEQ      *BLANKS
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN33
     C                   GOTO      LAG01
     C                   ENDIF                                                  <WSTEXT
     C     RMTKEY        SETLL     REMOTEF                                90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN32
     C                   GOTO      LAG01
     C                   ENDIF                                                  <*IN90
      *
      * Skriver posten
      *
     C                   MOVEL     WSTEXT        RMTEXT
     C                   MOVEL     WSP01         RMP01
     C                   MOVEL     WSP02         RMP02
     C                   MOVEL     WSP03         RMP03
     C                   MOVEL     WSP04         RMP04
     C                   MOVEL     WSP05         RMP05
     C                   MOVEL     WSP06         RMP06
     C                   MOVEL     WSP07         RMP07
     C                   MOVEL     WSP08         RMP08
     C                   MOVEL     WSP09         RMP09
     C                   MOVEL     WSP10         RMP10
     C                   MOVEL     WSP11         RMP11
     C                   MOVEL     WSP12         RMP12
     C                   MOVEL     WSP13         RMP13
     C                   MOVEL     WSP14         RMP14
     C                   WRITE     REMOTER
      *
     C     LAG99         TAG
     C                   MOVE      *OFF          *IN63
     C                   ENDSR
      ***********************************************
     C     SLETT         BEGSR
      ***********************************************
     C                   MOVE      *ON           *IN61
     C                   MOVEL     ZSTEXT        WSTEXT
     C     SLE01         TAG
     C                   EXFMT     RMT01B20
     C                   MOVEA     '000000'      *IN(30)
     C                   MOVEL     *BLANKS       MSGTXT
     C     *IN03         CABEQ     *ON           SLE99
     C     *IN12         CABEQ     *ON           SLE99
      *
      * Sletting må bekreftes med F11
      *
     C     *IN11         IFNE      *ON
     C                   MOVE      *ON           *IN30
     C                   MOVEL     TXT(1)        MSGTXT
     C                   GOTO      SLE01
     C                   ENDIF                                                  <*IN11
      *
      * Sletter posten
      *
     C     RMTKEY        CHAIN     REMOTEF                            90
     C     *IN90         IFEQ      *OFF
     C                   DELETE    REMOTER
     C                   ENDIF
      *
     C     SLE99         TAG
     C                   MOVE      *OFF          *IN61
     C                   ENDSR
      ***********************************************
      * COMPILE TIME ARRAY
      ***********************************************
**     TXT
Bekreft sletting med F11 eller trykk F12 for å annullere





    Sakstekst
  Henvendelsmåte
     Årsak
   Konklusjon
