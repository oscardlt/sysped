********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF12 Chain med update   .....
12XXX * 02 YBC PTF12 Ny fil SADVCN
********************************************************************
     H DFTACTGRP(*NO)
     fsadh      uf a e           k disk
     fsadhko    uf a e           k disk
     fsadf      uf a e           k disk
     fsadfko    uf a e           k disk
     fsadv      uf a e           k disk
     fsadvko    uf a e           k disk
N02  fsadvcn    uf a e           k disk
N02  fsadvcnko  uf a e           k disk
     fsadvj     uf a e           k disk
     fsadvjko   uf a e           k disk
     fsadw      uf a e           k disk
     fsadwko    uf a e           k disk

     d xst             s              1a
     ‚*  Prototype for sad061x
     d sad061x         pr
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d umod_                               like(umod)
     ‚*  *ENTRY Interface for Main Procedure
     d sad061x         pi
     d uavd                           4  0
     d utdn                           7  0
     d umod                           1

      /free

       select;
       when umod = '1';    // Bevar opprinnelig SAD
         // Hode
         chain(n) (uavd: utdn) sadhko;
         if not %found;
           chain (uavd: utdn) sadh;
           if %found;
             write sadhkor;
           endif;
         endif;

         // Faktura
         chain(n) (uavd: utdn) sadfko;
         if not %found;
           setll (uavd: utdn) sadf;
           reade (uavd: utdn) sadf;
           dow not %eof;
             write sadfkor;
             reade (uavd: utdn) sadf;
           enddo;
         endif;

         // Linje
         chain(n) (uavd: utdn) sadvko;
         if not %found;
           setll (uavd: utdn) sadv;
           reade (uavd: utdn) sadv;
           dow not %eof;
             write sadvkor;
             reade (uavd: utdn) sadv;
           enddo;
         endif;
N02
N02      // Linje - container
N02      chain(n) (uavd: utdn) sadvcnko;
N02      if not %found;
N02        setll (uavd: utdn) sadvcn;
N02        reade (uavd: utdn) sadvcn;
N02        dow not %eof;
N02          write sadvcnkor;
N02          reade (uavd: utdn) sadvcn;
N02        enddo;
N02      endif;

         // Linje - justering
         chain(n) (uavd: utdn) sadvjko;
         if not %found;
           setll (uavd: utdn) sadvj;
           reade (uavd: utdn) sadvj;
           dow not %eof;
             write sadvjkor;
             reade (uavd: utdn) sadvj;
           enddo;
         endif;

         // Linje - bilfortolling
         chain(n) (uavd: utdn) sadwko;
         if not %found;
           setll (uavd: utdn) sadw;
           reade (uavd: utdn) sadw;
           dow not %eof;
             write sadwkor;
             reade (uavd: utdn) sadw;
           enddo;
         endif;


       when umod = '2';    // Slett bevarte SAD
         // Hode
         chain (uavd: utdn) sadhko;
         if %found;
           if sist = '7' or sist = '8' or sist = '9';
             update sadhkor;                 // Ikke slett pga. allerede tilbake. Ingen aksjon
             *inlr = *on;
             return;
           endif;

           sist = '7';                       // Merke ferdig med oppbevaring
           update sadhkor;
           *inlr = *on;
           return;
           //delete sadhkor;
         endif;

         // Faktura
         setll (uavd: utdn) sadfko;
         reade (uavd: utdn) sadfko;
         dow not %eof;
           delete sadfkor;
           reade (uavd: utdn) sadfko;
         enddo;

         // linje
         setll (uavd: utdn) sadvko;
         reade (uavd: utdn) sadvko;
         dow not %eof;
           delete sadvkor;
           reade (uavd: utdn) sadvko;
         enddo;
N02
N02      // linje container
N02      setll (uavd: utdn) sadvcnko;
N02      reade (uavd: utdn) sadvcnko;
N02      dow not %eof;
N02        delete sadvcnkor;
N02        reade (uavd: utdn) sadvcnko;
N02      enddo;

         // linje justering
         setll (uavd: utdn) sadvjko;
         reade (uavd: utdn) sadvjko;
         dow not %eof;
           delete sadvjkor;
           reade (uavd: utdn) sadvjko;
         enddo;

         // linje bilfortolling
         setll (uavd: utdn) sadwko;
         reade (uavd: utdn) sadwko;
         dow not %eof;
           delete sadwkor;
           reade (uavd: utdn) sadwko;
         enddo;

       when umod = '3';    // Flytt bevarte SAD tilbake
S01    // PTF01 chain(n) (uavd: utdn) sadhko;
N01      chain    (uavd: utdn) sadhko;
         if %found;
           if sist = '7' or sist = '8' or sist = '9';
           // Allerede er flyttet tilbake. Ingen aksjon
             update sadhkor;
             *inlr = *on;
             return;
           endif;

           // Hode
           chain (uavd: utdn) sadh;
           if %found;
             xst = sist;
             delete sadhr;
           endif;
           chain (uavd: utdn) sadhko;
           sist = xst;
           si0035 = '7';
           write sadhr;
           // delete sadhkor;
           sist = '9';
           update sadhkor;

           // Faktura
           setll (uavd: utdn) sadf;
           reade (uavd: utdn) sadf;
           dow not %eof;
             delete sadfr;
             reade (uavd: utdn) sadf;
           enddo;
           setll (uavd: utdn) sadfko;
           reade(n) (uavd: utdn) sadfko;
           dow not %eof;
             write sadfr;
             // delete sadfkor;
             reade(n) (uavd: utdn) sadfko;
           enddo;

           // linje
           setll (uavd: utdn) sadv;
           reade (uavd: utdn) sadv;
           dow not %eof;
             delete sadvr;
             reade (uavd: utdn) sadv;
           enddo;
           setll (uavd: utdn) sadvko;
           reade(n) (uavd: utdn) sadvko;
           dow not %eof;
             write sadvr;
             // delete sadvkor;
             reade(n) (uavd: utdn) sadvko;
           enddo;
N02
N02        // linje container
N02        setll (uavd: utdn) sadvcn;
N02        reade (uavd: utdn) sadvcn;
N02        dow not %eof;
N02          delete sadvcnr;
N02          reade (uavd: utdn) sadvcn;
N02        enddo;
N02        setll (uavd: utdn) sadvcnko;
N02        reade(n) (uavd: utdn) sadvcnko;
N02        dow not %eof;
N02          write sadvcnr;
N02          // delete sadvcnkor;
N02          reade(n) (uavd: utdn) sadvcnko;
N02        enddo;

           // linje justering
           setll (uavd: utdn) sadvj;
           reade (uavd: utdn) sadvj;
           dow not %eof;
             delete sadvjr;
             reade (uavd: utdn) sadvj;
           enddo;
           setll (uavd: utdn) sadvjko;
           reade(n) (uavd: utdn) sadvjko;
           dow not %eof;
             write sadvjr;
             // delete sadvjkor;
             reade(n) (uavd: utdn) sadvjko;
           enddo;

           // linje bilfortolling
           setll (uavd: utdn) sadw;
           reade (uavd: utdn) sadw;
           dow not %eof;
             delete sadwr;
             reade (uavd: utdn) sadw;
           enddo;
           setll (uavd: utdn) sadwko;
           reade(n) (uavd: utdn) sadwko;
           dow not %eof;
             write sadwr;
             // delete sadwkor;
             reade(n) (uavd: utdn) sadwko;
           enddo;

         endif;
       endsl;

       *inlr = *on;
       return;

      /end-free
