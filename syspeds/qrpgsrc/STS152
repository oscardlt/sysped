      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STS152 MODULE(*LIBL/STS152 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FGSSOLL01  IF   E           K DISK    usropn
     FGSSOLL04  IF   E           K DISK    usropn
     F                                     RENAME(GSSOLFR:GSSOLFR4)
     FGSSORF    IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D XXUSER          S             10
     D XXTURNR         S             11
     D XXPGM           S             10
     D FEILKODE        s              1
     D lstbrd          s              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D i               s             10i 0
      *
      *
     D                 DS
     D  XXCLID35               1     35
     D  XXPRE                  1      3  0
     D  XXTRNO                 4     14  0
     D  XXBNO                 15     17  0
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
     C                   EXSR      INIT
      *
     C                   SELECT
     C                   WHEN      *IN60
     C                   MOVE      '1'           FEILKODE
     C                   exsr      SetSkl51B
     C                   WHEN      BIINN = ' '
     C                   MOVE      '1'           FEILKODE
     C                   exsr      SetSkl51B
      * Blank, ikke gjør noe..(vis bilde på ny)
     C                   WHEN      XXCLID35 = *blanks
     C                   EXSR      SetSkl152
     C                   OTHER
     C     GSSOLL01K     CHAIN     GSSOLL01                           33
     C   33XXCLID35      CHAIN     GSSOLL04                           33
     C                   ENDSL
      *
     C                   IF        *IN33
     C                   MOVE      '3'           FEILKODE
     C                   EXSR      SetSkl152
     C                   ELSE
     C     OLONO         CHAIN     GSSORF                             33
     C                   SELECT
     C                   WHEN      ((ORST > '3') OR (ORST = '2'))
     C                   MOVE      '2'           FEILKODE
     C                   EXSR      SetSkl152
     C                   WHEN      ((ORTNO <> XXTNO) AND
     C                              (XXTNO <> 0))
     C                   MOVE      '4'           FEILKODE
     C                   EXSR      SetSkl152
     C                   OTHER
     C                   EXSR      SetSkl153
     C                   ENDSL
     C                   END
      *
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * 1-Section /$top
     C                   exsr      SetTop
     C                   SELECT
     C                   WHEN      *IN60
     C                   callp     wrtsection('all')
     C                   WHEN      BIINN = ' '
     C                   callp     wrtsection('all')
     C                   OTHER
     C                   callp     wrtsection('top')
     C                   ENDSL
      *
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     GSSOLL01
     C                   close     GSSOLL04
     C                   close     GSSORF
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
      *
     C                   eval      XXCLID35 = zhbgetvar('CLID')
     C                   eval      XXUSER = zhbgetvar('USER')
     C                   eval      XXTURNR= zhbgetvar('TURNR')
     C                   eval      XXPGM  = zhbgetvar('PGM')
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl51B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS51B'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl152     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   IF        XXPGM = 'STS152'
     C                   eval      Mbr = 'STS152'
     C                   ELSE
     C                   eval      Mbr = 'STS52'
     C                   END
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl153     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   IF        ORST = '3'
     C                   eval      Mbr = 'STS153'
     C                   ELSE
     C                   eval      Mbr = 'STS53'
     C                   END
     C                   MOVE      ORTNO         XXTURNR
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      * Color for text, background, links, visited links, active links
     C                   exsr      setcolor
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   callp     updhtmlvar('CLID':XXCLID35)
     C                   callp     updhtmlvar('TURNR':XXTURNR)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
      *
     C                   endsr
      *=====================================================================******
      * Color for font, background, links, visited links, active links
      *=====================================================================******
     C     SetColor      begsr
     C                   eval      BckGnd = uppify(BckGnd)
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('TXTCOLOR':'white')
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
     C                   endsr
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     GSSOLL01K     KLIST
     C                   KFLD                    XXTRNO
     C                   KFLD                    XXBNO
      *
     C                   open      BRIDF
     C     XXUSER        CHAIN     BRIDF                              60
     C                   IF        *IN60 = *OFF
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSOLL01
     C                   open      GSSOLL04
     C                   open      GSSORF
     C                   MOVE      XXTURNR       XXTNO            11 0
     C                   END
     C                   MOVE      *BLANKS       FEILKODE
      *
     C                   ENDSR
