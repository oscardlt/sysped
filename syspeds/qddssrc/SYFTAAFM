********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 02 YBC PTF11 OMBEREGNING
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                  1  2'SYSPED/SYFTAA'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 DIV1703 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 23MSGCON(045 DIV1704 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(034 SSI0030 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0B  6 39DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  COLOR(RED)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(078 SPA0001 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE02
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Gå til forrige bilde')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFTAA'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 DIV1703 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(045 DIV1704 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 68TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  4 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  4MSGCON(030 DIV1707 *LIBL/SYMSGF)
     A            WSDL           1A  B  8 37TEXT('BRUKE DATALISTE ?')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A                                      VALUES('J' 'N' '1' ' ')
     A  99                                  COLOR(RED)
     A                                  8 45MSGCON(025 DIV1708 *LIBL/SYMSGF)
     A                                  9  6MSGCON(015 DIV1709 *LIBL/SYMSGF)
     A            WSA4           1A  B  9 37DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  9 45MSGCON(009 DIV1710 *LIBL/SYMSGF)
     A                                 11  4MSGCON(030 DIV1714 *LIBL/SYMSGF)
     A            WSTDN          7Y 0B 11 37TEXT('TELLER TIL SAD-IMPORT')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  95                                  COLOR(RED)
     A                                 12  4MSGCON(030 DIV1715 *LIBL/SYMSGF)
     A            WSTOLK         1A  B 12 37TEXT('BRUKE TOLLKOMMUNIKASJON ?')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(PC)
     A  94                                  COLOR(RED)
     A                                 13  4'Importeres eksterne data ? '
     A            WSEKST         1   B 13 37VALUES('J' 'N' 'R')
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 13 45'J/N/R  Ved R kan egen ref'
     A                                 14 45'inntastes for søking'
      *
     A          R BILDE03
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Gå til forrige bilde')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFTAA'
     A                                      COLOR(BLU)
     A                                  1 23MSGCON(045 DIV1703 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 69DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 23MSGCON(045 DIV1704 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 69TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  4 21TEXT('AVDELINGSNAVN')
     A                                      COLOR(WHT)
     A                                  5  2'TVINN . :'
     A            WSTOLK         1A  O  5 15TEXT('BRUKE TOLLKOMMUNIKASJON ?')
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  9  4'Vent å utskrive inngende melding?'
     A            WSST           1A  B  9 43DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  9 52'J/N'
     A N45                             10  4MSGCON(033 DIV1719 *LIBL/SYMSGF)
     A            WSTD0A         8Y 0B 10 43TEXT('DEKLARANTENS REFRANSE')
     A  45                                  DSPATR(PR)
     A  45                                  DSPATR(ND)
     A                                 11  4MSGCON(033 DIV1720 *LIBL/SYMSGF)
     A            WSTDB4         6Y 0B 11 43TEXT('TIL EKSP. ENHET')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  96                                  COLOR(RED)
N02  A                                 11 54'SO:'
N02  A            WSTDB4O1       6Y 0B 11 58TEXT('TIL EKSP. ENHET')
N02  A                                      EDTCDE(4)
N02  A                                 11 65'EB/RE:'
N02  A            WSTDB4O2       6Y 0B 11 72TEXT('TIL EKSP. ENHET')
N02  A                                      EDTCDE(4)
     A                                 12  4MSGCON(036 SHU0791 *LIBL/SYMSGF)
     A            WSFORE         9   O 12 43COLOR(WHT)
     A  97                                  DSPATR(RI)
     A                                 12 54'('
     A                                      COLOR(WHT)
     A            KOAKNR         8  0O 12 56COLOR(WHT)
     A                                      EDTCDE(4)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                 12 65')'
     A                                      COLOR(WHT)
     A                                 13  4MSGCON(033 DIV1722 *LIBL/SYMSGF)
     A            WSNODA        35A  B 13 43TEXT('NODI ID AVDELINGEN')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(PC)
     A  94                                  COLOR(RED)
     A                                 14  6MSGCON(033 DIV1721 *LIBL/SYMSGF)
     A            WSNODT        35A  B 14 43TEXT('NODI ID TOLLVESENET')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(PC)
     A  95                                  COLOR(RED)
     A                                 15  8'Application Reference . . . . :'
     A            W0026         14A  B 15 43
     A                                 17  4MSGCON(033 DIV1723 *LIBL/SYMSGF)
     A            W0035          1A  B 17 43TEXT('Testindikator')
     A                                      VALUES(' ' '1' '2')
     A                                 17 52MSGCON(018 DIV1724 *LIBL/SYMSGF)
     A                                 19  4'Skriv tollkv. ved utlev.attest . .-
     A                                      :'
     A            WSUATK         1A  B 19 43VALUES('J' 'N')
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE04
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Gå til forrige bilde')
     A                                  1  2'SYSPED/SYFTAA'
     A                                      COLOR(BLU)
     A                                  1 20'Standardverdier tolldekl. import'
     A                                      COLOR(WHT)
     A                                  1 54MSGCON(008 DIV1726 *LIBL/SYMSGF)
     A            WSDTY          2A  B  1 64TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDP           2Y 0B  1 67TEXT('Dekl.prosedyre')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                  1 72MSGCON(008 DIV1727 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  2  2MSGCON(010 DIV1728 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  2 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAS         30A  B  3  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 41MSGCON(008 DIV1729 *LIBL/SYMSGF)
     A            WSNTK          7Y 0B  3 50TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 58MSGCON(004 DIV1730 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0O  3 63COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  3 68COLOR(WHT)
     A                                      EDTCDE(4)
     A            SISG           3A  O  3 76COLOR(WHT)
     A            WSADS1        30A  B  4  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 41MSGCON(015 DIV1731 *LIBL/SYMSGF)
     A                                  4 60'D/K'
     A                                  4 68MSGCON(012 DIV1732 *LIBL/SYMSGF)
     A            WSADS2        30A  B  5  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSKI          1A  B  5 48TEXT('Selg,Kjøper,Ingen')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'S' 'K' 'I')
     A            WSKDDK         1A  B  5 61TEXT('Dagsoppgjør/Kontant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVKB          9Y 0B  5 69TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSADS3        30A  B  6  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2MSGCON(010 DIV1733 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B  7 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 22MSGCON(005 DIV1734 *LIBL/SYMSGF)
     A            WSRG          11A  B  7 28TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 40MSGCON(016 DIV1735 *LIBL/SYMSGF)
     A                                  7 58MSGCON(016 DIV1736 *LIBL/SYMSGF)
     A                                  7 77'+/-'
     A            WSNAK         30A  B  8  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVAL1         3A  B  8 40TEXT('Valutakode Frakt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSBEL1        11Y 2B  8 44TEXT('Beløp tollb.frakt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVAL2         3A  B  8 58TEXT('Val.kode A. kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSBEL2        11Y 2B  8 62TEXT('Beløp A. kost')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSFTG2         1A  B  8 78TEXT('Fortegn +/- a.kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK1        30A  B  9  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40MSGCON(016 DIV1737 *LIBL/SYMSGF)
     A            WSLKA          2A  B  9 57TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 62MSGCON(007 DIV1738 *LIBL/SYMSGF)
     A            WSRAB          4Y 2B  9 70DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSADK2        30A  B 10  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 11  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 40MSGCON(009 DIV1739 *LIBL/SYMSGF)
     A            WSKDC          1A  B 11 50TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  1MSGCON(012 DIV1740 *LIBL/SYMSGF)
     A            WSTLF         12A  B 12 14TEXT('Telefon Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 40MSGCON(014 DIV1741 *LIBL/SYMSGF)
     A            WSLV           3A  B 12 55TEXT('Leveringsvilkår kod')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLVT         17A  B 12 59TEXT('Leveringsvilkår txt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAD         30A  B 13  2TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 40MSGCON(018 DIV1742 *LIBL/SYMSGF)
     A                                 13 59MSGCON(010 DIV1743 *LIBL/SYMSGF)
     A                                 13 70MSGCON(010 DIV1744 *LIBL/SYMSGF)
     A                                 14  1MSGCON(010 DIV1745 *LIBL/SYMSGF)
     A            WSTRID        17A  B 14 12TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKT          2A  B 14 30TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVAL3         3A  B 14 40TEXT('Val.kode Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSBEL3        13Y 2B 14 44TEXT('Beløp Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVKU          7Y 3B 14 60TEXT('Valutakurs')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTST          1Y 0B 14 71TEXT('Transaksjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 15  1MSGCON(018 DIV1746 *LIBL/SYMSGF)
     A            WSTRM          2Y 0B 15 20TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 15 38MSGCON(010 DIV1747 *LIBL/SYMSGF)
     A            WSFIF         17A  B 15 49TEXT('Finans oppl.faknr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 68MSGCON(005 DIV1748 *LIBL/SYMSGF)
     A            WSFID          6Y 0B 15 74TEXT('Finans oppl.fakdat')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16  1MSGCON(010 DIV1749 *LIBL/SYMSGF)
     A            WSLS          16A  B 16 12TEXT('Lagringssted tekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKDLS         2A  B 16 29TEXT('Lagringssted kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16 32'48.KT.'
     A            WSKTC          1A  B 16 39DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKTA          5Y 0B 16 41TEXT('Tollkontonr-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKTB          2Y 0B 16 47TEXT('Tollkontonr-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 50'49.Godsn'
     A            WSGN          15A  B 16 59TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  4MSGCON(035 DIV1752 *LIBL/SYMSGF)
     A            WSPOS          9A  B 17 59TEXT('Posisjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  1'31.'
     A            WSFT1         45A  B 18  5TEXT('Fritekst-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*                                18 55MSGCON(005 DIV1753 *LIBL/SYMSGF)
S01  A*           WSKDH          2Y 0B 18 61TEXT('Havnekode')
N01  A                                 18 53MSGCON(005 DIV1753 *LIBL/SYMSGF)
N01  A            WSKDH          4Y 0B 18 59TEXT('Havnekode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A*                                18 64MSGCON(007 DIV1754 *LIBL/SYMSGF)
     A*           WSKDTR         1A  B 18 72TEXT('Transportavgift kode')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A*           WSAS           5Y 1B 18 74TEXT('Avgiftssats transp')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A*                                     EDTCDE(4)
     A            WSFT2         45A  B 19  5TEXT('Fritekst-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19 53MSGCON(008 TRO0499 *LIBL/SYMSGF)
     A            SITARF        16A  O 19 62TEXT('TARIFFØRSNAVN')
     A                                      COLOR(WHT)
     A                                 20  1'44.'
     A            WSFT3         45A  B 20  5TEXT('Fritekst-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 53MSGCON(005 DIV1748 *LIBL/SYMSGF)
     A                                 20 66MSGCON(007 DIV1756 *LIBL/SYMSGF)
     A                                 20 75MSGCON(005 DIV1757 *LIBL/SYMSGF)
     A            WSFT4         45A  B 21  5TEXT('Fritekst-4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDTG         10A  B 21 53TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTLL          5A  B 21 66TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTLE          3A  B 21 75TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22  1MSGCON(005 DIV1758 *LIBL/SYMSGF)
     A            WSDST         15A  B 22  7TEXT('Deklarantens sted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 25MSGCON(018 DIV1759 *LIBL/SYMSGF)
     A            SIDT           8Y 0H
     A            WSDT           6Y 0O 22 44TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                 23  1MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
