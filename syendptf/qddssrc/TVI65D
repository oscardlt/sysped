     A*%%TS  SD  20220106  113214  SVEIN       REL-V7R4M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TVI65PNL)
     A                                      HLPTITLE('EDI UTVEKSLINGER')
     A                                      HELP
     A                                      ALTHELP
     A          R TVI65DA
     A                                      OVERLAY
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 28'Meldinger som er ubesvarte  '
     A                                      COLOR(WHT)
     A                                  1 73'TVI65D A'
     A                                      COLOR(BLU)
     A                                  2 37'Start'
     A                                      COLOR(WHT)
     A                                  4  2'Velg og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  6  2'Avdeling . . . . . . . . . . . . .-
     A                                       . :'
     A            ZAVD      R     D  B  6 43REFFLD(MAVD)
     A  31                                  COLOR(RED)
     A                                  6 56'Nummer,  0=alle'
     A                                  8  2'Til og med dato  . . . . . . . . .-
     A                                       . :'
     A            ZDT       R        B  8 43REFFLD(MDT)
     A  32                                  DSPATR(PC)
     A  32                                  COLOR(RED)
     A                                  8 56'≈≈MMDD'
     A                                  9  4'Tid  . . . . . . . . . . . . . . .-
     A                                       :'
     A            ZTM       R        B  9 43REFFLD(MTM)
     A  33                                  DSPATR(PC)
     A  33                                  COLOR(RED)
     A                                  9 56'TTMMSS'
     A                                 23  2'F3=GÂ ut'
     A                                      COLOR(BLU)
     A          R TVI65DB                   SFL
     A*%%TS  SD  20220106  113214  SVEIN       REL-V7R4M0  5770-WDS
     A            MSN       R        H
     A            MMN       R        H
     A            MSR       R        H
     A            ZOP            1A  I 10  3VALUES(' ' '4' '5' '7')
     A  30                                  COLOR(RED)
     A  30                                  DSPATR(PC)
     A            MAVD      R        O 10  6EDTCDE(4)
     A            MTDN      R        O 10 11EDTCDE(4)
     A            MDT       R        H
     A            MDT6           6S 0O 10 19
     A            MTM       R        O 10 26EDTWRD(' 0:  :  ')
     A            M0035     R        O 10 35
     A            M0065     R        H
     A            M1001     R        H
     A            WSIMEX         6A  O 10 37
     A            M1N07     R        O 10 44
     A            M0068A    R        H
     A            M3039E    R        O 10 48EDTCDE(4)
     A            M006825       25A  O 10 55
     A            AVSMOT        40A  O 10 81
     A          R TVI65DC                   SFLCTL(TVI65DB)
     A*%%TS  SD  20220106  113214  SVEIN       REL-V7R4M0  5770-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('TVIN Meldingslogg, Heading')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      CF14(14 'Forandre ventestatus')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 29'Meldinger som er ubesvarte'
     A                                      COLOR(WHT)
     A                                  1 73'TVI65D B'
     A                                      COLOR(BLU)
     A                                  2 38'Valg'
     A                                      COLOR(WHT)
     A                                  4  2'Avdeling:'
     A            ZAVD      R        O  4 12REFFLD(MAVD)
     A                                      EDTCDE(4)
     A                                  4 19'Til og med dato:'
     A            ZDT       R        O  4 36REFFLD(MDT)
     A                                  4 47'Tid:'
     A            ZTM       R        O  4 52REFFLD(MTM)
     A                                      EDTWRD(' 0:  :  ')
     A                                  6  2'Velg og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  7  4'4=Besvare internt   5=Vise   7=Vis-
     A                                      e fysisk komponent'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  8 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  9  2'Alt'
     A                                      COLOR(WHT)
     A                                  9  6'Avd.'
     A                                      COLOR(WHT)
     A                                  9 11'Tdek.nr'
     A                                      COLOR(WHT)
     A                                  9 19'Sent'
     A                                      COLOR(WHT)
     A                                  9 35'T'
     A                                      COLOR(WHT)
     A                                  9 37'Imp/Ex'
     A                                      COLOR(WHT)
     A                                  9 44'Fun'
     A                                      COLOR(WHT)
     A                                  9 48'Exp.st'
     A                                      COLOR(WHT)
     A                                  9 55'           Dato  Sekve Ve Avsender-
     A                                      -Mottager                          -
     A                                            '
     A                                      COLOR(WHT)
     A          R TVI65DD
     A                                      OVERLAY
     A                                 23  2'F3=GÂ ut   F5=Forny   F12=Annuller'
     A                                      COLOR(BLU)
     A          R TVI65Dÿ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
