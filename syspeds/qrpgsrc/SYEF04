      *
      ************************************************************
      *** PROGRAM SKAL VISE OPPLYSNINGER AV SELGER/KJØPER      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01-11 13-26                       ***
      *** LEDIGE INDIKATORER 27-32 34-99                       ***
      ************************************************************
      *
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FFRILKSD   UF A E           K DISK
     FSYEF04FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WST1                   1     79
     D  WST2                  80    158
     D  WST3                 159    237
     D  WST4                 238    316
     D  WST5                 317    395
     D  WST6                 396    474
     D  WST7                 475    553
     D  WST8                 554    632
     D  WST9                 633    711
     D  WST10                712    790
     D  WST11                791    869
     D  WST12                870    948
     D  WST13                949   1027
     D  WST14               1028   1106
     D  WST15               1107   1185
     D  WST16               1186   1264
     D  WST17               1265   1343
     D  WST18               1344   1422
     D  WST19               1423   1501
     D  WST20               1502   1580
     D  TX                     1   1580
     D                                     DIM(20)
     D                 DS
     D  HA                     1     20
     D                                     DIM(20)
     D  HAR                    1     20
     D                 DS
     D  KLKNVN                 1     30
     D  KLKNV4                 1      4
     C*
     C                   EXSR      INIT
     C*** UFLAGG=1 BETYR PROGRAMKALLE FRA REGISTRERINGSPROGRAM
     C*** UFLAGG<>1 BETYR PROGRAMKALLE FRA PROGRAM SOM SKAL VISE TEKST
     C     REFR          TAG
     C     UFLAGG        COMP      1                                      27
     C*
     C*
     C     START         TAG
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
      *
      * TEST KUNDE
      *
     C                   EXSR      TESTKU
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYIN2        SETLL     FRILKSD
     C     START1        TAG
     C     KEYIN2        READE(N)  FRILKSD                                94
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
      * GÅR I LØKKE OG LESER POSTER
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C*** HENT LAND OG  DESTINASJON
     C                   MOVE      *BLANKS       KLKNVN
     C                   MOVE      *BLANKS       ST2NVN
     C     KEYSD2        CHAIN     STED2                              33
     C   33              MOVEL     '*ALLE*'      ST2NVN
     C     KEYLA2        CHAIN     KODTLK                             33
     C*
     C                   WRITE     SUBFIL
     C     KEYST2        SETLL     FRILKSD
     C     KEYIN2        READE(N)  FRILKSD                                94
     C  N94              MOVE      FZKUND        YKUND             8 0
     C  N94              MOVE      FZTYPE        YTYPE             2
     C  N94              MOVE      FZKODE        YKODE             5
     C  N94              MOVE      FZDELS        YDELS             1
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
      *
      * BEHANDLIN HVIS SUBFIL ER TOM
      *
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   EXFMT     NYWRK
     C                   SETOFF                                       717273
     C                   SETOFF                                       7430
     C     WSCLOC        DIV       256           LINNBR            3 0
     C                   MVR                     POSNBR            3 0
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C   08              Z-ADD     2             POSNBR
     C   08              GOTO      START6
     C   06              Z-ADD     2             POSNBR
     C   06              GOTO      START5
     C   03              GOTO      SLUTT
     C   04              EXSR      SOKDTA
     C   04              GOTO      START3
     C     WSNR2         IFEQ      '1'
     C                   EXSR      TESTB0
     C   30              GOTO      START3
     C                   EXSR      MOVLKW
     C                   EXFMT     BILDE01
     C   12              GOTO      START
     C                   EXSR      OPDFIL
     C                   EXSR      MOVB1
     C                   GOTO      START
     C                   END
     C                   GOTO      START3
      *
      * BEHANDLING NÅ SUBFIL INNEHOLDER DATA
      *
     C                   ELSE
     C                   WRITE     BUNN
     C                   END
     C                   SETOFF                                       98
     C                   Z-ADD     3             POSNBR
     C                   Z-ADD     6             LINNBR
     C                   EXFMT     SUBCTL
     C     WSCLOC        DIV       256           LINNBR            3 0
     C                   MVR                     POSNBR            3 0
     C                   SETOFF                                       717273
     C                   SETOFF                                       7430
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** SØKING
     C   04              EXSR      SOKDTA
     C   04              GOTO      START3
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** BYTT KUINDE
     C   06              Z-ADD     2             POSNBR
     C   06              GOTO      START5
     C*** SETT NYTT STARTE
     C   08              Z-ADD     2             POSNBR
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KEYROL        SETLL     FRILKSD
     C  N94KEYKUN        READE     FRILKSD                                94
     C                   GOTO      START2
     C                   END
      *
      * DIREKTE INNLEGGIN PÅ BILDET  MED OPSJON 1
      *
     C     WSNR2         IFEQ      '1'
     C                   EXSR      TESTB0
     C   30              GOTO      START3
     C                   EXSR      MOVLKW
     C                   EXFMT     BILDE01
     C   12              GOTO      START
     C                   EXSR      OPDFIL
     C                   EXSR      MOVB1
     C                   GOTO      START
     C                   END
     C                   EXSR      HENT
     C                   GOTO      START3
      *
      *** BYTTE KUNDENUMMER
      *
     C     START5        TAG
     C                   Z-ADD     21            LINNBR
     C                   EXFMT     VAREL4
     C                   SETOFF                                       3074
     C   12              MOVE      STKUND        WSKUND
     C   12              GOTO      START3
     C   04              EXSR      SOKDTA
     C   04              GOTO      START5
     C                   EXSR      TESTKU
     C   30
     CAN 74              Z-ADD     2             POSNBR
     C   30              GOTO      START5
     C                   MOVE      WSKUND        STKUND
     C                   MOVE      *BLANKS       STDELS
     C                   MOVE      *BLANKS       STTYPE
     C                   MOVE      *BLANKS       STKODE
     C                   GOTO      START
      *
      *** SETT NYE SØKEVERIER
      *
     C     START6        TAG
     C                   Z-ADD     21            LINNBR
     C                   EXFMT     VAREL3
     C   12              GOTO      START3
     C   04              EXSR      SOKDTA
     C   04              GOTO      START6
     C                   MOVE      'J'           SETTV             1
     C                   EXSR      TESTB0
     C                   MOVE      'N'           SETTV             1
     C   30
     CAN 74              Z-ADD     2             POSNBR
     C   30
     CAN 71              Z-ADD     14            POSNBR
     C   30
     CAN 73              Z-ADD     25            POSNBR
     C   30
     CAN 72              Z-ADD     73            POSNBR
     C   30              GOTO      START6
     C                   MOVE      WSKUND        STKUND
     C                   MOVE      WSDELS        STDELS
     C                   MOVE      WSTYPE        STTYPE
     C                   MOVE      WSKODE        STKODE
     C     KEYINI        SETLL     FRILKSD
     C                   EXSR      MOVB1
     C                   GOTO      START1
     C*
     C     SLUTT         TAG
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C                   Z-ADD     1             XNR
     C     XNR           DOWLE     20
     C                   MOVE      XNR           XFZLNR
     C     KEYSTA        CHAIN     FRILKSD                            33
     C     TX(XNR)       IFEQ      *BLANKS
     C  N33              DELETE    FRILKSDR
     C                   ELSE
     C                   MOVE      TX(XNR)       FZTXT
     C   33              MOVE      WSKUND        FZKUND
     C   33              MOVE      WSDELS        FZDELS
     C   33              MOVE      WSTYPE        FZTYPE
     C   33              MOVE      WSKODE        FZKODE
     C   33              MOVE      XNR           FZLNR
     C   33              WRITE     FRILKSDR
     C  N33              UPDATE    FRILKSDR
     C                   END
     C                   ADD       1             XNR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TESTB0        BEGSR
     C***********************************************
     C                   SETOFF                                       717273
     C                   SETOFF                                       7430
     C*
     C     WSDELS        IFNE      ' '
     C                   MOVEL     WSDELS        FDELSY
     C     KEYKOF        CHAIN     KOFAST                             71
     C   71              MOVE      'SHU0429'     MSGNR
     C   71              SETON                                        30
     C                   END
      *
      *** TEST DESTINASJON
      *
     C                   MOVE      *BLANKS       WSKODT           15
     C     WSKODE        IFNE      *BLANKS
     C     KEYSTE        CHAIN     STED2                              72
     C  N72              MOVEL     ST2NVN        WSKODT
     C  N72              MOVE      ST2LK         WSTYPE
     C   72              MOVE      'SPS0012'     MSGNR
     C   72              SETON                                        30
     C                   ELSE
     C                   MOVEL     '*ALLE'       WSKODT
     C                   END
      *
      *** TEST  LAND
      *
     C                   MOVE      *BLANKS       WSTYPT           15
      * IKKE STOPP HVIS SETTV = J
     C     WSTYPE        IFEQ      *BLANKS
     C     SETTV         ANDEQ     'J'
     C                   GOTO      HOPP
     C                   END
     C     KEYLAN        CHAIN     KODTLK                             73
     C  N73              MOVEL     KLKNVN        WSTYPT
     C   73              MOVE      'SPL0001'     MSGNR
     C   73              SETON                                        30
      *
     C     HOPP          TAG
      *
      * TEST KUNDE
      *
     C                   EXSR      TESTKU
      *
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     TESTKU        BEGSR
     C***********************************************
     C*
     C                   MOVE      *BLANKS       KNAVN
     C     WSKUND        IFNE      *ZEROS
     C     KUND1K        CHAIN     CUNDL01                            74
     C   74              MOVE      'SPK0001'     MSGNR
     C   74              SETON                                        30
     C                   ELSE
     C                   MOVEL     'ALLE'        KNAVN
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UKUNDI            8 0
     C                   PARM                    UTYPE             2
     C                   PARM                    UKODE             5
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UDELSY            1
     C                   MOVE      UDELSY        UUUDEL            1
     C                   MOVEL     'DELSYS'      UTYP             10
     C                   MOVE      UKUNDI        WSKUND            8 0
     C                   MOVE      UKUNDI        STKUND            8 0
     C                   MOVE      UTYPE         STTYPE            2
     C                   MOVE      UKODE         STKODE            5
     C                   MOVE      UDELSY        STDELS            1
     C*
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUND
     C     KEYKOF        KLIST
     C                   KFLD                    UTYP
     C                   KFLD                    FDELSY            5
      *
     C     KEYSTA        KLIST
     C                   KFLD                    WSKUND
     C                   KFLD                    WSDELS
     C                   KFLD                    WSTYPE
     C                   KFLD                    WSKODE
     C                   KFLD                    XFZLNR
     C                   MOVE      *ZEROS        XFZLNR            2 0
      *
     C     KEYINI        KLIST
     C                   KFLD                    STKUND
     C                   KFLD                    STDELS
     C                   KFLD                    STTYPE
     C                   KFLD                    STKODE
     C                   KFLD                    XFZLNR
     C                   MOVE      *ZEROS        XFZLNR            2 0
      *
     C     KEYIN2        KLIST
     C                   KFLD                    STKUND
      *
      *
     C     KEYKUN        KLIST
     C                   KFLD                    WSKUND
      *
      *
     C     KEYLAN        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    WSTYPE
     C                   MOVE      'LK '         YLKUNI            3
     C     KEYLA2        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    FZTYPE
      *
     C     KEYST2        KLIST
     C                   KFLD                    FZKUND
     C                   KFLD                    FZDELS
     C                   KFLD                    FZTYPE
     C                   KFLD                    FZKODE
     C                   KFLD                    XMAX              2 0
     C                   Z-ADD     99            XMAX
     C     KEYROL        KLIST
     C                   KFLD                    YKUND             8 0
     C                   KFLD                    YDELS
     C                   KFLD                    YTYPE
     C                   KFLD                    YKODE
     C                   KFLD                    XMAX              2 0
      *
     C     KEYSTE        KLIST
     C                   KFLD                    WSKODE
     C     KEYSD2        KLIST
     C                   KFLD                    FZKODE
     C     FRATX2        KLIST
     C                   KFLD                    WSKUND
     C                   KFLD                    WSDELS
     C                   KFLD                    WSTYPE
     C                   KFLD                    WSKODE
     C                   READ      FIRM                                   33
     C                   ENDSR
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     KLKNV4        COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVWS
     C                   EXSR      MOVLKW
     C                   EXFMT     BILDE01
     C  N12              EXSR      OPDFIL
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
      *
      * FEIL VED SLETTING   ALLREDE SLETTET
      *
     C  N98WSNR          IFEQ      '4'
     C     KLKNV4        COMP      '*DEL'                                 98
     C   98              GOTO      SLHENT
     C                   END
      *
      *** SLETT RECORD
      *
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL5
     C  N12              DO
     C                   EXSR      MOVWS
     C                   EXSR      SLETT
     C                   EXSR      MOVB1
     C                   MOVE      *BLANKS       KLKNVN
     C                   MOVEL     '*DELETE*'    KLKNVN
     C                   END
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C*
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     MOVWS         BEGSR
     C***********************************************
     C  N33              MOVE      FZKUND        WSKUND
     C  N33              MOVE      FZTYPE        WSTYPE
     C  N33              MOVE      FZKODE        WSKODE
     C  N33              MOVE      FZDELS        WSDELS
     C                   ENDSR
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *BLANKS       WSTYPE
     C                   MOVE      *BLANKS       WSDELS
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVLKW        BEGSR
     C***********************************************
      * LESER ALLE FRIE LINJER FRA SUBFIL
      *
     C                   MOVEA     *BLANKS       TX
     C*
     C*** HENT FRITEKST
     C*
     C*
     C     FRATX2        SETLL     FRILKSD
     C                   SETOFF                                       33
     C     LESNYW        TAG
     C     FRATX2        READE(N)  FRILKSD                                33
     C  N33              MOVE      FZLNR         XNR               2 0
     C  N33              MOVE      FZTXT         TX(XNR)
     C  N33              GOTO      LESNYW
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     SLETT         BEGSR
     C***********************************************
      * SLETTER ALLE FRIE LINJER
      *
     C*
     C*** HENT FRITEKST
     C*
     C*
     C     FRATX2        SETLL     FRILKSD
     C                   SETOFF                                       33
     C     LESNYS        TAG
     C     FRATX2        READE     FRILKSD                                33
     C  N33              DELETE    FRILKSDR
     C  N33              GOTO      LESNYS
     C                   ENDSR
     C*
     C***********************************************
     C     SOKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR            3 0
     C                   MVR                     POSNBR            3 0
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
      * SØK DELSYSTEM
      *
     C     LINNBR        IFEQ      6
     C     POSNBR        ANDGE     7
     C     POSNBR        ANDLE     7
     C                   CALL      'SYFAKDA'
     C                   PARM                    UTYP             10
     C                   PARM                    VEDL              1
     C                   PARM                    RETURK            5
     C                   MOVEL     RETURK        WSDELS
     C                   Z-ADD     10            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
      * SØK KUNDE
      *
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     2
     C     POSNBR        ANDLE     9
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUND
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C* SØK STEDSKODE
     C*
     C     LINNBR        IFEQ      6
     C     POSNBR        ANDGE     44
     C     POSNBR        ANDLE     48
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSKODE
     C                   PARM                    UNVN             30
     C                   PARM                    WSTYPE
     C                   GOTO      SLSØK
     C                   END
     C*
     C* SØK LAND
     C*
     C     LINNBR        IFEQ      6
     C     POSNBR        ANDGE     10
     C     POSNBR        ANDLE     11
     C                   MOVE      'J'           USPED             1
     C                   CALL      'SYFI10L'
     C                   PARM                    WSTYPE
     C                   PARM                    USPED
     C                   Z-ADD     16            POSNBR
     C                   Z-ADD     44            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     SLSØK         ENDSR
     C*
      *
