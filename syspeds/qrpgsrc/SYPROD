      *
      *         SYSPED400 MODULER     - VEDLIKEHOLD/SPØRRING
      *
     FSYPRODFM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     FSYPROD    UF A E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  PRTX1                  1     35
     D  SLETT                  1      8
     D                 DS
     D  W2TX2                  1    256
     D  WSTXA                  1     70
     D  WSTXB                 71    140
     D  WSTXC                141    210
     D  WSTXD                211    256
     D                 DS
     D  W2TX2E                 1    256
     D  WSTXEA                 1     70
     D  WSTXEB                71    140
     D  WSTXEC               141    210
     D  WSTXED               211    256
      *
      *
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    PRKODE        UUKOD
     C     *LIKE         DEFINE    WSKODE        WWKOD
     C     *ENTRY        PLIST
     C                   PARM                    UPDATE            1
     C                   PARM                    UUKOD
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEY           KLIST
     C                   KFLD                    PRKODE
      *
     C* Update tillatt ??   50 ON=KUN VEDLIKEHOLD KODER PÅ SAMME TYPE
     C     UPDATE        COMP      'J'                                5050
      *
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      WSKODE        WWKOD
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     *LOVAL        SETLL     SYPROD
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C                   READ(N)   SYPROD                                 94
     C     *IN94         DOWEQ     '0'
      *
     C     FILAND        IFNE      'NO'
     C                   MOVE      PRTX1E        PRTX1
     C                   END
     C                   ADD       1             ZRECNO                           X3
     C                   WRITE     SUBFIL
      *
     C                   READ(N)   SYPROD                                 94
     C  N94              END
      *
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV0473'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     BLANKREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_____________
      * F6=Lag ny
      *"""""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      CRTNEW
     C                   GOTO      START
     C                   END
      *_____________
      * Options på topplinja
      *"""""""""""""
     C     TOPOPT        IFNE      ' '
     C     TOPOPT        IFEQ      '1'
     C     UPDATE        IFNE      'E'
     C                   EXSR      CRTNEW
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       SLETT
     C                   MOVE      TOPOPT        WSNR
     C                   MOVE      WSKODE        PRKODE
     C                   EXSR      OPTION
     C                   END
      * Søk utvalg til gammel verdi.. Vis subfila fra front igjen..
     C                   MOVE      WWKOD         WSKODE
     C                   MOVE      ' '           TOPOPT
     C                   GOTO      START
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     WSKODE        IFNE      WWKOD
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C                   EXSR      OPTION
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         TAG
      *'X' I UPDATE VED RETUR FRA SPØR/PLUKK BETYR AVBRUDD/IKKE PLUKKET
     C                   MOVE      'X'           UPDATE
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
     C     WSNR          IFEQ      '4'
     C     UPDATE        ANDNE     'E'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY           CHAIN     SYPROD                             55
     C  N55              DELETE    SYPRODR
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   MOVE      *BLANKS       PRKODE
     C                   MOVEL     '*SLETTET'    PRTX1
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   END
     C     WSNR          IFEQ      '2'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY           CHAIN(N)  SYPROD                             55
     C     *IN55         IFEQ      '0'
     C                   MOVE      PRKODE        W2KODE
     C                   MOVE      PRTX1         W2TX1
     C                   MOVE      PRTX1E        W2TX1E
     C                   MOVE      PRTX2         W2TX2
     C                   MOVE      PRTX2E        W2TX2E
     C                   MOVE      PRINST        W2INST
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     22            POSNBR
     C     CHGFEI        TAG
     C                   EXFMT     CHGREC
     C     *IN12         IFEQ      '1'
     C                   GOTO      CHGUT
     C                   END
     C                   EXSR      TESTB
     C   30              GOTO      CHGFEI
     C                   MOVE      W2KODE        PRKODE
     C     KEY           CHAIN     SYPROD                             55
     C                   MOVE      W2TX1         PRTX1
     C                   MOVE      W2TX1E        PRTX1E
     C                   MOVE      W2TX2         PRTX2
     C                   MOVE      W2TX2E        PRTX2E
     C                   MOVE      W2INST        PRINST
     C                   UPDATE    SYPRODR
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   END
     C                   END
     C     CHGUT         TAG
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     CRTNEW        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       W2TX1
     C                   MOVE      *BLANKS       W2TX1E
     C     TOPOPT        IFEQ      '1'
     C                   MOVE      WSKODE        W2KODE
     C                   ELSE
     C                   MOVE      *BLANKS       W2KODE
     C                   END
     C                   Z-ADD     7             LINNBR
     C     W2KODE        IFEQ      *BLANKS
     C                   Z-ADD     16            POSNBR
     C                   ELSE
     C                   Z-ADD     22            POSNBR
     C                   END
     C*
     C     NEWFEI        TAG
     C                   EXFMT     NEWREC
     C     *IN12         IFEQ      '1'
     C                   GOTO      NEWUT
     C                   END
     C                   EXSR      TESTB
     C   30              GOTO      NEWFEI
     C                   MOVE      W2KODE        PRKODE
     C     KEY           CHAIN     SYPROD                             55
     C     *IN55         IFEQ      '1'
     C                   MOVE      W2TX1         PRTX1
     C                   MOVE      W2TX1E        PRTX1E
     C                   MOVE      W2TX2         PRTX2
     C                   MOVE      W2TX2E        PRTX2E
     C                   MOVE      W2INST        PRINST
     C                   WRITE     SYPRODR
     C     XRECNO        ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C     NEWUT         TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C                   SETOFF                                       30
      * Kode kan ikke være blank
     C     W2KODE        IFEQ      *BLANKS
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0477'     ZMSGNO
     C                   GOTO      UTTEST
     C                   END
     C*
      * Både Norsk og engelsk beskrivelse må gis
     C     W2TX1         IFEQ      *BLANKS
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0477'     ZMSGNO
     C                   GOTO      UTTEST
     C                   END
     C     W2TX1E        IFEQ      *BLANKS
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0477'     ZMSGNO
     C                   GOTO      UTTEST
     C                   END
     C     UTTEST        ENDSR
