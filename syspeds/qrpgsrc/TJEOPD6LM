      *********************************************************************
      *****  MOVE    av et oppdrag (til annen avd)                    *****
      *********************************************************************
CRTPG+*  CRTPGM SYSPED/TJEOPD6LM MODULE(*LIBL/TJEOPD6LM *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *********************************************************************
      *********************************************************************
      *  01 PTF11 JOV
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF A E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D HEAVDA          S              4
     D HEOPDA          S              7
     D NEWAVDA         S              4
     D NEWAVD          S              4  0
     D NEWOPD          S              7  0
     D JSONstring      S          32000
     D Error           S            150
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬heavd¬ : ¬'+%trim(%editc(HEAVD:'Z'))+'¬, '
        +'¬heopd¬ : ¬'+%trim(%editc(HEOPD:'Z'))+'¬, '
        +'¬newavd¬ : ¬'+%trim(%editc(NEWAVD:'Z'))+'¬, '
        +'¬newopd¬ : ¬'+%trim(%editc(NEWOPD:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"orderMove" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     KODTA

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      HEAVDA = zhbgetvar('AVD')
     C                   EVAL      HEAVD  = c2n2(HEAVDA)
     C                   EVAL      HEOPDA = zhbgetvar('OPD')
     C                   EVAL      HEOPD  = c2n2(HEOPDA)
     C                   EVAL      NEWAVDA= zhbgetvar('NEWAVD')
     C                   EVAL      NEWAVD  = c2n2(NEWAVDA)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     HEADKEY       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     NewAvdKey     KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    NEWAVD
     C                   MOVE      'A'           KOAUNI

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      HEADF
     C  N50              OPEN      KODTA
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6

     c                   ENDSR
      *
     ****************************************************************
     C     TEST          BEGSR
     ****************************************************************
      *
     C     HEADKEY       CHAIN(N)  HEADF                              33
     C     *in33         ifeq      '1'
     C                   eval      Error = 'Ukjent oppdragsnr '
     c                             +%trim(%editc(HEAVD:'Z'))+'/'
     c                             +%trim(%editc(HEOPD:'Z'))
     c                   goto      UtTest
     c                   endif
      *
N04  C     hest          ifne      ' '
N04  C     hest          andne     'U'
N04  C                   eval      Error = 'Feil status. Kun blank/U kan '
     c                             +'flyttes'
     c                   goto      UtTest
     c                   endif
      *
N04  C     hepro         ifne      0
N04  C                   eval      Error = 'Feil turstatus, Kun oppdrag som '
     c                             +'ikke er på tur kan flyttes'
     c                   goto      UtTest
     c                   endif
      *
N04  C     TRSLAG        Ifne      'HO'
N04  C                   eval      Error = 'Feil type oppdrag. Kun Hoved'
     c                             +'oppdrag kan flyttes'
N04  c                   goto      UtTest
N04  C                   ENDIF
      *
N04  C     TROPD1        IFNE      0
N04  C     TROPD2        ORNE      0
N04  C                   eval      Error = 'Feil DUP-status. Oppdrag UTEN DUP  '
     c                             +'under seg kan flyttes'
N04  c                   goto      UtTest
N04  C                   ENDIF
      *
     C     NewAvd        ifeq      HEAVD
     C                   eval      Error = 'Avdelingen en flytter til '
     c                             +'kan ikke være lik den oppdraget har nå'
     c                   goto      UtTest
     c                   endif
      *
     C     NewAvd        ifeq      *zeros
     C                   eval      Error = 'Avdeling for flytt til '
     c                             +'kan ikke være 0'
     c                   goto      UtTest
     c                   endif
     C     NewAvdKey     CHAIN     KODTA                              33
     C     *in33         ifeq      '1'
     C                   eval      Error = 'Ukjent avdeling '
     c                             +%trim(%editc(NEWAVD:'Z'))
     c                   goto      UtTest
     c                   endif
      * gjennom alle tester - Move   oppdrag
     c                   exsr      SRM
      *
     c     UtTest        ENDSR
      *
      ***************************************************
     C     SRM           BEGSR
      ***************************************************
N04  c                   call      'SYGE81'
N04  c                   parm      HEAVD         uuavd             4 0          IN - param
N04  c                   parm      HEOPD         uuopd             7 0          IN - param
N04  c                   parm      NEWAVD        tilavd            4 0          IN - param
N04  c                   parm                    NEWOPD                         OUT- param
N04   *
     C                   ENDSR
