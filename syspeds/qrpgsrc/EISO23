      *********************************************************************
      * PROGRAM VISE AKTUELL SAKER
      *
CRTPG+*  CRTPGM SYSPED/EISO23 MODULE(*LIBL/EISO23
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 UTVIDER TABELL TIL AVVIKES ART.
10XXX * 02 YBC PTF10 DIVERSE ENDRINGER ETTER MØTE 09.02.2012
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FISOKDA    IF   E           K DISK    usropn
     FISOKDP    IF   E           K DISK    usropn
     FISOKDT    IF   E           K DISK    usropn
     FWRKCGISS  IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D OddEven         s             10
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSAAR           s              4
     D WSMNF           s              2
     D WSMNT           s              2
     D WINT            s              6
     D WSMND           S              2
N02  D WSINNL          S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D  XXKEY                  1    256
     D  WBMND                  4      5  0
     D                 DS
     D  WDATA                  1   1024
     D  WANTISOP               1      5  0
     D  WANTOPDP               6     10  0
     D  TPT                   11    160  0 DIM(30)
     D  TAA                  161    460  0 DIM(60)
     D* TAP                  461    760  0 DIM(60)
     D  TAP                  461    960  0 DIM(100)
     D                 DS
     D  KDPT                   1    150    DIM(30)
     D  KDPTV                  1    150
     D                 DS
S01  D* KDAA                   1    600    DIM(60)
S01  D* KDAAV                  1    600
N01  D  KDAA                   1   1000    DIM(100)
N01  D  KDAAV                  1   1000
     D                 DS
     D* KDAP                   1    300    DIM(60)
     D  KDAP                   1    500    DIM(100)
     D* KDAPV                  1    300
     D  KDAPV                  1    500
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
      * 3-Section /$tablestr
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      * 4-Sections /$tablerow
      *
     C                   EXSR      HENTKD
     C     WRKCGIK       CHAIN     WRKCGISS                           50
      *
     C     1             DO        30            XN                3 0
     C                   IF        TPT(XN) <> 0
     C                   exsr      SetPTRow
     C                   callp     wrtsection('tabPTrow')
     C                   END
     C                   ENDDO                                                  *hival
     C                   callp     wrtsection('tabPTend')
      *
     c                   eval      OddEven = EVEN
     C                   callp     wrtsection('tabAAstr')
     C     1             DO        60            XN                3 0
     C                   IF        TAA(XN) <> 0
     C                   exsr      SetAARow
     C                   callp     wrtsection('tabAArow')
     C                   END
     C                   ENDDO                                                  *hival
     C                   callp     wrtsection('tabAAend')
      *
     c                   eval      OddEven = EVEN
     C                   callp     wrtsection('tabAPstr')
     C*    1             DO        60            XN                3 0
     C     1             DO        100           XN                3 0
     C                   IF        TAP(XN) <> 0
     C                   exsr      SetAPRow
     C                   callp     wrtsection('tabAProw')
     C                   END
     C                   ENDDO                                                  *hival
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      *
      * 5a-Section /$foundsome
     C                   exsr      SetFndSome
     C                   callp     wrtsection('foundsome2')
      * 6-Section /$endhtml
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
N02  C                   EVAL      WSINNL  = zhbgetvar('WSINNL')
      *
     C                   EVAL      WINT    = zhbgetvar('WINTEG')
     C                   EVAL      WINTEG  = c2n2(WINT)
     C*                  EVAL      XXKEY = '991'
     C                   EVAL      XXKEY   = zhbgetvar('WSTYPE')
     C                   EVAL      WSMND   = zhbgetvar('WSMND')
     C                   EVAL      WBMND   = c2n2(WSMND)
     C                   EVAL      WSAAR   = zhbgetvar('WSAAR')
     C                   EVAL      WSMNF   = zhbgetvar('WSMNF')
     C                   EVAL      WSMNT   = zhbgetvar('WSMNT')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':BIBESK)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
N02  C                   callp     updHTMLvar('WSINNL':WSINNL)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updHTMLvar('WSAAR':WSAAR)
     C                   callp     updHTMLvar('WSMNF':WSMNF)
     C                   callp     updHTMLvar('WSMNT':WSMNT)
     C                   callp     updHTMLvar('WINTEG':
     C                             %trim(%editc(WINTEG:'Z')))
     C                   callp     updHTMLvar('WSMND':WSMND)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetPTRow      begsr
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     c* Sequence of this Recd (numeric, converted to char)
     C                   MOVE      KDPT(XN)      TKODE
     C     TKODE         CHAIN     ISOKDT                             33
     C                   callp     updhtmlvar('TKODE':TKODE)
     C                   callp     updhtmlvar('TTEXT':TTEXT)
     C                   callp     updhtmlvar('WSANT':
     C                             %trim(%editc(TPT(XN):'Z')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetAARow      begsr
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     c* Sequence of this Recd (numeric, converted to char)
     C                   MOVEL     KDAA(XN)      APKD
     C                   MOVE      KDAA(XN)      AKODE
     C     ISOKDAK       CHAIN     ISOKDA                             33
     C                   callp     updhtmlvar('AKODE':AKODE)
     C                   callp     updhtmlvar('ATEXT':ATEXT)
     C                   callp     updhtmlvar('WSANT':
     C                             %trim(%editc(TAA(XN):'Z')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetAPRow      begsr
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     c* Sequence of this Recd (numeric, converted to char)
     C                   MOVE      KDAP(XN)      PKODE
     C     PKODE         CHAIN     ISOKDP                             33
     C                   callp     updhtmlvar('PKODE':PKODE)
     C                   callp     updhtmlvar('PTEXT':PTEXT)
     C                   callp     updhtmlvar('WSANT':
     C                             %trim(%editc(TAP(XN):'Z')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome    begsr
      * Repeat background color
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'EISO23'
     C*                  CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  HENT ISO-KODER
      *=====================================================================******
     C     HENTKD        begsr
      *
     C                   MOVE      *BLANKS       KDAAV
     C                   MOVE      *ZEROS        XN
     C     *LOVAL        SETLL     ISOKDA
     C                   READ      ISOKDA                                 33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVEL     APKD          KDAA(XN)
     C                   MOVE      AKODE         KDAA(XN)
     C                   READ      ISOKDA                                 33
     C                   ENDDO
     C                   MOVE      *BLANKS       KDAPV
     C                   MOVE      *ZEROS        XN
     C     *LOVAL        SETLL     ISOKDP
     C                   READ      ISOKDP                                 33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVE      PKODE         KDAP(XN)
     C                   READ      ISOKDP                                 33
     C                   ENDDO
     C                   MOVE      *BLANKS       KDPTV
     C                   MOVE      *ZEROS        XN
     C     *LOVAL        SETLL     ISOKDT
     C                   READ      ISOKDT                                 33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVE      TKODE         KDPT(XN)
     C                   READ      ISOKDT                                 33
     C                   ENDDO
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      ISOKDA
     C                   OPEN      ISOKDP
     C                   OPEN      ISOKDT
     C                   OPEN      WRKCGISS
      *
     C     WRKCGIK       klist
     C                   kfld                    WINTEG
     C                   kfld                    XXKEY
     C     ISOKDAK       klist
     C                   kfld                    APKD
     C                   kfld                    AKODE
      *
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     WRKCGISS
     C                   close     ISOKDA
     C                   close     ISOKDP
     C                   close     ISOKDT
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
