     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
SV5  A*           TUDTÅ          2Y 0H
SV5  A*           TUDTM          2Y 0H
SV5  A*           TUDTD          2Y 0H
NV5  A            TUDT           8Y 0H
     A            WSNR           1A  I  6  2VALUES( ' ' '5' '6' '8' '9')
     A            TUAVD          4Y 0O  6  4EDTCDE(4)
     A  27                                  COLOR(YLW)
     A            TUBILN        10A  O  6 27
     A  28                                  COLOR(YLW)
     A            TUSTEF    R        O  6 38REFFLD(TURERR/TUSTEF *LIBL/TURER)
     A                                      COLOR(WHT)
     A            WSDATF         6S 0O  6 44COLOR(WHT)
     A            TUSTET    R        O  6 51REFFLD(TURERR/TUSTET *LIBL/TURER)
     A                                      COLOR(WHT)
     A            WSDATT         6S 0O  6 57COLOR(WHT)
     A            TRNA17        17A  O  6 64
     A            TURUND         8Y 0O  6  9EDTCDE(4)
     A  27                                  COLOR(YLW)
     A            TUPRO          8Y 0O  6 18EDTCDE(4)
     A  27                                  COLOR(YLW)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(24)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYTR90'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(030 DIV5079 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            UDSAVD         4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(079 DIV5080 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(018 DIV5081 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 23MSGCON(018 DIV5085 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 43MSGCON(015 DIV2386 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 61MSGCON(018 SHU0001 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(24)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØKE TUR')
     A                                      CF12(12 'TIL SYEX10')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØKE TUR')
     A                                      CF12(12 'TIL SYTR10')
     A                                  1  2'SYSPED/SYTR90'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(030 DIV5079 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            UDSAVD         4  0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV3807 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SØKTUR
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(24)
     A                                      CF03(03 'AVSLUTT')
     A                                      CA04(04 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      SLNO(18)
     A                                      CLRL(07)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSSKOD         1A  B  1  2COLOR(YLW)
     A  70                                  DSPATR(RI)
     A  70                                  DSPATR(PC)
     A            WSAVD          4Y 0B  1  5DSPATR(UL)
     A N70                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSRUND         8Y 0B  1 10DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 21MSGCON(035 DIV5082 *LIBL/SYMSGF)
     A            WSSTEF         5A  B  2  2COLOR(YLW)
     A            WSSTET         5A  B  2 13COLOR(YLW)
     A                                  2 21MSGCON(030 DIV3805 *LIBL/SYMSGF)
     A            WSSONF         5A  B  3  2COLOR(YLW)
     A            WSSONT         5A  B  3 13COLOR(YLW)
     A                                  3 21MSGCON(030 DIV3806 *LIBL/SYMSGF)
     A            WSBILN        10A  B  4  2COLOR(YLW)
     A            WSBDAT         6Y 0B  4 13EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  4 21MSGCON(030 DIV0981 *LIBL/SYMSGF)
     A            WSKNT2    R        B  5  2REFFLD(TURERR/TUKNT2 *LIBL/TURER)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSMNDT         2Y 0B  5 13COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSAART         2Y 0B  5 17COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5 21MSGCON(035 DIV5083 *LIBL/SYMSGF)
     A N30                              6  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 44MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
