********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/AIRLD)
     A                                      PRINT
S02  A*                                     HLPDOC('SYEF03010' 'SYEF03 ' 'SY400-
S02  A*                                     LO/MAINT')
N02  A                                      HLPPNLGRP(HLPSCR SYEF03PNL)
N02  A                                      HLPTITLE('VEDLIKEHOLD AV FLYFRAKTRA-
N02  A                                      TER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            XDLT           7A  H
     A            WSNR           1A  I  6  2
     A            AIRCCP    R        O  6  5REFFLD(AIRLDR/AIRCCP)
     A            AIRCOD    R        O  6  7REFFLD(AIRLDR/AIRCOD)
     A            AIRWEI    R        O  6 13REFFLD(AIRLDR/AIRWEI)
     A                                      EDTCDE(4)
     A*           AIRTRC    R        O  6 18REFFLD(AIRLDR/AIRTRC)
     A            AIRGRN    R        O  6 21REFFLD(AIRLDR/AIRGRN)
     A                                      EDTCDE(4)
     A            AIRAWB    R        O  6 36REFFLD(AIRLDR/AIRAWB)
     A                                      EDTCDE(4)
     A            AIRHAW    R        O  6 51REFFLD(AIRLDR/AIRHAW)
     A                                      EDTCDE(4)
     A            AIRHAM    R        O  6 66REFFLD(AIRLDR/AIRHAW)
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF03(03 'AVSLUTT')
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF12(12 'FORRIGE')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
S02  A*         H                           HLPDOC('SYEF03020' 'SYEF03' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYEF03020 SYEF03PNL)
     A                                      HLPARA(01 001 20 080)
     A                                  1  2'SYSPED/SYEF03RA'
     A                                      COLOR(BLU)
     A                                  1 21'Vedlikehold flyfraktrate (brutto)'
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ULIN      R        O  3 33REFFLD(AIRLDR/AIRLIN)
     A                                      COLOR(WHT)
     A            USDT          30A  O  3 38COLOR(WHT)
     A                                  4  2'2=Endre  3=Kopier  4=Slett'
     A                                      COLOR(BLU)
     A            UPFR      R        O  4 33REFFLD(AIRLDR/AIRPFR)
     A                                      COLOR(WHT)
     A                                  4 37'-->'
     A            UPTO      R        O  4 41REFFLD(AIRLDR/AIRPTO)
     A                                      COLOR(WHT)
     A                                  5  2'Op Ratekl. Vektgr.     Gr.pris/min-
     A                                            Kilopris  Hawb Kilopris   Haw-
     A                                      bminimun'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF03(03 'AVSLUTT')
     A                                      CF12(12 'Forrige')
     A N40                                  CF04(04 'Søk')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      PROTECT
     A                                      CSRLOC(LINNBR     POSNBR)
S02  A*         H                           HLPDOC('SYEF03030' 'SYEF03' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYEF03030 SYEF03PNL)
     A                                      HLPARA(02 001 02 008)
S02  A*         H                           HLPDOC('SYEF03040' 'SYEF03' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYEF03040 SYEF03PNL)
     A                                      HLPARA(02 009 02 019)
S02  A*         H                           HLPDOC('SYEF03050' 'SYEF03' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYEF03050 SYEF03PNL)
     A                                      HLPARA(02 020 02 035)
S02  A*         H                           HLPDOC('SYEF03060' 'SYEF03' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYEF03060 SYEF03PNL)
     A                                      HLPARA(02 036 02 050)
S02  A*         H                           HLPDOC('SYEF03070' 'SYEF03' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYEF03070 SYEF03PNL)
     A                                      HLPARA(02 051 02 065)
S02  A*         H                           HLPDOC('SYEF03080' 'SYEF03' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYEF03080 SYEF03PNL)
     A                                      HLPARA(02 066 02 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   Ratekl. Vektgr.     Gr.pris/min-
     A                                            Kilopris  Hawb Kilopris   Haw-
     A                                      bminimun'
     A                                      COLOR(WHT)
     A            WSRCCP    R        B  2  5REFFLD(AIRLDR/AIRCCP)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSRCOD    R        B  2  7REFFLD(AIRLDR/AIRCOD)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A  90                                  DSPATR(RI)
     A            WSRWEI    R        B  2 13REFFLD(AIRLDR/AIRWEI)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A  89                                  DSPATR(RI)
     A*           WSRCP     R        B  2 14REFFLD(AIRLDR /AIRCP)
     A*           WSRTRC    R        B  2 18REFFLD(AIRLDR /AIRTRC)
     A            WSRGRN    R        B  2 21REFFLD(AIRLDR/AIRGRN)
     A                                      EDTCDE(4)
     A            WSRAWB    R        B  2 36REFFLD(AIRLDR/AIRAWB)
     A                                      EDTCDE(4)
     A            WSRHAW    R        B  2 51REFFLD(AIRLDR/AIRHAW)
     A                                      EDTCDE(4)
     A            WSRHAM    R        B  2 66REFFLD(AIRLDR/AIRHAW)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N40                              3 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF12(12 'Forrige')
S02  A*         H                           HLPDOC('SYEF03020' 'SYEF03' 'SY400L-
S02  A*                                     O/MAINT')
N02  A          H                           HLPPNLGRP(SYEF03020 SYEF03PNL)
     A                                      HLPARA(01 001 20 080)
     A                                  1  2'SYSPED/SYEF03RA'
     A                                      COLOR(BLU)
     A                                  1 21'Vedlikehold flyfraktrate (brutto)'
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ULIN      R        O  3 33REFFLD(AIRLDR/AIRLIN)
     A                                      COLOR(WHT)
     A            USDT          30A  O  3 38COLOR(WHT)
     A                                  4  2'2=Endre  3=Kopier  4=Slett'
     A                                      COLOR(BLU)
     A            UPFR      R        O  4 33REFFLD(AIRLDR/AIRPFR)
     A                                      COLOR(WHT)
     A                                  4 37'-->'
     A            UPTO      R        O  4 41REFFLD(AIRLDR/AIRPTO)
     A                                      COLOR(WHT)
     A                                  5  2'Op Ratekl. Vektgr.     Gr.pris/min-
     A                                            Kilopris  Hawb Kilopris   Haw-
     A                                      bminimun'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2MSGCON(070 SHU0101 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            AIRCP     R        O  2  5REFFLD(AIRLDR/AIRCCP)
     A                                      DSPATR(PC)
     A            AIRCOD    R        O  2  7REFFLD(AIRLDR/AIRCOD)
     A            AIRWEI    R        O  2 13REFFLD(AIRLDR/AIRWEI)
     A                                      EDTCDE(4)
     A*           AIRTRC    R        O  2 18REFFLD(AIRLDR /AIRTRC)
     A            AIRGRN    R        O  2 21REFFLD(AIRLDR/AIRGRN)
     A                                      EDTCDE(4)
     A            AIRAWB    R        O  2 36REFFLD(AIRLDR/AIRAWB)
     A                                      EDTCDE(4)
     A            AIRHAW    R        O  2 51REFFLD(AIRLDR/AIRHAW)
     A                                      EDTCDE(4)
     A            AIRHAM    R        O  2 66REFFLD(AIRLDR/AIRHAW)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
