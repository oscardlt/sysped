     FHEADF     IF   E           K DISK
     FTRACKL02  IF   E           K DISK
      *
     c                   exsr      INIT
      *
     c                   select
     c                   when      TRSLAG = 'FF'
     c                   exsr      TESTFF
     c                   when      TRSLAG = 'VF'
     c                   exsr      TESTVF
     c                   other
     c                   exsr      TESTHO
     c                   endsl
      *
     c     slutt         tag
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     c                   parm                    NextACT           3
     c                   parm                    Term             20
     C*--------------------------------------------------------------
     C     KEY           KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C*
     C     TT02Key       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C*
     C     KEY           chain     Headf
     c                   if        not %found
     c                   eval      *inLR=*on
     c                   return
     c                   endif
     C*
     c                   move      *blanks       TE2txt            7
     C                   ENDSR
      *
      ***********************************************
     C     TESTFF        BEGSR
      ***********************************************
      * FF.oppdrag er henting hos avsender - el lev til terminal
     c                   eval      NextAct = 'COL'
      * alleredde hentet/levering neste ??
     c                   eval      TTACTI  = 'COL'
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'LET'
     c                   end
      * Også leveringen ferdig
     c                   if        NextAct  = 'LET'
     c                   eval      TTACTI  = 'TE2'
     c                   eval      TE2txt  = 'Levert '
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'FER'
     c                   end
     c                   end
     c
     c                   if        NextAct  = 'LET'
     c                   eval      Term     = HESDT
     c                   endif
      *
     C                   ENDSR
      *
      ***********************************************
     C     TESTVF        BEGSR
      ***********************************************
      * VF.oppdrag er henting på terminal  - el lev til mottaker (eller term)
     c                   eval      NextAct = 'HET'
      * alleredde hentet/levering neste ??
     c                   eval      TTACTI  = 'TE2'
     c                   eval      TE2txt  = 'Hentet '
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'POD'
     c                   end
      * Også leveringen ferdig (sjekk både Term og POD)
     c                   if        NextAct  = 'POD'
     c                   eval      TTACTI  = 'TE2'
     c                   eval      TE2txt  = 'Levert '
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'FER'
     c                   end
     c                   end
     c                   if        NextAct  = 'POD'
     c                   eval      TTACTI  = 'POD'
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'FER'
     c                   end
     c                   end
      *
     c                   if        NextAct  = 'HET'
     c                   eval      Term     = HESDF
     c                   endif
     c
     C                   ENDSR
      *
      ***********************************************
     C     TESTHO        BEGSR
      ***********************************************
      * i utgangspunkett hjenting hos avsender / leverong til mottaker
      * men derso oppdraghet har FF-dup foran seg eller VF-dup etter så er det til terminal
      * HO.oppdrag er henting hos avsender(el. term) /  - el lev til terminal (eller mottaker)
     c                   eval      NextAct = 'COL'
     c                   if        TRSLAG = 'HO'
     c                             and HESDFF <> *blanks
     c                   eval      NextAct = 'HET'
     c                   endif
      * alleredde hentet/levering neste ??
     c                   eval      TTACTI  = 'COL'
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'POD'
     c                   else
      * prøv også om henting på term finnes
     c                   eval      TTACTI  = 'TE2'
     c                   eval      TE2txt  = 'Hentet '
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'POD'
     c                   end
     c                   end
      **
     c                   if        NextAct  = 'POD'
      * hvis den har viderefrakt etter seg da skal HO levere til terminal
     c                   if        TRSLAG = 'HO'
     c                             and HESDVT <> *blanks
     c                   eval      NextAct = 'LET'
     c                   endif
      * Også leveringen ferdig
     c                   if        NextAct  = 'POD'
     c                   eval      TTACTI  = 'POD'
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'FER'
     c                   else
      * prøv også om Levering  term finnes
     c                   eval      TTACTI  = 'TE2'
     c                   eval      TE2txt  = 'Levert '
     c                   exsr      tstAct
     c                   if        actFins  = 'Y'
     c                   eval      NextAct  = 'FER'
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      *
     c                   if        NextAct  = 'HET'
     c                   eval      Term     = HESDFF
     c                   endif
     c                   if        NextAct  = 'LET'
     c                   eval      Term     = HESDVT
     c                   endif
      *
     C                   ENDSR
      *
      ***********************************************
     C     TSTACT        BEGSR
      ***********************************************
      * Sjekk om en gitt ACTKOD finnes på oppdraget (og ikke er kopiert fra annet i kjeden..)
      * Ved TE2 må også 7 første av tekst  stemme (Hentet / Levert )  - kan finnes fler TE2..
     c                   move      'N'           ActFins           1
     c     tt02Key       setll     trackl02
     c     tt02Key       reade     trackl02
     c                   dow       not %eof
     c                   IF        TTEDRE <> 'KOP'
     c                   IF        TTACTI <> 'TE2'
     c                   move      'Y'           ActFins
     c                   leave
     c                   else
     c                   IF        %subst(TTTEXL:1:7) = TE2txt
     c                   move      'Y'           ActFins
     c                   leave
     c                   endif
     c                   endif
     c                   endif
     c     tt02Key       reade     trackl02
     c                   enddo
      *
     C                   ENDSR
