     A*%%TS  SD  20141208  093432  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX88PNL)
     A                                      HLPTITLE('ARBEIDE MED TURER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20141208  092531  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  6  2
     A  98                                  DSPATR(RI)
     A                                      VALUES(' ' 'A' '5' 'F' 'I' 'B')
     A            HEAVD          4Y 0O  6  4EDTCDE(4)
     A            HEOPD          7Y 0O  6  9EDTCDE(4)
     A            WSESTI         9Y 2O  6 68EDTWRD('       .  ')
     A  24                                  COLOR(RED)
     A            SFPA01        20   O  6 26
     A            SFPA02        20   O  6 47
     A            SFDATO         8   O  6 17
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20141208  093432  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0015)
     A  *DS4                                SFLSIZ(0015)
     A  *DS3                                SFLPAG(0014)
     A  *DS4                                SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYOP04R'
     A                                      COLOR(BLU)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  27                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(077 SHU1481 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 19MSGCON(030 SHU1482 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N32                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØKE TUR')
     A                                      CF12(12 'AVSLUT')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A N32                              4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                              4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 SHU0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SØKTUR
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'FORRIGE')
     A                                      CF14(14 'legge inn tillatelser')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            WSAVD          4Y 0B  1  2DSPATR(UL)
     A                                      DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1  8MSGCON(008 SHU0825 *LIBL/SYMSGF)
     A            WSPERI         6Y 0B  1 18EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  1 26MSGCON(008 SHU0826 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B  1 36DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  1 45MSGCON(009 SHU0827 *LIBL/SYMSGF)
     A            WSSG           3   B  1 56COLOR(YLW)
     A            WSSTAT         1A  B  2  2
     A                                  2  5MSGCON(060 SHU0568 *LIBL/SYMSGF)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A N32                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A  27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØKE TUR')
     A  32                                  CF12(12 'TIL SYEX10')
     A                                  1  2'SYSPED/SYOP04R'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  27                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 61TIME
     A                                      COLOR(BLU)
     A N32                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                             23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 SHU0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 20MSGCON(040 SHU0571 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(075 SHU0572 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(077 SHU0573 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
