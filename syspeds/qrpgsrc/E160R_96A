********************************************************************
***** * Dette programmet legger ut en veldig basic iftsta.         *
***** * Baserer seg på at et forprogram har klargjort parametre    *
********************************************************************
***** * OBS wilson m.fl: MOVEL  'F11'  S9015  må inn som spesiel etter V9
***** *                  dato-lengde mm må sjekkes....
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 FTX                .....
********************************************************************
     FEDIC      UF   E             DISK
     FEUNH      O    E             DISK
     FEDIM      O    E             DISK
     FECNI96A   O    E             DISK
     FEDTM96A   O    E             DISK
     FEFTX96A   O    E             DISK
     FENAD96A   O    E             DISK
     FERFF96A   O    E             DISK
     FESTS96A   O    E             DISK
      *____________
      * DS for Dato
      *""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTM4                   1      4
     D  WTM                    1      6
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDT                    1      8
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                SDS
     D  USER                 254    263
      *______________________
      * Initialize subroutine
      *""""""""""""""""""""""
     C                   EXSR      INIT
     C                   EXSR      SNDRQS
     C                   RETURN
      *////////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////////
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UERFA            35
     C                   PARM                    URFF             35
     C                   PARM                    USIGN            10
     C                   PARM                    UTIM              4
     C                   PARM                    UDAT              8
     C                   PARM                    U9011             3
     C                   PARM                    U4440A           70
     C                   PARM                    U4440B           70
     C                   PARM                    U4440C           70
     C                   PARM                    URECIP           35
     C                   PARM                    USEND            35
     C                   PARM                    U0010            35
     C                   PARM                    U0004            35
      *__________________
      * Define workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    CMN           WMN
      *__________________________
      * Retreive System Date/Time
      *""""""""""""""""""""""""""
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
     C                   ENDSR
      *////////////////////////////////////////////////////////////////
     C     SNDRQS        BEGSR
      *////////////////////////////////////////////////////////////////
      *_____________________________________________________
      * Retrieve last used recordnumber from EDIC and update
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               90
     C     *IN90         IFEQ      '0'
     C                   ADD       1             CMN
     C                   Z-ADD     CMN           WMN
     C                   UPDATE    EDICR
     C                   ENDIF
      *_____________________
      * Write record to EUNH
      *"""""""""""""""""""""
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTSTA'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '96A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'DEFO03'      U0057
     C                   MOVEL     'F01'         U1001
     C                   MOVEL     WMN           U1004
     C                   MOVEL     WMN           U0062B
     C                   WRITE     UNHR
      *____________________
      * Write record to DTM
      *""""""""""""""""""""
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     1             DREP0
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDT           D2380
     C     D2380         CAT       WTM4:0        D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *____________________
      * Write record to NAD
      *""""""""""""""""""""
      * MESSAGE RECIPIENT
     C     URECIP        ifne      *blanks
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     1             NGN
     C                   Z-ADD     1             NREP0
     C                   MOVEL     'MR'          N3035
     C                   MOVEL     URECIP        N3039
     C                   WRITE     NADR
     c                   end
      * MESSAGE SENDER
     C     USEND         ifne      *blanks
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     1             NGN
     C                   ADD       1             NREP0
     C                   MOVEL     'FR'          N3035
     C                   MOVEL     USEND         N3039
     C                   WRITE     NADR
     c                   end
      *____________________
      * Write record to CNI
      *""""""""""""""""""""
     C                   CLEAR                   CNIR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     4             CGN
     C                   Z-ADD     1             CREP0
     C                   MOVEL     '1'           C1490
     C                   MOVEL     UERFA         C1004
     C                   WRITE     CNIR
      *____________________
      * Write record to STS
      *""""""""""""""""""""
     C                   CLEAR                   STSR
     C                   Z-ADD     WMN           SMN
     C                   Z-ADD     5             SGN
     C                   Z-ADD     1             SREP0
     C                   Z-ADD     1             SREP1
     C                   MOVEL     '012'         S9011
     C     U9011         IFNE      *BLANKS
     C                   MOVEL     U9011         S9011
     C                   ENDIF
     C                   WRITE     STSR
      *____________________
      * Write record to RFF
      *""""""""""""""""""""
     C     URFF          ifne      *blanks
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     5             RGN
     C                   Z-ADD     1             RREP0
     C                   Z-ADD     1             RREP1
     C                   Z-ADD     1             RREP2
     C                   MOVEL     'FF'          R1153
     C                   MOVEL     URFF          R1154
     C                   WRITE     RFFR
     c                   end
      *____________________
      * Write record to DTM
      *""""""""""""""""""""
     C     UDAT          ifne      *blanks
     C     UDAT          andne     *zeros
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     5             DGN
     C                   Z-ADD     1             DREP0
     C                   Z-ADD     1             DREP1
     C                   Z-ADD     1             DREP2
     C                   MOVEL     '334'         D2005
     C                   MOVEL     UDAT          D2380
     C     D2380         CAT       UTIM:0        D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
     c                   end
      *____________________
      * Write record to FTX
      *""""""""""""""""""""
S01  C*    F4440A        IFNE      *BLANKS
N01  C     U4440A        IFNE      *BLANKS
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     5             FGN
     C                   Z-ADD     1             FREP0
     C                   Z-ADD     1             FREP1
     C                   Z-ADD     1             FREP2
     C                   MOVEL     'ZZZ'         F4451
     C                   MOVEL     U4440A        F4440A
     C                   MOVEL     U4440B        F4440B
     C                   MOVEL     U4440C        F4440C
     C                   WRITE     FTXR
     C                   ENDIF
      *___________________________________
      * Write record to NAD DELIVERY PARTY
      *"""""""""""""""""""""""""""""""""""
     C     USIGN         ifne      *blanks
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     5             NGN
     C                   Z-ADD     1             NREP0
     C                   Z-ADD     1             NREP1
     C                   Z-ADD     1             NREP2
     C                   MOVEL     'DP'          N3035
     C                   MOVEL     USIGN         N3036A
     C                   WRITE     NADR
     C                   end
      *__________________________
      * Write record to EDIM file
      *""""""""""""""""""""""""""
     C                   CLEAR                   EDIMR
     C                   MOVEL     U0004         M0004
     C                   MOVEL     U0010         M0010
     C                   MOVEL     U0065         M0065
     C                   MOVEL     USER          M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVEL     'S'           MSR
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   WRITE     EDIMR
      *
     C                   ENDSR
