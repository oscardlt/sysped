********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 RETTELSE ETTER UTVIDELSE AV OPPDRAGSNR
10007 * 02 YBC PTF10 OPPDRAGSNR OG AVDELING ER SKILT FRA HVER ANDRE
      * %%%%%%%%%%%%%%%%%%%%
10007 * 03 SH  PTF12 HERFA ØKT TIL 33
10007 * 04 JOV PTF12 Hvis I/E/B OG oppdr.nr utfylt skriv bare dette (Hvis J) Leseloop som før(f.DT)
********************************************************************
      **************************************************************
      *     TRANSPORTMODUL, SKRIV KJØRESEDDEL/ARB.ORDRE            *
      *     EKSTERN/INTERN/OMDISPONERINGSORDRE                     *
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FKODFRI    IF   E           K DISK
     FHODE      IF   E           K DISK
     FHEADF     UF   E           K DISK
     FOMDI      UF   E           K DISK
     FDISP      IF   E           K DISK
     FSYPARL1   IF   E           K DISK
     F* HEAD PÅ SIGNATUR OG DATO
     FHEADD     IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD2R)
     F* HEAD PÅ PRISJEKTNUMMER
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD11R)
     FSYTR40PF  O    E             PRINTER OFLIND(*IN64)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FTURER     IF   E           K DISK
     FFREETXT2  IF   E           K DISK
     D                 DS
     D  DFS                    1     10
     D                                     DIM(10)
     D  FSDOKK                 1     10
NRFA D                 DS
NRFA D  HERFA                  1     35
S03  D* HERFA15                1     15
N03  D  HERFA33                1     33
     D                 DS
     D  HEFT1                  1     51
     D  HEFT1A                 1     30
     D  HEFT1B                31     45
     D  HEFT1C                 1     30
     D TALL            C                   CONST('0123456789')
     D                 DS
     D  ENGAD3                 1     30
     D  SYPOGE                 1      9
     D  ADR3                  11     34
     D                 DS
     D  TUSJN1                 1     30
     D  TUSJ15                 1     15
     D                 DS
     D  UDATO                  1      6  0
     D  UDT1                   1      2  0
     D  UDT2                   3      4  0
     D  UDT3                   5      6  0
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D                 DS
     D  TRSDFD                 1      8  0
     D  TSDFDÅ                 1      4  0
     D  TSDFDM                 5      6  0
     D  TSDFDD                 7      8  0
     D                 DS
     D  TRSDTD                 1      8  0
     D  TSDTDÅ                 1      4  0
     D  TSDTDM                 5      6  0
     D  TSDTDD                 7      8  0
     D                 DS
     D  TRSFFD                 1      8  0
     D  TSFFDÅ                 1      4  0
     D  TSFFDM                 5      6  0
     D  TSFFDD                 7      8  0
     D                 DS
     D  TRSVTD                 1      8  0
     D  TSVTDÅ                 1      4  0
     D  TSVTDM                 5      6  0
     D  TSVTDD                 7      8  0
     D                 DS
     D  XRSDFD                 1      6  0
     D  XSDFDD                 1      2  0
     D  XSDFDM                 3      4  0
     D  XSDFDÅ                 5      6  0
     D                 DS
     D  XRSDTD                 1      6  0
     D  XSDTDD                 1      2  0
     D  XSDTDM                 3      4  0
     D  XSDTDÅ                 5      6  0
     D                 DS
     D  XRSFFD                 1      6  0
     D  XSFFDD                 1      2  0
     D  XSFFDM                 3      4  0
     D  XSFFDÅ                 5      6  0
     D                 DS
     D  XRSVTD                 1      6  0
     D  XSVTDD                 1      2  0
     D  XSVTDM                 3      4  0
     D  XSVTDÅ                 5      6  0
     D                 DS
     D  HELB                   1      5
     D  DUPSG                  1      3
     D                 DS
     D  DUPADS                 1    256
     D  DAAVD0                 1      4  0
     D  DAOPD0                 5     11  0
     D  DAAVD1                12     15  0
     D  DAOPD1                16     22  0
     D  DAAVD2                23     26  0
     D  DAOPD2                27     33  0
     D  DATLE                 34     39
     D  DATLL                 40     49
     D  DATLKU                40     50
     D  DAGN                  51     65
     D  DAPOS1                66     72
     D  DAPOS2                73     76  0
     C*
     C                   EXSR      INIT
     C* KJØREORDRE DIREKTE FRA ET ENKELTOPPDRAG?
     C     *IN28         IFEQ      '1'
     C     HEADFK        CHAIN(N)  HEADF                              33
     C                   GOTO      OPPD
     C                   END
     C*
     C* ELLERS LESELOOP F.O.M DATO eller PÅ TURNUMMER
     C*
     C*
     C     UTUR          IFEQ      *ZEROS
     C     HEAD2K        SETLL     HEADD
     C*
     C                   ELSE
     C     UTUR          SETLL     HEAD11
     C                   END
     C*
     C     START         TAG
     C*
     C     UTUR          IFEQ      *ZEROS
     C                   READ      HEADD                                  32
     C                   ELSE
     C     UTUR          READE     HEAD11                                 32
     C                   END
     C*
     C   32              GOTO      SLUTT
     C*
     C     HEPRO         IFEQ      *ZEROS
     C     HEST          IFNE      'S'
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C     USG           IFNE      *BLANKS
     C     HESG          ANDNE     USG
     C                   GOTO      START
     C                   END
     C*
     C*
     C     UTUR          IFEQ      *ZEROS
     C     UAVD          ANDNE     HEAVD
     C                   GOTO      START
     C                   END
     C*
     C     UTYPE         IFEQ      'I'
     C     HEPK3         ANDNE     'J'
     C                   GOTO      START
     C                   END
     C*
     C     UTYPE         IFEQ      'E'
     C     HEPK5         ANDNE     'J'
     C                   GOTO      START
     C                   END
     C*
     C     UTYPE         IFEQ      'B'                                          *BEGGE:
     C     HEPK3         ANDNE     'J'                                           MINST EN=J
N01  C     UTYPE         OREQ      'B'
     C     HEPK5         ANDNE     'J'                                           -GÅR VIDERE
     C                   GOTO      START
     C                   END
     C*
     C     HEUR          IFNE      'H'
     C                   GOTO      START
     C                   END
      *
N04  C     UOPD          IFNE      *ZEROS
N04  C                   If        HEAVD<>Uavd or HEOPD<>Uopd
N04  C                   GOTO      START
N04  C                   endif
N04  C                   END
     C*
     C     OPPD          TAG
     C*
     C     HEST          IFNE      'S'
     C     HEADFK        CHAIN     DISP                               33
     C     *IN28         IFEQ      '0'
     C  N33KEYTUR        CHAIN     TURER                              33
     C                   ELSE
     C     HEPRO         IFNE      *ZEROS
     C  N33KEYTUR        CHAIN     TURER                              33
     C                   END
     C                   END
     C                   END
     C* KJØREORDRE DIREKTE FRA ET ENKELTOPPDRAG?  UT OM IKKE OK!!
     C     *IN28         IFEQ      '1'
     C     *IN33         ANDEQ     '1'
     C                   GOTO      SLUTT
     C                   END
     C   33              GOTO      START
     C*
     C                   MOVE      ' '           INT               1
     C                   MOVE      ' '           EKS               1
      *
     C                   MOVE      TSDFDÅ        XSDFDÅ
     C                   MOVE      TSDFDM        XSDFDM
     C                   MOVE      TSDFDD        XSDFDD
     C                   MOVE      TSDTDÅ        XSDTDÅ
     C                   MOVE      TSDTDM        XSDTDM
     C                   MOVE      TSDTDD        XSDTDD
     C                   MOVE      TSFFDÅ        XSFFDÅ
     C                   MOVE      TSFFDM        XSFFDM
     C                   MOVE      TSFFDD        XSFFDD
     C                   MOVE      TSVTDÅ        XSVTDÅ
     C                   MOVE      TSVTDM        XSVTDM
     C                   MOVE      TSVTDD        XSVTDD
     C* SNUR DATOER
     C     FIDTFM        IFNE      3
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C     XRSDFD        PARM      XRSDFD        URSDFD            6 0
     C     XRSDTD        PARM      XRSDTD        URSDTD            6 0
     C     XRSFFD        PARM      XRSFFD        URSFFD            6 0
     C     XRSVTD        PARM      XRSVTD        URSVTD            6 0
     C                   END
     C     FIDTFM        IFNE      3
     C                   MOVE      HEDTÅ         UDT1              2 0
     C                   MOVE      HEDTM         UDT2              2 0
     C                   MOVE      HEDTD         UDT3              2 0
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C     PRDATO        PARM      UDATO         XXDATO            6 0
     C                   END
     C* PRINT EKSTERN
     C     UTYPE         IFEQ      'B'
     C     UTYPE         OREQ      'E'
     C     HEPK5         IFEQ      'J'
     C     *IN28         OREQ      '1'
     C                   MOVE      'J'           EKS
     C                   EXSR      PRINT
     C                   END
     C                   END
     C* PRINT INTERN
     C     UTYPE         IFEQ      'B'
     C     UTYPE         OREQ      'I'
     C     HEPK3         IFEQ      'J'
     C                   MOVE      'J'           INT
     C                   SETON                                        90
     C* INTERN ORDREUTSKRIFT
     C                   SETON                                        91
     C                   EXSR      PRINT
     C* INTERN FAKTURAORDRE
     C                   SETOFF                                       91
     C                   EXSR      PRINT
     C                   SETOFF                                       90
     C                   END
     C                   END
     C*
     C* KJØREORDRE DIREKTE FRA ET ENKELTOPPDRAG?  IKKE OPPDATER STATUS
     C     *IN28         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END
     C     HEADFK        CHAIN     HEADF                              33
     C  N33INT           IFEQ      'J'
     C                   MOVE      'P'           HEPK3
     C                   END
     C  N33EKS           IFEQ      'J'
     C                   MOVE      'P'           HEPK5
     C                   END
     C  N33              UPDATE    HEADFR
     C                   ADD       1             UANT
     C                   GOTO      START
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*****************************************************************
     C     PRINT         BEGSR
     C*****************************************************************
     C*
     C     KOPFM         SETLL     KODTFM
     C     KOPFM         READE     KODTFM                                 98
     C*
     C     STPRI         TAG
     C  N98KFMT1         IFNE      *BLANKS
     C                   WRITE     KJØPHH1
     C                   END
     C  N98KFMT2         IFNE      *BLANKS
     C                   WRITE     KJØPHH2
     C                   END
     C*
     C* PRINTER HEADING HVIS ØNSKELIG
     C*
     C*
     C     UHEAD         IFEQ      'J'
     C     UHEAD         OREQ      'Y'
     C                   WRITE     HEADP
     C                   ELSE
     C                   WRITE     HEAD0
     C                   END
     C                   SETOFF                                       64
     C*
     C* SKRIVER SLETTEDE ORDRE
     C*
     C     HEST          COMP      'S'                                    77
     C   77              WRITE     ANNUL
     C*
     C* SKJEKKER OM OMDISPONERT
     C*
     C                   SETOFF                                       78
     C     HEADFK        CHAIN     OMDI                               33
     C  N33              DELETE    OMDIR
     C  N33              SETON                                        78
     C   78              WRITE     OMDISP
     C*** HENT OPPDRAGSGIVER
     C*
     C                   MOVE      *BLANKS       PRNAA
     C                   MOVE      *BLANKS       PRADA1
     C                   MOVE      *BLANKS       PRADA2
     C                   MOVE      *BLANKS       PRADA3
     C*
     C     TRKNFA        IFNE      *ZEROS
     C     KUNKEO        CHAIN     CUNDL01                            33
     C  N33              MOVE      KNAVN         PRNAA
     C  N33              MOVE      ADR1          PRADA1
     C  N33              MOVE      ADR2          PRADA2
     C*
     C  N33POSTNR        IFEQ      0                                               1<-B
     C*
     C     SYPOGE        IFEQ      *BLANKS                                         2<-B
     C                   MOVEL     ADR3          PRADA3
     C                   ELSE                                                      2<-X
     C                   MOVE      ENGAD3        PRADA3
     C                   END                                                       2<-E
     C*
     C                   ELSE                                                      1<-X
     C                   MOVEL     POSTNR        PRADA3
     C                   MOVE      ADR3          PRADA3
     C                   END                                                       1<-E
     C                   END                                                       1<-E
     C*
     C*
     C*** FORTOLLINGSAGENT
     C*
     C                   MOVE      *BLANKS       PRNAU
     C                   MOVE      *BLANKS       PRADU1
     C                   MOVE      *BLANKS       PRADU2
     C                   MOVE      *BLANKS       PRADU3
     C*
     C     HEKNA         IFNE      *ZEROS
     C     KUNKEU        CHAIN     CUNDL01                            33
     C  N33              MOVE      KNAVN         PRNAU
     C  N33              MOVE      ADR1          PRADU1
     C  N33              MOVE      ADR2          PRADU2
     C*
     C  N33POSTNR        IFEQ      0                                               1<-B
     C*
     C     SYPOGE        IFEQ      *BLANKS                                         2<-B
     C                   MOVEL     ADR3          PRADU3
     C                   ELSE                                                      2<-X
     C                   MOVE      ENGAD3        PRADU3
     C                   END                                                       2<-E
     C*
     C                   ELSE                                                      1<-X
     C                   MOVEL     POSTNR        PRADU3
     C                   MOVE      ADR3          PRADU3
     C                   END                                                       1<-E
     C                   END                                                       1<-E
     C*
     C*** FAKTURAMOTTAGER
     C*
     C                   MOVE      *BLANKS       PRNAF
     C                   MOVE      *BLANKS       PRADF1
     C                   MOVE      *BLANKS       PRADF2
     C                   MOVE      *BLANKS       PRADF3
     C*
     C     HEKDFS        IFEQ      'X'
     C                   MOVE      HEKNKF        HEKNSF
     C                   END
     C*
     C*
     C     HEKNSF        IFNE      *ZEROS
     C     KUNKEF        CHAIN     CUNDL01                            33
     C  N33              MOVE      KNAVN         PRNAF
     C  N33              MOVE      ADR1          PRADF1
     C  N33              MOVE      ADR2          PRADF2
     C*
     C  N33POSTNR        IFEQ      0                                               1<-B
     C*
     C     SYPOGE        IFEQ      *BLANKS                                         2<-B
     C                   MOVEL     ADR3          PRADF3
     C                   ELSE                                                      2<-X
     C                   MOVE      ENGAD3        PRADF3
     C                   END                                                       2<-E
     C*
     C                   ELSE                                                      1<-X
     C                   MOVEL     POSTNR        PRADF3
     C                   MOVE      ADR3          PRADF3
     C                   END                                                       1<-E
     C                   END                                                       1<-E
     C*
     C*
     C* SKRIVER HEADING
     C*
     C     TROPD0        COMP      *ZEROS                             5050        DUP AV
     C     HEPRO         COMP      *ZEROS                             5151        PÅ TUR
      * Hvis DUP-oppdr. hent evt. løpenr fra overordnet.
     C                   SETOFF                                       52
     C     HETLL         IFEQ      *BLANKS
     C     TROPD0        ANDNE     *ZEROS
     C                   MOVE      TRAVD0        XXAVD0            4 0
     C                   MOVE      TROPD0        XXOPD0            7 0
     C                   MOVE      *BLANKS       XXPADS          256
     C                   CALL      'SYTR99'
     C                   PARM                    XXAVD0
     C                   PARM                    XXOPD0
     C                   PARM                    XXPADS
     C                   MOVE      XXPADS        DUPADS
     C                   MOVE      DATLE         HETLE
     C                   MOVE      DATLL         HETLL
     C                   MOVE      DAGN          HEGN
     C                   MOVE      DAPOS1        HEPOS1
     C                   Z-ADD     DAPOS2        HEPOS2
     C     HETLL         COMP      *BLANKS                            5252      "SE DUP AV"
     C                   END
     C                   WRITE     HEAD1A
     C     HEKNA         IFNE      *ZEROS
     C                   WRITE     FORT
     C                   END
     C   90              DO
     C     TRVERB        IFNE      *ZEROS
     C                   WRITE     VARE
     C                   END
     C                   END
     C     TRETTB        IFNE      *ZEROS
     C                   WRITE     ETT
     C                   END
     C   90              WRITE     FRAKT
     C                   WRITE     HEAD1B
      * Parameter for bruk at arb-ordre som skilleark (men kun intern ordre..)
     c     SkilKode      ifne      *blanks
     c     *in90         andeq     '1'
     c     *in91         andeq     '1'
     c                   movel     SkilKode      Skille           13
     c                   movel     HEAVD         SkiAvOp
     c                   move      HEOPD         SkiAvOp          11
     c                   move      SkiAvOp       Skille
     c                   movel     Skille        SkilleT          15
     C                   WRITE     SKILLBAR
     c                   end
     C     TRFA11        IFNE      *BLANKS
     C     TRFA21        ORNE      *BLANKS
     C                   WRITE     FARL
     C                   END
     C*
      * MELLOM B.L. OG   MERKN....
      * FRIE SØKEVEIER (KUN DE SOM HAR DOK.KODE LIK K)
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 33
     C     *IN33         DOWEQ     '0'
     C                   SORTA     DFS
      * VED INTERN SKRIVES ALLE SØKEVEIER (SIMULER AT DET STÅR K)
      * VED EKSTERN KUN HVIS S/K
     C     INT           IFEQ      'J'
     C                   MOVE      'K'           DFS(10)
     C                   END
     C     DFS(10)       IFNE      ' '
     C     'K'           LOOKUP    DFS                                    33
     C  N33'S'           LOOKUP    DFS                                    33
     C     *IN33         IFEQ      '1'
     C     FSKODE        CHAIN     KODFRI                             33
     C     *IN33         IFEQ      '0'
     C     *IN64         IFEQ      '1'
     C     UHEAD         IFEQ      'J'
     C     UHEAD         OREQ      'Y'
     C                   WRITE     HEADP
     C                   ELSE
     C                   WRITE     HEAD0
     C                   END
     C                   SETOFF                                       64
     C                   END
     C                   WRITE     KJØPFS
     C                   END
     C                   END
     C                   END
     C*
     C     HEADFK        READE     FRISOK                                 33
     C                   END
      *
      * OG UTLEV.FORBEHOLD
     C     HEKFT1        IFEQ      *BLANKS
     C     HEFT1A        ANDNE     *BLANKS
     C                   WRITE     KJØPUFB
     C                   END
      *
      * INTERNE MERKNADER.....
     C     INT           IFEQ      'J'
     C                   WRITE     DET1C
     C                   MOVE      ' '           XXSK
     C                   EXSR      FRILIN
     C                   END
      *
      * ØVRIGE  MERKNADER.....
     C                   WRITE     DET1D
     C                   MOVE      'S'           XXSK
     C                   EXSR      FRILIN
     C                   MOVE      'K'           XXSK
     C                   EXSR      FRILIN
     C* SKRIVER HEADING FERJEINFORMASJONER
     C*
     C                   WRITE     GODS
     C*
     C   78              GOTO      STPRI
      *
     C  N98KOPFM         READE     KODTFM                                 98
     C  N98              GOTO      STPRI
     C                   ENDSR
     C*****************************************************************
     C     FRILIN        BEGSR
     C*****************************************************************
     C* LESER OG SKRIVER FRITEKSTER
     C*
     C     FREETK        SETLL     FREETXT2
     C     STFREE        TAG
     C     FREETK        READE     FREETXT2                               33
     C*
     C   64
     CANN33UHEAD         IFEQ      'J'
     C     UHEAD         OREQ      'Y'
     C                   WRITE     HEADP
     C                   ELSE
     C                   WRITE     HEAD0
     C                   END
     C                   SETOFF                                       64
     C  N33              WRITE     DETT
     C*
     C  N33              GOTO      STFREE
     C*
     C                   ENDSR
     C*
     C*****************************************************************
     C     INIT          BEGSR
     C*****************************************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    AAVD              4
     C                   PARM                    USG               3
     C                   PARM                    ADATO1            6
     C                   PARM                    ATUR              8
N02  C                   PARM                    AOPD              7
     C                   PARM                    UTYPE             1
     C                   PARM                    UHEAD             1
     C                   PARM                    AANT              3
     C     TALL          CHECK     AAVD                                   99
     C  N99              MOVE      AAVD          UAVD              4 0
     C   99              MOVE      *ZEROS        UAVD
     C     TALL          CHECK     ATUR                                   99
     C  N99              MOVE      ATUR          UTUR              8 0
     C   99              MOVE      *ZEROS        UTUR
N02  C     TALL          CHECK     AOPD                                   99
N02  C  N99              MOVE      AOPD          UOPD              7 0
N02  C   99              MOVE      *ZEROS        UOPD
     C     TALL          CHECK     ADATO1                                 99
     C  N99              MOVE      ADATO1        UDATO1            6 0
     C   99              MOVE      *ZEROS        UDATO1
     C     TALL          CHECK     AANT                                   99
     C  N99              MOVE      AANT          UANT              3 0
     C   99              MOVE      *ZEROS        UANT
     C     KUNKEO        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TRKNFA
     C     KUNKEF        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    HEKNSF
     C*
     C     KUNKEU        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    HEKNA
     C*
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEAD2K        KLIST
     C                   KFLD                    KEDATO
     C*
     C     KEYTUR        KLIST
     C                   KFLD                    DITUAV
     C                   KFLD                    DITUR
     C     FREETK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXSK              1
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    UAVD
     C*
     C                   MOVE      ' '           YNET              1
     C                   MOVE      'K'           KODEKO            1
     C                   MOVE      'Y'           STAT              1
     C     UHEAD         IFEQ      'J'
     C     UHEAD         OREQ      'Y'
     C     HODEK         CHAIN     HODE                               33
     C                   END
     C                   READ      FIRM                                   33
     C*
     C     UTUR          COMP      0                                      27
     C     UDATO1        IFEQ      0
     C                   MOVE      *ZEROS        KEDATO            8 0
     C                   ELSE
     C     FIDTFM        IFNE      3
     C                   CALL      'DATFM5'
     C                   PARM                    FIDTFM
     C                   PARM                    UDATO1
     C                   END
     C     UDATO1        IFGT      501231
     C     UDATO1        ADD       19000000      KEDATO
     C                   ELSE
     C     UDATO1        ADD       20000000      KEDATO
     C                   END
     C                   END
     C* SINGLE KJØREORDRE BESTILT DIREKTE FRA ORDREPROGRAM (IKKE NØDV.PÅ TUR)
     C     UTYPE         IFEQ      'S'
     C                   MOVE      '1'           *IN28
     C                   MOVE      'E'           UTYPE
     C                   MOVE      UAVD          HEAVD
S02  C*                  MOVE      UTUR          HEOPD
N02  C                   MOVE      UOPD          HEOPD
     C                   END
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    WSPLNR
     C                   MOVE      'P'           WSPUNI            1
     C                   Z-ADD     51            WSPLNR            3 0
     C     KODTPK        CHAIN     KODTP                              99
      * arbeidsorde benyttes som skilleark
     C     BarKey        KLIST
     C                   KFLD                    XXPAKN            8 0
     C                   KFLD                    XXPAID            5
     C                   movel     'KKBAR'       XXPAID
     c     BarKey        chain     SYPARL1                            33
     C  N33              MOVEL     SYVRDA        SkilKode          2
     C  N33              MOVE      SYVRDA        SkilText         47
     C*
     C                   ENDSR
