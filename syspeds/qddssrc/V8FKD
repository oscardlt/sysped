     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            FNKNV          1A  H
     A            FNHKNV         1A  H
     A            WSKNV          1A  B  5  2VALUES(' ' 'J')
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A            FNFN          10A  O  5 10
     A            WSST           1A  O  5 21
     A            FNST           1A  H
     A            FNHST          1A  H
     A            FNTXT         50A  O  5 28
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/V8FKR'
     A                                      COLOR(BLU)
     A                                  1 19'Filkonvertering for versjon 8'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  3  2'Velg opsjon og trykk ENTER:   J=Ko-
     A                                      nverteres'
     A                                      COLOR(BLU)
     A                                  4  2'Opsjon  Filnavn    Status Filbeskr-
     A                                      ivelse'
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA19(19 'Merk ALLE')
     A                                      CA21(21 'Start konvertering')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=Avslutt   F5=Frisk  F19=Merk AL-
     A                                      LE   F21=Start KNV'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  2002  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/V8FKR'
     A                                      COLOR(BLU)
     A                                  1 19'Filkonvertering for versjon 8'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  3  2'Velg opsjon og trykk ENTER:   J=Ko-
     A                                      nverteres'
     A                                      COLOR(BLU)
     A                                  4  2'Opsjon  Filnavn    Status Filbeskr-
     A                                      ivelse'
     A                                      DSPATR(HI)
     A                                  6  2'Ingen fil skal konverteres'
     A                                      DSPATR(HI)
     A                                 23  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2004  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
