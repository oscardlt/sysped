     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR FAXSOKPNL)
     A                                      HLPTITLE('BRUK AV FAXLOGG')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            LOUSER    R        H      REFFLD(FAXLOGR/LOUSER *LIBL/FAXLOG)
     A            LOJONR    R        H      REFFLD(FAXLOGR/LOJONR *LIBL/FAXLOG)
     A            LOJONA    R        H      REFFLD(FAXLOGR/LOJONA *LIBL/FAXLOG)
     A            LOMAPP    R        H      REFFLD(FAXLOGR/LOMAPP *LIBL/FAXLOG)
     A            LOTIME    R        H      REFFLD(FAXLOGR/LOTIME *LIBL/FAXLOG)
     A            LODATE    R        H      REFFLD(FAXLOGR/LODATE *LIBL/FAXLOG)
     A            LOUDAT    R        H      REFFLD(FAXLOGR/LOUDAT *LIBL/FAXLOG)
     A            LOAVD     R        H      REFFLD(FAXLOGR/LOAVD *LIBL/FAXLOG)
     A            LOOPD     R        H      REFFLD(FAXLOGR/LOOPD *LIBL/FAXLOG)
     A            LOTUR     R        H      REFFLD(FAXLOGR/LOTUR *LIBL/FAXLOG)
     A            QFORIG    R        H      REFFLD(QFFTLOG/QFORIG *LIBL/QAFFTLO-
     A                                      G)
     A            WSNR           1A  I  5  2
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      VALUES(' ' '1' '3' '4' '5' '6' '8')
     A            QFSU      R        O  5  4REFFLD(QFFTLOG/QFSU *LIBL/QAFFTLOG)
     A  42                                  COLOR(WHT)
     A  90                                  DSPATR(RI)
     A            LOKODE         1A  O  5  6
     A  42                                  COLOR(WHT)
     A            QFDATE    R        O  5  8REFFLD(QFFTLOG/QFDATE *LIBL/QAFFTLO-
     A                                      G)
     A  42                                  COLOR(WHT)
     A            QFTIME    R        O  5 15REFFLD(QFFTLOG/QFTIME *LIBL/QAFFTLO-
     A                                      G)
     A  42                                  COLOR(WHT)
     A            LONAVN    R        O  5 22REFFLD(FAXLOGR/LONAVN *LIBL/FAXLOG)
     A  42                                  COLOR(WHT)
     A            LOFA15        15A  O  5 53
     A  42                                  COLOR(WHT)
     A            LODOCN    R        O  5 69REFFLD(FAXLOGR/LODOCN *LIBL/FAXLOG)
     A  42                                  COLOR(WHT)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPPNLGRP(FAXLOG010 FAXSOKPNL)
     A                                      HLPARA(01 001 02 080)
     A          H                           HLPPNLGRP(FAXLOG015 FAXSOKPNL)
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPPNLGRP(FAXLOG020 FAXSOKPNL)
     A                                      HLPARA(04 001 04 003)
     A          H                           HLPPNLGRP(FAXLOG025 FAXSOKPNL)
     A                                      HLPARA(04 004 04 005)
     A          H                           HLPPNLGRP(FAXLOG030 FAXSOKPNL)
     A                                      HLPARA(04 006 04 007)
     A          H                           HLPPNLGRP(FAXLOG035 FAXSOKPNL)
     A                                      HLPARA(04 008 04 068)
     A          H                           HLPPNLGRP(FAXLOG040 FAXSOKPNL)
     A                                      HLPARA(04 069 04 080)
     A          H                           HLPPNLGRP(FAXLOG020 FAXSOKPNL)
     A                                      HLPARA(05 001 05 003)
     A          H                           HLPPNLGRP(FAXLOG025 FAXSOKPNL)
     A                                      HLPARA(05 004 05 005)
     A          H                           HLPPNLGRP(FAXLOG030 FAXSOKPNL)
     A                                      HLPARA(05 006 05 007)
     A          H                           HLPPNLGRP(FAXLOG035 FAXSOKPNL)
     A                                      HLPARA(05 008 05 068)
     A          H                           HLPPNLGRP(FAXLOG040 FAXSOKPNL)
     A                                      HLPARA(05 069 05 080)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/FAXSOK '
     A                                      COLOR(BLU)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(050 SHU0214 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 20MSGCON(014 SHU0406 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 39MSGCON(012 DIV0688 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            USER          10A  O  2 55
     A                                  3  2MSGCON(030 SHU0219 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 39MSGCON(018 SHU0220 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 58MSGCON(015 SHU0397 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 SHU0215 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk opp')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(FAXLOG045 FAXSOKPNL)
     A                                      HLPARA(01 001 02 080)
     A            W2ALFA        10A  I  2 18DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(015 SHU0216 *LIBL/SYMSGF)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A          H                           HLPPNLGRP(FAXLOG045 FAXSOKPNL)
     A                                      HLPARA(01 001 02 080)
     A                                  1  2'SYSPED/FAXSOK '
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(050 SHU0214 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            USER          10   O  2 55
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 SHU0215 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(015 SHU0216 *LIBL/SYMSGF)
     A            W2ALFA        10A  I 23 37DSPATR(UL)
     A                                      DSPATR(PC)
     A  98        MSG511        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R BFAXNR
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(12 'Avslutt program')
     A                                      CF12(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2 22MSGCON(025 SHU0217 *LIBL/SYMSGF)
     A            LOFAXN    R        B  2 51REFFLD(FAXLOGR/LOFAXN *LIBL/FAXLOG)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(025 SHU0218 *LIBL/SYMSGF)
     A            SEDATE         6Y 0B  3 51EDTCDE(4)
     A            SETIME         6Y 0B  3 60EDTCDE(4)
     A  98        MSG206        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R BSKRIV
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A                                      WINDOW(*DFT 8 55 *NOMSGLIN)
     A                                  1  2'FAXSOKFM'
     A                                      COLOR(BLU)
     A            DEV           10A  I  5 23DSPATR(UL)
     A                                      CHECK(ME)
     A            HOLD           1A  I  6 23DSPATR(UL)
     A                                      VALUES('J' 'N' ' ')
     A                                  5  3MSGCON(013 SHU0400 *LIBL/SYMSGF)
     A                                  6  3MSGCON(013 SHU0401 *LIBL/SYMSGF)
     A                                  8  3MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
