     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
      ****************************************************************************************
      * Hente-oppdrag,  Send instruksjonsmail                                                *
      ****************************************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 TA MED AVSENDES ADREESSE OGSÅ VED MAIL TIL AVSENDER
11XXX * 02 SH  PTF11 ENDRE JAN OTTAR TIL GENERELL ADRESSE
11XXX * 03 JOV PTF11 TYPE=F = FRITTSTÅENDE (kun label/frbrev/ utenom hentesystemet)
11XXX * 04 sh  PTF11 variabel internbruker
11XXX * 05 sh  PTF11 sende lenke til CMR også
11XXX * 06 JOV PTF11 sikre at det ikke er appostorf i EMNE på EMLHTML
11XXX * 07 SH  PTF12 mmail kommando retter fra STMF->HTML
11XXX * 08 SH  PTF12 henter ikke data med blank kode =intern fra frettxt
********************************************************************
     FHeadf     IF   E           K DISK
     FFREETXT2  IF   E           K DISK
     FBRPARF    IF   E           K DISK
n04  FSYPARL3   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FTRACKF    O  A E           K DISK
      *__________
      * Variabler
      *""""""""""
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
     D Data            s             80a   Varying
     D Codepage        s              5a   Varying
     D CmdLine         S            512
     D CmdLen          S             15  5
     D SDATA           S           1000
     D WFilNam         S            256
     D SMS             S             15
      *___________
      * Konstanter
      *"""""""""""
     D LF              c                   x'25'
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
     D                SDS
     D  ZUSER                254    263
      *
     D LINK            S            150
     D MailSMS         S             70
     D Emne            S             70
     D AvAd            S             50
     D AvNa            S             50
     D Madr            S             50
     D timedata1       S               z                                        z=Timestamfield
     D                 DS
     D  HXSNDN                 1     20
     D  Snd17                  4     20
      *
     C                   EXSR      INIT
      *
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         CABEQ     *ON           SLUTT
      * Lag hentemailen
     C                   EXSR      LAGHENTM
      *
     C                   EXSR      SendMail
      *
     C                   EXSR      TTSR
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ******************************************************************************
     C     LAGHENTM      BEGSR
      ******************************************************************************
      * Lag fil og set codepage (pc)
     C                   time                    Timedata1
     C                   movel     timedata1     TS26             26            må være 26 lang
     C                   movel     TS26          TS               22
      *                                                 typisk TS er nå  2012-09-08-17.39.49.73
     C                   EVAL      WFilNam ='/SYSPEDDEV/TEMP/'
     C                                     + TS + '_' + AvdOpd + '.html'
     C                   EVAL      FILE_o = open( %TrimR( WFilNam )
     C                             : 'w, codepage=850' )
     C                   EVAL      rc = close( FILE_o )
     C                   EVAL      FILE_o = open( %TrimR( WFilNam )
     C                             : 'w' )
      *
N03  c                   if        UUANNEN = 'F'
N03  C                   Eval      EMNE  =  %trim(FIFT)
N03  C                                    +' oppdrag ' + %trim(HERFA)
N03  C                                    +' Vår ref:'
N03  C                                    +%trim(%editc(HEAVD:'Z')) +'/'
N03  c                                    +%trim(%editc(HEOPD:'Z'))
N03  c                   else
     C                   Eval      EMNE  = 'Hente-oppdrag '
     C                                    + %trim(HERFA) + ' for '
     C                                    + %trim(HENAA)
N03  c                   endif
      *
     C                   EXSR      HOVED
s08  C*                  EXSR      FRISPED
     C                   EXSR      FRITRANS
     C                   EXSR      BUNN
      * close file
     C                   EVAL      rc = close( FILE_o )
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     HOVED         BEGSR
      ******************************************************************************
      * LEGG UT HTML TOP / STYLE MM  (se f.esk SYSPED/HTMLSRC.EISO15 for inlin css.)
     C                   EVAL      SDATA  = '<HTML>'
     C                             + '<style type="text/css"> '
     c                             + 'th {font-size: 15pt; '
     c                             +    'font-family: arial, helvetica, helv; '
     c                             +    'font-weight: bold;}'
     c                             + 'td {font-size: 10pt; '
     c                             +    'font-family: arial, helvetica, helv; '
     c                             +    'font-weight: bold;}'
     c                             + 'BODY {'
     c                             + '  font-size:10pt;'
     c                             + '  font-family: arial,'
     c                             + '  helvetica, helv;'
     c                             + '  background-color: #E6E6FA;'
     c                             + '  margin-top: 50px;'
     c                             + '  margin-right: 5px;'
     c                             + '  margin-bottom: 5px;'
     c                             + '  margin-left: 50px;'
     c                             + '} '
     c                             + '</style>  '
     c                             + '<Body><table>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)       Skriv til IFS-fil
      *
      *
      * start ved send til hentetransportør
     c                   if        UUANNEN = 'T'
     C                   EVAL      SDATA  = '<tr><th>'
     C                                    + 'Melding om tildelt henteoppdrag '
     C                                    + 'til '+ %trim(UUTRANAV)
     c                                    + '</th></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      * Merknad ??
     c                   if        UUMERKN<> *blanks and UUANNEN = 'T'
      *
     C                   EVAL      SDATA  = '<tr><th><B>'
     C                                    + 'Merknad: ' + %trim(UUMERKN)
     c                                    + '</B></th></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     c                   endif
      *
     c                   endif
N03  c                   if        UUANNEN = 'F'
N03  C                   EVAL      SDATA  = '<tr><td>'
N03  C                                    + 'Info om oppdrag for '+ %trim(HENAA)
N03  C                                    + ' hos '+ %trim(FIFT)+'. Diverse '
N03  C                                    + 'nyttige lenker vedr gods:'
N03  c                                    + '</td></tr>'
N03  c                   else
      * start ved send til avsender/annen
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + 'På vegne av '+ %trim(HENAA)
     C                                    + ' er '+ %trim(FIFT)+' bedt om '
     C                                    + 'å hente følgende gods:'
     c                                    + '</td></tr>'
N03  c                   endif
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      * Varelinje/gosmerke på samme linje..
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + %trim(%editc(HENT:'Z'))
     C                             + ' ' + %trim(HEVS1)
     C                   if        HEVS2<>' '
     C                   EVAL      SDATA  = %trim(SDATA ) +', '
     C                             + %trim(HEVS2)
     c                   endif
     C                   EVAL      SDATA  = %trim(SDATA )
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     C                             + 'Vekt: '+ %trim(%editc(HEVKT:'Z'))
     C                   if        HEM3<>0
     C                   EVAL      SDATA  = %trim(SDATA )
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     C                             + 'Volum M3: '+ %trim(%editc(HEM3:'4'))
     c                   endif
     C                   if        HELM<>0
     C                   EVAL      SDATA  = %trim(SDATA )
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     C                             + 'Lastemeter: '+ %trim(%editc(HELM:'4'))
     c                   endif
     c                   if        HEGM1<>' '
     C                   EVAL      SDATA  = %trim(SDATA )
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     C                             + 'Merking: '+ %trim(HEGM1)
     c                   endif
     C                   if        HEGM2<>' '
     C                   EVAL      SDATA  = %trim(SDATA ) +', '
     C                             + %trim(HEGM2)
     c                   endif
     C                   EVAL      SDATA  = %trim(SDATA ) + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      * Merknad ??
     c                   if        UUMERKN<> *blanks and UUANNEN <> 'T'
      *
     C                   EVAL      SDATA  = '<tr><td><B>'
     C                                    + 'Merknad: ' + %trim(UUMERKN)
     c                                    + '</B></td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     c                   endif
      *
      *
      * start ved send til hentetransportør
     c                   if        UUANNEN = 'T'
     C                   EVAL      LINK   = 'http://'
     C                             + %trim(ARCPAE) + '/sycgip/esop11KT.pgm'
s04  c*                            +'?user=NN&MailAdr='+%trim(Madr)
n04  c                             +'?user='+%trim(SYVRDA)
n04  c                             +'&MailAdr='+%trim(Madr)
     c                             +'&heavd='+%trim(%editc(HEAVD:'Z'))
     c                             +'&heopd='+%trim(%editc(HEOPD:'Z'))
     c                             +'&wstra='+%trim(%editc(WSTRAN:'Z'))
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + '<a href="'+%trim(LINK)
     c                                    + '" target="TbW">'
     C                                    + 'For å få bilde for å bekrefte '
     C                                    + 'eller avvise tildelingen av hente'
     C                                    + 'oppdrag, klikke på denne lenken.'
     c                                    + '</a></td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     c                   endif
      *
     C                   EVAL      SDATA  = '<tr><td>&nbsp;</td></tr>'          Blank
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      * selve linken for labelprint
     C                   EVAL      LINK   = 'http://'
     C                             + %trim(ARCPAE) + '/sycgip/ss115.pgm'
S04  c*                            +'?user=NN&curtur='+AvdOpd
N04  c                             +'?user='+%trim(SYVRDA)+'&curtur='+AvdOpd
     c                             +'&labeltyp=L&lay=HE&copyprt=J'
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + '<a href="'+%trim(LINK)
     c                                    + '" target="PmW">'
     c                                    + 'Klikk her for å skrive ut '
     c                                    + 'merkelapper.'
     c                                    + '</a></td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
N03  c                   if        UUANNEN <> 'F'
     C                   EVAL      SDATA  = '<tr><td>&nbsp;</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      * selve linken for å melde oppdrag klart
     C                   EVAL      LINK   = 'http://'
     C                             + %trim(ARCPAE) + '/sycgip/esop11KH.pgm'
s04  c*                            +'?user=NN&MailAdr='+%trim(Madr)
n04  c                             +'?user='+%trim(SYVRDA)
n04  c                             +'&MailAdr='+%trim(Madr)
     c                             +'&heavd='+%trim(%editc(HEAVD:'Z'))
     c                             +'&heopd='+%trim(%editc(HEOPD:'Z'))
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + '<a href="'+%trim(LINK)
     c                                    + '" target="BeW">'
     C                                    + 'Når merkelappene er limt på god'
     C                                    + 'set og forsendelsen er klar for '
     C                                    + 'henting varsler du om det ved å '
     C                                    + 'klikke på denne lenken.'
     c                                    + '</a></td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>&nbsp;</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
N03  c                   endif
      *
      * selve linken for fraktbrevsprint
     C                   EVAL      LINK   = 'http://'
     C                             + %trim(ARCPAE) + '/sycgip/syfa12es.pgm'
s04  c*                            +'?user=NN'
n04  c                             +'?user='+%trim(SYVRDA)
     c                             +'&avd='+%trim(%editc(HEAVD:'Z'))
     c                             +'&opd='+%trim(%editc(HEOPD:'Z'))
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + '<a href="'+%trim(LINK)
     c                                    + '" target="PfW">'
     c                                    + 'Klikk her for å skrive ut '
     c                                    + 'fraktbrev.'
     c                                    + '</a></td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
N05   *
N05   * selve linken for CMR
N05  C                   EVAL      LINK   = 'http://'
N05  C                             + %trim(ARCPAE) + '/sycgip/esop11cm.pgm'
n05  c                             +'?user='+%trim(SYVRDA)
N05  c                             +'&avd='+%trim(%editc(HEAVD:'Z'))
N05  c                             +'&opd='+%trim(%editc(HEOPD:'Z'))
N05  C                   EVAL      SDATA  = '<tr><td>'
N05  C                                    + '<a href="'+%trim(LINK)
N05  c                                    + '" target="PfW">'
N05  c                                    + 'Klikk her for å skrive ut '
N05  c                                    + 'CMR-fraktbrev.'
N05  c                                    + '</a></td></tr>'
n05  C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>&nbsp;</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + 'Informasjon om oppdraget:  '
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
S01  c*                  if        UUANNEN <> ' '
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + 'Avsender: '
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     c                   if        UUTLF <>' ' or UUSMS <> ' '
     C                   EVAL      SDATA  = %trim(SDATA )+'        '
     C                                    + '( Tlf: ' +%trim(UUTLF)
     c                   if        UUSMS <> ' '
     C                   EVAL      SDATA  = %trim(SDATA )+' / '
     C                                    +%trim(UUSMS)
     c                   endif
     C                   EVAL      SDATA  = %trim(SDATA )+' )'
     c                   endif
     C                   EVAL      SDATA  = %trim(SDATA )
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     c                             + HENAS  + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     c                             + HEADS1 + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     c                             + HEADS3 + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
S01  c*                  endif
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + 'Mottaker:</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     c                             + HENAK  + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     c                             + HEADK1 + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     c                             + HEADK3 + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>&nbsp;</td></tr>'          Blank
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + 'Ref: ' + %trim(HERFA)
     C                                    + ' Vår ref:'
     C                                    + %trim(%editc(HEAVD:'Z'))+'/'
     C                                    + %trim(%editc(HEOPD:'Z'))
     C                                    +'  Sendingsnr: '+Snd17
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      * linken for spørring oppdrag
     C                   EVAL      LINK   = 'http://'
     C                             + %trim(ARCPAE) + '/sycgip/syso02.pgm'
s04  c*                            +'?user=NN&UsrSpcName=InT5WXz4BJ'
n04  c                             +'?user='+%trim(SYVRDA)
n04  c                             +'&UsrSpcName=InT5WXz4BJ'
     c                             +'&heavd='+%trim(%editc(HEAVD:'Z'))
     c                             +'&heopd='+%trim(%editc(HEOPD:'Z'))
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + '<a href="'+%trim(LINK)
     c                                    + '" target="SoW">'
     c                                    + 'Klikk her for oppdragsstatus og '
     c                                    + 'flere detaljer'
     c                                    + '</a></td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   ENDSR
      *
      *
      ******************************************************************************
     C     FRITRANS      BEGSR
      ******************************************************************************
      *
     C                   MOVE      'N'           XLEDERTXT         1
     C                   EVAL      FRTKOD = 'G'
     C     FREETXT2K     SETLL     FREETXT2
     C     FREETXT2K     READE     FREETXT2                               33
     C                   DOW       *IN33 = *OFF
      *
     C                   IF        XLEDERTXT = 'N'
     C                   EVAL      SDATA  = '<tr><td>&nbsp;</td></tr>'          Blank
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + 'Melding til transportør:'
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     C                   MOVE      'J'           XLEDERTXT         1
     C                   END
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                             + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
     C                             + %trim(FRTTXT)
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C     FREETXT2K     READE     FREETXT2                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ******************************************************************************
s08  C*    FRISPED       BEGSR
s08   ******************************************************************************
s08   *
s08  C*                  MOVE      'N'           XLEDERTXT         1
s08  C*                  EVAL      FRTKOD = ' '
s08  C*    FREETXT2K     SETLL     FREETXT2
s08   *    FREETXT2K     READE     FREETXT2                               33
s08   *                  DOW       *IN33 = *OFF
s08  C*
s08  C*                  IF        XLEDERTXT = 'N'
s08  C*                  EVAL      SDATA  = '<tr><td>&nbsp;</td></tr>'          Blank
s08   *                  EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
s08   *
s08  C*                  EVAL      SDATA  = '<tr><td>'
s08  C*                                   + 'Melding til avsender:'
s08  C*                                   + '</td></tr>'
s08   *                  EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
s08   *                  MOVE      'J'           XLEDERTXT         1
s08  C*                  END
s08  C*
s08   *                  EVAL      SDATA  = '<tr><td>'
s08   *                            + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
s08  C*                            + %trim(FRTTXT)
s08  C*                                   + '</td></tr>'
s08  C*                  EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
s08  C*
s08   *    FREETXT2K     READE     FREETXT2                               33
s08   *                  ENDDO
s08  C*
s08  C*                  ENDSR
      *
      *
      ******************************************************************************
     C     BUNN          BEGSR
      ******************************************************************************
      *
     C                   EVAL      SDATA  = '<tr><td>&nbsp;</td></tr>'          blank
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + 'Med vennlig hilsen'
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + FIFT
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + '<img src="http://'+%trim(ARCPAE)
     c                                    + '/syspeddev/systematop1.jpg">'
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + 'Telefon ' + TLF
     C                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + 'Denne eposten er skapt'
     C                             + ' automatisk av booking systemet.'
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '<tr><td>'
     C                                    + 'Vennligst ikke svar på den.'
     c                                    + '</td></tr>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   EVAL      SDATA  = '</table></Body></HTML>'
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     TTSR          BEGSR
      ******************************************************************************
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'CMS'         TTACTI
     C                   MOVE      ZUSER         TTUSER
     c                   if        UUUSER <> *blanks
     C                   MOVE      UUUSER        TTUSER
     c                   endif
     C                   EVAL      TTTEXT ='Coll. mail sent'
     C                   EVAL      TTTEXL ='Hentemail sendt'
     c                   eval      SMS = UUSMS
     c                   if        UUANNEN <> ' '
     c                   eval      SMS = UUASMS
     c                   endif
     c                   eval      MailSMS = UUMAIL
     c                   if        SMS<>*blanks
     c                   eval      MailSMS = %trim(MailSMS) + ' SMS:'
     c                                     + SMS
     c                   endif
     c                   eval      TTTEXT  = %trim(TTTEXT)+' '+ MailSMS
     c                   eval      TTTEXL  = %trim(TTTEXL)+' '+ MailSMS
     C                   MOVE      'S'           TTMANU
     C                   write     TRACKFR
      *
     c                   if        UUMERKN<>*blanks
     C                   MOVE      'CMM'         TTACTI
     C                   EVAL      TTTEXT ='Remarks: ' + UUMERKN
     C                   EVAL      TTTEXL ='Merknad: ' + UUMERKN
     C                   write     TRACKFR
     c                   endif
      *
      * TILDELING TIL TRANSPORTØR ??
     C     WSTRAN        IFNE      *ZEROS
     C                   MOVE      'CAT'         TTACTI
     C                   EVAL      TTTEXT ='Coll. assigned transporter'
     C                   EVAL      TTTEXL ='Henting tildelt transportør'
     c                   eval      TTTEXT  = %trim(TTTEXT)+' '
     c                             +%triml(%editc(WSTRAN:'Z'))+' '+UUTRANAV
     c                   eval      TTTEXL  = %trim(TTTEXL)+' '
     c                             +%triml(%editc(WSTRAN:'Z'))+' '+UUTRANAV
     C                   MOVEL     UUTRAA        TTDEPO
     C                   MOVE      'S'           TTMANU
     C                   write     TRACKFR
     C                   ENDIF
      *
      * SEND SMS ??
     C                   IF        SMS <> *BLANKS
     C                   EXSR      LAGSMS
     C                   ENDIF
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     LAGSMS        BEGSR
      ******************************************************************************
      * Lag fil og set codepage (pc)
     C                   time                    Timedata1
     C                   movel     timedata1     TS26             26            må være 26 lang
     C                   movel     TS26          TS               22
      *                                                 typisk TS er nå  2012-09-08-17.39.49.73
     C                   EVAL      WFilNam ='/SYSPEDDEV/TEMP/'
     C                                     + TS + '_' + AvdOpd + '.html'
     C                   EVAL      FILE_o = open( %TrimR( WFilNam )
     C                             : 'w, codepage=850' )
     C                   EVAL      rc = close( FILE_o )
     C                   EVAL      FILE_o = open( %TrimR( WFilNam )
     C                             : 'w' )
S01   *
N01  C                   Eval      EMNE  = *blanks
N01  C                   Eval      SDATA = 'Varsel om henting '
N01  C                                    + %trim(HERFA) + ' for '
N01  C                                    + %trim(HENAA)
N01  C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     C                   EVAL      SDATA  = 'Epost er sendt til '+%trim(UUMAIL)
     c                             +' med instrukser. '
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     C                   EVAL      SDATA  = 'Med hilsen fra '
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     C                   EVAL      SDATA  = FIFT
     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
      *
      * close file
     C                   EVAL      rc = close( FILE_o )
      *
     C                   EVAL      AvNa ='SMS.SYSTEMA'
     C                   EVAL      AvAd ='roger@systema.no'
     C                   EVAL      Madr =%trim(SMS)+'@esms.nu'
     C                   EXSR      SENDMail
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     INIT          BEGSR
      ******************************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    HEAVDa            4
     C                   PARM                    HEOPDa            7
     C                   PARM                    UUMAIL           50
     C                   PARM                    UUTLF            15
     C                   PARM                    UUSMS            15
     C                   PARM                    UUANNEN           1
     C                   PARM                    UUASMS           15
     C                   PARM                    UUTRAA            8
     C                   PARM                    UUTRANAV         20
     C                   PARM                    UUMERKN          50
     C                   PARM                    UUUSER           10
      *
     C     UUANNEN       IFEQ      'T'
     C                   MOVE      UUTRAA        WSTRAN            8 0
     C                   ENDIF
     C                   MOVE      HEAVDa        HEAVD
     C                   MOVE      HEOPDa        HEOPD
     C                   Movel     HEAVD         AvdOpd           11
     C                   Move      HEOPD         AvdOpd
n02  c                   call      'SYGE73CL'
n02  c                   parm                    epostf           70
s02  C*                  EVAL      AvNa ='Jan Ottar '
n02  C                   EVAL      AvNa =epostf
s02  C*                  EVAL      AvAd ='janottar@systema.no'
n02  C                   EVAL      AvAd =epostf
     C                   EVAL      Madr   = UUMAIL
     c     HeaKey        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c     FREETXT2K     klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c                   kfld                    FRTKOD
n04  c     sypak         klist
n04  c                   kfld                    sykd              5
n04  c                   move      'ESUSR'       SYKD
n04  c     sypak         chain     SYPARL3                            33
n04  C   33              EVAL      SYVRDA = 'NN'
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C     FIFIRM        CHAIN     FIRMARC                            33
      *
      * hent telefonnr
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   EVAL      PABRID = '*KUNDFT*' + %TRIM(FIFIRM)
     C                   MOVE      'TLF  '       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     c  n33              movel     PAVRDA        TLF              20
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *
      *
      ******************************************************************************
     C     SendMail      BEGSR
      ******************************************************************************
      * Send via MMAIL
     C                   EVAL      CmdLine = 'ADDLIBLE MMAIL'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'                            98
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen
      *
N06   * Appo i Emne gir error/sender ikke i MMMAIL (uten at bruker får varsel)
N06  c     x'7D':'"'     xlate     EMNE          EMNE
     C                   EVAL      CmdLine = 'EMLHTML SUBJECT('+ X'7D'
     C                             + %trimr(Emne) + X'7D' + ') '
     C                             + 'FROMNAME(' + X'7D'
     C                             + %trimr(AvNa) + X'7D' + ') '
     C                             + 'FROMADDR('  + X'7D'
     C                             + %trimr(AvAd) + X'7D' + ') '
     C                             + 'TO('+ X'7D'+%trimr(Madr)+ X'7D'+'/'+ X'7D'
     C                             + %trimr(Madr)+ X'7D'+'/*TO) '
E07  C                             + 'HTML('+ X'7D'+%trimr(WFilNam)+ X'7D'+ ')'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'                            98
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen
      *
     C                   EVAL      CmdLine = 'RMVLIBLE MMAIL'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'                            98
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen
      *
     C                   ENDSR
      *
