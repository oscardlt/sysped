     A*%%TS  SD  20230328  140344  CB          REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6XXXA* 01 YBC PTF6 VALG FOR ARBEID MED KODER FOR FØLSOM VARE
P8048A* 02 YBC PTF8 NYE FELTER FOR TOLLSTEDER
P8XXXA* 03 YBC PTF8 FEILMEDLINGER FOR GARANTI
P9041A* 04 YBC PTF9 FORHÅNDSVARSLING
12280A* 05 YBC PTF12 Nye koder for ekspressfortolling
12287A* 06 YBC PTF12 Nye koder for NCTS fase 5
12287A* 07 CB  PTF12 SØK PÅ UNIK
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R TR001D01
     A*%%TS  SD  20230328  140344  CB          REL-V7R4M0  5770-WDS
     A                                      CA03(03 'Avslutt')
     A                                      CA04(04 'Søk')
     A                                  1  2'SYSPED/TR001R'
     A                                      COLOR(BLU)
     A                                  1 19'Arbeid med koder for el. transit'
     A                                      COLOR(WHT)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  2 69DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  3 42'012=Språk kode'
     A                                      COLOR(BLU)
     A                                  4 42'013=Dokument kode'
     A                                      COLOR(BLU)
     A                                  5 42'014=Tidligere dokument'
     A                                      COLOR(BLU)
     A                                  6 42'017=Pakketype'
     A                                      COLOR(BLU)
     A                                  7  2'Velg type av koder:'
     A            WSUNIK         3Y 0B  7 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  7 42'031=Deklarasjons type'
     A                                      COLOR(BLU)
     A                                  8 42'039=Tilleggsopplysning'
     A                                      COLOR(BLU)
     A                                  9  2'018=Transport måte'
     A                                      COLOR(BLU)
     A                                  9 42'047=Kontroll resultat'
     A                                      COLOR(BLU)
     A                                 10  2'228=Tidligere dokoment type'
     A                                      COLOR(BLU)
     A                                 10 42'064=Følsom vare'
     A                                      COLOR(BLU)
     A                                 11  2'704=Aktør rolle kode'
     A                                      COLOR(BLU)
     A                                 11 42'096=Spesielle omstendigheter'
     A                                      COLOR(BLU)
     A                                 12  2'719=CUS kode'
     A                                      COLOR(BLU)
     A                                 12 42'105=Adgangskode for garanti'
     A                                      COLOR(BLU)
     A                                 13  2'729=Type av person'
     A                                      COLOR(BLU)
     A                                 13 42'106=Tollsted referansenr'
     A                                      COLOR(BLU)
     A                                 14  2'749=Type av gods'
     A                                      COLOR(BLU)
     A                                 14 42'116=Transportkostnad betalingsmåte'
     A                                      COLOR(BLU)
     A                                 15  2'750=Type av tr.middel id'
     A                                      COLOR(BLU)
     A                                 16  2'751=Type av tr.middel'
     A                                      COLOR(BLU)
     A                                 17  2'754=Transport dokumenttype'
     A                                      COLOR(BLU)
     A                                 17 42'023=Feilkoder i CONTROL'
     A                                      COLOR(BLU)
     A                                 18  2'756=Finasiell referanserolle'
     A                                      COLOR(BLU)
     A                                 18 42'041=Kontroll indikator VAREPOST'
     A                                      COLOR(BLU)
     A                                 19 42'042=Kontroll indikator TRANSIT'
     A                                      COLOR(BLU)
     A                                 20 42'049=Feilkoder i CUSRES feilmelding'
     A                                      COLOR(BLU)
     A                                 21  2'027/213/214/217/218/235/239/326/34-
     A                                      7/380'
     A                                      COLOR(BLU)
     A                                 21 42'149=Feilkoder for datagruppe i CUS-
     A                                      RES'
     A                                      COLOR(BLU)
     A                                 22  2'384/942(CL042)'
     A                                      COLOR(BLU)
     A                                 22 42'209=Feilkoder for garanti'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
N07  A                                  7 28'F4=Søk'
N07  A                                      COLOR(BLU)
      *
     A          R TR001D11                  SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            TKKODE        10   O  5  5
     A            TKTXTN        35A  O  5 16
     A            TKTXTE        35A  H
N02  A            TKAVG          1A  O  5 52
N02  A            TKANK          1A  O  5 56
N02  A            TKTRS          1A  O  5 60
      *
     A          R TR001D12                  SFLCTL(TR001D11)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CA03(03 'Avslutt program')
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A  91N93                               CA12(12 'Forrige')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/TR001R'
     A                                      COLOR(BLU)
     A            TR1MSG        40   O  1 22MSGID(&MSGNRW SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNRW         7A  P
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
N06  A            WSUNIK         3Y 0O  1 76DSPATR(UL)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  2  3MSGCON(030 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  3MSGCON(030 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
S02  A*                                 4  2'Op Kode       Beskrivelse'
N02  A N28                              4  2'Op Kode       Beskrivelse'
     A                                      COLOR(WHT)
N02  A  28                              4  2'Op Kode       Beskrivelse         -
N02  A                                                      Avg Ank Trs'
N02  A                                      COLOR(WHT)
      *
     A          R TR001D13
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CA12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                 23  2'F3=Avslutt   F5=Frisk   F6=Lage ny-
     A                                         F8=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R TR001D14
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
E02  A N28                              1  2'   Kode       Beskrivelse (Norsk/-
     A                                      engelsk)'
     A                                      COLOR(WHT)
N02  A  28                              1  2'   Kode       Beskrivelse (Norsk/-
N02  A                                      engelsk)        Avg Ank Trs'
N02  A                                      COLOR(WHT)
     A            WSKODE        10A  B  2  5TEXT('KODE')
     A N30N40
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WSTXTN        35A  B  2 16TEXT('FRITEKST')
     A N30 40
     AO 89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      DSPATR(UL)
N02  A  28        WSAVG          1A  B  2 52TEXT('AVGANG')
N02  A                                      DSPATR(UL)
N02  A                                      VALUES('J' 'N')
N02  A  28        WSANK          1A  B  2 56TEXT('ANKOMST')
N02  A                                      DSPATR(UL)
N02  A                                      VALUES('J' 'N')
N02  A  28        WSTRS          1A  B  2 60TEXT('TRANSITT')
N02  A                                      DSPATR(UL)
N02  A                                      VALUES('J' 'N')
     A            WSTXTE        35A  B  3 16TEXT('FRITEKST')
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  4  2MSGCON(018 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R TR001D15
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
E02  A N28                              1  2'   Kode       Beskrivelse (Norsk/-
     A                                      engelsk)'
     A                                      COLOR(WHT)
N02  A  28                              1  2'   Kode       Beskrivelse (Norsk/-
N02  A                                      engelsk)        Avg Ank Trs'
N02  A                                      COLOR(WHT)
     A            TKKODE        10A  O  2  5TEXT('KODE')
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            TKTXTN        35A  O  2 16TEXT('FRITEKST')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
N02  A  28        TKAVG          1A  O  2 52TEXT('AVGANG')
N02  A                                      DSPATR(HI)
N02  A                                      DSPATR(UL)
N02  A  28        TKANK          1A  O  2 56TEXT('ANKOMST')
N02  A                                      DSPATR(HI)
N02  A                                      DSPATR(UL)
N02  A  28        TKTRS          1A  O  2 60TEXT('TRANSITT')
N02  A                                      DSPATR(HI)
N02  A                                      DSPATR(UL)
     A            TKTXTE        35A  O  3 16TEXT('FRITEKST')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  4  2MSGCON(018 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R TR001D16
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  2  2'Søk fra kode  --->'
     A            W2KODE        10A  B  2 22TEXT('KODE')
     A                                      DSPATR(PC)
     A                                      DSPATR(UL)
     A                                  3  2'Søk fra tekst --->'
     A            W2TXTN        35A  B  3 22TEXT('FRITEKST')
     A                                      DSPATR(UL)
     A                                  4  2MSGCON(018 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R TR001D17
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CA12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/TR001R'
     A                                      COLOR(BLU)
     A            TR1MSG        40   O  1 22MSGID(&MSGNRW SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNRW         7A  P
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
N06  A            WSUNIK         3Y 0O  1 76DSPATR(UL)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  2  3MSGCON(030 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  3MSGCON(030 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
S02  A*                                 4  2'Op Kode       Beskrivelse'
N02  A N28                              4  2'Op Kode       Beskrivelse'
     A                                      COLOR(WHT)
N02  A  28                              4  2'Op Kode       Beskrivelse         -
N02  A                                                      Avg Ank Trs'
N02  A                                      COLOR(WHT)
     A                                 23  2'F3=Avslutt   F5=Frisk   F6=Lage ny-
     A                                         F8=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
