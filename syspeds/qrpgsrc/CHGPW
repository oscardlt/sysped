      *********************************************************************
      * Spørring på ISO (vis meny)
      * PROGRAM BEHANDLER INPUT FRA SYMN1
      *
CRTPG+*  CRTPGM SYSPED/CHGPW MODULE(*LIBL/CHGPW
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 VIS EGEN LOGO
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
     D UsrSpcName      s             10
     D WSPWGM          S             10
     D WSPWNY1         S             10
     D WSPWNY2         S             10
     D WSENDRE         S              1
     D FMELDNR         S              2
     D FEILMELDING     S             80
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      * For å konvertere uheldige bokstaver
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D*FEIL01          C                   CONST('Gjeldende passord er -
     D*                                    ugyldig.')
     D*FEIL02          C                   CONST('Nytt passord er ugyldig.')
     D*FEIL03          C                   CONST('Bekreftet passord er ikke -
     D*                                    samme som nytt passord.')
     D FEIL01          C                   CONST('Current password is not -
     D                                     valid.')
     D FEIL02          C                   CONST('New password is not valid.')
     D FEIL03          C                   CONST('Confirmed new password is -
     D                                     not same as new password.')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
     C                   IF        WSENDRE = 'J'
     C                   EXSR      TESTB
     C                   END
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      WSPWGM  = zhbgetvar('WSPWGM')
     C     LC:UC         XLATE     WSPWGM        WSPWGM
     C                   eval      WSPWNY1 = zhbgetvar('WSPWNY1')
     C     LC:UC         XLATE     WSPWNY1       WSPWNY1
     C                   eval      WSPWNY2 = zhbgetvar('WSPWNY2')
     C     LC:UC         XLATE     WSPWNY2       WSPWNY2
     C                   eval      WSENDRE = zhbgetvar('WSENDRE')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
N01  C                   callp     updHTMLvar('LogoImg':LogoImg)
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C     LC:UC         XLATE     KNAVN         KNAVN
     C                   callp     updHtmlVar('KNAVN':KNAVN)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updHtmlVar('WSPWGM':WSPWGM)
     C                   callp     updHtmlVar('WSPWNY1':WSPWNY1)
     C                   callp     updHtmlVar('WSPWNY2':WSPWNY2)
     C                   IF        WSENDRE = ' '
     C                   MOVE      'J'           WSENDRE
     C                   END
     C                   callp     updHTMLvar('WSENDRE':WSENDRE)
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
      *
     C                   endsr
      *
      *=====================================================================******
      *  TEST PASSORD
      *=====================================================================******
     C     TESTB         begsr
      *
     C     WSUSER        CHAIN(N)  BRIDF                              33
     C  N33WSPWGM        COMP      BIPO                               3333
     C                   IF        *IN33
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      SLTBDT
     C                   END
      *
     C                   IF        ((WSPWNY1 = *BLANKS) OR (WSPWNY1 = WSPWGM))
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      SLTBDT
     C                   END
      *
     C                   IF        WSPWNY1 <> WSPWNY2
     C                   MOVEL     FEIL03        FEILMELDING
     C                   MOVE      '03'          FMELDNR
     C                   GOTO      SLTBDT
     C                   END
      *
     C     WSUSER        CHAIN     BRIDF                              33
     C                   MOVE      WSPWNY1       BIPO
     C                   UPDATE    BRIDFR
     C                   MOVE      'F'           WSENDRE
      *
     C     SLTBDT        endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'CHGPW'
     C*                  CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
N01   * hent Parametre som går igjen i mange prog:
N01   *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
N01  c                   call      'ESGETPAR'
N01  c                   parm                    BIBRID
N01  c                   parm                    BIFIRM
N01  c                   parm                    BIKUNR
N01  c                   parm                    BIKNG
N01  c                   parm                    RowHead          10
N01  c                   parm                    Odd              10
N01  c                   parm                    Even             10
N01  c                   parm                    LogoImg          50
N01  c                   parm                    XSPRÅK            2
N01  c                   parm                    XINTERN           1
N01  c                   parm                    ParmDS1        1024
N01  c                   parm                    ParmDS2        1024
N01  c                   parm                    ParmDS3        1024
      *
     C                   EXSR      TSTUSER
      *
     C                   OPEN      CUNDL01
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     CUNDL01
     C                   eval      *inlr = *on
      *
     C                   endsr
