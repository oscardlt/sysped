     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/WRKAG)
     A                                      PRINT
     A                                      CF03(03 'Gå ut')
     A                                      CF10(10 'Send')
     A                                      CF12(12 'Forrige')
      *
     A          R E300DB                    SFL
     A            WRAVD     R        H
     A            WRPRO     R        H
     A            FIRMA     R        H
     A            ZOP            1A  I  7  3
     A  41                                  DSPATR(ND)
     A                                      DSPATR(PC)
     A                                      VALUES(' ' '4')
     A            KUNDNR    R        O  7  6EDTCDE(4)
     A            XCCNAM        30A  O  7 15
     A            XCOM          30   O  7 47COLOR(RED)
      *
     A          R E300DC                    SFLCTL(E300DB)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF05(05 'Refresh')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'EDI    '
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(020 EDI0546 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 76'E305D'
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(040 JOV0769 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(029 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  4MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(003 EDI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  7MSGCON(007 EDI0550 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 15MSGCON(004 EDI0186 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 47MSGCON(011 EDI0552 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E300DD
     A                                      OVERLAY
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 28MSGCON(008 EDI0810 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 39MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E300DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
