********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 logikk ved heknt
10007 * 02 JOV PTF10 Fjern fra tur: HOVEDTRANSPs forventede bet. på oppdrnivå (4/5) fjernes i KOSBU
10XXX * 03 SH  PTF10 A=Arkiv
10XXX * 04 SH  PTF10 S=Skilleark
10XXX * 05 SH  PTF10 nye parameter sytt01
10XXX * 06 SH  PTF10 Q=question pris
10XXX * 07 SH  PTF10 7=FRI SØKEVEI
10XXX * 08 jov PTF10 Ved PDA, godsbesk=ikke HENTET, GUL IKKE LEVERT - Grønn = OK
10XXX * 09 SH  PTF10 OPSJON = I   INNLANDSFRAKTBREV
10261 * 10 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
      *%%%%%%%%%%%%%%%%%%%%
10XXX * 11 sh  PTF11 opsjon 6 rett til faktura
10XXX * 12 sh  PTF11 arc045 opsjon Q rettet
10XXX * 13 jov PTF11 opsjon W=se oppdrag på web
10XXX * 14 Jov PTF11 opsjon U=Urgent/prioritert gods
10XXX * 15 sh  PTF11 bringtms summere helmla/hepoen   BRINGTMS-prosjekt.
********************************************************************
      *
      ****************************************************************
      *** PROGRAM SKAL VEDLI./SPØRRE PÅ TURENS INNHOLD             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 02 04 05 08-11 13-24                  ***
      *** LEDIGE INDIKATORER 28 29 32 35-90 95-97 99               ***
      *** INDIKATOR 27 PÅ --> KALLE FRA SYTR89   (SPØRRING PÅ TUR) ***
      ***           27 AV --> KALLE FRA SYTR88   (VEDL. TUR)       ***
      ****************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
      *
     FTURER     UF   E           K DISK
     FDISP      UF   E           K DISK
     FHEADF     UF   E           K DISK
     FHESTED    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD11R)
     FKOSBU9    UF   E           K DISK
     FFAK2      IF   E           K DISK
     FTKSGM     IF   E           K DISK
     FTRACKL02  IF   E           K DISK
s01  F*REETXT2  IF   E           K DISK
N01  FFREETXT3  IF   E           K DISK
     FSYTR88FB  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  FAVT                   1     20
     D  WSNØKK                20     20
     D                 DS
     D  HEVKT                  1      9  0
     D  HEVKTW                 5      9  0
     D                 DS
     D  HEVS1                  1     25
     D  HEVS10                 1     10
     D                 DS
     D  HENT                   1      7  0
     D  HENT4                  4      7  0
     D                 DS
     D  REFTi6                 1      6  0
     D  REFTID                 1      4  0
N02  D                 DS
N02  D  BUTXT                  1     35
N02  D  BUTX_20                1     20
N02  D  BUTX_7                29     35
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C     UPRO          Chain     TKSGM                              70
     C  N70              MOVE      TGTIR         REFTI6
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UPRO          SETLL     HEAD11
     C     START1        TAG
     C     UPRO          READE     HEAD11                                 94
      *
     C     START2        TAG
     C                   Move      'N'           PDAFarge          1
      *
     C  N94              DO                                                     1<-B
     C                   DO        6                                             2<-B
     C                   ADD       1             ZRECNO
     C                   MOVE      *BLANKS       IMCO
     C                   MOVE      *BLANKS       ETTE
     C     TRFA21        IFNE      *BLANKS
     C                   MOVE      'A'           IMCO                            *ADR.gods
     C                   END
     C     TRFA11        IFNE      *BLANKS
     C                   MOVE      'I'           IMCO                            *IMCO gods
     C                   END
     C     TRETTB        IFNE      *ZEROS
     C                   MOVE      'E'           ETTE
     C                   END
     C     FIDTFM        IFNE      3
     C                   MOVE      TRSDFD        WRSDFD
     C                   MOVE      TRSDTD        WRSDTD
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WRSDFD
     C                   PARM                    WRSDTD
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C                   Z-ADD(H)  HEM3          WSM3SU
     C     HEADFK        CHAIN     HESTED                             33
     C  N33              DO
     C                   MOVEL     HSFROM        HESDF
     C                   MOVEL     HSTO          HESDT
     C                   END
     C     TROPD0        COMP      *ZEROS                             5959
     C                   exsr      TSTPDA
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C     UPRO          READE     HEAD11                                 94
     C  N94              END                                                     2<-E
     C                   END                                                    1<-E
     C* Sist leste post (hvis ikke EOF) er 1. post for neste skjermside
     C* (det leses "en for langt" for at en skal kunne vise '+' i bunn)
     C* Bevar alle subfildata i arbeidsvariable slik at de kan hentes
     C* tilbake uten disk-read ved roll videre (forenkler + sparer tid)
     C*
S15  C*    *IN94         IFEQ      '0'
S15  C*                  EXSR      MOVSUX
S15  C*                  END
     C* Varsel ved ny/endret på PDA som ikke er bekreftet.
     C     *in30         IFEQ      '0'
     C     PDAFarge      andeq     'J'
     C                   MOVE      'JOV1374'     MSGNR
     C                   SETON                                            30
     C                   end
     C*
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9830
      *
      * F05= REFRESH
     C   05              GOTO      START
      *
      * F06= PLUKK
     C   06              DO
     C                   EXSR      PLUKKE
     C  N01
     CANN03              Z-ADD     1             YAVD
     C  N01
     CANN03              GOTO      START
     C                   END
      *
      * F07=OMDISP
     C   07              DO
     C                   EXSR      OMDISP
     C  N01
     CANN03              Z-ADD     1             YAVD
     C  N01
     CANN03              GOTO      START
     C                   END
     C   01              Z-ADD     1             UFLAGG
     C   01              GOTO      SLUTT
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C   12              Z-ADD     9             UFLAGG
     C   12              GOTO      SLUTT
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
S15  C*                  EXSR      MOVXSU
N15  C     UPRO          READPE    HEAD11                                 94
N15  C  N94UPRO          READE     HEAD11                                 94
S15  C*                  GOTO      START2
N15  C  N94              GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      TESTIN
     C     ENDRE         IFEQ      'J'
     C                   Z-ADD     1             YAVD
     C                   GOTO      START
     C                   END
     C                   END
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*
     C***********************************************
     C     TSTPDA        BEGSR
     C***********************************************
      * PDA-ordre NY ubekreftet 71=Rosa, ENDRET ubekreftet 72=Gul avsendernavn
      * PDA-ordre               76=Rød=ikke/hentet ikke levert, 77=Hentet men ikke levert
S08  C*                  Movea     '00000'       *IN(71)
N08  C*                  Movea     '0000000'     *IN(71)
N14  C                   Movea     '00000000'    *IN(71)
N14  C                   MOVE      'URG'         XXACTI
N14  c     TTKey         Chain     TRACKl02                           33
N14  c                   if        *in33=*off and %subst(TTTEXT:1:8)='*URGENT*'
N14  c                   move      *on           *in78
N14  c                   endif
      * 70 off = TUR - finnes i TKSGM = hentet av PDA
     C     *in70         ifeq      *off
     C     TKSGMK        Chain     TKSGM                              33
     C     *in33         ifeq      *off
     C     TGKOH         COMP      'A'                                    71
     C     TGKOH         COMP      'U'                                    72
     c     *in71         IFEQ      '1'
     c     *in72         Oreq      '1'
     c                   Move      'J'           PDAFarge
     c                   end
      * PDA-ordre - endret vekt vol, mm som ikke er kvittert av saksbehandler
S08  c*    TTCHGKey      Chain     TRACKl02                           33
N08  C                   MOVE      'CHG'         XXACTI
N08  c     TTKey         Chain     TRACKl02                           33
     C  N33              Move      *ON           *IN73
      * Ulest Melding (kode P) i notisblokk
s01  c*    PDFTSKey      Chain     FreeTxt2                           33
N01  c     PDFTSKey      Chain     FreeTxt3                           33
     C  N33              Move      *ON           *IN75
     c                   end
      *
N08  C                   MOVE      'POD'         XXACTI
N08  c     TTKey         Chain     TRACKl02                           33
N08  C                   if        *in33=*on and TRSLAG='FF'
N08  C   33              MOVE      'TEI'         XXACTI
N08  c   33TTKey         Chain     TRACKl02                           33
N08  c                   endif
N08  C     *in33         ifeq      *ON
N08  C                   MOVE      'TE2'         XXACTI
N08  c     TTKey         setll     TRACKl02
N08  c     TTKey         reade     TRACKl02                               33
N08  c     *in33         doweq     '0'
N08  c                   if        %subst(TTTEXT:1:9)='Signed by'
N08  c                   leave
N08  c                   endif
N08  c     TTKey         reade     TRACKl02                               33
N08  c                   enddo
N08  c                   endif
N08   * er ikke levert...
N08  C     *in33         ifeq      *ON
N08  C                   Movea     '01'          *IN(76)
N08  C                   MOVE      'COL'         XXACTI
N08  c     TTKey         Chain     TRACKl02                           33
N08  C   33              MOVE      'TEU'         XXACTI
N08  c   33TTKey         Chain     TRACKl02                           33
N08   * heller ikke hentet
N08  C   33              Movea     '10'          *IN(76)
N08  c                   endif
      *
     C                   end
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSUX        BEGSR
     C***********************************************
     C* Post er lest, men skal først senere skrives til subfil.
     C* Flytter de nødvendige subfilbegrep
     C* Felt i subfila som er hentet opp FØR SETSUB
     C                   MOVE      HEAVD         XXAVD
     C                   MOVE      HEOPD         XXOPD
     C                   MOVE      HENAS         XXNAS
     C                   MOVE      HENAK         XXNAK
     C                   MOVE      HESDF         XXSDF
     C                   MOVE      HESDT         XXSDT
     C                   MOVE      HENT4         XXNT4
     C                   MOVE      HEVS10        XXVS10
     C                   MOVE      HEVKTW        XXVKTW
     C                   MOVE      HELM          XXLM
N15  C                   MOVE      HELMLA        XXLMLA
N15  C                   MOVE      HEPOEN        XXPOEN
     C                   MOVE      HEOT          XXOT
     C                   MOVE      HEFR          XXFR
     C                   MOVE      HEST          XXST
     C                   MOVE      TROPD0        XXOPD0
     C                   MOVE      WSM3SU        XXM3SU
     C                   MOVE      WRSDFD        XXSDFD
     C                   MOVE      WRSDTD        XXSDTD
     C                   MOVE      IMCO          XXIMCO
     C                   MOVE      ETTE          XXETTE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVXSU        BEGSR
     C***********************************************
     C*
     C                   MOVE      XXAVD         HEAVD
     C                   MOVE      XXOPD         HEOPD
     C                   MOVE      XXNAS         HENAS
     C                   MOVE      XXNAK         HENAK
     C                   MOVE      XXSDF         HESDF
     C                   MOVE      XXSDT         HESDT
     C                   MOVE      XXNT4         HENT4
     C                   MOVE      XXVS10        HEVS10
     C                   MOVE      XXVKTW        HEVKTW
     C                   MOVE      XXLM          HELM
N15  C                   MOVE      XXLMLA        HELMLA
N15  C                   MOVE      XXPOEN        HEPOEN
     C                   MOVE      XXOT          HEOT
     C                   MOVE      XXFR          HEFR
     C                   MOVE      XXST          HEST
     C                   MOVE      XXOPD0        TROPD0
     C                   MOVE      XXM3SU        WSM3SU
     C                   MOVE      XXSDFD        WRSDFD
     C                   MOVE      XXSDTD        WRSDTD
     C                   MOVE      XXIMCO        IMCO
     C                   MOVE      XXETTE        ETTE
     C                   ENDSR
     C*
     C***********************************************
     C     CALTOT        BEGSR
     C***********************************************
      * KALLER EKSTERNT PROGRAM SOM SUMMERER OG OPPDATWERER TUREN
      *
N15  C                   CALL      'SYGE80'
N15  c                   parm                    UPRO              8 0
N15  c                   parm                    WSNT              7 0
N15  c                   parm                    WSVKT             9 0
N15  c                   parm                    WSM3              7 3
N15  c                   parm                    WSLM              4 2
N15  c                   parm                    WSLMLA            4 2
N15  c                   parm                    WSPOEN            5 0
N15  c                   parm                    WSNTO             5 0
S15  C*    UPRO          SETLL     HEAD11
S15  C*                  SETOFF                                       33
S15  C*    STCAL         TAG
S15  C*    UPRO          READE     HEAD11                                 33
S15  C* N33              DO
S15  C*                  ADD       HENT          WSNT
S15  C*                  ADD       HEVKT         WSVKT
S15  C*                  ADD       HEM3          WSM3
S15  C*                  ADD       HELM          WSLM
S15  C*                  ADD       HELMLA        WSLMLA
S15  C*                  ADD       HEPOEN        WSPOEN
S15  C*                  ADD       1             WSNTO
S15  C*                  GOTO      STCAL
S15  C*                  END
S15  C*
S15  C*    SLCAL         TAG
S15  C*    TURERK        CHAIN     TURER                              33
S15  C*                  Z-ADD     WSNTO         TUAO
S15  C*                  Z-ADD     WSNT          TUTS
S15  C*                  Z-ADD     WSVKT         TUTVKT
S15  C*                  Z-ADD     WSM3          TUTM3
S15  C*                  Z-ADD     WSLM          TUTLM
S15  C*                  Z-add     WSLMLA        TUTLM2
S15  C*                  Z-add     WSPOEN        TUPOEN
S15  C*                  UPDATE    TURERR
     C*
     C* FIKSER DATO
     C*
     C                   MOVE      TUDT          WSDAFR
     C                   MOVE      TUDTT         WSDATI
     C     FIDTFM        IFNE      1
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WSDAFR
     C                   PARM                    WSDATI
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C*
     C     TUKVKT        ADD       TUTARA        TUBER2
     C     WSVKT         ADD       TUTARA        WSBER1
     C                   Z-ADD     1             YAVD
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TESTIN        BEGSR
     C***********************************************
     C                   MOVE      'N'           ENDRE             1
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C*
     C* SLETTING FRA TUR
     C*
     C  N98WSNR          IFEQ      '4'                                           2<-B
     C   27              DO                                                       3<-B
     C                   MOVE      'SPO0008'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C  N98HENAS         IFEQ      XSLETT                                         3<-B
     C                   MOVE      'SPO0009'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C  N98              EXSR      SLETT
     C* ALL SUBFIL INFO  BLANKES ETTER SLETT LIKEVEL-UNØDVENDIG CHAIN
     C                   SETON                                        55
     C                   UPDATE    SUBFIL
     C                   SETOFF                                       55
     C                   GOTO      SLTST
     C                   END                                                     2<-E
     C*
     C* ENDRING AV ORDRE
     C*
     C  N98WSNR          IFEQ      '2'                                           2<-B
N11  C     WSNR          OREQ      '6'                                           2<-B
     C* KAN IKKE ENDRES VED KALL FRA FULLLOAD
     C     USPØR         IFEQ      'F'                                            3<-B
     C                   MOVE      'SHU0012'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C   27              DO                                                       3<-B
     C                   MOVE      'SPO0008'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C  N98HENAS         IFEQ      XSLETT                                         3<-B
     C                   MOVE      'SPO0009'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C  N98              MOVE      'J'           ENDRE
     C  N98              EXSR      CHANGE
     C   98              MOVE      ' '           WSNR
     C                   exsr      TSTPDA
     C                   UPDATE    SUBFIL
     C                   GOTO      SLTST
     C                   END
     C*
     C* SPØRRING PÅ ORDRE
     C*
     C  N98WSNR          IFEQ      '5'                                           2<-B
     C     HENAS         IFEQ      XSLETT                                         3<-B
     C                   MOVE      'SPO0009'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C                   exsr      TSTPDA
     C                   UPDATE    SUBFIL
     C  N98              EXSR      VISOPD
     C                   GOTO      SLTST
     C                   END                                                     2<-E
N07   *
N07   * Frie søkeveier (gammen 07 ISO flyttet til 17 )
N07   *
N07  C  N98WSNR          IFEQ      '7 '
N07  C                   exsr      TSTPDA
N07  C                   UPDATE    SUBFIL
N07  C                   MOVE      *ZEROS        OTIKU1            8 0
N07  C                   MOVE      *ZEROS        OTIKU2            8 0
N07  C     HEADFK        CHAIN(N)  HEADF                              33
N07  c                   move      TRKNFA        OTIKU1
N07  C     HEKNSF        IFNE      *zeros
N07  C     HEKNSF        andne     TRKNFA
N07  C                   MOVE      HEKNSF        OTIKU2
N07  C                   END
N07  C     HEKNKF        IFNE      *zeros
N07  C     HEKNKF        andne     TRKNFA
N07  C                   MOVE      HEKNKF        OTIKU2
N07  C                   END
N07  C                   CALL      'SYFA64'
N07  C                   PARM                    heavd
N07  C                   PARM                    heopd
N07  C                   PARM                    OTIKU1
N07  C                   PARM                    OTIKU2
N07  C                   END
      * OPT 8 = vis oppdrags notisblokk
     C  N98WSNR          IFEQ      '8'                                           2<-B
     C  N27              Z-ADD     0             XFLAGG
     C   27              Z-ADD     1             XFLAGG
     C                   CALL      'SYFA31CL'
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    XFLAGG            1 0
     C                   exsr      TSTPDA
     C                   UPDATE    SUBFIL
     C                   GOTO      SLTST
     C                   END                                                     2<-E
N09   * OPT I = PRINT INNLANDSOPPDRAG
N09  C  N98WSNR          IFEQ      'I'                                           2<-B
N09  C                   MOVE      HEAVD         AAVD              4
N09  C                   MOVE      HEOPD         AOPD              7
N09  C                   MOVE      *ZEROS        ATUR              8
N09  C                   CALL      'SYFA12CLD'
N09  C                   PARM                    AAVD
N09  C                   PARM                    AOPD
N09  C                   PARM                    ATUR
N09  C                   exsr      TSTPDA
N09  C                   UPDATE    SUBFIL
N09  C                   GOTO      SLTST
N09  C                   END                                                     2<-E
      * OPT L = LOGG ( TRACK & TRACE-LOGG)
     C  N98WSNR          IFEQ      'L'                                           2<-B
     C                   exsr      TSTPDA
     C                   UPDATE    SUBFIL
     C                   Z-ADD     1             TTFLAG            1 0
N05  C                   Z-ADD     0             TTTUR             8 0
     C                   CALL      'SYTT01CL'
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
N05  C                   PARM                    TTTUR
     C                   PARM                    TTFLAG
     C                   GOTO      SLTST
     C                   END                                                     2<-E
N03   * OPT A = Arkiv
N03  C  N98WSNR          IFEQ      'A'                                           2<-B
N03  C                   CALL      'ARC003'
N03  C                   PARM                    HEAVD
N03  C                   PARM                    HEOPD
N03  C                   PARM                    URUNDE           10
N03  C                   exsr      TSTPDA
N03  C                   MOVE      ' '           WSNR
N03  C                   UPDATE    SUBFIL
N03  C                   GOTO      SLTST
N03  C                   END                                                     2<-E
N04   * OPT S = Skilleark
N04  C  N98WSNR          IFEQ      'S'                                           2<-B
N04  C                   MOVE      '  '          SCATYP            2
N04  C                   MOVEL     HEAVD         SCASTR11         11
N04  C                   MOVE      HEOPD         SCASTR11         11
N04  C                   MOVEL     SCASTR11      SCASTR           20
N04  C                   CALL      'ARCA000CL'
N04  C                   PARM                    SCATYP
N04  C                   PARM                    SCASTR
N04  C                   exsr      TSTPDA
N04  C                   MOVE      ' '           WSNR
N04  C                   UPDATE    SUBFIL
N04  C                   GOTO      SLTST
N04  C                   END                                                     2<-E
N06   * OPT Q = Questin pris frakt på epost
N06  C  N98WSNR          IFEQ      'Q'                                           2<-B
N13  C                   move      'E'           UKODE
n06  C                   CALL      'ARC045'
n06  C                   PARM                    HEAVD
n06  C                   PARM                    HEOPD
N13  C                   parm                    kunul             8 0
s13  C*                  PARM                    URUNDE           10
n13  C                   PARM                    UKODE             1
N06  C                   MOVE      ' '           WSNR
N06  C                   UPDATE    SUBFIL
N06  C                   GOTO      SLTST
N06  C                   END                                                     2<-E
      * OPT P = PDA - SE DETALJER OM HVA SOM SIST ER SENDT TIL PDA
     C  N98WSNR          IFEQ      'P'                                           2<-B
     C                   exsr      TSTPDA
     C                   UPDATE    SUBFIL
     C                   CALL      'SYTK01'
     C                   PARM                    UPRO
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   GOTO      SLTST
     C                   END                                                     2<-E
     C*
     C  N98WSNR          IFEQ      'B'                                           2<-B
     C                   CALL      'SYTK02'
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   exsr      TSTPDA
     C                   UPDATE    SUBFIL
     C                   GOTO      SLTST
     C                   END                                                     2<-E
N14   * OPT U = Urgent/merk som prioritert gods
N14  C  N98WSNR          IFEQ      'U'                                           2<-B
N14  C                   CALL      'SYTRURG'
N14  C                   PARM                    HEAVD
N14  C                   PARM                    HEOPD
N14  c                   PARM                    URGIND            1
N14  C                   MOVE      ' '           WSNR
N14  C                   exsr      TSTPDA
N14  C                   UPDATE    SUBFIL
N14  C                   GOTO      SLTST
N14  C                   END                                                     2<-E
N13   * OPT W = se på Web/esped
N13  C  N98WSNR          IFEQ      'W'                                           2<-B
N13  C                   EVAL      Overskr = 'Se SYSPED oppdrag på WEB (eSped) '
N13  C                   EVAL      WebType = 'ESPEO'
N13  C                   move      HEAVD         HEAVDA            4
N13  C                   move      HEOPD         HEOPDA            7
N13  C                   EVAL      WebSok  = '&heavd='+HEAVDA+
N13  C                             '&heopd='+HEOPDA
N13  C                   CALL      'WEBLINKR'
N13  C                   PARM                    Overskr          40
N13  C                   PARM                    WebType           5
N13  C                   PARM                    WebSok           50
N13  C                   MOVE      ' '           WSNR
N14  C                   exsr      TSTPDA
N13  C                   UPDATE    SUBFIL
N13  C                   GOTO      SLTST
N13  C                   END                                                     2<-E
     C*
     C                   END                                                    1<-E
     C*
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C     SLTST         ENDSR
     C*
     C***********************************************
     C     SLETT         BEGSR
     C***********************************************
     C     HEADFK        CHAIN     HEADF                              33
     C     HEPRO         IFEQ      UPRO
     C     HEST          IFEQ      'X'                                           2<-B
     C                   UPDATE    HEADFR
     C                   MOVE      'JOV0428'     MSGNR
     C                   SETON                                        9830
     C                   GOTO      UTSLET
     C                   END                                                     2<-E
     C                   Z-ADD     0             HEPRO
     C                   MOVE      *ZEROS        TRRUND
     C                   MOVE      *BLANKS       HEHAWB
N01  C                   MOVE      *zeros        HEknt
     C                   MOVE      *ZEROS        HETRM
     C                   MOVE      0             HETRC
     C                   MOVE      *BLANKS       HETRCN
     C                   END
     C                   UPDATE    HEADFR
      * KORRIGER SKJERM-VARIABLE - BLAN SUBFILVARIABLE.
     C     HEPRO         IFEQ      0
     C                   SUB       HENT          WSNT
     C                   SUB       HEVKT         WSVKT
     C                   SUB       HEVKT         WSBER1
     C                   SUB       HEM3          WSM3
     C                   SUB       HELM          WSLM
N15  C                   SUB       HELMLA        WSLMLA
N15  C                   SUB       HEPOEN        WSPOEN
     C                   SUB       1             WSNTO
     C                   MOVE      XSLETT        HENAS
     C                   MOVE      *BLANKS       HENAK
     C                   MOVE      *ZEROS        HENT
     C                   MOVE      *BLANKS       HEVS1
     C                   MOVE      *BLANKS       HESDF
     C                   MOVE      *BLANKS       HESDT
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        WSM3SU
     C                   MOVE      *ZEROS        HEM3
     C                   MOVE      *ZEROS        HELM
N15  C                   MOVE      *ZEROS        HELMLA
N15  C                   MOVE      *ZEROS        HEPOEN
     C                   MOVE      *BLANKS       HEOT
     C                   MOVE      *BLANKS       HEFR
     C                   MOVE      *BLANKS       HEST
     C                   MOVE      *BLANKS       IMCO
     C                   MOVE      *BLANKS       ETTE
     C     TURERK        CHAIN     TURER                              33
     C                   Z-ADD     WSNTO         TUAO
     C                   Z-ADD     WSNT          TUTS
     C                   Z-ADD     WSVKT         TUTVKT
     C                   Z-ADD     WSM3          TUTM3
     C                   Z-ADD     WSLM          TUTLM
N15  C                   Z-add     WSLMLA        TUTLM2
N15  C                   Z-add     WSPOEN        TUPOEN
     C                   UPDATE    TURERR
     C     HEADFK        CHAIN     DISP                               33
     C  N33              MOVE      *ZEROS        DITUR
     C  N33              MOVE      *ZEROS        DIRUND
     C  N33              MOVE      *ZEROS        DITUAV
     C                   MOVE      ' '           DISTA1
     C  N33              UPDATE    DISPR
     C                   EXSR      BUTRMV
     C                   MOVE      *ZEROS        TRSDFD
     C                   MOVE      *ZEROS        WRSDFD
     C                   MOVE      *BLANKS       TRSTAF
     C                   MOVE      *ZEROS        TRSDTD
     C                   MOVE      *ZEROS        WRSDTD
     C                   MOVE      *BLANKS       TRSTAE
     C                   END
      * DERSOM DET VAR FELLESKOSTNAD PÅ... GI WARNING
     C                   MOVE      *ZEROS        FELKOS           11 2
     C                   MOVE      *ZEROS        ANDKOS           11 2
     C     HEADFK        SETLL     FAK2
     C     HEADFK        READE     FAK2                                   33
     C     *IN33         DOWEQ     '0'
     C     FAKDA         IFEQ      'K'
     C     WSNØKK        IFEQ      '2'
     C                   ADD       FABELN        FELKOS
     C                   ELSE
     C                   ADD       FABELN        ANDKOS
     C                   END
     C                   END
     C     HEADFK        READE     FAK2                                   33
     C                   END
     C     FELKOS        IFNE      *ZEROS
     C     VISFEL        TAG
     C                   EXFMT     FELWIN
     C     *IN10         IFEQ      *ON
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    DUMFLA
     C                   GOTO      VISFEL
     C                   END
     C                   END
     C*
     C     UTSLET        ENDSR
     C***********************************************
     C     CHANGE        BEGSR
     C***********************************************
     C                   MOVE      '0'           UUFUNK
s11  C*                  MOVE      '2'           UURETU
N11  C                   MOVE      WSNR          UURETU
     C                   MOVE      HEAVD         UUAVD
     C                   MOVE      *ZEROS        UUAVDC
     C                   MOVE      HEOPD         UUOPD
     C* T = KALLER SYTR10 FRA TURSYSTEMET
     C                   MOVE      'T'           UUSPØR
     C                   CALL      'SYTR10'
     C                   PARM                    UUAVD             4 0
     C                   PARM                    UUAVDC            4 0
     C                   PARM                    UUOPD             7 0
     C                   PARM                    UUSPØR            1
     C                   PARM                    UURETU            1
     C                   PARM                    UUFUNK            1
     C*
     C     HEADFK        CHAIN(N)  HEADF                              33
     C     HEADFK        CHAIN     HESTED                             33
     C  N33              DO
     C                   MOVEL     HSFROM        HESDF
     C                   MOVEL     HSTO          HESDT
     C                   END
     C*
     C                   EXSR      OMBER
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     BUTRMV        BEGSR
     C***********************************************
     C     HEOPD         CABEQ     *ZEROS        UTBUTR
     C* GÅR IGJENNOM EVT. REKVISISJONER PÅ DETTE OPPDRAG OG FJERNER
     C* TURNUMMER
     C                   MOVE      ' '           YYST
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      *ZEROS        BUTAVD
     C                   MOVE      *ZEROS        BUTUNR
     C     BUBILN        IFEQ      TUBILN
     C                   MOVE      *BLANKS       BUBILN
     C                   MOVE      *ZEROS        BUFEDT
     C                   MOVE      *BLANKS       BUBILK
     C                   END
N02   * dersom det er hovedtransp's avr-beløp (på oppdragsnivå)(tye 4/5) skal det SLETTES!!
N02  c     BUTX_7        Ifeq      '* OPD *'
N02  C                   delete    KOSBUR
N02  c                   else
N02   * andre åpne forventede kostnader blir liggende
     C                   UPDATE    KOSBUR
N02  c                   endif
     C     KOSB9K        READE     KOSBU9                                 33
     C  N33              END
     C* OPPDATER TUR OGSÅ PÅ EVT. DUMMY/DUPREKV. PÅ OPPDRAGET
     C                   EXSR      BUTRM2
     C*
     C     UTBUTR        ENDSR
     C*
     C***********************************************
     C     BUTRM2        BEGSR
     C***********************************************
     C* FJERN OGSÅ HENVISN. TIL EVT. DUMMYREKVISISJONER
     C                   MOVE      'D'           YYST
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      *ZEROS        BUTAVD
     C                   MOVE      *ZEROS        BUTUNR
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C  N33              END
     C                   ENDSR
     C*
     C***********************************************
     C     PLUKKE        BEGSR
     C***********************************************
     C                   CALL      'SYTR88C'
     C                   PARM                    UFIRM
     C                   PARM                    UAVD
     C                   PARM                    UPRO
     C                   PARM                    URUND
     C                   PARM                    UFLAGG            1 0
     C*
     C                   EXSR      OMBER
     C                   ENDSR
     C*
     C***********************************************
     C     OMBER         BEGSR
     C***********************************************
     C* GIR NY STATUS FOR TUREN
     C*
     C     TURERK        CHAIN(N)  TURER                              33
     C                   Z-ADD     TUAO          WSNTO
     C                   Z-ADD     TUTS          WSNT
     C                   Z-ADD     TUTVKT        WSVKT
     C     TUTVKT        ADD       TUTARA        WSBER1
     C                   Z-ADD     TUTM3         WSM3
     C                   Z-ADD     TUTLM         WSLM
N15  C                   Z-add     TUTLM2        WSLMLA
N15  C                   Z-add     TUPOEN        WSPOEN
     C     UFLAGG        COMP      1                                      01
     C     UFLAGG        COMP      3                                      03
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     OMDISP        BEGSR
     C***********************************************
     C                   CALL      'SYTR88D'
     C                   PARM                    UFIRM
     C                   PARM                    UAVD
     C                   PARM                    UPRO
     C                   PARM                    URUND
     C                   PARM                    UFLAGG            1 0
     C*
     C     TURERK        CHAIN(N)  TURER                              33
     C                   Z-ADD     TUAO          WSNTO
     C                   Z-ADD     TUTS          WSNT
     C                   Z-ADD     TUTVKT        WSVKT
     C     TUTVKT        ADD       TUTARA        WSBER1
     C                   Z-ADD     TUTM3         WSM3
     C                   Z-ADD     TUTLM         WSLM
N15  C                   Z-add     TUTLM2        WSLMLA
N15  C                   Z-add     TUPOEN        WSPOEN
     C     UFLAGG        COMP      1                                      01
     C     UFLAGG        COMP      3                                      03
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     VISOPD        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    UFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
     C                   MOVE      *ZEROS        UFLAGG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TURERK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UPRO
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KOSB9K        KLIST
     C                   KFLD                    YYST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      ' '           YYST              1
     C     TKSGMK        KLIST
     C                   KFLD                    UPRO
     C                   KFLD                    XXBlank           1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
s08  C*    TTCHGKey      KLIST
N08  C     TTKey         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXFBNR            3 0
     C                   KFLD                    XXACTI            3
S08  C*                  MOVE      'CHG'         XXACTI
      * Kode P / 0 i fakt-nr = ulest PDA-melding.
     C     PDFTSKey      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XFTKOD            1
S01  C*                  KFLD                    XFTFAK
N01  C                   KFLD                    XFLEST            1
     c*    *like         Define    FRTFAK        XFTFAK
     C                   MOVE      'P'           XFTKOD
     C*
     C     *ENTRY        PLIST
     C     UFIRM         PARM      UFIRM         UFIRM             2
     C     UAVD          PARM      UAVD          UAVD              4 0
     C     UPRO          PARM      UPRO          UPRO              8 0
     C     URUND         PARM      URUND         URUND             8 0
     C     USPØR         PARM      USPØR         USPØR             1
     C     UFLAGG        PARM      UFLAGG        UFLAGG            1 0
     C                   Z-ADD     0             DATO0             6 0
     C*
     C                   READ      FIRM                                   33
     C     USPØR         COMP      'J'                                    27
     C*
     C                   MOVE      *ZEROS        DATO0             6 0
     C                   Z-ADD     1             YAVD              4 0
S10  C*                  MOVE      *ZEROS        WSBER1            5 0
S10  C*                  MOVE      *ZEROS        TUBER2            5 0
N10  C                   MOVE      *ZEROS        WSBER1
N10  C                   MOVE      *ZEROS        TUBER2
     C                   MOVE      *ZEROS        REFTi6
     C*
     C                   MOVE      'DIV0698'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     30            XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XSLETT           30
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C                   EXSR      CALTOT
     C* Definer hjelpevariable for å bevare subfildata
     C     *LIKE         DEFINE    HEAVD         XXAVD
     C     *LIKE         DEFINE    HEOPD         XXOPD
     C     *LIKE         DEFINE    HENAS         XXNAS
     C     *LIKE         DEFINE    HENAK         XXNAK
     C     *LIKE         DEFINE    HESDF         XXSDF
     C     *LIKE         DEFINE    HESDT         XXSDT
     C     *LIKE         DEFINE    HENT4         XXNT4
     C     *LIKE         DEFINE    HEVS10        XXVS10
     C     *LIKE         DEFINE    HEVKTW        XXVKTW
     C     *LIKE         DEFINE    HELM          XXLM
N15  C     *LIKE         DEFINE    HELMLA        XXLMLA
N15  C     *LIKE         DEFINE    HEPOEN        XXPOEN
     C     *LIKE         DEFINE    HEOT          XXOT
     C     *LIKE         DEFINE    HEFR          XXFR
     C     *LIKE         DEFINE    HEST          XXST
     C     *LIKE         DEFINE    TROPD0        XXOPD0
     C     *LIKE         DEFINE    TRSDFD        XXSDFD
     C     *LIKE         DEFINE    TRSDTD        XXSDTD
     C     *LIKE         DEFINE    TRSTAF        XXSTAF
     C     *LIKE         DEFINE    TRSTAE        XXSTAE
     C     *LIKE         DEFINE    WSM3SU        XXM3SU
     C     *LIKE         DEFINE    IMCO          XXIMCO
     C     *LIKE         DEFINE    ETTE          XXETTE
     C*
     C                   ENDSR
