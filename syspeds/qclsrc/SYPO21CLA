             PGM        PARM(&UTSKRIFTNR)
             DCL        VAR(&UTSKRIFTNR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BRUKER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SVAR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DOKNAVN) TYPE(*CHAR) LEN(8)
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10)
             CHKDLO     DLO(DOCPRT) FLR(QPGMR) OBJTYPE(*DOC) +
                          USRID(*CURRENT)
             MONMSG     MSGID(CPF8A82 CPF8A77) EXEC(GOTO CMDLBL(SLUTT))
             RTVUSRPRF  RTNUSRPRF(&BRUKER)
             CALL       PGM(SYYA02A) PARM(&BRUKER &DOKNAVN &SVAR)
             IF         COND(&SVAR = 'N') THEN(GOTO CMDLBL(SLUTT))
             RTVJOBA    JOB(&WS)
             CLRPFM     FILE(PRTDOCA) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(PRTDOCA) +
                          MBR(&WS))
             OVRDBF     FILE(PRTDOCA) TOFILE(PRTDOCA) MBR(&WS)
             CLRPFM     FILE(PRTDOCB) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(PRTDOCB) +
                          MBR(&WS))
             OVRDBF     FILE(PRTDOCB) TOFILE(PRTDOCB) MBR(&WS)
             OVRPRTF    FILE(&UTSKRIFTNR) HOLD(*YES)
             CPYSPLF    FILE(&UTSKRIFTNR) TOFILE(PRTDOCA) +
                          SPLNBR(*LAST) TOMBR(&WS) CTLCHAR(*PRTCTL)
             MONMSG     MSGID(CPF3309) EXEC(GOTO CMDLBL(INTET))
             DLTSPLF    FILE(&UTSKRIFTNR) SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SYYA02B)
             MRGDOC     FROMDOC(DOCPRT) FROMFLR(QPGMR) +
                          TODOC(&DOKNAVN) TOFLR(&BRUKER) +
                          REPLACE(*YES) MRGTYPE(*FILE) +
                          DTAFILE(*LIBL/PRTDOCB) DTAMBR(&WS) +
                          JOBQ(*NO) ADJUST(*PAGES) MLTLINRPT(*NO) +
                          DSPOPT(*EDIT)
             CLRPFM     FILE(PRTDOCA)
             MONMSG     MSGID(CPF0000)
             CLRPFM     FILE(PRTDOCB)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(SLUTT)
 INTET:      SNDPGMMSG  MSG('Ingen utskrift.')
 SLUTT:      ENDPGM
