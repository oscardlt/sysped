             PGM        PARM(&STMF &FIL &LIB &RCDLEN &ERS &CRLF)
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FIL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RCDLEN) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&ERS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&CRLF) TYPE(*CHAR) LEN(10)

             IF         COND(&RCDLEN = 0) THEN(+
             CHGVAR     VAR(&RCDLEN) VALUE(256))

             DLTF  QTEMP/FRPCF
             MONMSG CPF0000
             CRTPF      FILE(QTEMP/FRPCF) RCDLEN(&RCDLEN) +
                          SIZE(1000000 10000)
             CPYFRMSTMF FROMSTMF(&STMF) +
                          TOMBR('/QSYS.LIB/QTEMP.LIB/FRPCF.FILE/FRPCF+
                          .MBR') MBROPT(*REPLACE) CVTDTA(*AUTO) +
                          ENDLINFMT(&CRLF) STMFCODPAG(*PCASCII)
/* FILEN FINNES IKKE - HOPP UT  */
             MONMSG     MSGID(CPFA095) EXEC(DO)
             SNDPGMMSG  MSG('FEIL OPPSTOD! Sjekk jobblog. FIL FINNES +
                          IKKE I IFS?? Innhenting/konvertering IKKE +
                          kjørt!!!') MSGTYPE(*INFO)
             GOTO       CMDLBL(END)
             ENDDO
 /* HVIS FILNAV OPPGITT - KOPIER*/
             IF         COND(&FIL *NE ' ') THEN(DO)

             IF         COND(&LIB = ' ') THEN(+
             CHGVAR     VAR(&LIB) VALUE('QTEMP'))

             CPYF       FROMFILE(QTEMP/FRPCF) TOFILE(&LIB/&FIL) +
                          CRTFILE(*YES)
/*HVIS FEIL SKYLDES FINNES FRA FØR OG OVERSKRIVING TILLATT...*/
             MONMSG     MSGID(CPF2817) EXEC(DO)
             IF         COND(&ERS *NE 'J') THEN(DO)
             SNDPGMMSG  MSG('FEIL VED KOPIERING TIL TILFIL. Finnes +
                          fra før??   Sjekk jobblog!!  ') +
                          MSGTYPE(*INFO)
             GOTO       CMDLBL(END)
             ENDDO
             CPYF       FROMFILE(QTEMP/FRPCF) TOFILE(&LIB/&FIL) +
                        MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817) EXEC(DO)
             SNDPGMMSG  MSG('FEIL VED KOPIERING TIL EKSISTERENDE +
                          FIL. Feil recordformat?? Sjekk jobblog!!  +
                          ') MSGTYPE(*INFO)
             GOTO       CMDLBL(END)
             ENDDO
             ENDDO
             ENDDO


 END:        ENDPGM
                                                                  /**/
