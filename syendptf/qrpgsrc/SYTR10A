      *****************************************************************
      * PROGRAM FOR Å Synkroniser ant/vekt/vol til dupp
      ***
      *  Bør fikses senere
      *     - dersom det er spredning (dup via flere fraktbrev skipp)
      *     - Hvis IKKE (ett oppdrag/ett fraktbrev= nr 001) oppdater også FRbrev
      *     - dersom oppdraget er på tur akkumuler turtotaler
      *****************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Kolli-IDs (linje 0) skal IKKE spres på alle
      * 02 JOV PTF12 Når en kommer inn (med HO) som har både FF og VF så mister en VF-key ved FF-beh
********************************************************************
      *
     FDOKUF     UF   E           K DISK
     FDOKUFM    UF A E           K DISK
     FDOKUFVF   UF A E           K DISK
     FDOKUFV    UF A E           K DISK
     FHEADF     UF   E           K DISK
      *
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *
     C                   EXSR      INIT
     c                   exsr      hoved
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *******************************************************
     C     INIT          BEGSR
      *******************************************************
      * kommer inn med hovedoppdrag
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
      *
      *
     C     HeaKey        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     HEAKEF        KLIST
     C                   KFLD                    TRAVD1H
     C                   KFLD                    TROPD1H
     C     HEAKEV        KLIST
     C                   KFLD                    TRAVD2H
     C                   KFLD                    TROPD2H
      *
     C     HeaKey        Chain(N)  Headf                              33
      *
     C                   move      travd1        TRAVD1H           4 0
     C                   move      tropd1        TROPD1H           7 0
     C                   move      travd2        TRAVD2H           4 0
     C                   move      tropd2        TROPD2H           7 0
     C     *LIKE         DEFINE    HENT          NYNT
     C     *LIKE         DEFINE    HEVS1         NYVS1
     C     *LIKE         DEFINE    HEVS2         NYVS2
     C     *LIKE         DEFINE    HEGM1         NYGM1
     C     *LIKE         DEFINE    HEGM2         NYGM2
     C     *LIKE         DEFINE    HEVKT         NYVKT
     C     *LIKE         DEFINE    HELM          NYLM
     C     *LIKE         DEFINE    HEM3          NYM3
     C     *LIKE         DEFINE    HEFBV         NYFBV
     C     *LIKE         DEFINE    HEPOEN        NYPOEN
      * TA VARE PÅ VERDIER FRA OPPDRAGET SOM ER ENDRET - DISSE KAL INN PÅ ALLE ANDRE
     C                   MOVE      HENT          NYNT
     C                   MOVE      HEVS1         NYVS1
     C                   MOVE      HEVS2         NYVS2
     C                   MOVE      HEGM1         NYGM1
     C                   MOVE      HEGM2         NYGM2
     C                   MOVE      HEVKT         NYVKT
     C                   MOVE      HELM          NYLM
     C                   MOVE      HEM3          NYM3
     C                   MOVE      HEFBV         NYFBV
     C                   MOVE      HEPOEN        NYPOEN

     C                   ENDSR

      ***********************************************
     C     HOVED         BEGSR
      ***********************************************
      * oppdaterer forfraktoppdrag  ikke hvis protect
      *
     C     travd1        ifne      0
     C     tropd1        andne     0
     C     HEAKEF        CHAIN(N)  HEADF                              33
     c     *in33         ifeq      '0'
     c     HESTL4        andne     'P'
     C     HEAKEF        CHAIN     HEADF                              33
     c                   exsr      moveh
     c                   update    headfr
      *
     c                   move      heavd         tilavd            4 0
     c                   move      heopd         tilopd            7 0
     c                   exsr      slettdup
     c                   exsr      flyttdup
     c                   exsr      dokufsr
     C                   END
     C                   END
      * oppdaterer viderefraktoppdrag
      *
S02  C*    travd2        ifne      0
S02  C*    tropd2        andne     0
N02  C     TRAVD2H       ifne      0
N02  C     TROPD2H       andne     0
     C     HEAKEV        CHAIN(N)  HEADF                              33
     c     *in33         ifeq      '0'
     c     HESTL4        andne     'P'
     C     HEAKEV        CHAIN     HEADF                              33
     c                   exsr      moveh
     c                   update    headfr
      *
     c                   move      heavd         tilavd
     c                   move      heopd         tilopd
     c                   exsr      slettdup
     c                   exsr      flyttdup
     c                   exsr      dokufsr
     C                   END
     C                   END
     C                   ENDSR
      **********************************************************************
     C     SLETTDUP      BEGSR
      **********************************************************************
     C     TILKEY        KLIST
     C                   KFLD                    KODE              1
     C                   KFLD                    TILAVD
     C                   KFLD                    TILOPD
     C                   kfld                    fbnr              3 0
     c                   move      'F'           kode
     c                   z-add     1             fbnr
     C     TILKEYF       KLIST
     C                   KFLD                    TILAVD
     C                   KFLD                    TILOPD
     C                   kfld                    fbnr              3 0
      *
      * DOKUFV
      *
     c                   setoff                                       33
     c     TILKEY        setll     dokufv
     c     *in33         doweq     '0'
     c     TILKEY        reade     dokufv                                 33
     c     *IN33         ifeq      '0'
     C                   delete    dokufvr
     c                   endif
     c                   enddo
      *
     c                   setoff                                       33
      *
      * DOKUFM
      *  OBSS - linje 0 = kolli-ID. De skal IKKE spres / linje ulik 0 = godsmerke = spres
     c                   setoff                                       33
     c     TILKEY        setll     dokufm
     c     *in33         doweq     '0'
     c     TILKEY        reade     dokufm                                 33
     c     *IN33         ifeq      '0'
N01  c                   if        FMLINR=*zeros
N01  C                   update    dokufmr
N01  c                   else
     C                   delete    dokufmr
N01  c                   endif
     c                   endif
     c                   enddo
      *
      * DOKUFVF
      *
     c                   setoff                                       33
     c     TILKEYF       setll     dokufvf
     c     *in33         doweq     '0'
     c     TILKEYF       reade     dokufvf                                33
     c     *IN33         ifeq      '0'
     C                   delete    dokufvfr
     c                   endif
     c                   enddo
     c                   endsr
      *
      **********************************************************************
     C     flyttdup      BEGSR
      **********************************************************************
      *
      * DOKUFV
      *
     C     FRAKEY        KLIST
     C                   KFLD                    KODE              1
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   kfld                    fbnr              3 0
     c                   move      'F'           kode
     c                   z-add     1             fbnr
      *
     C     FRAKEYF       KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   kfld                    fbnr              3 0
      *
      * DOKUFV
      *
     c                   setoff                                       33
     c     FRAKEY        setll     dokufv
     c     *in33         doweq     '0'
     c     FRAKEY        reade(n)  dokufv                                 33
     c     *IN33         ifeq      '0'
     c                   move      tilavd        FVAVD
     c                   move      tilopd        FVOPD
     c                   write     dokufvr
     c                   endif
     c                   enddo
      *
      * DOKUFM
      *   (kun varelinjers godsmerking - ikke varelinjer (=linjenr 0)
     c                   setoff                                       33
     c     FRAKEY        setll     dokufm
     c     *in33         doweq     '0'
     c     FRAKEY        reade(n)  dokufm                                 33
     c     *IN33         ifeq      '0'
N01  c                   if        FMLINR <>*zeros
     c                   move      tilavd        FMAVD
     c                   move      tilopd        FMOPD
     c                   write     dokufmr
N01  c                   endif
     c                   endif
     c                   enddo
      *
      * DOKUFVF
      *
     c                   setoff                                       33
     c     FRAKEYF       setll     dokufvf
     c     *in33         doweq     '0'
     c     FRAKEYF       reade(n)  dokufvf                                33
     c     *IN33         ifeq      '0'
     c                   move      tilavd        FFAVD
     c                   move      tilopd        FFOPD
     c                   write     dokufvfr
     c                   endif
     c                   enddo
     c                   endsr
      *
      *
      **********************************************************************
     C     moveh         BEGSR
      **********************************************************************
     C                   MOVE      NYVS1         HEVS1
     C                   MOVE      NYVS2         HEVS2
     C                   MOVE      NYGM1         HEGM1
     C                   MOVE      NYGM2         HEGM2
     C                   MOVE      NYNT          HENT
     C                   MOVE      NYVKT         HEVKT
     C                   MOVE      NYLM          HELM
     C                   MOVE      NYM3          HEM3
     C                   MOVE      NYFBV         HEFBV
     C                   MOVE      NYPOEN        HEPOEN
     c                   endsr
      **********************************************************************
     C     DOKUFSR       BEGSR
      **********************************************************************
     c     TILKEY        CHAIN     DOKUF                              33
     c     *in33         ifeq      '0'
N14  C                   Z-ADD     NYNT          DFNT
N14  C                   Z-ADD     NYVKT         DFVKT
N14  C                   Z-ADD     NYLM          DFLM
N14  C                   Z-ADD     NYM3          DFM3
N14  C                   Z-ADD     NYFBV         DFVKTF
     c                   update    dokufr
     C                   end
     C                   endsr
