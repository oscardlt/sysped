     A*%%TS  SD  20161129  114150  JOVO        REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261A* 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A*
     A          R BILDE01
     A*%%TS  SD  20161129  114150  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Avslutt program.')
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/FIXTUR'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(030 JOV0255 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8 23MSGCON(012 DIV1338 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  8 40DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 23MSGCON(012 DIV1339 *LIBL/SYMSGF)
     A            WSSG           3A  B 10 40TEXT('Signatur')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 23MSGCON(012 DIV1544 *LIBL/SYMSGF)
     A            WSTUR          8Y 0B 12 40TEXT('TURNUMMER')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 15 23MSGCON(012 DIV1341 *LIBL/SYMSGF)
     A            WSST           1A  B 15 40TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 17 23MSGCON(012 JOV1384 *LIBL/SYMSGF)
     A            WSPDST         1A  B 17 40TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 17 44MSGCON(030 JOV1385 *LIBL/SYMSGF)
     A                                 23 23MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 18 44'(2=Ferdig PDA, 3=Ferdig-IKKE PDA!)'
     A*
     A          R BILDE
     A*%%TS  SD  20161129  113848  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF05(05 'PLANLEGG TUR')
     A                                      CF12(12 'Forrige')
     A            TUDT           8Y 0H
     A            TUDTT          8Y 0H
     A                                  1  3'SYSPED/FIXTUR'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(030 JOV0255 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  8MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            TUAVD          4Y 0O  2 18EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 24MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            TUPRO          8Y 0O  2 35EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(010 DIV3772 *LIBL/SYMSGF)
     A            TURUND         8Y 0O  2 55EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(015 DIV3769 *LIBL/SYMSGF)
     A            TURMND         2Y 0O  3 18COLOR(WHT)
     A                                      EDTCDE(4)
     A            TURAAR         2Y 0O  3 21COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 24MSGCON(015 DIV3775 *LIBL/SYMSGF)
     A            TUBILK         3A  O  3 40COLOR(WHT)
     A                                  3 44MSGCON(015 DIV3770 *LIBL/SYMSGF)
     A            TUOPDT         2A  O  3 60DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 63MSGCON(015 DIV3771 *LIBL/SYMSGF)
     A            TUTRMA         2Y 0O  3 79COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(015 DIV0670 *LIBL/SYMSGF)
     A            TUBILN        10A  O  4 18DSPATR(UL)
     A                                      COLOR(WHT)
     A            TUHENG        10A  O  4 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(015 DIV3790 *LIBL/SYMSGF)
     A            TUCON1        17A  O  5 18COLOR(WHT)
     A            TULKC1         2A  O  5 36DSPATR(UL)
     A                                      COLOR(WHT)
     A            TUCON2        17A  O  5 40COLOR(WHT)
     A            TULKC2         2A  O  5 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(015 DIV0665 *LIBL/SYMSGF)
     A            TUKNT2         8Y 0O  6 18DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TUNAT         30A  O  6 27DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(015 DIV3776 *LIBL/SYMSGF)
     A            TUSJA1         8Y 0O  7 18COLOR(WHT)
     A                                      EDTCDE(4)
     A            TUSJN1        30A  O  7 27COLOR(WHT)
     A                                  8  2MSGCON(015 DIV3777 *LIBL/SYMSGF)
     A            TUSJA2         8Y 0O  8 18COLOR(WHT)
     A                                      EDTCDE(4)
     A            TUSJN2        30A  O  8 27COLOR(WHT)
     A                                 10  2MSGCON(015 DIV0662 *LIBL/SYMSGF)
     A            WSDT           6Y 0O 10 18DSPATR(UL)
     A                                      COLOR(WHT)
     A            TUTM           4Y 0O 10 25COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10 30MSGCON(015 DIV0673 *LIBL/SYMSGF)
     A            TUSTEF         5A  O 10 46COLOR(WHT)
     A            TUSDF         20A  O 10 55DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(015 DIV3766 *LIBL/SYMSGF)
     A            WSDTT          6Y 0O 11 18DSPATR(UL)
     A                                      COLOR(WHT)
     A            TUTMT          4Y 0O 11 25COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11 30MSGCON(015 DIV0674 *LIBL/SYMSGF)
     A            TUSTET         5A  O 11 46COLOR(WHT)
     A            TUSDT         20A  O 11 55DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2MSGCON(015 DIV3778 *LIBL/SYMSGF)
     A            TUTO1A         8A  O 12 18COLOR(WHT)
     A            TUTO1B         2A  O 12 27COLOR(WHT)
     A                                 13  2MSGCON(015 DIV3779 *LIBL/SYMSGF)
     A            TUTVAL         3A  O 13 18COLOR(WHT)
     A            TUTAKO         1A  O 13 22COLOR(WHT)
     A            TUTBEL        11Y 2O 13 24COLOR(WHT)
     A                                      EDTCDE(J)
     A                                 14  2MSGCON(015 DIV2273 *LIBL/SYMSGF)
     A            TUHØYB         4Y 2O 14 18COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(015 DIV2274 *LIBL/SYMSGF)
     A            TUHØYH         4Y 2B 15 18COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16  2MSGCON(015 DIV3773 *LIBL/SYMSGF)
S01  A*           TUTVKT         5Y 0O 16 26DSPATR(UL)
     A            TUTVKT         9Y 0O 16 26DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 16 59MSGCON(012 DIV0675 *LIBL/SYMSGF)
     A            TUAO           5Y 0O 16 72DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 19  2MSGCON(015 DIV3781 *LIBL/SYMSGF)
S01  A*           TUTM3          5Y 2O 19 26DSPATR(UL)
     A            TUTM3          7Y 3O 19 26DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 20  2MSGCON(015 DIV3782 *LIBL/SYMSGF)
     A            TUTLM          4Y 2O 20 26DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 20 59MSGCON(012 DIV0707 *LIBL/SYMSGF)
S01  A*           TUTS           5Y 0O 20 72DSPATR(UL)
     A            TUTS           7Y 0O 20 72DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 21  2MSGCON(012 DIV1343 *LIBL/SYMSGF)
     A            TUST           1A  O 21 15TEXT('Status GML')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 21 22MSGCON(012 DIV1341 *LIBL/SYMSGF)
     A            WSST           1A  O 21 35TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(RED)
     A                                 22  2MSGCON(012 JOV1386 *LIBL/SYMSGF)
     A            TUTST4         1A  O 22 15TEXT('Status GML')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 22 22MSGCON(012 JOV1384 *LIBL/SYMSGF)
     A            WSPDST         1A  O 22 35TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(040 DIV1342 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 37'blank=Tildelt,1=På PDA, 2=Ferdig P-
     A                                      DA,'
     A                                      COLOR(WHT)
     A                                 23 51'3=Ferdig-IKKE PDA!'
     A                                      COLOR(WHT)
