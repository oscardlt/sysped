      *
      *************************************************************
      ***       VEDLIKEHOLD AV KODEREGISTER FOR                 ***
      ***             båtgrupper                                ***
      ***                                                       ***
      *** PROGRAMMERT AV svein huse  , SYSTEMA AS, MAI 2008     ***
      *************************************************************
      *
     FBATGR     UF A E           K DISK
     FCDBOAT    IF   E           K DISK
     FSYFA73FM  CF   E             WORKSTN
     F                                     SFILE(H50:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C***************
     C* TØM SUBFILE
     C***************
     C     START         TAG
     C                   CLEAR                   SFGRUP
     C                   CLEAR                   SFNAVN
     C                   CLEAR                   SFkunr
     C                   CLEAR                   BESKR
     C     BAGRUP        SETLL     BATGRR
     C                   MOVE      '1'           *IN93
     C                   WRITE     K60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C*************************
     C* Fyll subfile
     C*************************
     C*
     C     START1        TAG
     C                   DO        13
     C                   READ      BATGRR                                 94
     C     *IN94         IFEQ      '0'
     C                   MOVEL     BAGRUP        SFGRUP
     C                   MOVE      BANAVN        SFNAVN
     C                   MOVE      BAKUNR        SFKUNR
     C                   ADD       1             ZRECNO
     C     BANAVN        CHAIN     CDBOAT                             90
     C     *IN90         IFEQ      '0'
     C                   MOVE      *BLANKS       BESKR
     C                   MOVEL     CBNAB         BESKR
     C                   ELSE
     C                   MOVE      *BLANKS       BESKR
     C                   END
     C                   WRITE     H50
     C                   END                                                    <*IN94
     C  N94              ENDDO
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
      ****************************
      * Vis subfile
      ****************************
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   MOVE      '1'           *IN31
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   WRITE     B20
     C                   EXFMT     B40
     C                   READ      B20                                    66
     C                   ELSE
     C                   MOVE      '0'           *IN31
     C                   WRITE     K60
     C                   EXFMT     B40
     C                   READ      K60                                    66
     C                   END
     C*
     C     FKEYS         TAG
     C*
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C*
     C                   SETOFF                                       999830
     C*** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
     C*** F5=Forny
     C     *IN05         IFEQ      '1'
     C                   CLEAR                   BAGRUP
     C                   CLEAR                   BANAVN
     C                   GOTO      START
     C                   END                                                    <*IN05
     C*** F6=Ny linje
     C     *IN06         IFEQ      '1'
     C                   Z-ADD     LINNBR        LINNBX
     C                   Z-ADD     POSNBR        POSNBX
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVEL     'TBR0106'     WMODNR
     C                   EXSR      CLRSCR
     C                   MOVE      '0'           *IN40
     C                   GOTO      START5
     C                   END                                                    <*IN06
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C                   CLEAR                   SFGRUP
     C                   CLEAR                   SFNAVN
     C                   CLEAR                   SFKUNR
     C                   CLEAR                   BESKR
     C                   GOTO      START
     C                   END                                                    <*IN12
     C*** Roll
     C     *IN25         IFEQ      '1'
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C     START4        TAG
     C     *IN94         IFEQ      '0'
     C                   GOTO      START1
     C                   END                                                    <*IN94
     C                   END                                                    <*IN25
     C*** *IN08=Søk på artikkelnr.
     C     FVKODE        IFNE      *BLANKS
     C                   MOVE      '1'           *IN08
     C                   GOTO      START6
     C                   ELSE
     C                   MOVE      '0'           *IN08
     C                   END                                                    <FAKUND
     C*** Hent valg fra skjerm
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C*** Ingen valg foretatt
     C     *IN97         IFEQ      '1'
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   EXFMT     B40
     C                   READ      K60                                    66
     C                   MOVE      '0'           *IN97
     C                   GOTO      FKEYS
     C                   END                                                    <*IN12
     C***                  MOVE '0'       *IN97
     C*** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
     C                   END                                                    <ZRECNO
     C     *IN98         IFEQ      '1'
     C     XRECNO        ANDNE     0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END                                                    <*IN98
     C                   GOTO      START3
     C*** Ny post
     C     START5        TAG
     C                   EXFMT     B10
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   MOVE      '0'           *IN30
     C*** F3=Exit
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   EXSR      CLRSCR
     C                   Z-ADD     LINNBX        LINNBR
     C                   Z-ADD     POSNBX        POSNBR
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C                   ELSE
     C     WSGRUP        IFEQ      *BLANKS
     C     WSNAVN        OREQ      *BLANKS
     C                   MOVE      'TBR0248'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN30
     C                   END                                                    <NAKUND
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      START5
     C                   END                                                    <*IN30
     C                   EXSR      POSLIN
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   END                                                    <*IN12
     C                   GOTO      START3
     C*** Søk på artikkelnr.
     C     START6        TAG
     C                   MOVEL     W2GRUP        BAGRUP
     C*** Finn søkekriterier
     C     W2GRUP        IFEQ      *BLANKS
     C                   MOVEA     '10'          *IN(50)
     C                   ELSE
     C                   MOVEA     '01'          *IN(50)
     C                   END                                                    <*SENAM
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START
     C*
     C     ENDPGM        TAG
     c                   exsr      kunde
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C*
     C                   EXSR      TSTSCR
     C     *IN30         IFEQ      '1'
     C                   GOTO      ENDTST
     C                   END                                                    <*IN30
     C     WSNAVN        CHAIN     CDBOAT                             90
     C     *IN90         IFEQ      '0'
     C                   MOVE      *BLANKS       BESKR
     C                   MOVEL     CBNAB         BESKR
     C                   ELSE
     C                   MOVE      'SHU1147'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN30
     C                   MOVE      *BLANKS       BESKR
     C                   GOTO      ENDTST
     C                   END
     C     WSGRUP        IFEQ      *BLANKS
     C                   MOVE      'SHU1148'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN30
     C                   MOVE      *BLANKS       BESKR
     C                   GOTO      ENDTST
     C                   END
     C                   MOVE      WSGRUP        BAGRUP
     C                   MOVE      WSNAVN        BANAVN
     C                   WRITE     BATGRR
     C     *IN40         IFEQ      '1'
     C                   UPDATE    H50
     C                   ELSE
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      WSGRUP        SFGRUP
     C                   MOVE      WSNAVN        SFNAVN
     C                   WRITE     H50
     C                   END                                                    <*IN40
     C                   EXSR      CLRSCR
     C*
     C     ENDTST        ENDSR
     C*
     C***********************************************************
     C     TSTSCR        BEGSR
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C*
     C     AKEY          CHAIN     BATGRR                             91
     C     *IN91         IFEQ      '0'
     C                   MOVE      'TBR0247'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVE      '1'           *IN61
     C                   MOVE      '1'           *IN30
     C                   END                                                    <NAKUND
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     GETOPT        BEGSR
     C                   Z-ADD     0             LINNBR
     C                   Z-ADD     0             OPTNBR            2 0
     C                   SETOFF                                       919299
     C                   MOVE      '1'           *IN97
     C     *IN98         DOWEQ     '0'
     C                   READC     H50                                    98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   END                                                    <*IN99
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
     C                   MOVE      '0'           *IN97
     C*** 2=Endre
     C     WSNR          IFEQ      '2 '
     C     WSNR          OREQ      ' 2'
     C     WSNR          OREQ      '02'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'TBR0107'     WMODNR
     C                   MOVE      '1'           *IN40
     C                   EXSR      CHGST
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   MOVE      '0'           *IN40
     C                   END                                                    <WSNR=2
     C*** 4=Slette
     C     WSNR          IFEQ      '4 '
     C     WSNR          OREQ      ' 4'
     C     WSNR          OREQ      '04'
     C                   ADD       1             OPTNBR
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'TBR0108'     WMODNR
     C                   EXSR      DLTST
     C*** F12=Avbryt
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN12
     C                   END                                                    <WSNR=4
     C                   ENDDO
     C*
     C     ENDGET        TAG
     C                   EXSR      POSLIN
     C     OPTNBR        IFGT      *ZEROS
     C                   ADD       8             LINNBR
     C                   ELSE
     C                   ADD       9             LINNBR
     C                   END                                                    <OPTNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGST         BEGSR
     C*
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C*
     C     SKEY          CHAIN     BATGRR                             77
     C                   MOVEL     BAGRUP        WSGRUP
     C                   MOVE      BANAVN        WSNAVN
     C*
     C     STCHG         TAG
     C                   EXFMT     B30
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(61)
     C                   MOVE      '0'           *IN(67)
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   MOVE      *BLANKS       SFGRUP
     C                   UPDATE    H50
     C                   GOTO      CHGEND
     C                   END                                                    <*IN12
     C                   MOVE      WSNAVN        BANAVN
     C                   UPDATE    BATGRR
     C                   MOVE      *BLANKS       BAGRUP
     C                   MOVEL     BAGRUP        SFGRUP
     C                   MOVE      BANAVN        SFNAVN
     C     AVDKY1        CHAIN     CDBOAT                             90
     C     *IN90         IFEQ      '0'
     C                   MOVE      *BLANKS       BESKR
     C                   MOVEL     CBNAB         BESKR
     C                   ELSE
     C                   MOVE      *BLANKS       BESKR
     C                   END
     C                   UPDATE    H50
     C     *IN30         IFEQ      '1'
     C                   GOTO      STCHG
     C                   END                                                    <*IN30
     C*
     C     CHGEND        TAG
     C                   ENDSR
     C*
     C***********************************************
     C     DLTST         BEGSR
     C*
     C                   MOVE      '1'           *IN99
     C                   MOVEA     '11'          *IN(88)
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     27            POSNBR
     C     SKEY          CHAIN     BATGRR                             77
     C                   SETON                                        80
     C                   EXFMT     B30
     C                   SETOFF                                       80
     C*
     C                   MOVEA     '00'          *IN(88)
     C     *IN12         IFEQ      '1'
     C     *IN03         OREQ      '1'
     C                   UPDATE    H50
     C                   GOTO      DLTEND
     C                   END                                                    <*IN77
     C*
     C     *IN77         IFEQ      '0'
     C                   DELETE    BATGRR
     C                   MOVE      '  '          WSNR
     C                   MOVE      *BLANKS       BESKR
     C                   MOVEL     '*DELETE*'    BESKR
     C                   UPDATE    H50
     C                   END                                                    <*IN77
     C*
     C     DLTEND        ENDSR
     C*
     C***********************************************
     C     CLRSCR        BEGSR
     C*
     C                   MOVE      '0'           *IN40
     C                   CLEAR                   WSGRUP
     C                   CLEAR                   WSNAVN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     POSLIN        BEGSR
     C*
     C     OPTNBR        IFGT      *ZERO
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C                   END                                                    <OPTNBR
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *LIKE         DEFINE    LINNBR        LINNBX
     C     *LIKE         DEFINE    POSNBR        POSNBX
     C*
     C     AVDKY1        KLIST
     C                   KFLD                    SFNAVN
     C*
     C*
     C     SKEY          KLIST
     C                   KFLD                    SFGRUP
     C                   KFLD                    SFNAVN
     C*
     C     AKEY          KLIST
     C                   KFLD                    WSGRUP
     C                   KFLD                    WSNAVN
     C*
     C                   MOVE      '1'           *IN50
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     kunde         BEGSR
     C***********************************************
     C*
     C                   setoff                                       33
     C     *loval        setll     BATGR
     C     *in(33)       doweq     '0'
     C                   read      BATGR                                  33
     C     *in(33)       ifeq      '0'
     c     banavn        chain     cdboat                             34
     C  N34              move      CBKN          BAKUNR
     c  N34              update    batgrr
     C                   end
     C                   end
     C*
     C                   ENDSR
