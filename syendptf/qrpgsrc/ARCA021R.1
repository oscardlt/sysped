      *********************************************************************
      * Agent som kobler alle dok. i gitt mappe til arkiv                 *
      *       Innparameter: Mappenavn (full path)                         *
      *                     Programnavn (som skal viderebehandle poster)  *
      *********************************************************************
     H DFTACTGRP(*NO)
     FARCA021F  UF A E           K DISK
     D PGM             S             10
     D PGM2            S             10
      *
      *_____________________________
      * Prototype for API prosedyrer
      *"""""""""""""""""""""""""""""
     Dlstat            PR            10I 0 EXTPROC('lstat')
     D                                 *   VALUE
     D                                 *   VALUE

     Dopendir          PR              *   EXTPROC('opendir')
     D                                 *   VALUE

     Dreaddir          PR              *   EXTPROC('readdir')
     D                                 *   VALUE

     Dclosedir         PR            10I 0 EXTPROC('closedir')
     D                                 *   VALUE
     D SndPgmMsg       PR              N
     D Qmsgid                         7    CONST
     D Qmsgf                         20    CONST
     D Qmsg                         128    CONST
     D Qmsgtp                        10    CONST OPTIONS(*NOPASS)

      *   stat data structure fra prosedyre: lstat()
     D StatDS          DS           128
     D  st_mode                      10U 0
     D  st_ino                       10U 0
     D  st_nlink                      5U 0
     D  reserved1                     2A
     D  st_uid                       10U 0
     D  st_gid                       10U 0
     D  st_size                      10U 0
     D  st_atime                     10U 0
     D  st_mtime                     10U 0
     D  st_ctime                     10U 0
     D  st_dev                       10U 0
     D  st_blksize                   10I 0
     D  st_allocsize                 10I 0
     D  st_objtype                   10A
     D  reserved2                     2A
     D  st_codepage                   5U 0
     D  st_reserved1                 62A
     D  st_ino_gen_id                10U 0

      *   direntry data structure fra prosedyre: readdir()
     D DirEntry        DS
     D d_reserved1                   16A
     D d_fileno_genid                10U 0
     D d_fileno                      10U 0
     D d_reclen                      10U 0
     D d_reserved3                   10I 0
     D d_reservedØ4                   6A
     D d_reserved5                    2A
     D d_ccsid                       10I 0
     D d_country_id                   2A
     D d_language_id                  3A
     D d_nls_reserved                 3A
     D d_namelen                     10U 0
     D d_name                       640A

     D Null            S              1A   Inz(X'00')
     D RETURNInt       S             10I 0
     D RETURNDir       S               *
     D PtrToEntry      S               *
     D RtnEntry        S                   BASED(PtrToEntry) Like(DirEntry)
     D EntryName       S            512A
     D EntryLen        S             10I 0
     D NewName         S            120A
     D EntryPath       S            256A
     D CmdLine         S            512
     D CmdLen          S             15  5
     D HHMMSS          S              6  0
     D DirError        C                   'Feil ved åpning av katalog'

      *   Input Parametere
     D DirName         S            100A
     D DirName2        S            100A
     D FullName        S            256A

      *
     D ObjVar          S             90
     D ObjVarLen       S             10I 0 Inz(%size(ObjVar))
     D ObjVarFmt       S              8
     D ObjTyp          S             10

     D APIERR          DS
     D  ERRSIZ                 1      4B 0 INZ(256)
     D  ERRLEN                 5      8B 0 INZ(0)
     D  ERRMIC                 9     15
     D  ERRNBR                16     16
     D  ERRDTA                17    272

     D PSDS           SDS
     D  JobNbr                        6A   Overlay(PSDS:264)
     D  JobName                      10A   Overlay(PSDS:244)
     D  JobUser                      10A   Overlay(PSDS:254)

      *
     C                   EXSR      INIT

      * HOVEDRUTINE

      * Vis liste over filer i katalogbane (gå i løkke)
     C                   EXSR      LESKAT

     C                   EVAL      *inlr = *on
     C                   RETURN
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C     *entry        PLIST
     C                   PARM                    DirName
     C                   PARM                    PGM
      *
     C     Qcmdexc       PLIST
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen
      * Clear workfile
     C     JobName       SETLL     ARCA021F
     C     JobName       READE     ARCA021F                               51
     C     *IN51         DOWEQ     *OFF
     C                   DELETE    ARCA021FR
     C     JobName       READE     ARCA021F                               51
     C                   ENDDO
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les fra katalog og legg ut oversikt               LESKAT /
      *////////////////////////////////////////////////////////////
     C     LESKAT        BEGSR
     C                   EVAL      FullName = %trimr(DirName) + Null
      * Åpne katalog
     C                   EVAL      RETURNDir = opendir(%addr(FullName))
      * Avslutt hvis feil ved åpning av katalog
     C                   IF        RETURNDir = *Null
     C                   CALLP     SndPgmMsg('CPF9898':'QCPFMSG'
     C                                       :DirError)
     C                   EVAL      *INLR = *ON
     C                   RETURN
     C                   ENDIF

     C                   DOU       PtrToEntry = *Null
      * Les neste katalog lenke
     C                   EVAL      PtrToEntry = readdir(RETURNDir)
      * Lenknavn er i feltet d_name
     C                   IF        PtrToEntry <> *Null
     C                   EVAL      DirEntry = RtnEntry
      * Hent katalog lenknavn
     C                   EVAL      EntryName = %str(%addr(d_name))
      * Vilken typ av lenke ?
     C                   EVAL      EntryPath = %trim(DirName) + '/'
     C                             + %trimr(EntryName) + Null
     C                   EVAL      RETURNInt = lstat(%addr(EntryPath)
     C                                         : %addr(StatDS))
      * Funnet gyldig fil - kall behandlingsprogram
     C                   IF        st_objtype='*STMF'
      *
      * FILEN KAN IKKE INNEHOLDE ' DA DUMPER RENAME
      *
     C     X'7D'         SCAN      EntryPath                              33
     C                   IF        *IN33 = *OFF
     C     '.zip'        SCAN      EntryPath                              34
     C                   IF        *IN34
      * ZIP-fil --> Videre med unzip
     C                   EVAL      *IN33 = *OFF
     C                   ELSE
     C                   EVAL      *IN33 = *ON
     C                   END
     C                   END
      *
     C                   IF        *IN33=*off
     C                   EVAL      EntryPath = %trim(DirName) + '/'
     C                             + %trimr(EntryName)
     C                   EVAL      EntryLen = %len(%trim(Entryname))
     C                   IF        EntryLen > 120
     C                   EXSR      FixFilNavn
     C                   ENDIF
     C                   CLEAR                   ARCA021FR
     C                   EVAL      FJOBN = %trim(JobName)
     C                   EVAL      FPATH = %trim(DirName) + '/'
     C                             + %trimr(EntryName)
     C                   EVAL      FNAME = %str(%addr(d_name))
     C                   MOVEL     st_ctime      FTIME
     C                   WRITE     ARCA021FR
      *
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDDO
      * Lukk katalog
     C                   EVAL      RETURNInt = closedir(RETURNDir)
      * Les wrkfile
     C                   EXSR      LESWRK
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les fra workfile                                  LESWRK /
      *////////////////////////////////////////////////////////////
     C     LESWRK        BEGSR
     C                   EVAL      *IN51=*OFF
     C     JobName       SETLL     ARCA021F
     C     JobName       READE     ARCA021F                               51
     C     *IN51         DOWEQ     *OFF

     C                   CALL      PGM
     C                   PARM                    DirName
     C                   PARM                    FNAME
     C                   PARM                    FPATH
     C                   PARM                    DirName2

     C                   EVAL      PGM2 = 'ARCA024R'
     C                   CALL      'ARCA023R'
     C                   PARM                    DirName2
     C                   PARM                    PGM2

     C     JobName       READE     ARCA021F                               51
     C                   ENDDO
     C                   ENDSR
      *
      **************************************************************
     C     FixFilNavn    BEGSR
      **************************************************************
      *
     C                   EVAL      NewName = %subst(Entryname:1:100)+'__'
     C                             + %subst(Entryname:EntryLen-17:18)
     C                   eval      CmdLine = 'REN OBJ(' + X'7D'
     C                             + %trim(EntryPath) + X'7D' + ')'
     C                             + ' NEWOBJ(' + X'7D'
     C                             + %trim(NewName) + X'7D' + ')'
     C                   eval      CmdLen  = 512
     C                   CALL      'QCMDEXC'
     C                   PARM                    CmdLine
     C                   PARM                    CmdLen
     C                   EVAL      EntryName = %trim(NewName)
     C                   EVAL      EntryPath = %trim(DirName) + '/'
     C                             + %trimr(EntryName)
     C                   ENDSR
      *-------------------------------------------------------------------------
      * Send pgm message
      *-------------------------------------------------------------------------
     P SndPgmMsg       B
     D                 PI              N
     D Msgid                          7    CONST
     D MsgF                          20    CONST
     D Msgdta                       128    CONST
     D Msgtp                         10    CONST OPTIONS(*NOPASS)
      * Work variables
     D Qmsgid          S              7
     D Qmsgf           S             20
     D Qmsgdta         S            128
     D Qmsgln          S             10I 0
     D Qmsgtp          S             10
     D Qmsgq           S             10
     D Qmsgqn          S             10I 0 INZ(3)
     D Qmsgky          S              4
     D Qmsger          S             15
      * Hvis bibliotek er lik blank set in *LIBL
     C                   EVAL      Qmsgid = Msgid
     C                   EVAL      Qmsgf = Msgf
     C                   EVAL      Qmsgdta = Msgdta
     C                   IF        %subst(Qmsgf:11:10) = *blank
     C                   EVAL      %subst(Qmsgf:11:10) = '*LIBL'
     C                   ENDIF
     C                   EVAL      Qmsgln = %len(%trim(Qmsgdta))
     C                   EVAL      Qmsgq = '*'
     C                   EVAL      Qmsgtp = '*DIAG'
     C                   IF        %PARMs > 3
     C                   EVAL      Qmsgtp = Msgtp
     C                   ENDIF
     C                   IF        Qmsgtp = '*STATUS'
     C                   EVAL      Qmsgq = '*EXT'
     C                   ENDIF
      *
     C                   CALL      'QMHSNDPM'                           99
     C                   PARM                    Qmsgid
     C                   PARM                    Qmsgf
     C                   PARM                    Qmsgdta
     C                   PARM                    Qmsgln
     C                   PARM                    Qmsgtp
     C                   PARM                    Qmsgq
     C                   PARM                    Qmsgqn
     C                   PARM                    Qmsgky
     C                   PARM      *LOVAL        Qmsger
      *
     C                   RETURN    *ON
     P                 E
