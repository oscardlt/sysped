      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SAKSBEHANDLER(SIGNATUR)   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FCUNDC     IF   E             DISK
     Fkundl01   IF A E           K DISK
     Ffirm      IF   E           K DISK
     Fmailspl2  IF   E           K DISK
     F                                     RENAME(mailspr:mailsp21)
     Fmailspl1  UF A E           K DISK
     Fmail02fm  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D                SDS
     D  ZUSER                254    263
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  XNAVN                  1     30
     D  KOSFN7                 1      7
     D                 DS
     D  XUSRDT                 1     10
     D  XUSRD1                 1      2
     D  XUSRD2                 3     10
     D                 DS
     D  USØKKN                 1     30
     D  USØKKO                 1      2
     D  URRN                   3     12
     D  epofax                13     13
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyz')
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2USER        YUSER            10
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSF1        SETLL     MAILSPl2
     C     START1        TAG
     C     KODSF1        READE     MAILSPl2                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        7
     C                   ADD       1             ZRECNO
     c                   move      *blanks       xnavn
     c                   move      *zeros        kundna
     c     xusrd1        ifeq      'KU'
     c                   move      xusrd2        kundnr
     c     keykun        chain     kundl01                            34
     c  N34              move      knavn         xnavn
     c                   end
     C                   WRITE     SUBFIL
     C     KODSF1        READE     MAILSPl2                               94
     C  N94              MOVE      XUSER         YUSER
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C* N94KODSF3        SETLL     MAILSPl2
     C  N94KODSF1        READE     MAILSPl2                               94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C  N30WTIL          COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SHU0942'     MSGNR
     C   89
     CANN30              Z-ADD     18            POSNBR
     C   89              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KEYUNIK       CHAIN     mailspl1                           33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    mailspr
     C  N33              UPDATE    SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
      * sende
      *
     C     WSNR          IFEQ      '1'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      SNDEP
     C                   GOTO      SLHENT
     C                   END
      *
      * endre
      *
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
      *
      * se på
      *
     C     WSNR          IFEQ      '5'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      SEESF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      XUSER         WUSER
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KEYUNI2       CHAIN     mailspl1                           33
     C                   move      'D'           xstat
     C  N33              update    mailspr
     c                   exsr      dltsf2
     C                   MOVEL     '*DELETE'     KOSFN7
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
      *
      * 04 søk
      *
     c   04              do
     C     LO:UP         XLATE     WTIL          usok
     C                   CALL      'VCUNDC2R'
     C                   PARM                    FIFIRM
     C                   PARM                    USOK             30
     C                   PARM                    URRN2            10 0
     c                   parm                    epofax            1
     C     URRN2         IFNE      *ZEROS
     C                   MOVE      URRN2         URRN
     C                   MOVE      'KP'          USØKKO
     C                   MOVE      URRN          RRN              10 0
     C     RRN           CHAIN     CUNDC                              31
     C  N31              MOVEL     CEMAIL        WTIL
     C  N31              MOVEL     'KU'          WUSRDT
     C  N31              MOVE      CCOMPN        WUSRDT
     c                   else
     c                   movel     usok          WTIL
     C                   END
     c                   goto      stchg
     C                   END
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *blanks       XUSER
     C                   MOVE      *blanks       XNAVN
     C                   MOVE      *blanks       XUSRDT
     C                   MOVE      *blanks       XFILE
     C                   MOVE      *blanks       XTIL
     C                   MOVE      *blanks       XTIL2
     C                   MOVE      *blanks       XTIL3
     C                   MOVE      *blanks       XTIL4
     C                   MOVE      *blanks       XTIL5
     C                   MOVE      *blanks       XJOBNA
     C                   MOVE      *blanks       XJOBNO
     C                   MOVE      *blanks       XSNBRA
     C                   MOVE      *blanks       XUSRDT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C*
     C                   MOVE      *blanks       WUSER
     C                   MOVE      *blanks       WNAVN
     C                   MOVE      *blanks       WUSRDT
     C                   MOVE      *blanks       WFILE
     C                   MOVE      *blanks       WTIL
     C                   MOVE      *blanks       WJOBNA
     C                   MOVE      *blanks       WJOBNO
     C                   MOVE      *blanks       WSNBRA
     C                   MOVE      *blanks       WUSRDT
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      XUSER         WUSER
     C                   MOVE      XNAVN         WNAVN
     C                   MOVE      XUSRDT        WUSRDT
     C                   MOVE      XFILE         WFILE
     C                   MOVE      XTIL          WTIL
     C                   MOVE      XTIL2         WTIL2
     C                   MOVE      XTIL3         WTIL3
     C                   MOVE      XTIL4         WTIL4
     C                   MOVE      XTIL5         WTIL5
     C                   MOVE      XJOBNA        WJOBNA           10
     C                   MOVE      XJOBNO        WJOBNO            6
     C                   MOVE      XSNBRA        WSNBRA            2
     C                   MOVE      XUSRDT        WUSRDT           10
     C                   SETON                                        40
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     10            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C                   MOVE      WUSER         XUSER
     C                   MOVE      WNAVN         XNAVN
     C                   MOVE      WUSRDT        XUSRDT
     C                   MOVE      WFILE         XFILE
     C                   MOVE      WTIL          XTIL
     C                   MOVE      WTIL2         XTIL2
     C                   MOVE      WTIL3         XTIL3
     C                   MOVE      WTIL4         XTIL4
     C                   MOVE      WTIL5         XTIL5
     C                   MOVE      WJOBNA        XJOBNA
     C                   MOVE      WJOBNO        XJOBNO
     C                   MOVE      WSNBRA        XSNBRA
     C                   MOVE      WUSRDT        XUSRDT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KEYUNIK       KLIST
     C                   KFLD                    WJOBNA
     C                   KFLD                    WUSER
     C                   KFLD                    WJOBNO
     C                   KFLD                    WSNBRA
     C     KEYUNI2       KLIST
     C                   KFLD                    XJOBNA
     C                   KFLD                    XUSER
     C                   KFLD                    XJOBNO
     C                   KFLD                    XSNBRA
     C     KODSF1        KLIST
     C                   KFLD                    W2USER
     C     KODSF2        KLIST
     C                   KFLD                    XUSER
     C     KODSF3        KLIST
     C                   KFLD                    YUSER
     C     KEYKUN        KLIST
     C                   KFLD                    FIFIRM
     c                   KFLD                    kundnr
     C*
     c                   move      zuser         w2user
     c                   read      firm
     C*
     C                   ENDSR
     C*
     c**********************************************************************
     c     SNDEP         begsr
     c**********************************************************************
     c                   move      *zeros        kundnr
     c                   move      *zeros        kundna
     c     xusrd1        ifeq      'KU'
     c                   move      xusrd2        kundna            8
     c                   end
     c                   movel     '*YES'        pdf               4
     C                   CALL      'MAIL03R'
     C                   PARM                    kundna
     C                   PARM                    XNAVN
     C                   PARM                    XUSER
     C                   PARM                    XJOBNO
     C                   PARM                    XJOBNA
     C                   PARM                    XFILE
     C                   PARM                    XSNBRA
     C                   PARM                    XTIL
     C                   PARM                    XTIL2
     C                   PARM                    XTIL3
     C                   PARM                    XTIL4
     C                   PARM                    XTIL5
     C                   PARM                    EMNE             40
     C                   PARM                    CVP1             40
     C                   PARM                    CVP2             40
     C                   PARM                    cvp3             40
     C                   PARM                    cvp4             40
     C                   PARM                    cvp5             40
     C                   PARM                    cvp6             40
     C                   PARM                    cvp7             40
     C                   PARM                    cvp8             40
     C                   PARM                    PDF               4
     c                   parm                    sendt             1
      *
      * sendt
      *
     c     sendt         ifeq      'J'
     C     KEYUNI2       CHAIN     mailspl1                           33
     C                   move      'S'           xstat
     C  N33              update    mailspr
     C                   EXSR      MOVB1
     C                   MOVEL     '*SENDT '     KOSFN7
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     c                   else
     C                   UPDATE    SUBFIL
     c                   end
     c                   endsr
     c**********************************************************************
     c     SEESF         begsr
     c**********************************************************************
     c                   move      xsnbra        xsnbrn            2 0
     C                   CALL      'SYGE36CL'
     C                   PARM                    XJOBNA
     C                   PARM                    XUSER
     C                   PARM                    XJOBNO
     C                   PARM                    XFILE
     C                   PARM                    XSNBRN
     c                   move      *blanks       wsnr
     C                   UPDATE    SUBFIL
     C                   endsr
     c**********************************************************************
     c     dltsf2        begsr
     c**********************************************************************
     c                   move      xsnbra        xsnbrn            2 0
     C                   CALL      'SYGE37CL'
     C                   PARM                    XJOBNA
     C                   PARM                    XUSER
     C                   PARM                    XJOBNO
     C                   PARM                    XFILE
     C                   PARM                    XSNBRN
     C                   endsr
