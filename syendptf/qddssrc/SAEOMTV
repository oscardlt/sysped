********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12064 * 01 YBC PTF12 FLERE BELØP-FELTER ER ENDRET FOR Å TA STØRRE VERDI
12248 * 02 YBC PTF12 VEKT, KOLLI OG MENDE ER UTVIDET.
12230 * 03 YBC PTF12 PREFERANSEKODE
12258 * 04 YBC PTF12 VF-KODE FRA OG MED 01.12.2021
********************************************************************
      *#############################################################
      * OBS!!!  SAMME SOM SAEV MED ET EKSTRA FELT FOR OMBEREGNINGSNR
      *#############################################################
      *_____________________________________
      * SYSPED / Tolldekl. EKSPORT Varelinjer
      *"""""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SAEOMTVR
      *** SV01 = B ---> HENTE MERKE & NR. FRA B/L    120691
     A            SV01           1          COLHDG('*blanks')
     A            SVAVD     R               REFFLD(AVD)
     A            SVTDN     R               REFFLD(TDN)
     A            SVOMBN         2S 0       COLHDG('OMBEREGNSNR')
     A            SVLI      R               REFFLD(LI5)
S01  A*           SVBELT    R               REFFLD(BEL11)
S01  A*                                     COLHDG('Beløp Tollverdi')
N01  A            SVBELT        13S 2       COLHDG('BELØP TOLLVERDI')
     A            SVFYL          2          COLHDG('FYLKE')
     A            SVVNT     R               REFFLD(VNT)
     A            SVAVT          1          COLHDG('FERSK FISK')
     A            SVAVTS         3          COLHDG('AVG.SEKVENS')
     A            SVAVTP         7S 3       COLHDG('PROSENTSATS AV SVAVT')
S02  A*           SVVKTB        12S 3       COLHDG('Vekt Brutto')
S02  A*           SVVKTN        12S 3       COLHDG('Vekt Netto')
S02  A*           SVNTM          9S 0       COLHDG('MENGDE')
N02  A            SVVKTB        14S 3       COLHDG('Vekt Brutto')
N02  A            SVVKTN        14S 3       COLHDG('Vekt Netto')
N02  A            SVNTM         11S 0       COLHDG('MENGDE')
     A            SVCREF         3          COLHDG('KOMMUNIKASJONREF.')
     A            SVTOA     R               REFFLD(TO22)
     A            SVTOB     R               REFFLD(TO23)
     A            SVFT          28A         COLHDG('Merke & nr')
S02  A*           SVNT           6P 0       COLHDG('ANTALL')
N02  A            SVNT           9P 0       COLHDG('ANTALL')
     A            SVEH      R               REFFLD(EH)
     A            SVVT          30A         COLHDG('VARETEKST')
     A            SVLN           5S 0       COLHDG('LINJENR')
     A            SVREFL         5S 0       COLHDG('ANT. TILL.LINJER')
     A            SVEXR01        5A         COLHDG('SYSTEM')
     A            SVEXR02       17A         COLHDG('EKSTERN REF.')
     A            SVEXR03       35A         COLHDG('ANDRE REF.')
     A            SVEXR04        9S 0       COLHDG('LINJENR')
     A            SVEXR05        5S 0       COLHDG('UNDERLINJENR')
     A            SVEXR06        3A         COLHDG('VALUTA')
     A            SVEXR07       11S 2       COLHDG('BELØP')
     A            SVLK           2A         COLHDG('LK Oppr. internt')
     A            SVNYL          1A         COLHDG('NY LINJE I OMBER.')
     A            SVERR          1          COLHDG('Error flag')
N03  A            SVPRE     R               REFFLD(PRE)
N04  A            SVVF      R               REFFLD(VF)
     A          K SVAVD
     A          K SVTDN
     A          K SVOMBN
     A          K SVLI
