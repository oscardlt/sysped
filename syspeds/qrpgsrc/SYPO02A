      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SONETABELLER              ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07-11 13-24
      *** LEDIGE INDIKATORER 31 32 34-39 41-88 95-97
      ***********************************************************
      *
     FSONES     UF A E           K DISK
     FSONESL01  IF   E           K DISK
     F                                     RENAME(SONESR:SONESR01)
     FSONESL02  IF   E           K DISK
     F                                     RENAME(SONESR:SONESR02)
     FSONET     UF A E           K DISK
     FSYPO02FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  SSTEXT                 1     50
     D  SSTE30                 1     30
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2PROD        X2PROD            1
     C                   MOVE      W2AVDE        X2AVDE            4 0
     C                   MOVE      W2ZONO        X2ZONO            5
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   27W2PROD        SETLL     SONES
     C   28W2AVDE        SETLL     SONESL01
     C   29W2ZONO        SETLL     SONESL02
     C     START1        TAG
     C   27              READ(N)   SONES                                  94
     C   28              READ      SONESL01                               94
     C   29              READ      SONESL02                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C   27              READ(N)   SONES                                  94
     C   28              READ      SONESL01                               94
     C   29              READ      SONESL02                               94
     C   27
     CANN94              MOVE      SSPROD        YPROD             1
     C   27
     CANN94              MOVE      SSAVDE        YAVDE             4 0
     C   27
     CANN94              MOVE      SSZONO        YZONO             5
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C     *IN04         IFEQ      '0'
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2PROD
     C                   MOVE      *ZEROS        W2AVDE
     C                   MOVE      *BLANKS       W2ZONO
     C                   END
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START3
     C                   END
     C*
     C*** FRISK OPP
     C   05              DO
     C                   MOVE      X2PROD        W2PROD
     C                   MOVE      X2AVDE        W2AVDE
     C                   MOVE      X2ZONO        W2ZONO
     C                   GOTO      START
     C                   END
     C*
     C*** LAGE NY
     C   06              GOTO      START5
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94SONSKY        CHAIN(N)  SONES                              94
     C                   GOTO      START2
     C                   END
      *
     C                   SELECT
     C     W2NR          WHENEQ    '1'
     C                   GOTO      START5
     C     W2NR          WHENEQ    '2'
     C     SONSKW        CHAIN(N)  SONES                              33
     C     *IN33         IFEQ      '0'
     C                   CALL      'SYPO02'
     C                   PARM                    W2PROD
     C                   PARM                    W2AVDE
     C                   PARM                    W2ZONO
     C                   END
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C     W2NR          WHENEQ    '3'
     C     SONSKW        CHAIN(N)  SONES                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      W2PROD        WKPROD
     C                   MOVE      W2AVDE        WKAVDE
     C                   MOVE      W2ZONO        WKZONO
     C                   EXSR      CPYTAB
     C  N12              GOTO      START
     C                   END
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C     W2NR          WHENEQ    '4'
     C     SONSKW        CHAIN     SONES                              33
     C     *IN33         IFEQ      '0'
     C                   EXSR      DELSR
     C     *IN12         IFEQ      '0'
     C                   MOVE      X2PROD        W2PROD
     C                   MOVE      X2AVDE        W2AVDE
     C                   MOVE      X2ZONO        W2ZONO
     C                   GOTO      START
     C                   END
     C                   END
     C                   GOTO      START3
     C     W2PROD        WHENEQ    '*'
     C                   MOVE      *BLANKS       W2PROD
     C                   MOVEA     '100'         *IN(27)
     C                   GOTO      START
     C     W2PROD        WHENNE    *BLANKS
     C                   MOVEA     '100'         *IN(27)
     C                   GOTO      START
     C     W2AVDE        WHENEQ    -1
     C                   MOVEA     '010'         *IN(27)
     C                   GOTO      START
     C     W2AVDE        WHENNE    0
     C                   MOVEA     '010'         *IN(27)
     C                   GOTO      START
     C     W2ZONO        WHENNE    *BLANKS
     C                   MOVEA     '001'         *IN(27)
     C                   GOTO      START
     C                   ENDSL
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   MOVE      *BLANKS       XXPROD
     C                   MOVE      *ZEROS        XXAVDE
     C                   MOVE      *BLANKS       XXZONO
     C                   CALL      'SYPO02'
     C                   PARM                    XXPROD            1
     C                   PARM                    XXAVDE            4 0
     C                   PARM                    XXZONO            5
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C     SSZONO        ANDNE     '*DEL*'                                      ENDRE
     C                   CALL      'SYPO02'
     C                   PARM                    SSPROD
     C                   PARM                    SSAVDE
     C                   PARM                    SSZONO
     C                   MOVE      '1'           *IN99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C     WSNR          IFEQ      '3'                                          KOPIER
     C     SSZONO        ANDNE     '*DEL*'                                      KOPIER
     C                   MOVE      '1'           *IN99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      SSPROD        WKPROD
     C                   MOVE      SSAVDE        WKAVDE
     C                   MOVE      SSZONO        WKZONO
     C                   EXSR      CPYTAB
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETT
     C     SSZONO        ANDNE     '*DEL*'                                      SLETT
     C                   EXSR      DELSR
     C                   MOVE      '1'           *IN99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   99              MOVEA     '00'          *IN(98)
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDEQ     11
     C                   MOVEL     'PRODTYPE'    KDTYPE           10
     C                   MOVE      'N'           KDUPD             1
     C                   MOVEL     W2PROD        KDKODE            5
     C                   CALL      'SYKUKD'
     C                   PARM                    KDTYPE
     C                   PARM                    KDUPD
     C                   PARM                    KDKODE
     C                   MOVEL     KDKODE        W2PROD
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     18
     C     POSNBR        ANDLE     21
     C                   CALL      'SYFA14B'
     C                   PARM                    W2AVDE
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   SETON                                        99
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SONESK        KLIST
     C                   KFLD                    SSPROD
     C                   KFLD                    SSAVDE
     C                   KFLD                    SSZONO
     C     SONSKW        KLIST
     C                   KFLD                    W2PROD
     C                   KFLD                    W2AVDE
     C                   KFLD                    W2ZONO
     C     SONSKY        KLIST
     C                   KFLD                    YPROD
     C                   KFLD                    YAVDE
     C                   KFLD                    YZONO
     C     SONCPK        KLIST
     C                   KFLD                    WKPROD
     C                   KFLD                    WKAVDE
     C                   KFLD                    WKZONO
     C     SONCP2        KLIST
     C                   KFLD                    WSPROD
     C                   KFLD                    WSAVDE
     C                   KFLD                    WSZONO
      *
     C                   MOVEA     '100'         *IN(27)
      *
     C                   ENDSR
     C***********************************************
     C     DELSR         BEGSR
     C***********************************************
     C                   EXFMT     BEKREFT
     C  N12              DO
      * SLETT HEADER
     C     SONESK        CHAIN     SONES                              33
     C  N33              DELETE    SONESR
      * SLETT LINJER
     C     SONESK        SETLL     SONET
     C     SONESK        READE     SONET                                  33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SONETR
     C     SONESK        READE     SONET                                  33
     C                   END
      *
     C                   MOVE      *OFF          *IN33
     C                   MOVE      *BLANKS       SSPROD
     C                   MOVE      *BLANKS       SSAVDE
     C                   MOVE      '*DEL*'       SSZONO
     C                   MOVE      *BLANKS       SSTEXT
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYTAB        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSPROD
     C                   MOVE      *ZEROS        WSAVDE
     C                   MOVE      *BLANKS       WSZONO
     C     STCPY         TAG
     C                   EXFMT     KOPIERE
     C                   MOVE      '0'           *IN30
     C  N12              DO
     C     WSPROD        IFEQ      *BLANKS
     C     WSAVDE        ANDEQ     0
     C     WSZONO        ANDEQ     *BLANKS
     C                   GOTO      STCPY
     C                   END
     C     SONCP2        CHAIN(N)  SONES                              33
     C  N33              MOVE      '1'           *IN30
     C   30              GOTO      STCPY
      * KOPIERE HEADER
     C     SONCPK        CHAIN(N)  SONES                              33
     C     *IN33         CABEQ     '1'           SLCPY
     C                   MOVE      WSPROD        SSPROD
     C                   MOVE      WSAVDE        SSAVDE
     C                   MOVE      WSZONO        SSZONO
     C                   WRITE     SONESR
     C     W2NR          IFEQ      ' '
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   END
      * KOPIERE LINJER
     C     SONCPK        SETLL     SONET                              33
     C     SONCPK        READE(N)  SONET                                  33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      WSPROD        SOPROD
     C                   MOVE      WSAVDE        SOAVDE
     C                   MOVE      WSZONO        SOZONO
     C                   WRITE     SONETR
     C     SONCPK        READE(N)  SONET                                  33
     C                   END
      *
     C                   MOVE      *OFF          *IN33
     C                   END
      *
     C     SLCPY         ENDSR
      *
