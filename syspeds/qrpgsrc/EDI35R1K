********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10102 * 01 CB  PTF10 Prøv først med å kopiere IFSlenke
********************************************************************
     FEDIRE1K   IF   E           K DISK
N01  FEDISX     IF   E           K DISK
N01  FEDIS4     IF   E           K DISK
N01  FEDII      IF   E           K DISK
     FQ         O    F 1024        DISK    USROPN
      *   Input Parametere
     D DirName         S            100A
     D FullName        S            256A

      *   Arbeidsvariabler
     D Outfile         DS
     D  OutFilNam                    10    INZ('Q         ')
     D  OutFilLib                    10    INZ('QTEMP     ')

     D ObjVar          S             90
     D ObjVarLen       S             10I 0 Inz(%size(ObjVar))
     D ObjVarFmt       S              8
     D ObjTyp          S             10
     D CmdLine         S            512
     D CmdLen          S             15  5

     D APIERR          DS
     D  ERRSIZ                 1      4B 0 INZ(256)
     D  ERRLEN                 5      8B 0 INZ(0)
     D  ERRMIC                 9     15
     D  ERRNBR                16     16
     D  ERRDTA                17    272


N01   * Fra IFS ?
N01  C     WMBR          CHAIN     EDISX                              52
N01  C  N52WMBR          CHAIN     EDIS4                              52
N01  C  N52S0004         CHAIN     EDII                               52
N01  C     *IN52         IFEQ      *OFF
N01  C     SXIFSOBJ      ANDNE     *BLANKS
N01  C                   CALL      'EDI36R1K'
N01  C                   PARM                    S0004
N01  C                   PARM                    INIFSR
N01  C                   PARM                    SXIFSOBJ
N01  C                   GOTO      SLUTT
N01  C                   ENDIF
      * Finnes ouput fil ?
     C                   CALL      'QUSROBJD'
     C                   PARM                    ObjVar
     C                   PARM                    ObjVarLen
     C                   PARM      'OBJD0100'    ObjVarFmt
     C                   PARM                    OutFile
     C                   PARM      '*FILE'       ObjTyp
     C                   PARM                    APIERR
      * Lag outfile hvis nødvendig
     C                   IF        ERRMIC = 'CPF9812'
     C                   EVAL      CmdLine = 'CRTPF FILE('
     C                             + %trimr(OutFilLib) + '/'
     C                             + %trimr(OutFilNam) + ')'
     C                             + ' RCDLEN(1024)'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'     Qcmdexc
     C                   ENDIF
      * Tøm outfile
     C                   EVAL      CmdLine = 'CLRPFM FILE('
     C                             + %trimr(OutFilLib) + '/'
     C                             + %trimr(OutFilNam) + ')'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'     Qcmdexc
      * Åpne fil
     C                   OPEN      Q
      * Kopiere fra EDIRE1K til QTEMP/Q
     C     WMBR          SETLL     EDIRE1K
     C     WMBR          READE     EDIRE1K                                51
     C     *IN51         DOWEQ     *OFF
     C                   EXCEPT    ADDEDI
     C     WMBR          READE     EDIRE1K                                51
     C                   ENDDO
      * Lukk QTEMP/Q
     C                   CLOSE     Q
      * Kopiere fra qtemp/q til *LIBL/EDI79F1K
     C                   EVAL      CmdLine = 'CPYF FROMFILE(QTEMP/Q)'
     C                             + ' TOFILE(EDI79F1K) TOMBR('
     C                             + WMBR
     C                             + ') MBROPT(*REPLACE) FMTOPT(*NOCHK)'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'     Qcmdexc
      * Last record
N01  C     SLUTT         TAG
     C                   EVAL      *INLR = *ON

      *////////////////////////////////////////////////////////////
      *  Init av PGM                                       *INZSR /
      *////////////////////////////////////////////////////////////
     C     *INZSR        BEGSR

     C     *ENTRY        PLIST
     C                   PARM                    WMBR             10

     C     Qcmdexc       PLIST
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen

     C                   ENDSR
     OQ         E            ADDEDI
     O                       RDATA             1024
