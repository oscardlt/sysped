     FLREHL01   IP   E           K DISK
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     FLEVEL01   UF   E           K DISK
     ILRESR         01
     I              FIRMA                       FIRMA         L1
     I              LEVNR                       LEVNR         L1
     ILREHR         01
     I              FIRMA                       FIRMA         L1
     I              LEVNR                       LEVNR         L1
      *
     C     LEVKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    LEVNR
      *
     C  N99              EXCEPT    PHEAD
     C  N99              SETON                                        99
     C                   ADD       BELOPN        SUM1
     C                   ADD       RESTN         SUM2
      *
     CL1   LEVKEY        CHAIN     LEVEL01                            33
     CL1   SUM1          IFNE      SUM2
     CL1   *INOF         IFEQ      '1'
     CL1                 EXCEPT    PHEAD
     CL1                 SETOFF                                       OF
     CL1                 END
     CL1                 EXCEPT    PDET
     CL1                 END
     C*
     CL1   SUM1          IFNE      KSALDO
     CL1                 Z-ADD     SUM1          KSALDO
     CL1N33              EXCEPT    OPDKUN
     CL1                 END
     CL1                 MOVE      *ZEROS        SUM1             13 2
     CL1                 MOVE      *ZEROS        SUM2             13 2
     OLISTE     E            PHEAD            02
     O                                           24 'DIFF LEVERANDØRRESKONTRO'
     O                                           38 'DATO:'
     O                       UDATE         Y     48
     OLISTE     E            PHEAD          1 04
     O                                           11 'FIRMA/LEVR'
     O                                           61 'POSTERINGER'
     O                                           81 'RESTBELØP'
     O                                          101 'SALDO LEVERFIL'
     O                       UDATE         Y     48
     OLISTE     E            PDET           1
     O                       FIRMA                2
     O                       LEVNR               10
     O                       LNAVN               41
     O                       SUM1          J     61
     O                       SUM2          J     81
     O                       KSALDO        J    101
     OLEVER     E            OPDKUN
     O                       KSALDO
