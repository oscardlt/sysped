      * Dette programmet startes fra CLP: EFQC_LG93A som i sin tur startes fra
      * SYEDI Standard gjenom tabellen Kunders standardmeldinger.
      *
      * Skriver til Børre Thoresens fraktapplikasjon, fil: FOP24_DAT/TOUTF.
      * Outputfilen er en flatfil med en recordlengde på 411.
      * Syntaksen er: 1-3=Segmentnavn, 4-5=Index, 6-261 Edifact data
     FEUNB      IF   E           K DISK
     FEUNH      IF   E           K DISK
     FEDIM6     IF   E           K DISK
     FECNT93A   IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FELOC93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FETCC93A   IF   E           K DISK
     FETDT93A   IF   E           K DISK
     FETOD93A   IF   E           K DISK
     FTOUTF     O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
      *_________________
      * Arrays for input
      *"""""""""""""""""
     D                 DS
     D  WUNB00                 1    256
     D  WUNB00A                1      4                                         0001
     D  WUNB00B                5      5                                         0002
     D  WUNB00C                6     40                                         0004
     D  WUNB00D               41     44                                         0007
     D  WUNB00E               59     93                                         0010
     D  WUNB00F               94     97                                         0007
     D  WUNB00G              112    117                                         0017
     D  WUNB00H              118    121                                         0019
     D  WUNB00I              122    141                                         0020
     D                 DS
     D  WUNH00                 1    256
     D  WUNH00A                1     14                                         0062
     D  WUNH00B               15     20                                         0065
     D  WUNH00C               21     23                                         0052
     D  WUNH00D               24     26                                         0054
     D  WUNH00E               27     28                                         0051
     D  WUNH00F               29     34                                         0057
     D                 DS
     D  WBGM00                 1    256
     D  WBGM00A                1      3                                         1001
     D  WBGM00B               45     79                                         1004
     D                 DS
     D  WDTM00                 1    256
     D  WDTM00A                1      3                                         2005
     D  WDTM00B                4     38                                         2380
     D  WDTM00C               39     41                                         2379
     D                 DS
     D  WFTX00                 1    256
     D  WFTX00A                1      3                                         4451
     D  WFTX00B               16     85                                         4440
     D  WFTX00C               86    155                                         4440
     D  WFTX00D              156    225                                         4440
     D                 DS
     D  WCNT00                 1    256
     D  WCNT00A                1      3                                         6069
     D  WCNT00B                4     21S 0                                      6066
     D  WCNT00C               22     24                                         6411
     D                 DS
     D  WTOD02                 1    256
     D  WTOD02A                1      3                                         4055
     D  WTOD02B                7      9                                         4053
     D  WTOD02C               10     12                                         1131
     D                 DS
     D  WRFF03                 1    256
     D  WRFF03A                1      3                                         1153
     D  WRFF03B                4     38                                         1154
     D                 DS
     D  WTDT08                 1    256
     D  WTDT08A                1      3                                         8051
     D                 DS
     D  WLOC08                 1    256
     D  WLOC08A                1      3                                         3227
     D  WLOC08B                4     28                                         3225
     D  WLOC08C               29     31                                         1131
     D                 DS
     D  WNAD10                 1    256
     D  WNAD10A                1      3                                         3035
     D  WNAD10B                4     20                                         3039
     D  WNAD10C               24     26                                         3055
     D  WNAD10D               27     61                                         3036
     D  WNAD10E               62     96                                         3042
     D  WNAD10F               97    131                                         3042
     D  WNAD10G              132    166                                         3164
     D  WNAD10H              167    175                                         3251
     D                 DS
     D  WTCC13                 1    256
     D  WTCC13A                1     17                                         8023
     D  WTCC13B               18     20                                         1131
     D  WTCC13C               21     23                                         3055
     D                 DS
     D  WMOA13                 1    256
     D  WMOA13A                1      3                                         5025
     D  WMOA13B                4     21S 0                                      5004
     D                 DS
     D  WUNT00                 1    256
     D  WUNT00A                1      6                                         0074
     D  WUNT00B                7     20                                         0062
     D                 DS
     D  WUNZ00                 1    256
     D  WUNZ00A                1      6                                         0036
     D  WUNZ00B                7     20                                         0020
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * BGM00                                               BGM00 /
      *////////////////////////////////////////////////////////////
     C     BGM00         BEGSR

     C                   CLEAR                   TOUTR

     C                   MOVEL     'BGM'         TOSEGM
     C                   MOVEL     U1001         WBGM00A
     C                   MOVEL     U1004         WBGM00B
     C                   MOVEL     WBGM00        TORCD

     C                   WRITE     TOUTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT93A                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'CNT'         TOSEGM
     C                   MOVEL     C6069         WCNT00A
     C                   MOVEA     C6066         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          WCNT00B
     C                   MOVEL     C6411         WCNT00C
     C                   MOVEL     WCNT00        TORCD

     C                   WRITE     TOUTR

     C  N51KEYMS0        READE     ECNT93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * DTM00                                               DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM93A                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'DTM'         TOSEGM
     C                   MOVEL     D2005         WDTM00A
     C                   MOVEL     D2380         WDTM00B
     C                   MOVEL     D2379         WDTM00C
     C                   MOVEL     WDTM00        TORCD

     C                   WRITE     TOUTR

     C  N51KEYMS0        READE     EDTM93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX00                                               FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EFTX93A                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'FTX'         TOSEGM
     C                   MOVEL     F4451         WFTX00A
     C                   MOVEL     F4440A        WFTX00B
     C                   MOVEL     F4440B        WFTX00C
     C                   MOVEL     F4440C        WFTX00D
     C                   MOVEL     WFTX00        TORCD

     C                   WRITE     TOUTR

     C  N51KEYMS0        READE     EFTX93A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1

     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get interchange values
     C     ZMN           CHAIN     EUNB                               51

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LOC08                                               LOC08 /
      *////////////////////////////////////////////////////////////
     C     LOC08         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     8             ZGN1
     C     KEYMS1        SETLL     ELOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC93A                                52
     C     *IN52         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'LOC'         TOSEGM
     C                   MOVEL     L3227         WLOC08A
     C                   MOVEL     L3225         WLOC08B
     C                   MOVEL     L1131A        WLOC08C
     C                   MOVEL     WLOC08        TORCD

     C                   WRITE     TOUTR

     C  N52KEYMS1        READE     ELOC93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA13                                               MOA13 /
      *////////////////////////////////////////////////////////////
     C     MOA13         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     13            ZGN2
     C     KEYMS2        SETLL     EMOA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EMOA93A                                53
     C     *IN53         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'MOA'         TOSEGM
     C                   MOVEL     M5025         WMOA13A
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          WMOA13B
     C                   MOVEL     WMOA13        TORCD

     C                   WRITE     TOUTR

     C  N53KEYMS2        READE     EMOA93A                                53
     C  N53              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD10                                               NAD10 /
      *////////////////////////////////////////////////////////////
     C     NAD10         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'NAD'         TOSEGM
     C                   MOVEL     N3035         WNAD10A
     C                   MOVEL     N3039         WNAD10B
     C                   MOVEL     N3055         WNAD10C
     C                   MOVEL     N3036A        WNAD10D
     C                   MOVEL     N3042A        WNAD10E
     C                   MOVEL     N3042B        WNAD10F
     C                   MOVEL     N3164         WNAD10G
     C                   MOVEL     N3251         WNAD10H
     C                   MOVEL     WNAD10        TORCD

     C                   WRITE     TOUTR
      *Les TCC under NAD
     C                   Z-ADD     NREP0         ZREPB0
     C                   EXSR      TCC13


     C     KEYMS0        READE     ENAD93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF03                                               RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     3             ZGN0
     C     KEYMS0        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF93A                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'RFF'         TOSEGM
     C                   MOVEL     R1153         WRFF03A
     C                   MOVEL     R1154         WRFF03B
     C                   MOVEL     WRFF03        TORCD

     C                   WRITE     TOUTR
      *
     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TCC13                                               TCC13 /
      *////////////////////////////////////////////////////////////
     C     TCC13         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     13            ZGN1
     C     KEYMS1        SETLL     ETCC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETCC93A                                52
     C     *IN52         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'TCC'         TOSEGM
     C                   MOVEL     T8023         WTCC13A
     C                   MOVEL     T1131A        WTCC13B
     C                   MOVEL     T3055A        WTCC13C
     C                   MOVEL     WTCC13        TORCD

     C                   WRITE     TOUTR
      *Les MOA under TCC
     C                   Z-ADD     TREP0         ZREPC0
     C                   Z-ADD     TREP1         ZREPC1
     C                   EXSR      MOA13

     C  N52KEYMS1        READE     ETCC93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TDT08                                               TDT08 /
      *////////////////////////////////////////////////////////////
     C     TDT08         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     8             ZGN0
     C     KEYMS0        SETLL     ETDT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT93A                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'TDT'         TOSEGM
     C                   MOVEL     T4055         WTDT08A
     C                   MOVEL     WTDT08        TORCD

     C                   WRITE     TOUTR

      *Les LOC under TDT
     C                   Z-ADD     TREP0         ZREPB0
     C                   EXSR      LOC08

     C  N51KEYMS0        READE     ETDT93A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TOD02                                               TOD02 /
      *////////////////////////////////////////////////////////////
     C     TOD02         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     2             ZGN0
     C     KEYMS0        SETLL     ETOD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETOD93A                                51
     C     *IN51         DOWEQ     '0'

     C                   CLEAR                   TOUTR

     C                   MOVEL     'TOD'         TOSEGM
     C                   MOVEL     T4055         WTOD02A
     C                   MOVEL     T4053         WTOD02B
     C                   MOVEL     T1131         WTOD02C
     C                   MOVEL     WTOD02        TORCD

     C                   WRITE     TOUTR

     C  N51KEYMS0        READE     ETOD93A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNB00                                               UNB00 /
      *////////////////////////////////////////////////////////////
     C     UNB00         BEGSR

     C                   CLEAR                   TOUTR

     C                   MOVEL     'UNB'         TOSEGM
     C                   MOVEL     U0001         WUNB00A
     C                   MOVEL     U0002         WUNB00B
     C                   MOVEL     U0004         WUNB00C
     C                   MOVEL     U0007S        WUNB00D
     C                   MOVEL     U0010         WUNB00E
     C                   MOVEL     U0007R        WUNB00F
     C                   MOVEL     U0017         WUNB00G
     C                   MOVEL     U0019         WUNB00H
     C                   MOVEL     U0020A        WUNB00I
     C                   MOVEL     WUNB00        TORCD

     C                   WRITE     TOUTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR

     C     MMN           CHAIN     EUNH                               51

     C                   CLEAR                   TOUTR

     C                   MOVEL     'UNH'         TOSEGM
     C                   MOVEL     U0062A        WUNH00A
     C                   MOVEL     U0065         WUNH00B
     C                   MOVEL     U0052         WUNH00C
     C                   MOVEL     U0054         WUNH00D
     C                   MOVEL     U0051         WUNH00E
     C                   MOVEL     U0057         WUNH00F
     C                   MOVEL     WUNH00        TORCD

     C                   WRITE     TOUTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNT00                                               UNT00 /
      *////////////////////////////////////////////////////////////
     C     UNT00         BEGSR

     C                   CLEAR                   TOUTR

     C                   MOVEL     'UNT'         TOSEGM
     C                   MOVEL     U0074         WUNT00A
     C                   MOVEL     U0062B        WUNT00B
     C                   MOVEL     WUNT00        TORCD

     C                   WRITE     TOUTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNZ00                                               UNZ00 /
      *////////////////////////////////////////////////////////////
     C     UNZ00         BEGSR

     C                   CLEAR                   TOUTR

     C                   MOVEL     'UNZ'         TOSEGM
     C                   MOVEL     U0036         WUNZ00A
     C                   MOVEL     U0020B        WUNZ00B
     C                   MOVEL     WUNZ00        TORCD

     C                   WRITE     TOUTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   EXSR      UNB00

     C     ZMN           CHAIN     EDIM6                              50
     C     *IN50         DOWEQ     *OFF

     C                   EXSR      UNH00
     C                   EXSR      BGM00
     C                   EXSR      DTM00
     C                   EXSR      FTX00
     C                   EXSR      CNT00
     C                   EXSR      TOD02
     C                   EXSR      RFF03
     C                   EXSR      TDT08
     C                   EXSR      NAD10
     C                   EXSR      UNT00

     C     ZMN           READE     EDIM6                                  50
     C                   ENDDO

     C                   EXSR      UNZ00
     C                   ENDSR
