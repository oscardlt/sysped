********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SJEKK OM KODE SKAL SLETTES
********************************************************************
      * LES GAMMEL KODTS8, SJEKK MOT NY FIL....
      * HVIS ENNÅ GYLDIG, OPPDATER(MEN BEHOLD TEKST, OG EVT KODE FOR
      *      IKKE MEDTA I MOMSGRUNNLAG (N=IKKE MED, ALT ANNET MED..)
      * HVIS IKKE, SLETT
      *
     H DATEDIT(*DMY)
     FKODTS8    UP   E             DISK
     FKODTS8NY  IF   E           K DISK
     F                                     RENAME(KOS8R:XXXX)
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     D                 DS
     D  KS8FTX                 1     52
     D  KS8FT1                 1     49
     D  KS8FT3                51     51
     D  KS8FT2                52     52
     D                 DS
     D  GMFTX                  1     52
     D  GMFT3                 51     51
      *
     C     *IN99         IFEQ      *OFF
     C                   Z-ADD     1             SIDE              4 0
     C                   TIME                    TID               6 0
     C                   EXCEPT    PHEAD                                         1 PAGE
     C                   SETON                                        99
     C                   END
     C   OF              ADD       1             SIDE              4 0
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF         OVERFLOW
      *
     C     KEY           KLIST
     C                   KFLD                    KS8AVG
     C                   KFLD                    KS8SKV
      *
     C     *LIKE         DEFINE    KS8STA        GMSTA
     C                   MOVE      KS8FTX        GMFTX
     C                   MOVE      KS8STA        GMSTA
      *
     C     KEY           CHAIN     KODTS8NY                           33
     C     *IN33         IFEQ      '0'                                            GAMMEL..
     C                   MOVE      KS8FT3        GMFT3
     C                   MOVE      GMFTX         KS8FTX
     C                   MOVE      GMSTA         KS8STA
     C                   UPDATE    KOS8R
     C                   ELSE                                                     UTGÅTT..
     C     KS8AVG        IFNE      'RT'
     C     KS8AVG        ANDNE     'TL'
N01  C                   CALL      'SYTIAV2B'
N01  C                   PARM                    KS8AVG
N01  C                   PARM                    KS8SKV
N01  C                   PARM                    USLT              1
N01  C                   IF        USLT = 'J'
     C                   DELETE    KOS8R
     C                   EXCEPT    DET
N01  C                   ELSE
N01  C                   UPDATE    KOS8R                                        IKKE SLETT
N01  C                   END
N01  C                   ELSE
N01  C                   UPDATE    KOS8R                                        RT/TL-AVGIFT
     C                   END
     C                   END
     OLISTE     E            PHEAD            02
     O                                           24 '** SLETTEDE AVGIFTSKODER'
     O                                           40 '/SEKVENSNR   ** '
     O                                           65 '(GRUNNDEFINISJONER)'
     O                                           86 'KJØRT DATO:'
     O                       UDATE         Y     96
     O                                          101 'TID:'
     O                       TID                110 '0  :  :  '
     O                                          120 'SIDE:'
     O                       SIDE          Z    125
     OLISTE     E            PHEAD            04
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            PHEAD            05
     O                                           24 ' AVG.KODE  SEKVENS     '
     O                                              'BESKRIVELSE...........'
     OLISTE     E            PHEAD            06
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            DET         1
     O                       KS8AVG               7
     O                       KS8SKV              17
     O                       KS8FT1              73
     O                       KS8FT2              75
     O                       KS8FT3              77
