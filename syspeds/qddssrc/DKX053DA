      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLAVD0' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '3' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            TIAVD          4Y 0O  6  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  6  9
     A            TITSB          8A  O  6 43
     A            X0004         10A  O  6 52
     A            TITIN         17A  O  6 63
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/DKX053RA'
     A                                      COLOR(BLU)
     A                                  1 34'NCTS Import'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  2 26'Underhåll standardupplysningar'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Vælj option och trykk Enter:'
     A                                      COLOR(BLU)
     A                                  4  4'2=Ændra   3=Kopiera   4=Radera'
     A                                      COLOR(BLU)
     A                                  5  2'Op Avd Avdelningsnamn             -
     A                                             Avs.T.pl Avs.UNB.ID Føretags-
     A                                      nr'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1  2'F3=avsluta   F5=Uppdatera   F6=Ska-
     A                                      pa ny   F8=Søk'
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk från--->'
     A            W2AAVD         4Y 0I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  4  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'Bekreft sletting')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  1  2'** Radera ** BEKRÆFTA MED F23'
     A                                      COLOR(RED)
     A                                  2  2'   Avd avdelningsnamn             -
     A                                             Avs.T.pl Avs.UNB.ID Føretags-
     A                                      nr'
     A                                      COLOR(WHT)
     A            TIAVD          4Y 0O  3  4EDTCDE(4)
     A                                      COLOR(WHT)
     A                                      DSPATR(PC)
     A            KOANVN        33A  O  3  9
     A            TITSB          8A  O  3 43
     A            X0004         10A  O  3 52
     A            TITIN         17A  O  3 63
     A                                  4  2'** Radera ** BEKRÆFTA MED F23'
     A                                      COLOR(RED)
     A                                  6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  5  2'F12=Avbryt   F23=Radera'
     A                                      COLOR(BLU)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/DKX053RA'
     A                                      COLOR(BLU)
     A                                  1 34'NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  2 24'Underhåll avdelningsupplysningar'
     A                                      COLOR(WHT)
     A                                  3  2'Vælj option och trykk Enter:'
     A                                      COLOR(BLU)
     A                                  4  4'2=Ændra   4=Radera'
     A                                      COLOR(BLU)
     A                                  5  2'Op Avd Avdelningsnamn             -
     A                                             Avs.T.pl Avs.UNB.ID Føretags-
     A                                      nr'
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                 23  2'F3=Avsluta   F5=Uppdatera   F6=Ska-
     A                                      pa ny   F8=Søk'
     A                                      COLOR(BLU)
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(7 25 4 30 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF04(04 'søking')
     A                                      CF12(12 'Forrige')
     A            MSGNR          7A  P
     A            TIAVD          4S 0O  2 14COLOR(WHT)
     A            WSAVDT         4Y 0B  3 14DSPATR(UL)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A  30        MSGTXT        28A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  1  2'Kopiera från/till avdelning'
     A                                      COLOR(WHT)
     A                                  2  2'Från avd:'
     A                                  3  2'Till avd:'
