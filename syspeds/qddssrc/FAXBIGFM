********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH PTF8 NY HELP - FUNKSJON
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('FAXBIG10' 'FAXBIG' 'SY400L-
S01  A*                                     O/FAX')
N01  A                                      HLPPNLGRP(HLPSCR FAXBIGPNL)
N01  A                                      HLPTITLE('GODSLISTE PÅ FAX')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØK    ')
     A                                      CF09(09 'SØK BIL')
     A                                      CF10(10 'SEND FAX')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/FAXBIG'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 SHU0224 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A                                  4  3MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 31DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('WRONG DEPARTMENT           -
     A                                       !!        .' 99)
     A                                  5  3MSGCON(024 DIV1498 *LIBL/SYMSGF)
     A            WSSG           3A  B  5 31COLOR(YLW)
     A  98                                  ERRMSG('WRONG INITIALS             -
     A                                      !!     .' 98)
     A                                      DSPATR(UL)
     A                                  6  3MSGCON(024 DIV1544 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  6 31DSPATR(UL)
     A  97                                  ERRMSG('WRONG TRIPNUMBER           -
     A                                      !!!.' 97)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(PC)
     A                                  7  3MSGCON(024 SHU0208 *LIBL/SYMSGF)
     A            TUKNA          8S 0O  7 31COLOR(WHT)
     A            KNAVN         30A  O  7 40COLOR(WHT)
     A                                  8  3MSGCON(024 SHU0167 *LIBL/SYMSGF)
     A            FAXNR         15A  B  8 31COLOR(YLW)
     A                                  8 48MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B  8 61COLOR(YLW)
     A                                  8 64'='
     A            FAXRET         6A  O  8 66
     A                                  9  3MSGCON(024 JOV0791 *LIBL/SYMSGF)
     A            WSBILN        10A  B  9 31COLOR(YLW)
     A            WSTEXT        20A  O  9 42COLOR(WHT)
     A                                 10  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B 10 11COLOR(YLW)
     A                                      CHECK(LC)
     A                                 12  3MSGCON(025 SHU0173 *LIBL/SYMSGF)
     A            WSTIME         6Y 0B 12 31DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                 13  3MSGCON(025 SHU0174 *LIBL/SYMSGF)
     A            WSDATE         6Y 0B 13 31DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A                                 14  3MSGCON(025 SHU0343 *LIBL/SYMSGF)
     A            WSCVRP         1A  B 14 31COLOR(YLW)
     A                                 23  3MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(018 JOV0789 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 43MSGCON(018 SHU0591 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             24  3MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
