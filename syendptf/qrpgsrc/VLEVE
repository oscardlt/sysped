      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE LEVERANDOR                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-84
      ***********************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10157 * 01 JOV PTF10 endring skjermbilde / tekster i MSGCON  - textkons i rpg
10157 * 02 SH  PTF10 kontohenvisning interrim
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 03 jov PTF11 kontohenvisning interrim - krav kun hvis utfylt Innført postnr Utland
      * 04 sh  PTF11 land reg.nummer
      * 05 sh  PTF11 bakgironummer
      * 06 sh  PTF12 blanke foretaksnummer
      * 07 sh  PTF12 skrive til visma
      * 08 sh  PTF12 fjernet alle øer direkte
      * 09 sh  PTF12 søke også på nummer
      * 10 sh  PTF12 nye tester visma
      * 11 sh  PTF12 leverandørtype
********************************************************************
      *
N10  FKODTLK    IF   E           K DISK
N07  FVISLEVE   UF A E           K DISK
N07  Ffirmvis   IF   E           K DISK
     FKHEDL01   IF   E           K DISK
     FKODAL01   IF   E           K DISK
     FLEVEL01   UF A E           K DISK
     FLEVEL02   IF   E           K DISK
     F                                     RENAME(LEVER:LEVER02)
     FCUNDL01   IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FVLEVEFM   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSCLO2               382    383B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D XTXT1           C                   CONST('LAGE NY LEVERANDOR')
     D XTXT2           C                   CONST('ENDRE LEVERANDOR')
     D XTXT3           C                   CONST('SLETT LEVERANDOR')
N01  D XTXT1e          C                   CONST('CREATE NEW SUPPLIER')
N01  D XTXT2e          C                   CONST('CHANGE SUPPLIER')
N03  D XTXT3e          C                   CONST('DELETE SUPPLIER')
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
N09  C     SOKNO         ifeq      'N'
     C     LEV2K         SETLL     LEVEL02
N09  C                   else
N09  C     LEV2KN        SETLL     LEVEL01
N09  c                   end
     C     START1        TAG
N09  C     SOKNO         ifeq      'N'
     C     FIFIRM        READE     LEVEL02                                94
N09  C                   else
N09  C     FIFIRM        READE(N)  LEVEL01                                94
N09  c                   end
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
N09  C     SOKNO         ifeq      'N'
     C     FIFIRM        READE     LEVEL02                                94
N09  C                   else
N09  C     FIFIRM        READE(N)  LEVEL01                                94
N09  c                   end
     C  N94              END
     C                   END
     C  N94              DO
     C                   MOVE      LEVNR         XXLNR             8 0
     C                   MOVE      LNAVN         XXNA             30
     C                   MOVE      ADR1          XXAD1            30
     C                   MOVE      ADR2          XXAD2            30
     C                   MOVE      POSTNR        XXPN              4 0
N03  C                   MOVE      POSTNU        XXPNU            10
     C                   MOVE      ADR3          XXAD3            24
n04  C                   MOVEL     land          XXland            2
n04  C                   MOVE      rnrale        XXrale           20
     C                   MOVE      SONAVN        XXNASO           10
     C                   MOVE      KUNDNR        XXKNR             8 0
     C                   MOVE      BETBET        XXBB              2
     C                   MOVE      KHENV         XXKH              3
N05  C                   MOVEL     BANKG         XXBANK           35
N11  C                   MOVEL     LEVTYP        XXLEVT            1
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      * FRISK OPP
     C   05              GOTO      START
      * LAGE NY
     C   06              DO                                                     1<-B
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   EXSR      MOVB2
     C                   MOVEL     XTXT1         XTYPE
N01  C     FIFIRM        IFNE      'NO'
N01  C                   EVAL      XTYPE =XTXT1e
N01  C                   ENDIF
     C                   GOTO      START5
     C                   END                                                    1<-E
      * SOK FRA
     C   08              GOTO      START6
      *
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94              DO
     C                   MOVE      XXLNR         LEVNR
     C                   MOVE      XXNA          LNAVN
     C                   MOVE      XXAD1         ADR1
     C                   MOVE      XXAD2         ADR2
     C                   MOVE      XXPN          POSTNR
N03  C                   MOVE      XXPNU         POSTNU
     C                   MOVE      XXAD3         ADR3
n04  C                   MOVEL     xxland        land
n04  C                   MOVE      xxrale        rnrale
     C                   MOVE      XXNASO        SONAVN
     C                   MOVE      XXKNR         KUNDNR
     C                   MOVE      XXBB          BETBET
     C                   MOVE      XXKH          KHENV
N05  C                   MOVEL     XXBANK        BANKG
N11  C                   MOVEL     XXLEVT        LEVTYP
     C                   END
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C*** CREATE A RECORD
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   SETOFF                                       30
      *
     C     WSCLO2        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SOKDTA
     C                   GOTO      START5
     C                   END
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
N09  C     w2nrso        ifne      0
N09  c                   move      'J'           sokno             1
N09  c                   else
N09  c                   move      'N'           sokno             1
N09  c                   end
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     WSNA          COMP      *BLANKS                                90
     C     *IN90         IFEQ      '1'
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC1830'     MSGNR
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSPS          COMP      *BLANKS                                89
     C     *IN89         IFEQ      '1'
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC1831'     MSGNR
     C                   GOTO      SLTB1
     C                   END
      *
     C     WSNASO        COMP      *BLANKS                                88
     C     *IN88         IFEQ      '1'
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC1832'     MSGNR
     C                   GOTO      SLTB1
     C                   END
      *
     C     KODA1K        CHAIN     KODAL01                            87
     C     *IN87         IFEQ      '1'
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC1833'     MSGNR
     C                   GOTO      SLTB1
     C                   END
      *
     C     KHED1K        CHAIN     KHEDL01                            86
     C     *IN86         IFEQ      '1'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC1834'     MSGNR
     C                   GOTO      SLTB1
     C                   END
      *
      *
N03  c     WSKHI         ifne      *blanks
n02  C     KHED1KI       CHAIN     KHEDL01                            83
n02  C     *IN83         IFEQ      '1'
n02  C                   Z-ADD     10            LINNBR
n02  C                   Z-ADD     43            POSNBR
n02  C                   MOVE      '1'           *IN30
n02  C                   MOVE      'YBC1834'     MSGNR
n02  C                   GOTO      SLTB1
n02  C                   END
n03  C                   END
      *
     C     WSKNR         IFNE      0
     C     CUND1K        CHAIN     CUNDL01                            85
     C     *IN85         IFEQ      '1'
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPK0001'     MSGNR
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
     C     WSVAL         IFNE      *BLANKS
     C     VALUL01K      CHAIN     VALUL01                            84
     C     *IN84         IFEQ      '1'
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPV0001'     MSGNR
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
n10   * land
n10  c     keyland       chain     kodtlk                             82
n10  C     *IN82         IFEQ      '1'
n10  C                   Z-ADD     12            LINNBR
n10  C                   Z-ADD     32            POSNBR
n10  C                   MOVE      '1'           *IN30
n10  C                   MOVE      'SHU2060'     MSGNR
n10  C                   GOTO      SLTB1
n10  C                   END
n10  c* poststed VED UTLAND
n10  c     wspn          comp      0                                  8181
n10  c   81wsland        comp      'NO'                               8181
n10  c     *in81         ifeq      '1'
n10  C                   Z-ADD     7             LINNBR
n10  C                   Z-ADD     22            POSNBR
n10  C                   MOVE      '1'           *IN30
n10  C                   MOVE      'SHU2061'     MSGNR
n10  C                   GOTO      SLTB1
n10  C                   END
n10  c* poststed VED NORGE
n10  c     wspn          comp      0                                      81
n10  c   81wsland        comp      'NO'                                   81
n10  c     *in81         ifeq      '1'
n10  C                   Z-ADD     7             LINNBR
n10  C                   Z-ADD     22            POSNBR
n10  C                   MOVE      '1'           *IN30
n10  C                   MOVE      'SHU2062'     MSGNR
n10  C                   GOTO      SLTB1
n10  C                   END
n10  c* visma
n10  c     visma         ifeq      'J'
n10  c     wspnu         comp      *blanks                            8080
n10  c     *in80         ifeq      '1'
n10  C                   Z-ADD     8             LINNBR
n10  C                   Z-ADD     42            POSNBR
n10  C                   MOVE      '1'           *IN30
n10  C                   MOVE      'SHU2059'     MSGNR
n10  C                   GOTO      SLTB1
n10  C                   END
n10  C                   END
     C     WSLNR         IFEQ      0
     C     LEVMAX        SETGT     LEVEL01
s07  C*    FIFIRM        READPE    LEVEL01                                33
n07  C     FIFIRM        READPE(N) LEVEL01                                33
     C     *IN33         IFEQ      '0'
     C     LEVNR         ADD       1             WSLNR
     C                   ELSE
     C                   Z-ADD     1             WSLNR
     C                   END
     C                   END
     C     LEV1K         CHAIN     LEVEL01                            33
     C                   EXSR      MOVWLE
     C                   MOVE      ' '           WSNR
     C   40              UPDATE    SUBFIL
     C  N40              DO
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   WRITE     SUBCTL
      *
     C  N33              UPDATE    LEVER
     C   33              WRITE     LEVER
n07  C     visma         IFEQ      'J'
n07  C     AKTKOD        ANDEQ     'A'
N07  C                   EXSR      VISMASR
n07  C                   END
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C     LNAVN         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGLEV
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '3'
     C     LNAVN         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CPYLEV
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C     LNAVN         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTLEV
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGLEV        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   SETON                                        40
     C                   EXSR      MOVLEW
     C                   MOVEL     XTXT2         XTYPE
N01  C     FIFIRM        IFNE      'NO'
N01  C                   EVAL      XTYPE =XTXT2e
N01  C                   ENDIF
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     22            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C     WSCLO2        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SOKDTA
     C                   GOTO      STCHG
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C   12              DO
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYLEV        BEGSR
     C***********************************************
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C     LEV1K2        CHAIN(N)  LEVEL01                            33
     C                   EXSR      MOVLEW
     C                   MOVE      *ZEROS        WSLNR
     C                   MOVEL     XTXT1         XTYPE
N01  C     FIFIRM        IFNE      'NO'
N01  C                   EVAL      XTYPE =XTXT1e
N01  C                   ENDIF
     C                   SETON                                        99
     C                   SETOFF                                       40
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     22            POSNBR
      *
     C     STCPY         TAG
     C                   EXFMT     VAREL2
     C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C     WSCLO2        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SOKDTA
     C                   GOTO      STCPY
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCPY
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTLEV        BEGSR
     C***********************************************
     C                   MOVEL     XTXT3         XTYPE
N01  C     FIFIRM        IFNE      'NO'
N01  C                   EVAL      XTYPE =XTXT3e
N01  C                   ENDIF
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     LEV1K2        CHAIN     LEVEL01                            33
     C  N33              DELETE    LEVER
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWLE        BEGSR
     C***********************************************
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      WSLNR         LEVNR
     C                   MOVE      WSNA          LNAVN
     C                   MOVE      WSAD1         ADR1
     C                   MOVE      WSAD2         ADR2
     C                   MOVE      WSPN          POSTNR
N03  C                   MOVE      WSPNU         POSTNU
     C                   MOVE      WSPS          ADR3
N04  C                   MOVEL     WSland        land
N04  C                   MOVE      WSrale        rnrale
     C                   MOVE      WSNASO        SONAVN
     C                   MOVE      WSBB          BETBET
     C                   MOVE      WSKH          KHENV
N05  C                   MOVEL     WSBANK        BANKG
N02  C                   MOVE      WSKHI         KHENIT
     C                   MOVE      WSKNR         KUNDNR
     C                   MOVE      WSVAL         VALKOD
N11  C                   MOVE      WSLEVT        LEVTYP
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVLEW        BEGSR
     C***********************************************
     C                   MOVE      LEVNR         WSLNR
     C                   MOVE      LNAVN         WSNA
     C                   MOVE      ADR1          WSAD1
     C                   MOVE      ADR2          WSAD2
     C                   MOVE      POSTNR        WSPN
N03  C                   MOVE      POSTNU        WSPNU
     C                   MOVE      ADR3          WSPS
N04  C                   MOVEL     land          wsland
N04  C                   MOVE      rnrale        wsrale
     C                   MOVE      SONAVN        WSNASO
     C                   MOVE      BETBET        WSBB
     C                   MOVE      KHENV         WSKH
N05  C                   MOVEL     BANKG         WSBANK
N02  C                   MOVE      KHENIT        WSKHI
     C                   MOVE      KUNDNR        WSKNR
     C                   MOVE      VALKOD        WSVAL
     C                   MOVE      LEVTYP        WSLEVT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
N06  C                   MOVE      *blanks       wsrale
     C                   MOVE      *ZEROS        LEVNR
     C                   MOVE      *BLANKS       LNAVN
     C                   MOVEL     '*DELETE'     LNAVN
     C                   MOVE      *BLANKS       ADR1
     C                   MOVE      *BLANKS       ADR2
     C                   MOVE      *ZEROS        POSTNR
N03  C                   MOVE      *BLANKS       POSTNU
     C                   MOVE      *BLANKS       ADR3
N04  C                   MOVE      *BLANKS       land
N04  C                   MOVE      *BLANKS       rnrale
     C                   MOVE      *BLANKS       SONAVN
     C                   MOVE      *BLANKS       BETBET
     C                   MOVE      *BLANKS       KHENV
N05  C                   MOVE      *BLANKS       BANKG
     C                   MOVE      *ZEROS        KUNDNR
     C                   MOVE      *BLANKS       VALKOD
N11  C                   MOVE      *BLANKS       LEVTYP
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
N06  C                   MOVE      *blanks       wsrale
     C                   MOVE      *ZEROS        WSLNR
     C                   MOVE      *BLANKS       WSNA
     C                   MOVE      *BLANKS       WSAD1
     C                   MOVE      *BLANKS       WSAD2
     C                   MOVE      *ZEROS        WSPN
N03  C                   MOVE      *BLANKS       WSPNU
     C                   MOVE      *BLANKS       WSPS
     C                   MOVE      *BLANKS       WSNASO
     C                   MOVE      *BLANKS       WSBB
     C                   MOVE      *BLANKS       WSKH
N05  C                   MOVE      *BLANKS       WSBANK
N02  C                   MOVE      *BLANKS       WSKHI
     C                   MOVE      *ZEROS        WSKNR
     C                   MOVE      *BLANKS       WSVAL
N11  C     visma         ifeq      'J'
N11  C                   MOVE      'T'           WSLEVT
N11  C                   ELSE
N11  C                   MOVE      *BLANKS       WSLEVT
N11  C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SOKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     22
     C     POSNBR        ANDLE     23
     C                   CALL      'VLEVE01'
     C                   PARM                    FIFIRM
     C                   PARM                    WSBB
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   GOTO      SLSOK
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     22
     C     POSNBR        ANDLE     24
     C                   CALL      'VLEVE02'
     C                   PARM                    FIFIRM
     C                   PARM                    WSKH
S02  C*                  Z-ADD     11            LINNBR
S02  C*                  Z-ADD     22            POSNBR
N02  C                   Z-ADD     10            LINNBR
N02  C                   Z-ADD     43            POSNBR
     C                   GOTO      SLSOK
     C                   END
      *
      *
N02  C     LINNBR        IFEQ      10
N02  C     POSNBR        ANDGE     43
N02  C     POSNBR        ANDLE     45
N02  C                   CALL      'VLEVE02'
N02  C                   PARM                    FIFIRM
N02  C                   PARM                    WSKHI
N02  C                   Z-ADD     11            LINNBR
N02  C                   Z-ADD     22            POSNBR
N02  C                   GOTO      SLSOK
N02  C                   END
      *
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     22
     C     POSNBR        ANDLE     29
     C                   MOVE      *BLANKS       USOKKN
     C                   MOVE      WSKNR         UKN               8 0
     C                   CALL      'SOEKKUN'
     C                   PARM                    UKN
     C                   PARM                    FIFIRM
     C                   PARM                    USOKKN           30
     C     UKN           IFNE      0
     C                   MOVE      UKN           WSKNR
     C                   END
     C                   GOTO      SLSOK
     C                   END
      *
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     22
     C     POSNBR        ANDLE     24
     C                   MOVE      'J'           USOK              1
     C                   CALL      'V200SY'
     C                   PARM                    USOK
     C                   PARM                    WSVAL
     C                   GOTO      SLSOK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSOK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
n10  C     KeyLAND       KLIST
n10  C                   KFLD                    XLKUNI
n10  C                   KFLD                    WSLand
n10  c                   moveL     'LK'          xlkuni            3
     C     KODA1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSBB
     C     KHED1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    XRECTY
     C                   KFLD                    WSKH
     C                   MOVE      'F'           XRECTY            1
     C     KHED1KI       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    XRECTY
     C                   KFLD                    WSKHI
     C                   MOVE      'F'           XRECTY            1
     C     LEV1K         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSLNR
     C     LEVMAX        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    LEVNRM            8 0
     C                   Z-ADD     99999999      LEVNRM
     C     LEV1K2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    LEVNR
     C     LEV2K         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    W2NASO
N09  C     LEV2KN        KLIST
N09  C                   KFLD                    FIFIRM
N09  C                   KFLD                    W2NRSO
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNR
     C     VALUL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSVAL
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   99
      *
N07  C     FIFIRM        CHAIN     FIRMVIS                            33
n07  c  n33              MOVE      'J'           VISMA             1
N09  c                   move      'N'           sokno             1
     C                   ENDSR
      *
N07  C***********************************************
n07  C     VISMASR       BEGSR
N07  C***********************************************
n07  C     LEV1K2        CHAIN     visleve                            32
n07  C     *IN32         ifeq      '1'
N07  C                   WRITE     vislever
n07  C                   ELSE
n07  C     LEV1K2        CHAIN(N)  level01                            32
n07  C                   UPDATE    VISlEVER
N07  C                   END
N07  C                   ENDSR
