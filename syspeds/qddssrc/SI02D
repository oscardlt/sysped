     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/SIF)
     A                                      PRINT
      *
     A          R SI02DA                    SFL
     A            ZOP            1A  I  7  3VALUES(' ' '2' '4')
     A            IUSR      R        O  7  7REFFLD(IUSR)
     A            INA       R        O  7 19REFFLD(INA)
     A            IKN       R        O  7 51REFFLD(IKN)
     A                                      EDTCDE(4)
     A            IAVD      R        O  7 66REFFLD(IAVD)
     A                                      EDTCDE(4)
     A            ISG       R        O  7 72REFFLD(ISG)
      *
     A          R SI02DB                    SFLCTL(SI02DA)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Gå ut')
     A                                      CF05(05 'Frisk opp')
     A                                      CF06(06 'Lage Ny')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SI02D A'
     A                                      COLOR(BLU)
     A                                  1 25MSGCON(035 SSI0450 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 SSI0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  4MSGCON(025 SSI0460 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  4 69SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  5 51MSGCON(008 SSI0500 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 61MSGCON(009 SSI0470 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 72MSGCON(009 SSI0470 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(003 SSI0280 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  7MSGCON(010 SSI0480 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 19MSGCON(010 SSI0490 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 51MSGCON(008 SSI0510 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 62MSGCON(008 SSI0530 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 72MSGCON(008 SSI0520 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SI02DC
     A                                      OVERLAY
     A                                 21  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 22  2MSGCON(070 SSI0540 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SI02DD
     A                                      OVERLAY
     A                                      CF12(12 'Forrige')
     A            ZUSR      R        B 21  7REFFLD(IUSR)
     A N41                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A            ZNA       R        B 21 19REFFLD(INA)
     A                                      CHECK(LC)
     A            ZKN       R     D  B 21 51REFFLD(IKN)
     A  41                                  DSPATR(PC)
     A            ZAVD      R     D  B 21 66REFFLD(IAVD)
     A            ZSG       R        B 21 72REFFLD(ISG)
     A                                 22  2MSGCON(020 SSI0390 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 24'Selger/Kjøper S/K'
     A            ZSK       R        B 22 42REFFLD(ISK)
     A                                 22 52'Ursprungskode'
     A            ZEUR      R        B 22 68REFFLD(IEUR)
      *
     A          R SI02DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
