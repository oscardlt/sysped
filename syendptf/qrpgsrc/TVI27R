********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 ikke print ved emailkunde og nei i print
10XXX * 02 YBC PTF10 SJEKK STATUS VED SLETTING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 SJEKK STATUS VED SLETTING F23
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 04 YBC PTF12 Feil status ved dokumentkontroll
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL PLUKKE MELDINGER FOR Å SKRIVE UT       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 06 07 09 11 13-22 24    ***
      *** LEDIGE INDIKATORER 28 29 31 32 36-90 95-97          ***
      ***********************************************************
      *
N01  FCUNDC03   IF   E           K DISK
N01  FARKTXT    IF   E           K DISK
N01  FFIRM      IF   E           K DISK
     Fsysthlp   IF   E             DISK
     FTVINRMD   UF   E           K DISK
     FTVINRMD1  IF   E           K DISK
     F                                     RENAME(TVINRMDR:TVINRMR1)
     FTVINRMD2  UF   E           K DISK
     F                                     RENAME(TVINRMDR:TVINRMR2)
     FKODTSI    IF   E           K DISK
     FSADH      UF   E           K DISK
     FSTANDI    IF   E           K DISK
     FTVI27D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   Z-ADD     *ZEROS        YRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     TVRM1K        SETLL     TVINRMD1
     C     START1        TAG
     C     TVRM1K        READE     TVINRMD1                               94
     C  N94
     CAN 27TRMTY         IFNE      W2MTY
     C                   GOTO      START1
     C                   END
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   MOVE      *BLANKS       WSUTSK
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C     TVRM1K        READE     TVINRMD1                               94
     C  N94
     CAN 27TRMTY         IFNE      W2MTY
     C                   GOTO      ST21
     C                   END
     C  N94              END
     C                   END
     C     ZRECNO        IFGT      YRECNO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     c   01              do
     c                   movel     'tvi27r'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START3
     c                   end
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     c   01              do
     c                   movel     'tvi27r'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START3
     c                   end
     C                   END
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** SØK FRA
     C   08              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     12            POSNBR
     C                   GOTO      START6
     C                   END
     C*** SKRIV ALLE MELDINGER
     C   10
     CANN27              DO
     C                   Z-ADD     1             UFLAGG
     C                   EXSR      UTFM
     C                   EXSR      UTUA
     C                   EXSR      UTTK
     C                   GOTO      SLUTT
     C                   END
     C   10
     CAN 27              DO
     C                   Z-ADD     1             UFLAGG
     C     W2MTY         CASEQ     '1'           UTFM
     C     W2MTY         CASEQ     '2'           UTUA
     C     W2MTY         CASEQ     '3'           UTTK
     C                   END
     C                   GOTO      SLUTT
     C                   END
     C*** SLETT ALLE FEILMELDINGER
     C   23              DO
     C                   EXSR      SLFM
     C                   GOTO      SLUTT
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     c   01              do
     c                   movel     'tvi27r'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START6
     c                   end
     C     W2MTY         COMP      ' '                                2727
     C     KODSIK        CHAIN     KODTSI                             33
     C   33              MOVE      *BLANKS       KSINAV
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C   98              DO                                                      2<-B
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END                                                     2<-E
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0                                             2<-B
     C                   Z-ADD     15            LINNBR
     C                   ELSE                                                    2
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                     2<-E
      * Slett feilmelding
     C     WSNR          IFEQ      '4'                                           2<-B
     C     TRMTY         IFEQ      '1'                                            3<-B
     C     WSUTSK        ANDEQ     *BLANKS
     C                   EXSR      OPDFIL
     C                   SETON                                        99
     C                   ELSE                                                     3
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * Se på tolldeklarasjon
     C     WSNR          IFEQ      '5'                                           2<-B
     C                   SETON                                        99
     C                   CALL      'SAD005A'
     C                   PARM                    TRAVD
     C                   PARM                    TRTDN
     C                   PARM                    UFLAG2            1 0
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END                                                     2<-E
      * Skriv ut melding
     C     WSNR          IFEQ      '6'                                           2<-B
     C     WSUTSK        IFEQ      *BLANKS                                        3<-B
     C                   EXSR      OPDFIL
     C                   SETON                                        99
     C                   Z-ADD     1             UFLAGG
     C                   ELSE                                                     3
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * DIREKTE TIL TVINNLOGG
     C     WSNR          IFEQ      '7'                                           2<-B
     C                   MOVE      '0'           UFLAGT
     C                   SETON                                        99
     C                   CALL      'SAD003'
     C                   PARM                    TRAVD
     C                   PARM                    TRTDN
     C                   PARM                    UFLAGT            1
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     TVRMDK        CHAIN     TVINRMD                            33
     C  N33              MOVE      'P'           TRST
     C  N33              EXCEPT    NYSTRM
     C     TRAVD         CHAIN     STANDI                             33
     C                   MOVEL     SILV2         XIUATK            1
     C     SADHK         CHAIN     SADH                               33
N01  C                   EXSR      ARKIV
     C  N33WSNR          IFEQ      '4'                                          1<-B
     C     TRMTY         IFEQ      '1'                                           2<-B
N02  C     SIST          ANDEQ     '1'
N04  C                   SELECT
N04  C                   WHEN      SI0035 = '5'                                 VAREKONTROLL
N04  C                   MOVE      'V'           SIST
N04  C                   WHEN      SI0035 = '6'                                 DOK.KONTROLL
N04  C                   MOVE      'D'           SIST
N04  C                   WHEN      SI0035 = '9'                                 MANUELL BEHANDLING
N04  C                   MOVE      'T'           SIST
N04  C                   OTHER
     C                   MOVE      'M'           SIST
N04  C                   ENDSL
     C                   END                                                     2<-E
     C                   ELSE                                                   1
     C     TRMTY         IFEQ      '1'                                           2<-B
N02  C     SIST          ANDEQ     '1'
     C                   MOVE      'Å'           SIST
     C                   ELSE                                                    2
     C     TRMTY         IFEQ      '2'                                            3<-B
     C     XIUATK        IFEQ      'J'                                             4<-B
     C                   MOVE      'DUA'         SIMF
N01  C     CPRINT        IFEQ      'J'
     C                   MOVE      'K'           SIST
N01  C                   ELSE
N01  C                   MOVE      'P'           SIST
N01  C                   END
     C                   ELSE                                                      4
     C                   MOVE      'L'           SIST
     C                   END                                                       4<-E
     C                   ELSE                                                     3
N01  C     CPRINT        IFEQ      'J'
     C                   MOVE      'K'           SIST
N01  C                   ELSE
N01  C                   MOVE      'P'           SIST
N01  C                   END
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   MOVE      UWS           SIWS
     C                   END                                                    1<-E
     C  N33              EXCEPT    NYSTSA
      *
     C     WSNR          IFEQ      '4'
     C                   MOVEL     'SLETTET '    WSUTSK
     C                   MOVE      ' '           WSUTSK
     C                   ELSE
     C                   MOVEL     'UTSKREVE'    WSUTSK
     C                   MOVE      'T'           WSUTSK
     C                   END
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     UTFM          BEGSR
     C***********************************************
     C                   MOVE      '1'           YMTY
     C     TVRM2K        SETLL     TVINRMD2
     C                   SETOFF                                       33
     C     STUTFM        TAG
     C     TVRM2K        READE     TVINRMD2                               33
     C  N33              DO
     C                   MOVE      'P'           TRST
     C                   EXCEPT    NYSTR2
     C     SADHK         CHAIN     SADH                               34
     C  N34              DO
     C                   MOVE      'Å'           SIST
     C                   MOVE      UWS           SIWS
     C                   EXCEPT    NYSTSA
     C                   END
     C                   GOTO      STUTFM
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     UTUA          BEGSR
     C***********************************************
     C                   MOVE      '2'           YMTY
     C     TVRM2K        SETLL     TVINRMD2
     C                   SETOFF                                       33
     C     STUTUA        TAG
     C     TVRM2K        READE     TVINRMD2                               33
     C  N33              DO
     C                   MOVE      'P'           TRST
     C                   EXCEPT    NYSTR2
     C     TRAVD         CHAIN     STANDI                             34
     C                   MOVEL     SILV2         XIUATK            1
     C     SADHK         CHAIN     SADH                               34
N01  C                   EXSR      ARKIV
     C  N34              DO
     C     XIUATK        IFEQ      'J'
     C                   MOVE      'DUA'         SIMF
N01  C     CPRINT        ifeq      'J'
     C                   MOVE      'K'           SIST
N01  C                   ELSE
N01  C                   MOVE      'P'           SIST
N01  C                   END
     C                   ELSE
     C                   MOVE      'L'           SIST
     C                   END
     C                   MOVE      UWS           SIWS
     C                   EXCEPT    NYSTSA
     C                   END
     C                   GOTO      STUTUA
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     UTTK          BEGSR
     C***********************************************
     C                   MOVE      '3'           YMTY
     C     TVRM2K        SETLL     TVINRMD2
     C                   SETOFF                                       33
     C     STUTTK        TAG
     C     TVRM2K        READE     TVINRMD2                               33
     C  N33              DO
     C                   MOVE      'P'           TRST
     C                   EXCEPT    NYSTR2
     C     SADHK         CHAIN     SADH                               34
N01  C                   EXSR      ARKIV
     C  N34              DO
N01  C     CPRINT        ifeq      'J'
     C                   MOVE      'K'           SIST
N01  C                   ELSE
N01  C                   MOVE      'P'           SIST
N01  C                   END
     C                   MOVE      UWS           SIWS
     C                   EXCEPT    NYSTSA
     C                   END
     C                   GOTO      STUTTK
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SLFM          BEGSR
     C***********************************************
     C                   EXFMT     VARESL
     C   23              DO                                                     1<-B
     C                   MOVE      '1'           YMTY
     C     TVRM2K        SETLL     TVINRMD2
     C                   SETOFF                                       33
     C     STSLFM        TAG
     C     TVRM2K        READE     TVINRMD2                               33
     C  N33              DO
     C                   MOVE      'P'           TRST
     C                   EXCEPT    NYSTR2
     C     SADHK         CHAIN     SADH                               34
     C  N34              DO
N03  C                   IF        SIST = '1'
     C                   MOVE      'M'           SIST
N03  C                   END
     C                   MOVE      *BLANKS       SIWS
     C                   EXCEPT    NYSTSA
     C                   END
     C                   GOTO      STSLFM
     C                   END
     C                   END                                                    1<-E
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TVRMDK        KLIST
     C                   KFLD                    TRST
     C                   KFLD                    TRAVD
     C                   KFLD                    TRTDN
     C                   KFLD                    TR068A
     C                   KFLD                    TR068B
     C                   KFLD                    TR068C
     C     TVRM1K        KLIST
     C                   KFLD                    YST               1
     C                   KFLD                    W2SG
     C     TVRM2K        KLIST
     C                   KFLD                    YST               1
     C                   KFLD                    W2SG
     C                   KFLD                    YMTY              1
     C     KODSIK        KLIST
     C                   KFLD                    YSIUNI            2
     C                   KFLD                    W2SG
     C     SADHK         KLIST
     C                   KFLD                    TRAVD
     C                   KFLD                    TRTDN
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UWS              10
     C*
     C                   MOVE      'SI'          YSIUNI
     C                   WRITE     NYVARE
     C                   EXFMT     VAREL2
     C     W2MTY         COMP      ' '                                2727
     C     KODSIK        CHAIN     KODTSI                             33
N01  C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C***********************************************
     C     HELPSR        BEGSR
     C***********************************************
     c
     c                   move      *blanks       pcocmd           70
     c     1             chain     systhlp                            33
     c     *in(33)       ifeq      '0'
      *
      * intern
     c                   movel     'http://'     pcocmd
     C     pcocmd        CAT       systip:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       systba:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       hlpna:0       pcocmd
     C     pcocmd        CAT       '.htm':0      pcocmd
      *
     c                   call      'SYGE34CL'
     c                   parm                    pcocmd
     c                   end
     C                   ENDSR
     C*
      *
N01  C****************************************************************
N01  C     ARKIV         BEGSR
N01  C****************************************************************
N01  C     EPOKEY        KLIST
N01  C                   KFLD                    FIFIRM
N01  C                   KFLD                    mail30
N01  C                   KFLD                    SIKNK
N01  C                   MOVE      'J'           CPRINT            1
N01  C                   MOVE      'si'          ARTYPE
N01  C     ARTYPE        CHAIN     ARKTXT                             35
N01  C     *IN(35)       IFEQ      '0'
N01  C     arkjn         andeq     'J'
N01  C     SIKNK         andne     0
N01   *
N01   * SKJEKKER OM DET ER MAILKUNDE SOM ER MOTTAKER
N01   *
N01  C                   MOVE      ARTXT         MAIL16           16
N01  C                   MOVEL     '*'           MAIL16
N01  c                   movel     MAIL16        mail30           30
N01  c     epokey        CHAIN     cundc03                            35
N01  C                   END
N01  C                   ENDSR
      *
     OTVINRMDR  E            NYSTRM
     O                       TRST
     OTVINRMR2  E            NYSTR2
     O                       TRST
     OSADHR     E            NYSTSA
     O                       SIST
     O                       SIWS
