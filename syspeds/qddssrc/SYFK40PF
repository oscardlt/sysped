     A*****************************************************************
     A*   KJØREKVITTERING/OPPDRAGSFORMULAR                            *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=80, OVERFLOW=60
     A*****************************************************************
N01N03*  NYE FELTER
     A          R KJØPHH1
     A            KFMT1         75A     1  1
     A          R KJØPHH2
     A            KFMT2         75A     1  1
     A          R KJØPNYS                   SPACEB(2)
     A                                    50MSGCON(10 YBC0492 SYMSGF)
     A          R KJØP
     A            HOHT1         80A     1  1HIGHLIGHT
     A            HOHT2         80A     2  1HIGHLIGHT
     A            HOHT3         80A     3  1HIGHLIGHT
     A            HOHT4         80A     4  1HIGHLIGHT
     A            HOHT5         80A     5  1HIGHLIGHT
     A            HOHT6         80A     6  1HIGHLIGHT
     A            HOHT7         80A     7  1HIGHLIGHT
     A                                  9  2MSGCON(9 DIV0197 SYMSGF)
     A  26                              9 68'SIDE:'
     A  26        XSIDE          3  0   9 74
     A N77        KNAVN         30A    11 11
     A  77        HENAK         30A    11 11
     A                                 11 54'************************'
     A                                      HIGHLIGHT
     A N77        ADR1          30A    12 11
     A  77        HEADK1        30A    12 11
     A                                 12 54MSGCON(24 DIV0198 SYMSGF)
     A                                      HIGHLIGHT
     A N77        ADR2          30A    13 11
     A  77        HEADK2        30A    13 11
     A N99                             13 54'************************'
     A                                      HIGHLIGHT
     A  99                             13 54MSGCON(24 DIV2969 SYMSGF)
     A                                      HIGHLIGHT
     A N77        POSTNR         4  0  14 11
     A N77        ADR3          24A    14 17
     A  77        HEADK3        30A    14 11
     A  88                             15 11MSGCON(10 DIV3559 SYMSGF)
     A  88        TLF           15A    15 22
     A                                 15 54MSGCON(12 DIV0199 SYMSGF)
     A            HEKNK          8  0  15 70EDTCDE(3) HIGHLIGHT
     A                                 16 54MSGCON(5 DIV0062 SYMSGF)
     A                                 16 70DATE EDTCDE(Y) HIGHLIGHT
     A                                 17  2MSGCON(9 DIV0200 SYMSGF)
     A            HENAS         30A    17 12
     A                                 17 54MSGCON(15 DIV0203 SYMSGF)
SV5  A*           HEDT           6  0  17 70HIGHLIGHT
NV5  A            XEDTOP         6  0  17 70HIGHLIGHT
     A                                      EDTCDE(Y)
N03  A            HEADS1        30A    18 12
S03  A*                                18 52MSGCON(9 DIV0201 SYMSGF)
N03  A                                 18 54MSGCON(7 DIV0201 SYMSGF)
     A            HEAVD          4  0  18 62HIGHLIGHT
     A                                 18 66'/'
     A            HEOPD          7  0  18 67HIGHLIGHT
     A                                 18 74'/'
     A            HESG           3A    18 75HIGHLIGHT
N03  A            HEADS2        30A    19 12
N03  A            HEADS3        30A    20 12
N03   * RESTEN FLYTTES 1 NEDOVER
     A                                 21  2MSGCON(9 DIV0202 SYMSGF)
     A            HESDL         20A    21 12HIGHLIGHT
     A                                 21 54MSGCON(15 DIV0205 SYMSGF)
     A            HEPRO          8  0  21 70HIGHLIGHT
     A                                 22  2MSGCON(9 DIV0204 SYMSGF)
     A*           HERFA         15A    22 12HIGHLIGHT
     A            HERFA15       15A    22 12HIGHLIGHT
     A                                 22 54MSGCON(5 DIV0171 SYMSGF)
     A                                 23  2MSGCON(9 DIV0206 SYMSGF)
     A            HEHAWB         9     23 12HIGHLIGHT
     A            HETRI          2A    23 24HIGHLIGHT
     A                                 23 30MSGCON(7 DIV0207 SYMSGF)
     A            HETRM          2  0  23 39EDTCDE(4) HIGHLIGHT
     A            HESDFF         5A    23 55HIGHLIGHT
     A            HESDF          5A    23 61HIGHLIGHT
     A                                 23 66'-'
     A            HESDT          5A    23 67HIGHLIGHT
     A            HESDVT         5A    23 73HIGHLIGHT
     A                                 24  2MSGCON(9 DIV0208 SYMSGF)
     A            HEFR           3A    24 12HIGHLIGHT
     A                                 24 16'/'
     A            HEOT           2A    24 18HIGHLIGHT
     A                                 24 54MSGCON(13 DIV0209 SYMSGF)
     A            HEGN          15A    24 64HIGHLIGHT
     A                                 25  2MSGCON(13 DIV0210 SYMSGF)
     A            HETRC          1  0  25 15HIGHLIGHT
     A            HETRCN        17A    25 18HIGHLIGHT
     A                                 25 54MSGCON(7 DIV0211 SYMSGF)
     A            HEPOS1         7A    25 67HIGHLIGHT
     A            HEPOS2         4  0  25 74EDTCDE(4) HIGHLIGHT
     A                                 26  2MSGCON(24 DIV0212 SYMSGF)
     A N87                                  UNDERLINE
     A                                 26 26MSGCON(24 DIV0213 SYMSGF)
     A N87                                  UNDERLINE
     A                                 26 50MSGCON(24 DIV0214 SYMSGF)
     A N87                                  UNDERLINE
     A                                 26 73'________'
     A            HENT           7  0  27  1EDTCDE(4) HIGHLIGHT
     A            HEVS1         25A    27  9HIGHLIGHT
     A            HEVKT          9  0  27 36EDTCDE(4) HIGHLIGHT
     A            HEM3           7  3  27 46EDTCDE(4) HIGHLIGHT
     A            HELM           4  2  27 56EDTCDE(4) HIGHLIGHT
     A            HEGM1         15A    27 62HIGHLIGHT
     A          R KJØP1                     SPACEB(1)
     A  27        HEVS2         25A        9HIGHLIGHT
     A  27        HEGM2         15A       62HIGHLIGHT
     A          R KJØP2                     SPACEB(1)
     A                                     1' _______________________'
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'_______'
     A                                      SPACEA(2)
     A          R KJØPFS
     A  28        FRISTX        30         2
     A  28                                32':'
     A            FS1           21        35
     A            FS2           21        57SPACEA(1)
     A          R KJØPUFB
     A                                     2MSGCON(24 YBC0491 SYMSGF)
     A                                      HIGHLIGHT
     A                                    32':'
     A                                      HIGHLIGHT
     A            HEFT1A        30        35HIGHLIGHT
     A            HEFT1B        15        66HIGHLIGHT
     A                                      SPACEA(1)
     A          R KJØP3
     A                                     2MSGCON(24 DIV0215 SYMSGF)
     A                                      SPACEA(1)
     A          R KJØP4
     A            FRTTXT        70         2SPACEA(1)
     A                                     1' '
     A          R KJØP5
     A                                      SPACEB(2)
     A                                     1MSGCON(24 DIV0216 SYMSGF)
     A                                      SPACEB(1)
     A            HENAK         30A       11SPACEB(1)
     A            HEADK1        30A       11SPACEB(1)
     A            HEADK2        30A       11SPACEB(1)
     A            HEADK3        30A       11SPACEB(1)
     A          R DLINJE                    SPACEB(2)
     A                                     1' _______________________'
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'_______'
     A          R DEL                       SPACEB(1)
     A            FAVT          20A       11
     A            FABELN        13  2     43EDTCDE(3)
     A          R KJØPB                     SPACEB(3) HIGHLIGHT
     A                                    17MSGCON(64 DIV0217 SYMSGF)
N01  A  55                                 1' '
N01  A                                      SPACEB(4)
N01  A  55        SKANBAR       13        30BARCODE(CODE128 4 *HRITOP *HRZ +
N01  A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
