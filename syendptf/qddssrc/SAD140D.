      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD140R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold ekspressfort. ekstern -
     A                                      oppdrag'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  4 13MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 26TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 32TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  7 20MSGCON(035 DIV1901 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 20'Oppdrag:'
     A            WSTDN          7Y 0B  8 35TEXT('TURNUMMER')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9 20'Bilnr:'
     A            WSTRID        17A  B  9 35TEXT('Registreringsdato')
     A                                      CHECK(LC)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 20'Eksp.enh:'
     A            WS3039E        6Y 0B 10 35TEXT('TURNUMMER')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 26 20'F3=Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            CLAVD          4Y 0O  6  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A            CLPRO          7Y 0O  6 10TEXT('Oppdragsnr')
     A                                      EDTCDE(4)
     A  41                                  COLOR(TRQ)
     A            CLTRID        17A  O  6 18TEXT('Bilnr')
     A  42                                  COLOR(TRQ)
     A            CL3039E        6Y 0O  6 36TEXT('Ekspedisjonsted')
     A  43                                  COLOR(TRQ)
     A                                      EDTCDE(4)
     A            CLNTK          7Y 0O  6 43TEXT('Kolli')
     A                                      EDTCDE(4)
     A            CLVKB          9Y 0O  6 51TEXT('Vekt')
     A                                      EDTCDE(4)
     A            CLSDF          5A  O  6 61TEXT('Fra sted')
     A            CLSDT          5A  O  6 67TEXT('Til sted')
     A            CLPR           2A  O  6 73TEXT('Prosedyre')
     A            CLEID         18A  O  6 76TEXT('Eksport ID')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD140R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold ekspressfort. ekstern -
     A                                      oppdrag'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Lage 2=Endre 3=Kopiere 5=-
     A                                      Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag Bilnr             -
     A                                      E.enh.   Kolli      Vekt Fra/til-ST-
     A                                      D Pr Eksport Id'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRID        17A  B  5 18DSPATR(UL)
     A            W23039E        6Y 0B  5 36DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R BUNN
     A                                 26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD110R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold ekspressfort. ekstern -
     A                                      oppdrag'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Lage 2=Endre 3=Kopiere 5=-
     A                                      Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag Bilnr             -
     A                                      E.enh.   Kolli      Vekt Fra/til-ST-
     A                                      D Pr Eksport Id'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRID        17A  B  5 18DSPATR(UL)
     A            W23039E        6Y 0B  5 36DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
