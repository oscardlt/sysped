      *  PDF/AFP (*AFPDS) BARCODE DIREKTE I DDS
      *  PRINT HUMAN READABLE DOBBELT OPP VED AFP - ENKELTE PRINTERE TAKLER IKKE
      * http://publib.boulder.ibm.com/iseries/v5r2/ic2924/books/c415713531.htm
     A          R LABEL
     A                                  3  5'Fra/from'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A                                  3 20'Print date'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            WDATO          8  0   3 27EDTWRD('  /  /    ')
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            WTID4          4  0   3 35EDTWRD('  :  ')
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            EHLNAV        30A     4  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            EHLAD1        30A     5  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            EHLAD2        30A     6  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            EHLAD3        30A     7  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            EHLAD4        30A     8  9FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                  9  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                 10  5'Til/to'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            VADRNA        30A    12  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            VADRN1        30A    14  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            VADRN2        30A    16  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            VADRN3        30A    18  9FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                 19  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                 20  5'PO-nummer:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            EHBES         10A    21  7FNTCHRSET(C0S0OB10 T1D0BASE)
     A                                 22  5'Supl.ref:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            EHLREF        30A    23  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 24  5'Art.No.:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ELART         35A    25  7FNTCHRSET(C0S0OB10 T1D0BASE)
     A                                 26  5'Supl.A.No.:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ELARTL        35A    27  7FNTCHRSET(C0S0OB10 T1D0BASE)
     A                                 28  5'Description:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ELABES_30     30A    29  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ELABES_20     20A    31  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 32  5'Reference:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ELCONT        35A    33  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 35  5'Weight:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            XVEKT         11  3  36  7EDTCDE(3)
     A                                      FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 38  5'Volumn:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            XM3           11  3  39  7EDTCDE(3)
     A                                      FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 41  5'Stykk:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            KLLTXT        11A    41 14FNTCHRSET(C0S0OB10 T1D0BASE)
     A                                 42  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A  28                             44  5'EAN:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A  28        KOID14        14     45 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
     A  28        ELARTE        14A    51 12
      *
     A            VADRNA2       30A    45  2FNTCHRSET(C0C075H0 T1D0BASE)
      *
      *
     A                                 55  5'Kolli-ID:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            KOID21        21     56 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
     A            HRI_KOID      25A    62 12
