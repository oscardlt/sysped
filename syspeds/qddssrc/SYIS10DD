     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '5' '4' '3' '2')
     A            ISLPFK         5A  O  6  4
     A            ISLANS         5A  O  6 10
     A            ISLAV101      54A  O  6 16
     A            W2LRDA         6Y 0O  6 71EDTCDE(Y)
     A            ISLLNR         3Y 0H
     A            ISLAV1        75A  H
     A            ISLAV2        75A  H
     A            ISLAV3        75A  H
     A            ISLRDA         8Y 0H
     A            ISLRTM         6Y 0H
     A            ISLRDS         8Y 0H
     A            ISLRTS         6Y 0H
     A            ATEXT         35A  H
     A            PTEXT         35A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYIS10D'
     A                                      COLOR(BLU)
     A                                  1 23'Arbeid med tilleggsinfo'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2'Saksnr.  :'
     A            USAKNR         7Y 0O  2 13EDTCDE(4)
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A N51                              3  2'Velg opsjon,   2=Endre   3=Kopier -
     A                                        4=Slett   5=Vis'
     A                                      COLOR(BLU)
     A  51                              3  2'Velg opsjon,   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  1'Op Parts Ans.  Årsak              -
     A                                                                         -
     A                                       Reg.dato'
     A                                      COLOR(WHT)
     A                                  5  1'   forkl part                     -
     A                                                                         -
     A                                               '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  5  2'F3/F12=Avslutt   F5=Frisk   F6=Lag-
     A                                       ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NULLPS
     A                                      CF03(03 'Avslutt')
     A                                      CF06(06)
     A                                      CF12(12)
     A                                  1  2'SYSPED/SYIS10D'
     A                                      COLOR(BLU)
     A                                  1 23'Arbeid med tilleggsinfo'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2'Saksnr.  :'
     A            USAKNR         7Y 0O  2 13EDTCDE(4)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  4  1'Op Parts Ans.  Årsak              -
     A                                                                         -
     A                                       Reg.dato'
     A                                      COLOR(WHT)
     A                                  5  1'   forkl part                     -
     A                                                                         -
     A                                               '
     A                                      COLOR(WHT)
     A                                  7  1'   Ingen poster finnes.'
     A                                      COLOR(BLU)
     A                                 23  2'F3/F12=Avslutt   F5=Frisk   F6=Lag-
     A                                       ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ISLLNR         3S 0H
     A                                  1  2'Partsforkl:'
     A            WSLPFK         5A  B  1 14DSPATR(UL)
     A  89                                  COLOR(WHT)
     A            FTEXT         35A  H
     A            FTEXT20       20A  O  1 20
     A                                  1 42'Ans. part:'
     A            WSLANS         5A  B  1 53DSPATR(UL)
     A  88                                  COLOR(WHT)
     A            PTEXT         35A  H
     A            PTEXT20       20A  O  1 59
     A            WSLAV1        75A  B  2  5DSPATR(UL)
     A  87                                  COLOR(WHT)
     A            WSLAV2        75A  B  3  5DSPATR(UL)
     A            WSLAV3        75A  B  4  5DSPATR(UL)
     A                                  5  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ISLLNR         3S 0H
     A                                  1  2'Partsforkl:'
     A            ISLPFK         5A  O  1 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            FTEXT         35A  H
     A            FTEXT20       20A  O  1 20COLOR(WHT)
     A                                  1 42'Ans. part:'
     A            ISLANS         5A  O  1 53DSPATR(UL)
     A                                      COLOR(WHT)
     A            PTEXT         35A  H
     A            PTEXT20       20A  O  1 59COLOR(WHT)
     A            ISLAV1        75A  O  2  5DSPATR(UL)
     A                                      COLOR(WHT)
     A            ISLAV2        75A  O  3  5DSPATR(UL)
     A                                      COLOR(WHT)
     A            ISLAV3        75A  O  4  5DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A                                  5 40'Reg.dato/tid:'
     A            W2LRDA         6Y 0O  5 55DSPATR(UL)
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A            ISLRTM         6Y 0O  5 64DSPATR(UL)
     A                                      EDTWRD('  :  :  ')
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
