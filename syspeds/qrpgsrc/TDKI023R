      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKI023R MODULE(*LIBL/TDKI023R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D DKIH_12         S             15S 0
     D DKIH_12E        S             15S 0
     D DKIV_42         S             15S 0
     D DKIH_201        S              3
     D DKIH_222        S             15S 0
     D DKIV_T17A       S             15S 2
     D DKIV_T19        S             15S 2
     D A16             S             16
     D JSONstring      S          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"t17at19calc" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta detta ena ærendet.
      *
     c     Error         ifeq      *blanks
     C                   EXSR      AVG
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬dkiv_t17a¬ : ¬'+%trim(%editc(DKIV_T17A:'4'))+'¬, '
          +'¬dkiv_t19¬ : ¬'+%trim(%editc(DKIV_T19:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER     =  zhbgetvar('USER')
     C                   EVAL      A16        =  zhbgetvar('DKIH_12')
     C                   EVAL      DKIH_12    =  c2n2(A16)
     C                   EVAL      A16        =  zhbgetvar('DKIH_12E')
     C                   EVAL      DKIH_12E   =  c2n2(A16)
     C                   EVAL      A16        =  zhbgetvar('DKIV_42')
     C                   EVAL      DKIV_42    =  c2n2(A16)
     C                   EVAL      A16        =  zhbgetvar('DKIH_222')
     C                   EVAL      DKIH_222   =  c2n2(A16)
     C                   EVAL      DKIH_201   =  zhbgetvar('DKIH_201')
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF                                50
     C     *IN50         CABEQ     *ON           OPENTAG
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     OPENTAG       TAG
     C   50              eval      Error = 'Invalid userID'

     c                   endsr
      *
      *_________________________________________________
      * Avgiftsberækning                             AVG
      *"""""""""""""""""""""""""""""""""""""""""""""""""
     C     AVG           BEGSR
      * Regn ut faktor for T17A og T19A
     C                   IF        DKIH_222 > 0
     C     DKIV_42       DIV       DKIH_222      ZFAKTOR          18 8
     C                   ENDIF
      * Rækna ut T17A
     C     DKIH_12E      MULT      ZFAKTOR       DKIV_T17A
      * Rækna ut T19
     C     DKIH_201      IFNE      'DDU'
     C     DKIH_201      IFNE      'DDP'
     C     DKIH_12       MULT      ZFAKTOR       DKIV_T19
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
