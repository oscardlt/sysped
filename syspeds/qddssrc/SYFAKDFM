     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/FAK3)
     A                                      PRINT
     A                                      CF03(03 'Exit')
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CF12(12)
     A N50
     AO 49                                  CF06(06 'LAG NY')
     A N50N51                               CF07(07 'SE TYPER')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFAKD'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0469 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A            XXTYP         10A  O  2  9
     A            XXTXT         50A  O  2 20COLOR(WHT)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  50N49                           4  2MSGCON(018 JOV0474 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50
     AO 49                              4  2MSGCON(030 JOV0475 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(078 JOV0471 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            WSKOD     R        B  6  5REFFLD(KOFASTR/KFKOD *LIBL/KOFAST)
     A            WSTYP     R        B  6 66REFFLD(KOFASTR/KFTYP *LIBL/KOFAST)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                 23  2MSGCON(010 DIV2056 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50
     AO 49                             23 17MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50N51                          23 61MSGCON(018 JOV0479 *LIBL/SYMSGF)
     A    N51                          23 61MSGCON(018 JOV0479 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            WSNR           1A  B  7  2VALUES(' ' '2' '4' '1')
     A            KFKOD     R        O  7  5REFFLD(KOFASTR/KFKOD *LIBL/KOFAST)
     A            KFTXT     R        O  7 11REFFLD(KOFASTR/KFTXT *LIBL/KOFAST)
     A            KFTYP     R        O  7 66REFFLD(KOFASTR/KFTYP *LIBL/KOFAST)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A N50
     AO 49                                  CF06(06 'LAG NY')
     A N50N51                               CF07(07 'SE TYPER')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFAKD'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0469 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A            XXTYP         10A  O  2  9COLOR(WHT)
     A            XXTXT         50A  O  2 20COLOR(WHT)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 68SFLRCDNBR
     A                                      DSPATR(ND)
     A  50N49                           4  2MSGCON(018 JOV0474 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50
     AO 49                              4  2MSGCON(030 JOV0475 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(078 JOV0471 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            WSKOD     R        B  6  5REFFLD(KOFASTR/KFKOD *LIBL/KOFAST)
     A            WSTYP     R        B  6 66REFFLD(KOFASTR/KFTYP *LIBL/KOFAST)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A          R BOTREC
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50
     AO 49                             23 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  50
     AO 49                             23 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50N51                          23 61MSGCON(018 JOV0479 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
     A          R CHGREC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Annuller')
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                 21  5MSGCON(070 JOV0472 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KFTYP     R        O 22  5REFFLD(KOFASTR/KFTYP *LIBL/KOFAST)
     A                                      COLOR(WHT)
     A            KFKOD     R        O 22 16REFFLD(KOFASTR/KFKOD *LIBL/KOFAST)
     A                                      COLOR(WHT)
     A            W2TXT     R        B 22 22REFFLD(KOFASTR/KFTXT *LIBL/KOFAST)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A            W2TXTE    R        B 23 22REFFLD(KOFASTR/KFTXT *LIBL/KOFAST)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
     A          R NEWREC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Annuller')
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                 21  5MSGCON(070 JOV0472 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2TYP     R        B 22  5REFFLD(KOFASTR/KFTYP *LIBL/KOFAST)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A            W2KOD     R        B 22 16REFFLD(KOFASTR/KFKOD *LIBL/KOFAST)
     A            W2TXT     R        B 22 22REFFLD(KOFASTR/KFTXT *LIBL/KOFAST)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A            W2TXTE    R        B 23 22REFFLD(KOFASTR/KFTXT *LIBL/KOFAST)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
