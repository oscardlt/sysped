      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESPNRAVD MODULE(*LIBL/ESPNRAVD *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FNOPNR     IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D TER             s              4
     D TERN            s              4  0
     D PNR             s              4
     D PNRN            s              4  0
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Send section
     C                   callp     wrtsection('all')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
     C                   eval      user = zhbgetvar('user')
     C                   eval      TER     = zhbgetvar('TER')
     C                   eval      TERN    = c2n2(TER)
     C                   eval      PNR     = zhbgetvar('PNR')
     C                   eval      PNRN    = c2n2(PNR)
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     NOPNR
     C                   CLOSE     KODTA
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESPNRAVD':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$all
      *=====================================================================
     C     SetAll        begsr
      *
     c     *IN50         ifeq      *ON
     c                   eval      KOANVN = '#ERROR# Ukjent User !'
     c                   else
     C     InnlKey       CHAIN     NOPNR                              33
     C     KODTAK        CHAIN     KODTA                              33
     c   33              eval      KOANVN = '#ERROR# Ukjent terminal/postnr !'
     c                   end
     C* klargjøre HTMLvariable
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(KOAAVD:'Z')))
     C                   callp     updHTMLvar('NAVN':KOANVN)
      *
     C                   callp     wrtsection('*all')
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      NOPNR
     C                   OPEN      KODTA
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    NOAVDU
     C                   MOVE      'A'           KOAUNI
     C     InnlKey       KLIST
     C                   KFLD                    NOTERM
     C                   KFLD                    PNRN
      *
     C                   MOVE      1000          NOTERM
      * terminal oppgitt som parameter..
     c     TERN          ifne      *zeros
     C                   MOVE      TERN          NOTERM
     c                   end
      *
     C                   endsr
