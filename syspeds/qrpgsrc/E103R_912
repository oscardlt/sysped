TMP   *ERRF912 ER IKKE KLARGJORT I KATALOGEN - AVVENTER....
     H DATEDIT(*YMD)
     FEDIWS     UF A E           K DISK
     FEDIM      IF   E           K DISK
     FEFTX912   IF   E           K DISK
     FEGDS912   IF   E           K DISK
     FEGID912   IF   E           K DISK
     FELOC912   IF   E           K DISK
     FEMEA912   IF   E           K DISK
     FENAD912   IF   E           K DISK
     FEPCI912   IF   E           K DISK
     FETDT912   IF   E           K DISK
TMP  F*RFF912 IF  E           K        DISK
TMP  F*RISOK  O   E                    DISK                      A
     FSTED2     IF A E           K DISK
     FKUNKO2    IF   E           K DISK
     FHEADF     O  A E             DISK
     FTELL      UF   E           K DISK
     FHEDUMMY   IF   E           K DISK
     F                                     RENAME(HEADFR:DUMMY)
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D                 DS
     D  HEDTR                  1      8  0
     D  HEDTRC                 1      2  0
     D  HEDTRÅ                 3      4  0
     D  HEDTRM                 5      6  0
     D  HEDTRD                 7      8  0
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  M1004                  1     35
     D  W1004A                 1      4
     D  W1004B                 5      7
     D  W1004C                 8     10
     D                 DS
     D  N3164                  1     35
     D  N3164A                 1      2
     D  N3164B                 3     35
      *________________________
      * DS for break-handling
      *""""""""""""""""""""""""
     D                 DS
     D  SWRKDS                 1    256
     D  TURNR                  1      8  0
     D  POSNR                  9     11  0
     D  AVSEND                12     41
     D  AVSAD3                42     71
      * FRANKATUR-TEKSTER
     D DAFTX1          C                   CONST('FREE BELGIAN BORDER')
      *
     D DDUTX1          C                   CONST('FREE DELIVERED, NOT -
     D                                     CLEARED')
      *
     D DDPTX1          C                   CONST('FREE DELIVERED, CLEA-
     D                                     RED')
      *
     D EXWTX1          C                   CONST('EX WORKS')
     D FOTTX1          C                   CONST('FREE ON TRUCK')
      *
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * FTX10                                               FTX10 /
      *////////////////////////////////////////////////////////////
     C     FTX10         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     10            ZGN1
     C     KEYMS1        SETLL     EFTX912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX912                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'MKS'
     C     HEGM2         IFEQ      *BLANK
     C                   MOVEL     F4440A        HEGM2
     C                   ENDIF
     C                   ENDIF
      *
     C     F4451         IFEQ      'AAZ'
     C     HEFR          ANDEQ     *BLANK
     C                   EXSR      FRANKA
     C                   ENDIF
      *
     C  N52KEYMS1        READE     EFTX912                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FRANKA                                              FRANKA/
      *////////////////////////////////////////////////////////////
     C     FRANKA        BEGSR
      *
      * FINN FRANKATUR-KODE UTFRA TXT
     C                   SELECT
     C     F4440A        WHENEQ    DAFTX1
     C                   MOVE      'DAF'         HEFR
     C     F4440A        WHENEQ    DDUTX1
     C*          F4440A    OREQ DDUTX2
     C                   MOVE      'DDU'         HEFR
     C     F4440A        WHENEQ    DDPTX1
     C*          F4440A    OREQ DDPTX2
     C                   MOVE      'DDP'         HEFR
     C     F4440A        WHENEQ    EXWTX1
     C                   MOVE      'EXW'         HEFR
     C     F4440A        WHENEQ    FOTTX1
     C                   MOVE      'FOT'         HEFR
     C                   ENDSL
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX12                                               FTX12 /
      *////////////////////////////////////////////////////////////
     C     FTX12         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     12            ZGN2
     C     KEYMS2        SETLL     EFTX912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EFTX912                                53
     C     *IN53         DOWEQ     '0'
      *
     C     F4451         IFEQ      'AAA'
     C                   ENDIF
      *
     C  N53KEYMS2        READE     EFTX912                                53
     C  N53              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GDS12                                               GDS12 /
      *////////////////////////////////////////////////////////////
     C     GDS12         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     12            ZGN1
     C     KEYMS1        SETLL     EGDS912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EGDS912                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C                   EXSR      FTX12
      *
     C  N52KEYMS1        READE     EGDS912                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GID10                                               GID10 /
      *////////////////////////////////////////////////////////////
     C     GID10         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     EGID912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EGID912                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          HENT
     C                   MOVEL     G7065A        HEVS1
      *
     C                   Z-ADD     GREP0         ZREPB0
     C                   EXSR      PCI10
     C                   EXSR      FTX10
     C                   EXSR      GDS12
     C                   EXSR      MEA13
      *
     C     KEYMS0        READE     EGID912                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KUNKOK        KLIST
     C                   KFLD                    KUKUN1
     C                   KFLD                    KUINTN
     C                   Z-ADD     51192         KUKUN1
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C                   MOVE      '0'           ZVS               1
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
      * Workfile for å sjekke brudd mm. - Hent sist behandlede...
     C                   MOVE      *ZEROS        TURNR
     C                   MOVE      *ZEROS        POSNR
     C                   MOVE      MSN           SSN
     C     SSN           CHAIN     EDIWS                              51
     C   51              MOVE      'J'           NYSSN             1
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LOC05                                               LOC05 /
      *////////////////////////////////////////////////////////////
     C     LOC05         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     5             ZGN1
     C     KEYMS1        SETLL     ELOC912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC912                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEL     L3225A        HELKA
     C                   MOVEL     L3225A        HESDF
     C                   MOVEL     L3225B        HESDT
      * TEST / EVT OPPRETT STEDSKODER
     C                   CLEAR                   STED2R
     C                   MOVE      HESDF         ST2KOD
     C                   MOVE      *BLANKS       ST2NVN
     C                   MOVEL     L3224A        ST2NVN
     C                   EXSR      TSTSTE
     C                   CLEAR                   STED2R
     C                   MOVE      HESDT         ST2KOD
     C                   MOVE      *BLANKS       ST2NVN
     C                   MOVEL     L3224B        ST2NVN
     C                   EXSR      TSTSTE
      *
     C  N52KEYMS1        READE     ELOC912                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA13                                               MEA13 /
      *////////////////////////////////////////////////////////////
     C     MEA13         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     13            ZGN1
     C     KEYMS1        SETLL     EMEA912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA912                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C*                    ADD  ZNUM      HENT
     C* TNE = TONNS ....
     C     M6411         IFEQ      'TNE'
     C                   MULT      1000          ZNUM
     C                   ADD       ZNUM          HEVKT
     C                   END
     C     M6411         IFEQ      'MTQ'
     C                   ADD       ZNUM          HEM3
     C                   END
      *
     C  N52KEYMS1        READE     EMEA912                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD06                                               NAD06 /
      *////////////////////////////////////////////////////////////
     C     NAD06         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     6             ZGN0
     C     KEYMS0        SETLL     ENAD912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD912                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'SH'                                         GODSAVSENDER
     C                   MOVEL     N3124A        HENAS
     C                   MOVEL     N3124B        HEADS1
     C                   MOVEL     N3124C        HEADS2
     C                   MOVEL     N3124D        HEADS3
     C     N3039         IFNE      *BLANKS
     C                   MOVEL     N3039         KUINTN
     C     KUNKOK        CHAIN     KUNKO2                             33
     C     *IN33         IFEQ      '1'
     C                   MOVEL     KUINTN        KUK6              6
     C                   MOVE      KUK6          KUK7              7
     C                   MOVE      KUK7          HEADS3
     C                   ELSE
     C                   MOVE      KUKUN2        HEKNS
     C                   END
     C                   END
     C     N3035         WHENEQ    'CN'                                         MOTTAKER
     C                   MOVEL     N3124A        HENAK
     C                   MOVEL     N3124B        HEADK1
     C                   MOVEL     N3124C        HEADK2
     C                   MOVEL     N3124D        HEADK3
     C     N3039         IFNE      *BLANKS
     C                   MOVEL     N3039         KUINTN
     C     KUNKOK        CHAIN     KUNKO2                             33
     C     *IN33         IFEQ      '1'
     C                   MOVEL     KUINTN        KUK6              6
     C                   MOVE      KUK6          KUK7              7
     C                   MOVE      KUK7          HEADK3
     C                   ELSE
     C                   MOVE      KUKUN2        HEKNK
     C                   END
     C                   END
      *
     C                   ENDSL
      *
     C     KEYMS0        READE     ENAD912                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * PCI10                                               PCI10 /
      *////////////////////////////////////////////////////////////
     C     PCI10         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     10            ZGN1
     C     KEYMS1        SETLL     EPCI912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EPCI912                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEL     P7102A        HEGM1
     C                   MOVEL     P7102B        HEGM2
      *
     C  N52KEYMS1        READE     EPCI912                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
TMP   *
TMP   *////////////////////////////////////////////////////////////
TMP   * RFF05                                               RFF05 /
TMP   *////////////////////////////////////////////////////////////
TMP  C*          RFF05     BEGSR
TMP  C*                    MOVE '0'       *IN51
TMP  C*                    Z-ADD5         ZGN0
TMP  C*          KEYMS0    SETLLERFF912                  20
TMP  C*          *IN20     IFEQ '1'
TMP  C*          KEYMS0    READEERFF912                  51
TMP  C*          *IN51     DOWEQ'0'
TMP   *
TMP  C*          R1153     IFEQ 'FWN'                      FORW. REF
TMP  C*                    MOVELR1154     HERFA
TMP  C*                    ELSE
TMP  C*          R1153     IFEQ 'SRN'                      SHIP. REF
TMP  C*                    MOVE HEAVD     FSAVD
TMP  C*                    MOVE HEOPD     FSOPD
TMP  C*                    MOVE 'SRN'     FSKODE
TMP  C*                    MOVELR1154     FSSOK
TMP  C*                    WRITEFRISOKR
TMP  C*                    ENDIF
TMP  C*                    ENDIF
TMP   *
TMP   *
TMP  C* N51      KEYMS0    READEERFF912                  51
TMP  C* N51                ENDDO
TMP  C*                    ENDIF
TMP   *
TMP  C*                    ENDSR
      *////////////////////////////////////////////////////////////
      * TDT05                                               TDT05 /
      *////////////////////////////////////////////////////////////
     C     TDT05         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ETDT912                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT912                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     T8212A        HEHAWB
      *
     C                   Z-ADD     TREP0         ZREPB0
     C                   EXSR      LOC05
      *
     C  N51KEYMS0        READE     ETDT912                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   Z-ADD     24            HEAVD
     C     HEADFK        CHAIN     HEDUMMY                            53
     C     HEAVD         CHAIN     TELL                               53
     C     *IN53         IFEQ      '1'
     C                   Z-ADD     0             HEAVD
     C     HEAVD         CHAIN     TELL                               53
     C                   Z-ADD     24            HEAVD
     C                   ENDIF
     C                   Z-ADD     TEOPDN        HEOPD
     C                   ADD       1             TEOPDN
      * Hvis første posisjon på sendingen, tildel turnr
     C     NYSSN         IFEQ      'J'
     C                   Z-ADD     TETURN        TURNR
     C                   ADD       1             TETURN
     C                   ENDIF
     C                   UPDATE    TELLR
     C                   MOVE      'A'           HEUR
     C                   MOVE      'U'           HEST
     C                   MOVEL     'EDI'         HESG
     C                   MOVE      20            HEDTRC
     C                   MOVE      UYEAR         HEDTRÅ
     C                   MOVE      UMONTH        HEDTRM
     C                   MOVE      UDAY          HEDTRD
     C                   MOVE      'X'           HEKDFS
     C                   Z-ADD     KUKUN1        HEKNA
     C                   MOVE      'FT'          HEOT
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     0             ZREPB0
     C                   EXSR      TDT05
     C                   EXSR      NAD06
     C                   EXSR      GID10
      *__________________
      * Håndter bruddverdier
      *""""""""""""""""""
     C     HENAS         IFNE      AVSEND
     C     HEADS3        ORNE      AVSAD3
     C                   MOVE      *ZEROS        POSNR
     C                   END
     C                   ADD       1             POSNR
     C                   MOVEL     POSNR         HEPOS1
     C                   MOVE      TURNR         HEPRO
      *
     C                   MOVE      HENAS         AVSEND
     C                   MOVE      HEADS3        AVSAD3
     C     NYSSN         IFEQ      'J'
     C                   WRITE     EDIWSR
     C                   ELSE
     C                   UPDATE    EDIWSR
     C                   END
      *________________
      * WRITE TO SYSPED
      *""""""""""""""""
     C                   WRITE     HEADFR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TSTSTE                                              TSTSTE/
      *////////////////////////////////////////////////////////////
     C     TSTSTE        BEGSR
      *
     C     ST2KOD        CHAIN     STED2                              33
     C     *IN33         IFEQ      '1'
     C                   MOVEL     ST2KOD        ST2LK
     C     ST2LK         IFEQ      'NO'
     C                   Z-ADD     1             ST2SON
     C                   ELSE
     C                   Z-ADD     0             ST2SON
     C                   END
     C                   WRITE     STED2R
     C                   END
     C                   ENDSR
