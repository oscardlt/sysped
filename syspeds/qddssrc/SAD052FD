     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 30 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            AGSKV          3A  O  5  5
     A            AGSATS         5  3O  5  9EDTCDE(4)
     A            AGKD           1A  O  5 19
     A            AGPP           1A  O  5 24
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA05(05 'Frisk')
     A                                      CA03(12 'AVBRYT')
     A                                      CA12(12 'AVBRYT')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2  2SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD052D'
     A                                      COLOR(BLU)
     A                                  1 10'Plukking av avgift'
     A                                      COLOR(WHT)
     A                                  3  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  4  2'Op Skv   sats EU-land PP'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                      WINDOW(WINDOWSZ)
     A N30                              1  2'F5=Forny  F12=Forrige'
     A                                      COLOR(BLU)
     A  30                              1  2MSGCON(028 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'AVBRYT')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD052D'
     A                                      COLOR(BLU)
     A                                  1 10'Plukking av avgift'
     A                                      COLOR(WHT)
     A                                  3  2'Velg option:  1 = Plukke'
     A                                      COLOR(BLU)
     A                                  4  2'Op Skv   sats EU-land PP'
     A                                      COLOR(WHT)
