     A*%%TS  SD  20150526  131746  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/FAKT)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFI10XPNL)
     A                                      HLPTITLE('REG. AV FAKTURA')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20140914  214302  SVEIN       REL-V7R1M0  5770-WDS
     A            FALI      R    5 00H      TEXT('POSTNR')
     A            FAAVD     R    4 00H
     A            FAOPD     R    7 00H
     A            FAKUNR    R    8S00H
     A            FAVT      R   20A  H
     A            FAAVDR         4S 0H
     A            FADOCN    R   11   H
     A            FAAGRN         9S 0H
     A            FAKDUL    R    1   H
     A            FAKDUK    R    1   H
     A            FACU33    R    3   H
     A            FABELU    R   13 02H
     A            FALEVN         8S 0H
     A            FAEXRA         7S 4H
S14  A*           WSNR           1A  I  9  2VALUES(' ' '2' '4' '5' '9' 'F')
     A            WSNR           1A  I  9  2VALUES(' ' '2' '4' '5' '9' 'F' 'X')
     A  88                                  DSPATR(RI)
     A  88                                  DSPATR(PC)
     A  52
     AO 35                                  DSPATR(PR)
     A            FALIWS         2S 0O  9  5
     A            FASK      R    1   O  9  8TEXT('Selger/Kjøper S/K')
     A  51                                  DSPATR(BL)
     A            FAVK      R    3   O  9 11TEXT('Varekode')
     A            KGENOT        20A  O  9 15TEXT('NORSKTEKST')
     A            FAVAL     R    3   O  9 36TEXT('Valutakode')
     A            FABELV    R   13 02O  9 40TEXT('Beløp valuta')
     A                                      EDTCDE(L)
     A            FABELN    R   13 02O  9 56TEXT('Beløp NOK')
     A                                      EDTCDE(L)
     A            FAKDM     R    1   O  9 72TEXT('Momskode J/N')
     A            FACD11         1A  O  9 75TEXT('SAMMENSL.KODE')
     A            FAKDA     R    1   O  9 79TEXT('HVORDAN GENERERT')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20140914  214302  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  81N83N84                            ROLLUP(25)
     A  81N83N84                            ROLLDOWN(26)
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A          H                           HLPPNLGRP(SYFI10X010 SYFI10XPNL)
     A                                      HLPARA(01 001 07 080)
     A                                  1  2'SYSPED/SYFI10X'
     A                                      COLOR(BLU)
S21  A*                                 1 20MSGCON(032 DIV0987 *LIBL/SYMSGF)
     A                                  1 19MSGCON(026 DIV0987 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 51'/'
     A                                      COLOR(WHT)
     A            UOPD           7  0O  1 53EDTCDE(4)
     A N29                                  DSPATR(UL)
     A N29                                  COLOR(PNK)
N55  A  29                                  COLOR(RED)
     A                                  1 61MSGCON(008 DIV0988 *LIBL/SYMSGF)
     A            HESG           3A  O  1 70COLOR(WHT)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(013 DIV0134 *LIBL/SYMSGF)
     A            HEKNSF         8Y 0O  2 17TEXT('Kundenr Selg.fakm')
     A                                      EDTCDE(4)
     A  67 61                               COLOR(RED)
     A N67 61                               COLOR(WHT)
     A N61                                  DSPATR(UL)
     A N61                                  DSPATR(BL)
     A N61                                  COLOR(PNK)
     A            HENASF        30A  O  2 26TEXT('Navn Selger')
     A  67 61                               COLOR(RED)
     A N67 61                               COLOR(WHT)
     A N61                                  DSPATR(UL)
     A N61                                  DSPATR(BL)
     A N61                                  COLOR(PNK)
     A                                  2 61MSGCON(005 DIV0074 *LIBL/SYMSGF)
     A            HEPRO          8Y 0O  2 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(013 SHU1385 *LIBL/SYMSGF)
     A            HEKNSA         8Y 0O  3 17TEXT('Kundenr Selger')
     A  68                                  COLOR(RED)
     A N68                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            HENASA        30A  O  3 26TEXT('Navn Selg.fakm')
     A  68                                  COLOR(RED)
     A N68                                  COLOR(WHT)
     A                                  3 61MSGCON(004 SHU0509 *LIBL/SYMSGF)
     A            HEGN          15A  O  3 66TEXT('Godsnummer')
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(013 DIV0137 *LIBL/SYMSGF)
     A            HEKNKF         8Y 0O  4 17TEXT('Kundenr Kjøp.fakm')
     A  69 62                               COLOR(RED)
     A N69 62                               COLOR(WHT)
     A N62                                  DSPATR(BL)
     A N62                                  COLOR(PNK)
     A N62                                  DSPATR(UL)
     A                                      EDTCDE(4)
     A            HENAKF        30A  O  4 26TEXT('Navn Kjøper')
     A  69 62                               COLOR(RED)
     A N69 62                               COLOR(WHT)
     A N62                                  DSPATR(BL)
     A N62                                  COLOR(PNK)
     A N62                                  DSPATR(UL)
S21  A*                                 4 61MSGCON(005 DIV0995 *LIBL/SYMSGF)
S21  A*           HEAVD          4Y 0O  4 70TEXT('Avdeling')
S21  A*                                     EDTCDE(4)
S21  A*                                     COLOR(WHT)
     A                                  4 61MSGCON(006 JOV1606 *LIBL/SYMSGF)
     A            HEOT           2A  O  4 70TEXT('OPPDRTYPE')
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(013 SHU1386 *LIBL/SYMSGF)
     A            HEKNKA         8Y 0O  5 17TEXT('Kundenr Kjøper')
     A  70                                  COLOR(RED)
     A N70                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            HENAKA        30A  O  5 26TEXT('Navn Kjøp.fakm')
     A  70                                  COLOR(RED)
     A N70                                  COLOR(WHT)
     A                                  5 61MSGCON(006 DIV0997 *LIBL/SYMSGF)
     A            HEFR           3A  O  5 70TEXT('Frankatur')
     A                                      COLOR(WHT)
     A                                  5 74'F'
     A            FORANT         5Y 0O  5 76TEXT('ANT FORD')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(012 DIV0998 *LIBL/SYMSGF)
     A            WSFRA         18A  O  6 20COLOR(WHT)
     A            WSTIL         18A  O  6 40COLOR(WHT)
     A                                  6 61MSGCON(005 DIV0999 *LIBL/SYMSGF)
     A            HEFBV          9Y 0O  6 70TEXT('Fraktberegn.vekt')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(078 YBC1886 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  35                                  DSPATR(ND)
     A                                  8  2MSGCON(078 DIV1002 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA02(02 'LAGER-UTTAK/-INNLEGG')
     A                                      CF03(03 'Avslutt')
     A N36N35                               CF16(16 'Neste posisjon')
     A N35                                  CF05(05 'Frisk')
     A N35                                  CF06(06 'LAGE NY')
     A N35                                  CF07(07 'Ny beregning')
     A N39N35                               CF08(08 'Gå til tolldeklarasjon')
     A N39N35                               CF09(09 'Gå til tolldeklarasjon')
     A N35                                  CF10(10 'Ferdigmeldt')
     A N35                                  CF11(11 'Budsj./rekv')
     A N39N35                               CF12(12 'Gå til oppdrag')
     A                                      CF14(14 'SPØRRE OPPDRAG')
N22  A                                      CF17(17 'VIS ARKIV')
     A N35                                  CF18(18 'FRITEKST')
     A N35                                  CF20(20 'ENDRE FERDIGMEL')
     A                                      CF24(24 'MORE KEYS')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYFI10X999 SYFI10XPNL)
     A                                      HLPARA(03 001 06 080)
     A            MSGNR          7A  P
     A  87 90N77                        3  2MSGCON(040 JOV0432 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  87N90 77                        3  2MSGCON(040 JOV0433 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  87 90 77                        3  2MSGCON(040 JOV0434 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  87N90N77                        3  2MSGCON(040 JOV0435 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 43MSGCON(006 DIV1004 *LIBL/SYMSGF)
     A            WSUTLS         9Y 0O  3 50COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 60MSGCON(006 DIV0990 *LIBL/SYMSGF)
     A            WSTOTS        10Y 2O  3 67COLOR(WHT)
     A                                      EDTCDE(M)
     A                                  4 43MSGCON(006 DIV1004 *LIBL/SYMSGF)
     A            WSUTLK         9Y 0O  4 50COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 60MSGCON(006 DIV0994 *LIBL/SYMSGF)
     A            WSTOTK        10Y 2O  4 67COLOR(WHT)
     A                                      EDTCDE(M)
     A            MSG206        18A  O  5  2MSGID(&MSGC1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG207        18A  O  5 22MSGID(&MSGC2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG208        18A  O  5 42MSGID(&MSGC3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG209        18A  O  5 62MSGID(&MSGC4 SYMSGF)
     A                                      COLOR(BLU)
     A N79N30     UMSG          62A  O  6  2COLOR(RED)
     A  79N30                           6  2MSGCON(078 DIV1207 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A  30        MSGTXY        75A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A  30 85     FAVK           3A  O  6 78TEXT('VK M. KOSTN.KRAV')
     A                                      COLOR(WHT)
     A            MSGC1          7A  P
     A            MSGC2          7A  P
     A            MSGC3          7A  P
     A            MSGC4          7A  P
     A          R VARELI
     A*%%TS  SD  20150526  131746  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK GEBYRKODE')
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'BEKR. BUDSJ.TAP')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYFI10X015 SYFI10XPNL)
     A                                      HLPARA(02 001 02 004)
     A          H                           HLPPNLGRP(SYFI10X020 SYFI10XPNL)
     A                                      HLPARA(02 005 02 010)
     A          H                           HLPPNLGRP(SYFI10X025 SYFI10XPNL)
     A                                      HLPARA(02 011 02 036)
     A          H                           HLPPNLGRP(SYFI10X030 SYFI10XPNL)
     A                                      HLPARA(02 037 02 037)
     A          H                           HLPPNLGRP(SYFI10X035 SYFI10XPNL)
     A                                      HLPARA(02 039 02 060)
     A          H                           HLPPNLGRP(SYFI10X040 SYFI10XPNL)
     A                                      HLPARA(02 061 02 080)
     A          H                           HLPPNLGRP(SYFI10X045 SYFI10XPNL)
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPPNLGRP(SYFI10X050 SYFI10XPNL)
     A                                      HLPARA(04 001 04 018)
     A          H                           HLPPNLGRP(SYFI10X055 SYFI10XPNL)
     A                                      HLPARA(04 019 04 028)
     A          H                           HLPPNLGRP(SYFI10X060 SYFI10XPNL)
     A                                      HLPARA(04 029 04 037)
     A          H                           HLPPNLGRP(SYFI10X065 SYFI10XPNL)
     A                                      HLPARA(04 038 04 080)
     A          H                           HLPPNLGRP(SYFI10X999 SYFI10XPNL)
     A                                      HLPARA(05 001 06 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSSK           1A  B  2  2TEXT('Selger/Kjøper S/K')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      VALUES(' ' 'S' 'K' 'I' 'F' 'N' 'B' -
     A                                      'X' 'A')
     A                                      COLOR(YLW)
     A            WSVK           3A  B  2  5TEXT('Varekode')
     A                                      DSPATR(UL)
     A  71                                  DSPATR(PR)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTNR          1A  B  2  9DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSBELV        13Y 2B  2 13TEXT('Beløp valuta')
     A                                      DSPATR(UL)
     A                                      EDTCDE(M)
     A                                      COLOR(YLW)
     A            WSVAL          3A  B  2 30TEXT('Valutakode')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKDM          1A  B  2 37TEXT('Momskode J/N')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVT          20A  B  2 39TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKDUK         1A  B  3 60TEXT('Utgiftskonto J/N')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVALU         3A  B  3 62TEXT('Valutakode utlegg')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A            WSBELU        13Y 2B  3 66TEXT('Beløp')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      EDTCDE(M)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(007 DIV1206 *LIBL/SYMSGF)
     A            WSLEVN         8Y 0B  3 10EDTCDE(4)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 19MSGCON(004 DIV1010 *LIBL/SYMSGF)
     A            WSKURS         7Y 4B  3 24TEXT('Valutakurs kjøp')
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      COMP(GE ,0000)
     A                                  3 33MSGCON(006 DIV5047 *LIBL/SYMSGF)
     A            WSAVDR         4Y 0B  3 40EDTCDE(4)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 45MSGCON(006 JOV1466 *LIBL/SYMSGF)
     A            WSIOPD         7Y 0B  3 52EDTCDE(4)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(007 DIV5046 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0B  4 10EDTCDE(4)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSANT          5Y 2B  2 62DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A N46N50N41
     A N42N49                           4 30MSGCON(004 DIV1012 *LIBL/SYMSGF)
     A  41
     AO 42
     AO 46
     AO 50
     AO 49                              4 30MSGCON(004 DIV1013 *LIBL/SYMSGF)
     A N46N50N41
     A N42N49     WSCCF          1A  B  4 36DSPATR(UL)
     A                                      VALUES(' ' 'J')
     A                                      COLOR(YLW)
     A  41
     AO 42
     AO 46
     AO 50
     AO 49        WSKOM          1Y 0B  4 36DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  4 38MSGCON(004 SHU0308 *LIBL/SYMSGF)
     A            WSSAMM         1A  B  4 43COLOR(YLW)
     A                                  4 45MSGCON(006 DIV1004 *LIBL/SYMSGF)
     A            WSUTLS         9Y 0O  4 52COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 62MSGCON(006 DIV0990 *LIBL/SYMSGF)
     A            WSTOTS        10Y 2O  4 69COLOR(WHT)
     A                                      EDTCDE(M)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 45MSGCON(006 DIV1004 *LIBL/SYMSGF)
     A            WSUTLK         9Y 0O  5 52COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 62MSGCON(006 DIV0994 *LIBL/SYMSGF)
     A            WSTOTK        10Y 2O  5 69COLOR(WHT)
     A                                      EDTCDE(M)
     A N30        UMSG          62A  O  6  2COLOR(WHT)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  1  2MSGCON(078 SHU1579 *LIBL/SYMSGF)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2MSGCON(076 DIV1009 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            FASK      R    1A  O  2  2TEXT('Selger/Kjøper S/K')
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            FAVK      R    3A  O  2  5TEXT('Varekode')
     A                                      COLOR(WHT)
     A            FABELV    R   13Y02O  2 13TEXT('Beløp valuta')
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            FAVAL     R    3A  O  2 30TEXT('Valutakode')
     A                                      COLOR(WHT)
     A            FAKDM     R    1A  O  2 37TEXT('Momskode J/N')
     A                                      COLOR(WHT)
     A            FAVT      R   20A  O  2 40TEXT('Varetekst')
     A                                      COLOR(WHT)
     A            FAKDUK    R    1A  O  2 61TEXT('Utgiftskonto J/N')
     A                                      COLOR(WHT)
     A            FABELU    R   13Y02O  2 63TEXT('Beløp')
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(007 DIV1206 *LIBL/SYMSGF)
     A            FALEVN         8Y 0O  3 10COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 23MSGCON(004 DIV1010 *LIBL/SYMSGF)
     A            WSKURS         7Y 4O  3 28TEXT('Valutakurs kjøp')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            FAAVDR         4S 0O  3 39COLOR(WHT)
     A                                  3 44MSGCON(006 DIV1004 *LIBL/SYMSGF)
     A            WSUTLS         9Y 0O  3 51COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 61MSGCON(006 DIV0990 *LIBL/SYMSGF)
     A            WSTOTS        10Y 2O  3 68COLOR(WHT)
     A                                      EDTCDE(M)
     A                                  4  2MSGCON(007 DIV5046 *LIBL/SYMSGF)
     A            FAKUNR         8Y 0O  4 10COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 23MSGCON(003 DIV1011 *LIBL/SYMSGF)
     A            WSANT          5Y 2O  4 28EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  4 44MSGCON(006 DIV1004 *LIBL/SYMSGF)
     A            WSUTLK         9Y 0O  4 51COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 61MSGCON(006 DIV0994 *LIBL/SYMSGF)
     A            WSTOTK        10Y 2O  4 68COLOR(WHT)
     A                                      EDTCDE(M)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            KNAVN         30   O  5 23COLOR(WHT)
     A                                  5 61MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            FAAGRN    R        O  5 68REFFLD(FAKTR/FAAGRN *LIBL/FAKT)
     A                                      COLOR(WHT)
     A N30        UMSG          62A  O  6  2COLOR(WHT)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A*%%TS  SD  20140914  213356  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CA02(02 'LAGER-UTTAK/-INNLEGG')
     A                                      CF03(03 'Avslutt')
     A N36N35                               CF16(16 'Neste posisjon')
     A N35                                  CF05(05 'Frisk')
     A N35                                  CF06(06 'LAGE NY')
     A N35                                  CF07(07 'Ny beregning')
     A N39N35                               CF08(08 'Gå til tolldeklarasjon')
     A N39N35                               CF09(09 'Gå til tolldeklarasjon')
     A N35                                  CF10(10 'Ferdigmeldt')
     A N35                                  CF11(11 'Budsj./rekv')
     A N39N35                               CF12(12 'Gå til oppdrag')
     A                                      CF14(14 'SPØRRE FATURA')
     A                                      CF15(15 'HVIS ARKIV')
     A N35                                  CF18(18 'FRITEKST')
     A N35                                  CF20(20 'ENDRE FERDIGMEL')
     A                                      CF24(24 'MORE KEYS')
     A                                  1  2'SYSPED/SYFI10X'
     A                                      COLOR(BLU)
S21  A*                                 1 20MSGCON(032 DIV0987 *LIBL/SYMSGF)
     A                                  1 19MSGCON(026 DIV0987 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 46EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 51'/'
     A                                      COLOR(WHT)
     A            UOPD           7  0O  1 53EDTCDE(4)
     A N29                                  DSPATR(UL)
     A N29                                  COLOR(PNK)
N55  A  29                                  COLOR(RED)
     A                                  1 61MSGCON(008 DIV0988 *LIBL/SYMSGF)
     A            HESG           3A  O  1 70COLOR(WHT)
     A                                  2  2MSGCON(013 DIV0134 *LIBL/SYMSGF)
     A            HEKNSF         8Y 0O  2 17TEXT('Kundenr Selg.fakm')
     A                                      EDTCDE(4)
     A  67 61                               COLOR(RED)
     A N67 61                               COLOR(WHT)
     A    N61                               DSPATR(UL)
     A    N61                               COLOR(PNK)
     A            HENASF        30A  O  2 26TEXT('Navn Selger')
     A  67 61                               COLOR(RED)
     A N67 61                               COLOR(WHT)
     A    N61                               DSPATR(UL)
     A    N61                               COLOR(PNK)
     A                                  2 61MSGCON(005 DIV0074 *LIBL/SYMSGF)
     A            HEPRO          8Y 0O  2 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(013 SHU1385 *LIBL/SYMSGF)
     A            HEKNSA         8Y 0O  3 17TEXT('Kundenr Selger')
     A  68                                  COLOR(RED)
     A N68                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            HENASA        30A  O  3 26TEXT('Navn Selg.fakm')
     A  68                                  COLOR(RED)
     A N68                                  COLOR(WHT)
     A                                  3 61MSGCON(004 SHU0509 *LIBL/SYMSGF)
     A            HEGN          15A  O  3 66TEXT('Godsnummer')
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(013 DIV0137 *LIBL/SYMSGF)
     A            HEKNKF         8Y 0O  4 17TEXT('Kundenr Kjøp.fakm')
     A  69 62                               COLOR(RED)
     A N69 62                               COLOR(WHT)
     A    N62                               DSPATR(UL)
     A    N62                               COLOR(PNK)
     A                                      EDTCDE(4)
     A            HENAKF        30A  O  4 26TEXT('Navn Kjøper')
     A  69 62                               COLOR(RED)
     A N69 62                               COLOR(WHT)
     A    N62                               DSPATR(UL)
     A    N62                               COLOR(PNK)
S21  A*                                 4 61MSGCON(005 DIV0995 *LIBL/SYMSGF)
S21  A*           HEAVD          4Y 0O  4 70TEXT('Avdeling')
S21  A*                                     EDTCDE(4)
S21  A*                                     COLOR(WHT)
     A                                  4 61MSGCON(006 JOV1606 *LIBL/SYMSGF)
     A            HEOT           2A  O  4 70TEXT('OPPDRTYPE')
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(013 SHU1386 *LIBL/SYMSGF)
     A            HEKNKA         8Y 0O  5 17TEXT('Kundenr Kjøper')
     A  70                                  COLOR(RED)
     A N70                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            HENAKA        30A  O  5 26TEXT('Navn Kjøp.fakm')
     A  70                                  COLOR(RED)
     A N70                                  COLOR(WHT)
     A                                  2 61MSGCON(004 DIV0989 *LIBL/SYMSGF)
     A            WSDT           6Y 0O  2 70EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  5 61MSGCON(006 DIV0997 *LIBL/SYMSGF)
     A            HEFR           3A  O  5 70TEXT('Frankatur')
     A                                      COLOR(WHT)
     A                                  5 74'F'
     A            FORANT         5Y 0O  5 76TEXT('ANT FORD')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(012 DIV0998 *LIBL/SYMSGF)
     A            WSFRA         18A  O  6 20COLOR(WHT)
     A            WSTIL         18A  O  6 40COLOR(WHT)
     A                                  6 61MSGCON(005 DIV0999 *LIBL/SYMSGF)
     A            HEFBV          9Y 0O  6 70TEXT('Fraktberegn.vekt')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(026 DIV1000 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  35                                  DSPATR(ND)
     A                                  7 31MSGCON(020 DIV1001 *LIBL/SYMSGF)
     A  35                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(078 DIV1002 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  87 90N77                       21  2MSGCON(040 JOV0432 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  87N90 77                       21  2MSGCON(040 JOV0433 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  87 90 77                       21  2MSGCON(040 JOV0434 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  87N90N77                       21  2MSGCON(040 JOV0435 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 21 44MSGCON(006 DIV1004 *LIBL/SYMSGF)
     A            WSUTLS         9Y 0O 21 51COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 21 61MSGCON(006 DIV0990 *LIBL/SYMSGF)
     A            WSTOTS        10Y 2O 21 68COLOR(WHT)
     A                                      EDTCDE(M)
     A                                 22 44MSGCON(006 DIV1004 *LIBL/SYMSGF)
     A            WSUTLK         9Y 0O 22 51COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 61MSGCON(006 DIV0994 *LIBL/SYMSGF)
     A            WSTOTK        10Y 2O 22 68COLOR(WHT)
     A                                      EDTCDE(M)
     A            MSG522        18A  O 23  2MSGID(&MSGC1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG523        18A  O 23 22MSGID(&MSGC2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG524        18A  O 23 42MSGID(&MSGC3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG525        18A  O 23 62MSGID(&MSGC4 SYMSGF)
     A                                      COLOR(BLU)
     A            UMSG          62A  O 24  2COLOR(RED)
     A            MSGC1          7A  P
     A            MSGC2          7A  P
     A            MSGC3          7A  P
     A            MSGC4          7A  P
     A          R FRITXT
     A*%%TS  SD  20120222  134242  SVEIN       REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 45 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK KODE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYFI10X'
     A                                      COLOR(BLU)
     A                                  1 10'Krav Ekstra fritekst'
     A                                      DSPATR(HI)
     A            FAVKTI         3      1 32DSPATR(HI)
     A                                  8  2'F4=Søk'
     A                                      COLOR(BLU)
     A            FRI000        20   B  2  2
S46  A*           FAVK           3   O  2 25COLOR(WHT)
     A            FRI001        40   B  3  2
     A            FRI002        40   B  4  2
     A            FRI003        40   B  5  2
     A            FRI004        40   B  6  2
     A            FRI005        40   B  7  2
