     A          R GSS026WA
     A            STRI03        70      1  1
     A            KFMT1         75      2  1
     A                                  2  2'SHIPPED BY   :'
     A            FINM          30A     2 19
     A N28N31                           2 51'MANIFEST'
     A                                      HIGHLIGHT
     A N28 31                           2 51'PACKINGLIST'
     A                                      HIGHLIGHT
     A  28N31                           2 51'MANIFEST(COPY)'
     A                                      HIGHLIGHT
     A  28 31                           2 51'PACKINGLIST(COPY)'
     A                                      HIGHLIGHT
     A            TRTNO         11  0   2 66EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                  2 95'DATE'
     A                                  2100DATE EDTCDE(Y)
     A                                  2109'PAGE'
     A            SIDENR         3  0   2113EDTCDE(Z)
     A                                  3  2'FROM LOCATION:'
     A            XXLOCF         5A     3 19
     A                                  3 95'TIME'
     A            XTID4          4  0   3100EDTWRD('  .  ')
     A                                  4  2'CLIENT'
     A            TRCNO          8  0   4  9EDTCDE(Z)
     A            KNAVN         30A     4 19
     A                                  4 51'DESTINA:'
     A            XWMFT1        30A     4 60
     A                                  4 95'ATD DATE :'
     A            TRDTS          8  0   4108EDTWRD('    .  .  ')
     A                                  5  2'DELIVERY PLACE:'
     A            TRAD1         30A     5 19
     A                                  5 51'SENT BY:'
     A            XWMFT2        30A     5 60
     A                                  5 95'ETA DATE:'
     A            TRDTL          8  0   5108EDTWRD('    .  .  ')
     A            TRAD2         30A     6 19
     A  55                              6 51'COMMENTS:'
     C                                      HIGHLIGHT
     A  51        TRAD3         30A     7 19
     A  55        TRFT1         70A     7 51HIGHLIGHT
     A  52        TRAD4         30A     8 19
     A  56        TRFT2         70A     8 51HIGHLIGHT
     A  53        TRAD5         30A     9 19
     A  57        TRFT3         70A     9 51HIGHLIGHT
     A  54        TRAD6         30A    10 19
     A  58        TRFT4         70A    10 51HIGHLIGHT
     A          R GSS026WA27                SPACEB(1)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A                                    52'-------------------------'
     A                                    77'-------------------------'
     A                                   102'----------------'
     A                                      SPACEA(1)
     A          R GSS026WA2                 SPACEB(2)
     A                                      HIGHLIGHT
     A                                     2'UNIT NO'
     A            OLBOX          3S 0     10EDTCDE(Z)
     A                                    15'OF'
     A            TOTBOX         3S 0     18EDTCDE(Z)
     A                                    25'UNIT WEIGHT:'
     A            WSVKT          7S 1     38EDTCDE(4)
     A                                    47'KG'
     A                                    53'CUBE MTR:'
     A            WSM3           6S 3     63EDTCDE(4)
     A                                    71'M3'
     A                                    77'LOADING MTR:'
     A            WSLM           4S 2     90EDTCDE(4)
     A                                    96'LM'
     A                                      SPACEA(1)
     A  29                                25'Unit ID:'
     A  29        WSCID         17A       35SPACEA(1)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A                                    52' CONTENTS IN THIS UNIT   '
     A                                    77'-------------------------'
     A                                   102'----------------'
     A                                      SPACEA(1)
     A                                     2'SUPPLIER'
     A            XWTXT1        18A       16
     A                                    35'CLL"S'
     A                                    41'DESCRIPTIONS'
     A            XWTXT2         8A       58
     A                                    67'CUSTOMER'
     A                                    81'DESTINATION'
     A                                    95'POST CODE'
     A                                   105'AR'
     A                                   108'NEED DATE'
     A                                      SPACEA(1)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A                                    52'-------------------------'
     A                                    77'-------------------------'
     A                                   102'----------------'
      *
     A          R GSS026WB                  SPACEB(1)
     A            XWSNM         13A        2
     A            XWACKN        18A       16
     A            XWCLI          5  0     35EDTCDE(Z)
     A            XWDSC         16A       41
     A            XWM3LM         7  2     58EDTCDE(3)
     A            XWCNM         13A       67
     A            XWCAD1        13A       81
     A            XWCPN          9A       95
     A            ORAREA         2A      105
     A            ORDTWL         8  0    108EDTWRD('    .  .  ')
      *
     A          R GSS026WB2                 SPACEB(1)
     A            XXPB2         70A       27
      *
     A          R GSS026WD                  SPACEB(1)
     A                                     2'-------------------------'
     A                                    27'-----------------------  '
     A                                    52'END OF UNIT  ------------'
     A            WSNT           4S 0     78EDTCDE(4)
     A                                    84'COLLIES'
     A                                    77'                ---------'
     A                                   102'----------------'
     A          R GSS026WT                  SPACEB(2)
     A                                      HIGHLIGHT
     A                                     2'TOTAL SHIPPED UNITS:'
     A            TOTBOZ         3S 0     23EDTCDE(Z)
     A                                    30'COLLIES:'
     A            WSNTT          5S 0     37EDTCDE(4)
     A                                    48'WEIGHT:'
     A            WSVKTT         9S 1     54EDTCDE(4)
     A                                    66'KG'
     A                                    70'CUBE MTR:'
     A            WSM3T          7S 3     81EDTCDE(4)
     A                                    92'M3'
     A                                    97'LOAD.MTR:'
     A            WSLMT          4S 2    107EDTCDE(4)
     A                                   114'LM'
