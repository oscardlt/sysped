     H DATEDIT(*YMD)
     FTRACKF    O  A E           K DISK
     FHEADF     UF   E           K DISK
     FFAKT      IF A E           K DISK
     FFRISOL01  IF A E           K DISK
     FEDIM      IF   E           K DISK
     FKODFRI    IF   E           K DISK
     FECNT93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     c**************************************************************
     C     INIT          BEGSR
     c**************************************************************
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     HEADFK        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     FAKTK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XFALI
     C     FRISOL01K     KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
      *
     C     *LIKE         DEFINE    CMN           ZMN
     C     *LIKE         DEFINE    CGN           ZGN0
     C     *LIKE         DEFINE    HENT          XNT
     C     *LIKE         DEFINE    HEVKT         XVKT
     C     *LIKE         DEFINE    HEM3          XM3
     C     *LIKE         DEFINE    FALI          XFALI
     C     *LIKE         DEFINE    FABELN        XBELN
     C     *LIKE         DEFINE    FABELN        XBELNORI
     C     *LIKE         DEFINE    FABELN        XBELNRAB
     C                   EVAL      FSKODE = 'AGT'
     C                   EVAL      XFALI = 99999
      * INIT WORKFIELDS
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          MMN
     C                   Z-ADD     MMN           ZMN
     C     MMN           CHAIN     EDIM                               33
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE                  RTVD /
      *////////////////////////////////////////////////////////////
     C******************************************************************
     C     RTVD          BEGSR
     C******************************************************************
      * FINN VÅRT OPPDRAGSNR
     C                   EVAL      FSSOK = M1004
     C     FRISOL01K     CHAIN     FRISOL01                           33
     C   33              GOTO      SLRTV
      * DTM00
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        CHAIN     EDTM93A                            50
      * HENT KOLLI, VEKT OG KUBIKK
     C                   Z-ADD     0             ZGN0
     C                   EVAL      XNT  = -1
     C                   EVAL      XVKT = -1
     C                   EVAL      XM3  = -1
     C     KEYMS0        SETLL     ECNT93A
     C     KEYMS0        READE     ECNT93A                                50
     C     *IN50         DOWEQ     *OFF
     C                   MOVEA     C6066         A
     C                   EXSR      GETNUM
     C                   SELECT
     C                   WHEN      C6069 = '11'
     C                   EVAL(H)   XNT  = ZNUM
     C                   WHEN      C6069 = '7'
     C                   EVAL(H)   XVKT = ZNUM
     C                   WHEN      C6069 = '15'
     C                   EVAL(H)   XM3  = ZNUM / 1000
     C                   ENDSL
     C     KEYMS0        READE     ECNT93A                                50
     C  N50              ENDDO
      * HENT BELØP
     C                   Z-ADD     13            ZGN0
     C     KEYMS0        SETLL     EMOA93A
     C     KEYMS0        READE     EMOA93A                                50
     C     *IN50         DOWEQ     *OFF
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   SELECT
     C                   WHEN      M5025 = '23'
     C                   EVAL(H)   XBELNORI = ZNUM
     C                   WHEN      M5025 = '52'
     C                   EVAL(H)   XBELNRAB = ZNUM
     C                   ENDSL
     C     KEYMS0        READE     EMOA93A                                50
     C  N50              ENDDO
     C                   EVAL      XBELN = XBELNORI - XBELNRAB
      *
      * OPPDATER OPPDRAG OG FAKTURA
     C                   EXSR      SR
      *
     C     SLRTV         ENDSR
      *
      ***********************************************
     C     SR            BEGSR
      ***********************************************
     C     HEADFK        CHAIN     HEADF                              51
     C     *IN51         IFEQ      *OFF
      *---------------------------------------
      * OPPDATER DATO, SIGNATUR, KOLLI, VEKT OG KUBIKK
      *---------------------------------------
     C                   EVAL      HESGM  = 'TOLLPOST'
     C                   MOVEL     D2380         HEDTMO
     C                   IF        XNT <> -1
     C                   EVAL      HENT   = XNT
     C                   END
     C                   IF        XVKT <> -1
     C                   EVAL      HEVKT  = XVKT
     C                   END
     C                   IF        XM3 <> -1
     C                   EVAL      HEM3   = XM3
     C                   END
     C                   UPDATE    HEADFR
      * OPPDATER PÅ FRISOK
     C                   EVAL      FSKODE = 'POD'
     C                   EVAL      FSSOK = 'DT:' + %TRIM(HEDTMO)
     C                   MOVE      HEDTOP        FSDTOP
     C     FSKODE        CHAIN     KODFRI                             33
     C  N33              MOVE      KFSODK        FSDOKK
     C                   WRITE     FRISOKR
      *
      *---------------------------------------
      * OPPDATER FAKTURA
      *---------------------------------------
      *
     C                   IF        XBELN <> 0
     C     FAKTK         SETLL     FAKT
     C     HEADFK        READPE    FAKT                                   33
     C                   EVAL      XFALI = FALI + 1
     C                   CLEAR                   FAKTR
     C                   EVAL      FAAVD  = HEAVD
     C                   EVAL      FAOPD  = HEOPD
     C                   EVAL      FALI   = XFALI
     C                   EVAL      FASK   = 'S'
     C                   EVAL      FAVK   = 'INF'
     C                   EVAL      FAVAL  = 'NOK'
     C                   EVAL      FABELV = XBELN
     C                   EVAL      FABELN = XBELN
     C                   EVAL      FAKDM  = 'N'
     C                   EVAL      FAKDA  = 'E'
     C                   SELECT
     C                   WHEN      M0010  = '31'
     C                   EVAL      FAKUNR = 35454
     C                   ENDSL
     C                   WRITE     FAKTR
     C                   END
      *
      * OPPDATER TRACK&TRACE
     C                   EVAL      TTAVD  = HEAVD
     C                   EVAL      TTOPD  = HEOPD
     C                   EVAL      TTMANU = 'S'
     C                   EVAL      TTACTI = 'POD'
     C                   MOVEL     D2380         TTDATE
     C                   EVAL      TTTEXT = 'IFTMCS FRA TOLLPOST'
     C                   MOVEL     WDT           TTDATL
     C                   MOVEL     WTM           TTTIML
     C                   SELECT
     C                   WHEN      M0004 = 'NO000603'
     C                   EVAL      TTNAME = 'TOLLPOST'
     C                   EVAL      TTUSER = 'TOLLPOST'
     C                   OTHER
     C                   EVAL      TTNAME = M0004
     C                   EVAL      TTUSER = M0004
     C                   ENDSL
     C                   WRITE     TRACKFR
      *
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
