     A*%%TS  SD  20140408  080115  JOVO        REL-V7R1M0  5770-WDS
     A* NY LINJE FOR HENTET I HEADER.. RESTEN/SUBFIL 1 LINJE NED
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A          R NOERAS
     A                                      ASSUME
     A                                      OVERLAY
     A                                  1  2' '
     A          R WIND01
     A                                      OVERLAY
     A                                      WINDOW(*DFT 18 72)
     A          R SUBFIL                    SFL
     A            FNDATE6        6Y 0O  9  1EDTWRD('  .  .  ')
     A            FNTIME4        4Y 0O  9 10EDTWRD('  :  ')
     A            FNTERM         5A  O  9 16
     A            TERMTXT       25   O  9 22COLOR(WHT)
     A            FNUSER        10A  O  9 48
     A            FNDATL6        6Y 0O  9 59EDTWRD('  .  .  ')
     A            FNTIML4        4Y 0O  9 68EDTWRD('  :  ')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(WIND01)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF12(12 'tilbake')
     A                                      CF03(12)
     A                                      CF05(05)
     A                                      KEEP
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A            ZRECNO         4S 0H      SFLRCDNBR
     A                                  1  1'SYFA10B2'
     A                                      COLOR(BLU)
     A                                  1 22'Vise logg på ett kolli    '
     A                                      COLOR(WHT)
     A                                  2  2'Kolli-ID:'
     A            UUCLID        18A  O  2 17COLOR(WHT)
     A                                  3  2'Hentet avsend:'
     A            FMCODT6        6Y 0O  3 17EDTWRD('  .  .  ')
     A            FMCOTM4        4Y 0O  3 26EDTWRD('  :  ')
     A            FMCOUS        10A  O  3 32
     A                                  4  2'INN egen Term:'
     A            FMITDT6        6Y 0O  4 17EDTWRD('  .  .  ')
     A            FMITTM4        4Y 0O  4 26EDTWRD('  :  ')
     A            FMITUS        10A  O  4 32
     A                                  5  2'UT  egen Term:'
     A            FMUTDT6        6Y 0O  5 17EDTWRD('  .  .  ')
     A            FMUTTM4        4Y 0O  5 26EDTWRD('  :  ')
     A            FMUTUS        10A  O  5 32
     A                                  6  2'Levert mottak:'
     A            FMPODT6        6Y 0O  6 17EDTWRD('  .  .  ')
     A            FMPOTM4        4Y 0O  6 26EDTWRD('  :  ')
     A            FMPOUS        10A  O  6 32
     A                                  7 17'--- Øvrige hendelser/fremmede term-
     A                                      inaler --- '
     A                                      COLOR(WHT)
     A                                  8  1' Dato    Tid   Terminal           -
     A                                                   Loggført av.......    '
     A                                      COLOR(WHT)
     A          R BUNN
     A*%%TS  SD  20140408  080115  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                WINDOW(WIND01)
     A  *DS4                                WINDOW(WIND01)
     A                                      SLNO(17)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1 26'F5=Forny '
     A                                      COLOR(BLU)
     A                                  1 52'F3/F13=Avbryt'
     A                                      COLOR(BLU)
