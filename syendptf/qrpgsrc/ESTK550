      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK550 MODULE(*LIBL/ESTK550 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
      *%%%%%%%%%%%%%%%%%%%%
      * 02 JOV PTF11 språkkode mmm
      * 03 JOV PTF11 forbedre tekster / snu noen rekkefølger - mest i HTML
      *%%%%%%%%%%%%%%%%%%%%
      * 04 JOV PTF12 vise lenke for følgedokument
      * 05 JOV PTF12 motta TUR som innparameter - i så fall - forrige= turliste
      * 06 sh  PTF12 historisk arkiv
      * 07 Jov PTF12 Hent neste event fra egen rutine + ADR2 + Godsmerke
      * 08 JOV PTF12 "GetNextStat" skal IKKE kalles ved Submit
      *              Collected at customer /kunde bytte med .. at sender / avsender
      * 09 JOV PTF12 Melding til transportør
      * 10 JOV PTF12 IKKE logg i T&T dersomsamme finnes mindre en ett min. gammel
      * 11 JOV PTF12 Merkand om Hentes/Leveres på terminal..
      * 12 JOV PTF12 FRALIG GODS-info hentes inn til merkn til transp
      * 13 JOV PTF12 Hente/lev-sted - logikk utvidet
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FHeadf     UF   E           K DISK    usropn
     FHead39    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR39)
     FFRISOL01  IF   E           K DISK    USROPN
     F                                     RENAME(FRISOKR:FRI01R)
     FBRPARF    IF   E           K DISK    Usropn
     F*RACKF    O  A E           K DISK    Usropn
     FTRACKF    O  A E           K DISK    Usropn
     FTRACKT    UF   E           K DISK    Usropn
N04  FARKIVL02  IF   E           K DISK    Usropn
N04  FFIRMARC   IF   E           K DISK    Usropn
N04  FARKTXT    IF   E           K DISK    Usropn
N04  FARKEXT    IF   E           K DISK    Usropn
N09  FFREETXT2  IF   E           K DISK    Usropn
N11  FSTED2D    IF   E           K DISK    Usropn
     FQ         O  A F32000        DISK    Usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D WSuser          s             10
     D Snd             s             20
     D Snd17           s             17
     D Snd20           s             20
     D SndL            s             25
     D SIGN            s             50
     D Event           s              3
     D VERSJON         s             10
     D AvdAA           s             10
     D OpdAA           s             10
N05  D Tur             s              8
S02  D*JQ              s              1
N02  D Sprak           s              2
N02  D S               s              2
N02  D bytt_sprak      s              1
     D VisAvv          s              1
     D AvvButton       s            300
N04  D DocLnk          s            300
N09  D MLDTRA          s           5000
N02  D AvvTxt          s             50
     D AVVIK           s             50
     D KOMM            s             50
E07  D AVS             s            200
E07  D MOT             s            200
N07  D HEGM            s            100
     D Sted            s             20
     D AntKollL        s             10
     D AVVIK1          s             50
     D KOMM1           s             50
     D AVVIK2          s             50
     D KOMM2           s             50
     D AVVIK3          s             50
     D KOMM3           s             50
     D AVVIK4          s             50
     D KOMM4           s             50
     D AVVIK5          s             50
     D KOMM5           s             50
     D PODSelected     s             10
     D COLSelected     s             10
     D LETSelected     s             10
     D HETSelected     s             10
     D VistBilde1      s              1
     D Submited        s              1
     D Lat             s              9
     D Lng             s             10
     D LatN            s              8  6
     D LngN            s              9  6
     D NumBig          s             19  9
     D Fil             s          32000
     D SignFile        s          32000
     D ErrTxt          s           5000
     D TelefonNr       s             15
     D Avsluttet       s            300
N05  D BackUrl         s            200
N05  D RedirUrl        s            200
N07  D NextAct         s             25
N07  D NextActKod      s              3
     D                 DS
     D  DUP                    1   1100  0
     D                                     DIM(100)
     D                 DS
     D  TimeTmp                1     26
     D  TimeTmpÅ               1      4
     D  TimeTmpM               6      7
     D  TimeTmpD               9     10
     D  TimeTmpT              12     16
     D  TimeTmpTT             12     13
     D  TimeTmpTM             15     16
     D  TimeTmpTS             18     19
     D                 DS
     D  Idag                   1      8
     D  IdagTmpÅ               1      4
     D  IdagTmpM               5      6
     D  IdagTmpD               7      8
     D                 DS
     D  IdagTID                1      6
     D  IdagTT                 1      2
     D  IdagTM                 3      4
     D  IdagTTM                3      4
     D  IdagTS                 5      6
     D                 DS
     D   DSSnd2                1     20
     D   DSSnd1_3              1      3
     D   DSSnd4_20             4     20
     D   DSSnd11_20           11     20
     D   DSSnd17_17           17     17
     D   DSSnd18_18           18     18
     D                 DS
     D  AvdOpd                 1     11  0
     D   DsAvd                 1      4  0
     D   DsOpd                 5     11  0
N10  D                 DS
N10  D  GmDtTm                 1     14  0
N10  D    gmdate               1      8  0
N10  D    gmtime               9     14  0
N10  D                 DS
N10  D  NyDtTm                 1     14  0
N10  D    nydate               1      8  0
N10  D    nytime               9     14  0
      *
      * Receive query string from the browser
      /copy syspeds/qrpgsrcpy,prolog3
      *
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Set section variables
     C                   exsr      MAIN
      * Quit
     C                   exsr      Exit
     C                   eval      *inlr = *on
     C                   return
      *
      **************************************************
     C     Parse         begsr
      **************************************************
      * Parse variables from QUERY_STRING environment variable
     C                   eval      WSuser = zhbgetvar('user')
     C                   eval      WSuser = uppify(WSUSER)
     C                   eval      VERSJON = zhbgetvar('V')
     C                   eval      Event = ZhbGetVar('Event')
     c     Event         ifeq      *blanks
     C                   eval      Event = 'POD'
     c                   endif
     C                   eval      Snd  = zhbgetvar('SND')
     C                   eval      Tur  = zhbgetvar('Tur')
     C                   eval      Sign  = ZhbGetVar('Sign')
     C                   eval      VisAvv= ZhbGetVar('VisAvv')
     C                   eval      Avvik1= ZhbGetVar('Avvik1')
     C                   eval      Avvik2= ZhbGetVar('Avvik2')
     C                   eval      Avvik3= ZhbGetVar('Avvik3')
     C                   eval      Avvik4= ZhbGetVar('Avvik4')
     C                   eval      Avvik5= ZhbGetVar('Avvik5')
     C                   eval      Komm1   = ZhbGetVar('Komm1')
     C                   eval      Komm2   = ZhbGetVar('Komm2')
     C                   eval      Komm3   = ZhbGetVar('Komm3')
     C                   eval      Komm4   = ZhbGetVar('Komm4')
     C                   eval      Komm5   = ZhbGetVar('Komm5')
      * fil er signaturfil (som x/y-koordinater..)
     C                   eval      SignFile = ZhbGetVar('SignFile')
     C                   eval      Fil = ZhbGetVar('Fil')
     C     Fil           Ifne      *blanks
     c                   exsr      SignSR
     C                   end
     c                   eval      Sted   = ZhbGetVar('Sted')
     c*    Sted          ifeq      *blanks
     c*                  eval      Sted = '- Evt Sted/Term -'
     c*                  endif
     c                   eval      Lat       = ZhbGetVar('Lat')
     c                   eval      Lng       = ZhbGetVar('Lng')
     c     Lat           ifne      *blanks
     c     Lng           andne     *blanks
     C                   eval      NumBig  = c2n2(LAT)
     c     NumBig        Ifge      -90
     c     NumBig        andle     90
     C                   eval      LatN    = NumBig
     c                   end
     C                   eval      NumBig  = c2n2(Lng)
     c     NumBig        Ifge      -180
     c     NumBig        andle     180
     C                   eval      LngN    = NumBig
     c                   end
     c                   endif
     C                   eval      TelefonNr  = zhbgetvar('TelefonNr')
     C                   eval      VistBilde1 = zhbgetvar('VistBilde1')
     C                   eval      Submited   = zhbgetvar('Submited')
     c     snd           ifne      *blanks
     c                   move      'Y'           VistBilde1
     c                   endif
S01  C*                  eval      JQ  = zhbgetvar('JQ')
N02  C                   eval      Sprak = zhbgetvar('Sprak')
N02  C                   if        Sprak = *blanks
N02  C                   eval      Sprak = zhbgetvar('S')                       Kortnavn Språk
N02  C                   endif
N02  C                   if        Sprak = 'NO'
N02  C                   eval      Sprak = *blanks
N02  C                   endif
N02  C                   eval      bytt_sprak = zhbgetvar('bytt_sprak')
      *
     C*                  eval      HTTP_UserA =getenv('HTTP_USER_AGENT':
     C*                            qusec)
     c* 33 *ON='Windows CE' finnes i streng=fra PDA / 33 OFF-Finnes ikke=SmartPhone(fullskj.browser)
     c*    'Windows CE'  SCAN      HTTP_UserA    xxxPOS            3 0    33
     c*  33              move      'J'           FraPDA            1
     C                   endsr
N01   **************************************************
N01  C     GetNxtEvent   begsr
N01   **************************************************
N01  c                   call      'SYGE120'
N01  c                   parm                    heavd             4 0
N01  c                   parm                    heopd             7 0
N01  c                   parm                    NextActKod        3
N01  c                   parm                    Term             20
N01  c                   select
N01   * COL
N01  c                   when      NextActKod = 'COL'
N01  c                   eval      NextAct= 'Hentes avsender'
N01  C                   if        Sprak = 'EN'
N01  c                   eval      NextAct= 'Collect at sender'
N01  c                   endif
N01   * HET
N01  c                   when      NextActKod = 'HET'
N01  c                   eval      NextAct= 'Hent term '+Term
N01  C                   if        Sprak = 'EN'
N01  c                   eval      NextAct= 'Coll. term '+Term
N01  c                   endif
N01   * LET
N01  c                   when      NextActKod = 'LET'
N01  c                   eval      NextAct= 'Lever term '+Term
N01  C                   if        Sprak = 'EN'
N01  c                   eval      NextAct= 'Deliv term '+Term
N01  c                   endif
N01   * POD
N01  c                   when      NextActKod = 'POD'
N01  c                   eval      NextAct= 'Lever mottaker'
N01  C                   if        Sprak = 'EN'
N01  c                   eval      NextAct= 'Deliv. receiver'
N01  c                   endif
N01   * FER = Ferdig
N01  c                   when      NextActKod = 'FER'
N01  c                   eval      NextAct= 'Ferdig '
N01  C                   if        Sprak = 'EN'
N01  c                   eval      NextAct= 'Finished'
N01  c                   endif
N01  c                   endsl
N01   *
N01  c                   endsr
      **************************************************
     C     Exit          begsr
      **************************************************
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     TRACKF
     C                   CLOSE     TRACKT
     C                   CLOSE     HEADF
     C                   CLOSE     FIRM
     C                   CLOSE     BRPARF
N04  C                   CLOSE     ARKIVL02
N04  C                   CLOSE     FIRMARC
N04  C                   CLOSE     ARKTXT
N04  C                   CLOSE     ARKEXT
N09  C                   CLOSE     FREETXT2
N11  C                   CLOSE     STED2D
     C                   endsr
      **************************************************
     C     MAIN          begsr
      **************************************************
      * LOAD HTML   (JQ=Jqueryvariant)
s02  C*    JQ            ifeq      'Y'
s02  C*                  callp     gethtmlifs(
s02  C*                            '/Syspeddev/html/ESTK550.html':
s02  C*                            '/CGITAG/')
s02  C*                  else
s02  C*                  callp     gethtmlifs(
s02  C*                            '/Syspeddev/TKSW/ESTK550/ESTK550.html':
s02  C*                            '/CGITAG/')
s02  C*                  endif
N02  C                   callp     gethtmlifs('/Syspeddev/TKSW/ESTK550/'
N02  C                             +'ESTK550'+%trim(Sprak)+'.html'
N02  C                             :'/CGITAG/')
      * repeat enkelte variable
S02  C*                  callp     updHTMLvar('JQ':JQ)
     C                   callp     updHTMLvar('TelefonNr':TelefonNr)
     C                   callp     updHTMLvar('user':WSUSER)
N03  C                   callp     updHTMLvar('snd':SND)
N05  C                   callp     updHTMLvar('tur':TUR)
N05  C                   eval      BackUrl='/sycgip/ESTK550.pgm?user='
N05  C                             +%trim(WSUSER)
N05  C                   if        TUR<>*blanks
N05  C                   eval      BackUrl='/sycgip/ESTK550T.pgm?user='
N05  C                             +%trim(WSUSER)+'&t='+%trim(TelefonNr)
N05  C                             +'&tur='+%trim(Tur)
      * En redirect som når en etter Form-input kommer til ingangsbiledet
      * Når en kommer fra turliste (med turnr) skal en etter utført action tilbake til turliste
N05  C                   eval      RedirUrl='<meta http-equiv="refresh" '
N05  C                             +'content="0;url='
N05  C                             +'/sycgip/ESTK550T.pgm?user='
N05  C                             +%trim(WSUSER)+'&t='+%trim(TelefonNr)
N05  C                             +'&tur='+%trim(Tur)+'" />'
N05  C                   endif
N05  C                   callp     updHTMLvar('BackUrl':BackUrl)
N05  C                   callp     updHTMLvar('RedirUrl':RedirUrl)
     C                   callp     updHTMLvar('VistBilde1':'Y')
     C                   callp     updHTMLvar('AVSLUTTET':' ')
     C                   callp     updHTMLvar('FIRMA':FIKRTN)
     C                   callp     updHTMLvar2('Fil'
     C                                         :%addr(Fil):%len(fil))
     C                   callp     updHTMLvar2('SignFile'
     C                                         :%addr(SignFile):%len(SignFile))
      * hent data/feiltetst mm
     C                   eval      ErrTxt = *blanks
      * Ugyldig userID
     C     *IN50         IFEQ      '1'
     C                   callp     updHTMLvar('FIRMA':'**UKJENT**')
N02  c                   select
N02  c                   when      sprak = 'EN'
N02  C                   eval      ErrTxt = 'Unknown userID'
N02  c                   other
     C                   eval      ErrTxt = 'Ukjent userID'
N02  c                   endsl
     c                   goto      VisErr1
     c                   end
      *
N02  C     bytt_sprak    ifeq      'Y'                                          Buttet språk
N02  C                   if        Submited   = 'Y'
N02  C                   eval      Submited   = ' '
N02  C                   else
N02  C                   callp     updHTMLvar('ErrTxt':'')
N02  C                   callp     wrtsection('noSndnr')
N02  C                   goto      UtMain
N02  C                   endif
N02  C                   endif
     C
     C     SND           ifeq      *blanks
     C     ErrTxt        andeq     *blanks
     C     VistBilde1    andeq     'Y'
N02  c                   select
N02  c                   when      sprak = 'EN'
N02  C                   eval      ErrTxt = 'Waybill/order number required'
N02  c                   other
     C                   eval      ErrTxt = 'Sendingsnummer må oppgis'
N02  c                   endsl
     c                   goto      VisErr1
     C                   endif
      *
     c                   exsr      SNDOPD
      *
      * finner ikke sending
     C     Heopd         IFEQ      *zeros
     C                   eval      ErrTxt = 'Ukjent sendingsnr '+snd
N02  c                   select
N02  c                   when      sprak = 'EN'
N02  C                   eval      ErrTxt = 'Invalid waybill/order number '+snd
N02  c                   other
     C                   eval      ErrTxt = 'Ukjent sendingsnr '+snd
N02  c                   endsl
     c                   goto      VisErr1
     c                   end
      *
     c     VisErr1       tag
     c     ErrTxt        ifne      *blanks
     c     VistBilde1    orne      'Y'
     c     ErrTxt        ifne      *blanks
     c                   eval      ErrTxt = '<div class="alert alert-danger"> '
     c                             +%trim(ErrTxt)+'</div>'
     c                   endif
     C                   callp     updHTMLvar('ErrTxt':ErrTxt)
     C                   callp     wrtsection('noSndnr')
     C                   goto      UtMain
     c                   endif
      *
      * Bilde1-tester ferdig
      * Det er referert til OK sendingsnr. dersom 1. gang vis, ellers Test/vis på ny ved feil
     c
     C                   eval      ErrTxt = *blanks
     c     Submited      ifeq      'Y'
     c                   exsr      TstBilde2
     c                   endif

     c     Submited      ifne      'Y'                                          1.gang el visAvv
     c     ErrTxt        orne      *blanks                                      Submited m feil
     c                   exsr      VisSnd
     C                   goto      UtMain
     c                   endif
      * OK - Oppdater -
     C                   exsr      SRTT
N03   * ved henting behold siste send-nr i input felt (i fall neste er levering av samme)
N02  c                   select
N02  c                   when      sprak = 'EN'
N02  C                   eval      Avsluttet =
N03  c                             '<b>'
E03  c                             +'You may finish by closing/exiting '
E03  c                             +'the browser.<br><br></b>'
E03  c                             +'Or you may enter a new (or same) '
E03  c                             +'waybill/shipment<br>'
E03  c                             +'no to capture new events.<br><br>'
N02  C                   callp     updHTMLvar('AVSLUTTET':Avsluttet)
N02  c                   eval      ErrTxt = '<div class="alert alert-success"> '
N02  c                             +'The event is updated on '
N02  c                             +%trim(%editc(HEAVD:'Z'))+'*'
N02  c                             +%trim(%editc(HEOPD:'Z'))+'</div>'
N02  c                   other
     C                   eval      Avsluttet =
N03  c                             '<b>'
E03  c                             +'Du kan avslutte ved å gå ut '
E03  c                             +'av browseren.<br><br></b>'
E03  c                             +'Eller du kan angi nytt (eller samme) '
E03  c                             +'sendingsnr <br>'
E03  c                             +'for å registrere nye hendelser.<br><br>'
     C                   callp     updHTMLvar('AVSLUTTET':Avsluttet)
     c                   eval      ErrTxt = '<div class="alert alert-success"> '
     c                             +'Hendelsen er oppdatert på '
     c                             +%trim(%editc(HEAVD:'Z'))+'*'
     c                             +%trim(%editc(HEOPD:'Z'))+'</div>'
N02  c                   endsl
      *
N03   * ved henting bevar send kr (i fall lev på samm er neste)
N03  c                   if        Event<>'COL' and  Event<>'HET'
N03  c                   eval      Snd = *blanks
N03  c                   else
N03  c                   if        %subst(Snd:5:1)='_'
N03  c                   eval      Snd = %trim(%editc(HEAVD:'Z'))+'*'
N03  c                             +%trim(%editc(HEOPD:'Z'))
N03  c                   endif
N03  c                   endif
N03  C                   callp     updHTMLvar('snd':SND)
     C                   callp     updHTMLvar('ErrTxt':ErrTxt)
     C                   callp     wrtsection('noSndnr')
     C
     C     UtMain        endsr
     C
     C***********************************************
     C     TstBilde2     BEGSR
     C***********************************************
      * POD
     c                   if        Event='POD'
     c                   if        Sign =*blanks or Fil=*blanks
N02  c                   select
N02  c                   when      sprak = 'EN'
N02  c                   eval      ErrTxt='Sign (in letters AND grafically) '
N02  c                             +'required on POD.'
N02  c                   other
S02  c*                  eval      ErrTxt='Sign (blokkbokst OG grafisk) '
S02  c*                            +'angis ved POD.'
N02  c                   eval      ErrTxt='Sign (med bokstaver OG grafisk) '
     c                             +'må angis ved POD.'
N02  c                   endsl
     c                   endif
     c                   endif
      * LET = Levering terminal
     c                   if        Event='LET'
     c                   if        Sign =*blanks
N02  c                   select
N02  c                   when      sprak = 'EN'
N02  c                   eval      ErrTxt='Sign (in letters) '
N02  c                             +'required when delivering at terminal'
N02  c                   other
S02  c*                  eval      ErrTxt='Sign (blokkbokst) '
N02  c                   eval      ErrTxt='Sign (med bokstaver) må '
     c                             +'angis ved levering term.'
N02  c                   endsl
     c                   endif
     c                   endif
     c                   ENDSR
     C***********************************************
     C     VisSnd        BEGSR
     C***********************************************
N11   * Hentes.. / Leveres kan avvike fra oppdraget avsender/mottaker- I så fall Hentes:../Leverses:
N11   *            I så fall skriv en førstelinje med Hentes:.. / Leveres:.. i uthevet rødt
N11  c                   exsr      GetHenLev
N11   *
s07  c*                  eval      AVS=%trim(HENAS)
S02  c*                            +'<BR/>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
S07  c*                            +'<BR/>'
S07  c*                            + %trim(HEADS1)+', '+ %trim(HEADS3)
S07  c*                  eval      MOT=%trim(HENAK)
S02  c*                            +'<BR/>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;'
S07  c*                            +'<BR/>'
S07  c*                            + %trim(HEADK1)+', '+ %trim(HEADK3)
s07   * HELT PÅ NMY FOR RYDDIGHETENS SKYLD
S11  c*                  eval      AVS=%trim(HENAS)
N11  c                   eval      AVS=%trim(HENTES)+%trim(HENAS)
N07  c                             +'<BR/>'
N07  c                             + %trim(HEADS1)
N07  c                   if        HEADS2 <> *blanks
N07  c                   eval      AVS=%trim(AVS)+', '+ %trim(HEADS2)
N07  c                   endif
N07  c                   eval      AVS=%trim(AVS)+', '+ %trim(HEADS3)
S11  c*                  eval      MOT=%trim(HENAK)
N11  c                   eval      MOT=%trim(LEVERES)+%trim(HENAK)
N07  c                             +'<BR/>'
N07  c                             + %trim(HEADK1)
N07  c                   if        HEADK2 <> *blanks
N07  c                   eval      MOT=%trim(MOT)+', '+ %trim(HEADK2)
N07  c                   endif
N07  c                   eval      MOT=%trim(MOT)+', '+ %trim(HEADK3)
     C                   callp     updHTMLvar('Avs':AVS)
     C                   callp     updHTMLvar('Mot':MOT)
N07  c                   if        HEGM1 <> *blanks
N07  c                   eval      HEGM= 'Merke:'
N07  C                   if        Sprak = 'EN'
N07  c                   eval      HEGM= 'Marks:'
N07  C                   endif
N07  c                   eval      HEGM=%trim(HEGM)+' <b>'+ %trim(HEGM1)
     c                                 +' '+HEGM2+'</b>'
N07  C                   endif
N07  C                   callp     updHTMLvar('HEGM':HEGM)
     C                   callp     updHtmlVar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHtmlVar('HEVKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHtmlVar('HEM3':
     C                             %trim(%editc(HEM3:'3')))
     C                   callp     updHtmlVar('HELM':
     C                             %trim(%editc(HELM:'3')))
N09  C                   exsr      MeldTrans
N09  C                   callp     updHTMLvar('MLDTRA':MLDTRA)
N04  C                   exsr      ChkDocLnk
N04  C                   callp     updHTMLvar('DOCLNK':DOCLNK)
     C                   callp     updHTMLvar('SND':SND)
N07  C                   if        STED=*blanks  and Term<>*blanks
N07  C                   eval      STED=Term
N07  C                   endif
     C                   callp     updHTMLvar('STED':STED)
     C                   callp     updHTMLvar('SIGN':SIGN)
      *
     c                   select
     c     Event         wheneq    'POD'
     c                   eval      PODSelected ='Selected'
     c     Event         wheneq    'COL'
     c                   eval      COLSelected ='Selected'
     c     Event         wheneq    'LET'
     c                   eval      LETSelected ='Selected'
     c     Event         wheneq    'HET'
     c                   eval      HETSelected ='Selected'
     c                   endsl
     C                   callp     updHTMLvar('PODSelected':PODSelected)
     C                   callp     updHTMLvar('COLSelected':COLSelected)
     C                   callp     updHTMLvar('LETSelected':LETSelected)
     C                   callp     updHTMLvar('HETSelected':HETSelected)
     C                   callp     updHTMLvar('VisAvv':VisAvv)
     c     VisAvv        ifne      'Y'
N02  c                   select
N02  c                   when      sprak = 'EN'
N02  c                   eval      AvvTxt = 'Damages/Deviations'
N02  c                   other
N02  c                   eval      AvvTxt = 'Skade/Avvik'
N02  c                   endsl
     C                   eval      AvvButton ='<input type="button" '
     c                             +'class="btn btn-primary btn-block" '
S02  c*                            +'VALUE="Avvik" onClick="VisAvvik()">'
N02  c                             +'VALUE="'+%trim(AvvTxt)
N02  c                             +'" onClick="VisAvvik()">'
     c                   else
S01  C*                  eval      AvvButton ='<B>Avvik.</B> Max 5 tekstkoder '
S01  c*                            +'og/el fritekster:'
N02  c                   select
N02  c                   when      sprak = 'EN'
N02  C                   eval      AvvButton ='<B>Deviatons.</B> Select code '
N02  c                             +'and/or type text:'
N02  c                   other
N02  C                   eval      AvvButton ='<B>Avvik.</B> Velg kode '
N02  c                             +'og/eller skriv tekst:'
N02  c                   endsl
     c                   endif
     C                   callp     updHTMLvar('AvvButton':AvvButton)
      * skriv top / avvik / bot -seksjoner av HTMLen
     c     ErrTxt        ifne      *blanks
     c                   eval      ErrTxt = '<div class="alert alert-danger"> '
     c                             +%trim(ErrTxt)+'</div>'
     c                   endif
     C                   callp     updHTMLvar('ErrTxt':ErrTxt)
     C                   callp     wrtsection('Sndnr')
     c     VisAvv        ifeq      'Y'
     C                   exsr      SRAVVIK
     c                   endif
     C                   callp     wrtsection('bot')
      *
     c                   endsr
      *
N11   **************************************************
N11  C     GetHenLev     begsr
N11   **************************************************
N11  C                   move      *blanks       HENTES           70
N11  C                   move      *blanks       LEVERES          70
N11  C                   select
N11   * ved HO - avvikende hente/levstde dersom via er fylt ut
N11  C                   when      trslag = 'HO'
N11  C                   if        HESDFF <> *blanks
N11  C                   eval      ST2LK  = HELKS
N11  C                   eval      ST2KOD = HESDFF
N11  c                   eval      ST2NVN = *blanks
N11  c     Sted2dKey     chain     STED2D
N11  C                   eval      Hentes ='<font color=red>'
N11  C                                    + 'Hentes: '+ST2KOD
N11  C                                    +%trim(ST2NVN)+'</font>'
N11  C                                    +'<BR/>Avs:'
N11  C                   endif
N11  C                   if        HESDVT <> *blanks
N11  C                   eval      ST2LK  = HELKK
N11  C                   eval      ST2KOD = HESDVT
N11  c                   eval      ST2NVN = *blanks
N11  c     Sted2dKey     chain     STED2D
N11  C                   eval      Leveres='<font color=red>'
N11  C                                    + 'Leveres: '+ST2KOD
N11  C                                    +%trim(ST2NVN)+'</font>'
N11  C                                    +'<BR/>Mot:'
N11  C                   endif
N13   * Ren HO - UTEN DUP under - men Laste/lossestde utfylt ??
N13  C                   if        Hentes = *blanks and HESDLA <> *blanks
N13  C                   eval      Hentes ='<font color=red>'
N13  C                                    + 'Lastested: '+HESDLA+'</font>'
N13  C                                    +'<BR/>Avs:'
N13  C                   endif
N13  C                   if        Leveres= *blanks and HESDL  <> *blanks
N13  C                   eval      Leveres='<font color=red>'
N13  C                                    + 'Lossested: '+HESDL+'</font>'
N13  C                                    +'<BR/>Mot:'
N13  C                   endif
N11   * ved FF - avvikende leveringssted
N11  C                   when      trslag = 'FF'
N11  C                   eval      ST2LK  = HETRI
N11  C                   eval      ST2KOD = HESDT
N11  c                   eval      ST2NVN = *blanks
N11  c     Sted2dKey     chain     STED2D
N11  C                   eval      Leveres='<font color=red>'
N11  C                                    + 'Leveres: '+ST2KOD
N11  C                                    +%trim(ST2NVN)+'</font>'
N11  C                                    +'<BR/>Mot:'
N11   * ved VF - avvikende hentested
N11  C                   when      trslag = 'VF'
N11  C                   eval      ST2LK  = HELKA
N11  C                   eval      ST2KOD = HESDF
N11  c                   eval      ST2NVN = *blanks
N11  c     Sted2dKey     chain     STED2D
N11  C                   eval      Hentes ='<font color=red>'
N11  C                                    + 'Hentes: '+ST2KOD
N11  C                                    +%trim(ST2NVN)+'</font>'
N11  C                                    +'<BR/>Avs:'
N11  C                   endsl
N11   * HO
N11  c                   endsr
N11   *
N09  C***********************************************
N09  C     MeldTrans     BEGSR
N09  C***********************************************
N12   * hent info om farlig gods som langs streng (stolpe skille den enkelte linje)
N12  C                   call      'SYGE129'
N12  C                   PARM                    HEAVD
N12  C                   PARM                    HEOPD
N12  C                   PARM                    NullFBNR          3 0
N12  C                   PARM                    UADR           5000
N12  C                   PARM      '0'           UFINS             1
N12   /free
N12     // evt Farlig gods-merknad FØRST.
N12      if UADR  <> *blanks;
           UADR = %ScanRpl( ' | ': '</br>': UADR );
N12        if Sprak = 'EN';
N12          MLDTRA = '<div><b>Notes:</b> '+UADR;
N12        else;
N12          MLDTRA = '<div><b>Merkn:</b> '+UADR;
N12        endif;
N12      endif;
N12     // deretter andre meldinger til transportør
N09   /free
N09    setll (heavd : heopd : 'G' ) FREETXT2;
N09    reade (heavd : heopd : 'G' ) FREETXT2;
N09    dow not %eof(FREETXT2);
N09      if MLDTRA = *blanks;
N09        if Sprak = 'EN';
N09          MLDTRA = '<div><b>Notes:</b> '+FRTTXT;
N09        else;
N09          MLDTRA = '<div><b>Merkn:</b> '+FRTTXT;
N09        endif;
N09      else;
N09          MLDTRA = %trim(MLDTRA)+'</br>'+FRTTXT;
N09      endif;
N09      reade (heavd : heopd : 'G' ) FREETXT2;
N09    enddo;
N09    if MLDTRA <> *blanks;
N09      MLDTRA = %trim(MLDTRA)+'</div>';
N09    endif;
N09   /end-free
N09   *
N09  c                   endsr
N09   *
N04  C***********************************************
N04  C     ChkDocLnk     BEGSR
N04  C***********************************************
N04   *
N04   *  Finnes utlev.dokument = Dok av type ZU (hardkodet foreløpig)
N04  c                   eval      DocLnk = *blanks
     C     arkivkeyZU    KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    ARTYPE
     c                   move      'A'           ARSTAT
     c                   move      'ZU'          ARTYPE
     C     ARKEXTK       KLIST
     C                   KFLD                    ARFIRM
     C                   KFLD                    ARCEXT
     c     arkivkeyZU    chain     arkivl02                           33        (inneh. ARFIRM )
     c                   if        *in33=*off
     c     artype        chain     arktxt                             33
     C                   MOVE      ARKLAG        ARCEXT
     C     ARKEXTK       CHAIN     ARKEXT                             33
     C   33ARFIRM        CHAIN     FIRMARC                            33
N06  c     arbhis        ifne      *blanks
N06  c                   eval      arcane=arbhis
N06  c                   end
N04  c                   eval      DocLnk = '</br>'
N04  c                             +'<a class="btn btn-primary" href="'
N01  C                             +'http://'+%trim(arcpae)+'/'
N01  c                             +%trim(arcane)+'/'+%trim(ArLink)
N01  c                             +'">Se utlev.dok.</a>'
N04   *
     c                   endif
N04  c                   endsr
N04   *
      *
      *
     C***********************************************
     C     SRAvvik       BEGSR
     C***********************************************
     C                   MOVE      'TKHLA'       PAID
N02  C                   if        sprak <> *blanks
N02  C                   MOVE      'TKHLE'       PAID
N02  C                   endif
      *
     c     1             do        5             An                1 0
      * input avvik1-avvik5 burde jo ligge i array men skit au..
     c                   select
     c     An            wheneq    1
     c                   movel     Avvik1        TstAvv           50
     c     An            wheneq    2
     c                   movel     Avvik2        TstAvv
     c     An            wheneq    3
     c                   movel     Avvik3        TstAvv
     c     An            wheneq    4
     c                   movel     Avvik4        TstAvv
     c     An            wheneq    5
     c                   movel     Avvik5        TstAvv
     c                   endsl
      *
     c                   move      An            AnA               1
     C                   callp     updhtmlvar('ANA':ANA)
     C                   callp     wrtsection('AVtop')
N02  C                   select
N02  C                   when      sprak = 'EN'
N02  c                   eval      PAVRDA='-Deviaton '+AnA+ '- and/or text'
N02  C                   other
     c                   eval      PAVRDA='-Avvik '+AnA+ '- og/el.fritxt'
N02  C                   endsl
     c                   move      *blank        AVSEL             8
     C                   if        TSTAVV= *blanks
     c                   movel     'Selected'    AVSEL             8
     C                   endif
     C                   callp     updhtmlvar('AVKOD':' ')
     C                   callp     updhtmlvar('AVTXT':PAVRDA)
     C                   callp     updhtmlvar('AVSEL':AVSEL)
     C                   callp     wrtsection('AVrow')
     C     FiParKey      setll     BRPARF
     C     FiParKey      reade     BRPARF                                 33
     C     *in33         doweq     '0'
     c                   move      *blank        AVSEL             8
     C                   if        TSTAVV= PAVRDA
     c                   movel     'Selected'    AVSEL             8
     C                   endif
     C                   callp     updhtmlvar('AVKOD':PAVRDA)
     C                   callp     updhtmlvar('AVTXT':PAVRDA)
     C                   callp     updhtmlvar('AVSEL':AVSEL)
     C                   callp     wrtsection('AVrow')
     C     FiParKey      reade     BRPARF                                 33
     c                   enddo
     C                   callp     updHTMLvar('Komm2':Komm2)
     C                   callp     updHTMLvar('Komm3':Komm3)
     C                   callp     updHTMLvar('Komm4':Komm4)
     C                   callp     updHTMLvar('Komm5':Komm5)
      * input avvik1-avvik5 burde jo ligge i array men skit au..
     c                   select
     c     An            wheneq    1
     C                   callp     updHTMLvar('KomTxt':Komm1)
     c     An            wheneq    2
     C                   callp     updHTMLvar('KomTxt':Komm2)
     c     An            wheneq    3
     C                   callp     updHTMLvar('KomTxt':Komm3)
     c     An            wheneq    4
     C                   callp     updHTMLvar('KomTxt':Komm4)
     c     An            wheneq    5
     C                   callp     updHTMLvar('KomTxt':Komm5)
     c                   endsl
     C                   callp     wrtsection('AVBot')
     c                   enddo
      *
     C                   endsr
      *
     C***********************************************
     C     SRTT          BEGSR
     C***********************************************
     c     '*':'_'       xlate     SND           SND
     c*                  if        Sted = '- Evt Sted/Term -'
     c*                  eval      Sted = *blanks
     c*                  endif
      *Via DS -> Idag 8.0 dato  + IdagTid 6.0 klokke
     C                   movel     timedata1     TimeTmp
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
      *
     c     Event         ifeq      'POD'
     c     HeadKey       Chain     Headf                              30
     c     *in30         ifeq      '0'
     c     Hesgm         IFEQ      *blanks
     c     Hesgm         oreq      'X'
     c     Hesgm         oreq      'LEVERT'
     C                   movel     Sign          HESGM
     C                   movel     Idag          HEDTMO
     C                   movel     IdagTid       HEKLMO
     C                   end
     C                   update    headfr
     C                   end
      * Actual time of ARRIVAL  i trackt
     c     HeadKey       chain     Trackt                             33
     C     *in33         Ifeq      *OFF
     C     TXATAD        andeq     *zeros
     c                   movel     Idag          TXATAD
     c                   movel     IdagTid       TXATAK
     c                   update    tracktr
     C                   end
     C                   end
      * denne SR logger COL TE2 Hentet på Term - TE2 Levret Term - POD
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   MOVE      Idag          TTDATL
     C                   MOVE      IdagTID       TTTIML
     C                   MOVE      Idag          TTDATE
     C                   MOVE      IdagTID       TTTIME
     C                   MOVEL     WSUser        TTUSER
     c                   if        TelefonNr<>*blanks
     c                   eval      TTUSER='T:'+TelefonNr
     c                   endif
     C                   MOVEL     Sted          TTDEPO
     C                   eval      TTNAME = Sign
     c                   select
     c     Event         wheneq    'POD'
     C                   MOVE      'POD'         TTACTI
     C                   eval      TTTEXT = 'Signed by '+Sign
     C                   eval      TTTEXL = 'Kvittert av '+Sign
     c     Event         wheneq    'COL'
     C                   MOVE      'COL'         TTACTI
s08  C*                  eval      TTTEXT = 'Collected at customer'
s08  C*                  eval      TTTEXL = 'Hentet hos kunden'
N08  C                   eval      TTTEXT = 'Collected at sender '
N08  C                   eval      TTTEXL = 'Hentet hos avsender '
     c     Event         wheneq    'LET'
     C                   MOVE      'TE2'         TTACTI
     C                   eval      TTTEXT = 'Delivered terminal'
     C                   eval      TTTEXL = 'Levert terminal'
     c     Event         wheneq    'HET'
     C                   MOVE      'TE2'         TTACTI
     C                   eval      TTTEXT = 'Collected at terminal'
     C                   eval      TTTEXL = 'Hentet på terminal'
     c                   endsl
N10
N10   *   Dersom den finmnes fra før med eksakt samme dato/klokke - hopp ut
N10  C                   CALL      'SYGE46'
N10  C                   PARM                    TTAVD
N10  C                   PARM                    TTOPD
N10  C                   PARM                    TTFBNR
N10  C                   PARM                    TTACTI
N10  C                   PARM      'S'           Type              1
N10  C                   PARM      *zeros        SiDATE            8 0
N10  C                   PARM      *zeros        SiTIME            6 0
N10  C                   PARM      *zeros        antall            5 0
N10  c                   if        antall <> *zeros
N10  c                   move      sidate        gmdate
N10  c                   move      sitime        gmtime
N10  c                   move      ttdate        nydate
N10  c                   move      tttime        nytime
N10  c                   move      999999        TimeDiff         15 0
N10   * corny metode men enkel (slipper å konvertere til iso-dato..) Diff 100 = 1 minutt
N10   *    2020.08.02 16:04:03 - 2020.08.02 16:03:52  =  51 (reelt 10 sekund) = innafor
N10   *    2020.08.02 16:04:03 - 2020.08.02 16:03:03  = 100 (reelt 60 sekund) = innafor
N10  c                   eval      TimeDiff=NyDttm-Gmdttm
N10  c                   if        TimeDiff<101
N10  c                   goto      UtSRTT
N10  c                   endif
N10  c                   endif
      * Tillegs-info
     c     Sted          ifne      *blanks
     C                   eval      TTTEXT = %trim(TTTEXT)+' ('
     c                             + %trim(Sted)+')'
     C                   eval      TTTEXL = %trim(TTTEXL)+' ('
     c                             + %trim(Sted)+')'
     c                   endif
     c     Sign          ifne      *blanks
     c     Event         andne     'POD'
     C                   eval      TTTEXT = %trim(TTTEXT)+', Signed by '
     c                             + %trim(Sign)
     C                   eval      TTTEXL = %trim(TTTEXL)+', Kvittert av '
     c                             + %trim(Sign)
     c                   endif
     c     LatN          Ifne      *zeros
     c     LngN          andne     *zeros
     C                   EVAL      TTTEXT = %trim(TTTEXT) +
     c                             ' Lat: ' + %trim(%editc(LatN:'3')) +
     c                             ' Lon: ' + %trim(%editc(LngN:'3'))
     C                   EVAL      TTTEXL = %trim(TTTEXL) +
     c                             ' Lat: ' + %trim(%editc(LatN:'3')) +
     c                             ' Lon: ' + %trim(%editc(LngN:'3'))
     C                   end
      *
     c                   move      'S'           TTMANU
     c                   move      'APP'         TTEDRE
     C                   WRITE     TRACKFR
      * Oppdatere alle andre oppdrag i evt dup-struktur
     c                   exsr      DupOppd
      *
      * Eventuelle avvik
     c                   exsr      SRTTAvv
      *
N10  c     UtSRTT        tag
     C                   ENDSR
      *
      **************************************************************************
     C     SRTTAVV       begsr
      **************************************************************************
     c                   move      'AVV'         TTACTI
     c                   move      'APP'         TTEDRE
      *
     c     Avvik1        ifne      *blanks
     c     Komm1         orne      *blanks
     c     Avvik1        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik1) +' / '+ Komm1
     c                   eval      TTTEXL = %trim(Avvik1) +' / '+ Komm1
     c                   else
     c                   eval      TTTEXT = Komm1
     c                   eval      TTTEXL = Komm1
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
      *
     c     Avvik2        ifne      *blanks
     c     Komm2         orne      *blanks
     c     Avvik2        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik2) +' / '+ Komm2
     c                   eval      TTTEXL = %trim(Avvik2) +' / '+ Komm2
     c                   else
     c                   eval      TTTEXT = Komm2
     c                   eval      TTTEXL = Komm2
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
      *
     c     Avvik3        ifne      *blanks
     c     Komm3         orne      *blanks
     c     Avvik3        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik3) +' / '+ Komm3
     c                   eval      TTTEXL = %trim(Avvik3) +' / '+ Komm3
     c                   else
     c                   eval      TTTEXT = Komm3
     c                   eval      TTTEXL = Komm3
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
      *
     c     Avvik4        ifne      *blanks
     c     Komm4         orne      *blanks
     c     Avvik4        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik4) +' / '+ Komm4
     c                   eval      TTTEXL = %trim(Avvik4) +' / '+ Komm4
     c                   else
     c                   eval      TTTEXT = Komm4
     c                   eval      TTTEXL = Komm4
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
      *
     c     Avvik5        ifne      *blanks
     c     Komm5         orne      *blanks
     c     Avvik5        ifne      *blanks
     c                   eval      TTTEXT = %trim(Avvik5) +' / '+ Komm5
     c                   eval      TTTEXL = %trim(Avvik5) +' / '+ Komm5
     c                   else
     c                   eval      TTTEXT = Komm5
     c                   eval      TTTEXL = Komm5
     c                   endif
     C                   WRITE     TRACKFR
     c                   exsr      DupOppd
     c                   endif
     C                   ENDSR
      *
      **************************************************************************
     C     DupOppd       begsr
      **************************************************************************
     C                   MOVE      'KOP'         TTEDRE                         KOPI FRA ANNET...
      * Dupliser hendelse til alle oppdrag i "DUP-struktur"
     c     1             do        100           Nr
     c     Dup(Nr)       cabeq     *zeros        UtDupArr
     c                   move      Dup(Nr)       AvdOpd
     C     DsAvd         IFNE      TTAVD
     C     DsOpd         orne      TTOPD
     C                   MOVE      TTAVD         TmpAVD            4 0
     C                   MOVE      TTOPD         TmpOPD            7 0
     C                   MOVE      DsAvd         TTAVD
     C                   MOVE      DsOpd         TTOPD
     C                   WRITE     TRACKFR
     C                   MOVE      TmpAVD        TTAVD
     C                   MOVE      TmpOPD        TTOPD
      * dersom POD oppdater også oppdrag....
     C                   ENDIF
     c                   enddo
     C     UtDupArr      tag
     C                   MOVE      *BLANKS       TTEDRE                         EVENT PÅ DETTE ..
     C                   ENDSR
      *
      **************************************************
     C     INIT          begsr
      **************************************************
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     c     *in50         cabeq     '1'           UtInit
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      FIRM
     C                   OPEN      TRACKF
     C                   OPEN      TRACKT
     C                   OPEN      HEADF
     C                   OPEN      BRPARF
N04  C                   OPEN      ARKIVL02
N04  C                   OPEN      FIRMARC
N04  C                   OPEN      ARKTXT
N04  C                   OPEN      ARKEXT
N09  C                   OPEN      FREETXT2
N11  C                   OPEN      STED2D
     C     HeadKey       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FS1Key        KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
      *
     C     DsHeaKey      KLIST
     C                   KFLD                    DsAVD
     C                   KFLD                    DsOPD
     C     HEAKY0        KLIST
     C                   KFLD                    TRAVD0
     C                   KFLD                    TROPD0
N11  C     Sted2dKey     KLIST
N11  C                   KFLD                    ST2LK
N11  C                   KFLD                    ST2KOD
     C     FiParKey      klist
     C                   kfld                    FIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   movel     '*KUNDFT*'    FIBRID           10
     C                   move      BIFIRM        FIBRID
     c
     C     BIFIRM        CHAIN     FIRM                               33
      *
     C     UtInit        ENDSR
      *
      *
      **************************************************
     C     SNDOPD        begsr
      **************************************************
      * finn - avd oppdrag
      * sedningsnummer er ...
      * enten AVD*OPD eller AVD_OPD eller Send.nr
     c                   move      *zeros        f                 3 0
     C     '*'           SCAN      Snd           f
     c     f             ifeq      *zeros
     C     '_'           SCAN      Snd           f                              Ikke A*O, prøv A_O
     c                   endif
     c     f             cabeq     *zeros        UtEopdSnd                      Ikke A*O/A_O prøv sn
     c                   eval      avdaa = %SUBST(Snd:1:f-1)
     c                   eval      opdaa = %SUBST(Snd:f+1:20-f-1)
     C                   eval      HEAVD   = c2n2(avdaa)
     C                   eval      HEOPD   = c2n2(opdaa)
      * avd*opd er mottatt . gjør om til AAAA_BBBBBBB
     c                   movel     '    _    '   Avop12           12
     c                   movel     HEAVD         Avop12
     c                   move      HEOPD         Avop12
     c                   eval      snd = Avop12
      * SND   - Hent via skutt sendingsnr
     c     UtEopdSnd     tag
     c                   move      snd           DSsnd2
     c                   eval      snd17 =       snd
     c                   eval      snd20 =       snd
     c     DSSnd1_3      ifeq      '401'
     c                   eval      snd17 =       DSsnd4_20
     c                   else
     c     DSSnd17_17    ifne      *blanks
     c     DSSnd18_18    andeq     *blanks
     c                   eval      snd20 =       '401'+snd
     c                   end
     c                   end
     C                   OPEN      HEAD39
     c     snd20         chain     Head39                             33
     C                   CLOSE     HEAD39
     c     *in33         ifeq      *on
     c                   eval      FSKODE = 'IFB'
     c                   eval      FSSOK  = snd17
     C                   OPEN      FRISOL01
     c     FS1Key        chain     FRISOL01                           33
     C                   CLOSE     FRISOL01
     C  n33              eval      HEAVD   = FSavd
     C  n33              eval      HEOPD   = FSopd
     c                   end
      *
     c     HEOPD         cabeq     *zeros        UtOpd                          Ikke A*O/A_O prøv sn
      * Finn ut om oppdraget er en del av en "DUP-struktur" - legg alle i ARRAY
     C* Hent opp ingangsoppdraget
     C     HeadKey       CHAIN(N)  HEADF                              33
     c     *in33         ifeq      *on
     C                   eval      HEAVD   = 0
     C                   eval      HEOPD   = 0
     c                   goto      UtOpd
     c                   endif
      *
     C                   move      HEAVD         gmavd             4 0
     C                   move      HEOPD         gmopd             7 0
      * FINN TOPP-NIVÅET
     C     TROPD0        DOWNE     0
     C     HEAKY0        CHAIN     HEADF                              33
     C                   ENDDO
     C* Legg inn topp-oppdraget som start i array
     c                   clear                   DUP
     c                   z-add     1             Nr                3 0
     c                   movel     heavd         Dup(Nr)
     c                   move      heopd         Dup(Nr)
     c     1             do        100           Cur               3 0
     c     Dup(Cur)      cabeq     *zeros        UtDup
     c                   move      Dup(Cur)      AvdOpd
     C     DSHeaKey      CHAIN     HEADF                              33
     C  N33TrOpd1        IFNE      *ZEROS
     C                   add       1             Nr
     c                   movel     TrAvd1        Dup(Nr)
     c                   move      TrOpd1        Dup(Nr)
     C                   ENDIF
     C  N33TrOpd2        IFNE      *ZEROS
     C                   add       1             Nr
     c                   movel     TrAvd2        Dup(Nr)
     c                   move      TrOpd2        Dup(Nr)
     C                   ENDIF
     c                   enddo
     C     Utdup         tag
     C                   move      gmAVD         heavd
     C                   move      gmOPD         heopd
     C     HeadKey       CHAIN(N)  HEADF                              33
N08  C                   if        Submited  <> 'Y'
N07  c                   exsr      GetNxtEvent
N07  c                   if        NextActKod <> 'FER'
N07  c                   eval      Event = NextActKod
N07  c                   endif
N08  c                   endif
     C     UtOpd         ENDSR
      **************************************************
     C     SignSR        begsr
      **************************************************
     C                   eval      rc = docmd(' +
     C                             dltf FILE(QTEMP/Q)  +
     C                             ')
     C                   eval      rc = docmd(' +
     C                             CRTPF FILE(QTEMP/Q) RCDLEN(32000) +
     C                             ')
     C                   OPEN      Q
     C                   EXCEPT    QData
     C                   close     Q
     c                   eval      SndL=Snd
      *
     c     Event         ifne      'POD'
     c                   eval      SndL=Event+'_'+Snd
     c                   endif
     C                   eval      rc = docmd(' +
     C                             CPYTOSTMF  +
     C                             FROMMBR("/QSYS.LIB/QTEMP.LIB/Q.FILE/Q.MBR") +
     C                             TOSTMF("/syspeddev/tksigntmp/'+%trim(SndL)+
     C                             '.txt") STMFOPT(*REPLACE) +
     C                             CVTDTA(*AUTO) STMFCODPAG(*PCASCII) +
     C                             ENDLINFMT(*CRLF) +
     C                             ')
      * endre til full *RWX-tilgang (kan ikke være sikker på rettighetene
      *  til den user som har åpnet nettverksstasjon/ starter retegne-prog.
      *  ellers måtte denne ha *ALLOBJ-tilgang. dette  er nå unødig )
     C                   eval      rc = docmd(' +
     c                             CHGAUT +
     C                             OBJ("/syspeddev/tksigntmp/'+%trim(SndL)+
     C                             '.txt") USER(*PUBLIC) DTAAUT(*RWX)+
     C                             ')
     C                   ENDSR
     OQ         EADD         Qdata
     O                       Fil              32000
