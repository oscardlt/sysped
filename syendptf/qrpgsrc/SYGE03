      **************************************************************
      * OBS!!! SYGE13 MÅ HOLDES SYNKRONT!!!!!!!
      *        SYGE03 KAN MED TIDEN "FASES UT"????
      *
      *****************************************************************
      *** PROGRAM SKAL BEREGNE FRAKT AVDELING OG SONE LOGIKK        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 27-32 34-99                            ***
      *****************************************************************
PXXXXX* 01 JOV PTF10 half adjust på M3 mm
PXXXXX* 01 JOV PTF10 GRUNNPRIS-logikk samt MINIM på %% (tatt inn fra NF  )
      * 02 JOV PTF10 avvikene innh-/utkj-sone ved over xxx kilo
      * 03 JOV PTF10 Ta hensyn til grunnpris også ved NOPNR - KUN VED pris pr XXX
P10034* 04 JOV PTF10 INNLAND/NOPNR-BEREGN  KUN DERSOM TAB FINNES(kan være kun for framf-tider)
P10034* 05 SH  PTF10 ny parameter avtaletype
P10034* 06 SH  PTF11 avvik tabell ved over en fraktvekt
      * 07 SH  PTF11 TEST PÅ UTLØPSDATO
      *****************************************************************
      *
     FHEADF     IF   E           K DISK
     FSTED2D    IF   E           K DISK
     FSTEDIL02  IF   E           K DISK
     FBASES     IF   E           K DISK
     FSONET     IF   E           K DISK
     FSONES     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FAVFRH     IF   E           K DISK    USROPN
     FAVFRHL03  IF   E           K DISK
     F                                     RENAME(AVFRHR:AVFRHR3)
     FAVFRD     IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK
     FUSRDFT    IF   E           K DISK
     FNOPNR     IF   E           K DISK
N01  FNOPNRK    IF   E           K DISK
     D* Datastruktur for Å HENTE USER
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D                 DS
     D  FE                     1      5
     D                                     DIM(5)
     D  FELT                   1      5
     D                 DS
     D  F2                     1      5
     D                                     DIM(5)
     D  FELT2                  1      5
     D                 DS            11
     D NUM             C                   CONST(' 0123456789')
      *
     C     *ENTRY        PLIST
     C                   PARM                    GEBKO             3
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UVAL              3
     C                   PARM                    UBELØP           13 2
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    UOMRL             5 0
     C                   PARM                    UOMRM             5 0
     C                   PARM                    UOMR1             5 0
     C                   PARM                    UOMR2             5 0
     C                   PARM                    UKUNDE            8 0
     C                   PARM                    UFAVNR            9 0
N01  C                   PARM                    AVTALT            1
      *
     C                   Move      UOMRL         UUOMRL            5 0
     C                   Move      UOMRM         UUOMRM            5 0
     C                   Move      UOMR1         UUOMR1            5 0
     C                   Move      UOMR2         UUOMR2            5 0
     c     Usone         ifeq      'G    '
     c                   move      *blanks       Usone
     c                   move      'J'           GrPrJN            1
     c                   end
      *
     C     HEAKEY        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     FORKEY        KLIST
     C                   KFLD                    XXXCUS            8 0
     C                   KFLD                    HELKS
     C                   KFLD                    HESDFF
     C     FORKEE        KLIST
     C                   KFLD                    XXXCUS
     C                   KFLD                    HELKS
     C     VIDKEY        KLIST
     C                   KFLD                    XXXCUS
     C                   KFLD                    HELKK
     C                   KFLD                    HESDVT
     C     VIDKEE        KLIST
     C                   KFLD                    XXXCUS
     C                   KFLD                    HELKK
     C     HOVKEY        KLIST
     C                   KFLD                    HELKS
     C                   KFLD                    HESDF
     C                   KFLD                    HELKK
     C                   KFLD                    HESDT
     C     SONEFK        KLIST
     C                   KFLD                    HELKS
     C                   KFLD                    HESDFF
     C     SONETK        KLIST
     C                   KFLD                    HELKK
     C                   KFLD                    HESDVT
     C     SONKE1        KLIST
     C                   KFLD                    HEKDPL
     C                   KFLD                    UAVD
     C                   KFLD                    SONTAB            5
     C                   KFLD                    HEFBV
     C     SONKE2        KLIST
     C                   KFLD                    HEKDPL
     C                   KFLD                    UAVD
     C                   KFLD                    SONTAB            5
     C     SONKE3        KLIST
     C                   KFLD                    HEKDPL
     C                   KFLD                    UAVDNU
     C                   KFLD                    SONTAB            5
     C     SONK1B        KLIST
     C                   KFLD                    HEKDPL
     C                   KFLD                    UAVDNU            4 0
     C                   KFLD                    SONTAB            5
     C                   KFLD                    HEFBV
     C     SONK2B        KLIST
     C                   KFLD                    HEKDPL
     C                   KFLD                    UAVDNU            4 0
     C                   KFLD                    SONTAB            5
      *
     C     AV3KEY        KLIST
     C                   KFLD                    AFKUND
     C                   KFLD                    AFLKFR
     C     AVKEY         KLIST
     C                   KFLD                    AFKUND
     C                   KFLD                    AFPROD
     C                   KFLD                    AFAVDE
     C                   KFLD                    AFLKFR
     C                   KFLD                    SONTAB
     C     AVKEYE        KLIST
     C                   KFLD                    AFKUND
     C                   KFLD                    AFPROD
     C                   KFLD                    AFAVDE
     C                   KFLD                    AFLKFR
     C     VKTKEY        KLIST
     C                   KFLD                    AFAVNR
     C                   KFLD                    AFCDE1
     C     VKTKE2        KLIST
     C                   KFLD                    AFAVNR
     C                   KFLD                    AFCDE1
     C                   KFLD                    HEFBV
      *
     C                   MOVE      *ZEROS        XXXCUS
     C     GEBKO         IFEQ      'POS'
     C                   MOVE      99999998      XXXCUS
     C                   MOVE      'FRA'         GEBKO
     C                   END
      *
     C                   MOVE      *ZEROS        SOTIL1            5 0
     C                   MOVE      *ZEROS        SOTIL2            5 0
     C                   MOVE      *ZEROS        SOHOVE            5 0
     C     *LIKE         DEFINE    XBELØP        INNHPR
     C     *LIKE         DEFINE    XBELØP        UTKJPR
     C     *LIKE         DEFINE    XBELØP        HOVEPR
     C                   MOVE      *ZEROS        INNHPR
     C                   MOVE      *ZEROS        UTKJPR
     C                   MOVE      *ZEROS        HOVEPR
      *
     C     HEAKEY        CHAIN     HEADF                              33
     C  N33              Z-ADD     HEFBV         XXFBV             9 0
      * I transportoppdrag ligger "ytterpunktenee" I HESDF / HESDT
      * Disse legges over i..FF/..VT -"Basested" legges i HESDF / HESDT
     C     HEUR          IFEQ      'H'
     C                   MOVE      HESDF         HESDFF
     C                   MOVE      HESDT         HESDVT
     C                   MOVE      HELKA         HELKS
     C                   MOVE      HETRI         HELKK
     C     SONEFK        CHAIN     STED2D                             33
     C                   MOVE      ST2KO2        HESDF
     C                   MOVE      ST2LK         HELKA
     C     SONETK        CHAIN     STED2D                             33
     C                   MOVE      ST2KO2        HESDT
     C                   MOVE      ST2LK         HETRI
     C                   END
     C     KOAKEY        KLIST
     C                   KFLD                    XAUNI
     C                   KFLD                    UAVD
     C                   MOVE      'A'           XAUNI             1
     C     KOAKEY        CHAIN     KODTA                              33
     C   33              MOVE      ' '           KOAIE
      *
      * FORENKLET PRISOPPSLAG VED REN INNLAND - KUN EN FIL...
     C     KOAIE         IFGE      '1'
     C     KOAIE         ANDLE     '3'
     C     USONE         ANDEQ     *Blanks
     C                   MOVE      'J'           INNLAND           1
     C     zuser         CHAIN     usrdft                             33
     C   33              MOVE      ' '           INNLAND
     C     INNLAND       IFEQ      'J'
     C                   movel     HESDVT        TSTPNR            4
     C     KOAIE         IFeq      '2'
     C                   movel     HESDFF        TSTPNR
     C                   END
     C     TSTPNR        IFge      '0000'
     C     TSTPNR        andle     '9999'
     C                   move      TSTPNR        NOPONR
     C                   END
     C     InnlKey       KLIST
     C                   KFLD                    ZZTERM
     C                   KFLD                    NOPONR
     C     InnlKey       CHAIN     NOPNR                              33
     C     *IN33         IFEQ      *OFF
     C                   select
     C     HEKDPL        wheneq    ' '
     C                   movel     NOHTAB        SONTAB
     C     HEKDPL        wheneq    NOAPR1
     C                   movel     NOATA1        SONTAB
     C     HEKDPL        wheneq    NOAPR2
     C                   movel     NOATA2        SONTAB
     C     HEKDPL        wheneq    NOAPR3
     C                   movel     NOATA3        SONTAB
     C     HEKDPL        wheneq    NOAPR4
     C                   movel     NOATA4        SONTAB
     C     HEKDPL        wheneq    NOAPR5
     C                   movel     NOATA5        SONTAB
     C                   endsl
N01   *
N01   * Kundeavvik på ren "standardtabell" i NOPNR ???
N01   * denne KAN gjelde bare utgående / bare inngående eller begge veier
N01   * (dette KUNNE være bygd som rene postal kundeavtale med mengder av postnr-soner...
N01   *  Hensikten med å legge det i en flat fil er forenklet datautveksling med EXCEL)
N01  C     KunInnlKey    KLIST
N01  C                   KFLD                    UKUNDE
N01  C                   KFLD                    ZZTERM
N01  C                   KFLD                    NOPONR
N01  C                   KFLD                    NOKUUI
N01  C     KunInnlKey    CHAIN     NOPNRK                             33
N01  C   33              move      ' '           NOKUUI
N01  C   33KunInnlKey    CHAIN     NOPNRK                             33
N01  C     *IN33         IFEQ      *OFF
N01  C                   select
N01  C     HEKDPL        wheneq    ' '
N01  c     NKHTAB        andne     *blanks
N01  C                   movel     NKHTAB        SONTAB
N01  C     HEKDPL        wheneq    NKAPR1
N01  c     NKATA1        andne     *blanks
N01  C                   movel     NKATA1        SONTAB
N01  C     HEKDPL        wheneq    NKAPR2
N01  c     NKATA2        andne     *blanks
N01  C                   movel     NKATA2        SONTAB
N01  C     HEKDPL        wheneq    NKAPR3
N01  c     NKATA3        andne     *blanks
N01  C                   movel     NKATA3        SONTAB
N01  C     HEKDPL        wheneq    NKAPR4
N01  c     NKATA4        andne     *blanks
N01  C                   movel     NKATA4        SONTAB
N01  C     HEKDPL        wheneq    NKAPR5
N01  c     NKATA5        andne     *blanks
N01  C                   movel     NKATA5        SONTAB
N01  C                   endsl
N01  C                   end
N04  c                   endif                                                  Fins i NOPNR
     C* Hentpris  - Har funnet postnr i NOPNR (evt NOPNRK) MED SONETABELL ULIK BLANK-HENT/hopp ut
N04  c     SONTAB        ifne      *blanks
     C                   EXSR      HENTPR
     C                   Goto      INNLpris
     C                   END
     C                   END                                                    END INNLAND = J
     C                   END                                                    END KOAIE 1-3
      *
      * Ren innland er vedlikeholdt??? (finnes minst en post 99999999)
     C     XXXCUS        IFEQ      *ZEROS
     C     HELKS         andeq     'NO'
     C     HELKK         andeq     'NO'
     C                   MOVE      99999999      XXXCUS
     C     FORKEE        CHAIN     STEDIL02                           33
     C   33              MOVE      *zeros        XXXCUS
     C                   END
      *
      * HENT BASESTED (I STEDET FOR Å STOLE PÅ DET I HESDF/HESDT)
     C     FORKEY        SETLL     STEDIL02
     C     FORKEE        READE     STEDIL02                               33
     C  N33STINPF        IFLE      HESDFF                                         3B
     C                   MOVE      STINBA        HESDF
     C                   END
     C     VIDKEY        SETLL     STEDIL02
     C     VIDKEE        READE     STEDIL02                               33
     C  N33STINPF        IFLE      HESDVT                                         3B
     C                   MOVE      STINBA        HESDT
     C                   END
      *
      *
      *
      * FORFRAKT
      *
     C     GEBKO         IFEQ      'FR1'                                        1B
     C     HEUR          OREQ      'H'
     C     USONE         IFEQ      *BLANKS                                       2B
      * Sone er ikke oppgitt. ---> Finn sone
     C     FORKEY        SETLL     STEDIL02
     C     FORKEE        READE     STEDIL02                               33
     C  N33STINPF        IFLE      HESDFF                                         3B
      * STINS1 = Sonested funnet
N02  c     STINSL        ifne      *zeros
N02  c     HEFBV         andgt     STINSL
N02  C                   move      stins3        stins1
N02  c                   endif
     C* NÅR RENT NUMERISK TABELLNAVN, ADDER TIL HOVEDSONE
     C     HEUR          IFNE      'H'
     C     STINS1        andeq     *blanks
     C                   move      stinba        stins1
     C                   end
     C     NUM           CHECK     STINS1        NUMJN             1 0
     C     NUMJN         IFEQ      0
     C                   MOVE      STINS1        FELT
     C                   EXSR      NUMKON
     C                   Z-ADD     NUMVRD        SOTIL1
     C                   ELSE                                                      4X
      *Alfa sone:rent prisoppslag, ved tr.mod bevar beløp i variabel
     C                   MOVE      STINS1        SONTAB
     C                   EXSR      HENTPR
     C     HEUR          IFEQ      'H'                                              5B
      * I TRMOD. GJØRES INNTIL 3 OPPSLAG PÅ ETT KALL. SJEKK RAB.V/HVERT
     C                   EXSR      TRARAB
     C                   Z-ADD     XBELØP        INNHPR
     C                   MOVE      *ZEROS        XBELØP
     C                   END                                                        5E
     C                   END                                                       4E
     C                   END                                                      3E
     C                   ELSE                                                    2X
      * Sone er oppgitt.
     C                   MOVE      USONE         SONTAB
     C                   EXSR      HENTPR
     C                   END                                                     2E
     C                   END                                                    1E
      *
      * VIDEREFRAKT
      *
     C     GEBKO         IFEQ      'FR2'                                        1B
     C     HEUR          OREQ      'H'
     C     USONE         IFEQ      *BLANKS                                       2B
      * Sone er ikke oppgitt. ---> Finn sone
     C     VIDKEY        SETLL     STEDIL02
     C     VIDKEE        READE     STEDIL02                               33
     C  N33STINPF        IFLE      HESDVT                                         3B
      * STINS2 = SONESTED  FUNNET
N02  c     STINSL        ifne      *zeros
N02  c     HEFBV         andgt     STINSL
N02  C                   move      stins4        stins2
N02  c                   endif
     C* NÅR RENT NUMERISK TABELLNAVN, ADDER TIL HOVEDSONE
     C     HEUR          IFNE      'H'
     C     STINS2        andeq     *blanks
     C                   move      stinba        stins2
     C                   end
     C     NUM           CHECK     STINS2        NUMJN             1 0
     C     NUMJN         IFEQ      0
     C                   MOVE      STINS2        FELT
     C                   EXSR      NUMKON
     C                   Z-ADD     NUMVRD        SOTIL2
      *Alfa sone:rent prisoppslag, ved tr.mod bevar beløp i variabel
     C                   ELSE                                                      4x
     C                   MOVE      STINS2        SONTAB
     C                   EXSR      HENTPR
     C     HEUR          IFEQ      'H'                                              5b
      * I TRMOD. GJØRES INNTIL 3 OPPSLAG PÅ ETT KALL. SJEKK RAB.V/HVERT
     C                   EXSR      TRARAB
     C                   Z-ADD     XBELØP        UTKJPR
     C                   MOVE      *ZEROS        XBELØP
     C                   END                                                        5e
     C                   END                                                       4e
     C                   END                                                      3E
     C                   ELSE                                                    2x
      * Sone er oppgitt.
     C                   MOVE      USONE         SONTAB
     C                   EXSR      HENTPR
     C                   END                                                     2E
     C                   END                                                    1E
      *
      * HOVEDFRAKT
      *
     C     GEBKO         IFEQ      'FRA'                                        1<B
     C     USONE         IFEQ      *BLANKS                                       2<-B
     C     HOVKEY        CHAIN     BASES                              33
      * BASSOT = SONESTED  FUNNET
     C  N33              DO                                                       3<-B
N06  C     baslmt        ifne      *zeros
N06  C     hefbv         andgt     baslmt
N06  c                   move      bassol        bassot
N06  c                   end
     C                   MOVE      BASSOT        SONTAB
     C* NÅR ALLE ER NUMERISKE - SLÅ SAMMEN
     C     NUM           CHECK     SONTAB        NUMJN             1 0
     C     NUMJN         IFEQ      0
     C                   MOVE      SONTAB        FELT
     C                   EXSR      NUMKON
     C                   Z-ADD     NUMVRD        SOHOVE
     C     SOHOVE        ADD       SOTIL1        SOTABX            5 0
     C     SOTABX        ADD       SOTIL2        SOTABY            5 0
     C                   EXSR      NUMKO2
     C                   MOVE      FELT          SONTAB
     C                   END
     C                   EXSR      HENTPR
      *
     C                   END                                                      3<-E
     C     HEUR          IFEQ      'H'                                              5b
      * I TRMOD. GJØRES INNTIL 3 OPPSLAG PÅ ETT KALL. SJEKK RAB.V/HVERT
     C                   EXSR      TRARAB
     C                   Z-ADD     XBELØP        HOVEPR
     C                   MOVE      *ZEROS        XBELØP
     C                   END                                                        5e
     C                   ELSE                                                    2
      * Sone er oppgitt.
     C                   MOVE      USONE         SONTAB
     C                   EXSR      HENTPR
     C     HEUR          IFEQ      'H'                                              5b
     C                   Z-ADD     XBELØP        HOVEPR
     C                   END                                                        5e
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
      *
      *
     C* NÅR TRANSPMOD-FRA-TIL LIK - SLÅ OPP SUM AV INNH/UTKJ. SONE
     C     HESDF         IFEQ      HESDT
     C     HEUR          ANDEQ     'H'                                          1<-B
     C     SOTIL1        IFNE      *ZEROS                                       1<-B
     C     SOTIL2        ORNE      *ZEROS                                       1<-B
     C     SOTIL1        ADD       SOTIL2        SOTABY            5 0
     C                   EXSR      NUMKO2
     C                   MOVE      FELT          SONTAB
     C                   EXSR      HENTPR
     C                   Z-ADD     XBELØP        HOVEPR
     C                   END                                                    1<-E
     C                   END                                                    1<-E
      * Hvis feltene INNHPR/UTKJPR inneholder beløp betyr det at
      * alfa sonetabell er benyttet (i trans.modul) på innh/utkj
      * I så fall add ferdig utregnet beløp til hovedfrakten.
     C     HEUR          IFEQ      'H'
     C                   Z-ADD     0             XBELØP
     C                   ADD       INNHPR        XBELØP
     C                   ADD       UTKJPR        XBELØP
     C                   ADD       HOVEPR        XBELØP
     C                   END
      *
      *
N03  C     INNLPris      tag
      * GRUNNPRIS PÅ AVTALEN (Flyttet FORAN rabattering(JOV))
     C     SSGRPR        IFNE      0
S03  C*    UBELØP        ANDNE     0
N03  C     SOPRPR        ANDNE     0
N03  C     XBELØP        ANDNE     0
     C                   ADD       SSGRPR        XBELØP
     C                   END
     C*
S03  C*    INNLPris      tag
      * Har aktuell kunde generell rabatt på gitte tabeller??
      * (Gjelder IKKE når kall fra SYGE04 - gitt postnrintervall ->
      *    prosent av ..eller brutto på tabell.... UKUNDE er da 0)
     c                   move      *zeros        AFMINI
     c                   move      *zeros        AFMAXI
     C     UKUNDE        IFNE      *ZEROS
     C     SONTAB        ANDNE     *BLANKS
     C                   EXSR      GENRAB
     C                   END
      *
      * Trekk rabatt (MOTTATT SOM INNPARAM.) fra bergnet frakt
      *                     - gitt postnrintervall -> prosent av .....)
     C     URABAT        IFNE      0
     C     URABAT        DIV       100           WRK
     C     1             SUB       WRK           WRK
     C                   MULT(H)   WRK           XBELØP
      * ABSLOLUTT MINI/MAXI ANGITT I %%-AVTALE..
     C     XBELØP        IFLT      AFMINI                                        2<-B
     C     AFMINI        ANDGT     0
     C                   Z-ADD     AFMINI        XBELØP
     C                   ELSE                                                    2
     C     XBELØP        IFGT      AFMAXI                                         3<-B
     C     AFMAXI        ANDGT     0
     C                   Z-ADD     AFMAXI        XBELØP
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END
      *
     C                   ADD       XBELØP        UBELØP
      * Avrunding
     C     UBELØP        IFGT      0
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     FICURR        IFEQ      UVAL
     C     FIDECI        IFEQ      0
     C                   ADD       0.5           UBELØP
     C                   MOVE      '00'          UBELØP
     C                   ELSE
     C     FIDECI        IFEQ      1
     C                   ADD       0.05          UBELØP
     C                   MOVE      '0'           UBELØP
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C* GRENSEFRANKATUR
      * (Er et %-avslag - gjør ingenting at det ligger etter rabatt)
     C     SSGREF        IFEQ      HEFR
     C     SSGREP        ANDGT     0
     C     100           SUB       SSGREP        SSGREP
     C     SSGREP        DIV       100           WRK
     C                   MULT      WRK           UBELØP
     C                   END
      *
      * RETURNER SONE / RARBATT SOM ER BENYTTET.
     C     URABAT        IFEQ      *ZEROS
     C                   MOVE      URABHO        URABAT
     C                   END
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C***************************************************
     C     TRARAB        BEGSR
     C***************************************************
      * I TR.MODUL GJØRES 3 OPPSLAG - SISTE ER HOVEDFRAKTEN
      * BEVAR SONE/RABATT-SATS (FOR HOVEDFRAKT)  I HJELPEVARIABLER
     C                   MOVE      *BLANKS       USONHO            3
     C                   MOVE      *ZEROS        URABHO            5 2
     C* NÅR KALLT FRA SYGE04/14 ER UKUNDE 0
     C     UKUNDE        IFNE      *ZEROS
     C     XBELØP        ANDNE     *ZEROS
     C                   EXSR      GENRAB
     C     URABAT        IFGT      0
     C     URABAT        DIV       100           WRK
     C     1             SUB       WRK           WRK
     C                   MULT(H)   WRK           XBELØP
     C                   END
     C                   MOVE      URABAT        URABHO
     C                   MOVE      *ZEROS        URABAT
     C                   END
     C                   MOVE      SONTAB        USONHO
     C* SIKRE AT DET IKKE TREKKES RAB OGSÅ TIL SLUTT
     C                   MOVE      *BLANKS       SONTAB
     C                   ENDSR
     C***************************************************
     C     NUMKON        BEGSR
     C***************************************************
     C                   MOVE      *ZEROS        NUMVRD            5 0
     C                   MOVE      *ZEROS        NUTEGN            1 0
     C     1             DO        5             NR                1 0
     C     FE(NR)        IFGE      '0'
     C     FE(NR)        ANDLE     '9'
     C                   MULT      10            NUMVRD
     C                   MOVE      FE(NR)        NUTEGN
     C                   ADD       NUTEGN        NUMVRD
     C                   ELSE
     C                   GOTO      UTNUM
     C                   END
     C                   END
     C     UTNUM         ENDSR
     C***************************************************
     C     NUMKO2        BEGSR
     C***************************************************
     C* Flytt "SUM AV SONER" fra numerisk til alfa
     C                   MOVE      SOTABY        FELT2
     C                   MOVE      *BLANKS       FELT
     C                   MOVE      *ZEROS        NR
     C                   MOVE      'N'           NULLOK            1
     C* Skipp ledende nuller
     C     1             DO        5             N2                1 0
     C     F2(N2)        IFGT      '0'
     C     NULLOK        OREQ      'J'
     C                   ADD       1             NR
     C                   MOVE      F2(N2)        FE(NR)
     C                   MOVE      'J'           NULLOK
     C                   END
     C                   END
     C                   ENDSR
      *
     C***************************************************
     C     HENTPR        BEGSR
     C***************************************************
      * ved grunn-pris - bytt ot første bokstav av normal brutto tab med G
     c     GrPrJN        ifeq      'J'
     c                   movel     'G'           SONTAB
     c                   end
     C                   MOVE      *ZEROS        XBELØP
      *
     C     SONKE2        CHAIN     SONES                              33
     C   33SONKE3        CHAIN     SONES                              33
     C  N33              DO
     C*
     C* GRENSEFRANKATUR
     C     SSGREF        IFEQ      HEFR
     C     SSGRES        ANDNE     *BLANKS
     C                   MOVE      SSGRES        SONTAB
     C     SONKE2        CHAIN     SONES                              33
     C   33SONKE3        CHAIN     SONES                              33
     C   33              MOVE      SSPROD        SONTAB
     C                   END
N05  C     AVTALT        IFNE      ' '
N05  C                   MOVE      AVTALT        SSTYPE
N05  C                   END
     C     SSTYPE        IFEQ      'M'
     C                   MOVEL     HEM3          XXXM3             6 0            *HEM3=7,3
     C                   Z-ADD     XXXM3         HEFBV
     C                   ELSE
     C     SSTYPE        IFEQ      'L'
     C                   MOVE      *ZEROS        HEFBV
     C                   MOVE      HELM          HEFBV
     C                   END
     C                   END
     C     SSTYPE        IFEQ      ' '
     C                   EXSR      FINNVK
     C                   END
     C     SSTYPE        IFEQ      'B'
     C                   Z-ADD     HEVKT         HEFBV
     C                   END
     C     SSTYPE        IFEQ      'X'
     C                   Z-ADD     HENT          HEFBV
     C                   END
      *
     C     SONKE1        SETLL     SONET
     C     SONKE2        READE     SONET                                  33
     C   33              DO
     C                   SETOFF                                       33
     C     SONK1B        SETLL     SONET
     C     SONK2B        READE     SONET                                  33
     C                   END
     C  N33              DO                                                     1<-B
     C*
     C* PRIS PR ENHET
     C*
     C     SSTYPE        IFEQ      'V'
     C     TRVERB        MULT      SOPRIC        WRK
     C     WRK           DIV(H)    100           XBELØP
     C                   MOVE      TRVERV        UVAL
     C                   ELSE
      *
     C     SOPRPR        IFGT      0                                             2<-B
     C     SOADJU        IFNE      0                                              3<-B
      * Vekt skal forhøyes til SOADJU       (eks. SOADJU=50  HEFBV=120
     C                   SUB       1             HEFBV                            HEFBV=119
     C                   ADD       SOADJU        HEFBV                            HEFBV=169
     C                   DIV       SOADJU        HEFBV                            HEFBV=3
     C                   MULT      SOADJU        HEFBV                            HEFBV=150)
     C                   END                                                      3<-E
      *
     C     SOPRIC        MULT(H)   HEFBV         WRK              17 4
     C     WRK           DIV(H)    SOPRPR        XBELØP           13 2
      *
     C     SSMIMA        IFEQ      '1'                                            3<-B
N01  C     SSMIMA        OREQ      '3'                                            3<-B
s01  C*    SOMMBE        ANDGT     XBELØP
N01  C     SOMMBE        IFGT      XBELØP
      * XBELØP < Minimum beløp  ==>  Flytt minimum beløp inni UBELØP
     C                   Z-ADD     SOMMBE        XBELØP
N01  C                   end                                                      3
     C                   ELSE                                                     3
     C     SSMIMA        IFEQ      '2'                                             4<-B
     C     SOMMBE        ANDLT     XBELØP
      * XBELØP > Maximum beløp  ==>  Flytt maximum beløp inni XBELØP
     C                   Z-ADD     SOMMBE        XBELØP
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      *
     C                   ELSE                                                    2
     C                   Z-ADD     SOPRIC        XBELØP
     C                   END                                                     2<-E
     C                   MOVE      SSVALK        UVAL
      *
     C                   END
      *
     C     XBELØP        IFLT      SSMINI                                        2<-B
     C     SSMINI        ANDGT     0
      * XBELØP < Abs. minimum  ==>  Flytt abs. minimum inni XBELØP
     C                   Z-ADD     SSMINI        XBELØP
     C                   ELSE                                                    2
     C     XBELØP        IFGT      SSMAXI                                         3<-B
     C     SSMAXI        ANDGT     0
      * XBELØP > Abs. maximum  ==>  Flytt abs. maximum inni XBELØP
     C                   Z-ADD     SSMAXI        XBELØP
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
     C                   END
      *
     C                   ENDSR
      *
     C******************************************
     C     FINNVK        BEGSR
     C******************************************
     C*
     C* FINNER FRAKTBEREGNINGSVEKT
      *
     C* TAR HØYDE FOR AVVIK I KUNDEAVTALEN
      *
     C     UUOMRL        IFNE      *ZEROS
     C                   Z-ADD     UUOMRL        SSOMRL
     C                   END
     C     UUOMRM        IFNE      *ZEROS
     C                   Z-ADD     UUOMRM        SSOMRM
     C     UUOMR1        IFNE      *ZEROS
     C     HEVKT         ANDGE     UUOMR1
     C     UUOMR2        ANDNE     *ZEROS
     C                   Z-ADD     UUOMR2        SSOMRM
     C                   END
     C                   END
e01  C     HELM          MULT(H)   SSOMRL        XFBV1             9 0
e01  C     HEM3          MULT(H)   SSOMRM        XFBV2             9 0
     C     HEVKT         IFGE      XFBV1
     C                   Z-ADD     HEVKT         HEFBV             9 0
     C                   ELSE
     C                   Z-ADD     XFBV1         HEFBV
     C                   END
     C     HEFBV         IFLT      XFBV2
     C                   Z-ADD     XFBV2         HEFBV
     C                   END
     C                   ENDSR
      ***********************************************
     C     GENRAB        BEGSR
      ***********************************************
      * KUNDEAVT. OM GENERELL %-RABATT PÅ VISSE TABELLNAVN/NUMMER ER
      * PR. DEFIN. GITT VED Å ANGI (I POSTAL KUNDEAVT) DUMMY LANDKODE
      * "%%"+TAB-NR FRA/TIL (F.EKS  %%  NO001 NO299 (TIL-OMRÅDE BLANK))
      * I LINJA FOR "FRA" SETTES "P" DERETTER VANLIGE %-RABATT-REGLER
      * (DVS. ENTEN FLAT % SATS ANGITT VED SATS PÅ HOVEDSIDEN ELLER
      *  HVIS 0 DER, KAN DET DIFERENSIERES PÅ VEKT.GRP I DETALJBILDET)
      *
      *
      * PRIORITERING:
      *       1=AKTUELT KUNDENR, AKTUELL AVDELING, AKTUELL PRODUKTKODE
      *       2=AKTUELT KUNDENR, AKTUELL AVDELING, BLANK PRODUKTKODE
      *       3=AKTUELT KUNDENR, 0000 I  AVDELING, AKTUELL PRODUKTKODE
      *       4=AKTUELT KUNDENR, 0000 I  AVDELING, BLANK PRODUKTKODE
      *
      *
     C                   MOVE      UKUNDE        AFKUND
     C                   MOVE      '%%'          AFLKFR
      *
      * De fleste kunder (kall på denne rutine) har INGEN %%-avtaler
      * Sjekker dette/hopper ut hvis ingen (spare DISK-I/O=Responstid)
     C     AV3KEY        SETLL     AVFRHL03                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      *ZEROS        UKUNDE
     C                   GOTO      UTRAB2
     C                   END
      *
     C* KUNDEN HAR MINST 1 %% - FINN EVT AVTALE SOM DEKKER TABELLEN.
      *
     C                   OPEN      AVFRH
      *PRIO 1:
     C                   MOVE      UAVD          AFAVDE
     C                   MOVE      HEKDPL        AFPROD
     C     AVKEY         SETGT     AVFRH
     C     NXTP1         TAG
     C     AVKEYE        READPE    AVFRH                                  33
     C     *IN33         IFEQ      '0'
     C     AFFRAN        IFNE      *BLANKS
     C     AFFRAN        ANDNE     HEFR
     C                   GOTO      NXTP1
     C                   END
     C     AFGEBY        IFNE      *BLANKS
     C     AFGEBY        ANDNE     GEBKO
     C                   GOTO      NXTP1
     C                   END
n07   * UTGÅTT AVTALE
N07  C     AFGJDT        IFNE      0
n07  C     HEDTOP        ANDGT     AFGJDT
N07  C                   GOTO      NXTP1
n07  C                   END
n07   * fra dato
N07  C     AFAKDT        IFNE      0
n07  C     HEDTOP        ANDLT     AFAKDT
N07  C                   GOTO      NXTP1
n07  C                   END
     C     AFPNF2        IFGE      SONTAB
     C                   GOTO      RABOK
     C                   END
     C                   END
      *PRIO 2:
     C                   MOVE      ' '           AFPROD
     C     AVKEY         SETGT     AVFRH
     C     NXTP2         TAG
     C     AVKEYE        READPE    AVFRH                                  33
     C     *IN33         IFEQ      '0'
     C     AFFRAN        IFNE      *BLANKS
     C     AFFRAN        ANDNE     HEFR
     C                   GOTO      NXTP2
     C                   END
     C     AFGEBY        IFNE      *BLANKS
     C     AFGEBY        ANDNE     GEBKO
     C                   GOTO      NXTP2
     C                   END
n07   * UTGÅTT AVTALE
N07  C     AFGJDT        IFNE      0
n07  C     HEDTOP        ANDGT     AFGJDT
N07  C                   GOTO      NXTP2
n07  C                   END
n07   * fra dato
N07  C     AFAKDT        IFNE      0
n07  C     HEDTOP        ANDLT     AFAKDT
N07  C                   GOTO      NXTP2
n07  C                   END
     C     AFPNF2        IFGE      SONTAB
     C                   GOTO      RABOK
     C                   END
     C                   END
      *PRIO 3:
     C                   MOVE      *ZEROS        AFAVDE
     C                   MOVE      HEKDPL        AFPROD
     C     AVKEY         SETGT     AVFRH                                        ** 2
     C     NXTP3         TAG
     C     AVKEYE        READPE    AVFRH                                  33    ** 2
     C     *IN33         IFEQ      '0'
     C     AFFRAN        IFNE      *BLANKS
     C     AFFRAN        ANDNE     HEFR
     C                   GOTO      NXTP3
     C                   END
     C     AFGEBY        IFNE      *BLANKS
     C     AFGEBY        ANDNE     GEBKO
     C                   GOTO      NXTP3
     C                   END
n07   * UTGÅTT AVTALE
N07  C     AFGJDT        IFNE      0
n07  C     HEDTOP        ANDGT     AFGJDT
N07  C                   GOTO      NXTP3
n07  C                   END
n07   * fra dato
N07  C     AFAKDT        IFNE      0
n07  C     HEDTOP        ANDLT     AFAKDT
N07  C                   GOTO      NXTP3
n07  C                   END
     C     AFPNF2        IFGE      SONTAB
     C                   GOTO      RABOK
     C                   END
     C                   END
      *PRIO 4:
     C                   MOVE      ' '           AFPROD
     C     AVKEY         SETGT     AVFRH
     C     NXTP4         TAG
     C     AVKEYE        READPE    AVFRH                                  33
     C     *IN33         IFEQ      '0'
     C     AFFRAN        IFNE      *BLANKS
     C     AFFRAN        ANDNE     HEFR
     C                   GOTO      NXTP4
     C                   END
     C     AFGEBY        IFNE      *BLANKS
     C     AFGEBY        ANDNE     GEBKO
     C                   GOTO      NXTP4
     C                   END
n07   * UTGÅTT AVTALE
N07  C     AFGJDT        IFNE      0
n07  C     HEDTOP        ANDGT     AFGJDT
N07  C                   GOTO      NXTP4
n07  C                   END
n07   * fra dato
N07  C     AFAKDT        IFNE      0
n07  C     HEDTOP        ANDLT     AFAKDT
N07  C                   GOTO      NXTP4
n07  C                   END
     C     AFPNF2        IFGE      SONTAB
     C                   GOTO      RABOK
     C                   END
     C                   END
     C*
     C                   MOVE      *ZEROS        UKUNDE
     C                   GOTO      UTRAB
      *
      * RABATT-LINJE FUNNET
     C     RABOK         TAG
      *
     C     AFMINI        IFne      *zeros
     C                   Z-ADD     AFMINI        SSMINI
     C                   END
     C     AFMINI        IFne      *zeros
     C                   Z-ADD     AFMAXI        SSMAXI
     C                   END
      * Rabatt på header-nivå (= Flat rabatt)
     C                   Z-ADD     AFPFRA        URABAT
     C                   Z-ADD     AFAVNR        UFAVNR
      * eller på detaljnnivå (= Vektgrp.-styrt rabatt)
     C     AFPFRA        IFEQ      *ZEROS
     C                   OPEN      AVFRD
     C                   MOVE      'H'           AFCDE1
      * SJEKKER FØRST OM DET FINNES NOE REG. I DENNE TABELLEN
     C     VKTKEY        SETLL     AVFRD                                  90
     C     *IN90         IFEQ      *ON
      * DET FINNES REG. I DENNE TABELLEN. SLÅ OPP PÅ VEKTINTERVALLET
      * OG HENT PROSENTSATSEN.
     C     VKTKE2        SETLL     AVFRD                                  90
     C     VKTKEY        READE     AVFRD                                  90
     C     *IN90         IFEQ      *OFF
     C     AFCOBE        IFEQ      'P'
     C                   Z-ADD     AFPRIA        URABAT
     C                   ELSE
     C     AFCOBE        IFEQ      'T'
     C                   EXSR      BERPRI
     C                   END
     C                   END
     C                   ENDIF
     C                   ENDIF
     C                   CLOSE     AVFRD
     C                   ENDIF
      *
     C     UTRAB         TAG
     C                   CLOSE     AVFRH
      *
     C     UTRAB2        ENDSR
     C***********************************************
     C     BERPRI        BEGSR
     C***********************************************
     C                   Z-ADD     XXFBV         HEFBV
      * dersom kundeavtalen har egen m3/lmfaktor reberegnes Fvkt
     C     AFOMRM        IFNE      *ZEROS
     C     AFOMRL        ANDNE     *ZEROS
     C     HEM3          IFNE      *ZEROS
     C     HELM          ORNE      *ZEROS
     C                   Z-ADD     AFOMRM        UUOMRM
     C                   Z-ADD     AFOMRL        UUOMRL
     C                   Z-ADD     AFOMR1        UUOMR1
     C                   Z-ADD     AFOMR2        UUOMR2
     C                   exsr      FINNVK
     C                   END
     C                   END
      *
      * Beregn frakt med frakttabell
     C     AFPRPR        IFEQ      0                                             2<-B
      * Fast frakt i denne vektintervallen
     C                   Z-ADD     AFPRIA        XBELØP
     C                   ELSE                                                    2
      *
     C     AFADJU        IFGT      0                                              3<-B
     C     HEFBV         ANDLT     999999999
      * Vekt skal oppjusteres med AFADJU
     C                   SUB       1             HEFBV
     C                   ADD       AFADJU        HEFBV
     C                   DIV       AFADJU        HEFBV
     C                   MULT      AFADJU        HEFBV
     C                   END                                                      3<-E
      *
      * Flytt pris inn i XWORK
     C                   Z-ADD     AFPRIA        XWORK            17 6
     C     AFPRPR        IFGT      1                                              3<-B
      * Divider pris med AFPRPR (pris pr.)
     C     AFPRIA        DIV(H)    AFPRPR        XWORK
     C                   END                                                      3<-E
      *
      * Beregn frakt ved å mutipilsere pris og vekt
     C     XWORK         MULT(H)   HEFBV         XBELØP
      *
     C     AMIMAP        IFNE      0                                              3<-B
     C     AFMIMA        IFEQ      '1'                                             4<-B
     C     AMIMAP        ANDGT     XBELØP
      * Frakt er mindre enn minimumfrakt i denne vektintervallen
      * Bruk minimumfrakt som frakt
     C                   Z-ADD     AMIMAP        XBELØP
     C                   ELSE                                                      4
     C     AFMIMA        IFEQ      '2'                                              5<-B
     C     AMIMAP        ANDLT     XBELØP
      * Frakt er større enn maximumfrakt i denne vektintervallen
      * Bruk maximumfrakt som frakt
     C                   Z-ADD     AMIMAP        XBELØP
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
      *
     C                   MOVE      AFVALK        UVAL
      *
     C                   ENDSR
      *
