      * Dette programmet startes fra CLP: E116C_96A2 som i sin tur startes fra
      * SYEDI Standard gjenom tabellen Kunders standardmeldinger.
      *
     FEUNB      IF   E           K DISK
     FEUNH      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FEDTM96A   IF   E           K DISK
     FEIMD96A   IF   E           K DISK
     FELIN96A   IF   E           K DISK
     FENAD96A   IF   E           K DISK
     FEPIA96A   IF   E           K DISK
     FEQTY96A   IF   E           K DISK
     FERFF96A   IF   E           K DISK
     FHORDL09   IF   E           K DISK    PREFIX(H9_)
     FKUNDL01   IF   E           K DISK    PREFIX(K1_)
     FVADRL01   IF A E           K DISK    PREFIX(V1_)
     FOTRAF     O  A E             DISK    PREFIX(O0_)
     FOTRAL01   IF   E           K DISK    PREFIX(O1_)
     F                                     RENAME(OTRAR:OTRAR01)
     FKUNKO2    IF A E           K DISK    PREFIX(K2_)
     D TAB1            S              1    DIM(29) CTDATA PERRCD(1)
     D TAB2            S              1    DIM(29) ALT(TAB1)
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D C               S              1    DIM(8)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  O0_LEVDATO             1      8  0
     D  O0_LEVAAR              1      4  0
     D  O0_LEVMND              5      6  0
     D  O0_LEVDAG              7      8  0
     D                 DS
     D  VNR                    1     14    DIM(14)
     D  VNR14A                 1     14
     D                 DS
     D  SONV                   1     10    DIM(10)
     D  SONV10                 1     10
     D                 DS
     D  N3039                  1     35
     D  XX3039                 1     17
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KUNDL01K      KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    O0_KUNDNR
     C     OTRAL01K      KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    O0_KUNDNR
     C     HORDL09K      KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    H9_OTBKOD
     C                   KFLD                    H9_ORDAAR
     C                   KFLD                    O0_KUNREF
     C     VADR1K        KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    O0_KUNDNR
     C                   KFLD                    XX_VADRNR
     C     VADR1K2       KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    O0_KUNDNR
     C     KUNKO2K1      KLIST
     C                   KFLD                    X_KUNDNR
     C                   KFLD                    XX3039
     C     KUNKO2K2      KLIST
     C                   KFLD                    O0_KUNDNR
     C                   KFLD                    XX3039
     C     KEYMS0        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN0
     C                   KFLD                    LREP0

     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
     C                   MOVE      'SY'          XX_FIRMA          2
     C                   MOVE      'O'           H9_OTBKOD
     C                   Z-ADD     999           XX_VADRNR         3 0
     C                   Z-ADD     1000          X_KUNDNR          8 0

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""

     C     ZMN           CHAIN     EDIM                               50
     C                   IF        *IN50 = *OFF                                 1<-B
     C                   CLEAR                   OTRAR

      * HENT KUNDES ORDRENR
     C     ZMN           CHAIN     EUNH                               51
     C                   MOVEL     U1004         O0_KUNREF
      * Get interchange values
     C     UIN           CHAIN     EUNB                               51

      * HENT KUNDENR
     C                   Z-ADD     2             ZGN0
     C     KEYMS0        SETLL     ENAD96A
     C     KEYMS0        READE     ENAD96A                                51
     C                   DOW       *IN51 = *OFF                                  2<-B
     C                   IF        N3035 = 'BY'                                   3<-B
     C     KUNKO2K1      CHAIN     KUNKO2                             33          3<-B
     C   33              GOTO      SLUTT
     C                   MOVE      K2_KUKUN2     O0_KUNDNR                         4<-B
     C                   GOTO      KUNDEOK
     C                   ENDIF                                                    3<-E
     C     KEYMS0        READE     ENAD96A                                51
     C  N51              ENDDO                                                   2<-E
     C     *IN51         CABEQ     '1'           SLUTT

     C     KUNDEOK       TAG
     C     KUNDL01K      CHAIN     KUNDL01                            51
     C                   MOVE      K1_LEVMAT     O0_LEVMOT

      * TEST OM BESTILLING FINNES I TRANSIT-FIL FRA FÿR.
     C     OTRAL01K      SETLL     OTRAL01
     C     OTRAL01K      READE     OTRAL01                                51
     C                   DOW       *IN51 = *OFF                                  2<-B
     C     O1_KUNREF     CABEQ     O0_KUNREF     SLUTT
     C     OTRAL01K      READE     OTRAL01                                51
     C  N51              ENDDO                                                   2<-E

      * TEST OM BESTILLING FINNES I ORDRE-FIL FRA FÿR.
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM96A
     C     KEYMS0        READE     EDTM96A                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
     C                   IF        D2005 = '137'                                  3<-B
     C                   MOVEL     D2380         H9_ORDAAR
     C                   LEAVE
     C                   ENDIF                                                    3<-E
     C     KEYMS0        READE     EDTM96A                                51
     C  N51              ENDDO                                                   2<-E
      *
     C     HORDL09K      SETLL     HORDL09
     C     HORDL09K      READE     HORDL09                                51
     C     *IN51         CABEQ     '0'           SLUTT

      * HENT LEVERINGSADRESSE
     C                   Z-ADD     2             ZGN0
     C     KEYMS0        SETLL     ENAD96A
     C     KEYMS0        READE     ENAD96A                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
     C                   IF        N3035 = 'DP'                                   3<-B
     C     KUNKO2K2      CHAIN     KUNKO2                             33
     C                   IF        *IN33                                           4<-B
      * LEVERINGSADRESSE FINNES IKKE FRA FÿR
      * LAG NY LEVERINGSADRESSE
     C     VADR1K        SETLL     VADRL01
     C     VADR1K2       READPE    VADRL01                                34
     C                   IF        *IN34                                            5<-B
     C                   MOVE      'A'           V1_AKTKOD
     C                   MOVE      XX_FIRMA      V1_FIRMA
     C                   MOVE      O0_KUNDNR     V1_KUNDNR
     C                   Z-ADD     1             V1_VADRNR
     C                   ELSE                                                       5
     C                   ADD       1             V1_VADRNR
     C                   ENDIF                                                      5<-E
     C                   MOVEL     N3036A        V1_VADRNA
     C                   MOVEL     N3042A        V1_VADRN1
     C                   MOVEL     N3042B        V1_VADRN2
     C                   MOVEL     N3164         V1_VADRN3
     C                   MOVEL     N3251         V1_POSTVA
     C                   MOVEL     N3036A        SONV10
     C     1             DO        10            XN
     C     SONV(XN)      LOOKUP    TAB1          TAB2                     33
     C   33              MOVE      TAB2          SONV(XN)
     C                   ENDDO
     C                   MOVEL     SONV10        V1_SONAVN
     C                   IF        (*IN51 AND (*IN51 = *OFF))
     C                   WRITE     VADRR
     C                   END
      * LAG KRYSSEREF.
     C                   MOVE      O0_KUNDNR     K2_KUKUN1
     C                   MOVE      O0_KUNDNR     K2_KUKUN2
     C                   MOVE      V1_SONAVN     K2_KUALFA
     C                   Z-ADD     V1_VADRNR     K2_KUVADR
     C                   IF        (*IN51 AND (*IN51 = *OFF))
     C                   WRITE     KUNKOR
     C                   END
     C                   ENDIF                                                     4<-E
      *
     C                   LEAVE
     C                   ENDIF                                                    3<-E

     C     KEYMS0        READE     ENAD96A                                51
     C  N51              ENDDO                                                   2<-E
     C                   IF        *IN51                                         2<-B
     C                   MOVE      *ZEROS        O0_VADRNR
     C                   ELSE
     C                   MOVE      K2_KUVADR     O0_VADRNR
     C                   ENDIF                                                   2<-E

      * HENT LEVERINGSDATO
     C                   MOVE      *ZEROS        XX_LEVDATO
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM96A
     C     KEYMS0        READE     EDTM96A                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
     C                   IF        D2005 = '2'                                    3<-B
     C                   MOVEL     D2380         XX_LEVDATO        8 0
     C                   LEAVE
     C                   ENDIF                                                    3<-E
     C     KEYMS0        READE     EDTM96A                                51
     C  N51              ENDDO                                                   2<-E

     C                   MOVE      '1'           O0_TRANS
     C                   MOVE      'N'           O0_BRKDOT
     C                   MOVE      XX_FIRMA      O0_FIRMA
     C                   MOVE      'O'           O0_ORDTYP
     C                   MOVE      'TT'          O0_STYPE

      * HENT VARENR OG ANTALL KOLLI
     C                   Z-ADD     25            ZGN0
     C     KEYMS0        SETLL     ELIN96A
     C     KEYMS0        READE     ELIN96A                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
      * HENT VARENR
     C                   EXSR      PIA25
      * HENT VARETEKST
     C                   EXSR      IMD25
      * HENT ANTALL KOLLI
     C                   EXSR      QTY25
      * HENT LEVERINGSDATO
     C                   EXSR      DTM25
     C                   WRITE     OTRAR
     C                   MOVE      ' '           O0_BRKDOT
     C     KEYMS0        READE     ELIN96A                                51
     C  N51              ENDDO                                                   2<-E

     C                   ENDIF                                                  1<-E

     C     SLUTT         ENDSR
      *////////////////////////////////////////////////////////////
      * HENT VARENR                                         PIA25 /
      *////////////////////////////////////////////////////////////
     C     PIA25         BEGSR
     C     KEYMS1        CHAIN     EPIA96A                            52
     C                   IF        *IN52
     C                   MOVEL     L7140         VNR14A
     C                   ELSE
     C                   MOVEL     P7140A        VNR14A
     C                   END

     C                   MOVE      *BLANKS       XVNR             15 0
     C                   MOVE      *BLANKS       O0_VARENR
     C     1             DO        14            XN                3 0            3<-B
     C                   SELECT                                                    4<-B
     C     VNR(XN)       WHENGE    '0'
     C     VNR(XN)       ANDLE     '9'
     C                   EVAL      XVNR = XVNR * 10
     C                   MOVE      VNR(XN)       XVNR
     C     VNR(XN)       WHENNE    ' '
      * ALFANUMMERISK
     C                   MOVEL     VNR14A        O0_VARENR
     C                   GOTO      SLVARENR
     C                   OTHER
     C                   LEAVE
     C                   ENDSL                                                     4<-E
     C                   ENDDO                                                    3<-E

     C                   MOVE      XVNR          VNR14A
     C     1             DO        14            XN                               3<-B
     C     VNR(XN)       IFEQ      '0'                                             4<-B
     C                   MOVE      ' '           VNR(XN)
     C                   ELSE                                                       5
     C                   LEAVE
     C                   ENDIF                                                      5<-E
     C                   ENDDO                                                     4<-E
      * NUMMERISK
     C                   MOVE      VNR14A        O0_VARENR
     C     SLVARENR      TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * HENT VARETEKST                                      IMD25 /
      *////////////////////////////////////////////////////////////
     C     IMD25         BEGSR
     C                   MOVE      *BLANKS       O0_VARTE1
     C                   MOVE      *BLANKS       O0_VARTE2
     C     KEYMS1        SETLL     EIMD96A
     C     KEYMS1        READE     EIMD96A                                52
     C                   DOW       *IN52 = *OFF
     C                   IF        I7077 = 'F'
     C                   SELECT
     C                   WHEN      O0_VARTE2 <> *BLANKS
     C                   LEAVE
     C                   WHEN      O0_VARTE1 = *BLANKS
     C                   MOVEL     I7008A        O0_VARTE1
     C                   MOVEL     I7008B        O0_VARTE2
     C                   OTHER
     C                   MOVEL     I7008A        O0_VARTE2
     C                   LEAVE
     C                   ENDSL
     C                   ENDIF
     C     KEYMS1        READE     EIMD96A                                52
     C  N52              ENDDO

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * HENT ANTALL KOLLI                                   QTY25 /
      *////////////////////////////////////////////////////////////
     C     QTY25         BEGSR
     C                   MOVE      *ZEROS        O0_BESTIL
     C     KEYMS1        CHAIN     EQTY96A                            52
     C                   IF        *IN52 = *OFF
     C                   MOVEA     Q6060         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          O0_BESTIL
     C                   END

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * HENT LEVERINGSDATO                                  DTM25 /
      *////////////////////////////////////////////////////////////
     C     DTM25         BEGSR
     C                   MOVEL     XX_LEVDATO    O0_LEVDATO
     C     KEYMS1        SETLL     EDTM96A
     C     KEYMS1        READE     EDTM96A                                52
     C                   DOW       *IN52 = *OFF
     C                   IF        D2005 = '2'
     C                   MOVEL     D2380         O0_LEVDATO
     C                   LEAVE
     C                   ENDIF
     C     KEYMS1        READE     EDTM96A                                52
     C  N52              ENDDO

     C                   ENDSR
** KONVERTERINGSTABELL
aA
bB
cC
dD
eE
fF
gG
hH
iI
jJ
kK
lL
mM
nN
oO
pP
qQ
rR
sS
tT
uU
vV
wW
xX
yY
zZ
Ê∆
¯ÿ
Â≈
