      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/ESARKSP1 MODULE(*LIBL/ESARKSP1 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Klokkeslettskolonne skapes i RPG-en(forløpig hardkodet)
      *              Filter på transp(knyttet til kundenr) hvis ikke intern.
10XXX * 02 JOV PTF10 Ta hensyn til avvikende/eskternt arkiv
10XXX * 03 JOV PTF10 Dersom vedk. er agent har det prio 1
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 04 JOV PTF12 fjerne hardkodet N i krav om KLokke
10XXX * 05 SH  PTF12 historisk arkiv
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FTRACKL07  IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FFIRMARC   IF   E           K DISK    usropn
N02  FARKEXT    IF   E           K DISK    usropn
N02  FARKTXT    IF   E           K DISK    usropn
     FArkivl02  IF   E           K DISK    usropn
N01  FTRANL02   IF   E           K DISK    usropn
N01  FTURER14   IF   E           K DISK    usropn
N01  FSYPARL3   IF   E           K DISK    usropn
      ********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D WSUSER          S             10
     D TTFRA           S              3
     D TTTIL           S              3
     D TTTX            S             39
     D ARTY            S              2
N01  D KLTXT           S             30
N01  D KLINP           S             50
     D UsrSpcName      s             10
     D KAT2            S            120
N01   * Array med inntil 50 transportører
N01  D                 DS
N01  D  TN                     1    400  0 DIM(50)
      *
N01  D                 DS
N01  D  SYvrda                 1     50
N01  D    SYvrda1_2            1      2
     D                 DS
     D  DsAvdOpd               1     12
     D  DsAVD                  1      4
     D  DsSlash                5      5
     D  DsOPD                  6     12

      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Parse input / cvt to numeric
     C                   EXSR      PARSE
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   EXSR      LoadHtml
      * Set section variables
     C                   EXSR      Settop
     C                   CALLP     wrtsection('top')

      * HOVEDRUTINE

      * Vis liste over filer i katalogbane (gå i løkke)
     C                   EXSR      LESTT

     C                   CALLP     wrtsection('bot')

     C                   EXSR      Exit

     C                   EVAL      *inlr = *on
     C                   RETURN
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     PARSE         BEGSR
      * Parse variables from QUERY_STRING environment variable
     C                   EVAL      TTFRA    = zhbgetvar('TTFRA')
     C                   EVAL      TTTIL    = zhbgetvar('TTTIL')
     C                   EVAL      TTTX     = zhbgetvar('TTTX')
     C                   EVAL      ARTY     = zhbgetvar('ARTY')
      * Userid.....
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     CUNDL01
     C                   CLOSE     TRACKL07
     C                   CLOSE     HEADF
     C                   CLOSE     FIRMARC
N02  C                   CLOSE     ARKEXT
N02  C                   CLOSE     ARKTXT
     C                   CLOSE     ArkivL02
N01  C                   CLOSE     TRANL02
N01  C                   CLOSE     TURER14
N01  C                   CLOSE     SYPARL3
     C                   ENDSR
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      BEGSR
     C                   CALLP     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESARKSP1':'/CGITAG/')
     C                   ENDSR
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        BEGSR
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
     c                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   CALLP     updHTMLvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   CALLP     updHTMLvar('BESK':BIBESK)
     C                   CALLP     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   CALLP     updHTMLvar('KNAVN':KNAVN)
     C                   CALLP     updHTMLvar('TTFRA':TTFRA)
     C                   CALLP     updHTMLvar('TTTIL':TTTIL)
     C                   CALLP     updHTMLvar('TTTX':TTTX)
     C                   CALLP     updHTMLvar('ARTY':ARTY)
N01  c     KLKrav        ifeq      'J'
N01  c                   eval      KLTxt ='<td><B>Klokka</B></td>'
N01  c                   else
N01  c                   eval      KLTxt =*blanks
N01  c                   endif
N01  C                   CALLP     updHTMLvar('KLtxt':KLTXT)

     C                   ENDSR
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        BEGSR
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
      * finn arkiv-post /pdf..
     c                   exsr      finnArk
     C                   CALLP     updHTMLvar('KAT2':KAT2)
     c     HXSNDN        Ifeq      *blanks
     c                   MOVE      HEAVD         DsAvd
     c                   MOVE      '/'           DsSlash
     c                   MOVE      HEOPD         DsOpd
     c                   MOVEL     DsAvdOpd      HXSNDN
     c                   end
     C                   CALLP     updHTMLvar('HXSNDN':HXSNDN)
     C                   CALLP     updHTMLvar('AVD':
     c                                         %trim(%editc(TTAVD:'Z')))
     C                   CALLP     updHTMLvar('OPD':
     c                                         %trim(%editc(TTOPD:'Z')))
     C                   CALLP     updHTMLvar('FBN':
     c                                         %trim(%editc(TTFBNR:'Z')))
N01  C                   CALLP     updHTMLvar('TRAN':
N01  c                                         %trim(%editc(TUKNT2:'Z')))
     C                   CALLP     updHTMLvar('HENAK':%subst(HENAK:1:10))
     C                   CALLP     updHTMLvar('HEADK3':%subst(HEADK3:5:10))
     C                   callp     updHTMLvar('SCDATO':
     C                             %trim(%editw(TTDATE:'    .  .  ')))
N01  c     KLKrav        ifeq      'J'
N01  c                   eval      KLINP ='<td><input type=text name="KL" '
N01  c                             +'size=4 maxlength=4></td>'
N01  c                   else
N01  c                   eval      KLINP ='<input type="hidden" name="KL">'
N01  c                   endif
N01  C                   CALLP     updHTMLvar('KLINP':KLINP)
     C                   ENDSR
      *=====================================================================******
      *  Finn-ARK
      *=====================================================================******
      * les arkivposter på  gitt oppdrag samme dato/tid=Match.. finn mappe mm
     C     FinnArk       BEGSR
     c                   move      *blanks       KAT2
     c     Ark02key      setll     ArkivL02
     c     Ark02key      reade     ArkivL02                               33
     c     *in33         doweq     '0'
     c     ARDATE        ifeq      TTDATE
     c     ARTIME        andeq     TTTIME
N02  c     artype        chain     arktxt                             33
N02  C                   MOVE      ARKLAG        ARCEXT
N02  C     ARKEXTK       CHAIN     ARKEXT                             33
N02  C   33ARFIRM        CHAIN     FIRMARC                            33
N05  c     arbhis        ifne      *blanks
N05  c                   eval      arcane=arbhis
N05  c                   end
     C                   eval      KAT2='/'+%trim(ARCANE)+'/'
     c                             +%trim(ARLINK)
     c                   leave
     c                   endif
     c     Ark02key      reade     ArkivL02                               33
     C                   enddo

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      CUNDL01
     C                   OPEN      TRACKL07
     C                   OPEN      HEADF
     C                   OPEN      FIRMARC
N02  C                   OPEN      ARKEXT
N02  C                   OPEN      ARKTXT
     C                   OPEN      ArkivL02
N01  C                   OPEN      TRANL02
N01  C                   OPEN      TURER14
N01  C                   OPEN      SYPARL3

     C     TTKey7        KLIST
     C                   KFLD                    xxNAME
     C                   KFLD                    xxACTI
     C                   movel     TTFRA         xxACTI            3
     C                   movel     '*UNDEFINED'  xxNAME           10
     C     HEADkey       KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     Ark02Key      KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C                   move      'A'           ARSTAT
N02  C     ARKEXTK       KLIST
N02  C                   KFLD                    ARFIRM
N02  C                   KFLD                    ARCEXT
N01  C     SyparKey      KLIST
N01  C                   KFLD                    XXPAID            5
N01  C                   KFLD                    XXVRDA           50
N01  C                   movel     'ARKTT'       XXPAID
N01  C                   movel     ARTY          XXVRDA
N01   * ark-koden er del av KEY i SYPARL3 - alltid nok med EN chain
N01  C     SyparKey      setll     SYPARL3
N01  C     XXPAID        reade     SYPARL3                                33
e01  c  N33SYvrda1_2     comp      ARTY                               3333
n01  c  N33SyvrdN        ifeq      1
N01  c                   MOVE      'N'           KLKRAV            1
N01  c                   end
n01  c  N33SyvrdN        ifeq      2
N01  c                   MOVE      'J'           KLKRAV            1            Også klokkeslettkrav
N01  c                   end

     C     KunKey        CHAIN     Cundl01                            33
      * Les firmapost
S02  C*                  READ      FIRMARC                                41
      *
S04  c**                 MOVE      'N'           KLKRAV            1
N01   *
N01   * dersom ikke intern, vis kun poster for aktuell(e) transp-nr
N01  c     XINTERN       ifne      'J'
N01  c                   Clear                   TN
N01  C     KunKey        setll     Tranl02
N01  C     KunKey        reade     Tranl02                                35
N01  C     *in35         Doweq     '0'
N01  c                   add       1             Nr                3 0
N01  c                   move      VMTRAN        TN(nr)
N01  c     Nr            cabeq     50            UtTn
N01  C     KunKey        reade     Tranl02                                35
N01  C                   enddo
N01  C                   endif
N01  c     UtTn          tag
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les fra T&T og legg ut oversikt                   LESTT  /
      *////////////////////////////////////////////////////////////
      *******************************************
     C     LESTT         BEGSR
      *******************************************
     c     TTkey7        setll     TRACKL07
     c     TTkey7        reade     TRACKL07                               50
     c     *in50         doweq     '0'
     c     HeadKey       chain     HEADF                              33
     c     *in33         ifeq      *off
N01  c                   move      *zeros        TuKnt2
N01  c     HEPRO         chain     turer14
N01  c                   exsr      SrExclude
N01  c     Exclude       cabeq     'Y'           NextTT
     C                   EXSR      Setrow
     C                   CALLP     wrtsection('row')
     C                   endif
N01  c     NextTT        tag
     c     TTkey7        reade     TRACKL07                               50
     C                   enddo
      *
     C                   ENDSR
N01   *******************************************
N01  C     SrExclude     BEGSR
N01   *******************************************
N01   * setter Exclude=Y som start, passerer alle tester -> Exclude=N
N01  c                   move      'Y'           Exclude           1
N01   * Test agent/transportør utfra pålogget user (utføres ikke dersom intern)
N01  c     XINTERN       ifne      'J'
N03  c     BIKUNR        cabeq     HEKNA         UtTnOK                         Agent matcher ??
N01  C     1             Do        50            NR                             Kundenr i en av
N01  c     TN(nr)        cabeq     *zeros        Utexcl                         fler mulige transp-
N01  c     TN(nr)        cabeq     TUKNT2        UtTnOK                         ortørnr matcer ??
N01  C                   enddo
N01  c     UtTnOK        tag
N01  C                   endif
N01   *
N01   * Kommet hit = Passert alle testet - sett N i exclude
N01  c                   move      'N'           Exclude
N01  C     Utexcl        ENDSR
