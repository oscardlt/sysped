      *
      ****************************************************************
      *** PROGRAM SOM AVSLUTTER OPDRAG                             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24               ***
      *** LEDIGE INDIKATORER 34-90 95-97                           ***
      *** INDIKATOR 32 PÅ = KALLE FRA SYEX10                       ***
      ***           32 AV = KALLE FRA SYEX88CL                     ***
      ****************************************************************
      *
     FAVSLUTF   UF A E           K DISK
     FAVSLUTL2  IF   E           K DISK
     F                                     RENAME(AVSLUTFR:AVSLU2)
     FTELL      IF   E           K DISK
     FFAKT      IF   E           K DISK
     FKOSBU9    IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHEAD22    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAFRF)
     FHEADF     UF   E           K DISK
     FSYKS89FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D AL              S              1    DIM(20)
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEPERI                 1      6  0
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  UDATO                  1      8  0
     D  UAAR                   1      4  0
     D  UMND                   5      6  0
     D  UDAG                   7      8  0
     D                 DS
     D  WSPERI                 1      6  0
     D  WSCC                   1      2  0
     D  WSAA                   3      4  0
     D  WSMM                   5      6  0
     D  WSPER2                 1      8  0
      * PARAMETERSTRENG FOR KALL PÅ SYKS12
     D                 DS
     D  P1DS                   1    256
     D  P1FLAG                 1      1
     D  P1BNR                  2      8  0
     D  P1AVD                  9     12  0
     D  P1TUNR                13     20  0
     D  P1OAVD                21     24  0
     D  P1OPD                 25     31  0
     D  P1BL                  32     44  2
      *____________________________________________________________
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   27OPDE1K        SETLL     HEAD22
     C   28OPDE2K        SETLL     HEADF
     C     START1        TAG
     C                   EXSR      LESFIL
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C     ZRECNO        IFEQ      0
     C     ZRECNO        OREQ      XRECNO
     C                   MOVE      HEDTOP        WSPER2
     C                   MOVE      HEOPD         WSOPD
     C                   END
     C                   ADD       1             ZRECNO
     C                   EXSR      SUMOPD
     C                   WRITE     SUBFIL
     C                   EXSR      LESFIL
     C  N94              END
     C                   END
     C*
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9830
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C   05              DO
     C                   GOTO      START
     C                   END
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENTSR
     C   98              GOTO      START3
     C                   END
     C     WSNR          IFEQ      'F'
     C                   GOTO      START
     C                   END
     C     WSNR          IFEQ      'A'
     C                   GOTO      START
     C                   END
     C*
     C                   GOTO      START3
     C*
     C*** SØK FRA UTSKRIFTTYPE
     C     START6        TAG
     C                   EXFMT     SØKTUR
     C                   SETOFF                                       9930
     C   03              GOTO      SLUTT
     C   12              GOTO      START3
     C*
     C                   EXSR      TSTSØK
     C   30              GOTO      START6
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TILOPD        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    UFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
     C     UFLAGG        COMP      1                                      12
     C     UFLAGG        COMP      3                                      03
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     1             RUNDE             5 0
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C* SETTE AVSLUT STATUS
     C  N98HEAVSL        IFEQ      'T'
     C                   MOVE      'SHU0574'     MSGNR
     C                   SETON                                            30
     C                   GOTO      SLHENT
     C                   END
      *
      * SETTE AVSLUT STATUS
      *
     C  N98WSNR          IFEQ      'A'
     C                   ADD       1             RUNDE
     C                   EXSR      AVSLUT
     C                   UPDATE    SUBFIL
     C                   END
      *
      * SETTE FRIGJØR STATUS
      *
     C  N98WSNR          IFEQ      'F'
     C                   ADD       1             RUNDE
     C                   EXSR      FRIGJO
     C                   UPDATE    SUBFIL
     C                   END
      *
      * SE PÅ OPPDRAG
      *
     C  N98WSNR          IFEQ      '5'
     C                   ADD       1             RUNDE
     C                   EXSR      TILOPD
     C                   UPDATE    SUBFIL
     C                   END
      *
      * GÅ TIL BUDSJETT
      *
     C  N98WSNR          IFEQ      'B'
     C                   ADD       1             RUNDE
     C                   EXSR      BUDSR2
     C                   UPDATE    SUBFIL
     C                   END
      *
      * GÅ TIL FAKTURA
      *
      *
     C  N98WSNR          IFEQ      'I'
      * TAR IKKE STATUS S=SLETTET
     C     HEST          IFEQ      'S'
     C                   MOVE      '1'           *IN98
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0041'     MSGNR
     C                   GOTO      SLHENT
     C                   END
     C                   ADD       1             RUNDE
     C                   EXSR      CHGFAK
     C                   UPDATE    SUBFIL
     C                   END
     C*
     C                   END
     C*
     C   98              DO
     C     RUNDE         IFEQ      0
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   ELSE
     C                   SETOFF                                       98
     C                   END
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     TSTSØK        BEGSR
     C***********************************************
     C     WSAVD         CHAIN     TELL                               99
     C  N99WSAVD         COMP      0                                      99
     C   99              MOVE      'SPA0009'     MSGNR
     C   99              SETON                                        30
      *
      * NULL I PERIODE SØK OPPDRAG
      * ELLERS SØK PERIODE   *IN28
      *
     C  N99WSPERI        IFNE      0
     C                   MOVEA     '100'         *IN(27)
     C                   ELSE
     C                   MOVEA     '010'         *IN(27)
     C                   END
     C                   GOTO      SLTST
     C*
     C*
     C                   MOVE      'DIV0983'     MSGNR
     C                   SETON                                        9930
     C*
     C     SLTST         ENDSR
     C*
     C*
     C***********************************************
     C     LESFIL        BEGSR
     C***********************************************
     C     STLES         TAG
     C   27WSAVD         READE     HEAD22                                 94
     C   28WSAVD         READE(N)  HEADF                                  94
     C  N94              DO
     C     OPKEY         CHAIN(N)  AVSLUTF                            26
     C     WSSTAT        IFEQ      'A'
     C   26              GOTO      STLES
     C                   END
     C     WSSG          IFNE      *BLANKS
     C     WSSG          ANDNE     HESG
     C                   GOTO      STLES
     C                   END
     C     WSSTAT        IFEQ      'I'
     C  N26              GOTO      STLES
     C                   END
     C                   END
     C                   MOVE      ' '           HEAVSL
     C  N26              MOVE      'A'           HEAVSL
     C   26HEPRO         IFNE      *ZEROS
     C     TUKEY         CHAIN     AVSLUTL2                           20
     C  N20              MOVE      'T'           HEAVSL
     C                   END
     C*
     C*
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     VALU1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FAVAL
     C     KOSB9K        KLIST
     C                   KFLD                    YYST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      ' '           YYST              1
     C*
     C     FAKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     OPKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TUKEY         KLIST
     C                   KFLD                    HEPRO
     C     OPDE1K        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPER2
     C                   KFLD                    WSOPD
     C     OPDE2K        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UIMP              1
     C                   PARM                    U10               1
     C                   PARM                    UPRO              8 0
     C                   PARM                    UTRAN             1
     C     UIMP          COMP      'J'                                    31
     C     U10           COMP      'J'                                    32
     C*
     C                   MOVE      *ZEROS        WSOPD
     C                   MOVE      *ZEROS        WSPER2
     C                   MOVE      *ZEROS        HEPERI
     C     *LIKE         DEFINE    WSAVD         AVDZER
     C                   MOVE      'J'           USPØR             1
     C                   MOVE      UAVD          WSAVD
     C                   SETON                                        27
     C                   READ      FIRM                                   33
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     AVSLUT        BEGSR
     C***********************************************
     C     OPKEY         CHAIN(N)  AVSLUTF                            33
     C   33              DO
     C                   CLEAR                   AVSLUTFR
     C                   TIME                    XULTID           14 0
     C                   MOVE      XULTM         AVTIME
     C                   MOVE      XULDD         UDAG
     C                   MOVE      XULMM         UMND
     C                   MOVE      XULÅÅ         UAAR
     C                   MOVE      UDATO         AVDATE
     C                   MOVE      USG           AVSG
     C                   MOVE      ZUSER         AVUSER
     C                   MOVE      HEAVD         AVAVD
     C                   MOVE      HEOPD         AVOPD
     C                   WRITE     AVSLUTFR
     C                   END
     C* FEIL
     C*
     C  N33              MOVE      'SHU0569'     MSGNR
     C  N33              SETON                                        9930
     C                   ENDSR
     C*
     C***********************************************
     C     FRIGJO        BEGSR
     C***********************************************
     C     OPKEY         CHAIN     AVSLUTF                            33
     C  N33              DELETE    AVSLUTFR
     C* FEIL
     C*
     C   33              MOVE      'SHU0570'     MSGNR
     C   33              SETON                                        9930
     C                   ENDSR
     C*
     C***********************************************
     C     SUMOPD        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSINNT
     C                   MOVE      *ZEROS        WSKOST
     C                   MOVE      *ZEROS        WSRESU
     C                   MOVE      *ZEROS        WSESTI
     C                   SETOFF                                       34
     C     FAKEY         SETLL     FAKT
     C     *IN34         DOWEQ     '0'
     C     FAKEY         READE     FAKT                                   34
     C     *IN34         IFEQ      '0'
      *
     C     FAOPKO        IFNE      'D'
     C     FASK          IFEQ      'S'
     C     FASK          OREQ      'K'
     C     FASK          OREQ      'X'
     C     FASK          OREQ      'I'
      *
     C     FAKDA         IFEQ      'K'
     C                   ADD       FABELN        WSKOST
     C                   ELSE
     C     FAOPKO        IFGT      'E'
     C                   ADD       FABELN        WSINNT
     C                   ELSE
     C                   ADD       FABELN        WSESTI
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C                   END
     C                   END
     C                   EXSR      BUDSR
     C     WSKOST        ADD       WSINNT        WSRESU
     C                   ADD       WSESTI        WSRESU
     C                   ADD       WSESTK        WSRESU
     C     WSINNT        COMP      0                                    22
     C     WSKOST        COMP      0                                    23
     C     WSESTI        COMP      0                                    24
     C     WSESTK        COMP      0                                    25
     C     WSRESU        COMP      0                                    26
     C                   ENDSR
     C***********************************************
     C     BUDSR         BEGSR
     C***********************************************
     C*
     C*
     C                   MOVE      *ZEROS        WSESTK
     C     KOSB9K        SETLL     KOSBU9
     C     KOSB9K        READE     KOSBU9                                 33
     C     *IN33         DOWEQ     '0'
     C* Omregn valuta
     C     BUVAL         IFNE      FICURR
     C                   MOVE      BUVAL         FAVAL
     C     VALU1K        CHAIN     VALUL01                            34
     C  N34VALKU1        DIV       OMRFAK        XFAK              9 6
     C  N34              MULT(H)   XFAK          BUBL
     C                   END
     C                   SUB       BUBL          WSESTK
     C     KOSB9K        READE     KOSBU9                                 33
     C                   END
     C*
     C                   ENDSR
      ***********************************************
     C     BUDSR2        BEGSR
      ***********************************************
     C                   MOVE      'O'           P1FLAG
     C                   MOVE      *ZEROS        P1BNR
     C                   MOVE      *ZEROS        P1AVD
     C                   MOVE      *ZEROS        P1TUNR
     C                   MOVE      HEAVD         P1OAVD
     C                   MOVE      HEOPD         P1OPD
     C                   MOVE      *ZEROS        P1BL
     C                   MOVE      P1DS          PARM1           256
     C                   CALL      'SYKS12'
     C                   PARM                    PARM1
     C                   EXSR      SUMOPD
     C                   ENDSR
      ***********************************************
     C     CHGFAK        BEGSR
      ***********************************************
     C                   MOVE      'CHF'         UTYPE             3
     C                   MOVE      HEAVD         UAVD2             4 0
     C                   MOVE      HEOPD         UOPD2             7 0
     C                   MOVE      USG           USG2              3
      *
      *
     C     OPKEY         CHAIN     HEADF                              33
     C                   MOVE      HEST          XST               1
     C                   MOVE      ' '           HEST
     C                   EXCEPT    NYOPDS
      *
     C                   EXSR      WRKOPD
      *
     C     OPKEY         CHAIN     HEADF                              33
     C                   MOVE      XST           HEST
     C                   EXCEPT    NYOPDS
     C                   EXSR      SUMOPD
     C                   ENDSR
      ***********************************************
     C     WRKOPD        BEGSR
      ***********************************************
     C                   MOVE      ' '           UUR               1
     C                   MOVE      ' '           UKODE             1
     C                   MOVE      ' '           UDUMMY            1
     C                   MOVE      *ZEROS        UAVDNY            4 0
      *
     C                   SELECT
      * LAND IMPORT
     C     HEUR          WHENEQ    'A'
     C                   CALL      'SYFA10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE             1
     C                   PARM                    UDUMMY            1
     C                   PARM                    UAVDNY
      * LAND EXPORT
     C     HEUR          WHENEQ    'B'
     C                   CALL      'SYEX10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UDUMMY
     C                   PARM                    UAVDNY
      * FLY IMPORT
     C     HEUR          WHENEQ    'C'
     C                   CALL      'SYFI10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UDUMMY            1
     C                   PARM                    UAVDNY
      * FLY EXPORT
     C     HEUR          WHENEQ    'D'
     C                   CALL      'SYEF10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UDUMMY            1
     C                   PARM                    UAVDNY
      * FLY INNLAND
     C     HEUR          WHENEQ    'E'
     C                   CALL      'SYIN10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UAVDNY
      * SJØ IMPORT
     C     HEUR          WHENEQ    'F'
     C                   CALL      'SYSJ10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UAVDNY
      * SJØ EXPORT
     C     HEUR          WHENEQ    'G'
     C                   CALL      'SYSE10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UAVDNY
      * BÅT
     C     HEUR          WHENEQ    'S'
      * FRA LAND EXPORT
     C     UUR           IFEQ      'B'
     C                   CALL      'SYEX10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UDUMMY
     C                   PARM                    UAVDNY
     C                   ENDIF                                                  <UUR
      * FRA FLY EXPORT
     C     UUR           IFEQ      'D'
     C                   CALL      'SYEF10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UAVDNY
     C                   ENDIF                                                  <UUR
      * FRA SJØ EXPORT
     C     UUR           IFEQ      'G'
     C                   CALL      'SYSE10'
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UKODE
     C                   PARM                    UAVDNY
     C                   ENDIF                                                  <UUR
     C                   ENDSL
      *
      *
     C                   ENDSR
      *
     OHEADFR    E            NYOPDS
     O                       HEST
