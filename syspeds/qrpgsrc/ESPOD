      *********************************************************************
      * Spørring på POD
      *
CRTPG+*  CRTPGM SYSPED/ESPOD MODULE(*LIBL/ESPOD
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 skipp slettede oppdrag .
10XXX * 02 JOV PTF10 dersom parameter EXTRE mangler på user  bruk IFB
      *              hardkodet USER=NN fjernet
10172 * 03 SH  PTF10 norsk text til Track and trace
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 04 JOV PTF11 UBANE 70 -> 256 lang  i kall bpå BHRURL
      * 05 SH  PTF11 ENDRET TEST ETTER POD
********************************************************************
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
N05  FEDII      IF   E           K DISK    usropn
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     Farkivl02  IF   E           K DISK    usropn
     FFIRMARC   IF   E           K DISK    usropn
     FFIRM      IF   E           K DISK    usropn
     FARKTXT    IF   E           K DISK    usropn
     FARKEXT    IF   E           K DISK    usropn
     FFRISOL01  IF   E           K DISK    usropn
     FKOFAST    IF   E           K DISK    usropn
     FBRPARF    if   e           k disk    usropn
     FTRACKL01  IF   E           K DISK    usropn
     FTRACKL02  IF   E           K DISK    usropn
     F                                     RENAME(TRACKFR:TRACKFR2)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D KN              S              8  0 DIM(30)
     D DIR             s              1
     D WSAVD           S              4
     D WSOPD           S              7
     D EXTREF          S             35
     D WSSOK           S             80
     D WSSOKURL        S            200
ESPODD EXTURL          S            200
ESPOD * Definere  hex appostrof
ESPODD Ap              c                   x'7D'
     D WSFRTTXT        S            710
     D UtText          s            150
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D Snr             s             17

     D                 DS
     D  DSAVOP                 1     12
     D  DSAVD                  1      4
     D  Strek                  5      5
     D  DSOPD                  6     12
     D                 DS
      * FOR KORT STRENG, 120->200  INGEN MERKING.
     D  updf1                  1    200
     D  updf2                201    400
     D  updf3                401    600
     D  updf4                601    800
     D  updf5                801   1000
     D  updf6               1001   1200
     D  updf7               1201   1400
     D  updf8               1401   1600
     D  updf9               1601   1800
     D  updf10              1801   2000
     D  updf11              2001   2200
     D  updf12              2201   2400
     D  updf13              2401   2600
     D  updf14              2601   2800
     D  updf15              2801   3000
     D  updf16              3001   3200
     D  updf17              3201   3400
     D  updf18              3401   3600
     D  updf19              3601   3800
     D  updf20              3801   4000
     D  updf                   1   4000
     D                                     DIM(20)
     D                 DS
     D  DFS                    1     10    DIM(10)
     D  KFSODK                 1     10
     D                 DS
     D  KFTXT                  1     50
     D  KFTXTB                 5     49
     D  VisWEB                50     50
     D                 DS
     D  WDsÅMD                 1      8  0
     D  WDsÅMDÅ                1      4  0
     D  WDsÅMDM                5      6  0
     D  WDsÅMDD                7      8  0
     D                 DS
     D  WDsDMÅ                 1      8  0
     D  WDsDMÅD                1      2  0
     D  WDsDMÅM                3      4  0
     D  WDsDMÅÅ                5      8  0
     D                 DS
     D  HXSNDN                 1     20
     D  HXSNR17                4     20
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   exsr      DspPart
      *
     C                   callp     wrtsection('top')
      *
     C                   exsr      TRACKINFO
      *
     C                   callp     wrtsection('BOTT')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
S02  C*                  eval      WSUSER  = 'NN'
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSNA    = zhbgetvar('WSNA')
     C                   eval      WSAVD   = zhbgetvar('HEAVD')
     C                   MOVE      *ZEROS        HEAVD
     C                   EVAL      HEAVD = c2n2(WSAVD)
     C                   eval      WSOPD   = zhbgetvar('HEOPD')
     C                   MOVE      *ZEROS        HEOPD
     C                   EVAL      HEOPD = c2n2(WSOPD)
      * ekstern referanse
     C                   eval      EXTREF  = zhbgetvar('EXTREF')
     C     EXTREF        IFNE      *blanks
     C                   eval      UsrSpcName =  'InT5WXz4BJ'
     C                   end
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSNA':WSNA)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('WSAVD':WSAVD)
     C                   callp     updHtmlVar('WSOPD':WSOPD)
     C                   callp     updHtmlVar('DIR':DIR)
     C                   callp     updHtmlVar('EXTREF':EXTREF)
      *
     C                   endsr
      *=====================================================================******
     C     DspPart       begsr
      *
     c                   exsr      lesdok
      *
     C                   callp     updHTMLvar('UPDF1':UPDF1)
     C                   callp     updHTMLvar('UPDF2':UPDF2)
     C                   callp     updHTMLvar('UPDF3':UPDF3)
     C                   callp     updHTMLvar('UPDF4':UPDF4)
     C                   callp     updHTMLvar('UPDF5':UPDF5)
     C                   callp     updHTMLvar('UPDF6':UPDF6)
     C                   callp     updHTMLvar('UPDF7':UPDF7)
     C                   endsr
      *
      *=====================================================================******
      *  TRACKINFO
      *=====================================================================******
     C     TRACKINFO     begsr

      *  finn antall PODs på oppdraget (vis kun siste)
     c                   move      *zeros        PodNr             3 0
     c                   move      *zeros        antpod            3 0
     C                   move      *zeros        TTFBNR
     c     podkey        setll     trackl02
     c     podkey        reade     trackl02                               50
     c     *in50         doweq     *OFF
     c                   add       1             antpod
     c     podkey        reade     trackl02                               50
     c                   end
      *
     C                   callp     wrtsection('rowhead')
     c     TrackKey      setll     trackl01
     c     TrackKey      reade     trackl01                               50
     c     *in50         doweq     *OFF
     c                   move      ' '           VisWEB
     C                   EVAL      KFKOD = TTACTI
     c     ACTKey        chain     KOFAST
      * vis kun hendelser på oppdr.nivå - ved POD vis kun siste..
     C     TTFBNR        Ifne      0
     c                   move      ' '           VisWEB
     C                   else
     C     TTACTI        IFEQ      'POD'
     c                   add       1             PodNr
     C     PodNr         Ifne      AntPOD
     c                   move      ' '           VisWEB
     C                   end
     C                   end
     C                   end
      ****
ESPODC     TTACTI        ifne      'POD'
ESPODC**?  TTACTI        andne     '012'
ESPODC**?  TTACTI        andne     '091'
ESPODc                   move      ' '           VisWeb
ESPODc                   endif

     c     VisWEB        ifeq      'J'
      * snu dato Å.M.dag
     c                   move      TTDATE        WDsÅMD
     c                   move      WDsÅMDÅ       WDsDMÅÅ
     c                   move      WDsÅMDM       WDsDMÅM
     c                   move      WDsÅMDD       WDsDMÅD
     C                   callp     updHTMLvar('DATE':
     C                             %trim(%editw(WDsDMÅ:'  .  .    ')))
     C                   callp     updHTMLvar('TIME':
     C                             %trim(%editw(TTTIME:'  :  :  ')))
     c     TTacti        ifeq      'POD'
n05  c     XSPRÅK        ifeq      'EN'
     c                   eval      UTTEXT = KFTXTE + TTTEXT
s05  c*                  eval      UTTEXT = 'Proof Of Delivery'
N05  C                   else
N05  c                   eval      UTTEXT = KFTXT  + TTTEXL
N05  c                   end
     c                   else
n05  c     XSPRÅK        ifeq      'EN'
     c                   eval      UTTEXT = TTTEXT
N05  C                   else
N05  c                   eval      UTTEXT = TTTEXL
N05  c                   end
     C                   end
     C                   callp     updHTMLvar('TEXT':UTTEXT)
     C                   callp     wrtsection('row')
      * dersom det er kun ett fraktbrevsnr kan evt.  gif vises
     C     HXSNR17       IFNE      *BLANKS
     C                   EVAL      SNR  = HXSNR17
     C                   END
     c     TTacti        ifeq      'POD'
     c     snr           andne     'FLERE'
     c     snr           andne     *blanks
     C                   exsr      getgif
     C                   end
      * VEDL TRMODUL/UTLAND KAN SENDNR VÆRE  AVD_OPPDNR
     c     TTacti        ifeq      'POD'
     c     GIFOK         andne     'J'
     c                   move      HEAVD         DSAVD
     C                   move      '_'           Strek
     c                   move      HEOPD         DSOPD
     c                   eval      snr    = DSAVOP
     C                   exsr      getgif
     C                   end
      *
     c                   end
     c     TrackKey      reade     trackl01                               50
     c                   end
      *
      *
     C                   callp     wrtsection('Rowbot')
     C                   endsr
      *=====================================================================******
      *  Hent evt grafisk signatur
      *=====================================================================******
     C     GETGIF        begsr
      *
     c                   MOVE      'N'           GIFOK             1
S04  c*                  move      *blanks       ubane            70
N04  c                   move      *blanks       ubane           256
     C                   movel     '/syspeddev/' ubane
     C     ubane         CAT       'signatur/':0 ubane
     C     ubane         CAT       SNR:0         ubane
     C     ubane         CAT       '.gif':0      ubane
     c                   CALL      'BHRURLCL'
     c                   Parm                    ubane
     c                   Parm                    GIFOK
      * gif fantes ikke, prøv med bmp.
     c     GIFOK         ifne      'J'
S04  c*                  move      *blanks       ubane            70
N04  c                   move      *blanks       ubane
     C                   movel     '/syspeddev/' ubane
     C     ubane         CAT       'signatur/':0 ubane
     C     ubane         CAT       SNR:0         ubane
     C     ubane         CAT       '.bmp':0      ubane
     c                   CALL      'BHRURLCL'
     c                   Parm                    ubane
     c                   Parm                    GIFOK
     c                   end
N05   * gif fantes ikke, prøv transportør
N05  c     GIFOK         ifne      'J'
N05  c                   eval      inid=TTDEPO
N05  c     inid          CHAIN     EDII                               33
N05  C     *in33         ifeq      '0'
N05  C     INPOD01       ANDNE     *BLANKS
N05  c                   CALL      'SYGE91'
N05  c                   parm                    ttdepo
N05  c                   parm                    snr
N05  c                   parm                    ttavd
N05  c                   parm                    ttopd
N05  c                   parm                    ubane
N05  c                   parm                    gifok
N05  c                   end
N05  c                   end
N05   *
     c     GIFOK         ifeq      'J'
     c                   move      *blanks       uttext
     c                   movel     '<p><img bord'uttext
     C     uttext        CAT       'er="0" sr':0 uttext
     C     uttext        CAT       'c="':0       uttext
     C     uttext        CAT       ubane:0       uttext
     C     uttext        CAT       '"></p>':0    uttext
ESPOD * kun gif-linken..
ESPODC                   callp     updHTMLvar('UPDF1':'')
ESPODC                   callp     updHTMLvar('UPDF2':'')
ESPODC                   callp     updHTMLvar('UPDF3':'')
ESPODC                   callp     updHTMLvar('UPDF4':'')
ESPODC                   callp     updHTMLvar('EXTURL':'')
ESPODC                   callp     updHTMLvar('DATE':'')
ESPODC                   callp     updHTMLvar('TIME':'')
     C                   callp     updHTMLvar('TEXT':UtText)
     C                   callp     wrtsection('row')
     c                   move      *blanks       uttext
     c                   end
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'ESPOD'
     C                   CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C     arkivkey      KLIST
     C                   KFLD                    unkode
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     c                   move      'A'           unkode            1
     C     HEADFK        klist
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C     KOFASTK1      klist
     C                   kfld                    KFTYP1           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP1 = 'TRACKPREFI'
     C     KOFASTK2      klist
     C                   kfld                    KFTYP2           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP2 = 'TRACKSUFFI'
     C     KOFASTK3      klist
     C                   kfld                    KFTYP3           10
     C                   kfld                    KFKOD3            5
     C                   MOVEL     'WEBETRACK'   KFTYP3
     C     KOFASTK4      klist
     C                   kfld                    KFTYP3
     C                   kfld                    KFKOD4            5
     C                   MOVEL     '   **'       KFKOD4
     C     KOFASTK5      klist
     C                   kfld                    KFTYP3
     C                   kfld                    KFKOD5            5
     C                   MOVEL     'OTHER'       KFKOD5
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     TrackKey      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     PODKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     c                   movel     'POD'         TTACTI
     C     ActKey        KLIST
     C                   KFLD                    KFTYPT           10
     C                   KFLD                    KFKOD
     c                   MOVEL     'TRACKT'      KFTYPT
     C     ARKEXTK       KLIST
     C                   KFLD                    ARFIRM
     C                   KFLD                    ARCEXT
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   OPEN      HEADF
N05  C                   OPEN      EDII
     C                   OPEN      arkivl02
     C                   OPEN      firmarc
     C                   OPEN      FIRM
     C                   OPEN      arktxt
     C                   OPEN      ARKEXT
     C                   OPEN      FRISOL01
     C                   OPEN      KOFAST
     C                   OPEN      BRPARF
     C                   OPEN      TRACKL01
     C                   OPEN      TRACKL02
      *
      * Initiere DS (obs - pass at ikke verdier tatt inn i parse  nulles)
     c                   move      *zeros        WDsÅMD
     c                   move      *zeros        WDsDMÅ
      *
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      PABRID = WSUSER
      *
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      * logikk ved eksternref i parameterstreng
     C     EXTREF        IFNE      *BLANKS
     C     HEAVD         ANDEQ     0
     C     HEOPD         ANDEQ     0
     C                   MOVE      'EXTRE'       PAID
     C     BrParKey      chain     BRPARF                             33
s02  C*    *IN33         IFEQ      '0'
     C                   MOVEL     PAVRDA        FSKODE
N02  C   33              MOVEL     'IFB'         FSKODE
     C                   exsr      finnopd
s02  C*                  end
     C                   end
      *
     C     BIFIRM        CHAIN     FIRM                               33
      *
     C                   endsr
      ****************************************************************************
     C     FINNOPD       BEGSR
      ****************************************************************************
     C     FRIHEAD       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     FRIKEY0       KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   KFLD                    FSDTOP
     C     FRIKEY1       KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   EVAL      FSSOK  = EXTREF
     C                   EVAL      FSDTOP = 99999999
     C     FRIKEY0       SETGT     FRISOL01
     C                   setoff                                       33
     C     *in33         doweq     '0'
N01  C     NextFri       tag
     C     FRIKEY1       readpe    FRISOL01                               33
     C     *IN33         IFEQ      '0'
     C     FRIHEAD       CHAIN     HEADF                              34
     C     *in34         IFEQ      '0'
N01  c     HEST          cabeq     'S'           NextFri
     C                   movel     FSSOK         Snr              17
     C                   move      heavd         wsavd
     C                   move      heopd         wsopd
     C                   GOTO      UTFRI
     C                   END
     C                   END
     C                   END
     C                   Z-add     0             heavd
     C                   Z-add     0             heopd
     C     UTFRI         ENDSR
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
N05  C                   CLOSE     EDII
     C                   close     HEADF
     C                   CLOSE     arkivl02
     C                   CLOSE     firmarc
     C                   CLOSE     FIRM
     C                   CLOSE     arktxt
     C                   CLOSE     ARKEXT
     C                   CLOSE     FRISOL01
     C                   CLOSE     KOFAST
     C                   close     BRPARF
     C                   CLOSE     TRACKL01
     C                   CLOSE     TRACKL02
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
     c**************************************************************
     c     lesdok        begsr
     c**************************************************************
     c                   z-add     1             na                3 0
     c                   movea     *blanks       updf
      *
     C     arkivkey      SETLL     ARKIVL02
     C     *in(94)       doweq     '0'
     C     artype        ifeq      'ZP'
     c     artype        chain     arktxt                             95
     c  n95              exsr      genbane
     c  n95              add       1             na
     c                   endif
     C     arkivkey      READE     ARKIVL02                               94
     C                   enddo
     C*
     c     UtDOK         endsr
     C***********************************************
     C     genbane       BEGSR
     C***********************************************
     C                   MOVE      ARKLAG        ARCEXT
     C     ARKEXTK       CHAIN     ARKEXT                             33
     C   33ARFIRM        CHAIN     FIRMARC                            33
     C* genererer bane for lagring
     C                   MOVE      ARAVD         AAVD              4
     C                   MOVE      AROPD         AOPD              7
NTAG c                   eval      artxt = 'Scanned POD '
     c                   movel     '<a href="'   UPDF(na)
     C     UPDF(na)      CAT       '/':0         UPDF(na)
     C     UPDF(na)      CAT       arcane:0      UPDF(na)
     C     UPDF(na)      CAT       '/':0         UPDF(na)
     C     ARLINK        IFNE      *Blanks
     C     UPDF(na)      CAT       ArLink:0      UPDF(na)
     c                   else
     C     UPDF(na)      CAT       aavd:0        UPDF(na)
V10   * TEST LINK
V10  C                   EVAL      UXLINK = '/' + %TRIM(ARCANE)
V10  C                             + '/' + AAVD + AOPD
V10  C                             + %TRIM(ARTYPE)
V10  C                             + %TRIM(ARUNDE)
V10  C                             + '.pdf'
V10  C                   MOVE      'J'           UXOK
V10  C                   CALL      'CHECKLNK'
V10  C                   PARM                    UXLINK           50
V10  C                   PARM                    UXOK              1
V10   *
V10  C                   IF        UXOK = 'N'
V10   * GAMMEL DOKUMENT MED 5 SIFRE OPPDRAGSNR
V10  C                   MOVE      aopd          AOPD5             5
V10  C                   CAT       aopd5:0       UPDF(NA)
V10  C                   ELSE
     C     UPDF(na)      CAT       aopd:0        UPDF(na)
V10  C                   END
     C     UPDF(na)      CAT       artype:0      UPDF(na)
     C     UPDF(na)      CAT       arunde:0      UPDF(na)
     c                   end
     C     '.pdf'        SCAN      updf(na)                               33
     C  N33'.PDF'        SCAN      updf(na)                               33
     C  N33'.'           SCAN      arlink                                 33
     C  N33              IF        ARKLAG = *Blanks
     C                   CAT       '.pdf':0      UPDF(na)
     C                   END
     C                   CAT       '" target="':0UPDF(na)
     C                   CAT       'SyPDFWind': 0UPDF(na)
     C                   CAT       '">':0        UPDF(na)
     C                   IF        ARRFK = *BLANKS
     C                   CAT       ARTXT:0       UPDF(na)
     C                   ELSE
     C                   CAT       ARRFK:0       UPDF(na)
     C                   END
     C     UPDF(na)      CAT       '<img src="':0UPDF(na)
     C     UPDF(na)      CAT       '/syspeddev':0UPDF(na)
     C     UPDF(na)      CAT       '/PDF.gif"':0 UPDF(na)
     C     UPDF(na)      CAT       'ALT="':1     UPDF(na)
     C     UPDF(na)      CAT       artxt:0       UPDF(na)
     C     UPDF(na)      CAT       '"></a>':0    UPDF(na)
      *
     c                   endsr
      ***********************************************************
     C     missing       begsr
      ***********************************************************
     C                   callp     updHTMLvar('EXTREF':EXTREF)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     wrtsection('MISSING')
     c                   exsr      exit
     c                   return
     C                   endsr
