********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      HLPDOC('SPOP0010' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE02
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søke kunder')
     A                                      CF08(08 'Direkt til oppdrag')
     A                                      CF09(09 'Direkt til faktura')
     A                                      CF12(12 'FORRIGE')
     A                                      CF24(24 'FLERE TASTER')
     A          H                           HLPDOC('SPOP011' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 001 24 080)
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYCM21'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0044 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 55DATE
     A                                      EDTCDE(Y)
     A                                  1 68TIME
     A                                  4  2MSGCON(023 DIV1497 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                  4 45MSGCON(024 DIV0457 *LIBL/SYMSGF)
     A            KOANVN        33A  O  5 28TEXT('AVDELINGSNAVN')
     A                                      COLOR(WHT)
     A                                  6 10MSGCON(035 DIV0051 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(023 DIV0052 *LIBL/SYMSGF)
     A            WSFN           7Y 0B  8 28DSPATR(UL)
     A  41
     AO 99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                  9  2MSGCON(023 DIV0053 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B  9 28DSPATR(UL)
     A  42
     AO 98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                 10  2MSGCON(023 DIV0054 *LIBL/SYMSGF)
     A            WSSG           3A  B 10 28DSPATR(UL)
     A  43                                  DSPATR(PC)
     A                                      COLOR(GRN)
     A                                 10 45MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT1W         6D 0B 10 52DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                 11  2MSGCON(023 DIV0055 *LIBL/SYMSGF)
     A            WSGN          15A  B 11 28DSPATR(UL)
     A  44                                  DSPATR(PC)
     A                                      COLOR(GRN)
     A                                 12  2MSGCON(023 SHU0517 *LIBL/SYMSGF)
     A            WSKNSF         8Y 0B 12 28DSPATR(UL)
     A  45                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                 12 45MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT2W         6D 0B 12 52DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                 13  2MSGCON(023 SHU0518 *LIBL/SYMSGF)
     A            WSKNKF         8Y 0B 13 28DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                 13 45MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT3W         6D 0B 13 52DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                 14  2MSGCON(023 DIV0056 *LIBL/SYMSGF)
     A            WSOT           2A  B 14 28DSPATR(UL)
     A  47                                  DSPATR(PC)
     A                                      COLOR(GRN)
     A                                 14 45MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT4W         6D 0B 14 52DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A                                 15  2MSGCON(023 DIV0057 *LIBL/SYMSGF)
     A            WSRFA         15A  B 15 28DSPATR(UL)
     A  48                                  DSPATR(PC)
     A                                      COLOR(GRN)
     A                                 16  2MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B 16 28DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                      CHECK(RB)
     A            MSGT2K        18A  O 23  2MSGID(&MSGN2K SYMSGF)
     A                                      COLOR(BLU)
     A            MSGT2L        18A  O 23 22MSGID(&MSGN2L SYMSGF)
     A                                      COLOR(BLU)
     A            MSGT2M        18A  O 23 42MSGID(&MSGN2M SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGN2K         7A  P
     A            MSGN2L         7A  P
     A            MSGN2M         7A  P
     A            MSGNR          7A  P
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '5')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            HEFNK          6Y 0O  6  5EDTCDE(4)
     A  41                                  COLOR(WHT)
     A            HEOPD          7Y 0O  6 12EDTCDE(4)
     A  42                                  COLOR(WHT)
     A            HEST           1A  O  6 20
     A  27                                  DSPATR(RI)
     A            HESG           3A  O  6 22
     A  43                                  COLOR(WHT)
     A            WHEGN         11A  O  6 26
     A  44                                  COLOR(WHT)
     A            WSPOSS         7A  O  6 38
     A  45                                  COLOR(WHT)
     A            WSPOSK         7A  O  6 46
     A  46                                  COLOR(WHT)
     A            HEOT           2A  O  6 54
     A  47                                  COLOR(WHT)
     A            HEFR           3A  O  6 58
     A            HENT           7Y 0O  6 62EDTCDE(4)
     A            HEVKT          9Y 0O  6 70EDTCDE(4)
     A            HEDTOP         8S 0H
     A            WSDTOP         6Y 0O  7  6DSPATR(UL)
     A                                      EDTCDE(4)
     A            HESDF          5A  O  7 13DSPATR(UL)
     A            HESDT          5A  O  7 19DSPATR(UL)
     A            HEPRO          8Y 0O  7 25DSPATR(UL)
     A                                      EDTCDE(4)
     A  49                                  COLOR(WHT)
NRFA A*           HERFA         15A  O  7 34DSPATR(UL)
NRFA A            HERFA15       15A  O  7 34DSPATR(UL)
     A  48                                  COLOR(WHT)
     A            WHEGNN        11A  O  7 50DSPATR(UL)
     A            HEFNS          6Y 0O  7 62DSPATR(UL)
     A                                      EDTCDE(4)
     A  41                                  COLOR(WHT)
     A            HEUR           1A  O  7 74DSPATR(UL)
     A            HEAVD          4Y 0O  7 76EDTCDE(4)
     A            HESGM         10A  H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPDOC('SPOP110' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 001 24 080)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYCM21'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV0044 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  2 61TIME
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A  41                              2 20MSGCON(023 DIV0052 *LIBL/SYMSGF)
     A  41        WSFN           7Y 0O  2 44EDTCDE(4)
     A                                      COLOR(WHT)
     A  42                              2 20MSGCON(023 DIV0053 *LIBL/SYMSGF)
     A  42        WSOPD          7Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  43                              2 20MSGCON(023 DIV0054 *LIBL/SYMSGF)
     A  43        WSSG           3A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  43                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  43        WSDT1W         6S 0O  2 67DSPATR(UL)
     A                                      COLOR(WHT)
     A  44                              2 20MSGCON(023 DIV0055 *LIBL/SYMSGF)
     A  44        WSGN          15A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  45                              2 20MSGCON(023 DIV0060 *LIBL/SYMSGF)
     A  45        WSKNSF         8Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  45                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  45        WSDT2W         6D 0O  2 67DSPATR(UL)
     A                                      COLOR(BLU)
     A  46                              2 20MSGCON(023 DIV0061 *LIBL/SYMSGF)
     A  46        WSKNKF         8Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  46                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  46        WSDT3W         6D 0O  2 67DSPATR(UL)
     A                                      COLOR(BLU)
     A  47                              2 20MSGCON(023 DIV0056 *LIBL/SYMSGF)
     A  47        WSOT           2A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  47                              2 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A  47        WSDT4W         6D 0O  2 67DSPATR(UL)
     A                                      COLOR(BLU)
     A  48                              2 20MSGCON(023 DIV0057 *LIBL/SYMSGF)
     A  48        WSRFA         15A  O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A  49                              2 20MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A  49        WSPRO          8Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            WSALLE         1A  O  2 75DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32MSGCON(017 DIV0048 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV0045 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(078 DIV0046 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYVARE
     A                                      CF01(01 'BYTT AVDELNGER')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                      OVERLAY
     A                                  1  3'SYSPED/SYCM21'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 DIV0044 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  2 62TIME
     A                                  2  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 32MSGCON(017 DIV0048 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(078 DIV0045 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(078 DIV0046 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
