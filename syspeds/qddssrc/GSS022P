********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SKRIV UT FRITEKST FRA TUR
10027 * 02 YBC PTF10 SKRIV ORDRETYPE
********************************************************************
     A          R GSS022CO
     A            STRICO        70      1  1
     A          R GSS022PA
     A            STRI01        70      1  1
     A            KFMT1         75      1  1
     A            KFMT2         75      1  1
     A            FINM          30A     2  2
     A                                  2 34'PACKINGLIST FOR TRIP NO.'
     A            TRTNO         11  0   2 60EDTCDE(Z)
     A                                  2 81'DATE'
     A                                  2 86DATE EDTCDE(Y)
     A                                  2 95'TIME'
     A            XTID4          4  0   2100EDTWRD('  .  ')
     A                                  2107'PAGE'
     A                                  2112PAGNBR
S09  A* 50                              3107'C O P Y'
N09  A  50N51                           3107'C O P Y'
     A                                      HIGHLIGHT
     A                                  4  2'CLIENT'
     A            TRCNO          8  0   4  9EDTCDE(Z)
     A            KNAVN         30A     4 19
     A            KNAVNX        30A     4 19
     A N40                              4 51'RCV'
     A N40        TRBCD          5A     4 55
     A N40        CBNAB         30A     4 61
     A                                  4 95'DELIVERY DATE'
SV5  A*           TRDTL          6  0   4110EDTCDE(Y)
NV5  A            XTDTL          6  0   4110EDTCDE(Y)
     A                                  5  2'DELIVERY PLACE'
     A            TRAD1         30A     5 19
     A                                  5 95'SENT'
SV5  A*           TRDTS          6  0   5110EDTCDE(Y)
NV5  A            XTDTS          6  0   5110EDTCDE(Y)
     A            TRAD2         30A     6 19
     A                                  6 51'SENT BY'
     A            TRDW          15A     6 59
     A            TRAD3         30A     7 19
     A            TRAD4         30A     8 19
     A            TRAD5         30A     9 19
     A            TRAD6         30A    10 19
     A                                 12  2'NATURE  /VENDOR'
     A                                 12 33'CLIENTS P.O.NO.'
     A                                 12 53'NO.PCS/DATE GROS/VOLUME'
     A                                 12 78'POSISJON 1/2/3'
     A                                 12 93'COL. FROM/TO'
     A                                 12106'SECURE'
N02  A                                 12113'TYPE'
     A                                 13  2' '
      *
     A          R GSS022PB                  SPACEB(1)
     A            ORDSC         30A        2
     A  29                                  HIGHLIGHT
     A            XCONO1        20A       33
     A  29                                  HIGHLIGHT
     A            ORCLI          5  0     55EDTCDE(Z)
     A  29                                  HIGHLIGHT
     A            ORWT           7  1     65EDTCDE(4)
     A  29                                  HIGHLIGHT
     A            ORPOS1         5A       80
     A  29                                  HIGHLIGHT
     A            ORCLNF         3  0     95EDTCDE(Z)
     A  29                                  HIGHLIGHT
     A            XSECUR         1       106
     A  29                                  HIGHLIGHT
     A                                      SPACEA(1)
N02  A            ORTYPE         1       113
     A            SFNM          30A        2
     A  29                                  HIGHLIGHT
     A            XCONO2        20A       33
     A  29                                  HIGHLIGHT
     A            XODTL          6  0     55EDTCDE(Y)
     A  29                                  HIGHLIGHT
     A            ORVOL          5  2     67EDTCDE(4)
     A  29                                  HIGHLIGHT
     A            ORPOS2         5A       80
     A  29                                  HIGHLIGHT
     A            ORCLNT         3  0     95EDTCDE(Z)
     A  29                                  HIGHLIGHT
     A                                      SPACEA(1)
     A            ORONO          7  0      2EDTCDE(Z)
     A  29                                  HIGHLIGHT
     A            XCONO3        20A       33
     A  29                                  HIGHLIGHT
     A            ORPOS3         5A       80
     A  29                                  HIGHLIGHT
     A N40                                  SPACEA(1)
     A  40        CBNAB         30A       90SPACEA(1)
     A  35        XCONO4        20A       33SPACEA(1)
     A  36        XCONO5        20A       33SPACEA(1)
     A  37        XCONO6        20A       33SPACEA(1)
      *
     A          R GSS022PC                  SPACEB(1)
     A                                     2'----------------------------------'
     A                                    36'---------------------  L A S T   P'
     A                                    70' A G E  --------------------------'
     A                                   104'----------------'
      *
     A          R GSS022PD                  SPACEB(1)
     A                                    90'CONTIUOUS'
      *
     A          R GSS022PE
     A            GDDSC         55A        2SPACEA(1)
      *
     A          R GSS022PF                  SPACEB(1)
     A            XXFT          30A        2
      *
     A          R GSS022PG
     A            XCONOX        20A       33SPACEA(1)
N01   *
N01  A          R GSS022PH
N01  A            XTRFT         70A        2SPACEA(1)
