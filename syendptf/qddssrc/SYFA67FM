     A*%%TS  SD  20180207  144659  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA67PNL)
     A                                      HLPTITLE('ENDRING AV PRISER I PROSE-
     A                                      NT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A*%%TS  SD  20180207  144659  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'End program')
     A                                      CF04(04 'CUSTOMER BY ALPHA')
     A                                      TEXT('Parameters for price raise')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFA67'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(043 DIV2096 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(029 DIV2097 *LIBL/SYMSGF)
     A            WBFRPM         1A  B  2 35VALUES('+' '-' 'M')
     A            WBFRPR         4Y 2B  2 37CHECK(RB)
     A                                      EDTWRD('  ,  ')
     A                                  2 45MSGCON(014 DIV2098 *LIBL/SYMSGF)
     A                                  2 60MSGCON(020 JOV0370 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                  3  2'M'
     A                                  3  8MSGCON(023 DIV2099 *LIBL/SYMSGF)
     A            WBFRAV         1Y 0B  3 35
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                  3 45MSGCON(014 DIV2100 *LIBL/SYMSGF)
     A                                  3 60MSGCON(013 JOV0371 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A            WBFRSI         7Y 0O  3 74COLOR(WHT)
     A N65                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                  4 60MSGCON(013 JOV0372 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A            WBFRTO         7Y 0O  4 74COLOR(WHT)
     A N65                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                  5  8MSGCON(023 DIV2101 *LIBL/SYMSGF)
     A            WBFRFR         5A  B  5 35
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A            WBFRFL         2A  B  5 41
     A                                  5 45MSGCON(014 JOV0374 *LIBL/SYMSGF)
     A                                  6  8MSGCON(023 DIV2103 *LIBL/SYMSGF)
     A            WBFRTI         5A  B  6 35
     A  97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A            WBFRTL         2A  B  6 41
     A                                  6 45MSGCON(014 JOV0374 *LIBL/SYMSGF)
     A                                  8  2MSGCON(029 DIV2104 *LIBL/SYMSGF)
     A            WBGEPM         1A  B  8 35VALUES('+' '-' 'M' 'B')
     A            WBGEPR         4Y 2B  8 37EDTWRD('  ,  ')
     A                                      CHECK(RB)
     A                                  8 43MSGCON(007 SHU1135 *LIBL/SYMSGF)
     A            WBGEVA         3A  B  8 51
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                  8 60MSGCON(013 JOV0371 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A            WBGESI         7Y 0O  8 74COLOR(WHT)
     A N65                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                  9  2'M/B'
     A                                  9  8MSGCON(023 DIV2099 *LIBL/SYMSGF)
     A            WBGEAV         1Y 0B  9 35
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                  9 45MSGCON(014 DIV2100 *LIBL/SYMSGF)
     A                                  9 60MSGCON(013 JOV0372 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A            WBGETO         7Y 0O  9 74COLOR(WHT)
     A N65                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 10  8MSGCON(023 DIV2105 *LIBL/SYMSGF)
     A            WBGETA         1A  B 10 35
     A                                 10 45MSGCON(014 JOV0368 *LIBL/SYMSGF)
     A                                 11  8MSGCON(023 SHU1134 *LIBL/SYMSGF)
     A            WAGB01         3A  B 11 35
     A            WAGB02         3A  B 11 39
     A            WAGB03         3A  B 11 43
     A            WAGB04         3A  B 11 47
     A            WAGB05         3A  B 11 51
     A            WAGB06         3A  B 11 55
     A            WAGB07         3A  B 11 59
     A                                 12  2MSGCON(029 DIV2106 *LIBL/SYMSGF)
     A            WAFRPM         1A  B 12 35VALUES('+' '-' 'M')
     A            WAFRPR         4Y 2B 12 37EDTWRD('  ,  ')
     A                                      CHECK(RB)
     A                                 12 45MSGCON(014 DIV2098 *LIBL/SYMSGF)
     A                                 12 60MSGCON(013 JOV0371 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A            WAFRSI         7Y 0O 12 74COLOR(WHT)
     A N65                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 13  2'M'
     A                                 13  8MSGCON(023 DIV2099 *LIBL/SYMSGF)
     A            WAFRAV         1Y 0B 13 35
     A  95                                  DSPATR(PC)
     A  95                                  DSPATR(RI)
     A                                 13 45MSGCON(014 DIV2100 *LIBL/SYMSGF)
     A                                 13 60MSGCON(013 JOV0372 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A            WAFRTO         7Y 0O 13 74COLOR(WHT)
     A N65                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 14 61MSGCON(020 JOV1549 *LIBL/SYMSGF)
     A                                 15  8MSGCON(023 DIV2107 *LIBL/SYMSGF)
     A            WAFRKU         8Y 0B 15 35EDTCDE(4)
     A                                      CHECK(RB)
     A  92                                  DSPATR(PC)
     A  92                                  DSPATR(RI)
     A                                 15 45MSGCON(015 JOV0367 *LIBL/SYMSGF)
     A            WAFKU1         8Y 0B 15 61EDTCDE(4)
     A                                      CHECK(RB)
     A                                 15 70'->'
     A            WAFKU2         8Y 0B 15 73EDTCDE(4)
     A                                      CHECK(RB)
     A                                 16  8MSGCON(023 DIV2101 *LIBL/SYMSGF)
     A            WAFRFR         5A  B 16 35
     A  94                                  DSPATR(PC)
     A  94                                  DSPATR(RI)
     A            WAFRFL         2A  B 16 41
     A                                 16 45MSGCON(014 JOV0374 *LIBL/SYMSGF)
     A                                 17  8MSGCON(023 DIV2103 *LIBL/SYMSGF)
     A            WAFRTI         5A  B 17 35
     A  93                                  DSPATR(PC)
     A  93                                  DSPATR(RI)
     A            WAFRTL         2A  B 17 41
     A                                 17 45MSGCON(014 JOV0374 *LIBL/SYMSGF)
     A                                 18 60MSGCON(013 JOV0371 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A            WAGESI         7Y 0O 18 74COLOR(WHT)
     A N65                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 19  2MSGCON(029 DIV2109 *LIBL/SYMSGF)
     A            WAGEPM         1A  B 19 35VALUES('+' '-' 'M' 'B')
     A            WAGEPR         4Y 2B 19 37EDTWRD('  ,  ')
     A                                      CHECK(RB)
     A                                 19 43MSGCON(007 SHU1135 *LIBL/SYMSGF)
     A            WAGEVA         3A  B 19 51
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                 19 60MSGCON(013 JOV0372 *LIBL/SYMSGF)
     A N65                                  DSPATR(ND)
     A            WAGETO         7Y 0O 19 74COLOR(WHT)
     A N65                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                 20  2'M/B'
     A                                 20  8MSGCON(023 DIV2099 *LIBL/SYMSGF)
     A            WAGEAV         1Y 0B 20 35
     A  91                                  DSPATR(PC)
     A  91                                  DSPATR(RI)
     A                                 20 45MSGCON(014 DIV2100 *LIBL/SYMSGF)
     A                                 21  8MSGCON(023 DIV2107 *LIBL/SYMSGF)
     A            WAGEKU         8Y 0B 21 35CHECK(RB)
     A                                      EDTCDE(4)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A                                 21 45MSGCON(015 JOV0367 *LIBL/SYMSGF)
     A            WAGKU1         8Y 0B 21 61EDTCDE(4)
     A                                      CHECK(RB)
     A                                 21 70'->'
     A            WAGKU2         8Y 0B 21 73EDTCDE(4)
     A                                      CHECK(RB)
     A                                 22  8MSGCON(023 SHU1134 *LIBL/SYMSGF)
     A            WAGE01         3A  B 22 35
     A            WAGE02         3A  B 22 39
     A            WAGE03         3A  B 22 43
     A            WAGE04         3A  B 22 47
     A            WAGE05         3A  B 22 51
     A            WAGE06         3A  B 22 55
     A            WAGE07         3A  B 22 59
     A                                 22 63MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  8MSGCON(023 DIV2111 *LIBL/SYMSGF)
     A            WAGEDA         6Y 0B 23 35CHECK(RB)
     A                                      EDTCDE(4)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A                                 23 63MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
