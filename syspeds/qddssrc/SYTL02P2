     A*****************************************************************
     A* UTSKRIFT AV GODS-FORHÅNDSREGISTERING (GRENSEPASS ULIK BLANK)  *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=132 OVERFLOW=??       *
     A*****************************************************************
     A          R HEADF1                    SKIPB(1)
     A
     A* HEADING
     A                                    45'ANMODNING OM FORHÅNDSREGISTRERING'
     A                                      HIGHLIGHT
     A                                     2'Til fax-nummer: '
     A                                      SPACEB(2)
     A            GFFAX         15A       18
     A                                      HIGHLIGHT
     A                                    37'Lagerholder:'
     A            GFLBS1        30A       50
     A                                      HIGHLIGHT
     A                                    96MSGCON(22 TRO0436 SYMSGF)
     A            GFLBKO         5A      120
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
     A            GFLBS2        30A       50
     A                                      HIGHLIGHT
     A                                   111MSGCON(05 TRO0449 SYMSGF)
     A            UDAY           2S 0    117
     A                                   119'.'
     A            UMONTH         2S 0    120
     A                                   122'.'
     A            UYEAR          2S 0    123
     A            GFLBS3        30A       50
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                   111'Kl..:'
     A            TID            6S 0    117
     A                                       EDTCDE(Y)
     A            GFLBS4        30A       50
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A  66                               111'Side:'
     A  66        FSIDE          3S 0    122EDTCDE(Z)
     A*DETALJ HEADINGER
     A                                    19'TRANSPORTMIDLETS'
     A                                      SPACEB(2)
     A                                   +01'REISERUTE FRA         '
     A                                   +01'ANKOMSTTIDSPUNKT      '
     A                                   +01'ANKOMSTTIDSPUNKT TIL  '
     A                                   +01'TYPE LAST   '
     A                                     2'GODSNUMMER'
     A                                      SPACEB(1)
     A                                    19'NAVN,KJENNETEGN '
     A                                   +01'OPPLASTINGSSTEDET     '
     A                                   +01'TIL INNPASS. TOLLSTED '
     A                                   +01'FØRSTE LOSSESTED      '
     A                                   +01'(MOTTAKER)  '
     A
     A*DETALJER
     A          R DETALF                    SPACEB(1)
     A            GOGN          15A        2
     A                                   +01'|'
     A            GOBILN        13A      +01
     A                                   +01'|'
     A            GOREI1        20A      +01
     A                                   +01'|'
     A            GOGREN        20A      +01
     A                                   +01'|'
     A            GOLSEN        20A      +01
     A                                   +01'|'
     A            GOTYPE        20A      +01
     A          R DETAL2                    SPACEB(1)
     A                                    18'|'
     A                                   +15'|'
     A            GOREI2        20A      +01
     A                                   +01'|'
SV5  A*           GOGRDT         6  0    +01EDTCDE(Y)
NV5  A            XOGRDT         6  0    +01EDTCDE(Y)
     A            GOGRKL         4  0    +01EDTCDE(Y)
     A                                   +07'|'
SV5  A*           GOLSDT         6  0    +01EDTCDE(Y)
NV5  A            XOLSDT         6  0    +01EDTCDE(Y)
     A            GOLSKL         4  0    +01EDTCDE(Y)
     A                                   +07'|'
     A            GOMOTT        15A      +01
     A          R DETAL3                    SPACEB(1)
     A                                    18'|'
     A                                   +15'|'
     A            GOREI3        20A      +01
     A                                   +01'|'
     A                                   +22'|'
     A                                   +22'|'
     A          R DETAL4                    SPACEB(1)
     A                                    18'|'
     A                                   +15'|'
     A            GOREI4        20A      +01
     A                                   +01'|'
     A                                   +22'|'
     A                                   +22'|'
      * STREK
     A          R STREK                     SPACEB(1)
     A                                     1'------------------------------'
     A                                    31'------------------------------'
     A                                    61'------------------------------'
     A                                    91'------------------------------'
     A                                   121'-----------'
     A*TOTAL (BUNN)
     A          R BOTTF
     A                                 35  1'------------------------------'
     A                                 35 31'------------------------------'
     A                                 35 61'------------------------------'
     A                                 35 91'------------------------------'
     A                                 35121'-----------'
     A                                 36 66'|'
     A                                 37 66'|'
     A                                 37  5'ENDRINGER VEDR. REISERUTE,'
     A                                 37 70'FORHÅNDSREGISTRERING AV'
     A                                 37 96'......'
     A                                 37103'TRANSPORTER GODKJENT'
     A                                 38  5'ANKOMSTTIDSPUNKT O.L      '
     A                                 38 66'|'
     A                                 39 66'|'
     A                                 40 66'|'
     A                                 41 66'|'
     A                                 42 66'|'
     A                                 42 70'DATO:'
     A                                 42 90'STEMPEL:'
     A                                 43 66'|'
     A                                 44 66'|'
     A                                 45 66'|'
     A                                 45 70'UNDERSKRIFT:'
     A                                 46 66'|'
     A                                 46 84'..................................'
      * Fortsetter
     A          R FORTS                     SPACEB(1)
     A                                   100'Fortsetter........'
     A                                      HIGHLIGHT
