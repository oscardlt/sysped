      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS113 MODULE(*LIBL/SS113
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FGSSORL10  IF   E           K DISK    USROPN
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSRFF    IF   E           K DISK    USROPN
     FGSSTRL01  IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           k disk    usropn
     FHEADF     UF   E           k disk    usropn
     FDOKUF     UF   E           k disk    usropn
     FGSSOLF    UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D CURTUR          S             11
     D CURTURN         S             11  0
     D KUNDE           S              8
     D KUNDEN          S              8  0
     D TotCol          S              5  0
     D TotVkt          S              7  1
     D TotM3           S              7  2
     D TotLM           S              7  2
     D GrpNr           s              8
     D ItemCount       s             10i 0
     D i               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Number of records matching search criteria
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * MIDLERTIDIG - LÅNER FRITEKST 4 FOR AREA
     D                 DS
     D  ORFT4                  1     30
     D  ARTXT                  1      2
     D  ORLMA                 11     14
     D                 DS
     D  WTIME                  1     14  0
     D  WTID4                  1      4  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  PDATO                  1      8  0
     D  PDATOÅ                 1      4  0
     D  PDATOM                 5      6  0
     D  PDATOD                 7      8  0
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *------------------                                                   ******
      * 3-Section /$tablestr
     C                   callp     wrtsection('tablestr')
      * 4-Sections /$tablerow
     C                   z-add     0             bseq              3 0
     C                   z-add     0             TotCol
     C                   z-add     0             TotVkt
     C                   z-add     0             TotM3
     C                   z-add     0             TotLM
      *
     c     strles        tag
      *
      * STATUS <option value="">IPR In PRoduct & RFP Released
      *        <option value="R">Only RFP Released From Prod.
     C     SHIPFROM      SETLL     GSSORL10
     C     SHIPFROM      READE     GSSORL10                               50
      *
     C     *IN50         DOWEQ     '0'
      * Hopp over poster som ikke er på turen
     c     ORTNO         CABNE     CURTURN       NextRec
      *
     C                   IF        ORLMA = *BLANKS
     C                   MOVE      *ZEROS        ORLM              4 2
     C                   ELSE
     C                   MOVE      ORLMA         ORLM
     C                   END
      *
     C     ORONO         SETLL     GSSPOF
     C     ORONO         READE     GSSPOF                                 33
     C   33              MOVE      *BLANKS       POCONO
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 33
     C     *IN33         DOWEQ     '0'
     C     RFKD          IFEQ      'CPO'
     C                   LEAVE
     C                   ENDIF
     C     ORONO         READE     GSSRFF                                 33
     C  N33              ENDDO
     C   33              MOVE      *BLANKS       RFREF
     C     CUNDL1K       CHAIN     CUNDL01                            33
     C   33              EVAL      KNAVN = '*UNKNOW'
      *
     c                   exsr      FixCli
     C                   eval      bseq=bseq + 1
     C                   eval      Totcol=totcol + ORCLI
     C                   eval      TotVkt=totvkt + ORWT
     C                   eval      TotM3 =totm3  + ORVOL
     C                   eval      TotLM =totLM  + ORLM
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     c     NextRec       tag
     C     SHIPFROM      READE     GSSORL10                               50
     C  N50              ENDDO
      *
      *  -Section /$TRIPBOT
     C                   callp     updhtmlvar('TOTCOL':
     C                             %trim(%editc(TOTCOL:'Z')))
     C                   callp     updhtmlvar('TOTVKT':
     C                             %trim(%editc(TOTVKT:'M')))
     C                   callp     updhtmlvar('TOTM3':
     C                             %trim(%editc(TOTM3:'M')))
     C                   callp     updhtmlvar('TOTLM':
     C                             %trim(%editc(TOTLM:'M')))
     C                   callp     wrtsection('TripBot')
      *
      *  -Section /$tableend
     C                   callp     wrtsection('tableend')
      *
      * Oppdater fraktbrev og oppdrag.
     c     HeadfKey      chain     HEADF                              33
     c     *in33         IfEQ      *off
     c                   z-add     TotCol        HENT
     c                   z-add     TotVkt        HEVKT
     c                   z-add     TotM3         HEM3
     c                   z-add     TotLM         HELM
     c                   update    HEADFR
     c                   end
     c     DokufKey      chain     DOKUF                              33
     c     *in33         IfEQ      *off
     c                   z-add     TotCol        DFNT
     c                   z-add     TotCol        DFNTLA
     c                   z-add     TotVkt        DFVKT
     c                   z-add     TotM3         DFM3
     c                   z-add     TotLM         DFLM
     c                   update    DOKUFR
     c                   end
      *
      * GENERER FRAKTBREVS PDF
     C                   MOVE      CURTURN       TUR              11
     C                   call      'SS116C'
     c                   parm                    BIFIRM
     c                   parm                    TUR
      *
     C     SLUTT         tag
     C                   EXSR      SetEnd
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   MOVE      'N'           XFEIL             1
     C                   eval      lng     = zhbgetvar('lng')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      CURTUR  = zhbgetvar('CURTUR')
     C                   eval      CURTURN = c2n2(CURTUR)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':'white')
      *
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   TIME                    WTIME
     C                   MOVE      WÅÅ           PDATOÅ
     C                   MOVE      WMM           PDATOM
     C                   MOVE      WDD           PDATOD
     C                   callp     updHTMLvar('PDATO':
     C                             %trim(%editw(PDATO:'    .  .  ')))
     C                   callp     updHTMLvar('PTID':
     C                             %trim(%editw(WTID4:'  :  ')))
     C                   callp     updhtmlvar('CURTUR':
     C                             %trim(%editc(CURTURN:'Z')))
     C                   callp     updhtmlvar('KUNDE':
     C                             %trim(%editc(TRCNO:'Z')))
     C                   MOVE      TRCNO         ORCNO
     C     CUNDL1K       CHAIN     CUNDL01                            33
     C   33              EVAL      KNAVN = '*UNKNOW'
     C                   callp     updhtmlvar('KUNDENA':KNAVN)
      * Transp-date
     C                   callp     updHTMLvar('TDATO':
     C                             %trim(%editw(TRDTS:'    .  .  ')))
      * Reciever
     C                   callp     updhtmlvar('TRAD1':TRAD1)
     C                   callp     updhtmlvar('TRAD2':TRAD2)
     C                   callp     updhtmlvar('TRAD3':TRAD3)
     C                   callp     updhtmlvar('TRAD4':TRAD4)
     C                   callp     updhtmlvar('TRAD5':TRAD5)
     C                   callp     updhtmlvar('TRAD6':TRAD6)
     C                   callp     updhtmlvar('SHIPFROM':SHIPFROM)
     C                   callp     updhtmlvar('SHIPTO':TRLOCT)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
     c* Sequence of this Recd (numeric, converted to char)
     C                   move      bseq          bseqchar          3
     C                   callp     updhtmlvar('SEQ':bseqchar)
     C                   callp     updhtmlvar('KNAVN':KNAVN)
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   callp     updhtmlvar('RFREF':RFREF)
     C                   callp     updhtmlvar('KLL':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   callp     updhtmlvar('VKT':
     C                             %trim(%editc(ORWT:'M')))
     C                   callp     updhtmlvar('M3':
     C                             %trim(%editc(ORVOL:'M')))
     C                   callp     updhtmlvar('LM':
     C                             %trim(%editc(ORLM:'M')))
     C                   callp     updhtmlvar('DATE':
     C                             %trim(%editw(ORDTWL:'    .  .  ')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS113'
      *------------------
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSTRL01                             50
     C                   open      GSSORL10                             50
     C                   open      GSSPOF                               50
     C                   open      GSSRFF                               50
     C                   open      CUNDL01                              50
     C                   OPEN      BRPARF
     C                   OPEN      HEADF
     C                   OPEN      DOKUF
     C                   open      GSSOLF                               50
      *
     C     CUNDL1K       klist
     C                   kfld                    BIFIRM
     C                   kfld                    ORCNO
      *
     c                   Movel     CurturN       DFAVD
     c                   Move      CurturN       DFOPD
     C     HeadfKey      klist
     C                   kfld                    DFAVD
     C                   kfld                    DFOPD
      *
     C     DokufKey      klist
     C                   kfld                    DFRI
     C                   kfld                    DFAVD
     C                   kfld                    DFOPD
     C                   kfld                    DFFBNR
     c                   move      'F'           DFRI
     c                   z-add     1             DFFBNR
      *
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Hent fabrikk kode (Ship From) / Navn
     c                   Move      *blanks       SHIPFROM          5
     C                   MOVE      'SSFAB'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        SHIPFROM
     c  n33              MoveL     PAVRDA        WSNA
      *
     c     CurTurN       chain     GSSTRL01                           55
     c
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     GSSTRL01
     C                   close     GSSORL10
     C                   close     GSSPOF
     C                   close     GSSRFF
     C                   close     CUNDL01
     C                   close     BRPARF
     C                   close     HEADF
     C                   close     DOKUF
     C                   close     GSSOLF
     C                   eval      *inlr = *on
      *
     C                   endsr
      *=====================================================================******
      *  FIXCLI - Ant poster i kollifil GSSOLF tilsvarende ant KLL
      *=====================================================================******
     C     FIXCLI        begsr
     C                   Move      *zeros        KllNr             3 0
      * slett hvis det finnes flere poster enn KOLLI...
     c     ORONO         SETLL     GSSOLF
     c     ORONO         READE     GSSOLF                                 33
     c     *in33         DOWEQ     *OFF
     C                   add       1             KllNr
     c     Kllnr         ifgt      ORCLI
     c                   DELETE    GSSOLFR
     c                   else
     c                   move      ORTNO         OLTNO
     c                   move      ORPOS1        OLSTD
     c                   update    GSSOLFR
     c                   end
     c     ORONO         READE     GSSOLF                                 33
     c                   end
      * Legg till hvis det finnes FÆRRE poster enn KOLLI...
     c     Kllnr         DOWLT     ORCLI
     c     Kllnr         IFEQ      99
     C                   leave
     c                   end
     C                   add       1             Kllnr
     c                   clear                   GSSOLFR
     c                   move      ORONO         OLONO
     c                   z-add     kllnr         OLLNO
     c                   move      ORTNO         OLTNO
     c                   move      ORPOS1        OLSTD
     c                   WRITE     GSSOLFR
     C                   enddo
      *
     C                   endsr
