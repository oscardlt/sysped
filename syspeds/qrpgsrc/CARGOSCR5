********************************************************************
      * Program for å summere kolli i CAGOF basert på HENTE-OPPDRAG (HENOP/HENOS/HENOK)
      * Kalles KUN (fra CARGOSCR3) når kolli (henok) er komplett skutt på gitt sending(henos)
      * Oppdaterer vkt/m3/lm (+ status HENOS) - dersom 999-oppdrag(Lager)-Auto-overfør
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10153 * 01 JOV PTF10 Total omskr - Nytt prog miks av gml CARGOSUM /CARGOSCR3
10153 * 01 JOV PTF10 CHAIN AV HENOP FLYTTET  (for å utføeres uavhengig av vektomregning)
      * 02 jov PTF10 Vekt settes til 1 kg pr kll dersom den mangler
********************************************************************
     FHENOK     IF   E           K DISK
     FHENOK01   UF   E           K DISK
     F                                     RENAME(HENOKR:HENOK1R)
     FCARGOF    UF   E           K DISK
     FCARGOO1   UF A E           K DISK
     FHENOS     UF   E           K DISK
     FHENOP     IF   E           K DISK
     Fcum3lm    IF   E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  HOSNDN                 1     20
     D  HOSNDN3                1      3
      *
     c                   EXSR      INIT
      *
     c                   EXSR      MAIN
      *
      * heller ikke gammel vesjon utfører noen rekalk...
      * har betydning kun for fordeling av hente-kost - oppdraget rekalkes jo etter overføring
     c*                  if        HEVKT<>GMVKT or HEM3<>GMM3 or HELM<>GMLM
     c*                  EXSR      REKALK
     c*                  endif
      *
      * Sndnr på header-nivå er 999......, da er det ikke henting,
      *       men ensending/direkte innskyting fra lager(og de har kun en sendin)- AUTO-overfør
      * MEN KUN NÅR Alle skutt-> Test er overkill- kalles kun når alle skutt
     c                   if        sumNTX = sumNTA
     c     HOSNDN3       IFEQ      '999'
     c                   call      'SYTR34Z'
     c                   parm                    HSLNR
     c                   end
     c                   end
      *
     c                   move      *on           *inlr
     c                   return
      *
     C***********************************************
     C     MAIN          BEGSR
     C***********************************************
      * Akkumuler NÅ-verdier til sum...
     C     SndKey        SETLL     HENOK
     C     SndKey        READE     HENOK                                  33
     C     *IN33         Doweq     '0'
     C                   add       1             sumNTA                         Ant kolliIDs ALLE
     c     HKSTIN        ifne      ' '
     C                   add       1             sumNTX                         Ant sjekke INN TERM
     c                   endif
     c                   eval      KOID = HKID
     C     CARGOFKey     Chain     CARGOF                             34
     C   34              MOVE      *blanks       KOSTNR
     C   34              MOVE      *zeros        KOMVKT
     C   34              MOVE      *zeros        KOMM3
     C   34              MOVE      *zeros        KOMLM
     C  N34              add       1             sumNTCS                        Ant kolli CS/CARGOF
      * oppdater at kolliet er matchet til oppdraget (gjøre også i ..CSR3 - overgang )
     C  N34              move      UULNR         KOLNR
     C  N34              move      UULNR2        KOLNR2
     C  N34              update    CARGOFR
     C     KOSTNR        ifeq      '02'                                         OSL pall
     C     KOSTNR        oreq      '52'                                         DRA pall
     C     KOSTNR        oreq      '98'                                         PDA- på pall
     C                   add       1             sumNTP                         Ant kolli på PALL
     C                   end
     C     KOMVKT        IFEQ      *ZEROS
S02  C*                  z-add     10            KOMVKT
N02  C                   z-add     1             KOMVKT
     C                   end
     C                   add       KOMVKT        sumVKT
     C                   add       KOMM3         sumM3
     C                   add       KOMLM         sumLM
     C     SndKey        READE     HENOK                                  33
     C                   enddo
      *
      * Dersom NÅ-verdier er større enn Opprinnelig - oppdater
     C     SndKey        Chain     HENOS                              34
     c                   z-add     HSVKT         GMVKT
     c                   z-add     HSM3          GMM3
     c                   z-add     HSLM          GMLM
      * om henteoppdraget alt er overført - avbryt (skal ikke kunne skje..)
     C     HSST          IFEQ      'O'
     C                   update    HENOSR
     C                   move      *on           *inlr
     c                   return
     C                   end
      * Alle skutt-> status på send-nivå. Test er overkill- kalles kun når alle skutt(pr 01.05.2012)
     c                   if        sumNTX = sumNTA
     C                   MOVE      'X'           HSSTIN
     c                   endif
      *
     C     Sndkey        Chain     Cargoo1                            35
      * Dersom første gang - benytt verdier fra oppdraget som GM /Original (COG...-felt)
     c     *in35         ifeq      '1'
     C                   move      UULNR         COLNR
     C                   move      UULNR2        COLNR2
     C                   move      *zeros        COAVD
     C                   move      *zeros        COOPD
     C                   z-add     HSANT         COGNTB
     C                   z-add     HSANTP        COGNTP
     C                   z-add     HSVKT         COGVKT
     C                   z-add     HSM3          COGM3
     C                   z-add     HSLM          COGLM
     C                   z-add     HSFBV         COGFBV
     C                   z-add     HSBELH        COFRAB
     C                   END
      *
     c                   z-add     sumNTCS       COMNT                          Kolli I CS/CARGOF
     c                   z-add     sumNTP        COMNTP                         På pall
     c                   z-add     sumNTP        HSANTP
      * IKKE lavere enn COG-verdier (G=GM/Original(EDI?) - lagres i CARGOO ved første WRITE.)
     c                   z-add     sumVKT        COMVKT
     C     sumVKT        iflt      COGVKT
     c                   z-add     COGVKT        sumVKT
     C                   endif
     C                   ADD       0.99          sumVKT                         (sumvkt=7.2 )
     C                   MOVE      00            sumVKT
     c                   z-add     sumVKT        HSVKT
      *
     c                   z-add     sumM3         COMM3
     C     sumM3         iflt      COGM3
     c                   z-add     COGM3         sumM3
     C                   endif
     c                   z-add(H)  sumM3         HSM3
      *
     c                   z-add     sumLM         COMLM
     C     sumLM         iflt      COGLM
     c                   z-add     COGLM         sumLM
     C                   endif
     c                   z-add     sumLM         HSLM
      *
     C                   exsr      FinnFvkt
     c                   z-add     COMFBV        HSFBV
      * siste pos av HSDS indikerer J=endret av CS (sytr34 vil da åpne for R =rekalk agent)
     c                   if        HSVKT<>GMVKT or HSM3<>GMM3 or HSLM<>GMLM
     c                   MOVE      'J'           HSDS
     c                   endif
     c  N34              update    HENOSR
     c     *in35         ifeq      '1'
     C                   write     CARGOOR
     c                   else
     C                   update    CARGOOR
     c                   endif
      *
      *
     C                   ENDSR
      *
     C***********************************************
     C     FinnFvkt      BEGSR
     C***********************************************
      *
     C                   Z-add(H)  COMVKT        COMFBV
     C     COMM3         IFeq      *zeros
     C     COMLM         andeq     *zeros
     c                   goto      UtFbv
     c                   endif
      *
      * Burde være hentet fra avd..
     C                   z-add     286           XXM3IN            3 0
     C                   z-add     2000          XXLMIN            5 0
      * avvikende Kundefaktor??
S01  C*    UULNR         CHAIN     HENOP                              33
     C                   select
     C     HSFR          wheneq    'M  '
     C                   move      HSKNK         M3KUND
     C     HSFR          wheneq    'A  '
     C                   move      HSKNA         M3KUND
     C                   other
     C                   move      HOKNS         M3KUND
     C                   endsl
     C     KUNKEY        CHAIN     CUM3LM                             33
     C     *in33         IFEQ      *OFF
     C     m3m3          Ifne      *zeros
     C                   Z-ADD     m3m3          XXM3IN
     C                   endif
     C     mllm          Ifne      *zeros
     C                   Z-ADD     mllm          XXLMIN
     C                   endif
     C                   ENDIF
      *
      * Finn m3-fvekt
     C     COMM3         MULT(H)   XXM3IN        COMM3F            5 0
     C     Comm3F        Ifgt      COMFBV
     C                   Z-add(H)  COMM3F        COMFBV
     C                   END
      * Finn LM-fvekt
     C     COMLM         MULT(H)   XXLMIN        COMLMF            5 0
     C     COMLMF        Ifgt      COMFBV
     C                   Z-add(H)  COMLMF        COMFBV
     C                   END
      *
     C     UtFbv         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UULNR             7 0
     C                   PARM                    UULNR2            3 0
     C                   PARM                    UUTERM           20
      *
     C                   eval      KOTERM = UUTERM
      *
     C     SndKey        KLIST
     C                   KFLD                    UULNR
     C                   KFLD                    UULNR2
     C     KunKey        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    M3KUND
     C     CARGOFKey     KLIST
     C                   KFLD                    KOID
     C                   KFLD                    KOTERM
      *
     C     *loval        setll     FIRM
     C                   read      firm
      *
N01  C     UULNR         CHAIN     HENOP                              33
      *
     C     *like         define    HsVKT         GMVKT
     C     *like         define    HsM3          GMM3
     C     *like         define    HsLM          GMLM
     C     *like         define    COGNTA        sumNTA
     C     *like         define    sumNTA        sumNTX
     C     *like         define    COMNT         sumNTCS
     C     *like         define    COMNTP        sumNTP
     C     *like         define    COMVKT        sumVKT
     C     *like         define    COMM3         sumM3
     C     *like         define    COMLM         sumLM
     C     *like         define    COMFBV        sumFBV
     C                   Move      *zeros        COGNTA
     C                   Move      *zeros        COMNT
     C                   Move      *zeros        COMNTP
     C                   Move      *zeros        COMVKT
     C                   Move      *zeros        COMM3
     C                   Move      *zeros        COMLM
     C                   Move      *zeros        COMFBV
      *
     C                   ENDSR
