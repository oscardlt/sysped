      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYOPOS2 MODULE(*LIBL/SYOPOS2 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NY SØK PÅ GODSNR
10XXX * 02 YBC PTF10 OPPDATER DATO OG TID PÅ USRSPC
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 NY SØK PÅ KOLLIID
********************************************************************
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR0)
     FHEAD17    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR17)
N01  FHEAD18    IF   E           K DISK    USROPN
N01  F                                     RENAME(HEADFR:HEADFR18)
     FBILLOF    IF   E           K DISK    usropn
     FBILLOFLG  IF   E           K DISK    usropn
     F                                     RENAME(BILLOFR:BILLOFG)
     FBLV1      IF   E           K DISK    usropn
     FBLJOINLF  IF   E           K DISK    USROPN
     F                                     RENAME(BILLOFR:BLJOIN0)
     F                                     RENAME(BLV1R:BLJOIN1)
     F                                     RENAME(BLV2R:BLJOIN2)
N03  FDOKUFM1   IF   E           K DISK    USROPN
     FTRACKT    IF   E           K DISK    usropn
     FBRPARF    IF   E           K DISK    usropn
     FFRISOK    IF   E           K DISK    usropn
     FFRISOL01  IF   E           K DISK    usropn
     F                                     RENAME(FRISOKR:FRISOKR1)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D OddEven         s             10
      * Client input variables
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
     D WSUSER          s             10
     D*BOOKREF         S             15
     D BOOKREF         S             35
     D SENDREF         S             35
     D BLNR            S             16
     D CONTNR          S             17
N01  D GODSNR          S             15
     D XSTATUS         s             15
     D XFRBREVNR       s             35
     D WSCONTNR        s            500
N03  D KOLLIID         S             35
     D XXCMD           S            128
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      * Message ID for CrtUsrSpc
     D MsgId           s              7
      * State related variables
     D State           ds                  based(StateP)
     D  UBRID                        10
N02  D  UINNDT                        8
N02  D  UINNTM                        6
     D  UAVD                1001   1080  0 DIM(20)
     D  UOPD                1081   1220  0 DIM(20)
     D                 DS
     D HEAVD                   1      4  0
     D FSAVD                   1      4  0
     D BLAVD                   1      4  0
     D BVAVD                   1      4  0
N03  D FMAVD                   1      4  0
     D                 DS
     D HEOPD                   1      7  0
     D FSOPD                   1      7  0
     D BLOPD                   1      7  0
     D BVOPD                   1      7  0
N03  D FMOPD                   1      7  0
     D                 DS
     D BLMN                    1     17
     D BVMN                    1     17
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File Open / keys
     C                   EXSR      INIT
      *
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      * FYLLE ARBEIDSFIL
     C                   SELECT
     C                   WHEN      BOOKREF <> *BLANKS
     C     BOOKREF       SETLL     HEAD17
     C     BOOKREF       READE     HEAD17                                 35
     C                   WHEN      SENDREF <> *BLANKS
     C     FRISOL01K     SETLL     FRISOL01
     C     FRISOL01K     READE     FRISOL01                               35
     C                   WHEN      BLNR <> *BLANKS
     C     BLNR          SETLL     BILLOFLG
     C     BLNR          READE     BILLOFLG                               35
     C                   WHEN      CONTNR <> *BLANKS
     C     CONTNR        SETLL     BLJOINLF
     C     CONTNR        READE     BLJOINLF                               35
N01  C                   WHEN      GODSNR <> *BLANKS
N01  C     GODSNR        SETLL     HEAD18
N01  C     GODSNR        READE     HEAD18                                 35
N03  C                   WHEN      KOLLIID <> *BLANKS
N03  C     KOLLIID       SETLL     DOKUFM1
N03  C     KOLLIID       READE     DOKUFM1                                35
     C                   OTHER
     C                   GOTO      BOTTEN
     C                   ENDSL
      *
     C                   DOW       *IN35 = *OFF
     C                   exsr      Setrow
     C                   IF        XVISOPD = 'J'
     C                   callp     wrtsection('row')
     C                   END
     C                   SELECT
     C                   WHEN      BOOKREF <> *BLANKS
     C     BOOKREF       READE     HEAD17                                 35
     C                   WHEN      SENDREF <> *BLANKS
     C     FRISOL01K     READE     FRISOL01                               35
     C                   WHEN      BLNR <> *BLANKS
     C     BLNR          READE     BILLOFLG                               35
     C                   WHEN      CONTNR <> *BLANKS
     C     CONTNR        READE     BLJOINLF                               35
N01  C                   WHEN      GODSNR <> *BLANKS
N01  C     GODSNR        READE     HEAD18                                 35
N03  C                   WHEN      KOLLIID <> *BLANKS
N03  C     KOLLIID       READE     DOKUFM1                                35
     C                   ENDSL
     C                   ENDDO
      *
     c     BOTTEN        tag
     C                   callp     wrtsection('bot')
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   exsr      Exit
      *
     c     slutt         tag
     C                   eval      *inlr = *on
     C                   return
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   EVAL      WSUSER   = zhbgetvar('USER')
     C     LC:UC         XLATE     WSUSER        WSUSER
      * Div filterparamtere...
     C                   EVAL      BOOKREF = zhbgetvar('BOOKREF')
     C     LC:UC         XLATE     BOOKREF       BOOKREF
     C                   EVAL      SENDREF = zhbgetvar('SENDREF')
     C     LC:UC         XLATE     SENDREF       SENDREF
     C                   EVAL      BLNR    = zhbgetvar('BLNR')
     C     LC:UC         XLATE     BLNR          BLNR
     C                   EVAL      CONTNR  = zhbgetvar('CONTNR')
     C     LC:UC         XLATE     CONTNR        CONTNR
N01  C                   EVAL      GODSNR  = zhbgetvar('GODSNR')
N01  C     LC:UC         XLATE     GODSNR        GODSNR
N03  C                   EVAL      KOLLIID = zhbgetvar('KOLLIID')
N03  C     LC:UC         XLATE     KOLLIID       KOLLIID
      *
     C                   endsr
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
      *
     C                   CLOSE     BRIDF
     C                   CLOSE     HEADF
     C                   CLOSE     HEAD17
N01  C                   CLOSE     HEAD18
N03  C                   CLOSE     DOKUFM1
     C                   CLOSE     BILLOF
     C                   CLOSE     BILLOFLG
     C                   CLOSE     BLV1
     C                   CLOSE     BLJOINLF
     C                   CLOSE     TRACKT
     C                   CLOSE     BRPARF
     C                   CLOSE     FRISOK
     C                   CLOSE     FRISOL01
      *
     C                   endsr
      *
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYOPOS2':'/CGITAG/')
      *
     C                   eval      UsrSpcName = CrtUsrSpc(
     C                               UsrSpcLib : StateP : MsgID)
     C                   EVAL      XXCMD = 'GRTOBJAUT OBJ(SYCGIUSP/'
     C                   CAT       UsrSpcName:0  XXCMD
     C                   CAT       ') OBJTYPE':0 XXCMD
     C                   CAT       '(*USRSPC)':0 XXCMD
     C                   CAT       'USER(*PUB':1 XXCMD
     C                   CAT       'LIC)':0      XXCMD
     C                   CAT       'AUT(*ALL)':1 XXCMD
     C                   EVAL      RC = DOCMD('XXCMD')
     C                   callp     updHTMLvar('UsrSpcName':UsrSpcName)
     C                   EVAL      UBRID = WSUSER
N02  C                   CALL      'SYGE15C'
N02  C                   PARM                    UINNDT
N02  C                   PARM                    UINNTM
     C                   MOVEA     *ZEROS        UAVD
     C                   MOVEA     *ZEROS        UOPD
     C                   MOVE      *ZEROS        XNOPD             3 0
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updHTMLvar('KNAVN':BIBESK)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
      * Søkeparam
     C                   callp     updHTMLvar('BOOKREF':BOOKREF)
     C                   callp     updHTMLvar('SENDREF':SENDREF)
     C                   callp     updHTMLvar('BLNR':BLNR)
     C                   callp     updHTMLvar('CONTNR':CONTNR)
N01  C                   callp     updHTMLvar('GODSNR':GODSNR)
N03  C                   callp     updHTMLvar('KOLLIID':KOLLIID)
      *
     c                   movel     timedata1     time26           26
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     C                   MOVE      *BLANKS       BLBLNR
     C                   MOVE      *BLANKS       BLCH
     C                   MOVE      *ZEROS        TXETDD
     C                   MOVE      *ZEROS        TXATDD
     C                   MOVE      *ZEROS        TXETAD
     C                   MOVE      *ZEROS        TXATAD
      *
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        HEST = 'S'
     C                   MOVE      'N'           XVISOPD           1
     C                   GOTO      SLSETROW
     C                   ELSE
     C                   MOVE      'J'           XVISOPD
     C                   END
      *
     C                   IF        XNOPD < 20
     C                   ADD       1             XNOPD
     C                   MOVE      HEAVD         UAVD(XNOPD)
     C                   MOVE      HEOPD         UOPD(XNOPD)
     C                   END
      *
      * HENT FRAKTBREVNR
     C                   EVAL      XFRBREVNR = *BLANKS
     C                   IF        (BOOKREF = *BLANKS) AND (SENDREF <> *BLANKS)
     C                   EVAL      XFRBREVNR = SENDREF
     C                   ELSE
     C     FRISOKK       SETLL     FRISOK
     C     FRISOKK       READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        FSKODE = 'IFB'
     C                   EVAL      XFRBREVNR = FSSOK
     C                   LEAVE
     C                   END
     C     FRISOKK       READE     FRISOK                                 33
     C                   ENDDO
     C                   END
      *
     C     BILLOFK       CHAIN     BILLOF                             33
     C     TRACKTK       CHAIN     TRACKT                             33
      *
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     C                   MOVE      'N'           XCONTNR           1
     C                   EVAL      WSCONTNR = BLMN
     C     BILLOFK       SETLL     BLV1
     C     BILLOFK       READE     BLV1                                   33
     C                   DOW       *IN33 = *OFF
     C                   IF        BVMN <> *BLANKS
     C                   EVAL      XCONTNR = 'J'
     C                   IF        WSCONTNR = *BLANKS
     C                   EVAL      WSCONTNR = %TRIM(BVMN)
     C                   ELSE
     C                   EVAL      WSCONTNR = %TRIM(WSCONTNR) +','+ %TRIM(BVMN)
     C                   END
     C                   END
     C     BILLOFK       READE     BLV1                                   33
     C                   ENDDO
      *
     C                   SELECT
     C                   WHEN      TXATAD <> 0
     C                   EVAL      XSTATUS = 'Arrived  '
     C                   WHEN      TXATDD <> 0
     C                   EVAL      XSTATUS = 'Shipped  '
     C                   WHEN      BLMM    = 'New'
     C                   EVAL      XSTATUS = 'New'
     C                   WHEN      BLMM    = 'Updated'
     C                   EVAL      XSTATUS = 'Updated'
     C                   OTHER
     C                   SELECT
     C                   WHEN      ((BVMN <> *BLANKS) and (BVMN <> '?'))
     C                   EVAL      XSTATUS = 'Confirmed'
     C                   WHEN      TXETDD <> 0
     C                   EVAL      XSTATUS = 'Booked'
     C                   WHEN      XCONTNR = 'N'
     C                   EVAL      XSTATUS = 'Pending'
     C                   OTHER
     C                   EVAL      XSTATUS = 'Pending'
     C                   ENDSL
     C                   ENDSL
      *
     C                   callp     updHTMLvar('HEAVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHTMLvar('HEOPD':
     C                             %trim(%editc(HEOPD:'Z')))
     C                   callp     updHTMLvar('HEPRO':
     C                             %trim(%editc(HEPRO:'Z')))
     C                   callp     updHTMLvar('HERFA':HERFA)
     C                   callp     updHTMLvar('XFRBREVNR':XFRBREVNR)
     C                   callp     updHTMLvar('HENAS':HENAS)
     C                   callp     updHTMLvar('HENAK':HENAK)
     C                   callp     updHTMLvar('HESDF':HESDF)
     C                   callp     updHTMLvar('HESDT':HESDT)
N01  C                   callp     updHTMLvar('HEGN':HEGN)
N01  C                   callp     updHTMLvar('HETLL':HETLL)
N01  C                   callp     updHTMLvar('HETLE':HETLE)
     C                   callp     updHTMLvar('STATUS':XSTATUS)
     C                   callp     updHTMLvar('ST':XSTATUS)
      *
     C                   callp     updHTMLvar('WSCONTNR':WSCONTNR)
     C                   callp     updHTMLvar('CON':WSCONTNR)
     C                   callp     updHTMLvar('BLBLNR':BLBLNR)
     C                   callp     updHTMLvar('BL':BLBLNR)
     C                   callp     updHTMLvar('BLORR':BLCH)
      *
     C     TXETDD        ifne      *zeros
     C                   callp     updHTMLvar('TXETDD':
     C                             %trim(%editw(TXETDD:'    .  .  ')))
     c                   else
     C                   callp     updHTMLvar('TXETDD':'__________')
     c                   end
     C     TXATDD        ifne      *zeros
     C                   callp     updHTMLvar('TXATDD':
     C                             %trim(%editw(TXATDD:'    .  .  ')))
     c                   else
     C                   callp     updHTMLvar('TXATDD':'__________')
     c                   end
     C     TXETAD        ifne      *zeros
     C                   callp     updHTMLvar('TXETAD':
     C                             %trim(%editw(TXETAD:'    .  .  ')))
     c                   else
     C                   callp     updHTMLvar('TXETAD':'__________')
     c                   end
     C     TXATAD        ifne      *zeros
     C                   callp     updHTMLvar('TXATAD':
     C                             %trim(%editw(TXATAD:'    .  .  ')))
     c                   else
     C                   callp     updHTMLvar('TXATAD':'__________')
     c                   end
      *
     C                   callp     updHTMLvar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHtmlVar('HEVKT':
     C                             %trim(%editc(HEVKT:'Z')))
     c     hem3          ifne      *zeros
     C                   callp     updHtmlVar('HEM3':
     C                             %trim(%editc(HEM3:'4')))
     c                   else
     C                   callp     updHTMLvar('HEM3':'0,000')
     c                   end
     C                   callp     updHtmlVar('HELM':
     C                             %trim(%editc(HELM:'4')))
      *
     C     SLSETROW      ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   OPEN      HEADF
     C                   OPEN      HEAD17
N01  C                   OPEN      HEAD18
N03  C                   OPEN      DOKUFM1
     C                   OPEN      BILLOF
     C                   OPEN      BILLOFLG
     C                   OPEN      BLV1
     C                   OPEN      BLJOINLF
     C                   OPEN      TRACKT
     C                   OPEN      BRPARF
     C                   OPEN      FRISOK
     C                   OPEN      FRISOL01
      *
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TRACKTK       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     BILLOFK       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FRISOKK       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FRISOL01K     KLIST
     C                   KFLD                    XFSKODE
     C                   KFLD                    SENDREF
      *
     C                   MOVE      'IFB'         XFSKODE           3
      *
     C                   endsr
      *
