      ****************************************************************
      *** PROGRAM SOM SKRIVER TURSTATISTIKK                        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24               ***
      *** LEDIGE INDIKATORER 34-90 95-97                           ***
      *** INDIKATOR 32 P≈ = KALLE FRA SYEX10                       ***
      ***           32 AV = KALLE FRA SYEX88CL                     ***
      *
     FCUNDL01   IF   E           K DISK
     FLEVEL01   IF   E           K DISK
     FHEAD11    IF   E           K DISK
     FFAK8      IF   E           K DISK
     FSYKS88P   O    E             PRINTER OFLIND(*IN88)
     FTURER     IF   E           K DISK
     D AL              S              1    DIM(20)
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XUL≈≈                 11     14  0
     D                 DS
     D  TUDT                   1      8  0
     D  TUAAAR                 3      4  0
     D  TUAMND                 5      6  0
     D  TUADAG                 7      8  0
     D                 DS
     D  TUDTT                  1      8  0
     D  TUBAAR                 3      4  0
     D  TUBMND                 5      6  0
     D  TUBDAG                 7      8  0
     D                 DS
     D  FAAVD                  1      4  0
     D  FAOPD                  5     11  0
     D  FAFAKT                12     18  0
     D  FAKUNR                19     26  0
     D  FADATA                 1     26  0
     D                 DS
     D  GMAVD                  1      4  0
     D  GMOPD                  5     11  0
     D  GMFAKT                12     18  0
     D  GMKUNR                19     26  0
     D  GMDATA                 1     26  0
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     C*
     C                   EXSR      INIT
     C*
     C*
     C                   EXSR      SKRIV
     C*
     C     TOINNT        ADD       TOKOST        TORESU           11 2
     C                   WRITE     PDETT
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     SKRIV         BEGSR
     C***********************************************
     C                   SETOFF                                       33
     C                   MOVE      *ZEROS        PRINNT
     C                   MOVE      *ZEROS        PRKOST
     C                   MOVE      *ZEROS        TORESU
     C     TUKEY         CHAIN     TURER                              33
     C     *IN33         IFNE      '1'
     C                   WRITE     PHEAD
     C     UPRO          SETLL     HEAD11
     C     *IN33         DOWEQ     '0'
     C     UPRO          READE     HEAD11                                 33
     C     *IN33         IFEQ      '0'
     C                   EXSR      SUMFAK
     C                   END
     C                   END
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     SUMFAK        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        PRINNT
     C                   MOVE      *ZEROS        PRKOST
     C                   Z-ADD     0             LEST              8 0
     C                   SETOFF                                       34
     C     FAKEY         SETLL     FAK8
     C     *IN34         DOWEQ     '0'
     C     FAKEY         READE     FAK8                                   34
     C     *IN34         IFEQ      '0'
      *
     C     FAOPKO        IFNE      'D'
     C     FASK          IFEQ      'S'
     C     FASK          OREQ      'K'
     C     FASK          OREQ      'X'
     C     FASK          OREQ      'I'
     C*UNNG≈R BRUDD 1. LINJE
     C     LEST          IFEQ      0
     C                   MOVE      FADATA        GMDATA
     C                   END
     C                   ADD       1             LEST
     C* BRUDD
     C     GMDATA        IFNE      FADATA
     C                   EXSR      PRINT
     C                   MOVE      *ZEROS        PRINNT
     C                   MOVE      *ZEROS        PRKOST
     C                   END
      *
     C     FAKDA         IFEQ      'K'
     C                   ADD       FABELN        PRKOST           11 2
     C                   ADD       FABELN        TOKOST           11 2
     C                   ELSE
     C                   ADD       FABELN        PRINNT           11 2
     C                   ADD       FABELN        TOINNT           11 2
     C                   END
     C                   END
      *
     C                   END
     C                   MOVE      FADATA        GMDATA
     C                   MOVE      FAMND         GMMND             2 0
     C                   MOVE      FALEVN        GMLEVN            8 0
     C                   MOVE      FAKDA         GMKDA             1
     C                   ELSE
     C     LEST          IFNE      0
     C                   MOVE      FADATA        GMDATA
     C                   MOVE      FAMND         GMMND             2 0
     C                   MOVE      FALEVN        GMLEVN            8 0
     C                   MOVE      FAKDA         GMKDA             1
     C                   ELSE
     C                   MOVE      *ZEROS        GMDATA
     C                   MOVE      ' '           GMKDA
     C                   MOVE      *ZEROS        GMMND
     C                   MOVE      *ZEROS        GMLEVN
     C                   END
     C                   EXSR      PRINT
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C* HEADING
     C     *IN88         IFEQ      *ON
     C                   WRITE     PHEAD
     C                   MOVE      *OFF          *IN88
     C                   END
     C                   MOVE      *BLANKS       NAVN
     C     GMKDA         IFEQ      'K'
     C     LEKEY         CHAIN     LEVEL01                            35
     C  N35              MOVEL     LNAVN         NAVN             20
     C                   ELSE
     C     HEKNKF        IFNE      *ZEROS
     C                   MOVE      HEKNKF        GMKUNR
     C                   ELSE
     C                   MOVE      HEKNSF        GMKUNR
     C                   END
     C     GMKUNR        IFEQ      *ZEROS
     C     HEKNK         IFNE      *ZEROS
     C                   MOVE      HEKNK         GMKUNR
     C                   ELSE
     C                   MOVE      HEKNS         GMKUNR
     C                   END
     C                   END
     C     KUKEY         CHAIN     CUNDL01                            35
     C  N35              MOVEL     KNAVN         NAVN
     C                   END
      *
     C                   WRITE     PDET
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KUKEY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    GMKUNR
     C     LEKEY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    GMLEVN
     C     FAKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TUKEY         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UPRO
     C*
     C     *ENTRY        PLIST
     C                   PARM                    FIFIRM            2
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   ENDSR
     C*
     C*
