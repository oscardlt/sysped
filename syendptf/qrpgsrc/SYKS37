********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 TEST AVD/OPD I FRI SØKEVEI (MÅ VÆRE STREK I POS 5 ), TEST OGSÅ 'FF'
11044 * 02 CB  PTF11 send med fritekst fra fakturalinje
12065 * 03 CB  PTF12 Ikke send med 0-linjer med mindre de er manuelle
********************************************************************
     FFAK3      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTW     IF   E           K DISK
     FKIDTRAN   IF   E           K DISK
     FINTFAS    O  A E             DISK    USROPN
     D                 DS
     D  FAVT                   1     20
     D  FAVT1_2                1      2
     D  FAVT5_5                5      5
     D                 DS
     D  FSSOK                  1     35
     D  XXAVD                  1      4
N01  D  XXSTREK                5      5
     D  XXOPD                  6     12
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  INUDÅÅ                 1      4  0
     D  INUDMM                 5      6  0
     D  INUDDD                 7      8  0
     D  INUDAT                 1      8  0
     D                 DS
     D  WKID                   1     19
     D  INKUNR                 1      8  0
     D  TEFFNR                 9     15  0
     D  XNULL                 16     19  0
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *
      ******************************************
     C     INIT          BEGSR
      ******************************************
     C     *ENTRY        PLIST
     c                   PARM                    UUOPKO            1
     C                   PARM                    UUFAKT            7
     C                   PARM                    UUSEID           35
     C                   PARM                    UUMOID           35
     C                   PARM                    UUAVRE            1
     C                   PARM                    UUKUNR            8
      *
      * VENT INTILL INTFAS ER LEDIG
     C                   CALL      'SYKS37C'
     C                   OPEN      INTFAS
      *
     C     HEAKEY        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C     FAKKEY        KLIST
     C                   KFLD                    UUOPKO
     C                   KFLD                    INFANR
     C     KODTK         KLIST
     C                   KFLD                    XXUNI
     C                   KFLD                    FAAVD
     C                   MOVE      'W'           XXUNI             1
      * TYPE FOR IDENTIFIKASJON NÅR EDI-SENDT
     C                   MOVEL     '*IN#'        INTYPE
     C                   MOVE      ' '           INST
     C                   MOVE      UUSEID        INSEID
     C                   MOVE      UUMOID        INMOID
     C                   MOVE      UUFAKT        INFANR
      * HENT DATO/KL
     C                   TIME                    XULTID
     C                   MOVEL     XULTM         INUTID
     C                   MOVE      XULDD         INUDDD
     C                   MOVE      XULMM         INUDMM
     C                   MOVE      XULÅÅ         INUDÅÅ
      *
      * Les til en finner en linje (hent kundenr M.M.)
     C     FAKKEY        SETLL     FAK3
     C     FAKKEY        READE     FAK3                                   33
     C     *IN33         DOWEQ     '0'
      * avregning = K-linjer, kundenr fra best.prog
     C     UUAVRE        IFEQ      'J'
     C     FAKDA         CABNE     'K'           NEXT0
     C                   MOVE      UUKUNR        INKUNR
     C                   ELSE
      * vanlig faktura,  IKKE K-linjer
     C                   MOVE      FAKUNR        INKUNR
     C     FAKDA         IFEQ      'K'
     C     FAKUNR        OREQ      *ZEROS
     C                   GOTO      NEXT0
     C                   END
     C                   END
      * POST FUNNET HENT....
     C                   MOVE      FADATO        INDATO
     C                   MOVE      FADAFF        INDAFF
     C                   MOVE      FAÅR          INAAR
     C                   MOVE      FAMND         INMND
      * HENT KID
     C     KODTK         CHAIN     KODTW                              33
     C     *IN33         IFEQ      '0'
     C     KOWBBS        ANDNE     ' '
     C                   MOVE      *ZEROS        TEFFNR
     C                   MOVE      INFANR        TEFFNR
     C                   MOVE      *ZEROS        XNULL
     C                   MOVE      *ZEROS        UPAR21           21
     C                   MOVE      WKID          UPAR21
     C                   CALL      'SYGE09'
     C                   PARM                    UPAR21
     C                   PARM                    UMOD              1 0
     C                   MOVE      UMOD          XBAK              1 0
     C                   MOVEL     WKID          KID20            20
     C                   MOVE      XBAK          KID20
     C     KID20         CHAIN     KIDTRAN                            33
     C  N33              MOVE      *BLANKS       KID20
     C  N33              MOVEL     TRKI08        KID20
     C                   MOVE      KID20         INKID
     C                   END
     C                   LEAVE
     C     NEXT0         TAG
     C     FAKKEY        READE     FAK3                                   33
     C                   END
      * KEY FOR KODTA
     C                   MOVE      'A'           XXUNI
     C                   ENDSR
      *
      *
      ******************************************
     C     HOVED         BEGSR
      ******************************************
      *
     C     FAKKEY        SETLL     FAK3
     C     FAKKEY        READE     FAK3                                   33
     C     *IN33         DOWEQ     '0'
      * avregning = K-linjer/ VANLIG faktura, IKKE K-linjer
     C     UUAVRE        IFEQ      'J'
     C     FAKDA         CABNE     'K'           NEXT
     C                   ELSE
     C     FAKDA         CABEQ     'K'           NEXT
     C                   END
N03  C                   IF        FABELN=*ZEROS
N03  C                   IF        FABELV=*ZEROS
N03  C     FAKDA         CABNE     'M'           NEXT
N03  C                   ENDIF
N03  C                   ENDIF
      * HENTE OPPL. FRA AVD/OPD kun ved brudd
     C     FAAVD         IFNE      GMAVD
     C     FAOPD         ORNE      GMOPD
      *
     C                   MOVE      FAAVD         INUAVD
     C                   MOVE      FAOPD         INUOPD
     C                   CLEAR                   INAVD
     C                   CLEAR                   INOPD
     C                   CLEAR                   INANNE
     C                   CLEAR                   INANN2
      * Hent bestillers AVD/OPPD fra frie søkeveier
     C     UUAVRE        IFNE      'J'
     C     HEAKEY        SETLL     FRISOK
     C     HEAKEY        READE     FRISOK                                 34
     C     *IN34         DOWEQ     '0'
     C     FSKODE        IFEQ      'FFR'
     C     FSKODE        OREQ      'OME'
N01  C     FSKODE        OREQ      'FF '
N01  C     XXSTREK       IFEQ      '/'
     C                   TESTN                   XXAVD                34
     C     *IN34         IFEQ      '1'
     C                   MOVE      XXAVD         INAVD
     C                   END
     C                   TESTN                   XXOPD                34
     C     *IN34         IFEQ      '1'
     C                   MOVE      XXOPD         INOPD
     C                   END
     C                   LEAVE
N01  C                   ENDIF
     C                   END
     C     HEAKEY        READE     FRISOK                                 34
     C                   END
     C                   END
      * ...ferdig hent fra frie søkeveier
      *
      * Annen ref fra send.nr hvis tr.modul innland, eller A-ref
     C     KODTK         CHAIN     KODTA                              34
     C   34              MOVE      ' '           KOAIE
     C     HEAKEY        CHAIN     HEADF                              34
     C     *IN34         IFEQ      '0'
     C     KOAIE         IFEQ      '1'
     C     KOAIE         OREQ      '2'
     C                   MOVEL     HXSNDN        INANNE
     C                   MOVEL     HERFA         INANN2
     C                   ELSE
     C                   MOVE      *BLANKS       INANNE
     C                   MOVEL     HERFA         INANNE
     C                   END
     C                   END
     C                   END
      * ...ferdig hent oppl. ved brudd avd/oppd.
      *
      * FRA DEN ENKELTE FAKTURALINJE
     C                   MOVE      FAVK          INVK
     C     UUAVRE        IFEQ      'J'
     C                   MOVE      'A_K'         INVK
     C                   end
     C                   MOVE      FAKDM         INMVA
     C                   Z-ADD     FABELN        INBELN
     C                   Z-ADD     FABELV        INBELV
     C                   MOVEL     FAVAL         INVAL
N02  C                   MOVEL     FAVT          INTXT
      * SKRIV TIL FIL
     C                   WRITE     INTFASR
      *
     C                   MOVE      FAAVD         GMAVD             4 0
     C                   MOVE      FAOPD         GMOPD             7 0
      *
     C     NEXT          TAG
     C     FAKKEY        READE     FAK3                                   33
     C                   END
      *
     C                   ENDSR
      *
     C*
