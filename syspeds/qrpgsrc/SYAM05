      *
      **************************************************************
      *** PROGRAM SKAL KREDITERE EN AMETAFAKTURA                 ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER                                     ***
      **************************************************************
      *
     FFIRM      IF   E           K DISK
     Ftellfa    uF   E           K DISK
     FFAK3      UF A E           K DISK
     FSYAM05FM  CF   E             WORKSTN
     FAMHE1     UF   E           K DISK
     FFAIN      O  A E           K DISK
     D                UDS
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  WF                   101    310  0
     D                                     DIM(35)
     D                 DS
     D  FAVT                   1     20
     D  WTID                   1      6
     D  WDATO                  8     13
     D  WSG                   15     17
     C     *ENTRY        PLIST
     C                   PARM                    UIN10             1
     C                   MOVE      '0'           UIN10
      *
     C                   EXSR      INIT
      *
     C     CHOISE        TAG
     C                   Z-ADD     04            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   MOVE      '0'           *IN34
     C                   EXFMT     BILDE1
      *
      **  AVSLUTT
      *
     C     *IN03         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END                                                    <*IN03
      *
      * FAKTURA IKKE OVERFØRT REGNSKAP - F10 ER BRUKT
      *
     C     *IN10         IFEQ      *ON
      *
      * SETTER STATUS TIL 'K' OG BLANKER FAKTURANUMMER
      *
     C                   MOVE      WIFN          YFAKNR
     C     AMHEK1        CHAIN     AMHE1                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'K'           AHST
     C                   CLEAR                   AHFAKT
     C                   UPDATE    AMHER
     C                   END                                                    <*IN33
      *
      * SETTER FAKDA TIL 'D' FOR Å SLETTE FØR OVERFØRING
      *
     C                   MOVE      'F'           ZOPKO
     C                   MOVE      WIFN          ZFAKT
     C     FAKKEY        SETLL     FAK3
      *
     C     LESFAK        TAG
     C     FAKKEY        READE     FAK3                                   70
     C     *IN70         IFEQ      '0'
     C                   MOVE      'D'           FAKDA
     C                   CLEAR                   FAVT
     C                   TIME                    WTIME            12 0
     C                   MOVEL     WTIME         WTID
     C                   MOVE      WTIME         WDATO
     C                   MOVEL     USG           WSG
     C                   UPDATE    FAKTR
     C                   GOTO      LESFAK
     C                   END                                                    <*IN70
     C                   MOVE      'DIV2551'     MSGNR
     C                   SETON                                        30
     C                   GOTO      CHOISE
     C                   END                                                    <*IN10
      *
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'
     C                   GOTO      CHOISE
     C                   END                                                    <*IN30
      *
      * KREDITER POSTER SOM ER OVERFØRT TIL REGNSKAP
      *
     C     FAKKEY        SETLL     FAK3
     C     FAKLES        TAG
     C     FAKKEY        READE(N)  FAK3                                   39
     C     *IN39         IFEQ      '0'
      *
      * TA VARE PÅ FAKTURANUMMER FOR KREDIT
      *
     C                   MOVE      FAFAKT        YFAKNR
      *
      * HENT FAKTURANUMMER
      *
     C     *IN34         IFEQ      '0'
     C                   MOVE      '1'           *IN34
     C     FIFIRM        CHAIN     tellfa                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      FIFNR         WAFAKT
     C                   ADD       1             FIFNR
     C                   UPDATE    tellfar
     C                   ELSE
     C                   UPDATE    tellfar
     C                   END                                                    <*IN33
      *
     C     AMHEK1        CHAIN     AMHE1                              77
     C     *IN77         IFEQ      *OFF
     C                   MOVE      'K'           AHST
     C                   MOVE      WAFAKT        AHKRED
     C                   UPDATE    AMHER
     C                   END                                                    <*IN77
      *
     C                   END                                                    <*IN34
      *
      *  FINNER LINJENUMMER FOR FAKTURAFIL
      *
     C                   MOVE      FAAVD         XXAVD             4 0
     C                   MOVE      FAOPD         XXOPD             7 0
     C                   MOVE      *ZEROS        XXLI
     C                   CALL      'SYGE06'
     C                   PARM                    XXAVD
     C                   PARM                    XXOPD
     C                   PARM                    XXLI
      *
     C                   MOVE      XXLI          FALI
     C                   MOVE      WAFAKT        FAFAKT
     C                   MOVE      'F'           FAOPKO
     C     FABELV        MULT      -1            FABELV
     C     FABELN        MULT      -1            FABELN
     C                   WRITE     FAKTR
     C                   GOTO      FAKLES
     C                   END                                                    <*IN39
      * SKRIV RECORD I FAIN
     C                   Z-ADD     FAFAKT        FIFN
     C                   Z-ADD     FAAVD         FIAVD
     C                   Z-ADD     FAOPD         FIOPD
     C                   WRITE     FAINR
      * LEGG UT FAKTNR I TABELL
     C                   MOVE      WAFAKT        AHFAKT
     C                   ADD       1             IX
     C                   MOVE      FAFAKT        WF(IX)
     C                   MOVE      '1'           UIN10
     C                   MOVE      FAAVD         UAVD
      *
     C                   MOVE      'DIV2551'     MSGNR
     C                   MOVE      FAFAKT        WNYFAK
     C                   CLEAR                   WIFN
     C                   SETON                                        30
      *
     C                   GOTO      CHOISE
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
      *
     C                   MOVEA     '0'           *IN(30)
     C                   MOVEA     '0'           *IN(70)
      *
      * FAKTURANUMMER IKKE UTFYLT
      *
     C     WIFN          IFEQ      0
     C                   SETON                                        30
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLTB1
     C                   ENDIF                                                  <WIFN
      *
      * FAKTURA FINNES IKKE I FAKT
      *
     C                   MOVE      'F'           ZOPKO
     C                   MOVE      WIFN          ZFAKT
     C     FAKKEY        CHAIN(N)  FAK3                               70
     C     *IN70         IFEQ      '1'
     C                   MOVE      'O'           ZOPKO
     C                   MOVE      WIFN          ZFAKT
     C     FAKKEY        CHAIN(N)  FAK3                               71
     C     *IN71         IFEQ      '1'
     C                   MOVE      'DIV2553'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLTB1
     C                   ELSE
     C                   SETOFF                                       30
     C                   END                                                    <*IN71
     C                   END                                                    <*IN70
      *
      * FAKTURA SLETTET FØR OVERFØRING (KREDITERT TIDLIGERE)
      *
     C                   MOVE      'F'           ZOPKO
     C                   MOVE      WIFN          ZFAKT
     C     FAKKEY        CHAIN(N)  FAK3                               70
     C     *IN70         IFEQ      '0'
     C     FAKDA         IFEQ      'D'
     C                   MOVE      'DIV2554'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLTB1
     C                   END                                                    <FAKDA
     C                   END                                                    <*IN70
      *
      * FAKTURA OVERFØRT REGNSKAP ??
      *
     C                   MOVE      'O'           ZOPKO
     C                   MOVE      WIFN          ZFAKT
     C     FAKKEY        CHAIN(N)  FAK3                               70
     C     *IN70         IFEQ      '1'
     C                   MOVE      'DIV2552'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLTB1
     C                   END                                                    <*IN70
     C     SLTB1         ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    FALI          XXLI
      *--------------------------------------------------------------
      * DEFINERER NØKKEL TIL FAK3
      *--------------------------------------------------------------
     C     *LIKE         DEFINE    FAOPKO        ZOPKO
     C     *LIKE         DEFINE    FAFAKT        ZFAKT
     C     FAKKEY        KLIST
     C                   KFLD                    ZOPKO
     C                   KFLD                    ZFAKT
      *--------------------------------------------------------------
      * DEFINERER NØKKEL TIL AMHE1
      *--------------------------------------------------------------
     C     AMHEK1        KLIST
     C                   KFLD                    YFAKNR
      *--------------------------------------------------------------
     C     *LIKE         DEFINE    FAFAKT        WAFAKT
     C     *LIKE         DEFINE    FAFAKT        YFAKNR
      *
     C                   READ      FIRM                                   33
     C                   Z-ADD     0             WIFN
     C                   Z-ADD     0             WNYFAK
     C                   Z-ADD     0             IX                2 0
     C                   CLEAR                   WF
     C     UTI           ENDSR
