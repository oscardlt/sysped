      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE035                   ***
      *** Write-Off Notification                                    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH05  UF   E           K DISK
     FNCTSEHC   UF A E           K DISK
     FTVINF     O  A E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM2     IF   E           K DISK    RENAME(EDIMR:EDIMR2)
     F                                     PREFIX(X_)
N10  FKODTSI    IF   E           K DISK
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104rf
     d ncts104rf       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104rf       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d xn              s              3s 0
     d x_thtrnr        s                   like(thtrnr)
     d x_IE035         s              1a
     d x_sn            s              3s 0
     d x_bel           s             16s 2
     d x_val           s              3a
     d x_tsd           s              8a
     d x_tsrd          s              8a
     d x_tin           s             17a
     d x_na            s             70a
     d x_ad1           s             70a
     d x_pn            s             17a
     d x_ps            s             35a
     d x_lk            s              2a
     d wdt             s              8
     d wtm             s              6
     d uemne           s             40
     d utxt            s            256
     d uep             s             70
     d                 ds
     d x_wdta                  1      8  0
     d x_wdtaa                 1      8
     d                 ds
     d x_wdtr                  1      8  0
     d x_wdtra                 1      8
     d                 ds
     d x_txt                   1    512
     d x_tx                    1    490    dim(7)
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
      *----------------------------------------------------------------------
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC035C';
             x_ie035 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'RecoveryNotification';
             exsr srxml2;
           when xrnv = 'CustomsOfficeOfDeparture';
             exsr srxml3;
           when xrnv = 'CustomsOfficeOfRecoveryAtDeparture';
             exsr srxml4;
           when xrnv = 'HolderOfTheTransitProcedure';
           when xrnv = 'Guarantor';
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie035 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;
         xn    = 0;
         x_txt = *blanks;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
           when xrnv = 'recoveryNotificationDate';
           when xrnv = 'declarationAcceptanceDate';
             x_wdtaa = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                    + %subst(xrverd:9:2);
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'recoveryNotificationDate';
             x_wdtra = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                    + %subst(xrverd:9:2);
           when xrnv = 'recoveryNotificationText';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             x_txt = xrvrd;
           when xrnv = 'amountClaimed';
             x_bel = %dec(xrverd:16:2);
           when xrnv = 'currency';
             x_val = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_tsd = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_tsrd = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         f0077 = 'INF';
         ft(1) = 'Declaration acceptance date: ' + %trim(x_wdtaa);
         ft(2) = 'Recovery notification date: ' + %trim(x_wdtra);
         ft(3) = 'Currency: ' + %trim(x_val)
                + '   Amount: ' + %trim(%editc(x_bel:'4'));
         ft(4) = %subst(x_txt:1:70);
         ft(5) = %subst(x_txt:71:70);
         write tvinff;

       endsr;

       //**********************************************
       begsr srxml5;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'identificationNumber';
             x_tin = xrverd;
           when xrnv = 'name';
             x_na = xrverd;
           when xrnv = 'streetAndNumber';
             x_ad1 = xrverd;
           when xrnv = 'postcode';
             x_pn = xrverd;
           when xrnv = 'city';
             x_ps = xrverd;
           when xrnv = 'country';
             x_lk = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         f0077 = 'INF';
         ft(1) = 'Gurantor: ' + %trim(x_tin);
         ft(2) = x_na;
         ft(3) = x_ad1;
         ft(4) = x_pn;
         ft(5) = %trim(x_ps) + '  ' + %trim(x_lk);
         write tvinff;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         chain x_thtrnr nctseh05;
         if %found;
           thst035 = 'J';
           update nctsehr;

           chain (thavd: thtdn) nctsehc;
           tcdta  = x_wdta;
           tcdtr  = x_wdtr;
           tctxt  = x_txt;
           tcbel  = x_bel;
           tcval  = x_val;
           tctsd  = x_tsd;
           tctsrd = x_tsrd;
           tctin  = x_tin;
           tcna   = x_na;
           tcad1  = x_ad1;
           tcpn   = x_pn;
           tcps   = x_ps;
           tclk   = x_lk;
           if %found;
             update nctsehcr;
           else;
             tcavd  = thavd;
             tctdn  = thtdn;
             write nctsehcr;
           endif;

           setgt (thavd: thtdn) edim2;
           readpe (thavd: thtdn) edim2;
           dow not %eof;
             if x_m0068a <> 0;
               leave;
             endif;
             readpe (thavd: thtdn) edim2;
           enddo;
         endif;

         m0065  = 'XMLRES';
         m0068  = x_thtrnr;
         m1001  = 'TET';
         m1225  = '035';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x_m0068a;
         m0068b = x_m0068b;
         m1n07  = '035';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = 'T35';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'Recovery acceptance date: '
                  + %trim(%editw(x_wdtr:'    .  .  '));
         tttexl = 'Gjenoppretting dato: '
                  + %trim(%editw(x_wdtr:'    .  .  '));
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
