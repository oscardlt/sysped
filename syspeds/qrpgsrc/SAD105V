      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE Userparameter PAPIRLØS    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FADOBEF    UF A E           K DISK
N01  FFIRMARC   IF   E           K DISK
N01  FARKTXT    IF   E           K DISK
N01  FARKEXT    IF   E           K DISK
     FSAD105FV  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  ADIFTM                 1     30
     D  ADIFT7                 1      7
     D  SFIFTM                 1     15
     D                 DS
     D  ADNETV                 1     30
     D  SFNETV                 1     10
     D                 DS
     D  ADPCTM                 1     70
     D  SFPCTM                 1     13
     D                 DS
     D  ADPATH                 1    150
     D  SFPATH                 1     17
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2USER        YAUSER           10
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSF1        SETLL     ADOBEF
     C     START1        TAG
     C                   READ(N)   ADOBEF                                 94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ(N)   ADOBEF                                 94
     C  N94              MOVE      ADUSER        YAUSER           10
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     4             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     12            POSNBR
     C                   MOVE      *BLANKS       WAUSER
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSF3        SETLL     ADOBEF
     C  N94              READ(N)   ADOBEF                                 94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     4             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C  N90KODSFK        CHAIN     ADOBEF                             33
     C  N90
     CANN33              DO
     C                   UPDATE    ADOBEFR
     C                   SETON                                        90
     C                   MOVE      'SPK0024'     MSGNR
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     12            POSNBR
     C                   SETON                                        30
     C                   goto      sltb
     C                   END
     C                   END
     C*
     c     WAARTY        ifeq      *blanks
     C                   MOVE      'JOV1553'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     6             POSNBR
     C                   SETON                                        30
     C                   goto      sltb
     C                   END
      *
     C* bane arkiv
     c     WAARKM        ifeq      *blanks
N01  c     *loval        setll     firmarc
N01  c                   read      firmarc
N01  c     WAARTY        chain     ARKTXT
N01  C     ARKLAG        ifne      *blanks
N01  c     arklagk       chain     arkext
N01  c                   endif
     c     ARCANE        ifne      *blanks
     c                   eval      WAARKM ='/'+%trim(ARCANE)+'/'
N01  c                   endif
N01  c                   endif
      *
     c                   movel     WAARKM        WAARKM1           1
     c     WAARKM        ifeq      *blanks
     c     WAARKM1       orne      '/'
     C                   MOVE      'JOV1554'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   SETON                                        30
     C                   goto      sltb
     C                   END
     C*
     c     WAIFTM        ifeq      *blanks
N02  c     WAPATH        andne     'STDBROWSER'
n02  c     WAPATH        andne     'CHROME'
     c     WAPATH        andne     'IE'
     c     WAPATH        andne     'LINK'
     C                   eval      WAIFTM='/sysped/TempPDF/'
     C                   END
     c     WAPATH        ifeq      *blanks
     c     WAPATH        oreq      'N'
     C                   eval      WAPATH ='C:\Programfiler\Adobe\'
     c                             +'Reader 9.0\Reader\AcroRd32.exe'
     c                   endif
     c     WAPATH        ifeq      'E'
     C                   eval      WAPATH ='C:\Program Files\Adobe\'
     c                             +'Reader 9.0\Reader\AcroRd32.exe'
     C                   END
     C*
S02  c*    WAPATH        ifne      'IE'
S02  c*    WAPATH        andne     'LINK'
     c     WAPAGE        iflt      '1'
     c     WAPAGE        orgt      '9'
     C                   eval      WAPAGE='1'
     C                   END
S02  C*                  END
     C*
     c     WANETV        ifeq      *blanks
     c     WAPATH        andne     'IE'
N02  c     WAPATH        andne     'STDBROWSER'
n02  c     WAPATH        andne     'CHROME'
     c     WAPATH        andne     'LINK'
     C                   MOVE      'JOV1555'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     6             POSNBR
     C                   SETON                                        30
     C                   goto      sltb
     C                   END
     C*
     c     WAPCTM        ifeq      *blanks
     c     WAPATH        andne     'IE'
N02  c     WAPATH        andne     'STDBROWSER'
n02  c     WAPATH        andne     'CHROME'
     c     WAPATH        andne     'LINK'
     C                   eval      WAPCTM='%USERPROFILE%\'
     C                   END
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSFK        CHAIN     ADOBEF                             33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    ADOBEFR
     C   33              WRITE     ADOBEFR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     ADIFT7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     ADIFT7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     4             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C     KODSF2        CHAIN(N)  ADOBEF                             33
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSF2        CHAIN     ADOBEF                             33
     C  N33              DELETE    ADOBEFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C     KODSF2        CHAIN(N)  ADOBEF                             33
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     12            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       ADUSER
     C                   MOVE      *BLANKS       ADARTY
     C                   MOVE      *BLANKS       ADARKM
     C                   MOVE      *BLANKS       ADIFTM
     C                   MOVE      *BLANKS       ADNETV
     C                   MOVE      *BLANKS       ADPCTM
     C                   MOVE      *BLANKS       ADPATH
     C                   MOVE      *BLANKS       ADPAGE
     C                   MOVEL     '*DELETE'     ADIFT7
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WAUSER
     C                   MOVE      *BLANKS       WAARTY
     C                   MOVE      *BLANKS       WAARKM
     C                   MOVE      *BLANKS       WAIFTM
     C                   MOVE      *BLANKS       WANETV
     C                   MOVE      *BLANKS       WAPCTM
     C                   MOVE      *BLANKS       WAPATH
     C                   MOVE      *BLANKS       WAPAGE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      ADUSER        WAUSER
     C                   MOVE      ADARTY        WAARTY
     C                   MOVE      ADARKM        WAARKM
     C                   MOVE      ADIFTM        WAIFTM
     C                   MOVE      ADNETV        WANETV
     C                   MOVE      ADPCTM        WAPCTM
     C                   MOVE      ADPATH        WAPATH
     C                   MOVE      ADPAGE        WAPAGE
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     18            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WAUSER        ADUSER
     C                   END
     C*
     C                   MOVE      WAUSER        ADUSER
     C                   MOVE      WAARTY        ADARTY
     C                   MOVE      WAARKM        ADARKM
     C                   MOVE      WAIFTM        ADIFTM
     C                   MOVE      WANETV        ADNETV
     C                   MOVE      WAPCTM        ADPCTM
     C                   MOVE      WAPATH        ADPATH
     C                   MOVE      WAPAGE        ADPAGE
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODSFK        KLIST
     C                   KFLD                    WAUSER
     C     KODSF1        KLIST
     C                   KFLD                    W2USER
     C     KODSF2        KLIST
     C                   KFLD                    ADUSER
     C     KODSF3        KLIST
     C                   KFLD                    YAUSER
N01  C     arklagk       KLIST
N01  C                   KFLD                    ARCFIR
N01  C                   KFLD                    arklag
     C*
     C                   ENDSR
     C*
