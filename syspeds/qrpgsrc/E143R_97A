********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
********************************************************************
     FEDIM      IF   E           K DISK
     FECNT97A   IF   E           K DISK
     FEMOA97A   IF   E           K DISK
     FDOKUF7    UF   E           K DISK
     FFREETXT   IF A E           K DISK
     FTRACKF    O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  WM5004_24N             1      6S 0
     D  WM5004_24A             1      6
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N20              EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT97A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT97A                                51
     C     *IN51         DOWEQ     '0'

     C                   SELECT
     C     C6069         WHENEQ    '8'
     C                   MOVEL     C6066         WC6066_8         10            No of pkg
     C     C6069         WHENEQ    '7'
     C                   MOVEL     C6066         WC6066_7         10            Weight
     C                   ENDSL

     C  N51KEYMS0        READE     ECNT97A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN0
     C     OPDKEYHIGH    KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     c                   KFLD                    HIGHDAT           8 0
     c                   Move      99999999      HIGHDAT
     C     OPDKEY        KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD

     C     *LIKE         DEFINE    MMN           ZMN
     C     *LIKE         DEFINE    MGN           ZGN0
     C     *LIKE         DEFINE    DF6060        WDF6060
     C     *LIKE         DEFINE    DF500A        WDF500A
     C     *LIKE         DEFINE    DF500B        WDF500B

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get time
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      * Get msg
     C     ZMN           CHAIN     EDIM                               50
      * Get DOKUF
     C     M1004         CHAIN(N)  DOKUF7                             20

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA00                                               MOA00 /
      *////////////////////////////////////////////////////////////
     C     MOA00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EMOA97A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMOA97A                                51
     C     *IN51         DOWEQ     '0'

     C                   MOVEA     *BLANKS       A
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM

     C                   SELECT
     C     M5025         WHENEQ    '24'
     C                   MOVEL     M5004         WM5004_24        10            Freight amount
     C                   Z-ADD     ZNUM          WDF500A
     C     M5025         WHENEQ    '39'
     C                   MOVEL     M5004         WM5004_39        10            Discount amount
     C                   Z-ADD     ZNUM          WDF500B
     C                   ENDSL

     C  N51KEYMS0        READE     EMOA97A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   EXSR      MOA00
     C                   EXSR      CNT00
     C                   EXSR      UNT00
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNT00   Write to SYSPSED                            UNT0D /
      *////////////////////////////////////////////////////////////
     C     UNT00         BEGSR
      *_________________
      * Write to FREETXT
      *"""""""""""""""""
      * Finn sist benyttede linjenr.
     C     OPDKEYHIGH    SETLL     FREETXT
     C     OPDKEY        READPE    FREETXT                                33
     c     *in33         IFEQ      *OFF
     c                   z-add     FRTLI         LINNR             2 0
     c                   else
     c                   Move      *zeros        LINNR             2 0
     c                   end
     C                   CLEAR                   FREETXTR
     C                   EVAL      FRTLI=LINNR
     C                   EVAL      FRTAVD=DFAVD
     C                   EVAL      FRTOPD=DFOPD
     C                   MOVEL     WDATO         FRTDT
     C                   MOVE      DFFBNR        AAFBNR            3
      * Concat freetxt-1
     c                   ADD       1             FRTLI
     C                   EVAL      FRTTXT='CS Fbr'
     C     FRTTXT        CAT       AAFBNR:1      FRTTXT

     C     FRTTXT        CAT       'Ant=':1      FRTTXT
     C     FRTTXT        CAT       WC6066_8:0    FRTTXT

     C     FRTTXT        CAT       'Vkt=':1      FRTTXT
     C     FRTTXT        CAT       WC6066_7:0    FRTTXT

     C                   WRITE     FREETXTR
      * Concat freetxt-2
     c                   ADD       1             FRTLI
     C                   EVAL      FRTTXT=*BLANKS
     C                   EVAL      FRTTXT='CS Fbr'
     C     FRTTXT        CAT       AAFBNR:1      FRTTXT


     C     FRTTXT        CAT       'F.bel=':1    FRTTXT
     C     FRTTXT        CAT       WM5004_24:0   FRTTXT

     C     FRTTXT        CAT       'Avg=':1      FRTTXT
     C     FRTTXT        CAT       WM5004_39:0   FRTTXT

     C     WDF500A       SUB       WDF500B       WM5004_24N

     C     FRTTXT        CAT       'N.bel=':1    FRTTXT
     C     FRTTXT        CAT       WM5004_24A:0  FRTTXT

     C                   WRITE     FREETXTR
      *________________
      * Write to TRACKF
      *""""""""""""""""
     C                   CLEAR                   TRACKFR
     C                   EVAL      TTACTI='ICS'
     C                   EVAL      TTAVD=DFAVD
     C                   EVAL      TTOPD=DFOPD
     C                   EVAL      TTFBNR=DFFBNR
     C                   MOVEL     WDATO         TTDATE
     C                   MOVEL     WTID          TTTIME
     C                   MOVEL     TTDATE        TTDATL
     C                   MOVEL     TTTIME        TTTIML
s01  C*                  EVAL      TTTEXT='Fraktavtale mottatt fra transportør'+
s01  C*                            ' (IFTMCS)'
n01  C                   EVAL      TTTEXL='Fraktavtale mottatt fra transportør'+
n01  C                             ' (IFTMCS)'
n01  C                   EVAL      TTTEXT='Freightagreement received from tran'+
n01  C                             'sporter (IFTMCS)'
     C                   EVAL      TTUSER='EDI'

     C                   WRITE     TRACKFR
      *_____________
      * Update DOKUF
      *"""""""""""""
     C     M1004         CHAIN     DOKUF7                             20
     C     *IN20         IFEQ      *OFF
     C                   EVAL      DF6060=WDF6060                               F.vekt i retur
     C                   EVAL      DF500A=WDF500A                               B.frakt i retur
     C                   EVAL      DF500B=WDF500B                               Rabatt i retur
     C                   UPDATE    DOKUFR
     C                   ENDIF

     C                   ENDSR
