********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 OVERSTYRING AV PRINTER
********************************************************************
     FSYLG01D   CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     FEDIM8     IF   E           K DISK
     FTRACKL05  IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FTRAN      IF   E           K DISK
     FDOKUF     IF   E           K DISK
S01  F*SYLG01P   O    E             PRINTER OFLIND(*IN98)
N01  FSYLG01P   O    E             PRINTER USROPN
N01  F                                     OFLIND(*IN98)
N01  D UCMD            DS          1024
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D                 DS
     D  ZPRO1                  1     30
     D  ZPRO1A                 1     20
     D                 DS
     D  ZPRO2                  1     30
     D  ZPRO2A                 1     20
     D                 DS
     D  ZDTTM                  1     14  0
     D  ZDTT                   1      8  0
     D  ZTMT                   9     14  0
     D                 DS
     D  MDTTM                  1     14  0
     D  MDT                    1      8  0
     D  MTM                    9     14  0
     D                 DS
     D  WTIME                  1     14  0
     D  WTM                    1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDT                    1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  XDT                    1      8  0
     D  XDTÅ                   1      4  0
     D  XDTM                   5      6  0
     D  XDTD                   7      8  0
     D  XTM                    9     12  0
     D  XTMH                   9     10  0
     D  XTMM                  11     12  0
     D                 DS
     D  YDT                    1      8  0
     D  YDTÅ                   1      4  0
     D  YDTM                   5      6  0
     D  YDTD                   7      8  0
     D  YTM                    9     12  0
     D  YTMH                   9     10  0
     D  YTMM                  11     12  0
     D                 DS
     D  WTID                   1      6  0
     D  WTIDH                  1      2  0
     D  WTIDM                  3      4  0
     D  WTIDS                  5      6  0
     D                 DS
      *     ..... *. 1 ...+... 2 ...+... 3 ...+... 4 ...+... 5 ...+... 6 ...+... 7
     D*EKS: Printed Waybill     EDI to: NORDISK EXPRESS AS       70710740001474570
     D  TTTEXT                 1     70
     D  TTTEXTB               21     70
     D  TTNAT20               29     48
     D  TTSndnr               54     70
     D                 DS
     D  VMNAVN                 1     30
     D  VMNAT20                1     20
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
     C     *ENTRY        PLIST
     C                   PARM                    UIN01             1
N01  C                   PARM                    ZPRT             10
N01  C                   PARM                    ZFORM            10
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    ZDTF
     C                   KFLD                    ZTMF
     C     KEYB          KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    MAVD
     C                   KFLD                    MTDN
     C                   KFLD                    MFFBNR
     C     KEYTRA        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ZTRAN
     c     TRACKKey      KLIST
     C                   KFLD                    TTACTI
     C                   KFLD                    ZDTF
     C                   KFLD                    ZTMF
     C                   MOVE      'PRW'         TTACTI
      * hent evt stringkode (KOPFM) fra utskriftsnr 13 (som KjøreKvitt )
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    WSPLNR
     C                   MOVE      'P'           WSPUNI            1
     C                   Z-ADD     13            WSPLNR            3 0
     C  N99KODTPK        CHAIN     KODTP                              99
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   40
      *_____________
      * Init av felt
      *"""""""""""""
     C* Hente dagens dato og klokkeslett.
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
     C                   Z-ADD     WDT           ZDTF
     C                   Z-ADD     WDT           ZDTT
     C                   Z-ADD     000000        ZTMF
     C                   Z-ADD     235959        ZTMT
N01  C                   Z-ADD     1024          UCMDL            15 5
N01  C                   BITON     '123457'      XAPS              1
N01  C                   BITOFF    '06'          XAPS
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SYLG01DØ
     C                   EXFMT     SYLG01DA
S01  C*                  MOVEA     '0000000'     *IN(30)
N01  C                   MOVEA     '00000000'    *IN(30)
     C     *IN03         IFEQ      '1'
     C                   MOVE      '1'           UIN01
     C                   GOTO      SRB9
     C                   END
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * Søk transportør.
     C     LINNBR        IFNE      15
     C                   CALL      'SYTR04A'
     C                   PARM                    ZTRAN
     C                   ELSE
      * Søk PROD-kode
     C                   EXSR      SRPRO
     C                   END
     C                   GOTO      SRB1
     C                   END
      * Teste transportør-nr (NODI-ID til må være utfylt).
     C     KEYTRA        CHAIN     TRAN                               30
     c     VMlayo        IFNE      'DU'
     C  N30VM0010        COMP      *BLANKS                                30
     c                   end
     C   30              MOVE      'SPT0105'     ZMSGNO
     C   30              MOVEA     '1000001'     *IN(30)
     C   30              GOTO      SRB1
      * Teste dato fra:
     C                   Z-ADD     ZDTF          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1010000'     *IN(30)
     C   30              GOTO      SRB1
      * Teste dato til:
     C                   Z-ADD     ZDTT          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1001000'     *IN(30)
     C   30              GOTO      SRB1
      * Fra dato større en til dato.
     C     ZDTF          IFGT      ZDTT
     C                   MOVE      'SPD0002'     ZMSGNO
     C                   MOVEA     '1011000'     *IN(30)
     C                   GOTO      SRB1
     C                   END
      * Teste tid fra:
     C                   Z-ADD     ZTMF          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1000100'     *IN(30)
     C   30              GOTO      SRB1
      * Teste tid til:
     C                   Z-ADD     ZTMT          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1000010'     *IN(30)
     C   30              GOTO      SRB1
      * Klokkeslett fra får ikke vare større en klokkeslett til.
     C     ZDTF          IFEQ      ZDTT
     C     ZTMF          ANDGT     ZTMT
     C                   MOVE      'SPK0007'     ZMSGNO
     C                   MOVEA     '1001010'     *IN(30)
     C                   GOTO      SRB1
     C                   END
N01   * PRINTERNAVN
N01  C                   IF        ZPRT = *BLANKS
N01  C                   MOVE      'YBC1978'     ZMSGNO
N01  C                   MOVEA     '10000001'    *IN(30)
N01  C                   GOTO      SRB1
N01  C                   END
N01   *
N01  C                   EVAL      UCMD = 'OVRPRTF FILE(SYLG01P) '
N01  C                             + 'PAGESIZE(70 80) CPI(10) OVRFLW(60) '
N01  C                             + 'OUTQ(' + %TRIM(ZPRT) + ') '
N01  C                             + 'FORMTYPE(' + XAPS + %TRIM(ZFORM)
N01  C                             + XAPS + ')'
N01  C                   CALL      'QCMDEXC'                            33
N01  C                   PARM                    UCMD
N01  C                   PARM                    UCMDL
N01  C                   OPEN      SYLG01P
N01   *
      * Indikator for om filter-begrensninger er gjort
     C     ZPRO1         COMP      *BLANKS                            5050
     C  N50ZPRO2         COMP      *BLANKS                            5050
     C  N50ZPROD1        COMP      *BLANKS                            5050
     C  N50ZPROD2        COMP      *BLANKS                            5050
     C  N50ZPROD3        COMP      *BLANKS                            5050
     C     ZavdF         COMP      *zeros                             5151
     C     VMLAYO        COMP      'DU'                                   52
     C     ZavdT         Ifeq      *zeros
     C                   move      ZavdF         ZavdT
     C                   end
      *_________
      * Skriv ut
      *"""""""""
      * stringkoden til laser ??
     C                   MOVE      *BLANKS       KFMT1
     C                   MOVE      *BLANKS       KFMT2
     C     KOPFM         SETLL     KODTFM
     C     KOPFM         READE     KODTFM                                 98
     C     START2        TAG
      *
     C                   WRITE     SYLG01PA
     c     VMlayo        IFNE      'DU'
     C     KEYA          SETLL     EDIM8
     C                   READ      EDIM8                                  94
     c                   else
     C     LC:UC         XLATE     VMNAVN        VMNAVN
      * Ved "DU-transportør" les T&T på PRW dato/tid.. (sjekk mot TRnavn senere)
     c     TrackKey      setll     TRACKL05
     C     TTACTI        READE     TRACKL05                               94
     C  N94              MOVE      TTDATL        MDT
     C  N94              MOVE      TTTIML        MTM
     c                   end
     C  N94MDTTM         COMP      ZDTTM                              94
      *
     C     *IN94         DOWEQ     '0'
     c     VMlayo        IFNE      'DU'
     C     M0010         cabne     VM0010        NEXT
     C     M0065         cabne     'IFTMIN'      NEXT
     C     MSR           cabne     'S'           NEXT
     c                   else
     C     LC:UC         XLATE     TTNAT20       TTNAT20
     C     TTNAT20       cabne     VMNAT20       NEXT
     C                   MOVE      TTAVD         MAVD
     C                   MOVE      TTOPD         MTDN
     C                   MOVE      TTFBNR        MFFBNR
     c                   end
      *
     C     *IN98         IFEQ      '1'
     C                   WRITE     SYLG01PA
     C                   MOVE      '0'           *IN98
     C                   END
      *
     C                   MOVEL     'F'           DFRI
     C     KEYB          CHAIN     DOKUF                              90
      * Filter på container nr eller prod-kode (1-3)
     C     *IN50         IFEQ      *ON
      * likt navn på flere taransp ved Dup-edi...
     c     VMlayo        IFEQ      'DU'
     C     ZTRAN         cabne     VMTRAN        NEXT
     c                   end
      * Produktkode
     C     ZPROD1        IFNE      *BLANKS
     C     ZPROD2        ORNE      *BLANKS
     C     ZPROD3        ORNE      *BLANKS
     C     DFPROK        IFEQ      *BLANKS
     C                   GOTO      NEXT
     C                   END
     C     ZPROD1        IFNE      DFPROK
     C     ZPROD2        ANDNE     DFPROK
     C     ZPROD3        ANDNE     DFPROK
     C                   GOTO      NEXT
     C                   END
     C                   END
      * Produkttekst 1
     C     ZPRO1         IFNE      *BLANKS
     C     ZPRO1         ANDNE     DFPRO1
     C                   GOTO      NEXT
     C                   END
      * Produkttekst 2
     C     ZPRO2         IFNE      *BLANKS
     C     ZPRO2         ANDNE     DFPRO2
     C                   GOTO      NEXT
     C                   END
      *
     C                   END
      *
      * Avd Fra
     C     ZavdF         IFNE      *zeros
     C     ZavdF         ANDGT     DFAVD
     C                   GOTO      NEXT
     C                   END
      * Avd Til
     C     ZavdT         IFNE      *zeros
     C     ZavdT         ANDLT     DFAVD
     C                   GOTO      NEXT
     C                   END
      *
      *
     C                   MOVEL     DF1004        X1004
     C                   MOVEL     DFNAVM        XMOTT
     c     XMOTT         ifeq      *blanks
     C                   MOVEL     DFNAVL        XMOTT
     c                   end
     C                   MOVEL     DFPOUL        XPOST
     C                   MOVEL     DFADUL        XSTED
     C                   Z-ADD     DFNT          XANT
     C                   Z-ADD     DFVKT         XVEKT
     C                   MOVEL     MTM           XTM
     C                   Z-ADD     DFM3          XVOL
     C                   WRITE     SYLG01PB
     C                   ADD       1             YSND
     C                   ADD       DFNT          YANT
     C                   ADD       DFVKT         YVEKT
      *
     C     NEXT          TAG
     c     VMlayo        IFNE      'DU'
     C                   READ      EDIM8                                  94
     c                   else
      * Ved "DU-transportør" les T&T på PRW dato/tid.. (sjekk mot TRnavn senere)
     C     TTACTI        READE     TRACKL05                               94
     C  N94              MOVE      TTDATL        MDT
     C  N94              MOVE      TTTIML        MTM
     c                   end
     C  N94MDTTM         COMP      ZDTTM                              94
     C  N94              END
      *
     C                   WRITE     SYLG01PØ
      * flere eksemplar styr via stringkoden??
     C  N98KOPFM         READE     KODTFM                                 98
     C  N98              GOTO      START2
S01  C*    SRB9          ENDSR
N01   *
N01  C     SRB9          TAG
N01  C                   CLOSE     SYLG01P
N01   *
N01  C                   ENDSR
N01   *
      *////////////////////////////////////////////////////////////
      *  Søke produktkode                                     SRA /
      *////////////////////////////////////////////////////////////
     C     SRPRO         BEGSR
     C                   MOVEL     'FRBREVPRO'   KFTYP            10
     C                   MOVE      *BLANKS       KFKOD             5
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    KFKOD
     C     POSNBR        IFGE      43
     C     POSNBR        ANDLE     47
     C                   MOVEL     KFKOD         ZPROD1
     C                   END
     C     POSNBR        IFGE      49
     C     POSNBR        ANDLE     53
     C                   MOVEL     KFKOD         ZPROD2
     C                   END
     C     POSNBR        IFGE      55
     C     POSNBR        ANDLE     59
     C                   MOVEL     KFKOD         ZPROD3
     C                   END
     C                   ENDSR
