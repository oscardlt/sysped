********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11085 * 01 CB  PTF11 Søk externe fakturer
     FSVE002D   CF   E             WORKSTN
     F                                     SFILE(SVE002DA:ZRECNO)
     FSVEF      UF A E           K DISK
     FSVEF1     IF   E           K DISK
     F                                     RENAME(SVEFR:SVEFR1)
     FSVTVK     IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EXSR      SRC
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    SVEF_SYAV     ZVEF_SYAV
     C     *LIKE         DEFINE    SVEF_SYOP     ZVEF_SYOP
     C     *LIKE         DEFINE    SVEF_VAKD     XVEF_VAKD
     C     *LIKE         DEFINE    SVEF_VAKU     XVEF_VAKU
     C     *LIKE         DEFINE    SVEF_FABL     XVEF_FABL
     C     *LIKE         DEFINE    SVVK_DTS      ZVVK_DTS
     C                   EVAL      ZVVK_DTS=99999999
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    ZVEF_SYAV
     C                   PARM                    ZVEF_SYOP
     C                   PARM                    XVEF_VAKD
     C                   PARM                    XVEF_VAKU
     C                   PARM                    XVEF_FABL
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA1         KLIST
     C                   KFLD                    ZVEF_SYAV
     C                   KFLD                    ZVEF_SYOP
     C     KEYA          KLIST
     C                   KFLD                    ZVEF_SYAV
     C                   KFLD                    ZVEF_SYOP
     C                   KFLD                    SVEF_FATX
     C     KEYB          KLIST
     C                   KFLD                    ZVEF_SYAV
     C                   KFLD                    ZVEF_SYOP
     C                   KFLD                    ZVEF_FATX
     C     SVTVKKEY      KLIST
     C                   KFLD                    ZVEF_VAKD
     C                   KFLD                    ZVVK_DTS
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN93=*ON
     C                   WRITE     SVE002DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA1         SETLL     SVEF1
     C     KEYA1         READE     SVEF1                                  94

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SVE002DA
     C     KEYA1         READE     SVEF1                                  94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     SVE002DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVE002DØ
     C                   WRITE     SVE002DC
     C                   EXFMT     SVE002DB

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
N01   *______________________
N01   * F4=Søk Externreferans
N01   *""""""""""""""""""""""
N01  C     *IN04         IFEQ      '1'
N01  C                   EXSR      SRBE
N01  C     *IN03         CABEQ     '1'           SRB9
N01  C                   GOTO      SRB1
N01  C                   ENDIF
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   ENDIF
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     SVE002DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      *
     C     SRBA1         TAG
      *
      *____________
      * Blanke felt
      *""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      *BLANKS       SVEF_FATY
     C                   MOVE      *BLANKS       SVEF_FATX
     C                   MOVE      *BLANKS       SVEF_VAKD
     C                   MOVE      *ZEROS        SVEF_VAKU
     C                   MOVE      *ZEROS        SVEF_FABL
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVE002DØ
     C                   EXFMT     SVE002DD
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN05         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'
      *____________
      * Teste input-1
      *""""""""""""
      * Post må ikke finnes fra før.
     C     KEYB          CHAIN(N)  SVEF                               53
     C                   IF        *IN53=*OFF
     C                   EVAL      ZMSGNO='SY00100'
     C                   MOVEA     '1110'        *IN(30)
     C                   GOTO      SRBA1
     C                   ENDIF
      *____________________________
      * Hent kurs hvis ikke oppgitt
      *""""""""""""""""""""""""""""
     C     ZVEF_VAKD     IFNE      *BLANKS
     C     ZVEF_VAKU     ANDEQ     *ZEROS
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  54
     C     *IN54         IFEQ      *OFF
     C                   EVAL      ZVEF_VAKU=SVVK_KRS
     C                   ENDIF
     C                   ENDIF
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   EVAL      SVEF_SYAV=ZVEF_SYAV
     C                   EVAL      SVEF_SYOP=ZVEF_SYOP
     C                   EVAL      SVEF_FATY=ZVEF_FATY
     C                   EVAL      SVEF_FATX=ZVEF_FATX
     C                   EVAL      SVEF_VAKD=ZVEF_VAKD
     C                   EVAL      SVEF_VAKU=ZVEF_VAKU
     C                   EVAL      SVEF_FABL=ZVEF_FABL
     C                   WRITE     SVEFR
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   EVAL      ZRECNO=WRECNO
     C                   WRITE     SVE002DA

     C                   GOTO      SRBA1

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE002DA
      *____________
      * Flytte felt
      *""""""""""""
     C     KEYA          CHAIN(N)  SVEF                               53
     C   53              GOTO      SRBB9
     C                   MOVEA     '0000'        *IN(30)
     C                   EVAL      ZVEF_FATY=SVEF_FATY
     C                   EVAL      ZVEF_FATX=SVEF_FATX
     C                   EVAL      ZVEF_VAKD=SVEF_VAKD
     C                   EVAL      ZVEF_VAKU=SVEF_VAKU
     C                   EVAL      ZVEF_FABL=SVEF_FABL

     C     SRBB1         TAG

     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVE002DØ
     C                   EXFMT     SVE002DD
     C                   MOVEA     '000'         *IN(30)
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
     C   30              GOTO      SRBB1
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     KEYA          CHAIN     SVEF                               53
     C   53              GOTO      SRBB9
     C                   EVAL      SVEF_FATY=ZVEF_FATY
     C                   EVAL      SVEF_FATX=ZVEF_FATX
     C                   EVAL      SVEF_VAKD=ZVEF_VAKD
     C                   EVAL      SVEF_VAKU=ZVEF_VAKU
     C                   EVAL      SVEF_FABL=ZVEF_FABL
     C                   UPDATE    SVEFR

     C                   ENDIF

     C     SRBB9         TAG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     KEYA          CHAIN     SVEF                               53
     C                   DELETE    SVEFR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *BLANKS       SVEF_FATY
     C                   MOVE      *BLANKS       SVEF_FATX
     C                   MOVE      *BLANKS       SVEF_VAKD
     C                   MOVE      *BLANKS       SVEF_VAKU
     C                   MOVE      *BLANKS       SVEF_FABL
     C                   UPDATE    SVE002DA
     C                   EVAL      *IN95=*ON

     C                   ENDSR
N01   *////////////////////////////////////////////////////////////
N01   *  Søk extern referanse                                SRBE /
N01   *////////////////////////////////////////////////////////////
N01  C     SRBE          BEGSR
N01   *________
N01   * Externt
N01   *""""""""
N01  C                   CALL      'SVE038R'
N01  C                   PARM                    ZVEF_SYAV
N01  C                   PARM                    ZVEF_SYOP
N01  C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Summera alla fakturor                                SRC /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
      * - Om alla poster ær av samma valuta skall dom summeras med aktuell valuta.
      * - Om det førekommer olika valutor skall omrækning til SEK ske.
      * - 95=Olika valutor          N95=Samma valuta
     C                   EXSR      SRCA

     C                   IF        *IN95
     C                   EXSR      SRCB                                         Olika valutor
     C                   ELSE
     C                   EXSR      SRCC                                         Samma valuta
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Summera alla fakturor, olika valutor ?              SRCA /
      *////////////////////////////////////////////////////////////
     C     SRCA          BEGSR

     C                   EVAL      *IN94=*OFF
     C                   EVAL      *IN95=*OFF
     C     KEYA1         SETLL     SVEF1
     C     KEYA1         READE     SVEF1                                  94
     C                   EVAL      XVEF_VAKD=SVEF_VAKD

     C     *IN94         DOWEQ     '0'
     C                   IF        XVEF_VAKD <> SVEF_VAKD
     C                   EVAL      *IN95=*ON
     C                   ENDIF
     C     KEYA1         READE     SVEF1                                  94
     C  N94              ENDDO

      * 95=Olika valutor          N95=Samma valuta

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Summera alla fakturor, Olika valutor                SRCB /
      *////////////////////////////////////////////////////////////
     C     SRCB          BEGSR

     C                   EVAL      *IN94=*OFF
     C                   EVAL      XVEF_VAKD='SEK'
     C                   EVAL      XVEF_VAKU=1
     C                   EVAL      XVEF_FABL=0
     C     KEYA1         SETLL     SVEF1
     C     KEYA1         READE     SVEF1                                  94
N03  C     *IN94         DOWEQ     '0'
     C                   EVAL      ZVEF_FABL=SVEF_FABL*SVEF_VAKU
     C                   ADD       ZVEF_FABL     XVEF_FABL

     C     KEYA1         READE     SVEF1                                  94
     C  N94              ENDDO

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Summera alla fakturor, Samma valuta                 SRCC /
      *////////////////////////////////////////////////////////////
     C     SRCC          BEGSR

     C                   EVAL      *IN94=*OFF
     C                   EVAL      XVEF_FABL=0
     C     KEYA1         SETLL     SVEF1
     C     KEYA1         READE     SVEF1                                  94
     C                   EVAL      XVEF_VAKD=SVEF_VAKD
     C                   EVAL      XVEF_VAKU=SVEF_VAKU

     C     *IN94         DOWEQ     '0'
     C                   ADD       SVEF_FABL     XVEF_FABL
     C     KEYA1         READE     SVEF1                                  94
     C  N94              ENDDO

     C                   ENDSR
