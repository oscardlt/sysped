*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12348A* 01 CB  PTF12 VISE FRA OG MED VAREPOSTNR FOR HELE TRANSITERINGEN
*****A**************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TR065PNL)
     A                                      HLPTITLE('REGISTRERING AV LOSSEMERK-
     A                                      NADER TET IMPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5' '9')
     A            TNLI2          5Y 0O  4  5EDTCDE(4)
     A  41                                  COLOR(YLW)
     A            TNCCH          6A  O  4 11
     A            TNCCCD         2A  O  4 18
     A            TNUNNR         4A  O  4 21
     A            TNVKB         13Y 3O  4 26EDTCDE(4)
     A            TNPTY          2A  O  4 41
     A            TNPNT          8Y 0O  4 44EDTCDE(4)
     A            TNPDTY         4A  O  4 53
     A            TNPDRF        70A  H
     A            TNPDRF_30     30A  O  4 58
S01  A*           TNLIT          5Y 0H
N01  A            TNLIT          5Y 0O  4106EDTCDE(4)
     A            TNDK           5A  H
     A            TNBLK          2A  H
     A            TNTINK        17A  H
     A            TNNAK         70A  H
     A            TNAD1K        70A  H
     A            TNPNK         17A  H
     A            TNPSK         35A  H
     A            TNLKK          2A  H
     A            TNCTX        120A  H
     A            TNCCUS         9A  H
     A            TNVKN         13Y 3H
     A            TNNTS         13Y 3H
     A            TNPMRK       120A  H
     A*           TNPDTY         4A  H
     A*           TNPDRF        70A  H
     A            TNPDGN         5S 0H
     A            TNPDIN        35A  H
     A            TNSDTY         4A  H
     A            TNSDRF        70A  H
     A            TNSDIN        35A  H
     A            TNTDTY         4A  H
     A            TNTDRF        70A  H
     A            TNADTY         4A  H
     A            TNADRF        70A  H
     A            TNAICD         5A  H
     A            TNAITX       120A  H
     A            NNVKB         13Y 3O  4 89EDTCDE(4)
     A            NNVKN         13Y 3H
     A            NNPTY          2A  O  4 94
     A            NNPNT          8Y 0O  4 97EDTCDE(4)
     A            NNCCH          6A  H
     A            NNCCCD         2A  H
     A            NNUNNR         4A  H
     A            NNCTX        120A  H
     A            NNCCUS         9A  H
     A            NNPMRK       120A  H
     A            NNSDTY         4A  H
     A            NNSDRF        70A  H
     A            NNSDIN        35A  H
     A            NNTDTY         4A  H
     A            NNTDRF        70A  H
     A            NNADTY         4A  H
     A            NNADRF        70A  H
     A            TN01           1A  O  4130COLOR(RED)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 86SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS066R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst, Item'
     A                                      COLOR(WHT)
     A                                  1 55'Tdeknr.'
     A            UAVD           4Y 0O  1 63COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 78'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 80COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                       9=Angre'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis'
     A                                      COLOR(BLU)
     A*                                 3  2'Op Linje EDI brt.vekt   Mott. brt.-
     A*                                     vekt Avsender                      -
     A*                                     Mottaker'
S01  A*                                 3  2'Op Linje EDI T.nr UN-nr EDI Brutto-
N01  A                                  3  2'Op    VP EDI T.nr UN-nr EDI Brutto-
     A                                      vekt Kolli info. Tidligere dokument-
S01  A*                                                       Mott. Brt.vekt Ko-
S01  A*                                     lli info.               S'
N01  A                                                        Mott. Brt.vekt De-
N01  A                                      kl.VP                   S'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF12(12 'Gå til forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              5  2'F3=Avslutt   F5=Forny   F6=Lag ny -
     A                                        F12=Forrige'
     A                                      COLOR(BLU)
     A  27                              5  2'F3=Avslutt   F5=Forny   F12=Forrig-
     A                                      e'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(*DFT 19 120 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/NCTS066R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst, Item'
     A                                      COLOR(WHT)
     A                                  1 55'Tdeknr.'
     A            UAVD           4Y 0O  1 63COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 78'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 80COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 86'-'
     A                                      COLOR(WHT)
     A            WSLI2          5Y 0O  1 88COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 3  2'D.g.item:'
N01  A                                  3  2'Dekl.VP:'
     A            TNLIT          5Y 0O  3 12TEXT('Deklarasjon gods item nr')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 18'Dekl.type:'
     A            TNDK           5A  O  3 29TEXT('Deklarasjonstype')
     A                                      COLOR(WHT)
     A                                  3 35'Best.land:'
     A            TNBLK          2A  O  3 46TEXT('Bestemmelsesland')
     A                                      COLOR(WHT)
     A                                  4  2'Mottaker:'
     A            TNTINK        17A  O  4 12TEXT('Org.nr')
     A                                      COLOR(WHT)
     A            TNNAK         70A  O  4 30TEXT('Mottakersnavn')
     A                                      COLOR(WHT)
     A*           TNAD1K        70A  O  5 30TEXT('Mottakersadr')
     A*                                     COLOR(WHT)
     A*           TNPNK         17A  O  6 30TEXT('Mottakers postnr')
     A*                                     COLOR(WHT)
     A*           TNPSK         35A  O  6 48TEXT('Mottakers poststed')
     A*                                     COLOR(WHT)
     A*           TNLKK          2A  O  6 84TEXT('Mottakers poststed')
     A*                                     COLOR(WHT)
     A                                  5  2'Godsbesk:'
     A            TNCTX        120A  H
     A            TNCTX_50      50A  O  5 12TEXT('Godsbeskrivelse')
     A                                      COLOR(WHT)
     A                                  5 63'CUS kode:'
     A            TNCCUS         9A  O  5 74TEXT('CUS kode')
     A                                      COLOR(WHT)
     A                                  5 84'Tariff:'
     A            TNCCH          6A  O  5 92TEXT('Tariffnr')
     A                                      COLOR(WHT)
     A            TNCCCD         2A  O  5 99TEXT('Tariffnr')
     A                                      COLOR(WHT)
     A                                  5102'UNNR:'
     A            TNUNNR         4A  O  5108TEXT('UNNR')
     A                                      COLOR(WHT)
     A                                  6  2'Brt.vekt:'
     A            TNVKB         13Y 3O  6 12TEXT('Bruttovekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6 27'Net.vekt:'
     A            TNVKN         13Y 3O  6 37TEXT('Nettovekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6 52'Annen enh:'
     A            TNNTS         13Y 3O  6 63TEXT('Annen enhet')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6 78'Enhet:'
     A            TNPTY          2A  O  6 85TEXT('Pakketype')
     A                                      COLOR(WHT)
     A                                  6 88'Kolli:'
     A            TNPNT          8Y 0O  6 95TEXT('Antall pakker')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7  2'Merking:'
     A            TNPMRK       120A  H
     A            TNPMRK_100   100A  O  7 12TEXT('Merke & nr')
     A                                      COLOR(WHT)
     A                                  8  2'Tidl.dok:'
     A            TNPDTY         4A  O  8 12TEXT('Dokumenttype')
     A                                      COLOR(WHT)
     A            TNPDRF        70A  O  8 17TEXT('Dokument ref')
     A                                      COLOR(WHT)
     A                                  9  2'St.dok:'
     A            TNSDTY         4A  O  9 12TEXT('Dokumenttype')
     A                                      COLOR(WHT)
     A            TNSDRF        70A  O  9 17TEXT('Dokument ref')
     A                                      COLOR(WHT)
      *
     A                                 10  2'Etter kontroll:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
      *
     A                                 11  2'Godsbesk:'
     A            WSCTX        120A  H
     A            WSCTX_100    100A  B 11 12TEXT('Godsbeskrivelse')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12  2'CUS kode:'
     A            WSCCUS         9A  B 12 12TEXT('CUS kode')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12 22'Tariff:'
     A            WSCCH          6A  B 12 30TEXT('Tariffnr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSCCCD         2A  B 12 37TEXT('Tariffnr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 42'Brt.vekt:'
     A            WSVKB         13Y 3B 12 52DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 67'Net.vekt:'
     A            WSVKN         13Y 3B 12 77DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 92'Enhet:'
     A            WSPTY          2A  B 12 99DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12102'Kolli:'
     A            WSPNT          8Y 0B 12109DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13  2'Mrk&nr:'
     A            WSPMRK       120A  H
     A            WSPMRK_100   100A  B 13 12TEXT('Merke & nr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14  2'St.dok.:'
     A            WSSDTY         4A  B 14 12TEXT('Støttende dokument')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSDRF        70A  B 14 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSDIN        35A  B 15 12TEXT('Komplett informasjon')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 16  2'Tr.dok.:'
     A            WSTDTY         4A  B 16 12TEXT('Transport dokument')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTDRF        70A  B 16 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17  2'Till.ref:'
     A            WSADTY         4A  B 17 12TEXT('Transport dokument')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSADRF        70A  B 17 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18  2'F4=Søk   F12=Avbryt   F15=Tillegg -
     A                                      opplysning'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      WINDOW(*DFT 19 120 *NOMSGLIN)
     A                                      CF12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                  1  2'SYSPED/NCTS066R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst, Item'
     A                                      COLOR(WHT)
     A                                  1 55'Tdeknr.'
     A            UAVD           4Y 0O  1 63COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 88'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 90COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 96'-'
     A                                      COLOR(WHT)
     A            TNLI2          5Y 0O  1 98COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 3  2'D.g.item:'
N01  A                                  3  2'Dekl.VP:'
     A            TNLIT          5Y 0O  3 12TEXT('Deklarasjon gods item nr')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 18'Dekl.type:'
     A            TNDK           5A  O  3 29TEXT('Deklarasjonstype')
     A                                      COLOR(WHT)
     A                                  3 35'Best.land:'
     A            TNBLK          2A  O  3 46TEXT('Bestemmelsesland')
     A                                      COLOR(WHT)
     A                                  4  2'Mottaker:'
     A            TNTINK        17A  O  4 12TEXT('Org.nr')
     A                                      COLOR(WHT)
     A            TNNAK         70A  O  4 30TEXT('Mottakersnavn')
     A                                      COLOR(WHT)
     A*           TNAD1K        70A  O  5 30TEXT('Mottakersadr')
     A*                                     COLOR(WHT)
     A*           TNPNK         17A  O  6 30TEXT('Mottakers postnr')
     A*                                     COLOR(WHT)
     A*           TNPSK         35A  O  6 48TEXT('Mottakers poststed')
     A*                                     COLOR(WHT)
     A*           TNLKK          2A  O  6 84TEXT('Mottakers poststed')
     A*                                     COLOR(WHT)
     A                                  5  2'Godsbesk:'
     A            TNCTX        120A  H
     A            TNCTX_50      50A  O  5 12TEXT('Godsbeskrivelse')
     A                                      COLOR(WHT)
     A                                  5 63'CUS kode:'
     A            TNCCUS         9A  O  5 74TEXT('CUS kode')
     A                                      COLOR(WHT)
     A                                  5 84'Tariff:'
     A            TNCCH          6A  O  5 92TEXT('Tariffnr')
     A                                      COLOR(WHT)
     A            TNCCCD         2A  O  5 99TEXT('Tariffnr')
     A                                      COLOR(WHT)
     A                                  5102'UNNR:'
     A            TNUNNR         4A  O  5108TEXT('UNNR')
     A                                      COLOR(WHT)
     A                                  6  2'Brt.vekt:'
     A            TNVKB         13Y 3O  6 12TEXT('Bruttovekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6 27'Net.vekt:'
     A            TNVKN         13Y 3O  6 37TEXT('Nettovekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6 52'Annen enh:'
     A            TNNTS         13Y 3O  6 63TEXT('Annen enhet')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6 78'Enhet:'
     A            TNPTY          2A  O  6 85TEXT('Pakketype')
     A                                      COLOR(WHT)
     A                                  6 88'Kolli:'
     A            TNPNT          8Y 0O  6 95TEXT('Antall pakker')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7  2'Merking:'
     A            TNPMRK       120A  H
     A            TNPMRK_100   100A  O  7 12TEXT('Merke & nr')
     A                                      COLOR(WHT)
     A                                  8  2'Tidl.dok:'
     A            TNPDTY         4A  O  8 12TEXT('Dokumenttype')
     A                                      COLOR(WHT)
     A            TNPDRF        70A  O  8 17TEXT('Dokument ref')
     A                                      COLOR(WHT)
     A                                  9  2'St.dok:'
     A            TNSDTY         4A  O  9 12TEXT('Dokumenttype')
     A                                      COLOR(WHT)
     A            TNSDRF        70A  O  9 17TEXT('Dokument ref')
     A                                      COLOR(WHT)
      *
     A                                 10  2'Etter kontroll:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
      *
     A                                 11  2'Godsbesk:'
     A            NNCTX        120A  H
     A            NNCTX_100    100A  O 11 12TEXT('Godsbeskrivelse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'CUS kode:'
     A            NNCCUS         9A  O 12 12TEXT('CUS kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 22'Tariff:'
     A            NNCCH          6A  O 12 30TEXT('Tariffnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            NNCCCD         2A  O 12 37TEXT('Tariffnr')
     A                                      DSPATR(UL)
     A                                 12 42'Brt.vekt:'
     A            NNVKB         13Y 3O 12 52DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 67'Net.vekt:'
     A            NNVKN         13Y 3O 12 77DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 92'Enhet:'
     A            NNPTY          2A  O 12 99DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12102'Kolli:'
     A            NNPNT          8Y 0O 12109DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13  2'Mrk&nr:'
     A            NNPMRK       120A  H
     A            NNPMRK_100   100A  O 13 12TEXT('Merke & nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14  2'St.dok.:'
     A            NNSDTY         4A  O 14 12TEXT('Støttende dokument')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            NNSDRF        70A  O 14 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            NNSDIN        35A  O 15 12TEXT('Komplett informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  2'Tr.dok.:'
     A            NNTDTY         4A  O 16 12TEXT('Transport dokument')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            NNTDRF        70A  O 16 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  2'Till.ref:'
     A            NNADTY         4A  O 17 12TEXT('Transport dokument')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            NNADRF        70A  O 17 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2'F12=Forrige   F15=Tillegg opplysni-
     A                                      nger'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til bilde02')
     A                                  1  2'SYSPED/NCTS066R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst, Item'
     A                                      COLOR(WHT)
     A                                  1 55'Tdeknr.'
     A            UAVD           4Y 0O  1 63COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 78'-'
     A                                      COLOR(WHT)
     A            ULI            5Y 0O  1 80COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                       9=Angre'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis'
     A                                      COLOR(BLU)
     A*                                 3  2'Op Linje EDI brt.vekt   Mott. brt.-
     A*                                     vekt Avsender                      -
     A*                                     Mottaker'
S01  A*                                 3  2'Op Linje EDI T.nr UN-nr EDI Brutto-
N01  A                                  3  2'Op    VP EDI T.nr UN-nr EDI Brutto-
     A                                      vekt Kolli info. Tidligere dokument-
     A                                                        Mott. Brt.vekt Ko-
     A                                      lli info.               S'
     A                                      COLOR(WHT)
     A N27                             22  2'F3=Avslutt   F5=Forny   F6=Lag ny -
     A                                        F8=Hopp til   F12=Forrige'
     A                                      COLOR(BLU)
     A  27                             22  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
S01  A*                                 3  2'Hopp til postnr. -->'
N01  A                                  3  2'Hopp til Dekl.VP -->'
     A            W3LI2          5S 0I  3 24DSPATR(UL)
     A                                  4  2'F12=Forrige'
     A                                      COLOR(BLU)
