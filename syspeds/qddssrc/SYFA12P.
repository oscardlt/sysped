      * OBS!!! VED ENDRING REKOMP OGSÅ SYFA12B
P9XXX * 03 SH  PTF10 TOLLPASSFRAKTBREV
P9XXX * 07 JOV PTF10 BOOKINGREF 2 TIL VENSTRE
P9XXX * 08 JOV PTF10 TOLLPASS- GODSNR LENGER OPP/FORENKLER- UNNGÅR SIDEBRUDD
      *         TIDL. PTF-ER+ FORMAT TPBUNNV/TPBUNNF/TPBUNN FYSISK SLETTET
      *    JOV PTF11 FARL. GODS I EGET FORMAT / ANNEN FONT  BRINGTMS
OBS!!!*              LPI(12) INNFØRT - ALLEN LINJENUMMER ER DOBLET(UNNT. KFMT..)
     A**************************************************'
     A* Stringkode linje 1/2
     A**************************************************'
     A          R FBREVH1
     A            KFMT1         75      1  1
     A          R FBREVH2
     A            KFMT2         75      2  1
      **************************************************'
      *   FRAKTBREV/TOLLPASS (50 ON), FAST HEADERDEL
      **************************************************'
     A          R FBREV
     A  53                              4  1'** OBS MIDLERTIDIG BREV, UMERKET '
     A                                       HIGHLIGHT
     A  53                              4 34'GODS **'
     A                                       HIGHLIGHT
     A            DFNAVS        30      6 11
     A            DFAD1S        30     10 11
     A            DFKNS         12     10 47
     A            PDATE1         6  0  10 64EDTCDE(Y)
     A            DFPNLS         4  0  14 11
     A            DFAD3S        30     14 17
     A            DFSREF        17     14 44
N07  A            DFBREF        17     14 60
     A            DFNAVM        30     18 11
     A            DFNAVX        30     18 44
     A            DFKNX         12     20 65
     A            DFAD1M        30     22 11
     A            DFAD1X        30     22 44
     A            DFAD3M        30     26 11
     A            DFAD3X        30     26 44
     A            DFNAVL        30     30 11
     A            DFKNM         12     30 47
     A            DFMREF        17     30 62
     A            DFAD1L        30     34 11
     A            DFFVBL         9  2  34 46EDTCDE(4)
     A            DFFKAT         4     34 58
     A            DFPOLI        13     34 62
     A            DFSKFO         1     34 76
     A            DFPOUL         4  0  38 11
     A                                       HIGHLIGHT
     A            DFADUL        30     38 17
     A                                       HIGHLIGHT
     A            DFLEVB         3     38 44
     A            DFLEVT        20     38 48
     A            DFNAT         20     42 11
      * Belastes 46=Sender,47=Mottaker,48=Annen (38 ON=Kyocera)
     A N38 46                          42 53'X'
     A N38 47                          42 61'X'
     A N38 48                          42 68'X'
     A  38 46                          42 52'X'
     A  38 47                          42 59'X'
     A  38 48                          42 66'X'
     A            DFOVER        17     46 11
     A            DFNTEU         2  0  46 31EDTCDE(Z)
     A            DFNTAU         2  0  46 39EDTCDE(Z)
     A            DFPROK         5     50 11
     A N61        DFLEVI        30     50 47
     A                                       HIGHLIGHT
     A  61                             50 44'OBS!!! ETTERKRAV:'
     A                                       HIGHLIGHT
     A  61        XTOTAL         9  2  50 62EDTCDE(4)
     A                                       HIGHLIGHT
     A  61                             50 73'OBS!!!'
     A                                       HIGHLIGHT
     A            DFPRO1        30     52 11
     A            DFPRO2        30     54 11
     A                                       HIGHLIGHT
     A  61        BANKG         15     54 47
     A N61        EBANKG        30     54 47
     A                                       HIGHLIGHT
     A N61        DFBELE         9  2  54 67EDTCDE(4)
     A                                       HIGHLIGHT
     A                                 58  1' '
      * FERDIG MED FAST "HEADER-DEL"
      *
      *
      * Varelinjedetaljer - VED STANDARD
      * OBS! Ved endring, se også VARELIF for fax..
     A          R VARELI                    SPACEB(1)
     A  34                                  HIGHLIGHT
     A            R33MRK        12         8
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R34ANT         7  0     18EDTCDE(Z)
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R35VSL        27        27
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R36VKT         9  0     52EDTCDE(Z)
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R37DM3         7  0     61EDTCDE(4)
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R37TXT         3        69
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R37LM          4  2     73EDTCDE(4)
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R37TX2         2        78
     A N75                                  FONT(416 (*POINTSIZE 8))
      *
      * Varelinje bunnstrek
     A          R VARELIU                   SPACEB(1)
     A                                    53'--------------------------'
     A                                      HIGHLIGHT
     A  49        LINJE          3  0     76
     A N75                                  FONT(416 (*POINTSIZE 8))
      *
      * Blanklinje, for kontrollert spacing
     A          R BLANKLIN                  SPACEB(1)
     A                                     1' '
     A  49        LINJE          3  0     76
     A N75                                  FONT(416 (*POINTSIZE 8))
      *
      * UFORTOLLET TEKST
     A          R UFORT
     A                                    10'UFORTOLLET GODS'
     A                                      FNTCHRSET(C0S0OB10 +
     A                                      T1D0BASE)
     A                                      SPACEB(1)
     A                                    33'Godsnummer:'
     A                                      FNTCHRSET(C0S0OB10 +
     A                                      T1D0BASE)
     A            HEGN          15        45
     A                                      FNTCHRSET(C0S0OB10 +
     A                                      T1D0BASE)
     A                                    61'Pos:'
     A                                      FNTCHRSET(C0S0OB10 +
     A                                      T1D0BASE)
     A            POSNR         12A       66
     A                                      FNTCHRSET(C0S0OB10 +
     A                                      T1D0BASE)
     A                                     1' '
     A                                      SPACEB(1)
     A  40                                33'Tr.måte:'
     A  40        HETRM          2  0     42
     A  40                                45'LK:'
     A  40        HETRI          2        49
     A  40                                53'Cont:'
     A  40        HETRC          1  0     59
     A  40                                60'/'
     A  40        HETRCN        17        61
     A                                    10'Nytt Godsnummer/Eksp.nr:'
     A                                      FNTCHRSET(C0S0OB10 +
     A                                      T1D0BASE)
     A                                      SPACEB(1)
     A            HEGNN         15        41
     A                                    10'** MÅ IKKE UTLEVERES UTEN TOLLVESE-
     A                                      NETS TILLATELSE ***'
     A                                      HIGHLIGHT
     A                                      SPACEB(2)
      *
      * ETTERKRAVS-header (+ 2 første varelinjer)
     A          R EKRHEA                    SPACEB(2)
     A                                    10'Etterkrav:'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A            XTOTAL         9  2     26EDTCDE(4)
     A                                      HIGHLIGHT
     A                                       UNDERLINE
     A                                    46'Gebyr                  Beløp mva'
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                      SPACEA(1)
     A                                    10'Kunde:'
     A            HEKNKF         8  0     16EDTCDE(Z)
     A N31                                26'Faktdato:'
     A N31        PDATE1         6S 0     36EDTCDE(Y)
     A            T1            20        46
     A            B1             9  2     66EDTCDE(4)
     A            M1             1        78
     A                                      SPACEA(1)
     A N31                                10'Faktnr:'
     A N31        XFN            7S 0     28EDTCDE(Z)
     A N31                                26'Forfdato:'
     A N31        PXFFDT         6  0     36EDTCDE(Y)
     A            T2            20        46
     A            B2             9  2     66EDTCDE(4)
     A            M2             1        78
     A  49        LINJE          3  0     76
      *
      * Etterkravs-linjer (utover 2 første)
     A          R EKRLIN                    SPACEB(1)
     A            TEKST         20        46
     A            BELØP          9  2     66EDTCDE(4)
     A            MOMSKO         1        78
     A  49        LINJE          3  0     76
      *
      * Moms-linje  (kun om moms finnes)
     A          R EKRMOMS                   SPACEB(1)
     A                                    46'MVA-GRL:'
     A            XMVAGR         8  2     55EDTCDE(J)
     A            XPMOMS         8  2     67EDTCDE(4)
     A  49        LINJE          3  0     76
      *
      * Evt FAST AVD. TP/FB-tekst
     A          R R38TPK                    SPACEB(1)
     A                                      HIGHLIGHT
     A            KFTFTX        51        15
     A N75                                  FONT(416 (*POINTSIZE 8))
     A  49        LINJE          3  0     76
     A N75                                  FONT(416 (*POINTSIZE 8))
      *
      * Fritekst-linjer rubr 38 (TIDLIGERE 29)  - 416=COURIER ROMAN MEDIUM
      * 2305=HELVETICA ROMAN BOLD    304=GOTHIC TEXT   420=COURIER ROMAN BOLD
     A          R R38LIN                    SPACEB(1)
     A            D29TXT        70        10
     A N75                                  FONT(416 (*POINTSIZE 8))
     A  49        LINJE          3  0     76
     A N75                                  FONT(416 (*POINTSIZE 8))
      * FALIG GODS - EGET FORMAT FOR Å ENDRE FONT
     A          R R38FARH                   SPACEB(1)
     A                                    10'FARLIG GODS:'
     A                                      FONT(2305 (*POINTSIZE 12))
     A          R R38FARL                   SPACEB(1)
     A            FARLTXT       90        10FONT(420 (*POINTSIZE 7))
     A                                      HIGHLIGHT
     A  49        LINJE          3  0     76
     A N75                                  FONT(416 (*POINTSIZE 8))
      *
      * Fraktb.vekt (men kun om den ikke er skrevet som del av rubr38)
     A          R FVEKT                     SKIPB(112)
     A            DFVKTF         9  0     66EDTCDE(Z)
      *
      *
      * Stringkode bunn
     A          R FBREVH3                   SKIPB(62)
     A            KFMT3         75         1
      *
      *
      **************************************************'
      *   FAXLAYOUT FRAKTBREV/TOLLPASS (50 ON), FAST HEADERDEL
      **************************************************'
     A          R FAXLAY
     A N71        WSSN20        20      6 52BARCODE(CODE128 2 *HRITOP *HRZ +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
     A  71        WSSN10        10      6 52BARCODE(CODE128 2 *HRITOP *HRZ +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
     A            DFNAVS        30     10 11
     A            DFAD1S        30     14 11
     A            DFKNS         12     14 48
     A            PDATE1         6  0  14 67EDTCDE(Y)
     A            DFPNLS         4  0  18 11
     A            DFAD3S        30     18 19
      * 52 = PDFDIREKTE  , N52= FAX/EMAIL
E08  A  52        DFSREF        17     18 45
E08  A N52        DFSREFX       17     18 47
E08  A  52        DFBREF        17     18 64
E08  A N52        DFBREFX       17     18 66
     A            DFNAVM        30     22 11
     A            DFNAVX        30     22 48
     A            DFKNX         12     24 66
     A            DFAD1M        30     26 11
     A            DFAD1X        30     26 48
     A            DFAD3M        30     30 11
     A            DFAD3X        30     30 48
     A            DFNAVL        30     34 11
     A            DFKNM         12     34 48
     A            DFMREF        17     34 66
     A            DFAD1L        30     38 11
     A            DFFVBL         9  2  38 48EDTCDE(4)
     A            DFFKAT         4     38 60
     A            DFPOLI        13     38 66
     A            DFSKFO         1     38 79
     A            DFPOUL         4  0  42 11
     A                                       HIGHLIGHT
     A            DFADUL        30     42 17
     A                                       HIGHLIGHT
     A            DFLEVB         3     42 48
     A            DFLEVT        20     42 52
     A            DFNAT         20     46 11
      * Belastes 46=Sender,47=Mottaker,48=Annen (38 ON=Kyocera)
E08  A  52 46                          46 54'X'
E08  A N52 46                          46 55'X'
E08  A  52 47                          46 62'X'
E08  A N52 47                          46 63'X'
E08  A  52 48                          46 69'X'
E08  A N52 48                          46 70'X'
     A            DFOVER        17     50 11
     A            DFNTEU         2  0  50 33EDTCDE(Z)
     A            DFNTAU         2  0  50 41EDTCDE(Z)
     A            DFPROK         5     54 11
     A N50        DFLEVI        30     54 48
     A  50                             54 48'TOLLAGER/-EKSPEDISJON'
N01  A                                      FNTCHRSET(C0S0OB10 +
N01  A                                      T1D0BASE)
     A                                       HIGHLIGHT
     A  61                             54 48'OBS!!! ETTERKRAV:'
     A                                       HIGHLIGHT
     A  61        XTOTAL         9  2  54 66EDTCDE(4)
     A                                       HIGHLIGHT
     A  61                             54 77'OBS!!!'
     A                                       HIGHLIGHT
     A N50        DFPRO1        30     56 11
N03  A  50                             56 11'TOLLPASSFRAKTBREV'
N01  A                                      FNTCHRSET(C0S0OB10 +
N01  A                                      T1D0BASE)
     A            DFPRO2        30     58 11
     A                                       HIGHLIGHT
     A  61        BANKG         15     58 48
     A N61        EBANKG        30     58 48
     A                                       HIGHLIGHT
     A N61        DFBELE         9  2  58 70EDTCDE(4)
     A                                       HIGHLIGHT
     A                                 59  1' '
      * FERDIG MED FAST "HEADER-DEL"
      *
      * Varelinjedetaljer - VED PDF DIREKTE
N08  A          R VARELIP                   SPACEB(1)
     A  34                                  HIGHLIGHT
     A            R33MRK        12         9
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R34ANT         7  0     19EDTCDE(Z)
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R35VSL        27        27
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R36VKT         9  0     52EDTCDE(Z)
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R37DM3         7  0     61EDTCDE(4)
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R37TXT         3        69
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R37LM          4  2     73EDTCDE(4)
     A N75                                  FONT(416 (*POINTSIZE 8))
     A            R37TX2         2        78
     A N75                                  FONT(416 (*POINTSIZE 8))
     A  49        LINJE          3  0     76
     A N75                                  FONT(416 (*POINTSIZE 8))
      * Varelinjedetaljer - VED FAX
     A          R VARELIF                   SPACEB(1)
     A  34                                  HIGHLIGHT
N08  A            R33MRK        12        10
     A N75                                  FONT(416 (*POINTSIZE 8))
N08  A            R34ANT         7  0     20EDTCDE(Z)
     A N75                                  FONT(416 (*POINTSIZE 8))
N08  A            R35VSL        27        30
     A N75                                  FONT(416 (*POINTSIZE 8))
N08  A            R36VKT         9  0     55EDTCDE(Z)
     A N75                                  FONT(416 (*POINTSIZE 8))
N08  A            R37DM3         7  0     64EDTCDE(4)
     A N75                                  FONT(416 (*POINTSIZE 8))
N08  A            R37TXT         3        72
     A N75                                  FONT(416 (*POINTSIZE 8))
N08  A            R37LM          4  2     76EDTCDE(4)
     A N75                                  FONT(416 (*POINTSIZE 8))
N08  A            R37TX2         2        81
     A N75                                  FONT(416 (*POINTSIZE 8))
     A  49        LINJE          3  0     76
     A N75                                  FONT(416 (*POINTSIZE 8))
     A          R OVL
     A                                      ENDPAGE
     A                                      OVERLAY(&FB01 0.1 0)
     A            FB01           8A  P
