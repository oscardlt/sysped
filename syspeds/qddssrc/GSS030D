*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 SH  PTF10 beregne turkostnader med kode C i wsnr
10XXXA* 02 SH  PTF10 nytt dangerous goods logikk
10027A* 03 YBC PTF10 ORDRETYPE
10XXXA* 04 YBC PTF10 UTSKRIFT FOR LABEL UANSETT TRANSFER ELLER IKKE
10XXXA* 05 YBC PTF10 OPSJON M FOR Å OPPDATERE MRN-NR
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      ALTHELP
     A                                      HELP
      *
     A          R GSS030DA                  SFL
     A            WSNR           1A  B  6  2VALUES(' ' '1' '2' '5' '6' '7' '9' -
S01  A*                                     'P' 'A' 'L' 'B' 'I')
S02  A*                                     'P' 'A' 'L' 'B' 'I' 'C')
N02  A*                                     'P' 'A' 'L' 'B' 'I' 'C' 'D')
N05  A                                      'P' 'A' 'L' 'B' 'I' 'C' 'M' 'T' 'D')
     A            TRDW          15   H
     A            TRLOCF         5A  H
     A            TRLOCT         5A  H
     A            TRTNO         11Y 0O  6  5EDTCDE(4)
     A  41                                  COLOR(WHT)
     A            TRST           1A  O  6 17
     A            TRAD1         30A  H
     A            XTRAD1        28A  O  6 19
     A            TRBCD          5A  O  6 48
     A  43                                  COLOR(WHT)
     A            TRCNO          8Y 0O  6 54EDTCDE(4)
     A  42                                  COLOR(WHT)
     A            TRDTL          8Y 0H
     A            WXDTL          6Y 0O  6 63EDTCDE(4)
     A            TRDTS          8Y 0H
     A            WXDTS          6Y 0O  6 70EDTCDE(4)
     A            TRFC           3A  O  6 77
      *
     A          R GSS030DB                  SFLCTL(GSS030DA)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'Frisk')
     A                                      CF23(23 'BEKREFTE')
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS030D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with proforma invoice'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'T=Tr.label M=MRN'
     A                                      COLOR(BLU)
     A                                  2 19'I=Box'
     A                                      COLOR(BLU)
     A                                  2 26'B=Box-labels'
     A                                      COLOR(BLU)
     A                                  2 40'L=Labels'
     A                                      COLOR(BLU)
     A                                  2 50MSGCON(010 SHU1010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S01  A*                                 2 61'P=Plukkliste'
     A                                  2 61'P=PICKLIST C=CALC'
     A                                      COLOR(BLU)
     A                                  3  2'   1=Edit trip  2=Edit collino.  5-
     A                                      =Display  6=Print  7=Copy pack.l.  -
     A                                      9=Confirm'
     A                                      COLOR(BLU)
     A                                  4  2'Op Trip No... St Reciever         -
     A                                                  BoatC ClientsN Del.DT D-
     A                                      is.DT Fir'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2DSPATR(UL)
     A            W2TNO         11Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2BCD          5A  B  5 48DSPATR(UL)
     A            W2CNO          8Y 0B  5 54DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R GSS030DC
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=End   F4=Query   F5=Refresh   F-
     A                                      23=Confirm'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS030DD
     A                                      WINDOW(*DFT 5 40 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Confirm on transfer trip'
     A                                      COLOR(WHT)
     A  51                              2  2'GIVE TYPE OF ORDER'
     A  51        WSTYPE         1A  B  2 21DSPATR(UL)
     A                                      VALUES('C' 'T')
     A                                  3  2'Give new stockpos:'
     A            WSPOS1         5A  B  3 21DSPATR(UL)
     A  99                                  DSPATR(HI)
     A                                      DSPATR(PC)
     A            WSPOS2         5A  B  3 27DSPATR(UL)
     A            WSPOS3         5A  B  3 33DSPATR(UL)
     A                                  4  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99                              5  2MSGCON(025 GSS0038 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R GSS030DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF23(23 'BEKREFTE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS030D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with proforma invoice'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2 19'I=Box'
     A                                      COLOR(BLU)
     A                                  2 26'B=Box-labels'
     A                                      COLOR(BLU)
     A                                  2 40'L=Labels'
     A                                      COLOR(BLU)
     A                                  2 50MSGCON(010 SHU1010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2'   1=Edit trip  2=Edit collino.  5-
     A                                      =Display  6=Print  7=Copy pack.l.  -
     A                                      9=Confirm'
     A                                      COLOR(BLU)
     A                                  4  2'Op Trip No... St Reciever         -
     A                                                  BoatC ClientsN Del.DT D-
     A                                      is.DT Fir'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2DSPATR(UL)
     A            W2TNO         11Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2BCD          5A  B  5 48DSPATR(UL)
     A            W2CNO          8Y 0B  5 54DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  8  2'No trip found'
     A                                      COLOR(RED)
     A                                 23  2'F3=End   F4=Query   F23=Confirm'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  2 61MSGCON(015 DIV2579 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R GSS030DX
     A                                      WINDOW(*DFT 6 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A            MSGHOD        25A  O  1  2MSGID(&MSGHN SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2'Printer name :'
     A            WSPRT         10A  B  2 17DSPATR(UL)
     A  99                                  DSPATR(HI)
     A                                      DSPATR(PC)
     A                                  3  2'No. of copies:'
     A            WSCPY          1S 0B  3 17DSPATR(UL)
     A                                  4  2'Send fax/email'
     A            WSFAX          1A  B  4 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  4 20'Y/N'
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99                              6  2MSGCON(025 GSS0042 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            MSGHN          7A  P
      *
     A          R GSS030DY
     A                                      WINDOW(*DFT 12 73 *NOMSGLIN)
     A                                      CF04(04 'Søking')
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A            MSGHOD        25A  O  1  2MSGID(&MSGHN SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2'HEADING TEXT'
     A            WSFT1         70A  B  3  2DSPATR(UL)
     A N98N99                               DSPATR(PC)
     A            WSFT2         70A  B  4  2DSPATR(UL)
     A                                  5  2'Printer name :'
     A            WSPRT         10A  B  5 17DSPATR(UL)
     A  99                                  DSPATR(HI)
     A  99                                  DSPATR(PC)
     A                                  5 32'Type of order:'
     A            WSTYPE         1A  B  5 47DSPATR(UL)
     A                                      VALUES(' ' 'C' 'T' 'X')
     A                                  5 49'(Blank=ALL)'
     A                                  6  2'No. of copies:'
     A            WSCPY          1S 0B  6 17DSPATR(UL)
     A                                  7  2'Layout:       '
     A            WSNYL          1A  B  7 17DSPATR(UL)
     A                                      VALUES('P' 'L' 'O')
     A                                  7 20'P=Portrait L=Landscape O=Portrait2'
     A                                  8  2'Curr. code   :'
     A            WSCUR          3A  B  8 17DSPATR(UL)
     A  98                                  DSPATR(HI)
     A  98                                  DSPATR(PC)
     A                                  9  2'Send fax/email'
     A            WSFAX          1A  B  9 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  9 20'Y/N'
S04  A*N27                             10  2'Labels?'
S04  A*N27        WSML           1A  B 10 10DSPATR(UL)
S04  A*                                     VALUES('Y' 'N')
S04  A*N27                             10 12'P.name:'
S04  A*N27        WSMLP         10A  B 10 20DSPATR(UL)
S04  A*N27                             10 31'Free text:'
S04  A*N27        WSFTXT        30A  B 10 42DSPATR(UL)
     A                                 10  2'Labels?'
     A            WSML           1A  B 10 10DSPATR(UL)
     A                                      VALUES('A' 'Y' 'N')
     A                                 10 12'P.name:'
     A            WSMLP         10A  B 10 20DSPATR(UL)
     A                                 10 31'Free text:'
     A            WSFTXT        30A  B 10 42DSPATR(UL)
     A                                 11  2'Comm.invoice?:'
     A            WSCOMM         1A  B 11 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                 11 22'Clientsnr:'
     A            WSKNC          8Y 0B 11 34DSPATR(UL)
     A  97                                  DSPATR(HI)
     A  97                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N99N98N97                       12  2'F4=Søk   F12=Cancel'
     A                                      COLOR(BLU)
     A  99                             12  2MSGCON(025 GSS0042 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  98                             12  2MSGCON(025 GSS0066 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  97                             12  2MSGCON(025 SPK0001 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            MSGHN          7A  P
      *
     A          R GSS030DZ
     A                                      WINDOW(*DFT 07 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Utskrift plukkliste m.m.'
     A                                      COLOR(WHT)
     A                                  2  2'Printer  :'
     A            WSPRT         10A  B  2 13DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'Sortering:'
     A            WSSORT         1A  B  3 13VALUES('L' 'O' 'B' 'P' 'F')
     A                                      DSPATR(PC)
     A                                  3 15'(L=Lo O=Or B=Bo)'
     A                                  4 15'(P=Pack.li./Box)'
     A                                  5  2'Send Email'
     A            WSFAX          1A  B  5 13DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  6  2'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  5 15'(F=Final Pack.l)'
     A  99        WSMSGN        25A  O  7  2MSGID(GSS 0042 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R GSS030DH
     A                                      WINDOW(*DFT 6 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print labels'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRTM        10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  89        WSMSGN        25A     6  2MSGID(GSS0042 SYMSGF)
      *
     A          R GSS030DI
     A                                      WINDOW(*DFT 6 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print labels'
     A                                      COLOR(WHT)
     A                                  3  2'Box-number  :'
     A            WSBOXN         3Y 0B  3 17DSPATR(UL)
     A N89                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  3 21'(0=All)'
     A                                  4  2'Printer name:'
     A            WSPRTM        10A  B  4 17DSPATR(UL)
     A  89                                  DSPATR(HI)
     A  89                                  DSPATR(PC)
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  89        WSMSGN        25A     6  2MSGID(GSS0042 SYMSGF)
N05   *
N05  A          R GSS030DJ
N05  A                                      WINDOW(*DFT 5 50 *NOMSGLIN)
N05  A                                      CF12(12 'Avbryt')
N05  A                                      OVERLAY
N05  A                                  1  2'Give MRN-no. to all T-orders'
N05  A                                  3  2'Code/MRN-No.     :'
N05  A            WSKD           5A  B  3 21DSPATR(UL)
N05  A  99                                  DSPATR(HI)
N05  A  99                                  DSPATR(PC)
N05  A            WSMRN         18A  B  3 27DSPATR(UL)
N05  A  98                                  DSPATR(HI)
N05  A  98
N05  AON99                                  DSPATR(PC)
N05  A                                  4  2'F12=Cancel'
N05  A                                      COLOR(BLU)
N05  A  99                              5  2'Code have be MRNI or MRNO.'
N05  A                                      COLOR(WHT)
N05  A  98                              5  2'MRN-No. is not valid.'
N05  A                                      COLOR(WHT)
