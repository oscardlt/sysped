********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PTF10 Skriv rett til IFS
     FEDIMA     IF   E           K DISK
     FEDIC      UF   E             DISK
     FSVTFI     IF   E             DISK
     FEDII      IF   E           K DISK
     FEDIS      O  A E             DISK
     D                 DS
     D  WMBR                   1     14
     D  WMBRA                  1      1
     D  WMBRB                  2     10  0
      *   SYEDI data area
     D EDIDTA          DS           256
     D  UELIBF               171    180
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    MSR           WSR
     C     *LIKE         DEFINE    MST           WST
     C     *LIKE         DEFINE    *IN51         WIN51
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSR
     C                   KFLD                    WST
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM      *IN51         WIN51
     C                   PARM      SMBR          SMBR
     C                   PARM      SSN           SSN
     C                   PARM                    INNETW
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'S'           WSR
     C                   MOVE      '+'           WST
     C                   MOVE      'S'           WMBRA
      *_____________________________
      * Hente data fra firmaregister
      *"""""""""""""""""""""""""""""
     C     1             CHAIN     SVTFI                              20
      *_________________________________
      * Hente dagens dato og klokkeslett
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      * Åpne dataarea
     C     *DTAARA       DEFINE                  EDIDTA
     C                   IN        EDIDTA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *________________________________
      * Oppdatere status i meldingslogg
      *""""""""""""""""""""""""""""""""
     C                   MOVE      '0'           *IN20
     C     KEYA          CHAIN     EDIMA                              51
     C     *IN51         DOWEQ     '0'
     C     MVEN          IFEQ      *BLANKS
     C     M0065         IFEQ      'CUSDEC'
     C     M1N07         ANDEQ     'SVI'
     C     M0010         CHAIN     EDII                               52
     C     *IN52         IFEQ      '0'
     C                   MOVE      '1'           *IN20
     C                   ELSE
     C                   MOVE      '0'           *IN20
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C  N20KEYA          READE     EDIMA                                  51
     C  N51
     CANN20              ENDDO
     C     *IN51         CABEQ     '1'           SRB9
      *________________________________________
      * Hente og oppdater neste sendningsnummer
      *""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               20
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *_________________
      * Logge sendningen
      *"""""""""""""""""
     C                   MOVE      M0004         S0004
     C                   MOVE      M0010         S0010
     C                   Z-ADD     CSN           WMBRB
     C                   MOVE      WMBR          S0020
     C                   EVAL      S0026='CUSDECI-IE'
     C                   MOVE      M0035         S0035
     C                   Z-ADD     0             S0036
     C                   Z-ADD     CSN           SSN
     C                   MOVE      'S'           SSR
     C                   MOVE      ' '           SST
     C                   MOVEL     WDT           SDT
     C                   MOVEL     WTM           STM
     C                   MOVEL     '*LIBL   '    SLIB
     C                   MOVE      '  '          SLIB
N01  C                   MOVEL     '*IFS'        SFILE
     C                   MOVEL     WMBR          SMBR
     C                   EVAL      SIFS = '/' + %trimr(UELIBF)
     C                             + '/EDISE/'
     C                             + SMBR
     C                   WRITE     EDISR
     C     SRB9          ENDSR
