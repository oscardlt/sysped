      * Status fra POSTEN,
      * (SH tatt utg.pkt i Nortrail. CE01R (fra CE) - bygd om for Posten   sommeren 2008 (A13))
      * (JOV - Kommentar - Kunne også benyttet E162R_96A ....)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 store omskrivinger ....  sendingsnr ligger i BGM.. (EDIM, M1004)
10172 * 02 SH  PTF10 norsk text til Track and trace
10172 * 03 JOV PTF10 Forbedret teksthåndtering - også sign, Location Reason
********************************************************************
      *              Hvert enklet KOLLI er CNI, Under der er NORMALT EN STS, men dek KAN være flere.
      *              skrevet totalt om :
      *              Gammel versjon behandlet HVER CNI (=3 kolli / 3-doble meldinger)
      *              NÅ - tar kun første kolli (gamler på at alle er like) / tar ALLE statuser
********************************************************************
     H DATEDIT(*YMD)
     FTRACKL02  IF A E           K DISK
     FDOKUF7    IF   E           K DISK
     FHEADF     UF   E           K DISK
     FFIRM      UF   E           K DISK
     FFRISOK    IF A E           K DISK
     FKODFRI    IF   E           K DISK
     FEdim      IF   E           K DISK
     FECMSG     IF   E           K DISK
     FECNI96A   IF   E           K DISK
     FEDTM96A   IF   E           K DISK
     FENAD96A   IF   E           K DISK
N03  FELOC96A   IF   E           K DISK
     FESTS96A   IF   E           K DISK
     D                 DS
     D  HEKLMO                 1      4  0
     D  HEKLMOA                1      4
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D  HEDTOP                 1      8  0
S03   *
S03  D* W1             C                   'Arrived distribution terminal'
S03  D* W20            C                   'Placed in warehouse'
S03  D* W21            C                   'Delivered customer'
S03  D* W48            C                   'Loaded distribution container'
S03  D* W56            C                   'Delivery to customer failed'
S03  D* WZPG           C                   'Ankommet terminal og videresendt'
S03  D* W113           C                   'Loaded distribution truck'
S03  D* WOTH           C                   'Code not defined'
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     c**************************************************************
     C     INIT          BEGSR
     c**************************************************************
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    CREP0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    CREP0
     C                   KFLD                    SREP1
     C     HEADK         KLIST
     C                   KFLD                    ZEAVD
     C                   KFLD                    ZEOPD
     C     FRIKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    WSKODE
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    CGN           ZGN1
     C     *LIKE         DEFINE    HEAVD         ZEAVD
     C     *LIKE         DEFINE    HEOPD         ZEOPD
     C     *LIKE         DEFINE    FSKODE        WSKODE
     C     *LIKE         DEFINE    FSKODE        WSKODE2
     C     *LIKE         DEFINE    ECIIDC        ECIIDCX
     C                   MOVEL     'S'           WCSR              1
     C                   MOVEL     'IFTSTA'      WC0065            6
     C                   read      firm                                   50
     C                   MOVEL     'POD'         WSKODE
      * INIT WORKFIELDS
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
     C     ZMN           CHAIN     EDIM                               51
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE                  RTVD /
      *////////////////////////////////////////////////////////////
     C******************************************************************
     C     RTVD          BEGSR
     C******************************************************************
      *_______________________
      * READ ALL MAIN SEGMENTS
      *"""""""""""""""""""""""
      * CNI    (KUN det FØRSTE segmentet= første kolli / forutsetter at de andre er like..)
      *        (T&T er uansett på oppdr-nivå / unngå dobbeltlogging ved flerkolli.....)
     C                   MOVE      '0'           *IN50
     C                   Z-ADD     4             ZGN0
     C     KEYMS0        chain     ECNI96A                            50
     C     M1004         ifne      *blanks
     c     M1004         chain     DOKUF7                             50
     C  N50              EXSR      RTVD2
     C                   END
     C                   ENDSR
     C******************************************************************
     C     RTVD2         BEGSR
     C******************************************************************
     C                   Z-ADD     5             ZGN1
     C                   MOVE      '0'           *IN51
      *
     C     KEYMS1        setll     ESTS96A
     C     KEYMS1        READE     ESTS96A                                51
     c     *in51         doweq     '0'
      * DTM05 -tid
     C                   MOVE      '0'           *IN52
     C     KEYMS2        CHAIN     EDTM96A                            52
      * NAD05 - kvittert av (ligger som NAD-segment 2 )
     C                   MOVE      '0'           *IN52
     C     KEYMS2        CHAIN     ENAD96A                            52
     C     *IN52         IFEQ      *OFF
     C     N3035         ANDNE     'AP'
     C     KEYMS2        READE     ENAD96A                                52
     C                   ENDIF
     C     *IN52         IFEQ      *ON
     C                   MOVE      *BLANKS       N3036A
     C                   ENDIF
N03  C     KEYMS2        CHAIN     ELOC96A                            52
N03  C     *IN52         IFEQ      *ON
N03  C                   MOVE      *BLANKS       L3225                          LOC ID
N03  C                   MOVE      *BLANKS       L3224                          LOC tekst
N03  C                   ENDIF
     c
     C                   EXSR      SR
     C     KEYMS1        READE     ESTS96A                                51
     C                   end
     C     NYLES         ENDSR
      ***********************************************
     C     SR            BEGSR
      ***********************************************
     C                   MOVE      DFAVD         ZEAVD
     C                   MOVE      DFOPD         ZEOPD
     C     HEADK         CHAIN     HEADF                              51
     C     *IN51         IFEQ      *OFF
      *---------------------------------------
      * OPPDATER DATO, KLOKKE OG SIGNATUR
      *---------------------------------------
     C     8             SUBST     D2380:1       UEDTMO            8
     C     4             SUBST     D2380:9       UEKLMO            4
S03  C*    S9011         IFEQ      '012'
S03  C*    S9011         OREQ      'ZPI'
S03  C*    S9011         OREQ      '113'
N03  C     S9011         IFEQ      'ZPI'
     C                   MOVEL     N3036A        HESGM
s03  C*    N3124A        IFEQ      *blanks
N03  C     N3036A        IFEQ      *blanks
     C                   MOVEL     'BRING'       HESGM
     C                   end
     C                   MOVEL     UEDTMO        HEDTMO
     C                   MOVEL     UEKLMO        HEKLMO
     C                   ENDIF
     C                   MOVEL     S9011         HELB
     C                   UPDATE    HEADFR
      *
      *---------------------------------------
      * OPPDATER FRI SØKEVEI MED POD
      *---------------------------------------
      *
     C     s9011         ifeq      'ZPI'
     C                   CLEAR                   frisokr
     C     FRIKEY        CHAIN     FRISOK                             51
     C     *in51         ifeq      '1'
     C                   move      ZEAVD         fsavd
     C                   move      ZEOPD         fsopd
     C                   move      'POD'         fskode
     C                   MOVEL     'DT:'         FSSOK
     C     FSSOK         CAT       HEDTMO:1      FSSOK
     C     FSSOK         CAT       'TM:':1       FSSOK
     C     FSSOK         CAT       HEKLMOA:1     FSSOK
     C                   MOVE      HEDTOP        FSDTOP
     C     FSKODE        CHAIN     KODFRI                             33
     C  N33              MOVE      KFSODK        FSDOKK
     C                   write     frisokr
     C                   end
     C                   end
      *
      *---------------------------------------
      * OPPDATER TRACK AND TRACE
      *---------------------------------------
      *
      * OPPDATER TRACK&TRACE
      *
     C     TRACKK        KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   CLEAR                   TRACKFR
      *
     C                   MOVEL     ZEAVD         TTAVD
     C                   MOVEL     ZEOPD         TTOPD
     C                   MOVE      *ZEROS        TTFBNR
     C                   MOVE      'S'           TTMANU
     C     s9011         ifeq      'ZPI'
     C                   MOVEL     'POD'         TTACTI
     C                   else
     C                   MOVEL     'MOV'         TTACTI
     c                   end
     C                   MOVEL     D2380         TTDATE
     C     4             SUBST     D2380:9       WTTTIME           4
     C                   MOVE      WTTTIME       TTTIME
     C     TTTIME        MULT      100           TTTIME
     C                   SELECT
N03   * tekst endret totalt uten merking
     C     S9011         WHENEQ    'ZPR'
     C                   eval      TTTEXL=%trim(S9011)+' '
     c                             +'Sendingen er innleveringsregistrert BRING '
     C                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
     c                   eval      TTTEXT=%trim(S9011)+' '
     c                             +'Shipment is handed in to BRING '
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
      *
     C     S9011         WHENEQ    'ZPG'
     C                   eval      TTTEXL=%trim(S9011)+' '
     c                             +'Sendingen er ankomstregistret på terminal '
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
     C                   eval      TTTEXT=%trim(S9011)+' '
     c                             +'Shipment arrived terminal '
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
      *
     C     S9011         WHENEQ    'ZP3'
     C                   eval      TTTEXL=%trim(S9011)+' '
     c                             +'Lastet på distribusjonsbil '
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
     C                   eval      TTTEXT=%trim(S9011)+' '
     c                             +'Loaded distribution truck '
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
      *
     C     S9011         WHENEQ    'ZPV'
     C                   eval      TTTEXL=%trim(S9011)+' '
     c                             +'Sendingen kan være forsinket '
     c                             +%trim(S9012A)+' '                           Reason
     C                   eval      TTTEXT=%trim(S9011)+' '
     c                             +'Shipment may be dalayed   '
     c                             +%trim(S9012A)+' '                           Reason
      *
     C     S9011         WHENEQ    'ZPH'
     C                   eval      TTTEXL=%trim(S9011)+' '
     c                             +'Forsøkt levert:'
     c                             +%trim(S9012A)+' '                           Reason
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
     C                   eval      TTTEXT=%trim(S9011)+' '
     c                             +'Delivery failed:'
     c                             +%trim(S9012A)+' '                           Reason
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
      *
     C     S9011         WHENEQ    'ZPI'
     C                   eval      TTTEXL=%trim(S9011)+' '
     c                             +'Sendingen er levert kunde - '
     c                             +%trim(N3036A)+' '                           signatur
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
     C                   eval      TTTEXT=%trim(S9011)+' '
     c                             +'Delivered to customer - '
     c                             +%trim(N3036A)+' '                           signatur
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
     C                   OTHER
     C                   eval      TTTEXL=%trim(S9011)+' '
     c                             +'*UKJ* '+%trim(S9010)+' '                   St.tekst
     c                             +%trim(S9012A)+' '                           Reason
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
     C                   eval      TTTEXT=%trim(S9011)+' '
     c                             +'*UKJ* '+%trim(S9010)+' '                   St.tekst
     c                             +%trim(S9012A)+' '                           Reason
     c                             +'(' + %trim(L3225 )+' '+%trim(L3224 ) +')'  LOC kode/tekst
     C                   ENDSL
s03  C*                  CAT       '/':1         TTTEXT
s03  C*                  CAT       N3036A:1      TTTEXT
     C                   MOVEL     WDT           TTDATL
     C                   MOVEL     WTM           TTTIML
     C                   EVAL      TTEDEV=S9011
     C                   EVAL      TTEDRE=S9013A
     C                   EVAL      TTNAME=N3036A
     C                   IF        N3036A=*BLANKS
     C                   EVAL      TTNAME=N3036A
     C                   ENDIF
     C                   MOVEL     'BRING'       TTUSER
s03  C*                  eval      tttexl=tttext
     C                   WRITE     TRACKFR
     c                   END
     C                   ENDSR
