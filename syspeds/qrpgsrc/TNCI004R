      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNCI004R MODULE(*LIBL/TNCI004R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVNH      UF   E           K DISK    USROPN
     FSVNL      IF   E           K DISK    USROPN
     FSVNLKL    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"oneorder" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta detta ena ærendet.
      *
     c     Error         ifeq      *blanks
     C                   MOVE      *ZEROS        WSVPOS
     C                   MOVE      *ZEROS        WSVPN             5 0
     C                   MOVE      *ZEROS        WSVKB
     C                   MOVE      *ZEROS        WSNTK
     C                   MOVE      'J'           X0MOTT            1
     C                   MOVE      *ZEROS        XLTOTIBV         11 3
     C                   MOVE      *ZEROS        XLTOTBVNE        11 3
      *
     C     SvnhKey       SETLL     SVNL
     C     SvnhKey       READE     SVNL                                   33
     C                   DOW       *IN33 = *OFF
      * SUMMER OPPRINGLIG BRUTTOVEKT PÅ LINJER
     C                   ADD       TVVKTB        XLTOTIBV
      * SUMMER OPPRINGLIG BRUTTOVEKT PÅ ALLE NYE LINJER
     C                   IF        NVCT = 'NE'
     C                   ADD       NVVKTB        XLTOTBVNE
     C                   END
      *
     C                   IF        NVCT = 'DI' AND NVNT = 0 AND NVNTEH = 0
     C                   MOVE      TVEH          XTVEH             2
     C                   MOVE      TVNT          XTVNT             5 0
     C                   MOVE      NVEH          XNVEH             2
     C                   MOVE      NVNT          XNVNT             5 0
     C                   MOVE      NVNTEH        XNVNTEH           5 0
     C                   MOVE      'J'           XHELMANKO         1
     C     SVNLKLK       SETLL     SVNLKL
     C     SVNLKLK       READE     SVNLKL                                 34
     C                   DOW       *IN34 = *OFF
     C                   IF        NVNT <> 0
     C                   MOVE      'N'           XHELMANKO
     C                   GOTO      TAGTRV01
     C                   END
     C     SVNLKLK       READE     SVNLKL                                 34
     C  N34              ENDDO
     C     TAGTRV01      TAG
     C                   MOVE      XTVEH         TVEH
     C                   MOVE      XTVNT         TVNT
     C                   MOVE      XNVEH         NVEH
     C                   MOVE      XNVNT         NVNT
     C                   MOVE      XNVNTEH       NVNTEH
     C                   ELSE
     C                   MOVE      'N'           XHELMANKO
     C                   END
      *
     C                   IF        NVCT = 'DI' AND XHELMANKO = 'J'
     C                   SUB       1             WSVPOS
     C                   END
     C                   IF        NVCT = 'DI' AND NVNT = 0 AND
     C                             NVNTEH = 0 AND X0MOTT = 'J'
     C                   ELSE
     C                   MOVE      'N'           X0MOTT
     C                   END
     C                   ADD       1             WSVPOS
     C                   IF        TVVKTB = 0
     C                   ADD       1             WSVPN
     C                   END
     C                   IF        XHELMANKO = 'N'
     C                   ADD       NVVKTB        WSVKB
     C                   END
     C                   IF        NVNT = 0
     C                   ADD       NVNTEH        WSNTK
     C                   ELSE
     C                   ADD       NVNT          WSNTK
     C                   END
     C     NVEH          IFEQ      'VQ'
     C     NVEH          OREQ      'VG'
     C     NVEH          OREQ      'VL'
     C     NVEH          OREQ      'VY'
     C     NVEH          OREQ      'VR'
     C     NVEH          OREQ      'VO'
     C                   ADD       1             WSNTK
     C                   END
      *
     C     SVNLKLK       SETLL     SVNLKL
     C     SVNLKLK       READE     SVNLKL                                 34
     C                   DOW       *IN34 = *OFF
     C                   IF        NVNT = 0
     C                   ADD       NVNTEH        WSNTK
     C                   ELSE
     C                   ADD       NVNT          WSNTK
     C                   END
     C     SVNLKLK       READE     SVNLKL                                 34
     C  N34              ENDDO
      *
     C     SvnhKey       READE     SVNL                                   33
     C  N33              ENDDO
      * HVIS OPPRINGELIG BRUTTOVEKT = 0, SPØR ETTER BRUTTOVEKT FRA BRUKER
      * MED DEFAULT VEKT = TOTAL BRUTTOVEKT + SUM AV NYE LINJES BRUTTOVEKT.
     C                   IF        XLTOTIBV = 0 AND WSVKB <> 0
     C                   EVAL      WSVKB = TIVKB + XLTOTBVNE
     C                   END
     C     SvnhKey       CHAIN     SVNH                               55
     C                   SELECT
     C                   WHEN      WSVPOS = TIVPOS
     C                   MOVE      *ZEROS        NIVPOS
     C                   WHEN      WSVPOS = 0
     C                   Z-SUB     1             NIVPOS
     C                   OTHER
     C                   MOVE      WSVPOS        NIVPOS
     C                   ENDSL
     C                   SELECT
     C                   WHEN      WSVKB = TIVKB
     C                   MOVE      *ZEROS        NIVKB
     C                   WHEN      X0MOTT = 'J' AND WSVKB = 0
     C                   Z-SUB     1             NIVKB
     C                   OTHER
     C                   MOVE      WSVKB         NIVKB
     C                   ENDSL
     C                   IF        TINTK < 0
     C                   Z-SUB     TINTK         XINTK             7 0
     C                   ELSE
     C                   Z-ADD     TINTK         XINTK
     C                   END
     C                   IF        WSNTK = XINTK
     C                   MOVE      *ZEROS        NINTK
     C                   ELSE
     C                   MOVE      WSNTK         NINTK
     C                   END
     C                   UPDATE    SVNHR
      *
     C     *IN55         IFEQ      '0'
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tiavd¬ : ¬'+%trim(%editc(TIAVD:'Z'))+'¬, '
          +'¬titdn¬ : ¬'+%trim(%editc(TITDN:'Z'))+'¬, '
          +'¬tivpos¬ : ¬'+%trim(%editc(NIVPOS:'Z'))+'¬, '
          +'¬tintk¬ : ¬'+%trim(%editc(NINTK:'Z'))+'¬, '
          +'¬tivkb¬ : ¬'+%trim(%editc(NIVKB:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     C                   ENDIF
     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVNH
     C  N50              CLOSE     SVNL
     C  N50              CLOSE     SVNLKL

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   eval      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   eval      OPDN    = c2n2(OPD)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SvnhKey       klist
     C                   kfld                    AVDN
     C                   kfld                    OPDN
     C     SVNLKLK       KLIST
     C                   kfld                    AVDN
     C                   kfld                    OPDN
     C                   KFLD                    TVLI
      *
     C     *LIKE         DEFINE    NIVPOS        WSVPOS
     C     *LIKE         DEFINE    NIVKB         WSVKB
     C     *LIKE         DEFINE    NINTK         WSNTK
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVNH
     C  N50              OPEN      SVNL
     C  N50              OPEN      SVNLKL

     c                   endsr
      *
