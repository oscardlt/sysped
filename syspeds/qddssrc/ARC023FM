     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            WBANE         70A  H      TEXT('BANE')
     A            WSLINE        76A  H      TEXT('EKSTERN')
     A            WSLINK        76A  H      TEXT('EKSTERN')
     A            WSLINW       120A  O  6  4
     A            WSTXT         35A  O  7 45
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/ARC023'
     A                                      COLOR(BLU)
     A                                  1 16'Vis opplastet dokumenter'
     A                                      COLOR(WHT)
     A            UREFF         17   O  1 50
     A            HEUNIK         9  0H
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 68TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(016 SHU1036 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSANT          3Y 0O  3 20COLOR(RED)
     A                                      EDTCDE(4)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  5  2MSGCON(078 SHU0858 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 41MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF08(08 'EXIT')
     A                                      CF12(03 'exit')
     A                                  1  2'SYSPED/ARC023'
     A                                      COLOR(BLU)
     A                                  1 16'Vis opplastet dokumenter'
     A                                      COLOR(WHT)
     A            UREFF         17   O  1 50
     A            HEUNIK         9  0H
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 68TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(016 SHU1036 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSANT          3Y 0O  3 20COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(070 SHU0857 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 SHU1033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
