*****F**************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnrF*Sek Sig Vers Beskrivelse...........................
"""""F*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11008F* 01 JOV PTF11 REKKEFØLGE Len Høy Bre  --> Len Bre Høy  mm
     F* 02 JOV PTF11 BRINGTMS-prosj, store endr. 132x27 mm (DDS umerket)
     F* 03 SH  PTF11 f4 søk enhetstype
     F* 04 JOV PTF11 KORR HÅNDTEING ENHETSTYPE
     F* 05 JOV PTF11 sjekk mot samlasting andre fareklasser  + cleare ADR-variable
     F* 06 SH  PTF11 FEILRETTELSER KOPIERING
     F* 07 SH  PTF11 slette også adr ved sletting
     F* 08 SH  PTF11 reorg ved sletting
     F* 09 SH  PTF11 reorg og  sletting slått sammer til SYGE85
10XXX * 10 JOV PTF11 utvidede tester Fralig gods   + G --> GR  i enhet
10XXX * 11 JOV PTF11 Et oppdrag kan være unntatt for ADRsjekk
10XXX * 12 JOV PTF11 benytt INDEX i ADRUNNRF derso fler like UNNR/EMBgrp
10XXX * 13 JOV PTF11 VARETEKSTENDRING
10XXX * 14 sh  PTF11 nullstille totlm2
      * 15 JOV PTF11 Sikre mot for høy ADR-poeng..
*****F**************************************************************
      *
      ***********************************************************
      *** PROGRAM MAINTAINS DOKUFV FILE                       ***
      *** INDICATORS  01 - 26 FUNCTION  / ROLL KEYS           ***
      *** INDICATORS USED                                     ***
      ***********************************************************
      *
N03  FENHETF    IF   E           K DISK
N11  FHEADF     IF   E           K DISK
     FDOKUFV    UF A E           K DISK
     FDOKUFM    UF A E           K DISK
N02  FDOKUFVF   UF A E           K DISK
N02  FADRUNNRF  IF   E           K DISK
S01  F*YFA10FA  CF   E             WORKSTN
N01  FSYFA10FA  CF   E             WORKSTN USROPN
     F                                     SFILE(FA10AH50:ZRECNO)
     F                                     INFDS(FBAREA)
     D TXT             S             15    DIM(1) CTDATA PERRCD(1)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  UVSINN                 1     25
     D  XPAKN                  1      3
     D  XVT                    5     25
N02  D                 DS
N02  D  WSMRK                  1     35
N02  D   WSMRKA                1     12
N02  D   WSMRKB               13     35
      *
N15  D BigNum          S             19S 5
      *
     C                   EXSR      INIT
      *
N01  C                   OPEN      SYFA10FA
     C     START         TAG
      *______________
      * Clear Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     FA10AK60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   Z-ADD     0             XSLN
      *_____________
      * Fill Subfile
      *"""""""""""""
     C     DOKKY1        SETLL     DOKUFV
     C     DOKKY1        READE(N)  DOKUFV                                 94
      *
     C     START1        TAG
     C     *IN94         DOWEQ     '0'
      * RENUMMERER LINJENR
     C                   ADD       1             XSLN
     C     FVLINR        IFNE      XSLN
     C     DOKKYM        SETLL     DOKUFM
     C     DOKKYM        READE(N)  DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C                   Z-ADD     XSLN          FMLINR
     C     DOKKYM        READE     DOKUFM                                 33
     C  N33              ENDDO
     C                   Z-ADD     XSLN          FVLINR
     C                   END
      *
N02  c                   move      *blanks       FMMRK1
N02  C                   Z-ADD     1             FMMKNR
N02  C                   Z-ADD     1             FMLONR
N02  C     DOKUMK        chain(N)  DOKUFM
N02  c                   movel     FMMRK1        SFMERK
     C                   MOVE      FVLINR        SFLINR
     C                   MOVE      FVANT         SFANT
     C                   MOVEL     FVPAKN        SFPAKN
     C                   MOVEL     FVVT          SFVT
     C                   MOVEL     FVVT          SFVT
     C                   MOVE      FVVKT         SFVKT
     C                   MOVE      FVVOL         SFVOL
     C                   ADD       1             ZRECNO
N02  c                   exsr      LinPoeng
     C                   WRITE     FA10AH50
     C     DOKKY1        READE(N)  DOKUFV                                 94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
S02  C*                  Z-ADD     9             LINNBR
N02  C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
      *________________
      * Display Subfile
      *""""""""""""""""
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     FA10AB20
     C                   ELSE
S02  C*                  WRITE     FA10AB40
S02  C*                  EXFMT     FA10AK60
N02  C                   WRITE     FA10AK60
N02  C                   EXFMT     FA10AB40
     C                   END
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C*
     C                   SETOFF                                       999830
     C*** F3=Exit
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   EXSR      SUMMER
     C     ANTFEL        IFEQ      1                                            VARSEL FØRSTE GANG.
     C     *IN30         CABEQ     '1'           START3                         VIS BILDE PÅ NY
     C                   END
      * AVSLUTT (ENTEN ALLE VERDIRE LIK SOM FØR ELLER ENDRING ER VARSLET..)
S02   * (SKAL VEL RETURNERES UANSETT  - 0 I VERDI ER VEL KUN OM DNE SLETTER LINJER ... )
S02  C*    TOTANT        IFNE      0
S02  C*    TOTVKT        ORNE      0
S02  C*    TOTLM         ORNE      0
S02  C*    TOTLM2        ORNE      0
S02  C*    TOTVOL        ORNE      0
S02  C*    TOTFARL       ORNE      0
     C                   Z-ADD     TOTANT        UANT
     C                   Z-ADD     TOTVKT        UVKT
     C                   Z-ADD     TOTLM         ULM
N02  C                   Z-ADD     TOTLM2        ULM2
     C                   Z-ADD     TOTVOL        UM3
N02  C                   Z-ADD     TOTFARL       UFARL
S02  C*                  END
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
     C*** F5=Refresh
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN05
N01
N01   * På sendingsnivå = kolli-IDs
N01  C     *in13         IFEQ      *ON
N01  C                   MOVE      *ZEROS        NulLin            2 0
N01  C                   MOVE      *BLANK        UVIS              1
N01  C                   CALL      'SYFA10B'
N01  C                   PARM                    UAVD
N01  C                   PARM                    UOPD
N01  C                   PARM                    UFBNR
N01  C                   PARM                    Nullin
N01  C                   PARM                    UVIS
N01  C                   GOTO      START
N01  C                   ENDIF
     C*** F6=New record
     C     *IN06         IFEQ      '1'
s02  C*                  Z-ADD     10            LINNBR
s02  C*                  Z-ADD     27            POSNBR
n02  C                   Z-ADD     24            LINNBR
n02  C                   Z-ADD     07            POSNBR
     C                   MOVEL     'Ny      '    WMODE
     C                   EXSR      CLRSCR
     C                   MOVE      '0'           *IN40
     C                   GOTO      START5
     C                   END                                                    <*IN06
     C*** Roll
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   ADD       13            ZRECNO
     C     ZRECNO        IFGT      YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C                   END                                                    <*IN25
     C*** Get options from screen
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C                   END                                                    <*IN12
     C                   GOTO      START
     C*** New record
     C     START5        TAG
     C                   EXFMT     FA10AB10
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   MOVE      '0'           *IN30
N02  c     *in04         ifeq      '1'
n03  c                   exsr      sokdta
N02  c                   goto      START5
N02  c                   endif
     C*** F12=Cancel
     C     *IN12         IFEQ      '1'
S02  C*                  Z-ADD     9             LINNBR
N02  C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   ELSE
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           START5
S02  C*                  EXSR      OPDMRK                                       Flyttet inn i TESTB1
S02  C*                  EXSR      OPDFARL                                      Flyttet inn i TESTB1
     C                   END                                                    <*IN12
N02  C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
      *
     C     ENDPGM        TAG
      *______________________
      * Regenerer linjenummer
      *""""""""""""""""""""""
     C                   Z-ADD     0             XSLN
     C     DOKKY1        SETLL     DOKUFV
     C     DOKKY1        READE     DOKUFV                                 94
     C     *IN94         DOWEQ     *OFF
     C                   ADD       1             XSLN
     C     FVLINR        IFNE      XSLN
     C     DOKKYM        SETLL     DOKUFM
     C     DOKKYM        READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C                   Z-ADD     XSLN          FMLINR
     C                   UPDATE    DOKUFMR
     C     DOKKYM        READE     DOKUFM                                 33
     C  N33              ENDDO
     C                   Z-ADD     XSLN          FVLINR
     C                   END
     C                   UPDATE    DOKUFVR
     C     DOKKY1        READE     DOKUFV                                 94
     C  N94              ENDDO
N01  C                   CLOSE     SYFA10FA
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*          N40 = NEW
     C*           40 = CHANGE
     C***********************************************
N04   * forpakning oppgitt - dersom definert kode - hent LxBxH ubetinget
N04  c                   if        FVPAKN<>*blanks
N04  c                   exsr      enhsr
N04  c                   endif
     C                   EXSR      TSTSCR
     C     *IN30         CABEQ     '1'           ENDTST
      *
     C     FVVOL         IFEQ      *ZEROS
     C     FVLEN         DIV       100           XWORK1            7 3
     C     FVHOY         DIV       100           XWORK2            7 3
     C     FVBRD         DIV       100           XWORK3            7 3
     C                   MULT(H)   XWORK2        XWORK1
     C     XWORK1        MULT(H)   XWORK3        FVVOL
     C                   MULT      FVANT         FVVOL
     C                   END                                                    <FVVOL
N04  C     FVLM2         IFEQ      *ZEROS
N04  C                   EVAL      FVLM2 = FVLM
N04  C                   ENDIF
     C     *IN40         IFEQ      '1'
     C                   UPDATE    DOKUFVR                              38
     C                   ELSE
     C                   WRITE     DOKUFVR
     C                   END                                                    <*IN40
     C*
N02  C                   EXSR      OPDMRK                                       Flyttet inn i TESTB1
N02  C                   EXSR      OPDFARL                                      Flyttet inn i TESTB1
     C*
     C                   MOVE      FVLINR        SFLINR
     C                   MOVE      FVANT         SFANT
     C                   MOVEL     FVPAKN        SFPAKN
     C                   MOVEL     FVVT          SFVT
     C                   MOVE      FVVKT         SFVKT
     C                   MOVE      FVVOL         SFVOL
N02  C                   EVAL      SFMERK = WSMRK
N02  c                   exsr      LinPoeng
     C     *IN40         IFEQ      '1'
     C                   UPDATE    FA10AH50
     C                   ELSE
     C                   MOVE      FVLINR        XSLN
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     FA10AH50
     C                   END                                                    <*IN40
     C*
     C     ENDTST        ENDSR
     C*
     C***********************************************************
     C     TSTSCR        BEGSR
     C***********************************************************
N02  C                   movea     '0000'        *in(77)
N02  C                   if        WSUNNR <> *blanks
N02  C* UNNR
N02  C     UnNrKeyW      chain     ADRUNNRF                           80
N02  C     *in80         ifeq      '1'
N02  C     WSUNNR        chain     ADRUNNRF                           80
N02  C     *in80         ifeq      '1'
N02  C                   eval      UNBESK = *blanks
N02  C                   MOVE      'JOV1653'     MSGNR
N02  C                   seton                                        30
N02  C                   Z-ADD     25            LINNBR
s12  C*                  Z-ADD     54            POSNBR
N12  C                   Z-ADD     52            POSNBR
N02  C                   GOTO      SLTB
N02  C                   endif
N02   * kommer hit kun når gitt UNNR+EMBG IKKE finnes men det finnes minst en på gitt UNNR
N02   * en og bare en=velg denne....
N02  C     WSUNNR        reade     ADRUNNRF                               33
N02  C     *in33         ifeq      *on
N02  C                   eval      WSEMBG = ADEMBG
N12  C                   eval      WSINDX = ADINDX
N02  C                   else
N02   * det finnes flere - må taste/velge
N02  C                   MOVE      'JOV1654'     MSGNR
N02  C                   seton                                        3090
N02  C                   Z-ADD     25            LINNBR
s12  C*                  Z-ADD     59            POSNBR
N12  C                   Z-ADD     57            POSNBR
N02  C                   GOTO      SLTB
N02  C                   endif
N02  C                   endif
N02   * UNNR/Emb.grp  OK - HENT INFO
N02  C                   eval      UNBESK = ADTEXT
N02  C                   eval      WSFAKT = ADFAKT
N02   * Test kll
N02  C     WSANTK        ifeq      *zeros
N02  C                   MOVE      'JOV1655'     MSGNR
N02  C                   seton                                        3079
N02  C                   Z-ADD     25            LINNBR
N02  C                   Z-ADD     68            POSNBR
N02  C                   GOTO      SLTB
N02  C                   endif
N02   * Test kll
N02  C     WSANTE        ifeq      *zeros
N02  C                   MOVE      'JOV1656'     MSGNR
N02  C                   seton                                        3078
N02  C                   Z-ADD     25            LINNBR
N02  C                   Z-ADD     84            POSNBR
N02  C                   GOTO      SLTB
N02  C                   endif
N02   * FIX L->LTR
N02  C     WSENH         ifeq      'L  '
N02  C                   eval      WSENH = 'LTR'
N02  C                   endif
N02   * FIX K->KGM
N02  C     WSENH         ifeq      'K  '
N02  C                   eval      WSENH = 'KG '
N02  C                   endif
N10   * FIX G->GR
N10  C     WSENH         ifeq      'G  '
N10  C                   eval      WSENH = 'GR '
N10  C                   endif
N02   * Test ENHET
N02  C     WSENH         ifne      'LTR'
N02  C     WSENH         andne     'KG '
N10  C     WSENH         andne     'GR '
N02  C                   MOVE      'JOV1657'     MSGNR
N02  C                   seton                                        3087
N02  C                   Z-ADD     25            LINNBR
s12  C*                  Z-ADD     54            POSNBR
N12  C                   Z-ADD     52            POSNBR
N02  C                   GOTO      SLTB
N02  C                   endif
N05   * Sjekk samlasting mot andre klasser
N11  C     HESTN8        IFNE      'X'
N05  c                   call      'SYGE84L'
N05  c                   parm                    Uavd              4 0
N05  c                   parm                    UOpd              7 0
N05  c                   parm      ADKLAS        UNyKlasse         4
N10  c                   parm      ADSEDD        UNySeddel        10
N05  c                   parm                    ufarSam           1
N05  C     UFARSAM       IFEQ      '1'
N05  C                   MOVE      'JOV1673'     MSGNR
N05  C                   SETON                                        30
N05  C                   Z-ADD     25            LINNBR
N05  C                   Z-ADD     92            POSNBR
N05  C                   GOTO      SLTB
N05  C                   endif
N10  C     UFARSAM       IFEQ      '2'
N10  C                   MOVE      'JOV1676'     MSGNR
N10  C                   SETON                                        30
N10  C                   Z-ADD     25            LINNBR
N10  C                   Z-ADD     92            POSNBR
N10  C                   GOTO      SLTB
N10  C                   endif
N10  C     UFARSAM       IFEQ      '3'
N10  C                   MOVE      'JOV1677'     MSGNR
N10  C                   SETON                                        30
N10  C                   Z-ADD     25            LINNBR
N10  C                   Z-ADD     92            POSNBR
N10  C                   GOTO      SLTB
N10  C                   endif
N11  C                   endif
      *
N02  C                   endif
N02   *
N02  C     SLTB          tag
N02   *
     C                   ENDSR
     C*
     C***********************************************
     C     GETOPT        BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C                   Z-ADD     1             XLIN              2 0
     C     *IN98         DOWEQ     '0'
     C                   READC     FA10AH50                               98
     C     *IN98         IFEQ      '1'
     C   99              MOVE      '0'           *IN98
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
      *
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              2 0
     C     SFVT          IFEQ      TXT(1)
     C                   UPDATE    FA10AH50
     C                   GOTO      ENDGET
     C                   END
S02  C*                  Z-ADD     10            LINNBR
S02  C*                  Z-ADD     27            POSNBR
N02  C                   Z-ADD     24            LINNBR
N02  C                   Z-ADD     07            POSNBR
     C*** 2=Change
     C  N69WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Endre   '    WMODE
     C                   EXSR      CHGST
     C*** F12=Cancel
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <WSNR=2
     C*** 3=Copy
     C  N69WSNR          IFEQ      '3'
S06  C*                  MOVE      ' '           WSNR
S06  C*                  UPDATE    FA10AH50
     C                   MOVEL     'Kopiere '    WMODE
     C                   EXSR      CPYST
     C*** F12=Cancel
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <WSNR=3
     C*** 4=Delete
     C  N69WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Slette  '    WMODE
     C                   EXSR      DLTST
     C*** F12=Cancel
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <WSNR=4
     C*** 5=Display
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Vise    '    WMODE
     C                   UPDATE    FA10AH50
     C     DOKKYV        CHAIN(N)  DOKUFV                             77
     C                   MOVE      '1'           *IN88
s02  C*                  Z-ADD     10            LINNBR
s02  C*                  Z-ADD     27            POSNBR
n02  C                   Z-ADD     24            LINNBR
n02  C                   Z-ADD     07            POSNBR
     C                   EXSR      GETMRK
     C                   EXSR      GETFARL
     C                   EXFMT     FA10AB10
     C*** F12=Cancel
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <WSNR=5
     C*
     C*
     C*** 6=Merke
     C     WSNR          IFEQ      '6'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    FA10AH50
     C* GÅ TIL VARELINJE VEDLIKEHOLD  - merke
     C                   CALL      'SYFA10B'
     C                   PARM                    SFAVD
     C                   PARM                    SFOPD
     C                   PARM                    SFFBNR
     C                   PARM                    SFLINR
     C                   PARM                    UVIS
     C                   END                                                    <WSNR=6
N02  C*** 8=Farlig gods
N02  C     WSNR          IFEQ      '8'
N02  C                   CALL      'SYFA10D'
N02  C                   PARM                    SFAVD
N02  C                   PARM                    SFOPD
N02  C                   PARM                    SFFBNR
N02  C                   PARM                    SFLINR
N02  C                   PARM                    UVIS
N02   * hente farl-verdier
N02  C                   PARM      SFAVD         FVAVD
N02  C                   PARM      SFOPD         FVOPD
N02  C                   PARM      SFFBNR        FVFBNR
N02  C                   PARM      SFLINR        FVLINR
N02  c                   exsr      LinPoeng
N02  C                   MOVE      ' '           WSNR
N02  C                   UPDATE    FA10AH50
N02  C                   END                                                    <WSNR=8
N02  C*** 9=Fakturatillegg
N02  C     WSNR          IFEQ      '9'
N02  C                   CALL      'SYFA10G'
N02  C                   PARM                    SFAVD
N02  C                   PARM                    SFOPD
N02  C                   PARM                    SFFBNR
N02  C                   PARM                    SFLINR
N02  C                   PARM                    UVIS
N02   * hente farl / linjetillegsverdier før uddata av subfil ??
N02  C                   PARM      SFAVD         FVAVD
N02  C                   PARM      SFOPD         FVOPD
N02  C                   PARM      SFFBNR        FVFBNR
N02  C                   PARM      SFLINR        FVLINR
N02  c**                 exsr      LinFaTill
N02  C                   MOVE      ' '           WSNR
N02  C                   UPDATE    FA10AH50
N02  C                   END                                                    <WSNR=8
     C                   ENDDO
      *
     C     ENDGET        TAG
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   ADD       7             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   ENDSR
     C*
     C***********************************************
     C     CHGST         BEGSR
     C***********************************************
     C     DOKKYV        CHAIN     DOKUFV                             77
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN40
     C                   EXSR      GETMRK
     C                   EXSR      GETFARL
     C     STCHG         TAG
     C                   EXFMT     FA10AB10
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   MOVE      '0'           *IN30
N02  c     *in04         ifeq      '1'
N03  c                   exsr      sokdta
N02  c                   goto      STCHG
N02  c                   endif
     C     *IN12         IFEQ      '1'
     C                   EXCEPT    DUMFV
     C                   UPDATE    FA10AH50
     C                   GOTO      CHGEND
     C                   END                                                    <*IN12
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           STCHG
S02  C*                  EXSR      OPDMRK                                       Flyttet inn i TESTB1
S02  C*                  EXSR      OPDFARL                                      Flyttet inn i TESTB1
     C*
     C     CHGEND        ENDSR
     C*
     C***********************************************
     C     CPYST         BEGSR
     C***********************************************
     C     DOKKYV        CHAIN(N)  DOKUFV                             77
     C                   MOVE      '1'           *IN99
     C                   MOVE      '0'           *IN40
N06  C                   MOVE      ' '           WSNR
     C                   UPDATE    FA10AH50
     C     XSLN          ADD       1             FVLINR
     C                   EXSR      GETMRK
     C                   EXSR      GETFARL
     C     STCPY         TAG
     C                   EXFMT     FA10AB10
     C                   MOVE      '0'           *IN30
N02  c     *in04         ifeq      '1'
N02  c                   CALL      'SYFA10D2'
N02  c                   parm                    WSUNNR
N02  c                   parm                    WSEMBG
N12  c                   parm                    WSINDX
N02  c                   if        WSUNNR <> *blanks
N02  C                   Z-ADD     25            LINNBR
N02  C                   Z-ADD     68            POSNBR
N02  c                   endif
N02  c                   goto      STCPY
N02  c                   endif
     C     *IN12         CABEQ     '1'           CPYEND
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           STCPY
S02  C*                  EXSR      OPDMRK
     C*
     C     CPYEND        ENDSR
     C*
     C***********************************************
     C     DLTST         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVEA     '11'          *IN(88)
s09  C*    DOKKYV        CHAIN     DOKUFV                             77
N09  C     DOKKYV        CHAIN(N)  DOKUFV                             77
     C                   EXFMT     FA10AB10
     C                   MOVEA     '00'          *IN(88)
     C     *IN11         IFEQ      '1'
      * SLETTER DOKUFV
s09  C*                  DELETE    DOKUFVR
s09   * SLETTER DOKUFM
s09  C*    DOKKYV        SETLL     DOKUFM
s09  C*    DOKKYV        READE     DOKUFM                                 77
s09  C*    *IN77         DOWEQ     '0'
s09  C*                  DELETE    DOKUFMR
s09  C*    DOKKYV        READE     DOKUFM                                 77
s09  C* N77              ENDDO
s09  C* SLETTER I FARLIG GODS
s09   *    Farlkey01E    SETLL     DOKUFVF
s09  C*    FarlKey01E    READE     DOKUFVF                                77
s09  C*    *IN77         DOWEQ     '0'
s09  C*                  DELETE    DOKUFVFR
s09  C*    FarlKey01E    READE     DOKUFVF                                77
s09  C* N77              ENDDO
      *
      *   SYGE85  sletter og reorganiserer
      *
N08  c                   call      'SYGE85'
N08  C                   parm                    SFAVD
N08  C                   parm                    SFOPD
N08  C                   parm                    SFFBNR
N08  C                   parm                    SFLINR
     C                   MOVE      *BLANKS       SFVT
     C                   MOVEL     TXT(1)        SFVT
     C                   MOVE      *BLANKS       SFPAKN
     C                   MOVE      *ZEROS        SFVOL
     C                   MOVE      *ZEROS        SFVKT
     C                   MOVE      *ZEROS        SFANT
     C                   MOVE      *ZEROS        SFLINR
     C                   END                                                    <*IN11
     C                   UPDATE    FA10AH50
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SUMMER        BEGSR
     C***********************************************
N02  C                   Z-ADD     0             TOTFARL
     C                   Z-ADD     0             TOTANT
     C                   Z-ADD     0             TOTVKT
     C                   MOVE      *ZEROS        TOTLM
N14  C                   MOVE      *ZEROS        TOTLM2
     C                   MOVE      *ZEROS        TOTVOL
     C     DOKKY1        SETLL     DOKUFV
     C     SUMDOK        TAG
     C     DOKKY1        READE(N)  DOKUFV                                 55
     C     *IN55         IFEQ      *OFF
     C                   ADD       FVANT         TOTANT
     C                   ADD       FVVKT         TOTVKT
     C                   ADD       FVLM          TOTLM
N02  C                   ADD       FVLM2         TOTLM2
     C                   ADD       FVVOL         TOTVOL
N02  C                   EXSR      LinPoeng
N02  C                   ADD       SFFARL        TOTFARL
     C                   GOTO      SUMDOK
     C                   END                                                    <*IN55
     C*
     C* Varsel til brukerne ved endringer
     C                   MOVE      '0'           *IN30
     C     TOTANT        IFNE      UANT
N02  C     TOTVKT        ORNE      UVKT
N02  C     TOTLM         ORNE      ULM
N02  C     TOTLM2        ORNE      ULM2
N02  C     TOTVOL        ORNE      UM3
     C                   MOVE      'JOV1628'     MSGNR
s02  C*                  Z-ADD     10            LINNBR
s02  C*                  Z-ADD     27            POSNBR
     C                   ADD       1             ANTFEL
     C                   MOVE      '1'           *IN30
S02  C*                  GOTO      SUMEND
     C                   END                                                    <TOTANT
S02   * samler testene i en - se over..
S02  C*    TOTVKT        IFNE      UVKT
S02  C*                  MOVE      'JOV1628'     MSGNR
S02  C*                  Z-ADD     10            LINNBR
S02  C*                  Z-ADD     27            POSNBR
S02  C*                  ADD       1             ANTFEL
S02  C*                  MOVE      '1'           *IN30
S02  C*                  GOTO      SUMEND
S02  C*                  END                                                    <TOTVKT
      *
     C     SUMEND        ENDSR
      *
     C***********************************************
     C     CLRSCR        BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN40
     C                   CLEAR                   DOKUFVR
     C                   MOVE      'F'           FVRI
     C                   MOVE      UAVD          FVAVD
     C                   MOVE      UOPD          FVOPD
     C                   MOVE      UFBNR         FVFBNR
     C     XSLN          ADD       1             FVLINR
     C                   MOVE      *BLANKS       WSMRK
N05  C                   Move      *blanks       WSUNNR
N05  C                   Move      *blanks       WSEMBG
N12  C                   Move      *blanks       WSINDX
N05  C                   Move      *zeros        WSANTK
N05  C                   Move      *zeros        WSANTE
N05  C                   Move      *blanks       WSENH
N05  C                   Move      *zeros        WSFAKT
N05  C                   Move      *zeros        WSPOEN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     GETMRK        BEGSR
     C***********************************************
     C*
N02  C                   Z-ADD     1             FMMKNR
N02  C                   Z-ADD     1             FMLONR
     C     DOKUMK        CHAIN(N)  DOKUFM                             33
     C  N33              MOVE      FMMRK1        WSMRK
     C   33              MOVE      *BLANKS       WSMRK
     C                   ENDSR
     C***********************************************
     C     OPDMRK        BEGSR
     C***********************************************
     C*
N02  C                   Z-ADD     1             FMMKNR
N02  C                   Z-ADD     1             FMLONR
     C     DOKUMK        CHAIN     DOKUFM                             33
     C     WSMRK         IFEQ      *BLANKS
     C  N33              DELETE    DOKUFMR
     C                   ELSE
     C                   MOVE      WSMRK         FMMRK1
     C   33              MOVE      FVRI          FMRI
     C   33              MOVE      FVAVD         FMAVD
     C   33              MOVE      FVOPD         FMOPD
     C   33              MOVE      FVFBNR        FMFBNR
     C   33              MOVE      FVLINR        FMLINR
     C   33              WRITE     DOKUFMR
     C  N33              UPDATE    DOKUFMR
     C                   END
     C                   ENDSR
     C***********************************************
     C     GETFARL       BEGSR
     C***********************************************
     C*
     C     FarlKey01     CHAIN(N)  DOKUFVF                            33
     C  N33              MOVE      FFUNNR        WSUNNR
     C  N33              MOVE      FFEMBG        WSEMBG
N12  C  N33              MOVE      FFINDX        WSINDX
     C  N33              MOVE      FFANTK        WSANTK
     C  N33              MOVE      FFANTE        WSANTE
     C  N33              MOVE      FFENH         WSENH
     C  N33              MOVE      FFFAKT        WSFAKT
     C  N33              MOVE      FFPOEN        WSPOEN
     C   33              MOVE      *BLANKS       WSUNNR
     C   33              MOVE      *BLANKS       WSEMBG
N12  C   33              MOVE      *BLANKS       WSINDX
     C   33              MOVE      *BLANKS       WSANTK
     C   33              MOVE      *BLANKS       WSANTE
     C   33              MOVE      *BLANKS       WSENH
     C   33              MOVE      *BLANKS       WSFAKT
     C   33              MOVE      *BLANKS       WSPOEN
     C     UnNrKeyW      chain     ADRUNNRF                           33
     C   33              eval      UNBESK = *blanks
     C  N33              eval      UNBESK = ADTEXT
      * hent også totaler (kan vere flere )
N02  c                   exsr      LinPoeng
     C                   ENDSR
     C***********************************************
     C     OPDFARL       BEGSR
     C***********************************************
     C*
     C     FarlKey01     CHAIN     DOKUFVF                            33
     C     WSUNNR        IFEQ      *BLANKS
     C  N33              DELETE    DOKUFVFR
     C                   ELSE
     C     UnNrKeyW      chain     ADRUNNRF
     C   33              MOVE      FVAVD         FFAVD
     C   33              MOVE      FVOPD         FFOPD
     C   33              MOVE      FVFBNR        FFFBNR
     C   33              MOVE      FVLINR        FFLINR
     C   33              MOVE      01            FFLIN2
     C                   MOVE      WSUNNR        FFUNNR
     C                   MOVE      WSEMBG        FFEMBG
N12  C                   MOVE      WSINDX        FFINDX
     C                   MOVE      WSANTK        FFANTK
     C                   MOVE      WSANTE        FFANTE
     C                   MOVE      WSENH         FFENH
     C                   MOVE      WSFAKT        FFFAKT
S15  c*                  eval      FFPOEN = FFANTE * FFFAKT
N15  C                   EVAL      BigNum = FFANTE * FFFAKT
N15  C                   IF        BigNum < 99999
N15  C                   EVAL      FFPOEN=BigNum
N15  C                   else
N15  C                   EVAL      FFPOEN=99999
N15  C                   endif
     C                   MOVE      ADKLAS        FFKLAS
     C                   MOVE      ADSEDD        FFSEDD
     C                   MOVE      ADTRES        FFTRES
     C   33              WRITE     DOKUFVFR
     C  N33              UPDATE    DOKUFVFR
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     LAGLIN        BEGSR
     C***********************************************
      * OM IKKE LINJE (MINST EN) FINNES - SKAP BASERT PÅ VERDIER FRA OPPDRAGET
     C     DOKKY1        SETLL     DOKUFV                             93
     C     DOKKY1        READE     DOKUFV                                 93
     C   93              DO
     C                   Z-ADD     1             FVLINR
     C                   Z-ADD     UANT          FVANT
     C                   MOVEL     XPAKN         FVPAKN
     C                   Z-ADD     UVKT          FVVKT
     C                   Z-ADD     ULM           FVLM
N02  C                   Z-ADD     ULM2          FVLM2
     C                   Z-ADD     UM3           FVVOL
     C                   MOVEL     XVT           FVVT
     C                   WRITE     DOKUFVR
N01
N01  C     UGM           IFNE      *BLANKS
N02  C                   Z-ADD     1             FMMKNR
N02  C                   Z-ADD     1             FMLONR
N01  C     DOKUMK        CHAIN     DOKUFM                             33
N01  C     *IN33         IFEQ      *ON
N01  C                   MOVEL     UGM           FMMRK1
N01  C                   MOVE      FVRI          FMRI
N01  C                   MOVE      FVAVD         FMAVD
N01  C                   MOVE      FVOPD         FMOPD
N01  C                   MOVE      FVFBNR        FMFBNR
N01  C                   MOVE      FVLINR        FMLINR
N01  C                   WRITE     DOKUFMR
N01  C                   ENDIF
N01  C                   ENDIF
     C                   END
      *
     C                   ENDSR
      *
N02  C***********************************************
N02  C     LinPoeng      BEGSR
N02  C***********************************************
N02   *
N02  c                   move      *off          *in70
N02  c                   move      *zeros        SFFARL
N02  C     FarlKey       SETLL     DOKUFVF
N02  C     FarlKey       READE(N)  DOKUFVF                                33
N02  C     *in33         DOWEQ     '0'
N02  c                   if        SFFARL <> *zeros
N02  c                   move      *On           *in70                          Mer enn 1
N02  c                   endif
S15  c*                  add       FFPOEN        SFFARL
N15  C                   EVAL      BigNum = SFFARL + FFPOEN
N15  C                   IF        BigNum < 99999
N15  C                   EVAL      SFFARL=BigNum
N15  C                   else
N15  C                   EVAL      SFFARL=99999
N15  C                   endif
N02  C     FarlKey       READE(N)  DOKUFVF                                33
N02  c                   enddo
N02   *
N02  C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFBNR             3 0
     C                   PARM                    UANT              7 0
     C                   PARM                    UVKT              9 0
     C                   PARM                    ULM               4 2
N02  C                   PARM                    ULM2              4 2
     C                   PARM                    UM3               7 3
N02  C                   PARM                    UFARL             5 0
     C                   PARM                    UGM              12
     C                   PARM                    UGM2             12
     C                   PARM                    UCMN              1
     C     UVSINN        PARM                    UVS              25
     C                   PARM                    UVIS              1
     C     UVIS          COMP      'J'                                    69
      *
N02  C                   Z-ADD     0             TOTFARL           5 0
     C                   Z-ADD     0             TOTANT            9 0
     C                   Z-ADD     0             TOTVKT            9 0
     C                   Z-ADD     0             TOTLM             4 2
N02  C                   Z-ADD     0             TOTLM2            4 2
     C                   Z-ADD     0             TOTVOL            7 3
     C                   Z-ADD     0             ANTFEL            1 0
     C                   Z-ADD     UAVD          SFAVD
     C                   Z-ADD     UOPD          SFOPD
     C                   Z-ADD     UFBNR         SFFBNR
     C                   MOVE      'F'           SFRI              1
     C                   MOVE      UAVD          FVAVD
     C                   MOVE      UOPD          FVOPD
     C                   MOVE      UFBNR         FVFBNR
     C                   MOVE      'F'           FVRI
     C*
N11  C     HEAKEY        KLIST
N11  C                   KFLD                    UAVD
N11  C                   KFLD                    UOPD
N11  C     HEAKEY        CHAIN     HEADF
     C*--------------------------------------------------------------
     C* DEFINE KEY FOR DOKUFV
     C*--------------------------------------------------------------
     C     DOKKY1        KLIST
     C                   KFLD                    SFRI
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPD
     C                   KFLD                    SFFBNR
     C     DOKKYV        KLIST
     C                   KFLD                    SFRI
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPD
     C                   KFLD                    SFFBNR
     C                   KFLD                    SFLINR
     C     DOKKYM        KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    FVAVD
     C                   KFLD                    FVOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    XSLN
     C     DOKUMK        KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    FVAVD
     C                   KFLD                    FVOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
     C                   KFLD                    FMMKNR
     C                   KFLD                    FMLONR
     C                   Z-ADD     1             FMMKNR
     C                   Z-ADD     1             FMLONR
N02  C     FarlKey       KLIST
N02  C                   KFLD                    FVAVD
N02  C                   KFLD                    FVOPD
N02  C                   KFLD                    FVFBNR
N02  C                   KFLD                    FVLINR
N02  C     FarlKey01     KLIST
N02  C                   KFLD                    FVAVD
N02  C                   KFLD                    FVOPD
N02  C                   KFLD                    FVFBNR
N02  C                   KFLD                    FVLINR
N02  C                   KFLD                    XXLINR01
N02  C                   z-add     1             XXLINR01          2 0
s09  C*    FarlKey01E    KLIST
s09   *                  KFLD                    FVAVD
s09  C*                  KFLD                    FVOPD
s09  C*                  KFLD                    FVFBNR
s09  C*                  KFLD                    FVLINR
     C     UnNrKeyW      KLIST
     C                   KFLD                    WSUNNR
N02  C                   KFLD                    WSEMBG
N12  C                   KFLD                    WSINDX
     C*
     C     *LIKE         DEFINE    FVLINR        XSLN
     C                   EXSR      LAGLIN
N01  C* KALT KUN FOR Å LAGE LINJE 1 - IKKE VIS BLIDE/AVSLUTT NÅ
N01  C     UVIS          IFEQ      'L'
N01  C                   MOVE      '1'           *INLR
N01  C                   RETURN
N01  C                   ENDIF
      *
     C                   ENDSR
N03  C***********************************************
N03  C     ENHSR         BEGSR
N03  C***********************************************
N03  C     FVPAKN        chain     enhetf                             34
N03  c     *in34         ifeq      '0'
N13  C     FVVT          IFEQ      *BLANKS
N04  C                   Movel     ENTEXT        FVVT
N13  C                   END
N03  c                   Z-add     ENLEN         FVLEN
N03  C                   Z-add     ENBRD         FVBRD
N03  C                   Z-add     ENHOY         FVHOY
N03  C     ENLM          ifne      0
N03  C                   Z-add     ENLM          FVLM
N03  C                   mult      fvant         FVLM
N03  c                   end
N03  C     ENLM2         ifne      0
N03  C                   z-add     ENLM2         FVLM2
N03  C                   mult      fvant         FVLM2
N03  C                   end
N03  C                   end
N03  C                   endsr
N03  C***********************************************
N03  C     SOKDTA        BEGSR
N03  C***********************************************
N03  c     linnbr        ifeq      25
N03  c                   CALL      'SYFA10D2'
N03  c                   parm                    WSUNNR
N03  c                   parm                    WSEMBG
N12  c                   parm                    WSINDX
N03  c                   if        WSUNNR <> *blanks
N03  C                   Z-ADD     25            LINNBR
N03  C                   Z-ADD     68            POSNBR
N03  c                   endif
N03  c                   endif
      *
N03  c     linnbr        ifeq      24
N03  C                   MOVE      'P'           UMODE             1
N03  C                   CALL      'SYTR27'
N03  C                   PARM                    UMODE
N03  C                   PARM                    FVPAKN
N03  C                   exsr      enhsr
N03  C                   Z-ADD     24            LINNBR
N03  C                   Z-ADD     36            POSNBR
N03  c                   endif
      *
N03  C                   endsr
      *
      *
     ODOKUFVR   E            DUMFV
**
*** SLETTET ***
