     FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FarkivP    O  A E           K DISK
     FDKEH      IF   E           K DISK
     FDKEV      IF   E           K DISK
     FDKEA      IF   E           K DISK
     FDKE006F2  UF A E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FFIRM      IF   E             DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FDKTHA     IF   E           K DISK
     FDKTVK     IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FDKE006P   O    E             PRINTER USROPN
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  ØN154                  1     15  4
     D  ØA154H                 1     11
     D  ØA154X                 7     11
     D  ØA154D                12     15
     D                 DS
     D  ØA10                   1     10
     D  Ø10                    1     10
     D                                     DIM(10)                              Num edit
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVIHASYAV         4
     C                   PARM                    UVIHASYOP         7
     C                   PARM                    CALLTYP           1
     C                   MOVE      UVIHASYAV     UVIH_SYAV
     C                   MOVE      UVIHASYOP     UVIH_SYOP
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    DKEH_SYAV     UVIH_SYAV
     C     *LIKE         DEFINE    DKEH_SYOP     UVIH_SYOP
     C     *LIKE         DEFINE    KOPUNI        UOPUNI
     C     *LIKE         DEFINE    KOPAVD        UOPAVD
     C     *LIKE         DEFINE    KOPLNR        UOPLNR
     C     *LIKE         DEFINE    KFMSD         UFMSD1
     C     *LIKE         DEFINE    KFMSD         UFMSD3
     C     *LIKE         DEFINE    KOAUNI        WKOAUNI
     C     *LIKE         DEFINE    DKEH_222      ZVIH_222
     C     *LIKE         DEFINE    DKVK_KD       ZVVK_KD
     C     *LIKE         DEFINE    DKVK_DTS      ZVVK_DTS
     C     *LIKE         DEFINE    FSKODE        WSKODE
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVIH_SYAV
     C                   KFLD                    UVIH_SYOP
     C     KEYP          KLIST
     C                   KFLD                    UOPUNI
     C                   KFLD                    UOPAVD
     C                   KFLD                    UOPLNR
     C     KEYF1         KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD1
     C     KEYF3         KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD3
     C     KEYVX         KLIST
     C                   KFLD                    DKEV_SYAV
     C                   KFLD                    DKEV_SYOP
     C                   KFLD                    DKEV_SYLI
     C     KEYKODTA      KLIST
     C                   KFLD                    WKOAUNI
     C                   KFLD                    DKEH_SYAV
     C     KEYCUND       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KOAKNR
     C     DKTVKKEY      KLIST
     C                   KFLD                    ZVVK_KD
     C                   KFLD                    ZVVK_DTS
     C     FRIKEY        KLIST
     C                   KFLD                    DKEH_SYAV
     C                   KFLD                    DKEH_SYOP
     C                   KFLD                    WSKODE
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C                   EVAL      UOPUNI='P'
     C                   EVAL      UOPAVD=UVIH_SYAV
     C                   EVAL      UOPLNR=251
     C                   EVAL      UFMSD1=1
     C                   EVAL      UFMSD3=3
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD           1024
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   EVAL      WKOAUNI='A'
     C                   MOVEL     '*CR'         WSKODE
      *______________________________________
      * Hente firmaopplysninger fra spedisjon
      *""""""""""""""""""""""""""""""""""""""
     C                   READ      FIRM
      *_____________________
      * Hente printerstyring
      *"""""""""""""""""""""
     C     KEYP          CHAIN     KODTP                              33
      *_______________
      * Printerstyring ikke ved arkiv
      *"""""""""""""""
     C     calltyp       ifne      'A'
     C     koplas        andne     'P'
     C     koplas        andne     'F'
     C                   OPEN      DKE006P
     C                   EVAL      UCMD='OVRPRTF FILE(DKE006P) OUTQ('
      *
     C     UCMD          CAT       KOPNVN:0      UCMD
     C     UCMD          CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     c                   end
      *
      * PDF vdirekte print
     C     KOPLAS        IFEQ      'P'
     C     KOPLAS        oreq      'F'
     C     calltyp       ifne      'A'
     C                   CALL      'DKE006C'
     C                   PARM                    KOPNVN
     C                   OPEN      DKE006P
     c                   end
     C                   END
      * arkivering
     C     calltyp       ifeq      'A'
     C                   CALL      'DKE006CA'
     C                   OPEN      DKE006P
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Start av file                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     DKEH

      *_________
      * Huvudark
      *"""""""""
      * Hur många varuposter finnes ?
     C                   EVAL      WRUBR5=0
     C                   EVAL      *IN52=*OFF
     C     KEYA          SETLL     DKEV
     C     KEYA          READE     DKEV                                   52
     C     *IN52         DOWEQ     *OFF
     C                   ADD       1             WRUBR5
     C     KEYA          READE     DKEV                                   52
     C                   ENDDO
      * 1 Varupost
     C                   IF        WRUBR5<2
     C                   EXSR      SRBA
     C                   ENDIF
      * Flera varuposter
     C                   IF        WRUBR5>1
     C                   EXSR      SRBB
     C                   ENDIF
      * Angivelsesart i text
     C                   SELECT
     C     DKEH_AART     WHENEQ    '20'
     C                   EVAL      WKEH_AART='AlU '
     C     DKEH_AART     WHENEQ    '21'
     C                   EVAL      WKEH_AART='FoU '
     C     DKEH_AART     WHENEQ    '22'
     C                   EVAL      WKEH_AART='Sup '
     C     DKEH_AART     WHENEQ    '23'
     C                   EVAL      WKEH_AART='SupH'
     C     DKEH_AART     WHENEQ    '24'
     C                   EVAL      WKEH_AART='FuaA'
     C     DKEH_AART     WHENEQ    '25'
     C                   EVAL      WKEH_AART='FuaF'
     C     DKEH_AART     WHENEQ    '26'
     C                   EVAL      WKEH_AART='Pro '
     C     DKEH_AART     WHENEQ    '27'
     C                   EVAL      WKEH_AART='ProM'
     C     DKEH_AART     WHENEQ    '28'
     C                   EVAL      WKEH_AART='Exs '
     C     DKEH_AART     WHENEQ    '30'
     C                   EVAL      WKEH_AART='IE507'
     C     DKEH_AART     WHENEQ    '40'
     C                   EVAL      WKEH_AART='YMFuaGo'
     C     DKEH_AART     WHENEQ    '41'
     C                   EVAL      WKEH_AART='YMFuaAo'
     C                   OTHER
     C                   EVAL      WKEH_AART=DKEH_AART
     C                   ENDSL
      * Cat av R.30
     C                   MOVEL     DKEH_301      WKEH_30
     C     WKEH_30       CAT       DKEH_302:0    WKEH_30
     C     WKEH_30       CAT       DKEH_303:0    WKEH_30
     C     WKEH_30       CAT       DKEH_304:0    WKEH_30
      * Cat av avsender postnr + by
     C                   MOVEL     DKEH_02D      WKEH_02D
     C     WKEH_02D      CAT       DKEH_02E:1    WKEH_02D
      * Cat av modtager postnr + by
     C                   MOVEL     DKEH_08D      WKEH_08D
     C     WKEH_08D      CAT       DKEH_08E:1    WKEH_08D
      * Hent opplysninger vedr R14 Represæntant
     C     UVIH_SYAV     CHAIN     DKEA                               55
     C                   MOVEL     DKEA_14C      WKEH_14C
     C                   MOVEL     DKEA_14D      WKEH_14D
     C                   MOVEL     DKEA_14E      WKEH_14E
     C     WKEH_14E      CAT       DKEA_14F:1    WKEH_14E
      * kunderef REFERANSE ISTEDEN FOR MOTTAKERNAVN
     C     FRIKEY        CHAIN     FRISOK                             39
     C  N39              MOVEL     FSSOK         WKEH_XREF
      * R54
     C     DKEH_SYSG     CHAIN     DKTHA
     C                   MOVEL     DKTH_NAMN     WKEH_54C
      * Skriv huvudark
     C     KEYKODTA      CHAIN     KODTA
     C     KEYCUND       CHAIN     CUNDL01
     C                   EVAL      WKEH_54A=ADR3
     C     KEYF1         CHAIN     KODTFM
     C                   WRITE     DKE006PA
      * 1 kopi ved arkiv
     C     calltyp       ifne      'A'
     C     KEYF3         CHAIN     KODTFM
     C                   WRITE     DKE006PA
     C                   end
     C     calltyp       ifeq      'C'
     C                   EXSR      ARKSR
     C                   end
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Varuposter, en                                      SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      * Delete av gamla arbetsposter i arbetsfil 2
     C                   EVAL      *IN52=*OFF
     C     KEYA          SETLL     DKE006F2
     C     KEYA          READE     DKE006F2                               52
     C     *IN52         DOWEQ     *OFF
     C                   DELETE    DKE006F2R
     C     KEYA          READE     DKE006F2                               52
     C                   ENDDO
      * Init av workfile
     C                   CLEAR                   DKE006F2R
     C                   EVAL      DKE006F2_A=DKEH_SYAV
     C                   EVAL      DKE006F2_O=DKEH_SYOP
      * R31 Godsmærkning
     C     DKEV_311      IFNE      *BLANKS
     C                   EVAL      DKE006F2_T=DKEV_311
     C                   WRITE     DKE006F2R
     C                   ENDIF
      * R31 Antall, Kollislag och varubeskrivning
     C                   EVAL      DKE006F2_T=*BLANKS
     C                   EVAL      ØN154=DKEV_313
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           DKE006F2_T
     C     DKE006F2_T    CAT       DKEV_314:1    DKE006F2_T
     C     DKE006F2_T    CAT       DKEV_315:1    DKE006F2_T
     C                   WRITE     DKE006F2R

N02   * R31 Conatinernummer
N02  C     DKEV_312A     IFNE      *BLANKS
N02  C                   EVAL      DKE006F2_T=*BLANKS
N02  C                   MOVEL     DKEV_312A     DKE006F2_T
N02  C                   WRITE     DKE006F2R
N02  C                   ENDIF
N02  C     DKEV_312B     IFNE      *BLANKS
N02  C                   EVAL      DKE006F2_T=*BLANKS
N02  C                   MOVEL     DKEV_312B     DKE006F2_T
N02  C                   WRITE     DKE006F2R
N02  C                   ENDIF
N02  C     DKEV_312C     IFNE      *BLANKS
N02  C                   EVAL      DKE006F2_T=*BLANKS
N02  C                   MOVEL     DKEV_312C     DKE006F2_T
N02  C                   WRITE     DKE006F2R
N02  C                   ENDIF
N02  C     DKEV_312D     IFNE      *BLANKS
N02  C                   EVAL      DKE006F2_T=*BLANKS
N02  C                   MOVEL     DKEV_312D     DKE006F2_T
N02  C                   WRITE     DKE006F2R
N02  C                   ENDIF
N02  C     DKEV_312E     IFNE      *BLANKS
N02  C                   EVAL      DKE006F2_T=*BLANKS
N02  C                   MOVEL     DKEV_312E     DKE006F2_T
N02  C                   WRITE     DKE006F2R
N02  C                   ENDIF
      * Læs arbetsfil och lægg ut till fælt i printerfil R31
     C                   EVAL      *IN52=*OFF
     C     KEYA          SETLL     DKE006F2
     C     KEYA          READE     DKE006F2                               52
     C     *IN52         DOWEQ     *OFF
      * R31 rad 1
     C                   IF        WRUBR31A=*BLANKS
     C                   EVAL      WRUBR31A=DKE006F2_T
     C                   GOTO      SRBA1
     C                   ENDIF
      * R31 rad 2
     C                   IF        WRUBR31B=*BLANKS
     C                   EVAL      WRUBR31B=DKE006F2_T
     C                   GOTO      SRBA1
     C                   ENDIF
      * R31 rad 3
     C                   IF        WRUBR31C=*BLANKS
     C                   EVAL      WRUBR31C=DKE006F2_T
     C                   GOTO      SRBA1
     C                   ENDIF
      * R31 rad 4
     C                   IF        WRUBR31D=*BLANKS
     C                   EVAL      WRUBR31D=DKE006F2_T
     C                   GOTO      SRBA1
     C                   ENDIF
      * R31 rad 5
     C                   IF        WRUBR31E=*BLANKS
     C                   EVAL      WRUBR31E=DKE006F2_T
     C                   GOTO      SRBA1
     C                   ENDIF
      * R31 rad 6
     C                   IF        WRUBR31F=*BLANKS
     C                   EVAL      WRUBR31F=DKE006F2_T
     C                   GOTO      SRBA1
     C                   ENDIF
      * R31 rad 7
     C                   IF        WRUBR31G=*BLANKS
     C                   EVAL      WRUBR31G=DKE006F2_T
     C                   GOTO      SRBA1
     C                   ENDIF
      * R31 rad 8
     C                   IF        WRUBR31H=*BLANKS
     C                   EVAL      WRUBR31H=DKE006F2_T
     C                   GOTO      SRBA1
     C                   ENDIF

     C     SRBA1         TAG

     C     KEYA          READE     DKE006F2                               52
     C                   ENDDO
      * R33
     C                   MOVEL     DKEV_331      WRUBR33A
     C                   MOVE      DKEV_331      WRUBR33B
     C                   EVAL      WRUBR33C=DKEV_332
     C                   EVAL      WRUBR34=DKEV_34A
     C                   EVAL      WRUBR35=DKEV_35
     C*                  EVAL      WRUBR36=DKEV_36
     C                   MOVEL     DKEV_37       WRUBR37A
     C                   MOVE      DKEV_37       WRUBR37B
     C                   EVAL      WRUBR38=DKEV_38
     C*                  EVAL      WRUBR39=DKEV_39
     C                   EVAL      WRUBR40=*BLANKS
     C                   MOVEL     DKEV_401A     WRUBR40
     C     WRUBR40       CAT       DKEV_402A:0   WRUBR40
     C     WRUBR40       CAT       DKEV_403A:1   WRUBR40
     C                   EVAL      WRUBR41=DKEV_412
     C                   IF        WRUBR41=999999999
N05  C                   EVAL      WRUBR41=*ZEROS
     C                   ENDIF
     C                   EVAL      WRUBR42=DKEV_42
     C                   EVAL      WRUBR44A=DKEV_446A
N02  C     WRUBR44A      CAT       DKEV_446B:1   WRUBR44A
     C                   EVAL      WRUBR46=DKEV_46
      * Finnes extra R44 ?     Kun 3 av 10 får plass
     C                   MOVEL     DKEV_446C     WRUBR44B
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Varuposter, flera                                   SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      * Skriv ut varuposter på egen lista
     C                   CALL      'DKE007R'
     C                   PARM                    UVIHASYAV
     C                   PARM                    UVIHASYOP
     C                   PARM                    CALLTYP
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA10
     C     ØA154D        IFEQ      '0000'                                       <-------------I
     C                   MOVE      ØA154H        ØA10                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA154X        ØA10                                         I
     C                   MOVE      ',    '       ØA10                                         I
     C                   MOVE      ØA154D        ØA10                                         I
     C     Ø10(10)       IFEQ      '0'
     C                   MOVE      ' '           Ø10(10)
     C     Ø10(9)        IFEQ      '0'
     C                   MOVE      ' '           Ø10(9)
     C                   END
     C                   END
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        9             ØX                             <-------------I
     C     Ø10(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø10(ØX)       ØAN
      *
     C                   ENDSR
      *
      ***********************************************
     C     ARKSR         BEGSR
      ***********************************************
      *
      * SKRIV TIL ARKIV
      *
      * SKAL DOKUMENT ARKIVERES
      *
     C                   MOVE      'DE'          ARTYPE
     C     FIFIRM        CHAIN     FIRMARC                            34
     C  N34ARTYPE        CHAIN     ARKTXT                             34
     C     *IN(34)       IFEQ      '0'
     C     arkjn         andeq     'J'
      *
n04  C                   z-add     DKEH_AVKN     ARKUND
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      FIFIRM        ARFIRM
     C                   MOVE      UVIH_SYAV     ARAVD
S03  C*                  MOVE      *zeros        AROPD
N03  C                   MOVE      UVIH_SYOP     AROPD
     C                   MOVE      ULDT          ARDATE
     C                   MOVE      XULTM         ARTIME
     C                   MOVE      ZUSER         ARUSER
     C                   MOVE      UVIH_SYOP     ARUNDE
     C                   movel     'DE:'         ARUNDE
     C                   WRITE     ARKIVR
      *_________________________
      * Aktivera arkivfunktionen  FJERNES SENERE
      *"""""""""""""""""""""""""
     C     DKEH_SYSG     CHAIN     DKTHA
     C                   CALL      'ARC002CLB'
     C                   PARM      'DE'          WATYPE            2
     C                   PARM                    ARUSER
     C                   END
     C                   ENDSR
