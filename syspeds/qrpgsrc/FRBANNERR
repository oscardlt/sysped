      *********************************************************************
      *  RPG ILE MODULE SYSPED/FRBANNERR
      *
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/FRBANNERR MODULE(*LIBL/FRBANNERR)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *=====================================================================
      * Includes to be used in all CGIs
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      *  Fields used for parsing the input string
     D action          s             10a
     D subaction       s             10a
      * aktiv tur...
     D Tur             s              8a
     D Turnr           s              8s 0
      * aktiv avd...
     D Avd             s              4a
     D Avdnr           s              4s 0
     D lng             s              2a
      *--------------------------------------------------------------------
      *  Other program variables and constants
      *=====================================================================
      * Read remote browser input string and parse it
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prolog3
     C                   exsr      Parse
      *=====================================================================
      * Main line
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
     C                   callp     gethtml('HTMLSRC':'*LIBL':  'FRBANNERR':
     C                             '/CGITAG/')
      *------------------
      * Action requested
     C                   eval      action = uppify(action)
     C                   eval      subaction = uppify(subaction)
      * Start html
     C                   callp     wrtsection('top')
      * Gjeldende tur/avdeling
     C                   callp     updHTMLvar('TUR':
     C                             %trim(%editc(TurNr:'Z')))
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(AvdNr:'Z')))
      * Vis kun når utfylt / men param finnes også som hidden
     C     Turnr         ifne      *zeros
     C     Avdnr         orne      *zeros
     C                   callp     wrtsection('gjeldende')
     C                   end
      *    buttons
     C                   callp     wrtsection('buttons')
      * If 'FRCNT02'
     C                   IF        action = 'FRCNT02'
     C                   if        TurNr  > 0
     C                   callp     wrtsection('dspcart')
     C                   callp     wrtsection('finish')
     C                   endif
     C                   callp     wrtsection('giveup')
     C                   ENDIF
      * If 'FRCNT03'
     C                   IF        action = 'FRCNT03'
     C                   callp     wrtsection('buymore')
     C                   callp     wrtsection('finish')
     C                   callp     wrtsection('giveup')
     C                   ENDIF
      * If 'FRCNT04'=Til kassa   (bruke som avslutt/godsliste)
     C                   IF        action='FRCNT04' and subaction<>'EPILOG'
     C                   callp     wrtsection('dspcart')
     C                   callp     wrtsection('buymore')
     C                   callp     wrtsection('giveup')
     C                   ENDIF
      * End html
     C                   callp     wrtsection('end')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   return
      *=====================================================================
      * Parse the input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      action     = zhbgetvar('action    ')
     C                   eval      subaction  = zhbgetvar('subaction ')
     C                   eval      lng        = zhbgetvar('lng       ')
     C                   eval      Avd        = zhbgetvar('AVD       ')
     C                   eval      AvdNr      = c2n2(Avd)
     C                   eval      Tur        = zhbgetvar('TUR       ')
     C                   eval      Turnr      = c2n2(tur)
     C                   endsr
