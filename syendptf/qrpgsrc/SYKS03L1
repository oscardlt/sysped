********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF11 R=reklamer / ikke O=opphev
12XXX * 02 SH  PTF12 half ajust valutaomregning
12XXX * 03 SH  PTF12 WSPF IKKE BLANK
********************************************************************
********************************************************************
      * Kontering bilag utenom trafikk
      * RETTINGER I DETTE PROGRAM:
      *
      *
N01  FARKLOGH4  IF   E           K DISK
N01  FTRACKL08  O  A E           K DISK
     FKODTÅ     IF   E           K DISK
     FNAVLEV    IF   E           K DISK
     FSYPARL2   IF   E           K DISK
     FKOSTT     UF   E           K DISK
     FVALUL01   IF   E           K DISK
     FKONTL01   IF   E           K DISK
     FKODIL01   IF   E           K DISK
     FKODJL01   IF   E           K DISK
     FPROSL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FLEVEL01   IF   E           K DISK
     FKOSTA     IF   E           K DISK
     FKOSTA14   IF   E           K DISK
     F                                     RENAME(KOSTAR:KOSTARU)
     FKOSTB     UF   E           K DISK
     F                                     INFDS(KOSTS)
     FKOSTBUPD  UF A E             DISK
     F                                     INFDS(KOSTSU)
     F                                     RENAME(KOSTBR:KOSTBRU)
     FSYKS03L1FMCF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D KOSTS           DS
     D  RRNP                 397    400B 0
     D KOSTSU          DS
     D  RRNPU                397    400B 0
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      * CURSOR LOKASJON AV AKTIV VINDU
     D  WSCLOW               382    383B 0
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  UUSER                254    263
     D                 DS
     D  MVTAB                  1     10
     D                                     DIM(10)                              I/O. frankatur
     D  MVAO                   1      1
     D  MVAOK                  2     10
     D                 DS
     D  kbrefc                 1     35
     D  fastko                 1      3
     D  kbrefcko               4      8  0
     D  fastav                 9     11
     D  kbrefcav              12     15  0
     D  fastba                16     18
     D  kbrefcba              19     22  0
     D  fastpr                23     25
     D  kbrefcpr              26     33  0
      *
      * fakturadato
      *
     D                 DS
     D  KAFDT                  1      8  0
     D  KAFDTÅ                 1      4  0
     D  KAFDTM                 5      6  0
     D  KAFDTD                 7      8  0
     D                 DS
     D  DATOFA                 1      6  0
N01  D  DATOAA                 1      6
     D  WSDDF                  1      2  0
     D  WSMMF                  3      4  0
     D  WSAAF                  5      6  0
      *
      * forfallsdato
      *
     D                 DS
     D  KAFFDT                 1      8  0
     D  KAFFDÅ                 1      4  0
     D  KAFFDM                 5      6  0
     D  KAFFDD                 7      8  0
     D                 DS
     D  DATOFO                 1      6  0
     D  WSDDFF                 1      2  0
     D  WSMMFF                 3      4  0
     D  WSAAFF                 5      6  0
      *
      * dobbeldefineringer
      *
     D                 DS
     D  KAVAL                  1      3
     D  KAVAL2                 1      3
     D                 DS
     D  KABL                   1     13  2
     D  KABL2                  1     13  2
N01  D                 DS
N01  D  XULTID                 1     14  0
N01  D  XULTM                  1      6  0
N01  D  XULDD                  7      8  0
N01  D  XULMM                  9     10  0
N01  D  XULÅÅ                 11     14  0
N01  D                 DS
N01  D  ULÅÅ                   1      4  0
N01  D  ULMM                   5      6  0
N01  D  ULDD                   7      8  0
N01  D  ULDT                   1      8  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * summer totaler
      *"""""""""""""
     c                   exsr      sumsr
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   z-add     6             LINNBR            3 0
     C                   z-add     3             POSNBR
      *
      * SPØRRING
      *
     C     *IN29         IFEQ      '1'
     C                   z-add     1             LINNBR            3 0
     C                   z-add     2             POSNBR
     C                   END
     c                   z-add     0             forste            5 0
      *
     C     KEYFIL        SETLL     KOSTB
     C     START1        TAG
     C     KEYFIL        READE(N)  KOSTB                                  94
      * kun uattersterte
     c  N94UATT          IFEQ      'J'
     c     kbsgg         andne     *blanks
     c                   goto      start1
     c                   end
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   z-add     RRNP          SISTE            10 0
      * omregn til valuta
     c     kbblhb        div       kavku         work154          15 4
s02  c*    work154       mult      kaomrf        kbblva
n02  c     work154       mult(H)   kaomrf        kbblva
      *
      * sett protekt hvis andre har attestert
      *
     c                   setoff                                       28
     c     kbsgg         ifne      uusg
     c     kbsgg         comp      *blanks                            2828
     c                   end
N01  c     kbrekl        comp      *blanks                            79
     c     *in28         ifeq      '1'
     c     forste        andeq     0
     c                   z-add     ZRECNO        forste
     c                   end
      *
     C                   WRITE     SUBFIL
     c     lesny         tag
     C     KEYFIL        READE(N)  KOSTB                                  94
      * kun uattersterte
     c  N94UATT          IFEQ      'J'
     c     kbsgg         andne     *blanks
     c                   goto      lesny
     c                   end
     C  N94              END
     C                   END
     C*
     c                   add       forste        linnbr
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       98
      *
     C                   MOVE      *BLANKS       MODE              5
      *
      *** AVSLUTT
      *
     C     ST41          TAG
     C   03              GOTO      SLUTT
      *
      *   FRISK OPP
      *
     C   05              GOTO      START
      *
      *  06   LAGE NY POST
      *
     C     *IN06         IFEQ      '1'
     c                   EVAL      MODE='NY   '
     C                   EXSR      MOVB2
     c                   EXSR      VARELISR
     C  N30              exsr      NYSR
     c                   GOTO      START3
     c                   end
      *___________________
      * F18=opphev attestasjon
      *"""""""""""""""""""
     C     *IN18         IFEQ      '1'
     C                   EXSR      CMD18
     C                   GOTO      START
     C                   ENDIF
      *
      *  16   made kun uattesterte
      *
     C     *IN16         IFEQ      '1'
     C     UATT          IFEQ      'J'
     C                   MOVE      'N'           UATT              1
     C                   ELSE
     C                   MOVE      'J'           UATT
     C                   end
     c                   goto      start
     c                   end
      *____________________________________
      * F10=Ferdigmeld (kan ikke vÆre diff)
      *""""""""""""""""""""""""""""""""""""
     C     *IN10         IFEQ      '1'
     C     DIFF          ANDEQ     0
     C                   EXSR      F10test
     C   30              goto      start3
      *
      * ENDELIG BILAGSNR MÅ VÆRE TILDELT VED F10
      *
     C     UBNR2         IFEQ      *ZEROS
     C                   EXSR      NYTTBILN
     C     *IN12         IFEQ      '1'
     C                   GOTO      START3
     C                   ENDIF
     C                   ENDIF
      *
      * SETT BRUKER SOM ATTESTERER
      *
     C                   EXSR      ATTSR
      *FERDIGMELDING VELLYKKET - SETT FLAGG (TIL SYKS02)
      *
     C                   Z-ADD     9             UFLAGG
     C                   GOTO      SLUTT
     C                   ENDIF
      *
      *** BLA VIDERE
      *
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KEYFIL        READE(N)  KOSTB                                  94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   EXSR      HENT
     C                   GOTO      START
     C                   END
      *
      *** SØK FRA
      *
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     c                   setoff                                       307273
     c                   setoff                                       747576
     C                   z-add     21            LINNBR            3 0
     C                   z-add     12            POSNBR
      *
      * kontonummer
      *
     c     kontkey       chain     kontl01                            71
     c     *in71         ifeq      '1'
     c                   move      'SPK0022'     msgnr
     c                   seton                                        30
     c                   end
     c
     C   71              z-add     45            POSNBR
     C   71              goto      sltb
      *
      * ksted
      *
     c     wskste        ifne      0
     c     kodjke        chain     kodjl01                            72
     c     *in72         ifeq      '1'
     c                   move      'SPK0054'     msgnr
     c                   seton                                        30
     c                   end
     c                   end
      * krav til kostnadssted
     c     wskste        ifeq      0
     c     stedk         andeq     'J'
     c                   move      'SPK0055'     msgnr
     c                   seton                                        3072
     c                   end
      * forbudt til kostnadssted
     c     wskste        ifne      0
     c     stedk         andeq     'N'
     c                   move      'SPK0056'     msgnr
     c                   seton                                        3072
     c                   end
     C   72              z-add     51            POSNBR
     C   72              goto      sltb
      *
      * Barer
      *
     c     wsbare        ifne      0
     c     kodike        chain     kodil01                            73
     c     *in73         ifeq      '1'
     c                   move      'SPB0109'     msgnr
     c                   seton                                        30
     c                   end
     c                   end
      * krav til bærer
     c     wsbare        ifeq      0
     c     barerk        andeq     'J'
     c                   move      'SPB0110'     msgnr
     c                   seton                                        3073
     c                   end
      * forbud til bærer
     c     wsbare        ifne      0
     c     barerk        andeq     'N'
     c                   move      'SPB0111'     msgnr
     c                   seton                                        3073
     c                   end
     C   73              z-add     56            POSNBR
     C   73              goto      sltb
      *
      * prosjekt
      *
     c     wspros        ifne      0
     c     proske        chain     prosl01                            74
     c     *in74         ifeq      '1'
     c                   move      'SPP0030'     msgnr
     c                   seton                                        30
     c                   end
     c                   end
      * krav til prosjekt
     c     wspros        ifeq      0
     c     prosjk        andeq     'J'
     c                   move      'SPP0031'     msgnr
     c                   seton                                        3074
     c                   end
      * forbud til prosjekt
     c     wspros        ifne      0
     c     prosjk        andeq     'N'
     c                   move      'SPP0032'     msgnr
     c                   seton                                        3074
     c                   end
     C   74              z-add     61            POSNBR
     C   74              goto      sltb
      *
      * momskode
      *
     c     WSPF          ifeq      'P'
     c                   z-add     1             WSPF2
     C     levtyp        ifeq      'I'
     c                   move      'F'           WSPF
     c                   z-add     0             WSPF2
     c                   end
     c                   end
     c     WSPF          ifeq      'F'
     c                   z-add     0             WSPF2
     c                   end
     c     WSPF          ifeq      '+'
     c                   z-add     1             WSPF2
     c                   move      'P'           WSPF
     C     levtyp        ifeq      'I'
     c                   move      'F'           WSPF
     c                   z-add     0             WSPF2
     c                   end
     c                   mult      fimvas2       WSBEL
     c                   end
      * tester om gyldig
      *
     c     wspf          ifeq      'P'
     c     '1'           lookup    mvtab                                  33
     c     *in(33)       ifeq      '0'
     c  N30              move      'SHU1672'     MSGNR
     c  N33              seton                                        3076
     c                   end
     c                   end
      *
      *
     c     wspf          ifeq      'F'
     c     '0'           lookup    mvtab                                  33
     c     *in(33)       ifeq      '0'
     c  N30              move      'SHU1673'     MSGNR
     c  N33              seton                                        3076
     c                   end
     c                   end
     c
N03   * IKKE BLANK MOMSKODE
N03   *
N03  c     wspf          ifeq      ' '
N03  c                   move      'SHU2063'     MSGNR
N03  c                   seton                                        3076
N03  c                   end
N03  C   76              z-add     70            POSNBR
N03  C   76              goto      sltb
      *
      * test periode
      *
      *
      * test av avvikende periode
      *
     C     WSMND         IFNE      0
     C                   Z-ADD     20            WSCC
     C     KODTÅK        CHAIN     KODTÅ                              75
     C     *in75         ifeq      '1'
     C     *in30         andeq     '0'
     C                   MOVE      'SPP0001'     MSGNR
     C                   Z-ADD     75            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     *IN30         IFEQ      '1'
     C                   GOTO      SLTB
     C                   ENDIF
      *
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
      *
      * leser opsjoner i subfil
      *
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'
     C                   GOTO      SLHENT
     C                   ENDIF
      *--------------
      * endre
      *--------------
     C  N98WSNR          IFEQ      '2'
     C                   EXSR      MOVFIW
     C                   MOVE      ' '           WSNR
     c                   EVAL      MODE='ENDRE'
     C                   EXSR      VARELISR
     C  N30              exsr      ENDRESR
     C                   GOTO      SLHENT
     C                   END
      *
      * sletting
      *
     C  N98WSNR          IFEQ      '4'
     C                   EXSR      SLETTSR
     C                   GOTO      SLHENT
     C                   END
      *--------------
      * attesterer
      *--------------
     C  N98WSNR          IFEQ      'A'
     C                   EXSR      ATTESTER
     C                   END
      *
N01   *--------------
N01   * reklamer
N01   *--------------
N01  C  N98WSNR          IFEQ      'R'
N01  C                   EXSR      REKSR
N01  C                   GOTO      SLHENT
N01  C                   END
N01   *--------------
N01   * reklamer opphev
N01   *--------------
N01  C  N98WSNR          IFEQ      'O'
N01  C                   EXSR      OREKSR
N01  C                   GOTO      SLHENT
N01  C                   END
N01   *--------------
N01   * S=Se epost
N01   *--------------------------------------
N01  C  N98WSNR          IFEQ      'S'
N01  C                   EXSR      DSPEPO
N01  C                   GOTO      SLHENT
N01  C                   END
N01   *
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     c                   CLEAR                   KOSTBR
     C                   MOVE      KABNR         KBBNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSKONT
     C                   MOVE      *ZEROS        WSKSTE
     C                   MOVE      *ZEROS        WSBARE
     C                   MOVE      *ZEROS        WSPROS
     C                   MOVE      KAMVA         WSPF
     c                   z-add     DIFF          WSBEL
     C                   MOVE      *ZEROS        WSBELN
     C                   MOVE      *ZEROS        WSMND
     C                   MOVE      *ZEROS        WSAAR
     C                   MOVE      *ZEROS        WSCC
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVFIW        BEGSR
     C***********************************************
     C                   MOVE      kbrefcko      WSKONT
     C                   MOVE      kbrefcav      WSKSTE
     C                   MOVE      kbrefcba      WSBARE
     C                   MOVE      kbrefcpr      WSPROS
      * omregn til valuta
     c                   z-add     kbblva        wsbel
     c                   z-add     kbblHB        wsbelN
     c                   move      kbblva        wsbel
     c                   move      KBKDPF        WSPF
     c                   move      KBKDM         WSPF2
     c                   move      KBPCC         WSCC
     c                   move      KBPÅR         WSAAR
     c                   move      KBPMN         WSMND
     C                   MOVE      KBSGG         WSSG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWFI        BEGSR
     C***********************************************
     c     wsbel         div       kaomrf        work154          15 4
     c     work154       mult      kavku         kbblhb
     c                   move      '*K*'         fastko
     c                   move      '*A*'         fastav
     c                   move      '*B*'         fastba
     c                   move      '*P*'         fastpr
     C                   MOVE      WSKONT        kbrefcko
     C                   MOVE      WSKSTE        kbrefcav
     C                   MOVE      WSBARE        kbrefcba
     C                   MOVE      WSPROS        kbrefcpr
     c                   move      WSPF          KBKDPF
     c                   move      WSPF2         KBKDM
     c                   move      WSCC          KBPCC
     c                   move      WSAAR         KBPÅR
     c                   move      WSMND         KBPMN
     c                   move      KABNR         KBBNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UKABNR            7 0
     C                   PARM                    UBNR2             7 0
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UVIS              1
     C     UFLAGG        IFEQ      5
     C                   MOVE      '1'           *IN29
     C                   ENDIF
     C     KODTÅK        KLIST
     C                   KFLD                    YÅUNI
     C                   KFLD                    WSCC              2 0
     C                   KFLD                    WSAAR
     C                   KFLD                    WSMND
     C                   MOVE      'K'           YÅUNI             1
      *
     C     KEYFIL        KLIST
     C                   KFLD                    KABNR
      *
     c                   move      ukabnr        kabnr
      *
      * sjekk kbrefc
      *
     c                   exsr      tstkbref
      *
      *
     C     KEYFIL        CHAIN     KOSTA                              33
     c                   z-sub     kabl          kabl
      *
     c                   move      KAFDTÅ        WSAAF
     c                   move      KAFDTM        WSMMF
     c                   move      KAFDTD        WSDDF
     c                   move      KAFFDÅ        WSAAFF
     c                   move      KAFFDM        WSMMFF
     c                   move      KAFFDD        WSDDFF
      *
      * HENT BRUKERS ATTESTASJONSKODE
      *
     C     parkey        klist
     c                   kfld                    syuser
     c                   kfld                    sypaid
     C                   move      'USRID'       SYPAID
     c                   move      uuser         syuser
     c     parkey        chain     syparl2                            33
     C  N33              movel     syvrda        UUSG              3
     C                   READ      firm                                   33
      *
      * momsverdi
      *
     c     FIMVAS        add       1             fimvas2           3 2
     c
      *
     C     UVIS          IFEQ      'J'
     C                   MOVE      *ON           *IN27
     C                   ENDIF
      *
     C     LEVKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KALNR
      *
     C     KONTKEY       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKONT
      *
     C     KODIKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSBARE
      *
     C     KODJKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKSTE
      *
      *
     C     VALKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KAVAL
      *
      *
     C     PROSKE        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    WSPROS
      *
     c     levkey        chain     level01                            33
N01  c  N33              move      kundnr        levkun            8 0
     c     levkey        chain     navlev                             33
     c   33              move      'N'           sokna             1
     C*
     C                   ENDSR
      *
     C***********************************************
     C     SLETTSR       BEGSR
     C***********************************************
      *
      *** SLETT LINJE
      *
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     RRNP          CHAIN     KOSTBUPD                           33
     C  N33              DELETE    KOSTBRU
     C                   EXSR      MOVB1
     C                   END
     C                   UPDATE    SUBFIL
     C                   ENDSR
      *
     C***********************************************
     C     ATTESTER      BEGSR
     C***********************************************
      *
      *** ATTESTER LINJE
      *
     C                   MOVE      ' '           WSNR
     C     RRNP          CHAIN     KOSTBUPD                           33
     c                   move      UUSG          KBSGG
     C  N33              UPDATE    KOSTBRU
     C                   UPDATE    SUBFIL
     C                   ENDSR
      *
     C***********************************************
     C     VARELISR      BEGSR
     C***********************************************
      *
     C                   z-add     21            LINNBR            3 0
     C                   z-add     12            POSNBR
     C     FEIL          TAG
     C                   EXFMT     VARELI
     C     WSCLOC        DIV       256           LINNBR            3 0
     C                   MVR                     POSNBR            3 0
     C                   MOVEA     '000'         *IN(68)
     C                   SETOFF                                       30
     C   03              SETON                                        30
     C   03              GOTO      UTFEIL
      *
      * avbryt
      *
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   SETON                                        30
     C                   EXSR      MOVB2
     C                   GOTO      UTFEIL
     C                   END
     c     *in04         ifeq      '1'
     c                   exsr      sok
     c                   goto      feil
     c                   end
     C                   EXSR      TESTB1
     c   30              goto      feil
     c     UTFEIL        ENDSR
      *
      *
     C***********************************************
     C     ENDRESR       BEGSR
     C***********************************************
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
      *
     C     RRNP          CHAIN     KOSTBUPD                           33
     C                   EXSR      MOVWFI
     C  N33              UPDATE    KOSTBRU
     C                   UPDATE    SUBFIL
      *
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     c                   endsr
     C*
      *
     C***********************************************
     C     NYSR          BEGSR
     C***********************************************
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
      *
     C                   EXSR      MOVWFI
     C                   WRITE     KOSTBRU
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     c                   endsr
      *
     C***********************************************
     C     SUMSR         BEGSR
     C***********************************************
      *
     c                   z-add     0             sumf
     c                   setoff                                       33
     C     KEYFIL        SETLL     KOSTB
     C     *IN(33)       DOWEQ     '0'
     C     KEYFIL        READE(N)  KOSTB                                  33
     c     *in(33)       ifeq      '0'
     c     kbblhb        div       kavku         work154          15 4
s02  c*    work154       mult      kaomrf        kbblva
n02  c     work154       mult(h)   kaomrf        kbblva
     c                   add       kbblva        sumf
      * ATETSTER
     c                   endif
     c                   enddo
     c     kabl          sub       sumf          diff
     c     diff          comp      0                                      95
     c                   endsr
     C***********************************************
     C     ATTSR         BEGSR
     C***********************************************
      * ATTESTERER POSTENE
     c                   setoff                                       33
     C     KEYFIL        SETLL     KOSTB
     C     *IN(33)       DOWEQ     '0'
     C     KEYFIL        READE     KOSTB                                  33
     c     *in(33)       ifeq      '0'
     c     kbsgg         ifeq      *blanks
     c                   move      uusg          kbsgg
     c                   end
     c                   update    kostbr
     c                   endif
     c                   enddo
     c                   endsr
      ***********************************************************************
     C     NYTTBILN      BEGSR
      ***********************************************************************
     C     STBUN2        TAG
     C                   EXFMT     BUNN2
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDF13
     C                   ENDIF
      *
      * HENTER BILAGSNUMMER FRA TELLER
      *
     C     WSBSR         IFNE      ' '
     C     WSBNR2        ANDEQ     *ZEROS
     C     WSBSR         CHAIN     KOSTT                              33
     C   33              GOTO      STBUN2
     C                   MOVE      KTNR          WSBNR2
     C                   ADD       1             KTNR
     C                   UPDATE    KOSTTR
     C                   ENDIF
      *
      * TILLATER IKKE 0 I BILAGSNUMMER VED FERDIGMELDING
      *
     C     WSBNR2        IFEQ      *ZEROS
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV0385'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     42            POSNBR
     C                   GOTO      STBUN2
     C                   ENDIF
      *
      * BILASGSNUMMER MÅ IKKE FINNES FRA FØR
      *
     C     WSBNR2        IFNE      *ZEROS
     C     WSBNR2        CHAIN     KOSTA14                            33
     C     *IN33         IFEQ      *OFF
     C                   MOVE      'JOV0387'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     42            POSNBR
     C                   GOTO      STBUN2
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      WSBNR2        UBNR2
      *
     C     ENDF13        TAG
     C                   MOVE      '0'           *IN30
     C                   ENDSR
      *
     C***********************************************
     C     SOK           BEGSR
     C***********************************************
      *
      *
      *** konto
      *
      * kostnadssted
     c     posnbr        ifge      45
     c     posnbr        andle     49
     c     posnbr        ifeq      45
     c     sokna         andne     'N'
     c                   call      'SYNA05'
     c                   parm                    kalnr
     c                   parm                    wskont
     c                   else
     c                   call      'SYNA09'
     c                   parm                    wskont
     c                   end
     c     wskont        ifne      0
     c                   z-add     51            posnbr
     c                   end
     c                   goto      utsok
     c                   end
      * kostnadssted
     c     posnbr        ifge      51
     c     posnbr        andle     54
     c     sokna         andne     'N'
     c                   call      'SYNA06'
     c                   parm                    kalnr
     c                   parm                    wskste
     c     wskste        ifne      0
     c                   z-add     56            posnbr
     c                   end
     c                   goto      utsok
     c                   end
      * kostnadssted
     c     posnbr        ifge      56
     c     posnbr        andle     59
     c     sokna         andne     'N'
     c                   call      'SYNA07'
     c                   parm                    kalnr
     c                   parm                    wsbare
     c     wsbare        ifne      0
     c                   z-add     61            posnbr
     c                   end
     c                   goto      utsok
     c                   end
      * Prosjektnr d
     c     posnbr        ifge      61
     c     posnbr        andle     68
     c                   call      'SYNA08'
     c                   parm                    kalnr
     c                   parm                    wspros
     c     wsbare        ifne      0
     c                   z-add     70            posnbr
     c                   end
     c                   goto      utsok
     c                   end
     c     utsok         endsr
      *
      *************************************************************
     c     tstkbref      begsr
      *************************************************************
     c                   setoff                                       94
     C     KEYFIL        SETLL     KOSTB
     C     *in94         doweq     '0'
     C     KEYFIL        READE     KOSTB                                  94
     c     *in94         ifeq      '0'
     c     kbrefc        ifeq      *blanks
     c                   eval      kbrefc='*K*00000*A*0000*B*0000*P*00000000'
     c                   ENDIF
     c                   update    kostbr
     c                   ENDIF
     C                   ENDDO
     C                   ENDSR
     C*
      *
      *************************************************************
     c     f10test       begsr
      *************************************************************
     C                   Z-ADD     5             LILES             2 0
     c                   setoff                                       88
     C     KEYFIL        SETLL     KOSTB
     C     *in88         doweq     '0'
     C     KEYFIL        READE(N)  KOSTB                                  88
     C     *in88         ifeq      '0'
     C                   ADD       1             LILES
     C                   MOVE      kbrefcko      WSKONT
     C                   MOVE      kbrefcav      WSKSTE
     C                   MOVE      kbrefcba      WSBARE
     C                   MOVE      kbrefcpr      WSPROS
     c                   exsr      movfiw
     c                   exsr      testb1
     C   30              Z-ADD     3             POSNBR
     C   30              Z-ADD     LILES         LINNBR
     c   30              goto      utf10
     c                   endif
     c                   enddo
     c     utf10         endsr
     C*
     C***********************************************
     C     CMD18         BEGSR
     C***********************************************
     C                   SETOFF                                       33
     C     UKABNR        SETLL     KOSTB
     C     *IN33         DOWEQ     '0'
     C     UKABNR        READE     KOSTB                                  33
     C     *IN33         IFEQ      '0'
     C                   MOVE      *BLANKS       KBSGG
     C                   UPDATE    KOSTBR
     C                   ENDIF
     C                   ENDDO
     C                   ENDSR
N01   ***********************************************
N01  C     REKSR         BEGSR
N01   ***********************************************
N01  C*                  MOVE      *BLANKS       FSSOK
N01  C*    FRIKEY0       CHAIN     FRISOK                             20
N01  c                   move      'L'           ukode
N01  c                   z-add     0             kbavdp            4 0
N01  c                   move      kabnr2        kabnra            7
N01  c                   move      kabnr2        kabnrp            7 0
N01  C                   eval      EMNE = 'Fakturanr:'
N01  C     EMNE          CAT       KAFNR:1       EMNE             80
N01  C     EMNE          CAT       'Dato:':1     EMNE
N01  C     EMNE          CAT       DATOAA:1      EMNE
N01  C     EMNE          CAT       'Vårt bil':1  EMNE
N01  C     EMNE          CAT       'agsnr:':0    EMNE
N01  C     EMNE          CAT       KABNRA:1      EMNE
N01  C                   CALL      'ARC045'
N01  C                   PARM                    kbavdp
N01  C                   PARM                    kabnrp
N01  c                   parm                    levkun
N01  C                   PARM                    UKODE             1
N01  C                   PARM                    EMNE             80
N01  C
N01   * hvis epost sendt oppdater
N01  C     ukode         ifeq      'S'
N01  C     RRNP          CHAIN     KOSTBUPD                           20
N01  c                   move      'RS'          KBREKL
N01  c                   update    kostbru
N01  c                   end
N01  C     KBGEBY        COMP      1                                      43
N01  C                   MOVE      ' '           WSNR
N01  C                   UPDATE    SUBFIL
N01  C                   endsr
N01   ***********************************************
N01  C     OREKSR        BEGSR
N01   ***********************************************
N01   * hvis epost sendt oppdater
N01  C     RRNP          CHAIN     KOSTBUPD                           20
N01  c                   move      'RO'          KBREKL
N01  c                   update    kostbru
N01  C     KBGEBY        COMP      1                                      43
N01  C                   MOVE      ' '           WSNR
N01  C                   UPDATE    SUBFIL
N01  c                   exsr      ttsr
N01  C                   endsr
N01   ***********************************************
N01  C     DSPEPO        BEGSR
N01   ***********************************************
N01  C     HEADKK        KLIST
N01  C                   KFLD                    KBAVD0            4 0
N01  C                   KFLD                    KABNR2
N01  C     HEADKK        CHAIN     ARKLOGH4                           33
N01  C  N33              DO
N01  C                   CALL      'ARC012'
N01  c                   PARM                    KBAVD0
N01  C                   PARM                    KABNR2
N01  C                   END
N01  C     KBGEBY        COMP      1                                      43
N01  C                   MOVE      ' '           WSNR
N01  C                   UPDATE    SUBFIL
N01   *
N01  C                   ENDSR
N01   *
N01  C***********************************************
N01  C     TTSR          BEGSR
N01  C***********************************************
N01  C                   MOVE      KBAVD         TTAVD
N01  C                   MOVE      KBOPD         TTOPD
N01  C                   TIME                    XULTID
N01  C                   MOVE      XULDD         ULDD
N01  C                   MOVE      XULMM         ULMM
N01  C                   MOVE      XULÅÅ         ULÅÅ
N01  C                   MOVE      'REO'         TTACTI
N01  C                   MOVE      ULDT          TTDATE
N01  C                   MOVE      XULTM         TTTIME
N01  C                   MOVE      TTDATE        TTDATL
N01  C                   MOVE      TTTIME        TTTIML
N01  C                   MOVE      UUSER         TTUSER
N01  C                   MOVE      *blanks       TTTEXT
N01  c                   move      *blanks       TTTEXL
N01  C                   eval      TTTEXT = 'Complaint ended inv:'
N01  C     TTTEXT        cat       KAFNR         TTTEXT
N01  C                   eval      TTTEXL = 'Reklamasjon avsluttet Fakt:'
N01  C     TTTEXL        CAT       KAFNR:1       TTTEXL
N01  C                   WRITE     TRACKFR
N01  C                   ENDSR
