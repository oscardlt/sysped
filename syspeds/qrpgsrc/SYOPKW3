      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/SYOPKW3 MODULE(*LIBL/SYOPKW3 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHENOP     IF   E           K DISK    USROPN
     FHENOS03   IF   E           K DISK    USROPN
     FHENOF01   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D user            s             10
     D Term            s              4
     D LumpSum         s              1
     D Avd1            s              4
     D Avd1N           s              4  0
     D Avd2            s              4
     D Avd2N           s              4  0
     D Avd3            s              4
     D Avd3N           s              4  0
     D Avd4            s              4
     D Avd4N           s              4  0
     D Dager           s              3
     D DagerN          s              5  0
     D VktGr           s              7
     D LumpVkt         s              7  0
     D M3Gr            s              7
     D LumpM3          s              7  3
     D LMGr            s              7
     D LumpLM          s              7  3
      *
     D XXDATN          s              8  0
     D XXTIMN          s              4  0
     D AddSub          s              3
     D Idag            s              8  0
     D DatoGmN         s              8  0
     D BlankIco        s             20a   inz('blank.gif')
     D MeldIco         s             20a   inz('melding.gif')
     D FarlIco         s             20a   inz('farlig.gif')
     D VarmIco         s             20a   inz('varme.gif')
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D  XULDDMM                7     10  0
     D  XULÅÅ2                13     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  HSds                   1     30
     D  XXDATE                 1      8
     D  XXTIME                 9     14
     D                 DS
     D  WDsÅMD                 1      8  0
     D  WDsÅMDÅ                1      4  0
     D  WDsÅMDM                5      6  0
     D  WDsÅMDD                7      8  0
     D                 DS
     D  WDsDMÅ                 1      8  0
     D  WDsDMÅD                1      2  0
     D  WDsDMÅM                3      4  0
     D  WDsDMÅÅ                5      8  0
     D                 DS
     D  HSNAK                  1     30
     D  XXNAK                  1     10
     D                 DS
     D  HSPNRK                 1      4  0
     D  XXPNRK                 1      4
     D                 DS
     D  HSSTDK                 1     25
     D  XXSTDK                 1     10
     D                 DS
     D  HONAS                  1     30
     D  XXNAS                  1      7
     D                 DS
     D  HOPNRS                 1      4  0
     D  XXPNRS                 1      4
     D                 DS
     D  HOSTDS                 1     25
     D  XXSTDS                 1      7
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
      *
      *
      * 1. Vis liste over registrete leveringer - I DAG
     C                   callp     wrtsection('top2a')
     C                   callp     wrtsection('top2t')
     c                   Move      *zeros        GmAvd             4 0
     c     TermSta       setLL     HENOS03
     c     TermSta       ReadE     HENOS03                                35
      *
     c     *IN35         doweq     '0'
     c                   exsr      excl
     c     exclude       cabeq     'J'           Next
     C                   eval      XXDATN   = c2n2(XXDATE)
     c     XXDATN        IFGE      ULDT
      *
     c     GmAvd         ifeq      *zeros
     c                   Move      HSAVDH        GmAvd             4 0
     c                   end
      *skriv subtotal ved brudd
     c     HsAvdH        ifne      GMavd
     c                   exsr      SetSubTot
     c                   end
      *
     c                   exsr      Getdata
      *skriv lest linje  (hvis den ikke er tatt inn i lumpsum)
     c     ILump         ifne      'J'
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   end
     c                   Move      HSAVDH        GmAvd             4 0
     c                   end
     c     next          tag
     c     TermSta       ReadE     HENOS03                                35
      *
     c                   end
      *skriv subtotal ved slutt på leseloop
     c     Gmavd         ifne      *zeros
     c                   exsr      SetSubTot
     c                   end
      *
      *
      *
      * 2. Vis liste over registrete sendinger XX foregående dager
     C                   callp     wrtsection('top2b')
     C                   callp     wrtsection('top2t')
     c                   Move      *zeros        GmAvd             4 0
     c     TermSta       setLL     HENOS03
     c     TermSta       ReadE     HENOS03                                35
      *
     c     *IN35         doweq     '0'
     c                   exsr      excl
     c     exclude       cabeq     'J'           Next2
     C                   eval      XXDATN   = c2n2(XXDATE)
     c     XXDATN        IFLT      ULDT
     c     XXDATN        andge     DatoGmN
      *
     c     GmAvd         ifeq      *zeros
     c                   Move      HSAVDH        GmAvd             4 0
     c                   end
      *skriv subtotal ved brudd
     c     HsAvdH        ifne      GMavd
     c                   exsr      SetSubTot
     c                   end
      *
     C                   exsr      getdata
      *skriv lest linje  (hvis den ikke er tatt inn i lumpsum)
     c     ILump         ifne      'J'
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   end
     c                   Move      HSAVDH        GmAvd             4 0
     c                   end
     c     next2         tag
     c     TermSta       ReadE     HENOS03                                35
      *
     c                   end
      *skriv subtotal ved slutt på leseloop
     c     Gmavd         ifne      *zeros
     c                   exsr      SetSubTot
     c                   end
      *
      *
      *
      * 3. Vis liste over ELDRE registrete sendinger
     C                   callp     wrtsection('top2c')
     C                   callp     wrtsection('top2t')
     c                   Move      *zeros        GmAvd             4 0
     c     TermSta       setLL     HENOS03
     c     TermSta       ReadE     HENOS03                                35
      *
     c     *IN35         doweq     '0'
     c                   exsr      excl
     c     exclude       cabeq     'J'           Next3
     C                   eval      XXDATN   = c2n2(XXDATE)
     c     XXDATN        IFLT      DatoGmN
      *
     c     GmAvd         ifeq      *zeros
     c                   Move      HSAVDH        GmAvd             4 0
     c                   end
      *skriv subtotal ved brudd
     c     HsAvdH        ifne      GMavd
     c                   exsr      SetSubTot
     c                   end
      *
     C                   exsr      getdata
      *skriv lest linje  (hvis den ikke er tatt inn i lumpsum)
     c     ILump         ifne      'J'
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   end
     c                   end
     c     next3         tag
     c     TermSta       ReadE     HENOS03                                35
      *
     c                   end
      *skriv subtotal ved slutt på leseloop
     c     Gmavd         ifne      *zeros
     c                   exsr      SetSubTot
     c                   end
      *
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *=====================================================================
      * Akkumuler totaler  ....
      *=====================================================================
     C     GetData       begsr
      * Akkumuler inn i avdelingstotal
     c                   add       1             TotSnd
     c                   add       HSAnt         TotAnt
     c                   add       HSVkt         TotVkt
     c                   add       HSM3          TotM3
     c                   add       HSLM          TotLM
     c     Henofkey      chain     henof01                            33
     c   33              Move      ' '           XSMELD            1
     c  N33              Move      'X'           XSMELD
      * Hentekundens navn
     c     HSLNR         Chain     HENOP
      *
      * Skal sendingen inngå i lumpsum?
     c                   move      'N'           ILump             1
     c     LumpSum       Ifeq      'Y'
     c     HSvkt         Ifgt      LumpVkt
     c     HSM3          orgt      LumpM3
     c     HSM3          orgt      LumpLM
     c                   goto      utLump
     c                   end
     c                   move      'J'           ILump
     c                   add       1             STotSnd
     c                   add       HSAnt         STotAnt
     c                   add       HSVkt         STotVkt
     c                   add       HSM3          STotM3
     c                   add       HSLM          STotLM
     C     XSMELD        IFNE      *BLANKS
     C                   MOVE      'X'           STotMeld
     c                   end
     C     HSFARL        IFNE      *BLANKS
     C                   MOVE      'X'           STotFarl
     c                   end
     C     HSVARM        IFNE      *BLANKS
     C                   MOVE      'X'           STotVarm
     c                   end
      *
     c                   end
      *
     C     Utlump        endsr
      *
      *=====================================================================
      * Exclude based on dpt...
      *=====================================================================
     C     Excl          begsr
     c                   Move      'J'           Exclude           1
      * hvis avd-begrensning, og sendingsAvd ikke med en av de - hopp ut
     c     Avd1N         ifne      *ZEROS
     c     Avd2N         orne      *ZEROS
     c     Avd3N         orne      *ZEROS
     c     Avd4N         orne      *ZEROS
     c     Avd1N         IFne      HSAVDH
     c     Avd2N         andne     HSAVDH
     c     Avd3N         andne     HSAVDH
     c     Avd4N         andne     HSAVDH
     c                   goto      UtExcl
     c                   end
     c                   end
      * Alle tester ok - ikke excluder
     c                   Move      'N'           Exclude           1
      *
     C     UtExcl        endsr
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      term     = zhbgetvar('term')
     c     term          IFEQ      '1000'
     c                   move      'Y'           TermSta           1
     c                   else
     c                   move      'Z'           TermSta           1
     c                   end
     C                   eval      Avd1    = zhbgetvar('Avd1')
     C                   eval      Avd1N   = c2n2(Avd1)
     C                   eval      Avd2    = zhbgetvar('Avd2')
     C                   eval      Avd2N   = c2n2(Avd2)
     C                   eval      Avd3    = zhbgetvar('Avd3')
     C                   eval      Avd3N   = c2n2(Avd3)
     C                   eval      Avd4    = zhbgetvar('Avd4')
     C                   eval      Avd4N   = c2n2(Avd4)
     C                   eval      Dager    = zhbgetvar('Dager')
     C                   eval      DagerN   = c2n2(Dager)
     c     DagerN        Ifeq      *zeros
     c                   eval      DagerN   = 3
     c                   end
     C                   eval      LumpSum  = zhbgetvar('LumpSum')
      *
     C                   eval      VKTGR    = zhbgetvar('VKTGR')
     C                   eval      LumpVkt  = c2n2(VKTGR)
     c     LumpVkt       Ifeq      *zeros
     c                   eval      LumpVkt  = 1500
     c                   end
      *
     C                   eval      M3GR     = zhbgetvar('M3GR')
     C                   eval      LumpM3   = c2n2(M3GR)
     c     LumpM3        Ifeq      *zeros
     c                   eval      LumpM3   = 5
     c                   end
      *
     C                   eval      LMGR     = zhbgetvar('LMGR')
     C                   eval      LumpLM   = c2n2(LMGR)
     c     LumpLM        Ifeq      *zeros
     c                   eval      LumpLM   = 0.75
     c                   end
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     HENOP
     C                   CLOSE     HENOS03
     C                   CLOSE     HENOF01
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYOPKW3':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('TERM':TERM)
     C                   callp     updHTMLvar('AVD1':
     C                             %trim(%editc(AVD1N:'Z')))
     C                   callp     updHTMLvar('AVD2':
     C                             %trim(%editc(AVD2N:'Z')))
     C                   callp     updHTMLvar('AVD3':
     C                             %trim(%editc(AVD3N:'Z')))
     C                   callp     updHTMLvar('AVD4':
     C                             %trim(%editc(AVD4N:'Z')))
     C                   callp     updHTMLvar('Dager':
     C                             %trim(%editc(DagerN:'Z')))
     C                   callp     updHTMLvar('LumpSum':LumpSum)
     C                   callp     updHTMLvar('VktGr':
     C                             %trim(%editc(LumpVkt:'Z')))
     C                   callp     updHTMLvar('M3Gr':
     C                             %trim(%editc(LumpM3:'3')))
     C                   callp     updHTMLvar('LMGr':
     C                             %trim(%editc(LumpLM:'3')))
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     updHTMLvar('LNR':
     C                             %trim(%editc(HSLNR:'Z')))
     C                   callp     updHTMLvar('HSSNDN':HSSNDN)
      * snu dato Å.M.dag
     c                   move      XXDATN        WDsÅMD
     c                   move      WDsÅMDÅ       WDsDMÅÅ
     c                   move      WDsÅMDM       WDsDMÅM
     c                   move      WDsÅMDD       WDsDMÅD
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(WDsDMÅ:'  .  .    ')))
     C                   Movel     XXTIME        XXTIM4            4
     C                   eval      XXTIMN   = c2n2(XXTIM4)
     C                   callp     updHTMLvar('TID':
     C                             %trim(%editw(XXTIMN:'  :  ')))
     C                   callp     updHTMLvar('XXNAS':XXNAS)
     C                   callp     updHTMLvar('XXPNRS':XXPNRS)
     C                   callp     updHTMLvar('XXSTDS':XXSTDS)
     C                   callp     updHTMLvar('XXNAK':XXNAK)
     C                   callp     updHTMLvar('XXPNRK':XXPNRK)
     C                   callp     updHTMLvar('XXSTDK':XXSTDK)
     C                   callp     updHTMLvar('HSAVDH':
     C                             %trim(%editc(HSAVDH:'Z')))
     C                   callp     updHTMLvar('HSANT':
     C                             %trim(%editc(HSANT:'Z')))
     C                   callp     updHTMLvar('HSVKT':
     C                             %trim(%editc(HSVKT:'Z')))
     C                   callp     updHTMLvar('HSM3':
     C                             %trim(%editc(HSM3:'3')))
     C                   callp     updHTMLvar('HSLM':
     C                             %trim(%editc(HSLM:'3')))
      * Melding til transportør??
     C     XSMELD        ifeq      *BLANKS
     C                   callp     updHTMLvar('MELDICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('MELDICO':MeldIco)
     c                   end
      * Farlig gods ??
     C     HSFARL        ifeq      *blanks
     C                   callp     updHTMLvar('FARLICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('FARLICO':FarlIco)
     c                   end
      * Varmegods ??
     C     HSVARM        ifeq      *blanks
     C                   callp     updHTMLvar('VARMICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('VARMICO':VarmIco)
     c                   end
      *
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row  - ved avdtot
      *=====================================================================
     C     SetSubtot     begsr
     C                   callp     updHTMLvar('LNR':spaces)
     C                   callp     updHTMLvar('HSSNDN':spaces)
      * Hvis det er samlet smågods i LUMPSUM - vis denne
     c     LumpSum       Ifeq      'Y'
     c     STotAnt       andne     *zeros
     C                   callp     updHTMLvar('HSAVDH':
     C                             %trim(%editc(Gmavd:'Z')))
     C                   callp     updHTMLvar('SUBTXT':'*Sum små-send+
     C                                        inger*')
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(STotSND:'Z')))
     C                   callp     updHTMLvar('HSANT':
     C                             %trim(%editc(STotANT:'Z')))
     C                   callp     updHTMLvar('HSVKT':
     C                             %trim(%editc(STotVKT:'Z')))
     C                   callp     updHTMLvar('HSM3':
     C                             %trim(%editc(STotM3:'3')))
     C                   callp     updHTMLvar('HSLM':
     C                             %trim(%editc(STotLM:'3')))
      * Meelding ?? på en av...
     C     STotMeld      ifeq      *BLANKS
     C                   callp     updHTMLvar('MELDICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('MELDICO':MeldIco)
     c                   end
      * Farlig gods ??
     C     STotFarl      ifeq      *blanks
     C                   callp     updHTMLvar('FARLICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('FARLICO':FarlIco)
     c                   end
      * Varmegods ??
     C     STotVarm      ifeq      *blanks
     C                   callp     updHTMLvar('VARMICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('VARMICO':VarmIco)
     c                   end
      * skriv subtotal for småsendingene
     C                   callp     wrtsection('subrow')
     c                   end
      *
      * Avdelingstotal
     C                   callp     updHTMLvar('HSAVDH':
     C                             %trim(%editc(Gmavd:'Z')))
     C                   callp     updHTMLvar('SUBTXT':'*Sum denne avde+
     C                                        ling*')
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(TotSND:'Z')))
     C                   callp     updHTMLvar('HSANT':
     C                             %trim(%editc(TotANT:'Z')))
     C                   callp     updHTMLvar('HSVKT':
     C                             %trim(%editc(TotVKT:'Z')))
     C                   callp     updHTMLvar('HSM3':
     C                             %trim(%editc(TotM3:'3')))
     C                   callp     updHTMLvar('HSLM':
     C                             %trim(%editc(TotLM:'3')))
     C                   callp     updHTMLvar('MELDICO':BlankIco)
     C                   callp     updHTMLvar('FARLICO':BlankIco)
     C                   callp     updHTMLvar('VARMICO':BlankIco)
      * skriv avdelingens subtotal
     C                   callp     wrtsection('subrow')
      *  avd-totaler
     c                   move      *zeros        TotSnd
     c                   move      *zeros        TotAnt
     c                   move      *zeros        TotVkt
     c                   move      *zeros        TotM3
     c                   move      *zeros        TotLM
      *  Små-gods-totaler
     c                   move      *zeros        STotSnd
     c                   move      *zeros        STotAnt
     c                   move      *zeros        STotVkt
     c                   move      *zeros        STotM3
     c                   move      *zeros        STotLM
     c                   move      ' '           STotMeld
     c                   move      ' '           STotFarl
     c                   move      ' '           STotVarm
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      HENOP
     C                   OPEN      HENOS03
     C                   OPEN      HENOF01
      *
      *
      * HENT DATO/KLOKKESLETT  (ULDT -dagens dato i form HHÅÅMMDD )
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
      * Finn datogrense for gruppa "foregående XX dager"
     c                   move      *zeros        Idag
     c                   move      *zeros        DatoGmN
     c                   move      'SUB'         AddSub
     C                   CALL      'SYGE20'
     C                   PARM                    AddSub
     C                   PARM                    IDag
     C                   PARM                    DatoGmN
     C                   PARM                    DagerN
      *
      * Key for sendings-fritekst
     C     HENOFKey      KLIST
     C                   KFLD                    HSLNR
     C                   KFLD                    HSSNDN
      *
      *
     c     *like         DEFINE    HSAnt         TotSnd
     c     *like         DEFINE    HSAnt         TotAnt
     c     *like         DEFINE    HSVkt         TotVkt
     c     *like         DEFINE    HSM3          TotM3
     c     *like         DEFINE    HSLM          TotLM
     c     *like         DEFINE    HSAnt         STotSnd
     c     *like         DEFINE    HSAnt         STotAnt
     c     *like         DEFINE    HSVkt         STotVkt
     c     *like         DEFINE    HSM3          STotM3
     c     *like         DEFINE    HSLM          STotLM
     c                   move      *zeros        TotSnd
     c                   move      *zeros        TotAnt
     c                   move      *zeros        TotVkt
     c                   move      *zeros        TotM3
     c                   move      *zeros        TotLM
     c                   move      *zeros        STotSnd
     c                   move      *zeros        STotAnt
     c                   move      *zeros        STotVkt
     c                   move      *zeros        STotM3
     c                   move      *zeros        STotLM
     c                   move      ' '           STotMeld          1
     c                   move      ' '           STotFarl          1
     c                   move      ' '           STotVarm          1
      *
     C                   endsr
      *
