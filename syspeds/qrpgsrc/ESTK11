      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK11 MODULE(*LIBL/ESTK11 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 kode Google = Y svar skal til google maps
10XXX * 02 JOV PTF10 Param for å gjøre cross domain kall
10XXX * 03 JOV PTF10 GOOGLE-LOGIKK NÅ FLYTTET TIL ESTK11G-Dvs denne er nå KUN AUTOROUTE
      *              (bruker ikke tid på å rydde her, men fremtidig vedlikehold unødig)
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FQ1024     IF   F 1024        DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D TIMN            s              3  0
     D MINN            s              2  0
     D FIL             s            100
N01  D GOOGLE          s              1
N02  D CallBack        s           1000
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  ES_PATH                1    100
     D  ESPED                  1      5
     D  PATH                   6     50
     D  OMR                   51     60
     D  TIM                   61     63
     D  MIN                   64     65
     D  UNTYPE                66     69
     IQ1024     NC  01
     I                                  1 1024  Data
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user = zhbgetvar('user')
     C                   eval      OMR  = zhbgetvar('OMR')
     C                   eval      TIM  = zhbgetvar('TIM')
     C                   eval      TIMN = c2n2(TIM)
     C                   MOVE      TIMN          TIM
     C                   eval      MIN  = zhbgetvar('MIN')
     C                   eval      MINN = c2n2(MIN)
     C                   MOVE      MINN          MIN
     C                   eval      PATH = zhbgetvar('PATH')
     C                   eval      UNTYPE= zhbgetvar('UNTYPE')
N01  C                   eval      Google = zhbgetvar('GOOGLE')
N02  C                   eval      CallBack = zhbgetvar('CallBack')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
N01  c     Google        ifne      'Y'
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESTK11':'/CGITAG/')
N01  c                   else
N01  C                   callp     gethtml('HTMLSRC':
N01  C                             '*LIBL':
N01  C                             'ESTK11G':'/CGITAG/')
N01  c                   end
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
N01  c**   Google        ifne      'Y'
     C                   callp     wrtsection('top')
N01  c**                 end
      *
     C                   EVAL      ESPED  = '*ES* '
n01  C     Google        ifeq      'Y'
n01  c                   move      'G'           ESPED
n02  C     CallBack      ifne      *blanks
n02  c                   move      'C'           ESPED
n02  c                   end
n01  c                   end
     c                   move      ES_PATH       FIL
     C                   CALL      'SYTK11CLE'
     C                   PARM                    FIL
      *
      * cross domain oppkall ??
N02  c     CallBack      ifne      *blanks
N02  c                   eval      Data=%trim(CallBack)+'('
N02  C                   callp     updHTMLvar('LINTXT':Data)
N02  C                   callp     wrtsection('lin')
N02  C                   end
     C                   MOVE      *ZEROS        ANTREC            5 0
     C                   OPEN      Q1024
     C                   READ      Q1024                                  35
     C     *IN35         DOWEQ     *OFF
     C                   ADD       1             ANTREC
     C     ANTREC        IFEQ      1
N01  c     Google        andne     'Y'
     C                   callp     updHTMLvar('LINTXT':'##OK')
     C                   callp     wrtsection('lin')
     C                   END
     C                   callp     updHTMLvar('LINTXT':Data)
     C                   callp     wrtsection('lin')
     C                   READ      Q1024                                  35
     C                   END
     C                   CLOSE     Q1024
     C     ANTREC        IFEQ      0
N01  c     Google        andne     'Y'
     C                   callp     updHTMLvar('LINTXT':'##INGEN')
     C                   callp     wrtsection('lin')
     C                   END
      *
      * Send section bot
N01  c     Google        ifne      'Y'
     C                   callp     wrtsection('bot')
N01  c                   end
      * cross domain oppkall ??
N02  c     CallBack      ifne      *blanks
N02  C                   callp     updHTMLvar('LINTXT':')')
N02  C                   callp     wrtsection('lin')
N02  C                   end
     C
     C                   endsr
     C
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * Ugyldig userID
NUSR c     *in50         ifeq      '1'
NUSR C                   callp     gethtml('HTMLSRC':'*LIBL':'ERRUSR':
NUSR c                             '/CGITAG/')
NUSR C                   callp     updhtmlvar('User':USER)
NUSR C                   callp     wrtsection('all')
NUSR C                   callp     wrtsection('*fini')
NUSR C                   close     BRIDF
NUSR C                   eval      *inlr = *on
NUSR C                   return
NUSR c                   endif
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C     UtInit        endsr
