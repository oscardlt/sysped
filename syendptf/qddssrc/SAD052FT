********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10318 * 01 YBC PTF10 VEKT ER ENDRET FRA 5.0 TIL 12.3
      *              VAREBESKRIVELSE ER ØKT FRA 15A TIL 30A
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 02 YBC PTF11 OMBEREGNING
12248 * 03 YBC PTF12 VEKT, KOLLI, MENGDE ER UTVIDET.
12255 * 04 YBC PTF12 Fra midlertidig innførsel til ordinal utførsel
12301 * 05 CB  PTF12 TOTAL BRUTTOVEKT MED DESIMALER
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SADE001' 'SAD052' 'SY400LO-
     A                                      /SADTVINN')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CF03(03 'Be om utskrift')
     A                                      CF12(12 'Gå til hovedtolldek.')
     A                                      CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                 10  5'DU HAR DIFFERANSE MELLOM FAKTURASU-
     A                                      M OG VARELINJER ----->'
     A                                      COLOR(RED)
     A            UDIFF         13  2O 10 63TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A  29                             11  5'Ved TVINN-sending er dette ikke ti-
     A                                      llatt'
     A                                      DSPATR(BL)
     A                                      COLOR(YLW)
     A                                 23  5'F3/F17 = Avslutt med diff'
     A                                      COLOR(BLU)
     A                                 23 31'F12 = Ikke godta differanse'
     A                                      COLOR(BLU)
      *
     A          R BILDE02
     A                                      CF12(12 'Avbryt uten utskrift')
     A                                      CF03(03 'Avbryt med utskrift')
     A                                      CF10(10 'Endre Ekspedisjonsenhet')
     A                                      CF23(23 'KJØRE DIU PÅ NY')
     A                                      OVERLAY
     A          H                           HLPDOC('SAD018' 'SAD052' 'SY400LO-
     A                                      /SADTVINN')
     A                                      HLPARA(01 001 24 080)
     A                                  1 26'Bekrefte TVINN for SAD Utførsel'
     A                                      COLOR(WHT)
     A                                  1 73'SAD052FT'
     A                                      COLOR(BLU)
     A                                  3  1'Trykk Enter for å bekrefte:'
     A                                      COLOR(BLU)
     A                                  5  3'Avdeling . . . . . . . . :'
     A            UAVD           4Y 0O  5 32EDTCDE(4)
     A                                  6  5'Tolldeklarasjonsnummer :'
S02  A*           UTDN           7Y 0O  6 32EDTCDE(4)
N02  A            UTDN           7Y 0O  6 32EDTCDE(L)
     A                                  7  3'Selgers navn . . . . . . :'
     A            SENAK         30A  O  7 32TEXT('Navn Selger')
     A                                  8  3'Kjøpers navn . . . . . . :'
     A            SENAS         30A  O  8 32TEXT('Navn Kjøper')
     A                                 10  3'Meldings funksjon  . . . :'
     A            W1N07     R        B 10 32REFFLD(M1N07 *LIBL/EDIM)
     A                                      TEXT('Meldingsfunksjon')
     A                                      DSPATR(UL)
S02  A*                                     VALUES('   ' 'DFU' 'DKO' 'DMA' 'DOM-
S02  A*                                     ' 'DFO' 'DEN')
N02  A                                      VALUES('   ' 'DFU' 'DKO' 'DMA' 'DFO-
N02  A                                      ' 'DEN' 'DEB' 'DRE' 'DSO')
E02  A N39                             10 48'DFU=Fullstendig tolldeklarasjon'
N02  A  39                             10 48'DEB=Etterberegning'
     A                                 11  3'OBS! Denne er avvist tidligere '
     A  99                                  DSPATR(RI)
     A  99                                  COLOR(RED)
     A N99                                  DSPATR(ND)
E02  A N39                             11 48'DMA=Manuell tolldeklarasjon'
N02  A  39                             11 48'DRE=Refusjon'
     A                                 12  3'(Siste tilbakemelding er "IU") '
     A  99                                  DSPATR(RI)
     A N99                                  DSPATR(ND)
     A  99                                  COLOR(RED)
E02  A N39                             12 48'DKO=Korrigert tolldeklarasjon'
N02  A  39                             12 48'DSO=Statistisk oppretting'
     A                                 13  3'F1/F3=Avbryt/IKKE send på TVINN'
     A  99                                  DSPATR(RI)
     A N99                                  DSPATR(ND)
     A  99                                  COLOR(RED)
E02  A N39                             13 48'DFO=Foreløpig tolldeklarasjon'
     A                                 14  3'Enter=Send som komplett ny sak '
     A  99                                  DSPATR(RI)
     A N99                                  DSPATR(ND)
     A  99                                  COLOR(RED)
E02  A N39                             14 48'DEN=Endelig tolldeklarasjon'
     A                                 15  3'Ekspedisjonsenhet  . . . :'
     A            W3039E         6D 0O 15 32
     A                                 16  3'Ønsket behandlingsdato . :'
     A            W2005B         6Y 0B 16 32
     A                                      TEXT('Ønsket Beh.dato')
     A                                      DSPATR(UL)
     A                                      EDTCDE(Y)
     A                                 16 48'DD/MM/ÅÅ eller 00/00/00'
     A                                 17  3'Depositum beløp  . . . . :'
     A            W5004D    R        B 17 32REFFLD(M5004D *LIBL/EDIM)
     A                                      TEXT('Beløp Depositum')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 18  3'Ventegrupe . . . . . . . :'
     A            WVEN      R        B 18 32REFFLD(MVEN *LIBL/EDIM)
     A                                      TEXT('Ventekode')
     A                                      DSPATR(UL)
     A                                 18 48'Blank=Sende direkte '
     A                                 19  3'Testekod . . . . . . . . :'
     A            W0035     R        B 19 32REFFLD(M0035 *LIBL/EDIM)
     A                                      RANGE(' ' '2')
     A                                 19 48'Blank=Produksjon'
     A                                 20  3'Ekspederingsprioritet. . :'
     A            W9N01     R        B 20 32REFFLD(M9N01 *LIBL/EDIM)
     A                                      TEXT('Meldingsprioritet')
     A                                      RANGE(0 4)
     A                                      EDTCDE(4)
     A                                 20 48'1-4 (1=Express)'
N04  A                                 21  3'Tidligere fortollingsdato:'
N04  A            WSDTT          6Y 0B 21 32TEXT('Tidligere fort.dato')
N04  A  98                                  DSPATR(PC)
N04  A  98                                  DSPATR(HI)
N04  A                                      DSPATR(UL)
N04  A                                      EDTCDE(Y)
N04  A                                 21 48'DD/MM/ÅÅ eller 00/00/00'
     A                                 23  1'F3=Avbryt med utskrift'
     A                                      COLOR(BLU)
     A                                 23 26'F10=Endre ekspedisjonsenhet'
     A                                      COLOR(BLU)
     A                                 23 55'F12=Avbryt uten utskrift'
     A                                      COLOR(BLU)
     A  30                             24  2'Du må benytte en av de 4 melding I-
     A                                      D på skjermen'
     A                                      COLOR(WHT)
      *
     A          R BILDE04
     A                                      OVERLAY
     A                                  1 28'Endre Ekspedisjonsenhet'
     A                                      COLOR(WHT)
     A                                  1 73'SAD052FT'
     A                                      COLOR(BLU)
     A                                  3  1'Trykk Enter for å bekrefte:'
     A                                      COLOR(BLU)
     A                                  5  3'Avdeling . . . . . . . . :'
     A            UAVD           4Y 0O  5 32EDTCDE(4)
     A                                  6  5'Tolldeklarasjonsnummer :'
     A            UTDN           7Y 0O  6 32EDTCDE(4)
     A                                  7  3'Selgers navn . . . . . . :'
     A            SENAK         30A  O  7 32TEXT('Navn Selger')
     A                                  8  3'Kjøpers navn . . . . . . :'
     A            SENAS         30A  O  8 32TEXT('Navn Kjøper')
     A                                 10  3'Meldings funksjon  . . . :'
S02  A*           W1N07     R        O 10 32REFFLD(M1N07 *LIBL/EDIM)
N02  A            W1N07     R        B 10 32REFFLD(M1N07 *LIBL/EDIM)
     A                                      TEXT('Meldingsfunksjon')
N02  A  39                             10 48'DEB=Etterberegning'
E02  A N39                             10 48'DFU=Fullstendig tolldeklarasjon'
N02  A  39                             11 48'DRE=Refusjon'
E02  A N39                             11 48'DMA=Manuell tolldeklarasjon'
N02  A  39                             12 48'DSO=Statistisk oppretting'
E02  A N39                             12 48'DKO=Korrigert tolldeklarasjon'
E02  A N39                             13 48'DFO=Foreløpig tolldeklarasjon'
E02  A N39                             14 48'DEN=Endelig tolldeklarasjon'
     A                                 15  3'Ekspedisjonsenhet  . . . :'
     A            W3039E         6D 0B 15 32
N02  A  39                             15 48'SO:'
N02  A  39        S3039EO1  R        O 15 52REFFLD(EDIMR/M3039E *LIBL/EDIM)
N02  A                                      COLOR(WHT)
N02  A                                      EDTCDE(4)
N02  A  39                             15 60'EB/RE:'
N02  A  39        S3039EO2  R        O 15 67REFFLD(EDIMR/M3039E *LIBL/EDIM)
N02  A                                      COLOR(WHT)
N02  A                                      EDTCDE(4)
     A                                 16  3'Ønsket behandlingsdato . :'
     A            W2005B         6Y 0O 16 32
     A                                      TEXT('Ønsket Beh.dato')
     A                                      EDTCDE(Y)
     A                                 16 48'DD/MM/ÅÅ eller 00/00/00'
     A                                 17  3'Depositum beløp  . . . . :'
     A            W5004D    R        O 17 32REFFLD(M5004D *LIBL/EDIM)
     A                                      TEXT('Beløp Depositum')
     A                                      EDTCDE(4)
     A                                 18  3'Ventegrupe . . . . . . . :'
     A            WVEN      R        O 18 32REFFLD(MVEN *LIBL/EDIM)
     A                                      TEXT('Ventekode')
     A                                 18 48'Blank=Sende direkte '
     A                                 19  3'Testekod . . . . . . . . :'
     A            W0035     R        O 19 32REFFLD(M0035 *LIBL/EDIM)
     A                                 19 48'Blank=Produksjon'
     A                                 20  3'Ekspederingsprioritet. . :'
     A            W9N01     R        O 20 32REFFLD(M9N01 *LIBL/EDIM)
     A                                      EDTCDE(4)
     A                                 20 48'1-4 (1=Express)'
N02  A  30 39                          24  2'Du må benytte en av de 3 melding I-
N02  A                                      D på skjermen'
N02  A                                      COLOR(WHT)
N02  A  30N39                          24  2'Du må benytte en av de 5 melding I-
N02  A                                      D på skjermen'
N02  A                                      COLOR(WHT)
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
      * START
     A          R WINDOWSZ
     A                                      WINDOW(7 25 4 30 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDNOR
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                  1  2'SEKVENS'
     A                                      COLOR(BLU)
     A                                  1 11'SEKVENS/VERSJON'
     A                                      DSPATR(HI)
     A                                  2  2'SEKVENS'
     A            M0068B         6Y 0O  2 14DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  3  2'VERSJON'
     A            M0068C         2Y 0O  3 14DSPATR(UL)
     A                                      COLOR(BLU)
      *
     A          R BILDE03
     A                                      CF12(12 'TILBAKE')
     A                                  8  5'BTUTT VEKT PÅ HOVED-SIDE........:'
S03  A*           SEVKB          9  0O  8 40TEXT('BRUTTO VEKT')
S05  A*           SEVKB         11  0O  8 40TEXT('BRUTTO VEKT')
N05  A            SEVKB143      14  3O  8 40TEXT('BRUTTO VEKT')
     A                                      COLOR(WHT)
S05  A*                                     EDTCDE(4)
N05  A                                      EDTCDE(L)
     A                                 10  5'SUM AV BRUTTO VEKT PÅ VARELINJER:'
S01  A*           WSVKB          9  0O 10 40TEXT('BRUTTO VEKT')
S03  A*           WSVKB         12  3O 10 40TEXT('BRUTTO VEKT')
N03  A            WSVKB         15  3O 10 40TEXT('BRUTTO VEKT')
     A                                      COLOR(WHT)
S01  A*                                     EDTCDE(4)
N01  A                                      EDTCDE(L)
     A                                 12  5'DIFFERANSE AV BRUTTO VEKT.......:'
S01  A*           WSVKTD         9  0O 12 40TEXT('DIFF AV BRUTTO VEKT')
S03  A*           WSVKTD        12  3O 12 40TEXT('DIFF AV BRUTTO VEKT')
N03  A            WSVKTD        15  3O 12 40TEXT('DIFF AV BRUTTO VEKT')
     A                                      COLOR(RED)
     A                                      EDTCDE(L)
     A                                 14  5'AKSEPTERER DU DIFFERANSE ?       '
     A            WSJN           1A  B 14 40DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 14 50'(J/N)'
     A                                 23  5'F12 = FORRRIGE'
     A                                      COLOR(BLU)
