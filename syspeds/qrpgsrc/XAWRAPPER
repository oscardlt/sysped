 /*OMS*********************************************************************/
 /*OMS* Thenon Log: Obj:‚XAWRAPPER €Type:‚RPGLE_SRC €Lvl:‚*BAS‚          €*/
 /*OMS*                                                                   */
 /*OMS*AplVernIR/CR    Source Retrieval Information             Rls  €*/
 /*OMS* RNS V019 000073/02 *CHG  28/02/07  QPGMR     /KTURNER             */
 /*OMS*                Presentation Repository change for Unity Media     */
 /*OMS* RNS V018 000067/01 *CHG  19/02/07  QPGMR     /KTURNER       00011 */
 /*OMS*                Rico drag and drop widget                          */
 /*OMS* RNS V017 000066/01 *CHG  05/02/07  QPGMR     /KTURNER       00010 */
 /*OMS*                Version 1.01.1 - installation fixes                */
 /*OMS* RNS V016 000060/01 *CHG  10/01/07  QPGMR     /KTURNER       00009 */
 /*OMS*                Accordion widget                                   */
 /*OMS* RNS V015 000053/07 *CHG  02/01/07  QPGMR     /BTHURLEY      00008 */
 /*OMS*                Open Source Project                                */
 /*OMS* RNS V014 000053/01 *CHG  11/12/06  QPGMR     /KTURNER       00008 */
 /*OMS*                Open Source Project                                */
 /*OMS* RNA V013 000022/01 *CHG  01/07/06  QPGMR     /KTURNER       00005 */
 /*OMS*                Replace lightweight ajax request for headings etc  */
 /*OMS* RNA V012 000017/02 *CHG  14/06/06  QPGMR     /DBAILEY       00002 */
 /*OMS*                Renaissance - Phase III service pgms               */
 /*OMS* RNA V011 000015/07 *CHG  30/05/06  QPGMR     /KTURNER       00002 */
 /*OMS*                Renaissance - Tab Dialog Edit + Performance tweaks */
 /*OMS* RNA V010 000015/02 *CHG  27/05/06  QPGMR     /KTURNER       00002 */
 /*OMS*                Renaissance - Phase II                             */
 /*OMS* RNA V009 000015/01 *CHG  19/05/06  QPGMR     /DBAILEY       00002 */
 /*OMS*                Renaissance - Phase II                             */
 /*OMS* RNA V008 000001/16 *CHG  08/05/06  QPGMR     /KTURNER       00002 */
 /*OMS*                Renaissance - Initial Development                  */
 /*OMS* RNA V007 000001/15 *CHG  25/04/06  QPGMR     /DBAILEY       00002 */
 /*OMS*                Renaissance - Initial Development                  */
 /*OMS* RNA V006 000001/04 *CHG  16/04/06  QPGMR     /KTURNER       00002 */
 /*OMS*                Renaissance - Arena Development - Phase I          */
 /*OMS* RNA V005 000001/11 *CHG  13/04/06  QPGMR     /DBAILEY       00002 */
 /*OMS*                Renaissance - Initial Development                  */
 /*OMS* RNA V004 000001/05 *CHG  29/03/06  QPGMR     /DBAILEY       00002 */
 /*OMS*                Renaissance - Initial Development -APIs            */
 /*OMS* RNA V003 000001/01 *CHG  22/02/06  QPGMR     /KTURNER       00002 */
 /*OMS*                Renaissance - Initial Development                  */
 /*OMS* RNA V002 000001/03 *CHG  21/02/06  QPGMR     /KTURNER       00002 */
 /*OMS*                Renaissance - Initial Development                  */
 /*OMS*********************************************************************/
      *+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+*+**
      * ***** BEGIN LICENCE BLOCK *****                    *
      * Version MPL 1.1                                    *
      *                                                    *
      * The contents of this file are subject to the       *
      * Mozilla Public Licence Version 1.1                 *
      * (the "Licence");  you may not use this file except *
      * in compliance with the Licence. You may obtain a   *
      * copy of the Licence at http://www.mozilla.org/MPL/ *
      *                                                    *
      * Software distributed under the Licence is          *
      * distributed on an "AS IS" basis, WITHOUT WARRANTY  *
      * OF ANY KIND, either express or implied.  See the   *
      * Licence for language governing the rights and      *
      * limitations under the Licence.                     *
      *                                                    *
      * The original code is "Renaissance Framework:       *
      * http://www.renaissanceframework.com".              *
      *                                                    *
      * The initial developer of the code is "CoralTree    *
      * Systems Ltd: http://www.coraltree.co.uk".          *
      *                                                    *
      * Portions created by "CoralTree Systems Ltd" are    *
      * Copyright (c) 2005-2007 CoralTree Systems Ltd.     *
      * All Rights Reserved.                               *
      *                                                    *
      *   Contributor(s):    ______________________        *
      *                                                    *
      * ***** END LICENCE BLOCK *****                      *
      *.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.**
      /copy qrpgleref,hspecsserv
      /copy qrpgleref,hspecsbnd
     H NoMain
     H********************************************************************************
     H*  RNS page wrapper
     H*
     H*  This service program provides the programmer with the functionality
     H*  required to build a basic page. This will include the standard
     H*  headings, menu treeview, footer etc
     H*
     H*  Author:       Kevin Turner
     H*  Date:         Nov 2005
     H*
     H********************************************************************************
V014 H*V003  *
V014 H*  Modification log
V014 H*  ================
V014 H*  Inits  Date     Modification
V014 H*  =====  ====     ============
V014 H*   JR  13/12/06  Added charting function JS files to path substitutions in
V014 H*                 WRP_Initialise
V016 H*   KT  13/12/06  Get logo and header image from theme if not present on environment
V016 H*   KT  10/01/07  Added functionality for accordian widget from rico
V016 H*   KT  18/01/07  Removed MSG_Inz() and put it in XC0003 so that we only have it initialised
V016 H*                 once per request
V018  *  KT   19/02/07  Drag and Drop widget
V014 H*
V014 H**************************************************************************

V009 FXSESSFUN  IF   E           K DISK    extfile('RNSWORK/XSESSFUN')
V008
      * Prototype definitions and standard system API error structure
      /copy qrpgleref,prototypeb
      /copy qrpgleref,qusec
      /copy qrpgleref,xacgi
      /copy qrpgleref,xaprompt
      /copy qrpgleref,xaenviron
      /copy qrpgleref,xaconfig
      /copy qrpgleref,xawrapper
      /copy qrpgleref,xamenutree
      /copy qrpgleref,xasession
      /copy qrpgleref,xastring
V015  /copy qrpgleref,xamessage
V014  /copy qrpgleref,xathenon
V014  /copy qrpgleref,xatheme
      *--------------------------------------------------------------------
      * Determine file accessibility
      *
      * int access(const char *path, int amode)
      *
      *--------------------------------------------------------------------
     D access          PR            10I 0 ExtProc('access')
     D   Path                          *   Value Options(*string)
     D   amode                       10I 0 Value

      **********************************************************************
      * Access mode flags for access() and accessx()
      *
      *   F_OK = File Exists
      *   R_OK = Read Access
      *   W_OK = Write Access
      *   X_OK = Execute or Search
      **********************************************************************
     D F_OK            C                   0
     D R_OK            C                   4
     D W_OK            C                   2
     D X_OK            C                   1
      **********************************************************************

V003 D BuildWorkArea   PR              n   extproc(g.pWorkAreaPrc)
V003 D BuildMenu       PR              n   extproc(g.pMenuTreePrc)
     D BuildMenuTree   PR              n
     D OutputButtons   PR              n
     D OutputMessages  PR              n
V003 D  autoPB                         n   const
V003 D BuildMsgArray   PR              n

      * Constant for UpdHTMLVar subprocedure
     D initHTMLVars    c                   '0'

     D pssrswitch      s               n   inz(*off)                            switch for pssr

      * Program timing variable
     D sec             s             15p 6

       // g is passed between procedures
     D g               ds                  qualified
V012 D  ajax                           n
V012 D  fullDisplay                    n
V012 D  popup                          n
V003 D  pWorkAreaPrc                   *   procptr
V003 D  pMenuTreePrc                   *   procptr
     D  caller                             like(wrpName)
     D  title                              like(wrpHeading) inz('')
     D  heading                            like(wrpHeading) inz('')
     D  subheading                         like(wrpHeading) inz('')
     D  logo                               like(wrpHeading) inz('')
     D  headerImg                          like(wrpHeading) inz('')
V008 D  sectionHead                        like(wrpHeading) inz('')
     D  theme                       256
     D  cr                           10
     D  crPath                      256    varying
     D  docPath                     256    varying
     D  focusOn                            like(wrpName)
     D  session                            like(wrpName)
     D  userid                             like(wrpName)
     D  environ                            like(wrpName)
     D  environd                     25
     D  xsession                      2
     D  wroteTop                       n   inz(*off)
     D  wrotePssr                      n   inz(*off)

     D  XPROMPTds    E DS                  extname(XPROMPT)
     D                 DS
V009 D gResult                      390
V009 D gText1                              Like(XPRTEXT1)
V009 D                                     Overlay(gResult )
     D
V009 D gText2                              Like(XPRTEXT2)
V009 D                                     Overlay(gResult :*next)
V009 D gText3                              Like(XPRTEXT3)
V009 D                                     Overlay(gResult :*next)
V009 D gTip1                               Like(XPRTIP1)
V009 D                                     Overlay(gResult :*next)
V009 D gTip3                               Like(XPRTIP2)
V009 D                                     Overlay(gResult :*next)
V009 D gNullPr                             Like(XPRNULLPR)
V009 D                                     Overlay(gResult :*next)
V009 D gConfTxt                            Like(XPRCONFTXT)
V009 D                                     Overlay(gResult :*next)
V013 D gChgPwdLink     s           1024    varying
V013 D gAjaxFeedback   s          16384    varying
V013 D gScrollPos      s          16384    varying
     D gVersion        s             10    dtaara('VERSION')
     D gUnlicensed     s               n
     D gUnlicTree      s            100    dim(15) ctdata perrcd(1)
V009 D gTreeView       s               n   dtaara('TREEVIEW')
V006
V006 D quot            c                   const('''')
V011 D EOF             c                   const('<EOF>')

      * For program status data structure and program status subroutine
     D psds           sds                                                       Pgm status DS
     D   psdsdata                   429                                         The data
V003 D   wrapper                     10    overlay(psdsdata:1)
V003 D   jobName                     10    overlay(psdsdata:244)
V003 D   jobUser                     10    overlay(psdsdata:254)
V003 D   jobNum                       6    overlay(psdsdata:264)

      // Procedures-------------------------------------------------


     P WRP_Initialise  B                   export
      *
      * Initialise the wrapper so that it can perform the necessary tasks
      *
     D WRP_Initialise  PI              n
      *  Calling program name.
     D  caller                             like(wrpName) value
      *  xPath to work area HTML. The root is obtained from the config.
     D  workAreaHTML                       like(wrpXPath) value
      *   Optional parameter containing a pointer to the procedure to
      *   build the menu tree
V003 D  pMenuTreePrc                   *   procptr options(*nopass)
     D                                     value
      *   Optional parameter containing the url for the 'up one level'
      *   link
     D  upLevelUrl                  256    const options(*nopass)
      *
     D  lWrapperHTML   s            256
     D  lWorkAreaHTML  s            256
V003 D  lAjax          s               n
V003 D  lMenuLicChk    s               n   static

      /free

       g.wrotePssr=*off;

       //  Determine the document paths
       WRP_SetPathDfts();

       //  Get paths to documents we need
V017   lWrapperHTML = WRP_GetPath('templates/xhwrapper.html');
V017   lWorkAreaHTML =  WRP_GetPath('templates/'+workAreaHTML);
       //  Load up the HTML we will need
       wrpHTML = %trim(lWrapperHTML) + ' ' + %trim(lWorkAreaHTML);
       gethtmlifs(wrpHTML:'<as400>');

       // Get environment info
       g.session  = EN_Session;
       g.userid   = EN_User;
       g.environ  = EN_Environ;
       g.environd = EN_EnvironD;
       g.xsession = EN_ExtSess;
       g.theme    = EN_Theme;
V003

       // Set up rest of global data
       g.caller=caller;
       STR_SetCaller(caller);
       // Put the calling program into the session as the current program
       SES_Set('current.pgm':%trim(caller));

       //  Set debug options
       CGI_setDebug();

       //   Clear messages
       ClrMsgs();

       // Initialise substitution variables
       updHTMLVar('me':g.caller:initHTMLVars);
       updHTMLVar('session':g.session);
       updHTMLVar('theme':g.theme);
V017   //updHTMLVar('docpath':%subst(g.docPath:2));
V017   updHTMLVar('docpath':'');
V017   updHTMLVar('theme.css':STR_Substitute(
V017                        WRP_GetPath('styles/' + %trim(g.theme) + '.css':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('stylesheet.css':STR_Substitute(
V017                          WRP_GetPath('styles/stylesheet.css':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('wrapper.js':STR_Substitute(
V017                           WRP_GetPath('scripts/wrapper.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('keyboard.js':STR_Substitute(
V017                           WRP_GetPath('scripts/keyboard.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('datepicker.js':STR_Substitute(
V017                           WRP_GetPath('scripts/datepicker.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('ajaxrequest.js':STR_Substitute(
V017                           WRP_GetPath('scripts/ajaxrequest.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('ua.js':STR_Substitute(
V017                           WRP_GetPath('scripts/ua.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('ftiens4.js':STR_Substitute(
V017                           WRP_GetPath('scripts/ftiens4.js':*on)
V017                          :'Æ':'%23'));
V017   // Charting functions
V017   updHTMLVar('mochikit.js':STR_Substitute(
V017                           WRP_GetPath('scripts/mochikit/mochikit.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('excanvas.js':STR_Substitute(
V017                           WRP_GetPath('scripts/plotkit/excanvas.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('plotkit_packed.js':STR_Substitute(
V017                        WRP_GetPath('scripts/plotkit/plotkit_packed.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('charting.js':STR_Substitute(
V017                           WRP_GetPath('scripts/charting.js':*on)
V017                          :'Æ':'%23'));
V017   // rico widgets
V017   updHTMLVar('prototype.js':STR_Substitute(
V017                           WRP_GetPath('scripts/prototype.js':*on)
V017                          :'Æ':'%23'));
V017   updHTMLVar('rico.js':STR_Substitute(
V017                           WRP_GetPath('scripts/rico.js':*on)
V017                          :'Æ':'%23'));
V003   updHTMLVar('jobtxt':'');
V003   updHTMLVar('jobdtls':'');
V003   updHTMLVar('crtxt':'');
V003   updHTMLVar('cr':'');
V003   // For developers, output the details of this job
V003   if EN_Mode='DEV';
V003     updHTMLVar('jobtxt':'Server:');
V003     // Update the job details on the page
V003     updHTMLVar('jobdtls':%trim(jobNum) +
V003                            '/' + %trim(jobUser) + '/' + %trim(jobName));
V003     if g.cr<>*blanks;
V003       updHTMLVar('crtxt':'C/R:');
V003       updHTMLVar('cr':g.cr);
V003     endif;
V003   endIf;

       // Get version
       in gVersion;
       updhtmlvar('version':
                  PR_GetConst('':'VERSION':gVersion));

       // Copyright notice
       updhtmlvar('copyright':
                  PR_GetConst('':'COPYRIGHT'));
V003
       //   Clear this program's button section
       WRP_ClearButtons();

       //   Clear this program's message section
       WRP_ClearMsgs();

       //  Write qualified job name to debug file.  The *on
       //  parameter forces output even if debugging is off.
       //  Remove this parameter or change it to *off if you
       //  want the output only if debugging is on.
       wrtjobdbg();

       //  Initialise the global variables in the prompt utility program
       PR_InitPrompt(caller);

       //  Initialise the error stuff.
V015   // ***Now in XC0001 so that it only occurs once per request
V015   //MSG_Inz();

       // If the caller has not provided a pointer to the procedure
       // that needs to load the tree view, default in the standard
       // menu builder and hide the "up one level" and XML view icons.
       if %parms<3;
V003      g.pMenuTreePrc=%paddr(BuildMenuTree);
          updHTMLVar('uplevel':'upLevelHidden');
          updHTMLVar('uplevellink':*blank);
          updHTMLVar('uplevelimg':*blank);
          // Also hide the XML view image button
          updHTMLVar('xmlview':'xmlViewHidden');
          updHTMLVar('xmlviewimg':*blank);
       else;
V003      g.pMenuTreePrc=pMenuTreePrc;
          updHTMLVar('uplevel':'upLevel');
          // If we haven't received a url for the uplevel icon, use
          // the default
          if %parms<4;
            updHTMLVar('uplevellink':
                        'javascript:treeRequest(&Æ39;XC0001&Æ39;)');
          else;
            updHTMLVar('uplevellink':upLevelUrl);
          endif;
          updHTMLVar('uplevelimg':%trim(PR_GetIcon('':'C':'UPLEVEL')));
       endif;
V003
V003   // Set a local variable with the AJAX preference
V003   lAjax=
V003       CFG_GetXPathVal('DEVPREFS/' + %trim(EN_User()) + '/AJAX');
V003   // Default to *on if not found
V003   if CFG_Error();
V003     lAjax=*on;
V003   endif;

V009   // Initialise standard buttons at top of menu
V009   gResult  = PR_GetIcon('':'C':'LOGOUT');
V009   updHTMLVar('logoutimg':%trim(gResult ));
V009   gResult  = PR_GetIcon('':'C':'HELP');
V009   updHTMLVar('helpimg':%trim(gResult ));
V009   gResult  = PR_GetIcon('':'C':'PRINTER');
V009   updHTMLVar('printerimg':%trim(gResult ));
V009   gResult  = PR_GetIcon('':'C':'EXIT');
V009   updHTMLVar('exitimg':%trim(gResult ));
V009   gResult  = PR_GetIcon('':'C':'REFRESH');
V009   updHTMLVar('refreshimg':%trim(gResult ));
V009   // Initialise standard button to pop up messages
V009   gResult  = PR_GetIcon('':'C':'POPUPMSGS');
V009   updHTMLVar('popupmsgsimg':%trim(gResult ));

V009   // At this point, if the environment allows the "Change password"
V009   // facility, build an appropriate link
V009   if EN_ChgPwd() and gChgPwdLink=*blanks;
V009     gChgPwdLink='<a class="chgPwdLink" href="javascript:treeRequest(';
V009     gChgPwdLink=gChgPwdLink + quot +
V003       lAjax + quot + ', ';
V009     gChgPwdLink=gChgPwdLink + quot + 'XC0007' + quot + ', ';
V009     gChgPwdLink=gChgPwdLink + quot + 'X' +quot + ', ';
V009     gChgPwdLink=gChgPwdLink + quot + 'FILEMAINT' + quot + ', ';
V009     gChgPwdLink=gChgPwdLink + quot + '9' + quot +                      ',';
V009     chain (g.session:'X':'FILEMAINT':'XC0007') XSESSFUNR;
V009     if not %found;
V009       XSFLOGREQ='Y';
V009     endif;
V009     gChgPwdLink=gChgPwdLink + quot + XSFLOGREQ + quot;
V009     gChgPwdLink=gChgPwdLink + ');">';
V009     gChgPwdLink=gChgPwdLink + %trim(PR_GetIcon('':'C':'CHGPWD'));
V009     gChgPwdLink=gChgPwdLink + '</a>';
V009     updHTMLVar('chgpwdlink':gChgPwdLink);
V009   else;
V009     updHTMLVar('chgpwdlink':'');
V009   endif;

       // Initialise constants at top of menu
       PR_setConst('':'USRTXT');
       PR_setConst('':'ENVTXT');
       PR_setConst('':'SESSTXT');
V008   SES_Set('pgmtxt':PR_GetConst('':'PGMTXT'));
V008   updhtmlvar('pgmtxt':SES_Get('pgmtxt'));

       // Is the user licensed to use the tree view without a link at the top?
       if not lMenuLicChk;
         lMenuLicChk=*on;
V009     in(e) gTreeView;
         gUnlicensed=not %error;
       endif;

       Return *off;

      /end-free

     P                 E


     PWRP_SetPathDfts...
     P                 B                   export
      *
      * Get the path to the item in question based on developers preferences
      *
     DWRP_SetPathDfts...
     D                 PI

      /free
       g.docPath = CFG_DocPath;
V002   g.cr=*blanks;
V002   g.crPath=*blanks;
V002   if EN_Mode='DEV';
V002     g.cr=CFG_GetXPathVal('DEVPREFS/' + %trim(EN_User) + '/CR');
V002   endIf;
       if g.cr<>*blanks;
         g.crPath=STR_Substitute(CFG_CrPath():'/%cr%/':
                                 %trim(STR_Substitute(g.cr:'OÆ':'cr')));
       endIf;
      /end-free

     P                 E


     P WRP_GetPath     B                   export
V017  *
V017  * Get the path to the item in question based on developers preferences
V017  *
V017 D WRP_GetPath     PI           256    varying
V017 D   objPath                    256    const varying
V017 D   htmlLink                      n   const options(*nopass)
V017  *
V017 D   lRoot         s            256    varying
V017 D   lTemplate     s               n
V017 D   lCRPath       s            256    varying
V017 D   lObjPath      s            256    varying
V017 D   lObjPathS     s            256    varying
V017 D   lFound        s               n   inz(*off)
V017 D   lCRRtv        s               n   static
V017 D   lAbsolute     s               n   inz(*off)
V017 D   lSeecÆ        s              3  0
V017
V017  /free
V017   // If the path starts with a '/' or 'http:' or 'https:' or '\\'
V017   // then assume they are using an absolute path and don't
V017   // tamper with it
V017   if (%subst(objPath:1:1)='/' or
V017       str_upper(%subst(objPath:1:2))='\\' or
V017       str_upper(%subst(objPath:1:5))='HTTP:' or
V017       str_upper(%subst(objPath:1:6))='HTTPS:');
V017     // Leave alone
V017     lObjPath = objPath;
V017     lAbsolute=*on;
V017   else;
         %len(lRoot)=0;
         if %scan('templates/':objPath)>0;
           lRoot=%trim(CFG_Root);
           lTemplate=*on;
         else;
           lRoot=%trim(CFG_DocPath);
         endif;
V017     if %parms>=2;
V017       if htmlLink;
V017         lObjPath = objPath;
V017       else;
V017         lObjPath = lRoot + objPath;
V017       endif;
V017     else;
V017       lObjPath = lRoot + objPath;
V017     endif;
V017   endif;
V017   // Replace theme if required
V017   lObjPath = STR_Substitute(lObjPath:'/%theme%/':%trim(g.theme));
V017   lObjPathS = lObjPath;
V017   %len(lObjPathS)=%len(%trim(lObjPath));
V017   %len(lObjPath)=%len(lObjPathS);
V017   // See if this user has developer preferences that indicate the use of a CR
V017   if g.cr<>*blanks and not lAbsolute;
V017     if not lCRRtv;
V017       gSeeFldrA=SEE_GetFldrCode('DEV':'RNS');
V017       lCRRtv=*on;
V017     endif;
V017     // See if required object is in the CR
V017     for lSeecÆ=1 to %elem(gSeeFldrs);
           if gSeeFldrs(lSeecÆ)=*blanks;
V017         leave;
           endif;
           lCRPath=%trim(g.crPath);
           if lTemplate;
V017         lObjPath=lCRPath + %trim(STR_Substitute(objPath:'templates/':''));
           else;
V017         lObjPath=lCRPath + objPath;
           endif;
V017       lObjPath=STR_Substitute(lObjPath:'/%theme%/':%trim(g.theme));
V017       lObjPath=STR_Substitute(%trim(lObjPath):'/%p%/':'p' +
V017                                                gSeeFldrs(lSeecÆ));
V017       if access(%trim(lObjPath):F_OK)>=0;
V017         // Found it !
V017         lFound=*on;
V017         leave;
V017       endIf;
V017     endFor;
V017     if not lFound;
V017       // Not found
V017       lObjPath=lObjPathS; // Restore default path
V017     endif;
V017   endIf;
V017   return lObjPath;
V017  /end-free
V017
     P                 E


     P WRP_SetFocus    B                   export
      *
      * Overwrite the field we want to focus on.
      * NOTE:  This procedure will only work when the system is in Ajax
      *        mode.
      *
     D WRP_SetFocus    PI              n
     D   focusField                  10    const
     D   force                         n   const options(*nopass)
      *
V011 D headingHtml     S          16384    varying
V011 D part1           S          16384    varying
V011 D part2           S          16384    varying
     D split           S              5P 0
     D lastSplit       S              5P 0
     D count           S              5P 0
     D focusLen        S              5P 0
     D focusOn         S             10
     D doIt            S               n

      /free
       if not EN_Ajax();
         // Fail
         return *on;
       else;
         g.focusOn = focusField;
V003     headingHtml=SES_Get('ajax.feedback');
V011     if headingHtml=*blanks;
V011       %len(headingHtml)=0;
V011     else;
V011       %len(headingHtml) = %scan(EOF:headingHtml)-1;
V011     endif;
V008     // Replace the sixth element of the headings with the
         // new focus field.
         split=1;
         count=0;
         split=%scan('|':headingHtml:split);
         dow split>1;
           count+=1;
V008       // If the count is 5, we have reached the start of
           // the field we want to focus on
V008       if count=5;
             part1=%subst(headingHtml:1:split);
           else;
V008         // If the count is 6, we have reached the start of
             // the help url and default buttons etc
V008         if count=6;
               focusLen=split-lastSplit-1;
               focusOn=%subst(headingHtml:(lastSplit+1):focusLen);
               part2=%subst(headingHtml:split);
               leave;
             endif;
           endif;
           lastSplit=split;
           split=%scan('|':headingHtml:(split+1));
         enddo;
V008     // We should arrive at the fifth occurrence of the bar.
         // The code after that represents the previous focus on
V008     if count>=5;
           // Only set the focus if it is currently blank or we have
           // been told to force it
           if focusLen<=0 or focusOn=*blanks;
             doIt=*on;
           else;
             if %parms>1;
               doIt=force;
             endif;
           endif;
           if doIt;
             headingHtml = part1 + g.focusOn + part2;
V013         SES_Set('ajax.feedback':headingHtml + EOF);
           endif;
           return *off;
         else;
           // Fail
           return *on;
         endif;
       endif;

      /end-free

     P                 E


     P WRP_Render      B                   export
      *
      * Render the page
      *
     D WRP_Render      PI              n
      *  Address of procedure to execute to load work area section.
V003 D  pWorkAreaPrc                   *   procptr value
      *   Form field to focus on
     D  focusOnField                       like(wrpName) options(*nopass)
     D                                     const
      *  Optional parameters
     D  sectionHead                        like(wrpHeading) options(*nopass)
     D                                     value
     D  replaceValue                 60    dim(10) const options(*nopass)
     D  title                              like(wrpHeading) options(*nopass)
     D                                     value

V011 D headingHtml     S          16384    varying
     D wkHlpLnk        s             80
V011 D lOnUnload       s           1024    inz('onunload="trapUnload(') varying
V011 D lOnLoad         s          16384    varying
V011 D lNewOnLoad      s          16384    varying
V006 D lJScriptEnd     s              3    inz(');"')
V011 D lPgmTxt         s            128    varying
V011 D lFastPath       s          16384    varying
V012  * Define work fields to contain the name of sections whose id
V012  * we may want to alter at run time (i.e. to hide them perhaps)
V012 D lHeaderDiv      s             30    varying inz('header')
V012 D lSessionDiv     s             30    varying inz('session')
V012 D lSectionsDiv    s             30    varying inz('sections')
V012 D lLeftBtnDiv     s             30    varying inz('leftSectionButtons')
V012 D lLeftDiv        s             30    varying inz('leftSection')
V012 D lCentreDiv      s             30    varying inz('centreSection')
V018 D lCentreDivStyl  s             30    varying inz('style="width:84%"')
V012 D lBottomDiv      s             30    varying inz('bottomSection')
V003 D lAjax           s               n
V003 D lX              s             10i 0

      /free

V003   // Set a local variable with the AJAX preference
V003   lAjax=
V003       CFG_GetXPathVal('DEVPREFS/' + %trim(EN_User()) + '/AJAX');
V003   // Default to *on if not found
V003   if CFG_Error();
V003     lAjax=*on;
V003   endif;
V003
       //  Render the page
       //  Deal with optional parameters
       if %parms<2;
         g.focusOn=*blanks;
       else;
         g.focusOn=focusOnField;
       endif;
       // Section Heading
       if %parms<3;
         g.sectionHead=*blanks;
       else;
        if %parms<4;
          g.sectionHead=PR_GetConst('':STR_Upper(sectionhead));
        else;
          g.sectionHead=PR_GetConst('':STR_Upper(sectionhead):replaceValue);
        endif;
       endif;
       if %parms<5;
         g.title='Renaissance';
       else;
         g.title=%trim(title);
       endif;
V003
V003   // Make sure nothing thinks we are doing an autopostback
V003   SES_Remove(%trim(g.caller) + '.postingback');

       // Heading (usually the module description)
       if EN_Module=*blanks;
         g.heading= 'Renaissance';
       else;
V013     g.heading= PR_GetDisplayValue('XFUNC':'XFUMODULE':EN_Module);
       endif;
       // Sub heading (usually the function group)
       if EN_FuncGroup=*blanks;
         g.subheading=*blanks;
       else;
V013     g.subheading=  PR_GetDisplayValue('XFUNC':'XFUGROUP':
V013                                             EN_FuncGroup);
       endif;
       // Get logo path
       g.logo =CFG_GetXpathVal('PREFS/' + %trim(EN_User()) +
                         '/LOGOPATH');
       if g.logo=*blanks;
         g.logo = CFG_GetXpathVal('ENV/GLOBAL/LOGOPATH');
V014     if g.logo=*blanks;
V014       g.logo = THM_Logo();
V014     endif;
       endIf;

V017   g.logo=%trim(STR_Substitute(WRP_GetPath(g.logo:*on):'Æ':'%23'));

       // Get header image path
       g.headerImg =CFG_GetXpathVal('PREFS/' + %trim(EN_User()) +
                         '/HEADPATH');
       if g.headerImg=*blanks;
         g.headerImg = CFG_GetXpathVal('ENV/GLOBAL/HEADPATH');
V014     if g.headerImg=*blanks;
V014       g.headerImg = THM_HeadingImg();
V014     endIf;
       endIf;

V017   g.headerImg=%trim(STR_Substitute(WRP_GetPath(g.headerImg:*on):
V017                                                     'Æ':'%23'));

       //  Initialise global data with work area procedure pointer
V003   g.pWorkAreaPrc=pWorkAreaPrc;

       // Initial variables
V012   g.ajax = EN_Ajax;
V012   g.popup = zhbgetvar('popup');
V012   if not g.popup;
V012     g.fullDisplay = (g.ajax=*off);
V012   else;
V012     g.fullDisplay = *on;
V012   endif;
V006   PR_SetVar('fSession':g.session);
V006   PR_SetVar('fProgram':g.caller);
V006   PR_SetVar('fAction':*BLANK);
V012   updHTMLVar('ajax':g.ajax);
       // Always return the scroll position if not in ajax mode
       if not g.ajax;
         gScrollPos=STR_Latin1Encode(zhbgetvar('fScrollPos'));
V011     updHTMLVar2('fScrollPos':%addr(gScrollPos)+2:%len(gScrollPos));
       else;
V011     updHTMLVar('fScrollPos':'');
       endif;

       // Set up data for writing standard output.

        // Clear the HTML buffer
        ClrHtmlBuffer();

        // Main body
V006    PR_SetVar('title':g.title);
V006    PR_SetVar('heading':g.heading);
V006    PR_SetVar('subheading':g.subheading);
V006    PR_SetVar('sectionheading':g.sectionHead);
V006    PR_SetVar('logo':g.logo);
V006    PR_SetVar('headerimg':g.headerImg);
V006    PR_SetVar('userid':g.userid);
V006    PR_SetVar('environ':g.environ);
V006    PR_SetVar('environd':g.environd);
V006    PR_SetVar('xsession':g.xsession);
V008    PR_SetVar('pgmdtls':g.caller);
V006    PR_SetVar('progress':'progress_stopped.gif');
        // Write sections of HTML.
        wrtsection('wrptop');

V012    if g.fullDisplay;
          // Write sections of HTML.
          wrtsection('wrpbody');
          // Build menu tree
V012      if not g.popup;
V012        BuildMenu();
V012      endif;
          // Set focus (if any)
          PR_FocusOn(g.focusOn);
V012      lOnload=SES_Get('onload');
          if not lAjax;
            lNewOnLoad=
V012           'onload="gScrollableElements=deserialiseScrollElements(' +
V012                   'document.getElementsByName(' + quot +
V012                                            'fScrollPos' +
                                                 quot + ')[0].value);' +
V012                   'restoreScrollPosition();';
V012        lOnload=STR_Substitute(lOnload:
V012                               'onload="':
V012                               lNewOnLoad
V012                               );
          endif;
V012      if not g.popup;
V006        // We need to trap the unload event. The function requires the
V006        // AJAX setting for the user. We can't use EN_Ajax at this point
V006        // because it may be artificially set to '0' just so we get the
V006        // basic screen out. Therefore we need to go direct to the config
V006        // to derive the AJAX setting for the user.
V012        updHTMLVar('onunload':lOnUnload + quot +
V003           lAjax + quot + lJScriptEnd);
V003        // For developers/testers, override the timeout
V003        if EN_Mode<>'PROD';
              lNewOnLoad=
V012             'onload="overrideTimeout(3600000);';
V012          lOnload=STR_Substitute(lOnload:
V012                                 'onload="':
V012                                 lNewOnLoad
V012                                 );
V012          PR_OutOnLoad(lOnLoad);
            endif;
V012      else;
V012        // We don't want the user logged off if they close a pop-up
V012        updHTMLVar('onunload':'');
V012        // We want to perform all the standard ajax feedback when
V012        // the popup window first loads. We can do this using
V012        // the renderPage function without any parms
V012        if g.ajax;
              lNewOnLoad=
V012               'onload="setPopupMode();renderPage();';
V003          // For developers/testers, override the timeout
V003          if EN_Mode<>'PROD';
                lNewOnLoad=
V012               'onload="overrideTimeout(3600000);';
              endif;
V012          lOnload=STR_Substitute(lOnload:
V012                                 'onload="':
V012                                 lNewOnLoad
V012                                 );
V012          PR_OutOnLoad(lOnLoad);
V012        endif;
V012      endif;
        endif;

        //  If we have any fields in error, let the service program know
        if not EN_Ajax();
          // Has to be done here in non-ajax mode because the onload
          // routine is part of the <body> element of the HTML
V015      PR_setErrorFld('A':MSG_Field(1):MSG_Field(2):
V015                  MSG_Field(3):MSG_Field(4):MSG_Field(5):
V015                  MSG_Field(6):MSG_Field(7):MSG_Field(8):
V015                  MSG_Field(9):MSG_Field(10));
          // It is possible that an error generated by the procedure
          // building the work area has changed the focus field, so give
          // this a chance to work. Passing a blank will not upset any
          // previous focus, but will allow errors to get focus
          PR_focusOn('');
        endif;

V012    if g.fullDisplay;
          wrtsection('wrpmainbody');
        endif;

        g.wrotetop = *on;


        // Create main container
V012    if g.ajax;
          // If we are using Ajax, store the HTML for the headings
          // in the session. The browser will retrieve it with another
          // lightweight ajax request. We will separate each part
          // of the HTML by a bar so that the javascript on
          // the client can parse and extract successfully
          headingHtml='<img class="logo" src="' +  %trim(g.logo) +
                                                 '">|';
          headingHtml=headingHtml + '<h2>/%heading%/</h2>|';
          headingHtml=headingHtml + '<h3>/%subheading%/</h3>|';
V003      // Also store the server job details
V003      headingHtml=headingHtml +
V003                  %trim(jobNum) +
V003                  '/' + %trim(jobUser) + '/' + %trim(jobName) + '|';
V008      // The fifth element will be the program details
V011      lPgmTxt=SES_Get('pgmtxt');
V011      if lPgmTxt=*blanks;
V011        %len(lPgmTxt)=0;
V011      else;
V011        %len(lPgmTxt)=%scan(' ':lPgmTxt)-1;
V011      endif;
V011      headingHtml=headingHtml + lPgmTxt +  ',' +
V008                                         g.caller + '|';
          // Also store the focusOn field as a hidden field
          headingHtml=headingHtml + %trim(g.focusOn) + '|';
          // Also store the updated help text link
V007      wkHlpLnk = 'javascript:helpWindow(''XCHELP.pgm?Page=' +
            %trim(g.caller) + ',' + %trim(PR_getData(' ':'FACTION')) +
            '&Lang=' + EN_Locale +
           ''');';
V011      headingHtml=headingHtml + %trim(wkHlpLnk) + EOF;

          headingHtml=STR_Substitute(%trim(headingHtml):'/%heading%/':
V013                             %trim(g.heading));
          headingHtml=STR_Substitute(%trim(headingHtml):'/%subheading%/':
V013                             %trim(g.subheading));
V011      SES_Set('ajax.feedback':headingHtml);
V012    endif;
V012
V012    // Change ids of sections we want to hide if we are in pop-up mode
V012    if g.popup;
V012      lSessionDiv = lSessionDiv + 'Hidden';
V012      lSectionsDiv = lSectionsDiv + 'Popup';
V012      lLeftBtnDiv = lLeftBtnDiv + 'Hidden';
V012      lLeftDiv = lLeftDiv + 'Hidden';
V012      lCentreDiv = lCentreDiv + 'Popup';
V018      lCentreDivStyl = *blanks;
V012      lBottomDiv = lBottomDiv + 'Popup';
V012    endif;
V012
V012    // Update the names of sections whose id may be variable
V012    updHTMLVar('header':lHeaderDiv);
V012    updHTMLVar('session':lSessionDiv);
V012    updHTMLVar('sections':lSectionsDiv);
V012    updHTMLVar('leftSectionButtons':lLeftBtnDiv);
V012    updHTMLVar('leftSection':lLeftDiv);
V018    updHTMLVar('centreSection':lCentreDiv);
V018    updHTMLVar('centreSectionStyle':lCentreDivStyl);
V012    updHTMLVar('bottomSection':lBottomDiv);
V012
V012    if g.fullDisplay;
          wrtsection('wrpmaincontainer');
V012      if not g.popup;
V012        wrtsection('wrpheader');
V012      endif;
V012      wrtsection('wrpsession');
          // Start of left section

          // Set the Help link to a relevant page
V014        wkHlpLnk = %trim(g.caller) + ',' + %trim(PR_GetData(' ':'FACTION'))
V014                    + '&Lang=' + EN_Locale;
            updHTMLVar('HelpPage':wkHlpLnk);

          wrtsection('wrpleftsection');
V012      if not g.popup;
            // Output unlicensed treeview link if appropriate
            if gUnlicensed;
              for lX=1 to %elem(gUnlictree);
                updhtmlvar('unlictreeview':gUnlictree(lX));
V007            wrtsection('wrpunlictreeview');
              endfor;
            endif;
            wrtsection('wrpmenutree');
          endif;
          wrtsection('wrpendleftsection');
        endif;

V003    // Build message array
V003    BuildMsgArray();

        // Create work area
V012    if g.fullDisplay;
          wrtsection('wrpworkarea');
        endif;

        // Create centre section
        wrtsection('wrpcentresection');

        // Execute the procedure requested to build the work area
        BuildWorkArea();

        // Error message cursor positioning
        if EN_Ajax();
          // Has to be done here in ajax mode because the programmer
          // may have overridden the field names in while building
          // the work area
V015      PR_setErrorFld('A':MSG_Field(1):MSG_Field(2):
V015                  MSG_Field(3):MSG_Field(4):MSG_Field(5):
V015                  MSG_Field(6):MSG_Field(7):MSG_Field(8):
V015                  MSG_Field(9):MSG_Field(10));
          // It is possible that an error generated by the procedure
          // building the work area has changed the focus field, so give
          // this a chance to work. Passing a blank will not upset any
          // previous focus, but will allow errors to get focus
          PR_focusOn('');
        endif;
V011
V011    // It is possible that the routine that built the page has
V011    // created a tab dialog edit. If this is true it will
V011    // be initialised by the lightweight Ajax call later on. If,
V011    // however, we are not in Ajax mode, we will need to clear it
V011    // from the session now.
V012    if not g.ajax;
V011      SES_Remove('tabdialogcontainer');
V013      SES_Remove('tabdialogcontainer.defaulttab');
V011    endif;
V008
V008    // Initialise fast path if required
V008    updHTMLVar('fastptxt':SES_Get('fastptxt'));
V011    lFastPath=SES_Get('fastpath');
        if not SES_Error();
V011      if lFastPath=*blanks;
V011        %len(lFastPath)=0;
V011      else;
V011        %len(lFastPath)=%scan(EOF:lFastPath)-1;
V011      endif;
V011      updHTMLVar2('fastpath':%addr(lFastPath)+2:%len(lFastPath));
        else;
V011      updHTMLVar('fastpath':'');
        endif;
V013
V013    // Provide ajax feedback. Poorly named but this is because
V013    // the data used to be retrieved via a separate ajax call
V013    if g.ajax;
V013      WRP_SetAjaxFeedback();
V013    else;
V013      updHtmlVar('ajaxFeedback':'');
V013    endif;
V014
V014    // Sort out any toggle areas
V014    PR_SetToggleControls();

        // End centre section
        wrtsection('wrpendcentresection');

        // Write button area
        OutputButtons();

        // Write message area
V003    OutputMessages(*off);

        // End work area
V012    if g.fullDisplay;
          wrtsection('wrpendworkarea');
        endif;

        // Get and write run time to debug file
        sec = TimerElapsed();
        wrtdebug(g.caller +
                 ': Execution time (seconds) ' +
                  %trim(%editc(sec:'N')):*on);
        // Finish off full page unless we are using Ajax
V012    if g.fullDisplay;
          wrtsection('wrpendhtml');
        endif;
        // Write the *fini section to ensure all buffered output is sent
        // to the browser.
        wrtsection('*fini');
V008
V008    // Initialise the fast path menu
V011    SES_Remove('fastptxt');
V011    SES_Remove('fastpath');
V008
       Return *off;

      /end-free
      ****************************************************************************
      * Program status subroutine
      ****************************************************************************
     C     *pssr         begsr
      /free
        // If have already been in pssr, get out to avoid looping
        if        pssrswitch=*on;
          *inlr = *on;
          return;
        endif;
        WRP_Pssr(psds);
        // Set on switch to indicate we've been here
        pssrswitch=*on;
        *inlr = *on;
        return;
      /end-free
     C                   endsr

     P                 E


     P WRP_AddButton   B                   export
     D WRP_AddButton   PI              n
     D  buttonHandle                 10    CONST
     D  formHandle                   10    CONST
     D  attributeIn                  10    CONST options(*nopass)
     D  parm1                       256    CONST options(*nopass)
     D  parm2                       256    CONST options(*nopass)
     D  parm3                       256    CONST options(*nopass)
     D  parm4                       256    CONST options(*nopass)
     D  parm5                       256    CONST options(*nopass)
     D  parm6                       256    CONST options(*nopass)
     D  parm7                       256    CONST options(*nopass)
     D  parm8                       256    CONST options(*nopass)
     D  parm9                       256    CONST options(*nopass)
     D  parm10                      256    CONST options(*nopass)
      *
     D blankButton     s                   like(wrpButton)
     D button          s                   like(wrpButton)
     D k               s              3P 0
V004 D directUrl       s               n
V012 D hidden          s               n
V012 D popup           s               n


     D wkButtonText    S            182
     D wkTipText       S            121
V019 D lFKeyNum        S              2P 0
V019 D lFKey           S              2
     D lButtonHandle   S             10

     D apos            C                   ''''
V004 D wkParms         S           5120    varying
     D parm            S              3P 0
      /free
        lButtonHandle=STR_Upper(buttonHandle);
V012    // Is this button going to be hidden (it will actually be very small
V012    // rather than hidden). Also see if the result should be a popup.
V004    if %parms>=3;
V004      hidden=(%scan('H':attributeIn)>0);
V012      popup=(%scan('P':attributeIn)>0);
V004    endif;
        // Build a button id based on
        // Get the text associated with the button handle
V009   gResult  = PR_GetText('':'B':lButtonHandle);
        // Button text; concatenate the first three fields with a space between each
V004   wkButtonText = *blanks;
V004   if not hidden;
V009     wkButtonText = %trim(gText1 ) + ' ' + %trim(gText2 );
V009     wkButtonText = %trim(wkButtonText) + ' ' + %trim(gText3 );
V004   endif;
        // Tip text; concatenate the first three fields with a space between each
V009   wkTipText = %trim(gTip1 ) + ' ' + %trim(gTip3 );
       wkButtonText = encode(wkButtonText);
V003   wkButtonText = STR_Latin1Encode(wkButtonText);
       wkTipText = encode(wkTiptext);
V003   wkTipText = STR_Latin1Encode(wkTipText);
V019   // Get default function key (if applic)
V019   lFKey  = PR_getFKey('':lButtonHandle);
V019   if lFKey <>*blanks;
V019     lFKeyNum  = %dec(lFKey :2:0);
V019     if lFKeyNum  <= 24;
V019       wkTipText = %trim(wkTiptext) + ' (F' + %char(lFKeyNum ) + ')';
         endif;
V019     CGI_AssociateFKey(lFKey:'btn' + %trim(buttonHandle));
       endif;

       %len(wkParms)=0;
       // If the attributes contain an 'X', then the URL will be
       // directly made up of the parameters we receive
       if %SCAN('X':attributeIn) > 0;
         directUrl=*on;
         if %parms >= 4;
           wkParms = %trimr(parm1);
         endif;
         if %parms >= 5;
           wkParms = wkParms        + %trimr(parm2);
         endif;
         if %parms >= 6;
           wkParms = wkParms        + %trimr(parm3);
         endif;
         if %parms >= 7;
           wkParms = wkParms        + %trimr(parm4);
         endif;
         if %parms >= 8;
           wkParms = wkParms        + %trimr(parm5);
         endif;
         if %parms >= 9;
           wkParms = wkParms        + %trimr(parm6);
         endif;
         if %parms >= 10;
           wkParms = wkParms        + %trimr(parm7);
         endif;
         if %parms >= 11;
           wkParms = wkParms        + %trimr(parm8);
         endif;
         if %parms >= 12;
           wkParms = wkParms        + %trimr(parm9);
         endif;
         if %parms >= 13;
           wkParms = wkParms        + %trimr(parm10);
         endif;
       else;
         directUrl=*off;
       //  Now build up a parameter string based on the parameters we have received
       // For ajax calls, the parameter for the script will be a single
       // string of name:value pairs
         If %parms >= 4;
V012       if g.ajax or popup;
            wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
            wkParms = wkParms        + %trim(parm1);
           else;
             wkParms = wkParms        + apos +  %trim(parm1) + apos;
           endif;
         EndIf;
         If %parms >= 5;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm2);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm2) + apos;
           endif;
         EndIf;
         If %parms >= 6;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm3);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm3) + apos;
           endif;
         EndIf;
         If %parms >= 7;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm4);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm4) + apos;
           endif;
         EndIf;
         If %parms >= 8;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm5);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm5) + apos;
           endif;
         EndIf;
         If %parms >= 9;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm6);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm6) + apos;
           endif;
         EndIf;
         If %parms >= 10;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm7);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm7) + apos;
           endif;
         EndIf;
         If %parms >= 11;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm8);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm8) + apos;
           endif;
         EndIf;
         If %parms >= 12;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm9);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm9) + apos;
           endif;
         EndIf;
         If %parms >= 13;
V012       if g.ajax or popup;
             wkParms = wkParms        + '|';
             wkParms = wkParms + %trim(CGI_AddParm( formHandle:parm));
             wkParms = wkParms        + %trim(parm10);
           else;
             wkParms = wkParms        + ',' + apos +  %trim(parm10) + apos;
           endif;
         EndIf;
       endif;

       // We can now build the HTML for the button
       button='<button id="btn' + %trim(buttonHandle) + '"';
V004   if hidden;
V011     button=button + ' class="hiddenbutton" type="button"';
V004   else;
V011     button=button        + ' class="sbmbutton" type="button"';
V004   endif;
V011   // Add script to ensure that if the user manually navigates here
V011   // they can press enter and it will execute
V011   button=button + ' onFocus="saveAndRemoveEnterKey();' +
V011                   ' overrideEnterKey($(' + quot +
V011                   'btn' + %trim(buttonHandle) +
V011                   quot + '));"';
V011   button=button + ' onBlur="restoreEnterKey();"';
       if directUrl;
V011     button=button        + ' onclick="' + wkParms + '" ';
       else;
V012     if g.ajax or popup;
V012       if popup;
V012         button=button        + ' onclick="javascript:executePopup(';
V012       else;
V012         button=button        + ' onclick="javascript:executeButton(';
V012       endIf;
V012       if popup;
V012         button=button + apos + %trim(buttonHandle) + apos + ',';
V012       endIf;
V011       button=button        + apos + 'f' + %trim(formhandle) + apos;
V011       button=button        + ',' + apos + %trim(gConfTxt ) + apos;
V011       button=button        + ',' + apos + wkParms + apos;
V012       button=button        + ',' + apos + g.ajax + apos + ');" ';
         else;
V011       button=button        + ' onclick="javascript:j';
V011       button=button        + %trim(formHandle) + '(';
V012       button=button        + apos + g.ajax + apos;
V011       button=button        + ',' + apos + %trim(gConfTxt ) + apos;
           if wkParms<>*blanks;
V011         button=button        + ',';
           endif;
V011       button=button        + ' ' + wkParms + ');" ';
         endif;
       endif;
       // Now add in the tooltip
V011   button=button        + ' title="';
V011   button=button        + %trim(wkTipText);
V011   button=button        + '" ';
       // Now add in any optional attributes requested
       If %parms >= 3;
         If %scan('R':attributeIn) > 0;
           button = button + ' readonly="readonly"';
         Endif;
         If %scan('D':attributeIn) > 0;
           button = button + ' disabled="disabled"';
         Endif;
       EndIf;
       button = button + '>';
       // Now add in the text associated with the button
       button = button +%trim(wkButtontext);
       // Finish it off
       button = button + '</button>';

       // Add button to button array
       k = %lookup(blankButton:wrpButtons:1:%elem(wrpButtons));
       if k<>0;
         wrpButtons(k)=button;
       endif;
       return *off;
      /end-free
     P                 E


     P WRP_AddMsg      B                   export
     D WRP_AddMsg      PI              n
     D   message                           like(wrpMessage) const
      *
     D blankMsg        s            256A
     D k               s              3P 0
      /free
       // Add message to message array
       k = %lookup(blankMsg:wrpMessages:1:%elem(wrpMessages));
       if k<>0;
         wrpMessages(k)=message;
       endif;
       return *off;
      /end-free
     P                 E


     P WRP_ClearButtons...
     P                 B                   export
     D WRP_ClearButtons...
     D                 PI              n
      /free
        clear wrpButtons;
        SES_Remove('functionkeys');
        return *off;
      /end-free
     P                 E


     P WRP_ClearMsgs   B                   export
     D WRP_ClearMsgs   PI              n
      /free
        clear wrpMessages;
        return *off;
      /end-free
     P                 E


     PWRP_RmvMenuCache...
     P                 B                   export
     DWRP_RmvMenuCache...
     D                 PI              n
      *
     D menuKey         ds
     D  menuCon                       4    inz('menu')
     D  menuSeq                       3P 0
      /free
        menuSeq=1;
        SES_Remove(menuKey);
        return *off;
      /end-free
     P                 E


     P WRP_Pssr        B                   export
     D WRP_pssr        PI
     D  psds                        429                                         Pgm status DS
     D  caller                       10    const options(*nopass)               Pgm status DS
      *
     D lExc            s              7
     D lMsgTxt         s             80
     D lStmt           s              8
     D lPgm            s             10
V017 D lEmailAddr      s            100    varying
      /free
        monitor;
         // Write HTML sections
         if not g.wrotePssr;
          g.wrotePssr=*on;
V017      gethtmlifs(%trim(WRP_GetPath('templates/xherror.html')):'<as400>');
          //if g.wroteTop=*off;
            // Clear the HTML buffer
            ClrHtmlBuffer();
          //  wrtsection('wrptop');
          //endif;
          lExc   =%subst(psds:40:7);
          lMsgTxt=%subst(psds:91:80);
          lStmt  =%subst(psds:21:8);
V017      lEmailAddr = CFG_GetXPathVal('ENV/GLOBAL/SUPPEMAIL');
          if lEmailAddr=*blanks;
            lEmailAddr='support';
          endif;
V006      PR_SetVar('exception':lExc);
V006      PR_SetVar('msgtext':lMsgTxt);
V006      PR_SetVar('stmtnum':lStmt);
V017      PR_SetVar('emailaddr':lEmailAddr);
          if %parms>1;
V006        lPgm = caller;
          else;
V006        lPgm = g.caller;
          endif;
V006      PR_SetVar('me':lPgm);
V006      PR_SetVar('session':EN_Session());

          // Sort out something useful for the email
          PR_SetVar('subject':'Exception occurred during Renaissance ' +
                               'processing');
          PR_SetVar('body':'Error ' + lExc + ' occurred in program ' +
                           %trim(lPgm) + ' at statement ' + lStmt + '.' +
                           ' The message text is: ' + %trim(lMsgTxt) +
                           ' The session ID is: ' + EN_Session() +
                           '. The environment: ' + EN_Environ() +
                           '. The job details are: ' +
V003                        %trim(jobNum) +
V003                        '/' + %trim(jobUser) + '/' + %trim(jobName));

          wrtsection('wrppssr');
V012      if not g.ajax;
            wrtsection('wrppssrhead');
          endif;
          wrtsection('wrppssrbody');
V012      if not g.ajax;
            wrtsection('wrppssrend');
          endif;
          wrtsection('*fini');
          // Send psds data to cgidebug physical file
          wrtpsds(psds);
         endif;
        on-error;
          return;
        endmon;
      /end-free
     P                 E


     P BuildMenuTree   B
     D BuildMenuTree   PI              n
      *
     D menuTreeDef     s          65535A   varying
     D ep              s             10I 0
     D sp              s             10I 0
      *
     D cr              s              1    inz(x'0D')
     D oneK            s           1024
      *
     D menuKey         ds
     D  menuCon                       4    inz('menu')
     D  menuSeq                       3P 0
      *
      /free
        PR_initPrompt(wrapper);
        // Load the menu from the session cache if it is there, else
        // rebuild from scratch and cache as we go
        menuSeq=1;
        oneK = SES_Get(menuKey);
        if SES_Error();
          // Not yet cached
          menuTreeDef=%trim(MT_GetMenuTree(g.userid));
          // Don't move this "wrtsection" - it has to be done AFTER the
          // GetMenuTree because it may write out some independent script.
          wrtsection('wrpstarttree');
          sp=1;
          ep = %scan(cr:%trim(menuTreeDef):sp);
          menuSeq=0;
          dow ep>0;
            // The substitute procedure can only deal with 1k at a time !
            oneK = %subst(menuTreeDef:sp:(ep-sp));
            oneK = STR_Substitute(%trim(oneK):'&encodedquot':'\\\&Æ39;');
            oneK = STR_Substitute(%trim(oneK):'&quot':'&Æ39;');
            UpdHtmlVar('treeinstruction':%trim(oneK));
            // Cache the entry in the users session
            menuSeq+=1;
            SES_Set(menuKey:%trim(oneK));
            // Write out the section
            wrtsection('wrptreeinstruction');
            sp=ep+1;
            if sp<=%len(%trim(menuTreeDef));
              ep = %scan(cr:%trim(menuTreeDef):sp);
              // Output the last bit
              UpdHtmlVar('treeinstruction':%subst(menuTreeDef:sp));
            else;
              leave;
            endif;
          enddo;
        else;
          // Menu has been cached
V007      CGI_addForm('XC0030':'fLevel':'fAction':'hXPATH':'hNODETYPE');
V007      CGI_addForm('XC0070':'fLevel':'fAction':'hXPATH':'hNODETYPE':
V007                                                       'hRoleId');
          wrtsection('wrpstarttree');
          UpdHtmlVar('treeinstruction':%trim(oneK));
          wrtsection('wrptreeinstruction');
          menuSeq+=1;
          oneK = SES_Get(menuKey);
          dow not SES_Error;
            UpdHtmlVar('treeinstruction':%trim(oneK));
            wrtsection('wrptreeinstruction');
            menuSeq+=1;
            oneK = SES_Get(menuKey);
          enddo;
        endif;
        wrtsection('wrpendtree');
        PR_InitPrompt(g.caller);
        return *off;
      /end-free
     P                 E


     P OutputButtons   B
     D OutputButtons   PI              n
      *
     D k               s              3P 0
     D x               s              3P 0
     D blankButton     s                   like(wrpButton)
      /free
        // Output messages
        wrtsection('wrpstartbuttons');
        k = %lookup(blankButton:wrpButtons:1:%elem(wrpButtons));
        if k>1;
          for x=1 to (k-1);
            updHtmlVar('button':wrpButtons(x));
            wrtsection('wrpbutton');
          endfor;
        endif;
        wrtsection('wrpendbuttons');
        return *off;
      /end-free
     P                 E


     P OutputMessages  B
     D OutputMessages  PI              n
V003 D  autoPB                         n   const
      *
     D k               s              3P 0
     D x               s              3P 0
     D blankMsg        s            256A
V009 D lMessageText    s            182A
V009 D lTipText        s            121A
V003 D lOutputText     s            500A
V003 D lErrMsgs        s           5020A   varying
      /free
        // Output messages
V003    %len(lErrMsgs)=0;
V003    if not autoPB;
V003      wrtsection('wrpstartmessages');
V003    endif;
        k = %lookup(blankMsg:wrpMessages:1:%elem(wrpMessages));
        if k>1;
          for x=1 to (k-1);
V009        lMessageText = %subst(wrpMessages(x):1:182);
V009        lTipText = %subst(wrpMessages(x):183:74);
V009        If lTipText = *Blank;
V009           lOutputText = %trim(lMessageText) + '<br></br>';
V009        Else;
V009           lOutputText = '<span title="' + %trim(lTipText) + '">' +
V009            %trim(lMessageText) + '</span><br></br>';
V009        EndIf;
V003        lErrMsgs=lErrMsgs + lOutputText;
V003        if not autoPB;
V003          updHtmlVar('message':loutputtext);
V003          wrtsection('wrpmessage');
V003        endif;
          endfor;
        endif;
V003    if not autoPB;
V003      wrtsection('wrpendmessages');
V003    endif;
V003    // We will also store the messages in the session in case they need to be
V003    // retrieved following an autopostback
V003    SES_Set(%trim(g.caller) + '.errormessages':lErrMsgs);
        return *off;
      /end-free
     P                 E
V008
V008 PWRP_AddFastPath...
V008 P                 b                   export
V008 DWRP_AddFastPath...
V008 D                 pi
V008 D  funcMod                       1    CONST
V008 D  funcGrp                      10    CONST
V008 D  func                         10    CONST
V008  *
V011 D  lFastPath      s          16384    varying
V011 D  lTrailer       s           1024    varying
V008 D  lCfgPath       s                   like(XSFEXECSTR)
V008 D  lDisplayAs     s             60
V008 D  lLink          s            256
V008 D  lSp            s             10i 0
V008  /free
V008   // Retrieve details from XSESSFUN
V008   chain (g.session:funcMod:funcGrp:func) XSESSFUNR;
V008   if %found;
V008     SES_Set('fastptxt':%trim(PR_GetConst('':'FASTPTXT')));
V011     lFastPath=SES_Get('fastpath');
V008     if lFastPath=*blanks;
V011       %len(lFastPath)=0;
V014       //lFastPath='<select name="fastpath" id="fastpath"' +
V014       //          ' onChange="setFastPath();' +
V014       //                    'executeFastPath(event);"' +
V014       //          ' onFocus="saveAndRemoveEnterKey();' +
V014       //                    'selectFocused(event);"' +
V014       //          ' onClick="selectClicked(event);"' +
V014       //          ' onBlur="restoreEnterKey();' +
V014       //                    'executeFastPath(event);"' +
V014       //          ' onKeyDown="' +
V014       //                   'trapEnterOnButton(' + quot + 'execfp' +
V014       //                                    quot + ',event);' +
V014       //                   'selectKeyed(event);"' +
V014       //          '>';
V014       lFastPath='<select name="fastpath" id="fastpath"' +
V014                 ' onChange="setFastPath();' +
V014                           'executeFastPath(event);"' +
V014                 ' onFocus="saveAndRemoveEnterKey();' +
V014                           'selectFocused(event);"' +
V014                 ' onClick="selectClicked(event);"' +
V014                 ' onBlur="restoreEnterKey();"' +
V014                 ' onKeyDown="' +
V014                          'trapEnterOnButton(' + quot + 'execfp' +
V014                                           quot + ',event);' +
V014                          'selectKeyed(event);"' +
V014                 '>';
V011       lFastPath=lFastPath + '<option value="' +
V008                  'javascript:parent.op()">' +
V008                  %trim(PR_GetConst('':'FASTPOPT')) + '</option>';
V011     else;
V011       %len(lFastPath) = %scan(EOF:lFastPath)-1;
V008     endif;
V008     lSp=%scan('</select>':lFastPath);
V008     if lSp>0;
V008       lTrailer=%subst(lFastPath:lSp);
V008       lFastPath=%subst(lFastPath:1:(lSp-1));
V008     else;
V008       lTrailer='</select>&nbsp;<a id="execfp"' +
V014                      ' href="javascript:document.fExecFP.submit();">' +
V008                      %trim(PR_GetIcon(*BLANK:'C':'EXECFP')) +
V008                      '</a>';
V008     endif;
V008     // Add in option
V008     lDisplayAs=%trim(PR_GetDisplayValue('XFUNC':'XFUFUNC':func));
V008     if func='XC0030';
V008       lLink=STR_Substitute(
V012               %trim(MT_buildCfgLink(XSFLEVEL:'':'GROUP':g.ajax)):
V008               '&quot':'''');
V008     else;
V008       if XSFEXECSTR<>*blanks;
V008         if %subst(XSFEXECSTR:1:7)='CONFIG:';
V008           // Get the Xpath (the string will be in the form
V008           // CONFIG:xxxxxxxx/yyyyyyyyy/zzzzzzzz
V008           lCfgPath = %subst(XSFEXECSTR:8);
V008           lLink=STR_Substitute(
V012            %trim(MT_buildCfgLink(XSFLEVEL:lCfgPath:'MAJORKEY':g.ajax)):
V008               '&quot':'''');
V008         else;
V008           lLink=%trim(XSFEXECSTR);
V008         endif;
V008       else;
V008         lLink=STR_Substitute(
V008               %trim(MT_BuildLink(funcMod:funcGrp:func:
V012               XSFLEVEL:XSFLOGREQ:g.ajax)):
V008               '&encodedquot':'''');
V008       endif;
V008     endif;
V011     lFastPath=lFastPath + '<option value="' +
V008                  %trim(lLink) + '">' +
V008                  %trim(lDisplayAs) +
V011                  '</option>';
V008     // Finish off
V011     SES_Set('fastpath':lFastPath + lTrailer + EOF);
V008   endif;
V008  /end-free
V008 P                 E
V013
V013 PWRP_SetAjaxFeedback...
V013 P                 b                   export
V013 DWRP_SetAjaxFeedback...
V013 D                 pi
V013  *
V013 D ajaxData        s          16384    varying
V016  *  Tab dialogs
V013 D tabContainer    s             10
V013 D dftTab          s             10
V016  * Other defaults
V013 D dftButtons      s           1000    varying
V013 D functionKeys    s           1024    varying
V013  /free
V013    ajaxData = SES_Get('ajax.feedback');
V013    if ajaxData=*blanks;
V013      %len(ajaxData)=0;
V013    else;
V013      %len(ajaxData) = %scan(EOF:ajaxData)-1;
V013    endif;
V014
V013    // Load up any button:function key pairs
V013    functionKeys = SES_Get('functionkeys');
V013    ajaxData = ajaxData + '|';
V013    if functionKeys<>*blanks;
V013      ajaxData = ajaxData + %trim(functionKeys);
V013      SES_Remove('functionkeys');
V013    endif;
V014
V013    // Load up tab dialog requirements (if any)
V013    tabContainer = SES_Get('tabdialogcontainer');
V013    ajaxData = ajaxData + '|';
V013    if tabContainer <>*blanks;
V013      dftTab = SES_Get('tabdialogcontainer.defaulttab');
V013      ajaxData = ajaxData + %trim(tabContainer) + ':' + %trim(dftTab);
V013      SES_Remove('tabdialogcontainer');
V013      SES_Remove('tabdialogcontainer.defaulttab');
V013    endif;
V016
V016    // Load up accordion requirements (if any)
V016    ajaxData = ajaxData + '|' +
V018      SES_Get('accordionargs');
V018    SES_Remove('accordionargs');
V018
V018    // Load up drag and drop requirements (if any)
V018    ajaxData = ajaxData + '|' +
V018      SES_Get('dndargs');
V018    SES_Remove('dndargs');
V014
V014    // Load up batch jobs requiring feedback
V014    ajaxData = ajaxData + '|';
V014    ajaxData = ajaxData + SES_Get('batchjobs');
V014
V013    // Load up any default button pairs separated by colons
V013    dftButtons = SES_Get('dftbuttons');
V013    SES_Remove('dftbuttons');
V013    ajaxData = ajaxData + '|';
V013    if dftButtons<>*blanks;
V013      ajaxData = ajaxData + %trim(dftButtons);
V013    endif;
V014
V014
V014    // Output ajax feedback
V013    %len(gAjaxFeedBack)=0;
V013    gAjaxFeedback=ajaxdata + EOF;
V013    updHTMLVar2('ajaxFeedback':%addr(gAjaxFeedback)+2:%len(gAjaxFeedback));
V013  /end-free
V013 P                 E
V013
V013 PWRP_AjaxFeedback...
V013 P                 b                   export
V013 DWRP_AjaxFeedback...
V013 D                 pi         16384    varying
V013  /free
V013    return gAjaxFeedback;
V013  /end-free
V013 P                 E
V003
V003 PBuildMsgArray...
V003 P                 b
V003 DBuildMsgArray...
V003 D                 pi              n
V003 D lReplacements   ds
V003 D  lReplaceValue                60    dim(10)
V015 D lType           s              1
     D i               s             10i 0
V003  /free
V003   //  If we have any errors, write out the messages
V015   If MSG_CodeCount > 0;
V015      For i = 1 to MSG_CodeCount;
V015        lReplacements=MSG_GetRplVars(MSG_Code(i):i);
V015        lType=MSG_Type(i);
            if lReplacements<>*blanks;
V015           WRP_AddMsg(PR_GetMsg(MSG_Code(i):lType:lReplaceValue));
            else;
V015           WRP_AddMsg(PR_GetMsg(MSG_Code(i):lType));
            endif;
V003      EndFor;
V003   EndIf;
V003
V003   Return *off;
V003  /end-free
V003 P                 E
V014
V014
V014 P WRP_SendHtml    B                   export
V014  *
V014  * Send HTML. Rather like WRP_Render but is used to send HTML
V014  *            snippets (i.e. for a response to an auto-postback
V014  *            field.
V014  *
V014 D WRP_SendHtml    PI              n
V014  *  Address of procedure to execute to load work area section.
V003 D  pWorkAreaPrc                   *   procptr value
V003  *   Form field to focus on
V003 D  focusOnField                       like(wrpName) options(*nopass)
V003 D                                     const
V014
V014  /free
V003   //  Deal with optional parameters
V003   if %parms<2;
V003     g.focusOn=*blanks;
V003   else;
V003     g.focusOn=focusOnField;
V003   endif;
V014
V014   //  Initialise global data with work area procedure pointer
V014   g.pWorkAreaPrc=pWorkAreaPrc;
V003
V003   // Set a session variable to say that we are performing an autopostback.
V003   // This will be used by XAPROMPT to stop it rendering the start and end
V003   // tags for the postback area (the div) again.
V003   SES_Set(%trim(g.caller) + '.postingback':*on);
V014
V014   // Clear the HTML buffer
V014   ClrHtmlBuffer();
V014
V014   wrtsection('wrptop');
V014
V014   g.wrotetop = *on;
V003
V003   // Build message array
V003   BuildMsgArray();
V014
V014   // Execute the procedure to build the HTML snippet
V014   BuildWorkArea();
V003
V003   // Deal with error fields
V015   PR_setErrorFld('A':MSG_Field(1):MSG_Field(2):
V015                MSG_Field(3):MSG_Field(4):MSG_Field(5):
V015                MSG_Field(6):MSG_Field(7):MSG_Field(8):
V015                MSG_Field(9):MSG_Field(10));
V003    // It is possible that an error generated by the procedure
V003    // building the work area has changed the focus field, so give
V003    // this a chance to work. Passing a blank will not upset any
V003    // previous focus, but will allow errors to get focus
V003    PR_focusOn('');
V003
V003   // Write message area
V003   OutputMessages(*on);
V003
V003   // If there are any error messages, write them out at the end in special
V003   // notation for the wrapper.js to pick out and deal with
V003   updHtmlVar('autopostbackmsgs':SES_Get(%trim(g.caller) +
V003              '.errormessages'));
V003   wrtsection('autopostbackmsgs');
V003
V003   // Allow the focus to be set
V003   updHtmlVar('autopostbackfocus':g.focusOn);
V003   wrtsection('autopostbackfocus');
V014
V014   // Write the *fini section to ensure all buffered output is sent
V014   // to the browser.
V014   wrtsection('*fini');
V014
V014   Return *off;
V014
V014  /end-free
V014  ****************************************************************************
V014  * Program status subroutine
V014  ****************************************************************************
V014 C     *pssr         begsr
V014  /free
V014    // If have already been in pssr, get out to avoid looping
V014    if        pssrswitch=*on;
V014      *inlr = *on;
V014      return;
V014    endif;
V014    WRP_Pssr(psds);
V014    // Set on switch to indicate we've been here
V014    pssrswitch=*on;
V014    *inlr = *on;
V014    return;
V014  /end-free
V014 C                   endsr
V014
V014 P                 E
**  Unlicensed tree menu link
<!------------------------------------------------------------------>
<!-- IMPORTANT NOTICE:                                            -->
<!-- Removing the following link will prevent this script         -->
<!-- from working.  Unless you purchase the registered            -->
<!-- version of TreeView, you must include this link.             -->
<!-- If you make any unauthorized changes to the following        -->
<!-- code, you will violate the user agreement.  If you want      -->
<!-- to remove the link, you can purchase a licence for the       -->
<!-- treeview from:                                               -->
<!-- http://www.regnow.com/softsell/nph-softsell.cgi?item=13669-5 -->
<!------------------------------------------------------------------>
<TABLE border=0 ID="TreeViewLink"><TR><TD><FONT size=-2>
<A style="font-size:7pt;text-decoration:none;color:silver"
href="http://www.treemenu.net/" target=_blank>Javascript Tree Menu
</A></FONT></TD></TR></TABLE>
