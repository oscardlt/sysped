      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED LEGEROPPTELLING            ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FLLLTLL01  UF   E           K DISK
     F                                     RENAME(LLLTLR:LLLTLR1)
     FLLLTLL02  UF   E           K DISK
     FLLAUTF    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FLL047P    O    E             PRINTER OFLIND(*IN29)
     F                                     USROPN
     FLL047D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D UCMD            DS          1024
     D                 DS
     D XCMD01          C                   CONST('OVRPRTF -
     D                                     FILE(LL047P) -
     D                                     OUTQ(')
     D XCMD02          C                   CONST('PAGESIZE(72 80) -
     D                                     CPI(10) -
     D                                     OVRFLW(66)')
     D                 DS
     D  LTLLOK                 1     25
     D  LTLOK1                 1      5
     D  LTLOK2                 6     25
     D                 DS
     D  XTID                   1      6  0
     D  XDTD                   7      8  0
     D  XDTM                   9     10  0
     D  XDTÅ                  11     14  0
     D  XTIME                  1     14  0
     D                 DS
     D  XIDAGÅ                 1      4  0
     D  XIDAGM                 5      6  0
     D  XIDAGD                 7      8  0
     D  XIDAG                  1      8  0
     D                 DS
     D  LTBATC                 1     25
     D  LTBAT1                 1      8
     D  LTBAT2                 1     10
     D                UDS
     D  UFIRM                  1      2
     D                SDS
     D  ZUSER                254    263
      *
     C                   EXSR      INIT
      *
      * HENT LAGERKUNDENR.
     C     BG1           TAG
     C     WSGRUP        COMP      0                                      49
     C  N30
     CANN04              Z-ADD     4             LINNBR
     C  N30
     CANN04              Z-ADD     20            POSNBR
     C                   EXFMT     BILDE1
     C                   MOVEA     '0000'        *IN(96)
     C                   MOVE      '0'           *IN30
      *
     C   03              GOTO      SLUTT
      * SØK LAGERKUNDENR.
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     LINNBR        IFEQ      3
     C     LINNBR        OREQ      4
     C     LINNBR        IFEQ      3
     C                   MOVE      'SØKGRP  '    UTYPE             8
     C                   ELSE
     C                   MOVE      WSGRUP        UTYPE
     C                   END
     C                   CALL      'LL004R'
     C                   PARM                    UTYPE
     C                   PARM                    WSGRUP
     C                   PARM                    WSKJED
     C                   ELSE
     C                   GOTO      BG1
     C                   END
     C                   END
      * TEST LAGERKUNDE
     C     WSGRUP        IFNE      *ZEROS
     C                   MOVE      WSGRUP        YYKNR
     C     KUNKEY        CHAIN     CUNDL01                            99
     C     *IN99         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      *BLANKS       NVNGRU
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   MOVE      'JOV0824'     MSGNR
     C                   GOTO      BG1
     C                   ELSE
     C                   MOVEL     KNAVN         NVNGRU
     C                   END
     C                   END
      *
     C                   MOVE      WSKJED        YYKNR
     C     KUNKEY        CHAIN     CUNDL01                            98
     C     *IN98         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      *BLANKS       NVNKJE
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   MOVE      'JOV0825'     MSGNR
     C                   GOTO      BG1
     C                   ELSE
     C                   MOVEL     KNAVN         NVNKJE
     C                   END
      *
     C     WSKJED        CHAIN     LLAUTF                             33
     C                   IF        *IN33 = *OFF
     C     LLAUTFK       KLIST
     C                   KFLD                    WSKJED
     C                   KFLD                    ZUSER
     C     LLAUTFK       CHAIN     LLAUTF                             33
     C   33              GOTO      SLUTT
     C                   END
      *
     C     LTLKEY        SETLL     LLLTLL01
     C     LTLKEY        READE(N)  LLLTLL01                               97
     C     *IN97         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   MOVE      'YBC0877'     MSGNR
     C                   GOTO      BG1
     C                   END
      *
     C     WSSLT         IFEQ      'X'
     C     *IN33         DOWEQ     '0'
     C     LTSTAT        IFEQ      'J'
     C                   MOVE      '1'           *IN96
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     30            POSNBR
     C                   MOVE      'YBC0878'     MSGNR
     C                   GOTO      BG1
     C                   END
     C     LTLKEY        READE(N)  LLLTLL01                               33
     C  N33              ENDDO
      * SLETT LISTE
     C     LTLKEY        SETLL     LLLTLL01
     C     LTLKEY        READE     LLLTLL01                               33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    LLLTLR1
     C                   MOVE      'J'           UOK
     C     LTLKEY        READE     LLLTLL01                               33
     C  N33              ENDDO
     C                   GOTO      SLUTT
     C                   END
      *
      * HENT LAGEROPPTELLINGS POSTER
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *
      * TEST OM ALLE POSTER HAR FÅTT SALDO FOR OPPTELLING
     C                   EXSR      TSTSAL
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LTLKEY        SETLL     LLLTLL01
     C     LTLKEY        READE(N)  LLLTLL01                               94
     C     *IN94         DOWEQ     '0'
     C     LTSTAT        COMP      'J'                                    28
     C                   MOVE      LTOPTS        WSOPTS
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     LTLKEY        READE(N)  LLLTLL01                               94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     51            POSNBR
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9998
     C                   SETOFF                                       30
      *
     C   03
     COR 07
     COR 10
     COR 12
     COR 23              EXSR      MATCHL
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** SKRIFT DIFFERANSE
     C   07              DO
     C                   EXSR      PRINT
     C                   GOTO      SLUTT
     C                   END
      *** MATCHER LAGER
     C   10              GOTO      START
      *** AVBRYT
     C   12              GOTO      BG1
      *** OPPDATER PÅ LAGER
     C   23              DO
     C                   EXSR      OPDLAG
     C     UFLAGG        IFGT      0
     C                   MOVE      '1'           *IN98
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC1847'     MSGNR
     C                   GOTO      START
     C                   END
     C                   GOTO      SLUTT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     TSTSAL        BEGSR
      ***********************************************
     C                   MOVE      'J'           XOK               1
     C     LTLKEY        SETLL     LLLTLL01
     C     LTLKEY        READE(N)  LLLTLL01                               94
     C     *IN94         DOWEQ     '0'
     C     LTOPTS        IFEQ      0
     C     LTDIFF        ANDEQ     0
     C                   MOVE      'N'           XOK
     C                   END
     C                   MOVE      LTLTDT        WSLTDT
     C     LTLKEY        READE(N)  LLLTLL01                               94
     C  N94              END
     C     XOK           COMP      'J'                                    27
      *
     C                   ENDSR
      *
     C***********************************************
     C     MATCHL        BEGSR
     C***********************************************
     C   03
     COR 121             DO        YRECNO        ZRECNO
     C     WSOPTS        IFNE      0
     C                   GOTO      STMATC
     C                   END
     C                   ENDDO
     C   03
     COR 12              GOTO      SLMATC
      *
     C     STMATC        TAG
     C     1             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     LTL2K         CHAIN     LLLTLL02                           33
     C                   MOVE      WSOPTS        LTOPTS
     C     LTSALD        SUB       WSOPTS        LTDIFF
     C                   UPDATE    LLLTLR
     C                   ENDDO
      *
     C     SLMATC        TAG
     C                   ENDSR
      *
     C***********************************************
     C     OPDLAG        BEGSR
     C***********************************************
     C                   CALL      'LL048R'
     C                   PARM      WSGRUP        UGRUP             8 0
     C                   PARM      WSKJED        UKJED             8 0
     C                   PARM      WSLNR         ULNR              7 0
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        CABGT     0             SLOPD
      * SLETT LISTE
     C     LTLKEY        SETLL     LLLTLL01
     C     LTLKEY        READE     LLLTLL01                               33
     C     *IN33         DOWEQ     '0'
     C     LTSTAT        IFEQ      'J'
     C                   DELETE    LLLTLR1
     C                   MOVE      'J'           UOK
     C                   ELSE
     C                   UPDATE    LLLTLR1
     C                   END
     C     LTLKEY        READE     LLLTLL01                               33
     C  N33              ENDDO
      *
     C     SLOPD         TAG
     C                   ENDSR
      *
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C     STPRT         TAG
     C                   EXFMT     PRTDIFF
     C                   MOVE      '0'           *IN90
     C                   MOVE      '0'           *IN30
     C   12              GOTO      SLPRT
     C     WSPRT         COMP      *BLANKS                                90
     C   90              GOTO      STPRT
      *
     C                   MOVE      *BLANKS       UCMD
     C     XCMD01        CAT       WSPRT:0       UCMD
     C                   CAT       ')':0         UCMD
     C                   CAT       XCMD02:1      UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C                   OPEN      LL047P
     C                   TIME                    XTIME
     C                   MOVE      XDTD          XIDAGD
     C                   MOVE      XDTM          XIDAGM
     C                   MOVE      XDTÅ          XIDAGÅ
      *
     C                   WRITE     LL047P1
     C     1             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     *IN33         IFEQ      '0'
     C     LTDIFF        ANDNE     0
     C   29              WRITE     LL047P1
     C   29              MOVE      '0'           *IN29
     C                   WRITE     LL047P2
     C                   END
     C                   ENDDO
     C                   WRITE     LL047P3
      *
     C                   CLOSE     LL047P
      *
     C     SLPRT         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KUNKEY        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    YYKNR             8 0
     C     LTLKEY        KLIST
     C                   KFLD                    WSGRUP
     C                   KFLD                    WSKJED
     C                   KFLD                    WSLNR
     C     LTL2K         KLIST
     C                   KFLD                    WSGRUP
     C                   KFLD                    WSKJED
     C                   KFLD                    LTMN
     C                   KFLD                    LTLNR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UOK               1
      *
     C                   MOVEL     '*JOB'        WSPRT
     C                   Z-ADD     1024          UCMDL            15 5
      *
     C                   ENDSR
      *
