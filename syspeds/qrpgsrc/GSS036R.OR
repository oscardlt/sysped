********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8XXX * 01 YBC PTF8 OPPBEVARE GAMMEL BOXNR I OLMN VED PLUKKING
P8XXX * 02 YBC PTF8 TEST OGSÅ BOXNR I GSSOLF
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VISE ALLE ORDRE MED SAMME KOLLI-ID     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 02 04 06-09 11 13-24
      *** LEDIGE INDIKATORER 29 31 32 34-90 95-97 99
      ***********************************************************
      *
     FGSSORF    UF   E           K DISK
     FGSSORL24  IF   E           K DISK    RENAME(GSSORFR:GSSORF24)
     FGSSOLF    UF   E           K DISK
     FGSS036D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
N01  D                 DS
N01  D  OLMN                   1     35
N01  D  OLMNPRE                1      3
N01  D  OLMNTNO                4     12
N01  D  OLMNBOX               13     15
     D                UDS
     D  UFIRM                  1      2
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UTNOI         SETLL     GSSORL24
     C     UTNOI         READE     GSSORL24                               94
     C                   DOW       *IN94 = *OFF
     C                   IF        ORST = USTF
N02  C                   IF        ORCLNF = 0
N02  C                   EXSR      HENTCLNR
N02  C                   END
     C                   IF        ORCLNT = 0
     C                   EVAL      ORCLNT = ORCLNT
     C                   END
     C                   IF        ((ORCLNF >= UCLNF) AND (ORCLNF <= UCLNT))
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   END
     C     UTNOI         READE     GSSORL24                               94
     C  N94              ENDDO
      *
     C     ZRECNO        IFEQ      0
     C                   EVAL      UINGEN = 'J'
     C                   GOTO      SLUTT
     C                   END
     C                   MOVE      ZRECNO        XRECNO            4 0
     C                   EVAL      ZRECNO = 1
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** BLA VIDERE
     C   10              DO
     C                   EXSR      PLALLE
     C                   GOTO      SLUTT
     C                   END
      *
      *** BLA VIDERE
     C   25              GOTO      START3
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N28
     CANN98WSNR          IFEQ      '1'
     C     ORONO         ANDNE     0
     C     ORONO         CHAIN     GSSORF                             33
     C                   MOVE      USTT          ORST
     C                   MOVE      UTNOU         ORTNO
     C                   ADD       ORWT          UWT
     C                   UPDATE    GSSORFR
     C                   EXSR      OPDLOK
     C                   MOVE      *ZEROS        ORONO
     C                   EVAL      ORBCD = '*PICK'
     C                   EVAL      ORLC  = *BLANKS
     C                   EVAL      ORSNM = *BLANKS
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
      *
     C   28
     CANN98WSNR          IFEQ      '4'
     C     ORONO         ANDNE     0
     C     ORONO         CHAIN     GSSORF                             33
     C                   MOVE      USTT          ORST
     C                   MOVE      *ZEROS        ORTNO
     C                   SUB       ORWT          UWT
     C                   UPDATE    GSSORFR
     C                   EXSR      OPDLOK
     C                   MOVE      *ZEROS        ORONO
     C                   EVAL      ORBCD = '*DLT'
     C                   EVAL      ORLC  = *BLANKS
     C                   EVAL      ORSNM = *BLANKS
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
      *
     C                   END
      *
     C     SLHENT        ENDSR
      *
     C***********************************************
     C     PLALLE        BEGSR
     C***********************************************
     C     1             DO        XRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     ORONO         IFNE      0
     C     ORONO         CHAIN     GSSORF                             33
     C                   MOVE      USTT          ORST
     C  N28              MOVE      UTNOU         ORTNO
     C   28              MOVE      *ZEROS        ORTNO
     C  N28              ADD       ORWT          UWT
     C   28              SUB       ORWT          UWT
     C                   UPDATE    GSSORFR
     C                   EXSR      OPDLOK
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPDLOK        BEGSR
     C***********************************************
     C     ORONO         SETLL     GSSOLF
     C     ORONO         READE     GSSOLF                                 33
     C     *IN33         DOWEQ     '0'
S01  C* N28              MOVE      UTNOU         OLTNO
S01  C*  28              MOVE      *ZEROS        OLTNO
N01  C                   IF        *IN28
N01  C                   MOVE      OLMNTNO       OLTNO
N01  C                   MOVE      OLMNBOX       OLBOX
N02  C                   MOVE      *BLANKS       OLMN
N01  C                   ELSE
N01  C                   MOVE      *BLANKS       OLMN
S02  C*                  MOVE      '998'         OLMNPRE
N02  C                   MOVE      '995'         OLMNPRE
N01  C                   MOVE      OLTNO         OLMNTNO
N01  C                   MOVE      OLBOX         OLMNBOX
N02  C                   MOVE      UTNOU         OLTNO
N01  C                   MOVE      *ZEROS        OLBOX
N01  C                   END
N01  C                   MOVE      ' '           OLST
     C                   UPDATE    GSSOLFR
     C     ORONO         READE     GSSOLF                                 33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
N02  C***********************************************
N02  C     HENTCLNR      BEGSR
N02  C***********************************************
N02  C     ORONO         SETLL     GSSOLF
N02  C     ORONO         READE(N)  GSSOLF                                 33
N02  C                   DOW       *IN33 = '0'
N02  C                   IF        UTNOI <> 0
N02  C                   IF        OLMNPRE = '995'
N02  C                   MOVE      OLMNBOX       OLMNBOX2          3 0
N02  C                   IF        ((ORCLNF = 0) OR (ORCLNF > OLMNBOX2))
N02  C                   MOVE      OLMNBOX2      ORCLNF
N02  C                   END
N02  C                   IF        ((ORCLNT = 0) OR (ORCLNT < OLMNBOX2))
N02  C                   MOVE      OLMNBOX2      ORCLNT
N02  C                   END
N02  C                   ELSE
N02  C                   IF        ((ORCLNF = 0) OR (ORCLNF > OLBOX))
N02  C                   MOVE      OLBOX         ORCLNF
N02  C                   END
N02  C                   IF        ((ORCLNT = 0) OR (ORCLNT < OLBOX))
N02  C                   MOVE      OLBOX         ORCLNT
N02  C                   END
N02  C                   END
N02  C                   END
N02  C     ORONO         READE(N)  GSSOLF                                 33
N02  C  N33              ENDDO
N02   *
N02  C                   ENDSR
N02   *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UTNOI             9 0
     C                   PARM                    UTNOU             9 0
     C                   PARM                    USTF              1
     C                   PARM                    USTT              1
     C                   PARM                    UWT               7 1
     C     UCLNF         PARM                    UCLNF             3 0
     C     UCLNT         PARM                    UCLNT             3 0
     C                   PARM                    UINGEN            1
      *
     C                   WRITE     WINDOWSZ
     C                   EVAL      UINGEN = 'N'
     C                   IF        UCLNT  = 0
     C                   EVAL      UCLNT  = UCLNF
     C                   END
     C     USTF          COMP      '0'                                2828
      *
     C                   ENDSR
      *
