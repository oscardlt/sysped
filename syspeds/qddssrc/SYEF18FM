     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTPB)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  4VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            KPBSD     R    3   O  6 14
     A            KPBBPR    R    5Y 0O  6 26EDTCDE(4)
     A            KPBRPR    R    5Y 2O  6 42EDTCDE(4)
     A            KPBBAN    R    5Y 0O  6 56EDTCDE(4)
     A            KPBRAN    R    5Y 2O  6 68EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYEF18'
     A                                      COLOR(BLU)
     A            MSG001        40A  O  1 19MSGID(DIV 1427 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A            MSG002        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG003        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG004        78A  O  5  2MSGID(DIV 1428 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A            MSG005        18A  O 23  1MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG006        18A  O 23 20MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG007        18A  O 23 39MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG008        18A  O 23 58MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24 12'Option not valid'
     A                                      COLOR(WHT)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG009        78A  O  1  2MSGID(DIV 1428 SYMSGF)
     A                                      COLOR(WHT)
     A            WSSD           3A  B  2 14
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40                                  DSPATR(PC)
     A  70                                  ERRMSG('Destination is not valid   -
     A                                                 ' 70)
     A            WSBPR          5Y 0B  2 26DSPATR(UL)
     A  40                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A            WSRPR          5Y 2B  2 42DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSBAN          5Y 0B  2 56DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSRAN          5Y 2B  2 68DSPATR(UL)
     A                                      EDTCDE(4)
     A            MSG010        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG011        18A  O  3 21MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG012        12A  O  2  2MSGID(DIV 0026 SYMSGF)
     A            W2SD           3A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG013        12A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG014        78A  O  1  2MSGID(DIV 1428 SYMSGF)
     A                                      COLOR(WHT)
     A            KPBSD     R    3   O  2 14DSPATR(PC)
     A                                      DSPATR(HI)
     A            KPBBPR    R    5Y00O  2 26DSPATR(HI)
     A                                      EDTCDE(4)
     A            KPBRPR    R    5Y02O  2 42DSPATR(HI)
     A                                      EDTCDE(4)
     A            KPBBAN    R    5Y00O  2 56DSPATR(HI)
     A                                      EDTCDE(4)
     A            KPBRAN    R    5Y02O  2 68EDTCDE(4)
     A                                      DSPATR(HI)
     A            MSG015        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYEF18'
     A                                      COLOR(BLU)
     A            MSG023        40A  O  1 19MSGID(DIV 1427 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A            MSG022        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG033        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG017        78A  O  5  2MSGID(DIV 1428 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG018        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG019        18A  O 23 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG020        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG021        18A  O 23 59MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
