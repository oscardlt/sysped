      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED ORDRE PÅ TUREN             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09 11 13-24             ***
      *** LEDIGE INDIKATORER 31 32 36-39 41-90 97
      ***********************************************************
      *
     FGSS053W   UF A E           K DISK    USROPN
     FGSS053W2  UF   E           K DISK    USROPN
     F                                     RENAME(GSS053WR:GSS053WR2)
     FGSSOLL01  IF   E           K DISK
     FGSSORF    UF   E           K DISK
     FGSSORL21  IF   E           K DISK    RENAME(GSSORFR:GSSORF21)
     FGSSBXF    IF   E           K DISK
     FGSS053D   CF   E             WORKSTN
     F                                     SFILE(GSS053DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     GSS053DB
     C                   MOVEA     '0000'        *IN(93)
     C                   MOVE      *ZEROS        ZRECNO
     C                   MOVE      *ZEROS        XONOGM
     C                   MOVE      *ZEROS        XBOXGM
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     GSS053W
     C                   READ(N)   GSS053W                                94
     C                   DOW       *IN94 = *OFF
     C                   IF        G53OK = 'N'
     C     ORONO         COMP      XONOGM                                 29
     C     OLBOX         COMP      XBOXGM                                 28
     C                   ADD       1             ZRECNO
     C                   WRITE     GSS053DA
     C                   MOVE      ORONO         XONOGM
     C                   MOVE      OLBOX         XBOXGM
     C                   END
     C                   READ(N)   GSS053W                                94
     C                   ENDDO
      *
     C                   IF        ZRECNO = 0
     C                   EVAL      UFLAGG = 0
     C                   GOTO      SLUTT
     C                   END
      *
     C                   Z-ADD     1             ZRECNO
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     32            POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     GSS053DB
     C                   EXFMT     GSS053DC
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      ***
      *
     C                   SETOFF                                       9830
      *** GODTA DIFFERANSE
     C   10              EVAL      UFLAGG = 0
     C   10              GOTO      SLUTT
      *** AVSLUTT
     C   12              EVAL      UFLAGG = 1
     C   12              GOTO      SLUTT
      *
     C                   EXSR      SROPSJON
     C                   IF        XENDRET = 'J'
     C                   EXSR      SRSJVKT
     C                   GOTO      START
     C                   ELSE
     C                   GOTO      START3
     C                   END
      *
     C     SLUTT         TAG
     C                   CLOSE     GSS053W
     C                   CLOSE     GSS053W2
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SROPSJON      BEGSR
      ***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C                   MOVE      'N'           XENDRET           1
      *
     C     *IN98         DOWEQ     '0'
     C                   READC     GSS053DA                               98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   IF        ORWT <> ORWTNY AND ORWTNY <> 0
     C     ORONO         CHAIN     GSSORF                             33
     C                   EVAL      XTOTO = XTOTO + ORWTNY - ORWT
     C                   EVAL      ORWT = ORWTNY
     C                   UPDATE    GSSORFR
     C                   EVAL      XORWTNY = ORWTNY
     C     ORONO         SETLL     GSS053W2
     C     ORONO         READE     GSS053W2                               33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      ORWT = XORWTNY
     C                   UPDATE    GSS053WR2
     C     ORONO         READE     GSS053W2                               33
     C                   ENDDO
     C                   MOVE      'J'           XENDRET
     C                   END
      *
     C                   ENDDO
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     32            POSNBR
     C                   SETOFF                                       99
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRHENT        BEGSR
      ***********************************************
     C                   CALL      'GSS053C'
     C                   OPEN      GSS053W
     C                   OPEN      GSS053W2
      *
     C     UTNO          SETLL     GSSBXF
     C     UTNO          READE     GSSBXF                                 33
     C                   DOW       *IN33 = *OFF
     C     GSSOLL01K     SETLL     GSSOLL01
     C     GSSOLL01K     READE     GSSOLL01                               33
     C                   DOW       *IN33 = *OFF
     C     OLONO         CHAIN(N)  GSSORF                             33
     C                   WRITE     GSS053WR
     C     GSSOLL01K     READE     GSSOLL01                               33
     C                   ENDDO
     C     UTNO          READE     GSSBXF                                 33
     C                   ENDDO
      *
     C                   EXSR      SRSJVKT
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSJVKT       BEGSR
      ***********************************************
      *
      * TØM STATUS
     C     *LOVAL        SETLL     GSS053W
     C                   READ      GSS053W                                33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   EVAL      G53OK   = ' '
     C                   EVAL      G53TYPE = ' '
     C                   UPDATE    GSS053WR
     C                   READ      GSS053W                                33
     C                   ENDDO                                                  1<-E
      *
      * LES BOX
     C     1             DO        998           XBOX                           1<-B
     C     XBOX          SETLL     GSS053W
     C     XBOX          READE(N)  GSS053W                                33
     C                   IF        ORONO <> 0 AND G53OK = ' '                    2<-B
      * SETT TYPE AV KOLLI:  G53TYPE = '1' --> EN BOX - ET ORDRE
      *                      G53TYPE = '2' --> EN BOX - FLERE ORDRE
      *                      G53TYPE = '3' --> FLERE BOX - ET ORDRE
     C                   EVAL      XONO = ORONO
      * SJEKK BOXTYPE: SKILLE TYPE 2 OG ANDRE FØRST
     C                   EVAL      XG53TYPE = '1'
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   IF        XONO <> ORONO                                   4<-B
     C                   EVAL      XG53TYPE = '2'
     C                   LEAVE
     C                   END                                                       4<-E
     C     XBOX          READE(N)  GSS053W                                33
     C                   ENDDO                                                    3<-E
      *
     C                   SELECT                                                   3<-B
     C                   WHEN      XG53TYPE = '2'                                 3
      * EN BOX - FLERE ORDRE
     C     XBOX          SETLL     GSS053W
     C     XBOX          READE(N)  GSS053W                                33
     C                   EVAL      XORDW = 0
     C                   EVAL      XONO2 = 0
     C                   EVAL      XBOXW = BXVKT
      * SUMMERE ORDREVEKT (BOXVEKT HAR ALLEREDE FÅTT)
     C                   DOW       *IN33 = *OFF                                    4<-B
     C                   IF        XONO2 <> ORONO                                    6<-B
     C                   EVAL      XORDW = XORDW + ORWT
     C                   EVAL      XONO2 = ORONO
     C                   END                                                         6<-E
     C     XBOX          READE(N)  GSS053W                                33
     C                   ENDDO                                                     4<-E
     C     XBOX          SETLL     GSS053W
     C     XBOX          READE     GSS053W                                33
     C                   DOW       *IN33 = *OFF                                    4<-B
     C                   EVAL      G53TYPE = '2'
      * OPPDATERE STATUS PÅ BOX/ORDRE:   J --> BOXVEKT = ORDREVEKT
     C                   IF        XORDW = XBOXW                                    5<-B
     C                   EVAL      G53OK = 'J'
     C                   ELSE                                                       5
     C                   EVAL      G53OK = 'N'
     C                   END                                                        5<-E
     C                   EVAL      G53DIFF = XBOXW - XORDW
     C                   UPDATE    GSS053WR
     C     XBOX          READE     GSS053W                                33
     C                   ENDDO                                                     4<-E
     C                   WHEN      XG53TYPE = '1'                                 3
      * SJEKK BOXTYPE: 1 ELLER 3
     C     XONO          SETLL     GSS053W2
     C     XONO          READE(N)  GSS053W2                               33
     C                   EVAL      XBOX2 = OLBOX
     C                   DOW       *IN33 = *OFF                                    4<-B
     C                   IF        XBOX2 <> OLBOX                                   5<-B
     C                   EVAL      XG53TYPE = '3'
     C                   LEAVE
     C                   END                                                        5<-E
     C     XONO          READE(N)  GSS053W2                               33
     C                   ENDDO                                                     4<-E
      *
     C                   SELECT                                                    4<-B
     C                   WHEN      XG53TYPE = '1'                                  4
      * ENTEN EN BOX - ET ORDRE
     C     XBOX          SETLL     GSS053W
     C     XBOX          READE(N)  GSS053W                                33
     C                   EVAL      XORDW = 0
     C                   EVAL      XONO2 = 0
     C                   EVAL      XBOXW = BXVKT
      * SUMMERE ORDREVEKT (BOXVEKT ER ALLEREDE FÅTT)
     C                   DOW       *IN33 = *OFF                                     5<-B
     C                   IF        XONO2 <> ORONO                                    6<-B
     C                   EVAL      XORDW = XORDW + ORWT
     C                   EVAL      XONO2 = ORONO
     C                   END                                                         6<-E
     C     XBOX          READE(N)  GSS053W                                33
     C                   ENDDO                                                      5<-E
     C     XBOX          SETLL     GSS053W
     C     XBOX          READE     GSS053W                                33
     C                   DOW       *IN33 = *OFF                                     5<-B
     C                   EVAL      G53TYPE = '1'
      * OPPDATERE STATUS PÅ BOX/ORDRE:   J --> BOXVEKT = ORDREVEKT
     C                   IF        XORDW = XBOXW                                     6<-B
     C                   EVAL      G53OK = 'J'
     C                   ELSE                                                        6
     C                   EVAL      G53OK = 'N'
     C                   END                                                         6<-E
     C                   EVAL      G53DIFF = XBOXW - XORDW
     C                   UPDATE    GSS053WR
     C     XBOX          READE     GSS053W                                33
     C                   ENDDO                                                      5<-E
      *
     C                   WHEN      XG53TYPE = '3'                                  4
      * FLERE BOX - ET ORDRE
     C     XONO          SETLL     GSS053W2
     C     XONO          READE(N)  GSS053W2                               33
     C                   EVAL      XBOXW = 0
     C                   EVAL      XBOX2 = 0
     C                   EVAL      XORDW = ORWT
      * SUMMERE BOXVEKT (ORDREVEKT ER ALLEREDE FÅTT)
     C                   DOW       *IN33 = *OFF                                     5<-B
     C                   IF        XBOX2 <> OLBOX                                    6<-B
     C                   EVAL      XBOXW = XBOXW + BXVKT
     C                   EVAL      XBOX2 = OLBOX
     C                   END                                                         6<-E
     C     XONO          READE(N)  GSS053W2                               33
     C                   ENDDO                                                      5<-E
     C     XONO          SETLL     GSS053W2
     C     XONO          READE     GSS053W2                               33
     C                   DOW       *IN33 = *OFF                                     5<-B
     C                   EVAL      G53TYPE = '3'
      * OPPDATERE STATUS PÅ BOX/ORDRE:   J --> BOXVEKT = ORDREVEKT
     C                   IF        XORDW = XBOXW                                     6<-B
     C                   EVAL      G53OK = 'J'
     C                   ELSE                                                        6
     C                   EVAL      G53OK = 'N'
     C                   END                                                         6<-E
     C                   EVAL      G53DIFF = XBOXW - XORDW
     C                   UPDATE    GSS053WR2
     C     XONO          READE     GSS053W2                               33
     C                   ENDDO                                                      5<-E
      *
     C                   ENDSL                                                     4<-E
      *
     C                   ENDSL                                                    3<-E
      *
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     GSSOLL01K     KLIST
     C                   KFLD                    UTNO
     C                   KFLD                    BXBOX
      *
     C     *ENTRY        PLIST
     C                   PARM                    UTNO             11 0
     C                   PARM                    UFLAGG            1 0
      *
     C     *LIKE         DEFINE    BXCN          XBXCN
     C     *LIKE         DEFINE    BXBOX         XBOX
     C     *LIKE         DEFINE    BXBOX         XBOX2
     C     *LIKE         DEFINE    BXBOX         XBOXGM
     C     *LIKE         DEFINE    ORONO         XONO
     C     *LIKE         DEFINE    ORONO         XONO2
     C     *LIKE         DEFINE    ORONO         XONOGM
     C     *LIKE         DEFINE    BXVKT         XBOXW
     C     *LIKE         DEFINE    ORWT          XORDW
     C     *LIKE         DEFINE    G53TYPE       XG53TYPE
     C     *LIKE         DEFINE    ORWTNY        XORWTNY
      *
      * SJEKK OM DET ER FLERE CONTAINER OG SUMMERE BOX VEKT
     C                   EVAL      XBXCN = '*START*'
     C     UTNO          SETLL     GSSBXF
     C     UTNO          READE     GSSBXF                                 33
     C                   DOW       *IN33 = *OFF
     C                   SELECT
     C                   WHEN      XBXCN = '*START*'
     C                   EVAL      XBXCN = BXCN
     C                   WHEN      XBXCN <> BXCN
      * FLERE CONTAINER --> IKKE SJEKK VEKT
     C                   EVAL      *INLR = *ON
     C                   RETURN
     C                   ENDSL
     C                   EVAL      XTOTB = XTOTB + BXVKT
     C     UTNO          READE     GSSBXF                                 33
     C                   ENDDO
      *
      * SUMMERE ORDRE VEKT
     C     UTNO          SETLL     GSSORL21
     C     UTNO          READE     GSSORL21                               33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      XTOTO = XTOTO + ORWT
     C     UTNO          READE     GSSORL21                               33
     C                   ENDDO
      *
     C                   EXSR      SRHENT
      *
     C                   ENDSR
      *
