      *
      ***********************************************************
      * BESTILLING AV FAX AV FRAKTBREV
      *** PROGRAM SKAL SKRIVE UT KOPI AV KJØREKVITTERING      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04-26                      ***
      *** LEDIGE INDIKATORER 27-29 31 32 35 38 51-56 58-69    ***
      ***                    74-78 80-87 89-99                ***
      ***********************************************************
     FCUNDC     IF   E             DISK
     FFAXLK     IF   E           K DISK
     FHEADF     IF   E           K DISK
     FDOKUF     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFAXSEN    IF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FKUNDL01   UF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR2)
     FFAX023FA  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D XXX            UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UPRO                  22     29  0
     D  UDT                   30     35  0
     D  UHOD                  47     47
     D  UBBS                  48     48
     D  UOPD                  82     88
     D  UFBNR                 89     91
     D  uufn1                101    107
     D  UGN                  981    995
     D                 DS
     D  F1                     1     21
     D                                     DIM(21)
     D  FAXNRP                 1     21
     D                 DS
     D  F2                     1     15
     D                                     DIM(15)
     D  FAXNR                  1     15
     D  FAX05A                 1      4
     D                 DS
     D  F3                     1      6
     D                                     DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D                 DS
     D  USØKKN                 1     30
     D  USØKKO                 1      2
     D  URRN                   3     12
     D  epofax                13     13
     D                 DS
     D EMNEDS          C                   CONST('Sending av fraktbrev-
     D                                     opplysninger')
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyz')
     D epotxt          C                   CONST('Sender over fraktbre-
     D                                     v via epost')
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   EXFMT     BILDE
     C                   EXSR      KDTFMT
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C   04              DO
     C     LINNBR        IFeq      14
     C     LO:UP         XLATE     EPOST         usok
     C                   CALL      'VCUNDC2R'
     C                   PARM                    FIFIRM
     C                   PARM                    USOK             30
     C                   PARM                    URRN2            10 0
     c                   parm                    epofax            1
     C     URRN2         IFNE      *ZEROS
     C                   MOVE      URRN2         URRN
     C                   MOVE      'KP'          USØKKO
     c                   else
     c                   movel     usok          epost
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       FAXNR
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    KUNDNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     c                   end
     C* RETUR MED KONTAKTPERS
     C     USØKKO        IFEQ      'KP'
     C                   MOVE      URRN          RRN              10 0
     C     RRN           CHAIN     CUNDC                              31
     c     epofax        ifne      'E'
     C  N31              MOVEL     CFAX          FAXNR
     c                   else
     c                   move      *blanks       faxnr
     c                   end
     c     epofax        ifne      'F'
     C  N31              MOVEL     CEMAIL        EPOST
     c                   else
     c                   move      *blank        epost
     c                   end
     C  N31              MOVE      CCOMPN        KUNDNR
     C                   END
     C                   END
     C                   EXSR      TEST
     C   30              GOTO      START
     C*
     C* OPPDATERER KUNDEREGISTER
     C*
     C     FAXNR         IFNE      *BLANKS
     C     TFAXNR        IFEQ      *BLANKS
     C     KUKEY         CHAIN     CUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD
     C     KUKEY         CHAIN     KUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD2
     C                   END
     C                   END
     C   10              EXSR      SNDFAX
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        lr
     C                   RETURN
     C*
     C***********************************************
     C     TEST          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(96)
     C*
     C                   Z-ADD     Wsfn1         HEOPD
     C     HEADFK        CHAIN     HEADF                              30
     C   30              MOVE      'SPO0001'     MSGNR
     C   30              GOTO      SLTB
     C     DOKUFK        CHAIN     DOKUF                              30
     C   30              MOVE      'JOV0539'     MSGNR
     C   30              GOTO      SLTB
     C     DFTOLL        COMP      0                                  7777
     C*
     C** TESTER KUNDENUMMER FRA OPPDRAG
     C**
     C**
     C     KUNDNR        IFEQ      *ZEROS
     C     KNAVN         ANDEQ     *BLANKS
     C                   MOVE      HEKNK         KUNDNR
     C                   END
     C*
     C     KUKEY         CHAIN(N)  CUNDL01                            31
     C  N31WSLAND        IFEQ      *BLANKS
     C                   MOVE      SYLAND        WSLAND
     C                   END
     C*
     C  N31FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     c                   move      syepos        epost
     C                   END
     C*
     C*
     C* N31FAXNR         IFEQ      *BLANKS
     C     FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   SETON                                        30
     C                   END
     C   30              MOVE      'SHU0350'     MSGNR
     C   30              GOTO      SLTB
     C*
      *
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        ANDEQ     *ZEROS
     C                   MOVE      'SHU0388'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C     WSTIME        IFEQ      *ZEROS
     C     WSDATE        ANDNE     *ZEROS
     C                   MOVE      'SHU0388'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C     WSTIME        IFLT      0
     C     WSTIME        ORGT      235959
     C                   MOVE      'SHU0389'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C*** TEST DATO
     C     WSDATE        IFNE      *ZEROS
     C                   CALL      'TESTDATO'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      96
     C   96
     CANN30              MOVE      'SPD0001'     MSGNR
     C   96              SETON                                        30
     C   30              GOTO      SLTB
     C                   END
     C*
      * BÅDE FAX OG EPOST-ADRESSE LAGT INN
     C     FAXNR         IFNE      *BLANKS
     C     EPOST         ANDNE     *BLANKS
     C                   SETON                                        3071
     C   30              MOVE      'SHU0538'     MSGNR
     C   30              GOTO      SLTB
     C                   END
     C*
     C     WSLAND        IFNE      *BLANKS
     C     WSLAND        CHAIN     FAXLK                              33
     C     FAX05A        IFEQ      FAX05B
     C                   MOVE      *BLANKS       FAXRET
     C                   END
     C                   END
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *DTAARA       DEFINE    *LDA          XXX
     C                   IN        XXX
     C     *ENTRY        PLIST
     C                   PARM                    DFRI
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    DFFBNR
     C                   READ      FIRM                                   44
     C                   READ      FAXSEN                                 44
     C     DFAVD         IFNE      *ZEROS
     C                   MOVE      DFAVD         UAVD
     C                   END
     C     DFOPD         IFNE      *ZEROS
     C                   Z-ADD     DFOPD         WSFN1             7 0
     c                   else
     C                   Z-ADD     0             WSFN1
     C                   END
     C     DFFBNR        IFNE      *ZEROS
     C                   Z-ADD     DFFBNR        WSFBNR
     C                   EXSR      TEST
     C                   ELSE
     C                   Z-ADD     1             WSFBNR
     C                   END
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    HEOPD
     C     DOKUFK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    UAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    WSFBNR
     C     KUKEY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     SNDFAX        BEGSR
     C***********************************************
     C     FAXRET        IFNE      *BLANKS
     C                   EXSR      FAXFIX
     C                   ELSE
     C                   MOVEL     FAXNR         FAXNRP
     C                   END
     C                   MOVE      wsfn1         uufn1
     C                   MOVE      *ZEROS        wsfn1
     C                   MOVE      'N'           UBBS
     C*
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        IFNE      *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVEL     *BLANKS       PDATE
     C                   MOVEL     WSTIME        PTIME
     C                   MOVEL     WSDATE        PDATE
     C                   END
     C                   END
     C                   MOVE      HEAVD         FAXAVD            4
     C                   MOVE      HEOPD         FAXOPD            7
     C                   MOVE      WSFBNR        FAXFBN            3
     C                   MOVEL     'FAX023P'     FILEN
     C     FAXSYS        IFEQ      'F'
     C     EPOST         orne      *BLANKS
     C                   MOVEL     'SYFA12P'     FILEN            10
     C                   END
     c     epost         ifne      *blanks
     c                   move      'J'           wscvrp
     c                   move      'J'           covp
     c                   end
     C     WSCVRP        IFEQ      'J'
     C     WSCVRP        OREQ      'Y'
     c     epost         ifeq      *blanks
     C                   MOVE      'J'           COVP
     C                   MOVEL     FIFT          COV05
     C                   MOVE      *BLANKS       COV01
     C                   MOVE      *BLANKS       COV03
     C                   MOVE      *BLANKS       COV04
     C                   MOVE      *BLANKS       COV07
     C                   MOVE      *BLANKS       COV08
     C                   CALL      'SYGE12CL'
     C                   PARM                    USE              10
     C                   PARM                    NVN              40
     C                   MOVEL     NVN           COV06
     C                   MOVEL     KNAVN         COV02
     C                   MOVEL     ADR1          COV03
     C                   CALL      'FAXCVP'
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C     FUNK          IFEQ      'A'
     C                   GOTO      SRFEIL
     C                   END
     c                   else
     c                   movel     epotxt        cov01
     C                   CALL      'EPOCVP'
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C     FUNK          CABEQ     'A'           SRFEIL
     C                   END
     C                   END
     C                   MOVEL     EMNEDS        EMNE             40
     C     EPOST         IFNE      *BLANKS
     C                   MOVE      'J'           EPOSTJ            1
     C                   END
     C*                  MOVE      WSFN1         UPRO7             7 0
     C*                  MOVEL     UPRO7         UPRO
     C*                  MOVE      WSFBNR        UPRO
     C                   MOVE      *ZEROS        UPRO
     C                   MOVE      WSFN1         UOPD
     C                   MOVE      WSFBNR        UFBNR
     C     DFTOLL        IFEQ      0
     C                   MOVE      'F'           UUKODE            1
     C                   MOVEL     'FRAKTBRE'    DOKNAV           12
     C                   MOVE      'V   '        DOKNAV           12
     C                   ELSE
     C                   MOVE      'T'           UUKODE
     C                   MOVEL     'TOLLPASS'    DOKNAV           12
     C                   MOVE      'FR.B'        DOKNAV           12
     C                   END
     C                   SETON                                        LR
     C                   OUT       XXX
     C                   CALL      'FAX023CL'
     C                   PARM                    FAXNRP
     C                   PARM                    FILEN
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    KNAVN
     C                   PARM                    FAXAVD
     C                   PARM                    FAXOPD
     C                   PARM                    UUKODE
     C                   PARM                    DOKNAV
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FAXFBN            3
     C                   PARM                    COVP              1
     C                   PARM                    EPOST
     C                   PARM                    EPOSTJ
     C                   PARM                    EMNE
     C*
     C                   MOVE      *BLANKS       WSLAND
     C                   MOVE      *BLANKS       FAXRET
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       FAXNR
     C                   MOVE      *ZEROS        KUNDNR
     c                   move      *blanks       epost
     c                   z-add     7             linnbr
     c                   z-add     16            posnbr
     C     SRFEIL        ENDSR
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   Z-ADD     1             A1                2 0
     C                   Z-ADD     1             A2                2 0
     C                   Z-ADD     1             A3                2 0
     C                   DO        6
     C     F3(A3)        IFNE      *BLANKS
     C                   MOVEL     F3(A3)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A3
     C                   END
     C                   DO        15
     C     F2(A2)        IFNE      *BLANKS
     C                   MOVEL     F2(A2)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A2
     C                   END
     C                   ENDSR
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDATE        XDTFMT            4 0
     C                   MOVEL     WSDATE        UDT1              2 0
     C                   MOVEL     XDTFMT        UDT2              2 0
     C                   MOVE      XDTFMT        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
     OKUNDR     E            UPD
     O                       TFAXNR
     OKUNDR2    E            UPD2
     O                       TFAXNR
