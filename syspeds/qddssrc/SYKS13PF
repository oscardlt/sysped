     A*****************************************************************
     A*   UTLISTING AV AVSETNINGER VED PERIODESLUTT DETALJERT/SUMMERT *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=132, OVERFLOW=44      *
     A* 03  TA MED SIGNATUR                                           *
     A*****************************************************************
     A**
     A** FORMAT NÅR DEALJER SKRIVES......
     A          R HEAD1P                    SKIPB(12)
     A                                     1MSGCON(132 JOV0233 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                   115DATE EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                   124TIME EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV0234 SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A            UPERF          4  0     36
     A                                      HIGHLIGHT
     A                                    40'/'
     A                                      HIGHLIGHT
     A            UPERT          4  0     41
     A                                      HIGHLIGHT
     A                                   118PAGNBR EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                    19MSGCON(50 JOV0306 SYMSGF)
     A                                      SPACEB(4)
     A                                      HIGHLIGHT
     A                                    19'I ='
     A                                      SPACEB(2)
     A                                    23MSGCON(50 JOV0238 SYMSGF)
     A                                    19'J ='
     A                                      SPACEB(1)
     A                                    23MSGCON(50 JOV0239 SYMSGF)
     A                                    19'K ='
     A                                      SPACEB(1)
     A                                    23MSGCON(50 JOV0240 SYMSGF)
     A                                    19'L ='
     A                                      SPACEB(1)
     A                                    23MSGCON(50 JOV0241 SYMSGF)
     A          R HEAD0D                    SKIPB(2)
     A                                     1MSGCON(132 JOV0233 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                   115DATE EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                   124TIME EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV0234 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            UPERF          4  0     36
     A                                      HIGHLIGHT
     A                                    40'/'
     A                                      HIGHLIGHT
     A            UPERT          4  0     41
     A                                      HIGHLIGHT
     A                                   118PAGNBR EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV0235 SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 PRI9999 SYMSGF)
     A                                      HIGHLIGHT
     A          R DET                       SPACEB(1)
     A            AVPERÅ         2  0      1
     A            AVPERM         2  0      4
     A            AVKONT         5  0      9
     A            AVKSTE         4  0     15EDTCDE(Z)
     A            AVKBÆR         4  0     21EDTCDE(Z)
     A            AVKOIN         1A       27
     A            AVTEXT        30A       29
     A            AVSIGN         3        63
     A            AVOAVD         4  0     67EDTCDE(Z)
     A            AVOPD          7  0     72EDTCDE(Z)
     A            AVTAVD         4  0     80EDTCDE(Z)
     A            AVTUR          8  0     85EDTCDE(Z)
     A            AVVAL          3A       95
     A            AVBELV        11S 2     99EDTCDE(J)
     A            AVBELL        11S 2    115EDTCDE(J)
     A          R DETT                      SPACEB(1)
     A                                      SPACEA(2)
     A            GMPERÅ         2  0      1
     A            GMPERM         2  0      4
     A            GMKONT         5  0      9
     A            GMKSTE         4  0     15EDTCDE(Z)
     A            GMKBÆR         4  0     21EDTCDE(Z)
     A            GMKOIN         1A       27
     A                                    30MSGCON(30 JOV0236 SYMSGF)
     A            DETTOT        13S 2    112EDTCDE(J)
     A          R GRAT                      SPACEB(1)
     A                                     1MSGCON(80 JOV0237 SYMSGF)
     A            GRATOT        13S 2    112EDTCDE(J)
     A**
     A** FORMAT NÅR KUN TOTALER SKRIVES......
     A          R HEAD0T                    SKIPB(2)
     A                                     1MSGCON(132 JOV0233 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                   118DATE EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                   127TIME EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV0242 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            UPERF          4  0     36
     A                                      HIGHLIGHT
     A                                    40'/'
     A                                      HIGHLIGHT
     A            UPERT          4  0     41
     A                                      HIGHLIGHT
     A                                   118PAGNBR EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV0243 SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 PRI9999 SYMSGF)
     A                                      HIGHLIGHT
     A          R SUMT                      SPACEB(1)
     A                                      SPACEA(1)
     A            GMPERÅ         2  0      1
     A            GMPERM         2  0      4
     A            GMKONT         5  0      9
     A            GMKSTE         4  0     15EDTCDE(Z)
     A            GMKBÆR         4  0     21EDTCDE(Z)
     A            DETTOT        13S 2     26EDTCDE(J)
     A  55                                47MSGCON(50 JOV0238 SYMSGF)
     A  56                                47MSGCON(50 JOV0239 SYMSGF)
     A  57                                47MSGCON(50 JOV0240 SYMSGF)
     A  58                                47MSGCON(50 JOV0241 SYMSGF)
     A          R SUMGT                     SPACEB(1)
     A                                     1MSGCON(20 JOV0244 SYMSGF)
     A            GRATOT        13S 2     26EDTCDE(J)
