     A          R GSS026CO
     A            STRICO        70      1  1
     A          R GSS026PA
     A            STRI03        70      1  1
     A            KFMT1         75      2  1
     A                                  2  2'SHIPPED BY   :'
     A            FINM          30A     2 19
     A N28                              2 51'PACKINGLIST FOR TRIP NO.'
     A                                      HIGHLIGHT
     A  28                              2 51'P.LIST(COPY) FOR TRIPNO.'
     A                                      HIGHLIGHT
     A            TRTNO         11  0   2 76EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                  2 95'DATE'
     A                                  2100DATE EDTCDE(Y)
     A                                  2109'PAGE'
     A            SIDENR         3  0   2113EDTCDE(Z)
     A                                  3  2'FROM LOCATION:'
     A            XXLOCF         5A     3 19
     A  32                              3 51'CONTAINERNR:'
     A                                      HIGHLIGHT
     A  32        XGMLCNR       17A     3 64HIGHLIGHT
     A                                  3 95'TIME'
     A            XTID4          4  0   3100EDTWRD('  .  ')
     A                                  4  2'CLIENT'
     A            TRCNO          8  0   4  9EDTCDE(Z)
     A            KNAVN         30A     4 19
     A N40N60                           4 51'VESSEL :'
     A N40 60                           4 51'SENT TO:'
     A N40        CBNAB         30A     4 60
     A                                  4 95'ETA DATE :'
     A            XTDTL          6  0   4108EDTCDE(Y)
     A                                  5  2'DELIVERY PLACE:'
     A            TRAD1         30A     5 19
     A                                  5 51'SENT BY:'
     A            TRDW          15A     5 60
     A                                  5 95'SENT DATE:'
     A            XTDTS          6  0   5108EDTCDE(Y)
     A            TRAD2         30A     6 19
     A  55                              6 51'COMMENTS:'
     C                                      HIGHLIGHT
     A  51        TRAD3         30A     7 19
     A  55        TRFT1         70A     7 51HIGHLIGHT
     A  52        TRAD4         30A     8 19
     A  56        TRFT2         70A     8 51HIGHLIGHT
     A  53        TRAD5         30A     9 19
     A  57        TRFT3         70A     9 51HIGHLIGHT
     A  54        TRAD6         30A    10 19
     A  58        TRFT4         70A    10 51HIGHLIGHT
     A          R GSS026PA27                SPACEB(1)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A                                    52'-------------------------'
     A                                    77'-------------------------'
     A                                   102'----------------'
     A                                      SPACEA(1)
     A          R GSS026PA2                 SPACEB(2)
     A                                      HIGHLIGHT
     A N60                                 2'BOX NO'
     A  60                                 2'UNIT NO'
     A            OLBOX          3S 0     10EDTCDE(Z)
     A  60                                19'OF'
     A  60        TOTBOX         3S 0     22EDTCDE(Z)
     A N60        XCLNR         12A       14
     A N60                                27'BOX WEIGHT:'
     A  60                                25'UNIT WEIGHT:'
     A            WSVKT          7S 1     38EDTCDE(4)
     A                                    47'KG'
     A N60                                53'BOX DIM:'
     A N60        WSMÅLL         4S 0     62EDTCDE(Z)
     A N60                                66'x'
     A N60        WSMÅLB         4S 0     68EDTCDE(Z)
     A N60                                72'x'
     A N60        WSMÅLH         4S 0     74EDTCDE(Z)
     A N60                                78'CM'
     A N60                                86'BOX CUBE MTR:'
     A N60        XM3X           9S 3     98EDTCDE(4)
     A N60                               110'M3'
     A N60                                  SPACEA(1)
     A  60                                53'CUBE MTR:'
     A  60        WSM3           6S 3     63EDTCDE(4)
     A  60                                71'M3'
     A  60                                77'LOADING MTR:'
     A  60        WSLM           4S 2     90EDTCDE(4)
     A  60                                96'LM'
     A  60                                  SPACEA(1)
     A  29N60N32                          27'Box ID:'
     A  29 60N32                          25'Unit ID:'
     A  29   N32  WSCID         17A       35SPACEA(1)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A N60                                52' CONTENTS IN THIS BOX    '
     A  60                                52' CONTENTS IN THIS UNIT   '
     A                                    77'-------------------------'
     A                                   102'----------------'
     A                                      SPACEA(1)
     A                                     2'SUPPLIER'
     A                                    35'P.O.NO.'
     A                                    51'CRT NO'
     A                                    60'DESCRIPTION'
     A                                   100'INTERNAL REF'
     A                                      SPACEA(1)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A                                    52'-------------------------'
     A                                    77'-------------------------'
     A                                   102'----------------'
      *
     A          R GSS026PB                  SPACEB(1)
     A            ORSNM         30A        2
     A            XOCONO        14A       35
     A            OLLNO          2  0     51EDTCDE(Z)
     A                                    54'/'
     A            XRCLI          2  0     55EDTCDE(Z)
     A            ORDSC         30A       60
     A            OLONO          7  0    100EDTCDE(Z)
      *
     A          R GSS026PB2                 SPACEB(1)
     A            XXPB2         70A       27
      *
     A          R GSS026PD                  SPACEB(1)
     A                                     2'-------------------------'
     A                                    27'-----------------------  '
     A N60                                52'END OF BOX  -------------'
     A N60                                77'-------------------------'
     A  60                                52'END OF UNIT  ------------'
     A  60        WSNT           4S 0     78EDTCDE(4)
     A  60                                84'COLLIES'
     A  60                                77'                ---------'
     A                                   102'----------------'
     A          R GSS026PT                  SPACEB(2)
     A                                      HIGHLIGHT
     A N60                                 2'TOTAL SHIPPED ITEMS (BOXES):'
     A  60                                 2'TOTAL SHIPPED UNITS:'
     A N60        TOTBOX         3S 0     31EDTCDE(Z)
     A  60        TOTBOZ         3S 0     23EDTCDE(Z)
     A  60                                30'COLLIES:'
     A  60        WSNTT          5S 0     37EDTCDE(4)
     A N60                                41'TOTAL WEIGHT:'
     A  60                                48'WEIGHT:'
     A            WSVKTT         9S 1     54EDTCDE(4)
     A                                    66'KG'
     A N60                                83'TOTAL CUBE MTR:'
     A N60        XM3XT          9S 3     98EDTCDE(4)
     A N60                               110'M3'
     A  60                                70'CUBE MTR:'
     A  60        WSM3T          7S 3     81EDTCDE(4)
     A  60                                92'M3'
     A  60                                97'LOAD.MTR:'
     A  60        WSLMT          4S 2    107EDTCDE(4)
     A  60                               114'LM'
      *
     A          R GSS026PTC                 SPACEB(2)
     A                                      HIGHLIGHT
     A                                     2'TOTAL IN THIS CONTAINER:'
     A            CTOTBOX        3S 0     27EDTCDE(Z)
     A                                    31'BOXES'
     A                                    41'TOTAL WEIGHT:'
     A            CTOTVKT        9S 1     54EDTCDE(4)
     A                                    65'KG'
     A                                    83'TOTAL CUBE MTR:'
     A            CTOTM3         9S 3     98EDTCDE(4)
     A                                   109'M3'
