********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S02  A*                                     HLPDOC('SYFT1801' 'SYFT18' 'SY400LO-
S02  A*                                     /MAINT')
N02  A                                      HLPPNLGRP(HLPSCR SYFT18PNL)
N02  A                                      HLPTITLE('VEDLIKEHOLD AV KUNDERS LI-
N02  A                                      KVIDITETSKODER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  7  2VALUES(' ' '2')
     A            KUNDNR         8Y 0O  7  5TEXT('Kundenummer')
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  7 14TEXT('Kundenavn')
     A            POSTNR         4S 0O  7 45TEXT('Postnummer')
     A            ADR3          24A  O  7 50TEXT('Sted')
     A            SYLIKV         1A  O  7 77TEXT('LIKV.KODE')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      OVERLAY
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFT18'
     A                                      COLOR(BLU)
     A            MSG101        40A  O  1 19MSGID(DIV 1400 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A            MSG103        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG104        15A  O  4  2MSGID(DIV 0656 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        78A  O  5  2MSGID(DIV 1700 SYMSGF)
     A                                      DSPATR(HI)
     A            WSKUND         8  0B  6  4
     A            WSALFA        10   B  6 14
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG201        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG204        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG301        30A  O  1  5MSGID(DIV 1701 SYMSGF)
     A            KUNDNR         8Y 0O  2  5TEXT('Kundenummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSLIKV         1A  B  2 23TEXT('LIKV.KODE')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG602        18A  O  3 20MSGID(DIV 0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        20A  O  2  2MSGID(DIV 1702 SYMSGF)
     A            W2ALFA        10A  I  2 23DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFT18'
     A                                      COLOR(BLU)
     A            MSG601        40A  O  1 19MSGID(DIV 1400 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            MSG603        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG604        30A  O  4  2MSGID(DIV 0656 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG605        78A  O  5  2MSGID(DIV 1700 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG606        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG607        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG608        18A  O 23 42MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
