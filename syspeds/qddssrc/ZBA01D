     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/DOKUF)
     A                                      PRINT
     A                                      CF03(03)
     A                                      CF10(10)
     A                                      CF12(12)
     A          R ZBA01DA
     A                                      OVERLAY
     A                                  1 24'Utskrift Strekkode-merkelapper'
     A                                      COLOR(WHT)
     A                                  1 75'ZBA01D'
     A                                      COLOR(BLU)
     A                                  2 33'Bestilling'
     A                                      COLOR(WHT)
     A                                  4  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'Avdeling . . . . . . . . . . . :'
     A            ZFAVD     R     Y  B  6 37REFFLD(DFAVD)
     A                                      EDTCDE(4)
     A                                  7  4'Oppdragsnummer . . . . . . . :'
     A            ZFOPD     R     Y  B  7 37REFFLD(DFOPD)
     A                                      EDTCDE(4)
     A                                      DSPATR(PC)
     A                                  8  6'Fraktbrevnummer  . . . . . :'
     A            ZFFBNR    R     Y  B  8 37REFFLD(DFFBNR)
     A                                      EDTCDE(4)
     A                                 11  6'Fraktbrevene ligger på.....:'
     A            ZFTYAL         1   B 11 37
     A                                 11 41'(I=Imp.oppdragene, H=Hovedoppdrage-
     A                                      t)'
     A                                 14  6'Hvis kun avd/oppdr.nr gis (0/blank-
     A                                       i fr.brev), tolkes dette som  '
     A                                 15  6'ønske om utskrift av merkelapp til-
     A                                       alle fordelingene (hvor ant.  '
     A                                 16  6'Z-merkel. er ulik 0) tilhørende de-
     A                                      t gitte oppdragsnr....         '
     A                                 21 76'Slutt'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt       F10=Forandre skri-
     A                                      ver ID'
     A                                      COLOR(BLU)
     A          R ZBA01DB
     A                                      OVERLAY
     A                                  1 24'Utskrift Strekkode-merkelapper'
     A                                      COLOR(WHT)
     A                                  1 75'ZBA01D'
     A                                      COLOR(BLU)
     A                                  2 33'Bestilling'
     A                                      COLOR(WHT)
     A                                  4  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6  2'Avdeling . . . . . . . . . . . :'
     A            ZFAVD     R        O  6 37REFFLD(DFAVD)
     A                                      EDTCDE(4)
     A                                  7  4'Oppdragsnummer . . . . . . . :'
     A            ZFOPD     R        O  7 37REFFLD(DFOPD)
     A                                      EDTCDE(4)
     A                                  8  6'Fraktbrevnummer  . . . . . :'
     A            ZFFBNR    R        O  8 37REFFLD(DFFBNR)
     A                                      EDTCDE(4)
     A                                 10  6'Sendningsnummer  . . . . . :'
     A            DF1004    R        O 10 37
     A                                 12  6'Start kolli  . . . . . . . :'
     A            ZFNTS          3Y 0B 12 37EDTCDE(4)
     A                                 13  8'Antall kolli . . . . . . :'
     A            ZFNT           3Y 0B 13 37EDTCDE(4)
     A                                 15  6'Antall stykk . . . . . . . :'
     A            ZANT           4Y 0B 15 37EDTCDE(4)
     A                                 21 76'Slutt'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt   F12=Forrige'
     A                                      COLOR(BLU)
     A          R ZBA01DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
