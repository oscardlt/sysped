      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT T-DOK. (NØDLØSNING)          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 1-26                             ***
      *** LEDIGE INDIKATORER 31 37 51-95
      ***********************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 KOPLAS F (AFP) ER LIKEVERDIG MED P
********************************************************************
     H DECEDIT('0,') DATEDIT(*DMY.)
     FKODTFM    IF   E           K DISK
     FKODTP     IF   E           K DISK
     FSVXKODL01 IF   E           K DISK
     FSVXSTD    IF   E           K DISK    PREFIX(STD_)
     FSVXH      UF   E           K DISK
     FSVXV      IF   E           K DISK
     FSVXTB     UF A E           K DISK
     FSVX027PF  O    E             PRINTER USROPN
     FSVX027PL  O    E             PRINTER USROPN
     F                                     OFLIND(*IN01)
     D F11             S             75    DIM(9)
     D F12             S             75    DIM(9)
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(SVX027PF) -
     D                                     OUTQ(')
     D XCMD12          C                   CONST('OVRPRTF -
     D                                     FILE(SVX027PL) -
     D                                     OUTQ(')
     D XCMD21          C                   CONST('PAGESIZE(72 120) -
     D                                     CPI(10) -
     D                                     OVRFLW(70)')
     D XCMD21P         C                   CONST('PAGESIZE(72 120) -
     D                                     CPI(15) -
     D                                     OVRFLW(70)')
     D XCMD22          C                   CONST('PAGESIZE(72 120) -
     D                                     CPI(15) -
     D                                     OVRFLW(67)')
     D*XCMD31          C                   CONST(' COPIES(')
     D XCMD41          C                   CONST(' FORMTYPE(')
     D XCMDPDF         C                   CONST('DEVTYPE(')
     D XCMDPDF1        C                   CONST('FRONTOVL(NCTS1 0 0.1)')
     D                 DS
     D  TVVT                   1     48
     D  TVVT30                 1     30
     D                 DS
     D  THNTTD                 1      1  0
     D  XHNTTD                 1      1
     D                 DS
     D  THNTLL                 1      1  0
     D  XHNTLL                 1      1
     D                 DS
     D  STD_THCNA              1     22
     D  XHCNA                  1     17
      *
     C                   EXSR      INIT
      *
     C     SVHK          CHAIN     SVXH                               33
     C                   IF        *IN33 = *OFF
     C     THAVD         CHAIN     SVXSTD                             33
     C     STD_THFMLL    COMP      'J'                                    27
     C*                  MOVEL     THCATS        YKODE
     C*    SVK01K        CHAIN     SVXKODL01                          33
     C* N33              MOVEL     TKTXTN        XKTXTC
     C*  33              EVAL      XKTXTC = THCATS
     C                   MOVEL     THTSB         YKODE
     C     SVK01K        CHAIN     SVXKODL01                          33
     C  N33              MOVEL     TKTXTN        XKTXTB
     C   33              EVAL      XKTXTB = THTSB
     C                   MOVEL     THTSB         XXTSB
      *
     C                   MOVE      'R'           THST
     C                   MOVEL     THTRNR        XAN10            10
     C                   IF        XAN10 = 'RES.RUTINE'
     C                   MOVE      THTRNR        XNPNR
     C                   ELSE
      * HENT NØDPROSEDYRENR
     C                   IF        STD_THNPNS = ' '
     C                   EVAL      STD_THNPNS = 'A'
     C                   END
     C     STD_THNPNS    CHAIN     SVXTB                              33
     C                   IF        *IN33
     C                   EVAL      STTTYP = STD_THNPNS
     C                   Z-ADD     2             STTNR
     C                   WRITE     SVXTBR
     C                   Z-ADD     1             XNPNR
     C                   ELSE
     C                   EVAL      XNPNR = STTNR
     C                   ADD       1             STTNR
     C                   UPDATE    SVXTBR
     C                   END
     C                   EVAL      THTRNR = 'RES.RUTINE ' + STD_THNPNS
     C                   MOVE      XNPNR         THTRNR
     C                   MOVE      XIDAG         THTRDT
     C                   END
     C                   UPDATE    SVXHR
      * HENT TOLLSTEDSNAVN
     C                   EXSR      HENTTS
     C                   EVAL      XHADA3 = %TRIM(THPNA) + ' ' + %TRIM(THPSA)
      *
     C                   OPEN      SVX027PF
     C                   OPEN      SVX027PL
     C     THGFT1        COMP      *BLANKS                            4949
     C                   IF        F11(1) = *BLANKS
     C                   EVAL      F11(1) = '.'
     C                   EVAL      F11(2) = '.'
     C                   EVAL      F11(3) = '.'
     C                   END
     C     1             DO        9             YN                3 0
     C                   IF        F11(YN) <> *BLANKS
     C                   IF        F11(YN) <> '.'
     C                   EVAL      PFFM1 = F11(YN)
     C                   END
     C                   EVAL      PFFM2 = F12(YN)
     C                   WRITE     SVX027PF1
     C     SVHK          SETLL     SVXV
     C     SVHK          READE     SVXV                                   33
     C  N33SVHK          READE     SVXV                                   33
     C                   IF        *IN33
     C                   WRITE     SVX027PF2
     C                   ELSE
     C                   WRITE     SVX027PF3
     C                   EXSR      SRLL
     C                   END
     C                   WRITE     SVX027PF4
     C                   WRITE     SVX027PF5
     C                   WRITE     SVX027PF6
     C                   END
     C                   ENDDO
     C                   CLOSE     SVX027PF
     C                   CLOSE     SVX027PL
     C                   END
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     HENTTS        BEGSR
      ***********************************************
     C                   EVAL      YKODE = THTSD1
     C     SVK01K        CHAIN     SVXKODL01                          34
     C  N34              MOVEL     TKTXTN        XKTXT1
     C   34              EVAL      XKTXT1 = THTSD1
     C                   MOVEL     THTSD1        XHTSD1
     C                   EVAL      YKODE = THTSD2
     C     SVK01K        CHAIN     SVXKODL01                          34
     C  N34              MOVEL     TKTXTN        XKTXT2
     C   34              EVAL      XKTXT2 = THTSD2
     C                   MOVEL     THTSD2        XHTSD2
     C                   EVAL      YKODE = THTSD3
     C     SVK01K        CHAIN     SVXKODL01                          34
     C  N34              MOVEL     TKTXTN        XKTXT3
     C   34              EVAL      XKTXT3 = THTSD3
     C                   MOVEL     THTSD3        XHTSD3
     C                   EVAL      YKODE = THTSD4
     C     SVK01K        CHAIN     SVXKODL01                          34
     C  N34              MOVEL     TKTXTN        XKTXT4
     C   34              EVAL      XKTXT4 = THTSD4
     C                   MOVEL     THTSD4        XHTSD4
     C                   EVAL      YKODE = THTSD5
     C     SVK01K        CHAIN     SVXKODL01                          34
     C  N34              MOVEL     TKTXTN        XKTXT5
     C   34              EVAL      XKTXT5 = THTSD5
     C                   MOVEL     THTSD5        XHTSD5
     C                   EVAL      YKODE = THTSD6
     C     SVK01K        CHAIN     SVXKODL01                          34
     C  N34              MOVEL     TKTXTN        XKTXT6
     C   34              EVAL      XKTXT6 = THTSD6
     C                   MOVEL     THTSD6        XHTSD6
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRLL          BEGSR
      ***********************************************
     C                   MOVE      *ON           *IN01
     C                   Z-ADD     0             XLNR
     C     SVHK          SETLL     SVXV
     C     SVHK          READE     SVXV                                   33
     C                   DOW       *IN33 = *OFF
     C                   IF        *IN01
     C                   WRITE     SVX027PL1
     C                   MOVE      *OFF          *IN01
     C                   END
     C                   ADD       1             XLNR
     C                   WRITE     SVX027PL2
     C     SVHK          READE     SVXV                                   33
     C                   ENDDO
      *
     C                   IF        *IN27
     C     XLNR          DIV       38            XLNR
     C                   MVR                     XLNR
     C                   IF        XLNR > 0
     C                   DOW       XLNR < 38
     C                   ADD       1             XLNR
     C                   WRITE     SVX027PL3
     C                   ENDDO
     C                   END
     C                   END
     C                   WRITE     SVX027PL4
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
      *
     C     SVHK          KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SVK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
     C     KODKEY        KLIST
     C                   KFLD                    KOPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    KOPLNR
      *
     C                   MOVE      'P'           KOPUNI
     C                   MOVE      '106'         YUNIK
     C                   Z-ADD     1024          UCMDL            15 5
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      *
     C                   EXSR      OVRF
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XIDAG
      *
     C                   ENDSR
      ***********************************************
     C     OVRF          BEGSR
      ***********************************************
     C                   MOVE      '0'           *IN97
      * Printerstyring for hovedark (A)
     C                   Z-ADD     255           KOPLNR
     C     KODKEY        CHAIN     KODTP                              33
     C                   MOVE      *BLANKS       PFFM1
     C                   MOVE      *BLANKS       PFFM2
     C     *IN33         IFEQ      '1'
     C                   MOVE      '1'           *IN97
     C                   MOVE      *BLANKS       KOPNVN
     C                   MOVEL     '*JOB'        KOPNVN
     C                   MOVE      *BLANKS       KOPTY
     C                   ELSE
     C     KOPFM         SETLL     KODTFM                             33
     C                   MOVE      *ZEROS        YN
     C     STFM1         TAG
     C     KOPFM         READE     KODTFM                                 33
     C  N33KFMT1         IFNE      *BLANKS
     C                   ADD       1             YN
     C                   MOVE      KFMT1         F11(YN)
     C                   MOVE      KFMT2         F12(YN)
     C                   END
     C  N33              GOTO      STFM1
     C                   ENDIF
     C                   MOVE      *BLANKS       UCMD
     C     XCMD11        CAT       KOPNVN:0      UCMD
     C                   CAT       ')':0         UCMD
     C     KOPLAS        IFEQ      'P'
N01  C     KOPLAS        OREQ      'F'
     C                   CAT       XCMDPDF:1     UCMD
     C                   CAT       '*AFPDS)':0   UCMD
     C                   CAT       XCMDPDF1:1    UCMD
     C                   CAT       XCMD21P:1     UCMD
     C                   ELSE
     C                   CAT       XCMDPDF:1     UCMD
     C                   CAT       '*SCS)':0     UCMD
     C                   CAT       XCMD21:1      UCMD
     C                   END
     C*                  CAT       XCMD31:0      UCMD
     C*                  CAT       '3)':0        UCMD
     C                   CAT       XCMD41:0      UCMD
     C                   CAT       GRAAPP:0      UCMD
     C                   CAT       KOPTY:0       UCMD
     C                   CAT       GRAAPP:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            34
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
      * Printerstyring for lasteliste
     C                   Z-ADD     95            KOPLNR
     C     KODKEY        CHAIN     KODTP                              33
     C                   MOVE      *BLANKS       PLFM1
     C                   MOVE      *BLANKS       PLFM2
     C     *IN33         IFEQ      '1'
     C                   MOVE      *BLANKS       KOPNVN
     C                   MOVEL     '*JOB'        KOPNVN
     C                   MOVE      *BLANKS       KOPTY
     C                   ELSE
     C     KOPFM         SETLL     KODTFM                             33
     C     KOPFM         READE     KODTFM                                 33
     C  N33KFMT1         IFNE      *BLANKS
     C                   MOVE      KFMT1         PLFM1
     C                   MOVE      KFMT2         PLFM2
     C                   END
     C                   ENDIF
     C                   MOVE      *BLANKS       UCMD
     C     XCMD12        CAT       KOPNVN:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   CAT       XCMD22:1      UCMD
     C*                  CAT       XCMD31:0      UCMD
     C*                  CAT       '3)':0        UCMD
     C                   CAT       XCMD41:0      UCMD
     C                   CAT       GRAAPP:0      UCMD
     C                   CAT       KOPTY:0       UCMD
     C                   CAT       GRAAPP:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            34
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C                   ENDSR
      *
