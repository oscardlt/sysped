********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB  PTF12 DFTACTGRP
********************************************************************
S01  h*option( *srcstmt ) bnddir( 'QC2LE' )
N01  H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
     fsad62cw   if   e             disk
     ‚*__________
     ‚* Variabler
     ‚*""""""""""
     d file_o          s               *
     d rc              s             10i 0
     d data            s          30000a   varying
     d codepage        s              5a   varying
     ‚*___________________________
     ‚* IFS Stream file funksjoner
     ‚*"""""""""""""""""""""""""""
     dopen             pr              *   extproc( '_C_IFS_fopen' )
     d                                 *   value options( *string )
     d                                 *   value options( *string )

     dfputs            pr            10i 0 extproc( '_C_IFS_fputs' )
     d                                 *   value options( *string )
     d                                 *   value

     dclose            pr            10i 0 extproc( '_C_IFS_fclose' )
     d                                 *   value
     ‚*   SYEDI data area
     d*edidta          ds           256
     d*uelibf                171    180
     D Edidta          ds           256    DTAARA('EDIDTA')
     D  Uelibf               171    180

     d hex25           s              1    inz(x'25')
     ‚*  Prototype for sad62r5
     d sad62r5         pr
     d wfilnam_                            like(wfilnam)
     ‚*  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
     ‚*  *ENTRY Interface for Main Procedure
     d sad62r5         pi
     d wfilnam                      256
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - TOP
     ‚*----------------------------------------------------------------------
     d wdt             s              8
     d wtm             s              6
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - BOTTOM
     ‚*----------------------------------------------------------------------
     ‚*//////////////
     ‚*  Hodelogikk /
     ‚*//////////////
      /free
       exsr sra;
       exsr srb;
       *inlr = *on;
       //////////////////////////////////////////////////////////////
       //  Initiering                                           SRA /
       //////////////////////////////////////////////////////////////
       begsr sra;
         //_________________
         // Input parametere
         //"""""""""""""""""
         //_____________
         // Init av felt
         //"""""""""""""
         // Hente dagens dato og klokkeslett.
         syge15c ( wdt : wtm );
         // Åpne dataarea
         //    *dtaara       define                  edidta
         in edidta;
       endsr;
       //////////////////////////////////////////////////////////////
       //  Lag .csv-fil                                         SRB /
       //////////////////////////////////////////////////////////////
       begsr srb;
         //___________________________________
         // Lag streng for katalognavn/filnavn
         //"""""""""""""""""""""""""""""""""""
         wfilnam = *blanks;
         wfilnam = '/' + %trimr(uelibf)
         + '/EDISE/TEMP/'
         + %trimr(wdt) + %trimr(wtm) + '.csv';
         //_____________________________
         // Lag fil og set codepage (pc)
         //"""""""""""""""""""""""""""""
         codepage = '819';
         file_o = open( %trimr( wfilnam )
         : 'w, codepage=' + codepage );

         rc = close( file_o );
         //________________________________________
         // Åpne fil, med tegnkonvertering for jobb
         //""""""""""""""""""""""""""""""""""""""""
         file_o = open( %trimr( wfilnam )
         : 'w' );

         if file_o <> *null;

           // Skriv overskrift
           data = 'Type;Dekl.dato;Eksp.enhet;Løpenr;' +
                 'Vår Ref;Avg;Sats;Gr.lag;' +
                 'Beløp;Fr.o.m;T.o.m;Kundref' + hex25;
           rc = fputs(data : file_o);
           // Skriv data fra arbetsfil
           read sad62cw;
           //*in51 = %eof;
           //dow *in51 = *off;
           DoW Not %EoF(sad62cw);
             data = %trimr(sad62c1)+';'+
                   %trimr(sad62c2)+';'+
                   %trimr(sad62c3)+';'+
                   %trimr(sad62c4)+';'+
                   %trimr(sad62c5)+';'+
                   %trimr(sad62c6)+';'+
                   %trimr(sad62c7)+';'+
                   %trimr(sad62c8)+';'+
                   %trimr(sad62c9)+';'+
                   %trimr(sad62ca)+';'+
                   %trimr(sad62cb)+';'+
                   %trimr(sad62cc)+';'+ hex25;
             rc = fputs(data : file_o);
             read sad62cw;
             //*in51 = %eof;
           enddo;

           //Steng fil
           rc = close( file_o );
         endif;
       endsr;
      /end-free
