     A*%%TS  SD  20170515  093354  JOVO        REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20170515  093354  JOVO        REL-V7R1M0  5770-WDS
     A            WSNR           1A  B  6  5VALUES(' ' '2' '4')
     A            ASITXT    R        O  6 39REFFLD(ARKEXTR/ARCPAD *LIBL/ARKEXT)
     A            ASIANE    R        O  6  7REFFLD(ARKEXTR/ARCANE *LIBL/ARKEXT)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20170515  093354  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0020)
     A  *DS4                                SFLSIZ(0020)
     A  *DS3                                SFLPAG(0018)
     A  *DS4                                SFLPAG(0018)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/ARC025'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(030 JOV1726 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  4MSGCON(070 JOV1725 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  7'Rotmappe:'
     A            ROOTM         30A  O  2 19COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VARELI
     A*%%TS  SD  20170515  090953  JOVO        REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  4MSGCON(070 JOV1725 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WRIANE    R        B  2  7REFFLD(ARKEXTR/ARCANE *LIBL/ARKEXT)
     A                                      CHECK(LC)
     A            WRITXT    R        B  2 39REFFLD(ARKEXTR/ARCPAD *LIBL/ARKEXT)
     A                                      CHECK(LC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VAREL3
     A*%%TS  SD  20170515  090953  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  3MSGCON(070 JOV1725 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ASITXT    R        O  2 38REFFLD(ARKEXTR/ARCPAD *LIBL/ARKEXT)
     A            ASIANE    R        O  2  6REFFLD(ARKEXTR/ARCANE *LIBL/ARKEXT)
     A                                  3  2MSGCON(024 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R NYWRK
     A*%%TS  SD  20170515  090953  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/ARC025'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(030 JOV1726 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  4MSGCON(070 JOV1725 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
