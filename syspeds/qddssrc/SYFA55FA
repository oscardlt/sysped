     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            AFAXNR        15   H      TEXT('FAXNR')
     A            ALAND          2   H      TEXT('LAND')
     A            FRETNR         2   H      TEXT('RETNR')
     A            SFNR           1A  B  8  3VALUES(' ' '4')
     A            KUNDNR    R        O  8  6REFFLD(KUNDR/KUNDNR *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A            KNAVN     R        O  8 15REFFLD(KUNDR/KNAVN *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A            ADR3      R        O  9 15REFFLD(KUNDR/ADR3 *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      COLOR(BLU)
     A                                  1  3'SYSPED/SYFA55A'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 SHU0294 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  5  3MSGCON(039 JOV0139 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(040 SHU0728 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 21MSGCON(040 SHU0729 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(060 SHU0730 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'SEND')
     A                                      CF12(12 'tilbake')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 41MSGCON(018 CBA0110 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNNS
     A                                      CF12(12 'SØK')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSALFA        10   I  1  3DSPATR(UL)
     A                                  2  3MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
