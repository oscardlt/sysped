/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 10023 * 01 YBC PTF10 SJEKK OM INVOIC OG CUSXXX KOMMER SAMMEN            */
/* 10030 * 02 CB  PTF10 FÅ MED IFSPAREMETER INN I VALIDERINGSPGM           */
/* 10XXX * 03 CB  DANSK FORTOLLING                                         */
/* 11XXX * 04 CB  DANSK FORTOLLING                                         */
/* 11XXX * 05 CB  TDS UTL ELLER ZEM                                        */
/* 12XXX * 06 CB  BYTT JOBQ PÅ IKKE EDIFACTJOBBER                          */
/* ********************************************************************    */
             PGM
        /*___________________*/
        /* Declare variables */
        /*"""""""""""""""""""*/
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MLNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DATE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&JUL) TYPE(*CHAR) LEN(3)
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(7)
             DCL        VAR(&W0065) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DATA) TYPE(*CHAR) LEN(30)
             DCL        VAR(&LEN) TYPE(*DEC) LEN(5 0) VALUE(30)
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(1)
             DCLF       FILE(EDI10C) RCDFMT(QWHFDML)
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
        /*________________*/
        /* Get memberlist */
        /*""""""""""""""""*/
             CLRPFM     FILE(QTEMP/EDI10C)
             MONMSG     MSGID(CPF0000)
             DSPFD      FILE(EDI79F) TYPE(*MBRLIST) OUTPUT(*OUTFILE) +
                          FILEATR(*PF) OUTFILE(QTEMP/EDI10C)
             MONMSG     MSGID(CPF0000)
        /*_________________*/
        /* Read memberlist */
        /*"""""""""""""""""*/
 READ:       RCVF       RCDFMT(*FILE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))
             IF         COND(&MLNAME *EQ '          ') THEN(GOTO +
                          CMDLBL(READ))
        /*______________*/
        /* Spec for RJE */
        /*""""""""""""""*/
             IF         COND(&MLNRCD *GT 0) THEN(GOTO +
                          CMDLBL(NOTRJE))
             CHGVAR     VAR(&PREFIX) VALUE(%SST(&MLNAME 1 1))
             IF         COND(&PREFIX *NE 'B') THEN(GOTO +
                          CMDLBL(NOTRJE))
                          GOTO READ
        /*___________________*/
        /* Any memberlocks ? */
        /*"""""""""""""""""""*/
             NOTRJE:      ALCOBJ     OBJ((EDI79F *FILE *EXCL &MLNAME)) WAIT(1)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(READ))
        /*____________________*/
        /* TAR BACKUP AV MOTTAK */
        /*""""""""""""""""""""*/
             CHGJOB     DATFMT(*JUL)
             RTVJOBA    DATE(&DATE)
             CHGJOB     DATFMT(*SYSVAL)
            DLYJOB     DLY(1)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
             CHGVAR     VAR(&JUL) VALUE(%SST(&DATE 3 3))
             CHGVAR     VAR(&MEMBER) VALUE('E' *CAT &JUL *CAT &TIME)
             OVRDBF     FILE(EDI79F) MBR(&MLNAME)
             CPYF       FROMFILE(*LIBL/EDI79F) TOFILE(EDI.BACK) +
                          FROMMBR(&MLNAME) TOMBR(&MEMBER) MBROPT(*ADD)
             MONMSG     MSGID(CPF0000)
             CALL       PGM(EDISXR1) PARM(&MLNAME &MEMBER)
        /*____________________*/
        /* Split the workfile */
        /*""""""""""""""""""""*/
 TAG01:      CLRPFM     FILE(EDI85F) MBR(EDI73C2)
             MONMSG     MSGID(CPF0000)
             OVRDBF     FILE(EDI79F) MBR(&MLNAME)
             OVRDBF     FILE(EDI85F) MBR(EDI73C2)
/*S02        CALL       PGM(EDI85R) PARM(&TYPE)  */
/*N02*/      CALL       PGM(EDI85R) PARM(&TYPE &MLNAME)
             IF         COND(&TYPE *EQ '    ') THEN(DO)
             RMVM       FILE(EDI79F) MBR(&MLNAME)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(READ)
             ENDDO
        /*__________________________________*/
        /* Read  errormessage from  IGN  IE */
        /*""""""""""""""""""""""""""""""""""*/
             IF         COND(&TYPE *EQ '*SYS') THEN(DO)
             CALL       PGM(EDI82R)
             GOTO       CMDLBL(TAG01)
             ENDDO
        /*___________________________________*/
        /* Create new records in file: EDIRE */
        /*"""""""""""""""""""""""""""""""""""*/
 NYMBR:      CALL       PGM(EDI10C2R) PARM(&MEMBER)
             CALL       PGM(EDISXR2) PARM(&MLNAME &MEMBER)
        /*_________________________________*/
        /* Find Messagetype in interchange */
        /*"""""""""""""""""""""""""""""""""*/
/*N01*/      CALL       PGM(EDI80RTV) PARM(&W0065)
/*N01*/      IF         COND(&W0065 *EQ CUSINV) THEN(DO)
/*N01*/      CHGVAR     VAR(&DATA) VALUE(&LIB *CAT 'EDIRE     ' +
/*N01*/                   *CAT &MEMBER)
/*N01*/      CALL       PGM(QSNDDTAQ) PARM(E081Q *LIBL    &LEN &DATA)
/*N01*/      MONMSG     MSGID(CPF0000)
/*N01*/      CHGVAR     VAR(&PGM) VALUE('TVI21C')
/*N01*/      GOTO       CMDLBL(SYSPED)
/*N01*/      ENDDO
             CALL       PGM(EDI80R) PARM(&W0065)
             CHGVAR     VAR(&PGM) VALUE(EDIOCC)
             IF         COND(&W0065 *EQ IMPORT) THEN(CHGVAR +
                          VAR(&PGM) VALUE(TVI21C))
             IF         COND(&W0065 *EQ EXPORT) THEN(CHGVAR +
                          VAR(&PGM) VALUE(TVI21C))
             IF         COND(&W0065 *EQ 'TET-IM') THEN(CHGVAR +
                          VAR(&PGM) VALUE(TR154C))
             IF         COND(&W0065 *EQ 'TET-EX') THEN(CHGVAR +
                          VAR(&PGM) VALUE(TR104C))
             IF         COND(&W0065 *EQ 'SV-KON') THEN(CHGVAR +
                          VAR(&PGM) VALUE(SVG100C))
             IF         COND(&W0065 *EQ 'SV-NCE') THEN(CHGVAR +
                          VAR(&PGM) VALUE(SVX104C))
             IF         COND(&W0065 *EQ 'SV-NCI') THEN(CHGVAR +
                          VAR(&PGM) VALUE(SVX154C))
             IF         COND(&W0065 *EQ 'CONTRL') THEN(CHGVAR +
                          VAR(&PGM) VALUE(SVG100C))
/*S05        IF         COND(&W0065 *EQ 'SV-UTL') THEN(CHGVAR +
                          VAR(&PGM) VALUE(SVU001C))                 */
/*N05*/      IF         COND(&W0065 *EQ 'SV-UTL') THEN(CHGVAR +
                          VAR(&PGM) VALUE(SVG009R))
/*S05        IF         COND(&W0065 *EQ 'SV-ZEM') THEN(CHGVAR +
                          VAR(&PGM) VALUE(SZU001C))                 */
/*N05*/      IF         COND(&W0065 *EQ 'SV-ZEM') THEN(CHGVAR +
                          VAR(&PGM) VALUE(SVG009R))
/*N03*/      IF         COND(&W0065 *EQ 'DK-CON') THEN(CHGVAR +
                          VAR(&PGM) VALUE(DKG100C))
/*N04*/      IF         COND(&W0065 *EQ 'DK-NCE') THEN(CHGVAR +
                          VAR(&PGM) VALUE(DKX104C))
/*N04*/      IF         COND(&W0065 *EQ 'DK-NCI') THEN(CHGVAR +
                          VAR(&PGM) VALUE(DKX154C))
             IF         COND((&W0065 *EQ IFTMIN) *OR (&W0065 *EQ +
                          IFTMBP) *OR (&W0065 *EQ IFTMBF) *OR +
                          (&W0065 *EQ IFCSUM) *OR (&W0065 *EQ +
                          INVOIC) *OR (&W0065 *EQ ORDERS) *OR +
                          (&W0065 *EQ ORDRSP) *OR (&W0065 *EQ +
                          IFTMCS) *OR (&W0065 *EQ IFTSTA) *OR +
                          (&W0065 *EQ APERAK) *OR (&W0065 *EQ +
                          DESADV) *OR (&W0065 *EQ PRICAT) *OR +
                          (&W0065 *EQ IFTMAN) *OR (&W0065 *EQ +
                          INVPRT) *OR (&W0065 *EQ PARTTC) *OR +
                          (&W0065 *EQ 'SV-CUS') *OR (&W0065 *EQ +
                          'DK-CUS')) THEN(DO)
             CHGVAR     VAR(&DATA) VALUE(&LIB *CAT 'EDIRE     ' +
                          *CAT &MEMBER)
             CALL       PGM(QSNDDTAQ) PARM(E081Q *LIBL    &LEN &DATA)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(TAG01)
                         ENDDO
        /*______________________________________*/
        /* Submit translate of messages in JOBQ */
        /*""""""""""""""""""""""""""""""""""""""*/
             IF         COND((&W0065 *EQ IMPORT) *OR (&W0065 *EQ +
                          EXPORT)) THEN(GOTO CMDLBL(SYSPED))
             IF         COND((&W0065 *EQ 'TET-IM') *OR (&W0065 *EQ +
                          'TET-EX')) THEN(GOTO CMDLBL(SYSPED))
             IF         COND((&W0065 *EQ 'SV-KON') *OR (&W0065 *EQ +
                          'SV-CUS')) THEN(GOTO CMDLBL(SYSPED))
             IF         COND(&W0065 *EQ 'CONTRL') THEN(GOTO +
                          CMDLBL(SYSPED))

/*N06*/      IF         COND(&PGM *EQ 'EDIOCC') THEN(DO)
             SBMJOB     CMD(CALL PGM(&PGM) PARM(EDIRE *LIBL +
                          &MEMBER)) JOB(RCVEDI) JOBD(SY400JOBD) +
                          JOBQ(SY400JOBQ)
/*N06*/      ENDDO
/*N06*/      IF         COND(&PGM *NE 'EDIOCC') THEN(DO)
             SBMJOB     CMD(CALL PGM(&PGM) PARM(EDIRE *LIBL +
                          &MEMBER)) JOB(RCVEDI) JOBD(SY400JOBD) +
                          JOBQ(SY400QER)
/*N06*/      ENDDO
        /*__________________________________*/
        /* Read next record from memberlist */
        /*""""""""""""""""""""""""""""""""""*/
             GOTO       CMDLBL(TAG01)
        /*_____________________________________________*/
        /* Submit translate of messages in SYSPED JOBQ */
        /*"""""""""""""""""""""""""""""""""""""""""""""*/
 SYSPED:     SBMJOB     CMD(CALL PGM(&PGM) PARM(EDIRE *LIBL +
                          &MEMBER)) JOB(RCVTVI) +
                          JOBD(SY400JOBD) +
                          JOBQ(SY400QER) INLLIBL(*JOBD)
        /*__________________________________*/
        /* Read next record from memberlist */
        /*""""""""""""""""""""""""""""""""""*/
             GOTO       CMDLBL(TAG01)
        /*______*/
        /* Exit */
        /*""""""*/
 EXIT:       RCLRSC
             ENDPGM
