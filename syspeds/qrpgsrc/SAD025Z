      *
      ************************************************************
      *** PROGRAM SKAL SKRIVE UT LASTELISTE (SYSPED) PLUKK OPD ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04 06 07 09-11 13-24        ***
      *** LEDIGE INDIKATORER 27-29 31-90 95-97                 ***
      ************************************************************
      *
     FKODTFM    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTSI    IF   E           K DISK
     FSADTRH    UF   E           K DISK
     FTURER     IF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD14    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR11)
     FSTED2     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FDATALIST  O    F  130        PRINTER OFLIND(*INOF)
     FKODTP     IF   E           K DISK
     FSAD025FZ  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HEVKT                  1      9  0
     D  WSVKT                  5      9  0
     D                 DS
     D  HEM3                   1      7  3
     D  WSM3                   3      6  2
     D                 DS
     D  HENAS                  1     30
     D  WSNAS                  1     20
     D                 DS
     D  HENAK                  1     30
     D  WSNAK                  1     25
     D                 DS
     D  HESV1                  1     25
     D  WSSV1                  1     25
     D                 DS
     D  TUDTD                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTÅ                  7      8  0
     D  TUDT                   1      8  0
     D                 DS
     D  XUDTD                  1      2  0
     D  XUDTM                  3      4  0
     D  XUDTÅ                  5      6  0
     D  XUDT                   1      6  0
      *
     C                   EXSR      INIT
      *
      ***
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     HEAD1K        SETLL     HEAD14
     C     UPRO          READE     HEAD14                                 94
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        9999
     C                   ADD       1             ZRECNO
     C                   MOVE      ' '           WSMED
     C                   MOVE      ' '           WSFT
     C                   MOVE      ' '           WSTXT
     C                   MOVE      HETLE         WSTLE
     C                   MOVE      HETLL         WSTLL
     C                   MOVE      HETLKU        WSTLKU
     C     HESDF         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       WSSDF
     C  N33              MOVEL     ST2NVN        WSSDF
     C     HESDT         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       WSSDT
     C  N33              MOVEL     ST2NVN        WSSDT
     C                   WRITE     SUBFIL
     C     UPRO          READE     HEAD14                                 94
     C  N94              END
     C                   END
      *
     C     YRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     YRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   ELSE
     C     ZRECNO        IFGT      YRECNO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   END
     C                   END
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      ***
      *
     C                   SETOFF                                       9830
      *
     C   03              MOVE      '1'           UIN01
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** HOPP TIL
     C   07              DO
     C                   EXSR      PRINT
     C  N40              GOTO      SLUTT
     C                   GOTO      START
     C                   END
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      VLHENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     VLHENT        BEGSR
     C***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     2             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       7             LINNBR
     C                   MVR                     LINNBR
     C     LINNBR        MULT      2             XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   SETON                                        99
     C     WSTLE         IFNE      *BLANKS
     C     WSTLL         ORNE      *BLANKS
     C     WSTLKU        ORNE      *BLANKS
     C     HEADFK        CHAIN     HEADF                              33
     C  N33              DO
     C                   MOVE      WSTLE         HETLE
     C                   MOVE      WSTLL         HETLL
     C                   MOVE      WSTLKU        HETLKU
     C                   EXCEPT    CHGHEA
     C                   END
     C                   END
     C     WSTLE         IFNE      *BLANKS
     C     WSTLL         ORNE      *BLANKS
     C     SATHK         CHAIN     SADTRH                             33
     C  N33              DO
     C                   MOVE      WSTLE         SETLE
     C                   MOVE      WSTLL         SETLL
     C                   EXCEPT    CHGSAE
     C                   END
     C                   END
     C     WSNR          IFEQ      '1'
     C     WSMED         ANDNE     'J'
     C                   ADD       1             XANTO             5 0
     C                   MOVE      'J'           WSMED
     C                   END
     C     WSNR          IFEQ      '4'
     C     WSMED         ANDEQ     'J'
     C                   SUB       1             XANTO             5 0
     C                   MOVE      ' '           WSMED
     C                   MOVE      ' '           WSFT
     C                   MOVE      *BLANKS       WSTXT
     C                   END
     C     WSNR          IFEQ      '5'
     C                   EXSR      VISE
     C                   END
     C     WSNR          IFEQ      '9'
     C                   EXFMT     BILDE02
     C     WSTXT         IFNE      *BLANKS
     C                   MOVE      'J'           WSFT
     C                   END
     C                   END
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       6             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISE          BEGSR
     C***********************************************
     C                   Z-ADD     2             UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
      *
     C                   ENDSR
      *
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C     XANTO         COMP      0                                  4040
     C   40              ADD       1             XLNR
     C   40              MOVE      XLNR          UANT
     C                   Z-ADD     0             XANTO
     C                   Z-ADD     21            XANTL             2 0
     C                   Z-ADD     1             XSIDE             2 0
     C                   EXCEPT    LHEAD
      *
     C                   Z-ADD     1             XRECNO            4 0
     C     STPRI         TAG
     C     XRECNO        CHAIN     SUBFIL                             60
     C  N60              DO
     C   40WSMED         IFNE      'J'
     C                   GOTO      STPRI2
     C                   END
     C     WSTLKU        COMP      'X'                                    61
     C     HEVS2         COMP      *BLANKS                                42
     C   42HEGM2         COMP      *BLANKS                                42
     C   42              ADD       1             XANTL
     C  N42              ADD       2             XANTL
     C     WSTXT         COMP      *BLANKS                                43
     C  N43              ADD       1             XANTL
     C     XANTL         IFGE      ANTGR
     C  N42
     CAN 27XANTL         IFEQ      ANTGR
     C                   EXCEPT    LLINJE
     C                   END
     C     KOPLAS        IFNE      'J'
     C                   EXCEPT    LBUNN
     C                   ELSE
     C                   EXCEPT    LBUNNL
     C                   END
     C                   ADD       1             XSIDE
     C                   EXCEPT    LHEAD
     C                   Z-ADD     22            XANTL
     C  N42              ADD       1             XANTL
     C                   END
     C                   ADD       1             XANTO
     C                   EXCEPT    LDET
     C  N42              EXCEPT    LDET2
     C  N43              EXCEPT    LDET3
     C                   END
     C     STPRI2        TAG
     C  N60              ADD       1             XRECNO
     C  N60              GOTO      STPRI
      *
     C   27XANTL         DOWLE     ANTGR
     C                   EXCEPT    LLINJE
     C                   ADD       1             XANTL
     C                   END
     C     KOPLAS        IFNE      'J'
     C                   EXCEPT    LBUNN
     C                   ELSE
     C                   EXCEPT    LBUNNL
     C                   END
     C                   Z-ADD     0             XANTO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    UAVD
     C     KODSIK        KLIST
     C                   KFLD                    YSUNI
     C                   KFLD                    USG
     C     TURERK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UPRO
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEAD1K        KLIST
     C                   KFLD                    UPRO
     C     SATHK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YKN
      *
     C     *ENTRY        PLIST
      *** UIN01='1'   --->   AVSLUTT PROGRAM
     C                   PARM                    UIN01             1
      *** UIN02='1'   --->   FORHÅNDSTRYKKEDE FORMULAR BENYTTES
     C                   PARM                    UIN02             1
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    USG               3
     C                   PARM                    UDTY              2
      *** UANT   ---> ANTALL UTSKRIFTER
     C                   PARM                    UANT              1
      *
     C     UIN02         COMP      '1'                                    27
     C     UANT          IFEQ      ' '
     C                   Z-ADD     0             XLNR              1 0
     C                   ELSE
     C                   MOVE      UANT          XLNR
     C                   END
      *
     C                   MOVE      '0'           UIN01
     C                   Z-ADD     0             YKN               8 0
     C                   MOVE      'SI'          YSUNI             2
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        HEM3
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     KODTAK        CHAIN     KODTA                              33
     C     KODSIK        CHAIN     KODTSI                             33
     C                   MOVE      KOAKNR        YKN
     C     KUND1K        CHAIN     CUNDL01                            33
     C  N33              DO
     C                   MOVE      KNAVN         XKNAVN           30
     C                   MOVE      ADR1          XADR1            30
     C                   MOVE      ADR2          XADR2            30
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          XADR3            30
     C                   ELSE
     C                   MOVEL     POSTNR        XADR3
     C                   MOVE      ADR3          XADR3
     C                   END
     C                   END
     C     TURERK        CHAIN     TURER                              33
     C                   MOVE      TUDTD         XUDTD
     C                   MOVE      TUDTM         XUDTM
     C                   MOVE      TUDTÅ         XUDTÅ
     C                   MOVE      TUKNA         YKN
     C     KUND1K        CHAIN     CUNDL01                            33
     C                   MOVE      KNAVN         XANAVN           30
     C*
      * HENT STRINGKODE
     C                   Z-ADD     95            WTSKRN            3 0
     C                   MOVE      UAVD          LOSAV2            4 0
     C                   CALL      'SYGE01'
     C                   PARM                    LOSAV2
     C                   PARM                    WTSKRN
     C                   PARM                    WOPNVN           10
     C                   PARM                    WOPTY            10
     C                   PARM                    WOPHEA            1
     C                   PARM                    WOPFM             5
      *
      *LASER ELLER IKKE
      *
     C     KODKEY        KLIST
     C                   KFLD                    WOPUNI
     C                   KFLD                    LOSAV2
     C                   KFLD                    WTSKRN
     C     *LIKE         DEFINE    KOPUNI        WOPUNI
     C                   MOVE      'P'           WOPUNI
     C     KODKEY        CHAIN     KODTP                              33
     C     KOPLAS        IFEQ      'J'
     C                   Z-ADD     58            ANTGR             3 0
     C                   ELSE
     C                   Z-ADD     64            ANTGR
     C                   END
      *
      *
     C     WOPFM         IFNE      *BLANKS
     C     WOPFM         CHAIN     KODTFM                             33
     C  N33KFMT1         IFNE      *BLANKS
     C                   MOVEL     KFMT1         XTEST1            1
     C     XTEST1        IFEQ      '¤'
     C                   MOVE      '0'           *IN27
     C                   END
     C                   END
     C                   END
     C*
      *
     C                   ENDSR
      *
     ODATALIST  E            LHEAD            01
     O                       KFMT1               75
     ODATALIST  E    27      LHEAD            04
     O                                           60 'L A S T E L I S T E'
     O          E    27      LHEAD            05
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E            LHEAD            06
     O               27                          73 '|'
     O               40                          83 'LISTE NR.'
     O               40      XLNR                85
     O                       UDTY               119
     O          E            LHEAD            06
     O                       UDTY               119
     O          E            LHEAD            07
     O                       XKNAVN              65
     O               27                          73 '|'
     O          E            LHEAD            08
     O                       XADR1               65
     O               27                          73 '|'
     O          E            LHEAD            09
     O                       XADR2               65
     O               27                          96 '------------------------'
     O               27                         120 '------------------------'
     O          E            LHEAD            10
     O                       XADR3               65
     O               27                          73 '|'
     O          E    27      LHEAD            11
     O                                           73 '|'
     O          E    27      LHEAD            12
     O                                           73 '|'
     O          E            LHEAD            13
     O                                           15 'TURNR:'
     O                       UPRO          Z     24
     O                                           35 'DATO:'
     O                       XUDT          Y     44
     O                                           56 'BILNR:'
     O                       TUBILN              67
     O               27                          73 '|'
     O          E            LHEAD            14
     O                                           15 'AGENT:'
     O                       TUKNA         Z     30
     O                       XANAVN              62
     O               27                          73 '|'
     O          E            LHEAD            15
     O                                           21 'TRANSPORTØR:'
     O                       TUKNT         Z     30
     O                       TUNAT               62
     O               27                          73 '|'
     O          E    27      LHEAD            16
     O                                           73 '|'
     O          E    27      LHEAD            17
     O                                           73 '|'
     O          E    27      LHEAD            18
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E    27      LHEAD            19
     O                                           12 'VAREPOST'
     O                                           16 '|'
     O                                           73 '|'
     O                                           82 'AVS/UTF.'
     O                                           83 '|'
     O                                           94 'BRUTTOVEKT'
     O                                           96 '|'
     O          E    27      LHEAD            20
     O                                           12 '   NR   '
     O                                           16 '|'
     O                                           45 'MERKER, NR, ANTALL KOLLI'
     O                                           70 'OG SLAG; VAREBESKRIVELSE'
     O                                           73 '|'
     O                                           82 '  LAND  '
     O                                           83 '|'
     O                                           94 '   (KG)   '
     O                                           96 '|'
     O                                          114 'FOR TOLLVESENET'
     O          E    27      LHEAD            21
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E   N27      LHEAD            21
     O          E            LDET        1
     O                       XANTO         Z     10
     O                       HEGM1               32
     O                       HENT          Z     41
     O                       HEVS1               70
     O                                           79 'NO'
     O                       HEVKT         Z     92
     O                       WSTLE              104
     O                                          106 '-'
     O                       WSTLL              117
     O               61                         109 'K.UTF'
     O               27                          16 '|'
     O               27                          73 '|'
     O               27                          83 '|'
     O               27                          96 '|'
     O          E            LDET2       1
     O                       HEGM2               32
     O                       HEVS2               70
     O               27                          16 '|'
     O               27                          73 '|'
     O               27                          83 '|'
     O               27                          96 '|'
     O          E            LDET3       1
     O                       WSTXT               70
     O               27                          16 '|'
     O               27                          73 '|'
     O               27                          83 '|'
     O               27                          96 '|'
     O          E    27      LLINJE      1
     O                                           16 '|'
     O                                           73 '|'
     O                                           83 '|'
     O                                           96 '|'
     O          E    27      LBUNN            66
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E            LBUNN            69
     O                       KSINAV             116
     O          E            LBUNN            69
     O                                           15 'RG-0199'
     O                                          120 '........................'
     O          E    27      LBUNN            70
     O                                          120 '     (UNDERSKRIFT)      '
     O          E    27      LBUNNL           60
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E            LBUNNL           63
     O                       KSINAV             116
     O          E            LBUNNL           63
     O                                           15 'RG-0199'
     O                                          120 '........................'
     O          E    27      LBUNNL           64
     O                                          120 '     (UNDERSKRIFT)      '
     ODATALIST  E            LBUNNL           65
     O                       KFMT2               75
     OHEADFR    E            CHGHEA
     O                       HETLE
     O                       HETLKU
     O                       HETLL
     OSADTRHR   E            CHGSAE
     O                       SETLL
     O                       SETLE
