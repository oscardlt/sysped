N01  A* UTVIDET FRA 11/43 TIL 16/70
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/CUOTI)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 16 70 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            COFIRM         2A  H
     A            COKUNR         8S 0H
     A            WSNR           1A  I  6  2VALUES(' ' '4' '9')
     A            COAVD     R        O  6  5REFFLD(CUOTIR/COAVD CUOTI)
     A            COKODE    R        O  6 10REFFLD(CUOTIR/COKODE CUOTI)
     A            KFSOTX        30A  O  6 14
     A            WSKRAV    R    1A  B  6 45REFFLD(CUOTIR/COKRAV CUOTI)
     A            SFGYLD        15   O  6 48COLOR(RED)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Avslutt')
     A                                      CF04(04 'SØKING')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'VCUNDO'
     A                                      COLOR(BLU)
     A                                  1 13MSGCON(050 JOV0868 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 65SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(010 JOV0867 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0O  2 13EDTCDE(4)
     A                                      COLOR(WHT)
     A            KUNAVN        30A  O  2 22COLOR(WHT)
     A                                  3  2MSGCON(035 JOV0866 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(065 JOV0869 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KODE         3A  B  5 10DSPATR(UL)
     A  90                                  DSPATR(HI)
     A            W2KRAV         1A  B  5 45DSPATR(UL)
     A  89                                  DSPATR(HI)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(16)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(29 YBC0237 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CF04(04 'SØKING')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'VCUNDO'
     A                                      COLOR(BLU)
     A                                  1 13MSGCON(050 JOV0868 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 JOV0867 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0O  2 13EDTCDE(4)
     A                                      COLOR(WHT)
     A            KUNAVN        30A  O  2 22COLOR(WHT)
     A                                  3  2MSGCON(035 JOV0866 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(065 JOV0869 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2KODE         3A  B  5 10DSPATR(UL)
     A  90                                  DSPATR(HI)
     A            W2KRAV         1A  B  5 45DSPATR(UL)
     A  89                                  DSPATR(HI)
     A N30                             16  2MSGCON(029 YBC0237 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 16  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
