      *
      **************************************************************
      *** PROGRAM SKAL ENDRE STATUS PÅ TUR ELLER ORDRE
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01-02 04-26
      *** LEDIGE INDIKATORER 27-29 31 32 34-97
      **************************************************************
      *
     FGSSORF    UF   E           K DISK
     FGSSORL21  UF   E           K DISK
     F                                     RENAME(GSSORFR:GSSORF21)
     FGSSTRF    UF   E           K DISK
     FSTS       O  A E           K DISK
     FGSS060D   CF   E             WORKSTN
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      6  0
     D  XDAG                   7      8  0
     D  XMND                   9     10  0
     D  XAAR                  11     14  0
     D                 DS
     D  SSDTÅ                  1      4  0
     D  SSDTM                  5      6  0
     D  SSDTD                  7      8  0
     D  SSDT                   1      8  0
      *
     C     *ENTRY        PLIST
     C                   PARM                    WSUSER           10
      *
     C                   MOVE      '1'           WSSTT
     C                   MOVE      '0'           WSSTO
     C     START         TAG
     C                   EXFMT     GSS060DA
     C                   MOVEA     '00'          *IN(98)
     C                   MOVE      '0'           *IN30
      *
     C   03              GOTO      SLUTT
      *
     C     WSTNO         IFNE      0
     C     WSTNO         CHAIN(N)  GSSTRF                             99
     C   99              MOVE      'YBC0537'     MSGNR
     C   99              MOVE      '1'           *IN30
     C   99              GOTO      START
     C*          WSSTT     CABNE'1'       START
     C     TRST          IFNE      '2'
     C     TRST          ANDNE     '3'
     C     TRST          ANDNE     'X'
     C                   GOTO      START
     C                   END
     C                   ELSE
     C     WSONO         CHAIN(N)  GSSORF                             98
     C   98              MOVE      'YBC0538'     MSGNR
     C   98              MOVE      '1'           *IN30
     C   30              GOTO      START
     C     ORST          IFNE      '2'
     C     ORST          ANDNE     '3'
     C     ORST          ANDNE     'X'
     C     ORST          ANDNE     'D'
     C                   GOTO      START
     C                   END
     C                   END
      *
     C                   TIME                    XTIME
     C                   MOVE      XAAR          SSDTÅ
     C                   MOVE      XMND          SSDTM
     C                   MOVE      XDAG          SSDTD
     C                   MOVE      XTID          SSTM
     C                   MOVE      WSUSER        SSUSR
     C                   MOVE      'SHI'         SSSG
      *
     C     WSTNO         IFNE      0
      *
     C     TRST          IFNE      'X'
     C     WSTNO         SETLL     GSSORL21
     C     WSTNO         READE     GSSORL21                               33
     C                   MOVE      'O'           SSRTYP
     C     *IN33         DOWEQ     '0'
     C                   MOVEL     ORONO         XN2               2 0
     C                   Z-ADD     XN2           SSAVD
     C                   MOVE      ORONO         SSOPD
     C                   MOVE      ORST          SSSTG
     C                   MOVE      WSSTT         SSSTN
     C                   WRITE     STSR
     C                   MOVE      WSSTT         ORST
     C     ORLC          IFEQ      'ONWAY'
     C     WSSTT         ANDEQ     '1'
     C                   MOVE      ORLC2         ORLC
     C                   MOVE      ORLC3         ORLC2
     C                   MOVE      *BLANKS       ORLC3
     C                   MOVE      ORDTL2        ORDTL
     C                   MOVE      ORDTL3        ORDTL2
     C                   MOVE      *ZEROS        ORDTL3
     C                   END
     C                   EXCEPT    NYST21
     C     WSTNO         READE     GSSORL21                               33
     C  N33              ENDDO
     C                   END
      *
     C     WSTNO         CHAIN     GSSTRF                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'T'           SSRTYP
     C                   MOVEL     TRTNO         SSAVD
     C                   MOVE      TRTNO         SSOPD
     C                   MOVE      TRST          SSSTG
     C                   MOVE      WSSTT         SSSTN
     C                   WRITE     STSR
     C                   MOVE      WSSTT         TRST
     C                   EXCEPT    NYSTTR
     C                   END
      *
     C                   ELSE
      *
     C     WSONO         CHAIN     GSSORF                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'O'           SSRTYP
     C                   MOVEL     ORONO         XN2               2 0
     C                   Z-ADD     XN2           SSAVD
     C                   MOVE      ORONO         SSOPD
     C                   MOVE      ORST          SSSTG
     C                   MOVE      WSSTO         SSSTN
     C                   WRITE     STSR
      *
     C     ORST          IFNE      'X'
     C     ORST          ANDNE     'D'
     C                   SELECT
     C     ORST          WHENEQ    '1'
     C                   MOVE      *ZEROS        ORTNO
     C                   OTHER
     C     ORLC          IFEQ      'ONWAY'
     C                   MOVE      ORLC2         ORLC
     C                   MOVE      ORLC3         ORLC2
     C                   MOVE      *BLANKS       ORLC3
     C                   MOVE      ORDTL2        ORDTL
     C                   MOVE      ORDTL3        ORDTL2
     C                   MOVE      *ZEROS        ORDTL3
     C                   END
     C                   MOVE      *ZEROS        ORTNO
     C                   ENDSL
     C                   END
      *
     C                   MOVE      WSSTO         ORST
     C                   EXCEPT    NYSTOR
     C                   END
      *
     C                   END
      *
     C                   MOVE      'YBC0539'     MSGNR
     C                   MOVE      *ZEROS        WSTNO
     C                   MOVE      *ZEROS        WSONO
     C                   MOVE      '1'           *IN30
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     OGSSORFR   E            NYSTOR
     O                       ORST
     O                       ORTNO
     O                       ORLC
     O                       ORLC2
     O                       ORLC3
     O                       ORDTL
     O                       ORDTL2
     O                       ORDTL3
     OGSSORF21  E            NYST21
     O                       ORST
     O                       ORLC
     O                       ORLC2
     O                       ORLC3
     O                       ORDTL
     O                       ORDTL2
     O                       ORDTL3
     OGSSTRFR   E            NYSTTR
     O                       TRST
