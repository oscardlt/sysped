     FDKT057D   CF   E             WORKSTN
     F                                     SFILE(DKT057DA:ZRECNO)
     FDKTVK     UF A E           K DISK
     FDKTVK1    IF   E           K DISK
     F                                     RENAME(DKTVKR:DKTVKR1)
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    *IN30         WIN30
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     VKKEY1        KLIST
     C                   KFLD                    DKVK_KD
     C                   KFLD                    DKVK_DTS
     C     VKKEY2        KLIST
     C                   KFLD                    ZVVK_KD
     C                   KFLD                    ZVVK_DTS
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN93=*ON
     C                   WRITE     DKT057DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     DKTVK1
     C                   READ      DKTVK1                                 94

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     DKT057DA
     C                   READ      DKTVK1                                 94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     DKT057DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     DKT057DE
     C                   WRITE     DKT057DØ
     C                   WRITE     DKT057DC
     C                   EXFMT     DKT057DB

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   ENDIF
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     DKT057DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR

     C                   CLEAR                   DKT057DD
     C                   MOVEA     '0000'        *IN(30)

     C     SRBA1         TAG

      *____________
      * Blanke felt
      *""""""""""""
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     DKT057DØ
     C                   EXFMT     DKT057DD
     C                   MOVEA     '000000'      *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN05         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'
      *_______
      * F4=Søk
      *"""""""
     C     *IN04         IFEQ      '1'
     C                   ENDIF
      *____________
      * Teste input-1
      *""""""""""""
      * Kod må vare utfyllt
     C     ZVVK_KD       IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'DKT0025'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Kurs
     C                   IF        ZVVK_KRS = 0
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'DKT0024'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Omräkningsfaktor
     C     ZVVK_OMR      COMP      1                                  3030
     C   30ZVVK_OMR      COMP      100                                3030
     C                   IF        *IN30
     C                   EVAL      *IN33=*ON
     C                   MOVE      'DKT0026'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Datum, från
     C                   IF        ZVVK_DTS = 0
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN34=*ON
     C                   MOVE      'DKT0028'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Datum, till
     C                   IF        ZVVK_DTE = 0
     C                   EVAL      ZVVK_DTE = 99999999
     C*                  EVAL      *IN30=*ON
     C*                  EVAL      *IN35=*ON
     C*                  MOVE      'DKT0029'     ZMSGNO
     C*                  GOTO      SRBA1
     C                   ENDIF
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   EVAL      DKVK_KD=ZVVK_KD
     C                   EVAL      DKVK_DTS=ZVVK_DTS
     C                   EVAL      DKVK_DTE=ZVVK_DTE
     C                   EVAL      DKVK_OMR=ZVVK_OMR
     C                   EVAL      DKVK_KRS=ZVVK_KRS
     C                   WRITE     DKTVKR
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   EVAL      ZRECNO=WRECNO
     C                   WRITE     DKT057DA

     C                   GOTO      SRBA1

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    DKT057DA
      *____________
      * Flytte felt
      *""""""""""""
     C     VKKEY1        CHAIN(N)  DKTVK                              53
     C                   MOVEA     '0000'        *IN(30)
     C                   EVAL      ZVVK_KD=DKVK_KD
     C                   EVAL      ZVVK_DTS=DKVK_DTS
     C                   EVAL      ZVVK_DTE=DKVK_DTE
     C                   EVAL      ZVVK_OMR=DKVK_OMR
     C                   EVAL      ZVVK_KRS=DKVK_KRS

     C     SRBB1         TAG

     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     DKT057DØ
     C                   EXFMT     DKT057DD2
     C                   MOVEA     '000000'      *IN(30)
     C     *IN12         IFEQ      '0'
      *_______
      * F4=Søk
      *"""""""
     C     *IN04         IFEQ      '1'
     C                   ENDIF
      *____________
      * Teste input-1
      *""""""""""""
      * Kurs
     C                   IF        ZVVK_KRS = 0
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'DKT0024'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Omräkningsfaktor
     C     ZVVK_OMR      COMP      1                                  3030
     C   30ZVVK_OMR      COMP      100                                3030
     C                   IF        *IN30
     C                   EVAL      *IN33=*ON
     C                   MOVE      'DKT0026'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     VKKEY2        CHAIN     DKTVK                              53
     C                   EVAL      DKVK_OMR=ZVVK_OMR
     C                   EVAL      DKVK_KRS=ZVVK_KRS
     C                   UPDATE    DKTVKR

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     VKKEY1        CHAIN     DKTVK                              53
     C                   DELETE    DKTVKR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   DKT057DA
     C                   UPDATE    DKT057DA
     C                   EVAL      *IN95=*ON

     C                   ENDSR
