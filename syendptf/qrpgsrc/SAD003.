********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NY OPSJON FOR KONVERTERING AV EDI
10XXX * 02 YBC PTF10 VIS FIL I IFS-OMRÅDE
10XXX * 03 YBC PTF10 OPPDRAGSNR ENDRET FRA 5.0 TIL 7.0
10321 * 04 YBC PTF10 VIS KONVERTERT UTGÅENDE EDI-FACT
10321 * 05 YBC PTF10 VIS alle sendinger på morttaket
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 06 YBC PTF11 OMBEREGNING
11110 * 07 YBC PTF11 VIS FIL FRA IFS-OMRÅDE
11134 * 08 CB  PTF11 VIS LOGG OG FTP-LOGG
12XXX * 09 SH  PTF12 EKSTRA PARAMETER VED KALL PÅ EDI41S OG EDI44S
12XXX * 10 SH  PTF12 1=omsending
12084 * 11 YBC PTF12 Vis regel/betingelse ved NCTS
********************************************************************
     FSAD003FM  CF   E             WORKSTN
     F                                     SFILE(SAD003A:ZRECNO)
     F                                     SFILE(SAD003E:ÆRECNO)
     FEDIM2     IF   E           K DISK
     FEDIM      IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMR0)
     FTVINF     IF   E           K DISK
s10  F*DIS      IF   E           K DISK
N10  FEDIS      UF   E           K DISK
N10  FEDISS     O  A E             DISK
N02  FEDISX     IF   E           K DISK
N02  FFIRMARC   IF   E           K DISK
N02  FARUSERF   IF   E           K DISK
N11  FTRREGEL1  IF   E           K DISK
N11  D                 DS
N11  D F0078A                  1     70
N11  D F0078B                 71    140
N11  D F0078C                141    210
N11  D F0078D                211    280
N11  D F0078E                281    350
N11  D FT                      1    350    DIM(5)
N02  D                SDS
N02  D   UserName            254    263
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    M0068A        W0068A
     C     *LIKE         DEFINE    M0068B        W0068B
     C     *LIKE         DEFINE    M0068C        W0068C
     C     *LIKE         DEFINE    FLN           WLN
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C                   MOVE      *ZEROS        W0068A
     C                   MOVE      *ZEROS        W0068B
     C                   MOVE      *ZEROS        W0068C
     C                   MOVE      *ZEROS        WLN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WTDN
     C                   KFLD                    W0068A
     C                   KFLD                    W0068B
     C                   KFLD                    W0068C
     C     KEYA1         KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WTDN
     C     KEYB          KLIST
     C                   KFLD                    MMN
     C                   KFLD                    WLN
     C     KEYC          KLIST
     C                   KFLD                    MSN
N11  C     TRREGEL1K     KLIST
N11  C                   KFLD                    TRUNI1
N11  C                   KFLD                    TRUNI2
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     WAVD          PARM                    WAVD
     C     WTDN          PARM                    WTDN
     C                   PARM                    WIN03
      * 41 PÅ  -->  TVINN-IMPORT
      * 42 PÅ  -->  TVINN-EXPORT
      * 43 PÅ  -->  TET-EKSPORT
      * 44 PÅ  -->  TET-IMPORT
      * 45 PÅ  -->  ALLE
     C     WIN03         COMP      '0'                                    41    SAD-IMPORT
     C     WIN03         COMP      '1'                                    42    SAD-EKSPORT
     C     WIN03         COMP      '2'                                    43    TET-UT
     C     WIN03         COMP      '3'                                    44    TET-INN
     C     WIN03         COMP      '4'                                    45    ALLE EDI-FACT
     C     WIN03         COMP      '5'                                    46    SV NCTS-UT
     C     WIN03         COMP      '6'                                    47    SY NCTS-INN
N10  C                   CALL      'SYGE15C'
N10  C                   PARM                    WDT               8
N10  C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     SAD003B
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EDIM2
     C     SRB2          TAG
     C     KEYA1         READE     EDIM2                                  94
     C  N94              DO
     C   41M1001         IFNE      '929'
     C                   GOTO      SRB2
     C                   END
     C   42M1001         IFNE      '830'
     C                   GOTO      SRB2
     C                   END
     C   43M1001         IFNE      'TET'
     C                   GOTO      SRB2
     C                   END
     C   44M1001         IFNE      'TEI'
     C                   GOTO      SRB2
     C                   END
     C   46M1001         IFNE      'STE'
     C                   GOTO      SRB2
     C                   END
     C   47M1001         IFNE      'STI'
     C                   GOTO      SRB2
     C                   END
     C     MSR           COMP      'R'                                    27
     C                   ADD       1             ZRECNO
     C                   MOVEL     M0004         W0004
     C                   MOVEL     M0010         W0010
     C                   WRITE     SAD003A
     C                   GOTO      SRB2
     C                   END
N06   *
N06  C                   EVAL      WTDN = WTDN * -1
N06  C                   IF        WTDN < 0
N06   * LES OMBEREGNING
N06  C     KEYA          SETLL     EDIM2
N06  C                   GOTO      SRB2
N06  C                   END
N06   *
     C     ZRECNO        CABEQ     0             SRB9
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SAD003Ø
     C                   WRITE     SAD003C
     C                   EXFMT     SAD003B
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SAD003A                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '5'           SRBE
     C     ZOP           CASEQ     '6'           SRBF
     C     ZOP           CASEQ     '7'           SRBG
     C     ZOP           CASEQ     '9'           SRBH
     C     ZOP           CASEQ     'P'           SRBI
N01  C     ZOP           CASEQ     'V'           SRBVSAD
S07  C*    ZOP           CASEQ     'I'           SRBIFS
N07  C     ZOP           CASEQ     'I'           SRBIFS2
N05  C     ZOP           CASEQ     'A'           SRALLE
N07  C     ZOP           CASEQ     'W'           SRBIFS
N08  C     ZOP           CASEQ     'L'           SRBFTP
N10  C     ZOP           CASEQ     '1'           SRBA
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3
     C                   END
      *
     C     SRB9          TAG
     C                   MOVE      *IN03         WIN03
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele meldingsloggen.                           SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      '0'           *IN28
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SAD003Ø
     C     MMN           CHAIN     EDIM                               20
     C     SRBE1         TAG
     C                   MOVEA     '00'          *IN(25)
     C                   MOVE      WAVD          MAVD
     C                   MOVE      WTDN          MTDN
     C  N28              EXFMT     SAD003D
     C   28              EXFMT     SAD003I
     C     *IN25         IFEQ      '1'
     C                   MOVE      '1'           *IN28
     C                   GOTO      SRBE1
     C                   END
     C     *IN26         IFEQ      '1'
     C                   MOVE      '0'           *IN28
     C                   GOTO      SRBE1
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBE9         TAG
     C                   MOVE      *BLANKS       ZOP
     C     MSR           COMP      'R'                                    27
     C                   UPDATE    SAD003A
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise feilmeldinger                                  SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
     C     SRBF1         TAG
     C                   MOVE      '1'           *IN30
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN83
     C                   MOVE      WAVD          MAVD
     C                   MOVE      WTDN          MTDN
     C                   WRITE     SAD003F
     C                   MOVEA     '000'         *IN(83)
     C                   Z-ADD     *ZERO         ÆRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYB          SETLL     TVINF
     C     MMN           READE     TVINF                                  84
      *
     C     *IN84         DOWEQ     '0'
     C                   MOVE      '0'           *IN30
     C                   ADD       1             ÆRECNO
     C                   WRITE     SAD003E
N11   *
N11   * VIS REGEL ELLER BETINGELSE
N11  C                   IF        M1001 = 'TET' OR M1001 = 'TEI'                2<-B
N11  C     'REGEL:'      SCAN      F0078A        XPOS              3 0    33
N11  C                   IF        *IN33                                          3<-B
N11  C                   EVAL      TRUNI1 = 'R'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078A:XPOS+7:3)
N11  C                   ELSE                                                     3
N11  C     'REGEL:'      SCAN      F0078B        XPOS              3 0    33
N11  C                   IF        *IN33                                           4<-B
N11  C                   EVAL      TRUNI1 = 'R'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078B:XPOS+7:3)
N11  C                   ELSE                                                      4
N11  C     'REGEL:'      SCAN      F0078C        XPOS              3 0    33
N11  C                   IF        *IN33                                            5<-B
N11  C                   EVAL      TRUNI1 = 'R'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078C:XPOS+7:3)
N11  C                   ELSE                                                       5
N11  C     'REGEL:'      SCAN      F0078D        XPOS              3 0    33
N11  C                   IF        *IN33                                             6<-B
N11  C                   EVAL      TRUNI1 = 'R'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078D:XPOS+7:3)
N11  C                   ELSE                                                        6
N11  C     'REGEL:'      SCAN      F0078E        XPOS              3 0    33
N11  C                   IF        *IN33                                              7<-B
N11  C                   EVAL      TRUNI1 = 'R'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078E:XPOS+7:3)
N11  C                   END                                                          7<-E
N11  C                   END                                                         6<-E
N11  C                   END                                                        5<-E
N11  C                   END                                                       4<-E
N11  C                   END                                                      3<-E
N11  C                   IF        *IN33                                          3<-B
N11  C                   EVAL      FT     = *BLANKS
N11  C                   EVAL      F0077  = *BLANKS
N11  C                   EVAL      F4815  = *BLANKS
N11  C     TRREGEL1K     CHAIN     TRREGEL1                           33
N11  C                   IF        *IN33 = *OFF                                    4<-B
N11  C                   EVAL      F0078A = TRTXT1
N11  C                   EVAL      F0078B = TRTXT2
N11  C                   EVAL      F0078C = TRTXT3
N11  C                   EVAL      F0078D = TRTXT4
N11  C                   EVAL      F0078E = TRTXT5
N11  C                   ADD       1             ÆRECNO
N11  C                   WRITE     SAD003E
N11  C                   IF        TRTXT6 <> *BLANKS                                5<-B
N11  C                   EVAL      F0078A = TRTXT6
N11  C                   EVAL      F0078B = TRTXT7
N11  C                   EVAL      F0078C = TRTXT8
N11  C                   EVAL      F0078D = TRTXT9
N11  C                   EVAL      F0078E = TRTXTA
N11  C                   ADD       1             ÆRECNO
N11  C                   WRITE     SAD003E
N11  C                   END                                                        5<-E
N11  C                   END                                                       4<-E
N11  C                   ELSE                                                     3
N11  C     'BETINGELSE:' SCAN      F0078A        XPOS              3 0    33
N11  C                   IF        *IN33                                          3<-B
N11  C                   EVAL      TRUNI1 = 'C'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078A:XPOS+12:3)
N11  C                   ELSE                                                     3
N11  C     'BETINGELSE:' SCAN      F0078B        XPOS              3 0    33
N11  C                   IF        *IN33                                           4<-B
N11  C                   EVAL      TRUNI1 = 'C'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078B:XPOS+12:3)
N11  C                   ELSE                                                      4
N11  C     'BETINGELSE:' SCAN      F0078C        XPOS              3 0    33
N11  C                   IF        *IN33                                            5<-B
N11  C                   EVAL      TRUNI1 = 'C'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078C:XPOS+12:3)
N11  C                   ELSE                                                       5
N11  C     'BETINGELSE:' SCAN      F0078D        XPOS              3 0    33
N11  C                   IF        *IN33                                             6<-B
N11  C                   EVAL      TRUNI1 = 'C'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078D:XPOS+12:3)
N11  C                   ELSE                                                        6
N11  C     'BETINGELSE:' SCAN      F0078E        XPOS              3 0    33
N11  C                   IF        *IN33                                              7<-B
N11  C                   EVAL      TRUNI1 = 'C'
N11  C                   EVAL      TRUNI2 = %SUBST(F0078E:XPOS+12:3)
N11  C                   END                                                          7<-E
N11  C                   END                                                         6<-E
N11  C                   END                                                        5<-E
N11  C                   END                                                       4<-E
N11  C                   END                                                      3<-E
N11  C                   IF        *IN33                                           4<-B
N11  C                   EVAL      FT     = *BLANKS
N11  C                   EVAL      F0077  = *BLANKS
N11  C                   EVAL      F4815  = *BLANKS
N11  C     TRREGEL1K     CHAIN     TRREGEL1                           33
N11  C                   IF        *IN33 = *OFF                                     5<-B
N11  C                   EVAL      F0078A = TRTXT1
N11  C                   EVAL      F0078B = TRTXT2
N11  C                   EVAL      F0078C = TRTXT3
N11  C                   EVAL      F0078D = TRTXT4
N11  C                   EVAL      F0078E = TRTXT5
N11  C                   ADD       1             ÆRECNO
N11  C                   WRITE     SAD003E
N11  C                   IF        TRTXT6 <> *BLANKS                                 6<-B
N11  C                   EVAL      F0078A = TRTXT6
N11  C                   EVAL      F0078B = TRTXT7
N11  C                   EVAL      F0078C = TRTXT8
N11  C                   EVAL      F0078D = TRTXT9
N11  C                   EVAL      F0078E = TRTXTA
N11  C                   ADD       1             ÆRECNO
N11  C                   WRITE     SAD003E
N11  C                   END                                                         6<-E
N11  C                   END                                                        5<-E
N11  C                   END                                                       4<-E
N11  C                   END                                                      3<-E
N11  C                   END                                                     2<-E
N11   *
     C     MMN           READE     TVINF                                  84
     C  N84              END
     C                   Z-ADD     1             ÆRECNO
      *_______________
      * Ingen poster ?
      *"""""""""""""""
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SY00110'     ZMSGNO
     C                   GOTO      SRBF9
     C                   END
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C                   MOVEA     '11'          *IN(81)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SAD003Ø
     C                   WRITE     SAD003C
     C                   EXFMT     SAD003F
     C     *IN05         CABEQ     '1'           SRBF1
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBF9         TAG
     C  N30              MOVE      *BLANKS       ZOP
     C  N30MSR           COMP      'R'                                    27
     C  N30              UPDATE    SAD003A
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk filkomponent                            SRBG /
      *////////////////////////////////////////////////////////////
     C     SRBG          BEGSR
      *___________________
      * Hente sendningsref
      *"""""""""""""""""""
s10  C*    KEYC          CHAIN     EDIS                               20
n10  C     KEYC          CHAIN(n)  EDIS                               20
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
S02  C*                  MOVEL(P)  'DSPPFM FILE('QCMDA            50
N02  C                   MOVEL(P)  'DSPPFM FILE('QCMDA           512
     C     QCMDA         CAT       SLIB:0        QCMDA
     C     QCMDA         CAT       '/':0         QCMDA
     C     QCMDA         CAT       SFILE:0       QCMDA
     C     QCMDA         CAT       '_) MBR(':0   QCMDA
     C     QCMDA         CAT       SMBR:0        QCMDA
     C     QCMDA         CAT       ')':0         QCMDA
S02  C*                  Z-ADD     50            QCMDL            15 5
N02  C                   Z-ADD     512           QCMDL            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI44R'
     C                   PARM      SLIB          SLIB
     C                   PARM      SFILE         SFILE
     C                   PARM      SMBR          SMBR
N08  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI44R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N08  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI44S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N08  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI44S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N08  C                   PARM                    ZOP
N07  C     SFILE         WHENEQ    '*IFS'
N07  C                   EXSR      SRBIFS2
N07  C                   GOTO      SRBG9
     C                   ENDSL
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C  N30              MOVE      *BLANKS       ZOP
     C  N30MSR           COMP      'R'                                    27
     C  N30              UPDATE    SAD003A
N07  C     SRBG9         TAG
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise EDI-FACT                                       SRBH /
      *////////////////////////////////////////////////////////////
     C*
     C     SRBH          BEGSR
s10  C*    KEYC          CHAIN     EDIS                               20
n10  C     KEYC          CHAIN(n)  EDIS                               20
     C     *IN20         IFEQ      '0'
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLSRBH
     C                   END
      * Vise komponent.
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
     C                   CALL      'EDI41RX'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N08  C                   PARM                    ZOP
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI41R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N08  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI41R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N08  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI41S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N08  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI41S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N08  C                   PARM                    ZOP
     C                   ENDSL
     C                   END
     C                   END
      *
     C                   MOVE      *BLANKS       ZOP
     C     MSR           COMP      'R'                                    27
     C                   UPDATE    SAD003A
      *
     C     SLSRBH        ENDSR
N05   ****************************************************************************
N05  C     SRALLE        BEGSR
N05   ****************************************************************************
N05  C                   CALL      'EDI66R'
N05  C                   PARM                    MSN
N05  C                   MOVE      *BLANKS       ZOP
N05  C     MSR           COMP      'R'                                    27
N05  C                   UPDATE    SAD003A
N05  C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Skrive ut melding                                   SRBI /
      *////////////////////////////////////////////////////////////
     C     SRBI          BEGSR
     C     M1N07         IFEQ      'DME'
     C     M1N07         OREQ      'DFI'
     C                   SELECT
     C     *IN41         WHENEQ    '1'
     C                   MOVE      'SADI'        UTYPE             4
     C     *IN42         WHENEQ    '1'
     C                   MOVE      'SADE'        UTYPE
     C                   ENDSL
     C                   CALL      'TVI28R'
     C                   PARM                    UTYPE
     C                   PARM                    MMN
     C                   PARM                    WAVD
     C                   PARM                    WTDN
     C                   END
      *
     C     M1001         IFEQ      'TEI'
     C     M1N07         IFEQ      '043'
     C                   MOVEL     WAVD          UWS              10
S03  C*                  MOVE      WTDN          UWS
S03  C*                  CALL      'TR157C'
N03  C                   MOVEL     WTDN          UUSER            10
N03  C                   CALL      'TR157R'
N03  C                   PARM                    UUSER
     C                   PARM                    UWS
     C                   END
      *
     C     M1N07         IFEQ      '008'
     C     M1N07         OREQ      '058'
     C     M1N07         OREQ      'CTL'
     C     M1N07         OREQ      '025'
     C                   MOVE      WAVD          UAVD              4
     C                   MOVE      WTDN          UTDN              7
     C                   MOVE      MMN           UMN               9
     C                   CALL      'TR156C'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UMN
     C                   END
     C                   END
      *
     C     M1001         IFEQ      'TET'
     C     M1N07         IFEQ      '009'
     C     M1N07         OREQ      '016'
     C     M1N07         OREQ      '051'
     C     M1N07         OREQ      '060'
     C     M1N07         OREQ      'CTL'
     C                   MOVE      WAVD          UAVD              4
     C                   MOVE      WTDN          UTDN              7
     C                   MOVE      MMN           UMN               9
     C                   CALL      'TR106C'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UMN
     C                   END
     C                   END
      *
     C                   MOVE      *BLANKS       ZOP
     C     MSR           COMP      'R'                                    27
     C                   UPDATE    SAD003A
      *
     C                   ENDSR
N01   *////////////////////////////////////////////////////////////
N01   *  VIS KONVERTERT SAD                               SRBVSAD /
N01   *////////////////////////////////////////////////////////////
N01  C     SRBVSAD       BEGSR
N01  C                   IF        *IN41
N01  C                   MOVE      WAVD          UAVD              4
N01  C                   MOVE      WTDN          UTDN              7
N01  C                   MOVE      MSN           USN               9
N01  C                   CALL      'SAD016CL'
N01  C                   PARM                    UAVD
N01  C                   PARM                    UTDN
N01  C                   PARM                    USN
N01  C                   END
N04  C                   IF        *IN42
N04  C                   MOVE      WAVD          UAVD              4
N04  C                   MOVE      WTDN          UTDN              7
N04  C                   MOVE      MSN           USN               9
N04  C                   CALL      'SAD017CL'
N04  C                   PARM                    UAVD
N04  C                   PARM                    UTDN
N04  C                   PARM                    USN
N04  C                   END
N01   *
N01  C                   MOVE      *BLANKS       ZOP
N01  C     MSR           COMP      'R'                                    27
N01  C                   UPDATE    SAD003A
N01   *
N01  C                   ENDSR
N02   *////////////////////////////////////////////////////////////
N02   *  VIS FIL I IFS-OMRÅDE                             SRBIFS  /
N02   *////////////////////////////////////////////////////////////
N02  C     SRBIFS        BEGSR
s10  C*    KEYC          CHAIN     EDIS                               20
n10  C     KEYC          CHAIN(n)  EDIS                               20
N02   * HEnt ifsnavn fra x-ref
N02  C                   IF        SIFS = *BLANKS
N02  C     SMBR          CHAIN     EDISX                              51
N02  C                   ELSE
N02  C                   EVAL      SXIFSOBJ = %SUBST(SIFS:2:255)
N02  C                   EVAL      *IN51 = *OFF
N02  C                   END
N02  c  N51SXIFSOBJ      comp      *blanks                                51
N02  C  N51*LOVAL        setll     FIRMARC
N02  c  N51              call      'SYGE35CL'
N02  c                   parm      SXIFSOBJ      UIfsObj         256
N02  c                   parm                    UIN51             1
N02  c  N51UIN51         comp      '1'                                    51
N02  c  N51              READ      firmarc                                51
N02  C                   IF        *IN51=*OFF
N02   * inetern/esketrn overstyrt på usernivå??
N02  c     UserName      chain     aruserf                            51
N02   * Fila må over i temporært område som ligger innenfor http-config
N02   * lag det om det ikke finnes
N02  C                   EVAL      QCMDA = *BLANKS
N02  C                   EVAL      QCMDA = 'MD DIR(' + X'7D'
N02  C                             + '/syspeddev/temp/' + X'7D'
N02  C                             + ')'
N02  C                   EVAL      QCMDL = %len(%trim(QCMDA))
N02  C                   CALL      'QCMDEXC'                            98
N02  C                   PARM                    QCMDA
N02  C                   PARM                    QCMDL
N02   * FINN SISTE SLASH + RENT FILNAVN
N02  C                   MOVE      *BLANKS       IFSFILE         256
N02  C                   Z-ADD     1             X                 3 0
N02  C     NextSlash     TAG
N02  C     '/'           SCAN      SXIFSOBJ:X    Y                 3 0    33
N02  C   33Y             ADD       1             X
N02  C   33              GOTO      NextSlash
N02  C                   EVAL      IFSFILE = '/syspeddev/temp/'
N02  C                             + %SUBST(SXIFSOBJ:X)
N02  C     '.xml'        SCAN      IFSFILE                                33
N02  C  N33'.XML'        SCAN      IFSFILE                                33
N02  C  N33'.txt'        SCAN      IFSFILE                                33
N02  C  N33'.TXT'        SCAN      IFSFILE                                33
N02  C  N33'.csv'        SCAN      IFSFILE                                33
N02  C  N33'.CSV'        SCAN      IFSFILE                                33
N02  C  N33              EVAL      IFSFILE = %trim(IFSFILE)+'.txt'
N02   * Vise fil
N02  C                   EVAL      QCMDA = *BLANKS
N02  C                   EVAL      QCMDA = 'CPY OBJ(' + X'7D'
N02  C                             + %trimr(SXIFSOBJ) + X'7D'
N02  C                             + ') TOOBJ('+ X'7D'
N02  C                             + %trimr(IFSFILE) + X'7D'
N02  C                             + ') REPLACE(*YES)'
N02  C                   EVAL      QCMDL = %len(%trim(QCMDA))
N02  C                   CALL      'QCMDEXC'                            98
N02  C                   PARM                    QCMDA
N02  C                   PARM                    QCMDL
N02   * VIS som weblink.. (ta hensyn til om user evt skal ha ekstern IP)
N02  c     arinek        ifeq      'E'
N02  c                   eval      arcpad = arcpae
N02  c                   end
N02  c                   EVAL      URL = 'http://'+%trim(arcpad)
N02  C                             + %trimr(IFSFILE)
N02  C                   CALL      'SYGE39CL'
N02  C                   PARM                    URL             512
N02   * Slett den temporære fila..
N02  C                   EVAL      QCMDA = *BLANKS
N02  C                   EVAL      QCMDA = 'DEL OBJLNK(' + X'7D'
N02  C                             + %trimr(IFSFILE) + X'7D'
N02  C                             + ')'
N02  C                   EVAL      QCMDL = %len(%trim(QCMDA))
N02  C                   CALL      'QCMDEXC'                            98
N02  C                   PARM                    QCMDA
N02  C                   PARM                    QCMDL
N02  C                   ENDIF
N02   *_________________
N02   * Oppdatere SUBFIL
N02   *"""""""""""""""""
N02  C                   MOVE      *BLANKS       ZOP
N02  C     MSR           COMP      'R'                                    27
N02  C                   UPDATE    SAD003A
N02   *
N02  C                   ENDSR
N02   *
N07   *////////////////////////////////////////////////////////////
N07   *  Vise IFS fil                                     SRBIFS2 /
N07   *////////////////////////////////////////////////////////////
N07  C     SRBIFS2       BEGSR
N07   * Endret fra her
N07   * Hent ifsnavn
N07  C     SIFS          IFNE      *BLANKS
N07  C                   MOVEL     SIFS          SXIFSOBJ
N07  C                   EVAL      *IN51=*OFF
N07  C                   ELSE
s10  C*    KEYC          CHAIN     EDIS                               20
n10  C     KEYC          CHAIN(n)  EDIS                               20
N07  C                   ENDIF
N07   * Endret til hit
N07  c  N51SXIFSOBJ      comp      *blanks                                51
N07  C                   IF        *IN51=*OFF
N07   * Vise fil
N07  C                   EVAL      QCMDA = *BLANKS
N07  C                   EVAL      QCMDA = 'DSPF STMF(' + X'7D'
N07  C                             + %trimr(SXIFSOBJ) + X'7D'
N07  C                             + ')'
N07  C                   EVAL      QCMDL = %len(%trim(QCMDA))
N07  C                   CALL      'QCMDEXC'                            98
N07  C                   PARM                    QCMDA
N07  C                   PARM                    QCMDL
N07  C                   ENDIF
N07   *_________________
N07   * Oppdatere SUBFIL
N07   *"""""""""""""""""
N07  C                   MOVE      *BLANKS       ZOP
N07  C     MSR           COMP      'R'                                    27
N07  C                   UPDATE    SAD003A
N07   *
N07  C                   ENDSR
N07
N08   *///////////////////////////////////////////////////////////
N08   * VIS FTP-LOGG                                      SRBFTP /
N08   *///////////////////////////////////////////////////////////
N08  C     SRBFTP        BEGSR
N08  C                   IF        MSR='S'
N08  C                   CALL      'EDI42R'
N08  C                   PARM                    MSN
N08  C                   ENDIF
N08
N08  C                   MOVE      *BLANKS       ZOP
N08  C     MSR           COMP      'R'                                    27
N08  C                   UPDATE    SAD003A
N08
N08  C                   ENDSR
N10   ***********************************************************************
N10  C     SRBA          BEGSR
N10   ***********************************************************************
n10  C     KEYC          CHAIN(n)  EDIS                               20
N10  C     *IN33         IFEQ      '0'
N10   * Finnes komponenten kvar ?
N10  C     SMBR          IFEQ      'REMOVED'
N10  C                   MOVE      'SPS0007'     ZMSGNO
N10  C                   MOVE      '1'           *IN30
N10  C                   GOTO      SRBA9
N10  C                   END
N10   *__________
N10   * Omkjøring.
N10   *""""""""""
N10  C     SSR           IFEQ      'S'
N10  C     SSN           CHAIN     EDIS                               30        Sendning
N10  C     *IN30         IFEQ      '0'
N10  C                   MOVE      'A'           SST
N10  C                   UPDATE    EDISR
N10   * SKRIV POST TIL DETALJ LOGG
N10  C                   CLEAR                   EDISSR
N10  C                   Z-ADD     SSN           SSSN
N10  C                   MOVEL     WDT           SSDT
N10  C                   MOVEL     WTM           SSTM
N10  C                   MOVEL     SST           SSST
N10  C                   EVAL      SSTEXT='Resend is ordered by:'
N10  C     SSTEXT        CAT       UserName:1    SSTEXT
N10  C                   WRITE     EDISSR
N10  C                   ENDIF
N10  C                   ELSE
N10  C     SFILE         IFEQ      'EDIRE1K'
N10  C                   CALL      'EDI35R1K'
N10  C                   PARM                    SMBR
N10  C                   ELSE
N10  C                   CALL      'EDI35R'
N10  C                   PARM                    SMBR
N10  C                   ENDIF
N10  C                   END
N10  C                   END
N10   *_________________
N10   * Oppdatere SUBFIL
N10   *"""""""""""""""""
N10  C                   MOVE      *BLANKS       ZOP
N10  C     MSR           COMP      'R'                                    27
N10  C                   UPDATE    SAD003A
N10  C     SRBA9         ENDSR
