     FDKIV      IF   E           K DISK
     FDKIVA     IF   E           K DISK
     FDKIH      IF   E           K DISK
     FDKTHA     IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FDKI007P   O    E             PRINTER OFLIND(*IN27) USROPN
     FFIRM      IF   E             DISK
     FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FARKIVP    O  A E           K DISK
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  ØN154                  1     15  4
     D  ØA154H                 1     11
     D  ØA154X                 7     11
     D  ØA154D                12     15
     D                 DS
     D  ØA10                   1     10
     D  Ø10                    1     10
     D                                     DIM(10)                              Num edit
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UKIHASYAV         4
     C                   PARM                    UKIHASYOP         7
     C                   PARM                    CALLTYP           1
     C                   MOVE      UKIHASYAV     UKIH_SYAV
     C                   MOVE      UKIHASYOP     UKIH_SYOP
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    DKIH_SYAV     UKIH_SYAV
     C     *LIKE         DEFINE    DKIH_SYOP     UKIH_SYOP
     C     *LIKE         DEFINE    KOPUNI        UOPUNI
     C     *LIKE         DEFINE    KOPAVD        UOPAVD
     C     *LIKE         DEFINE    KOPLNR        UOPLNR
     C     *LIKE         DEFINE    KFMSD         UFMSD
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UKIH_SYAV
     C                   KFLD                    UKIH_SYOP
     C     KEYB          KLIST
     C                   KFLD                    UKIH_SYAV
     C                   KFLD                    UKIH_SYOP
     C                   KFLD                    DKIV_SYLI
     C     KEYP          KLIST
     C                   KFLD                    UOPUNI
     C                   KFLD                    UOPAVD
     C                   KFLD                    UOPLNR
     C     KEYF          KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C                   EVAL      UOPUNI='P'
     C                   EVAL      UOPAVD=UKIH_SYAV
     C                   EVAL      UOPLNR=252
     C                   EVAL      UFMSD=1
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD           1024
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
      *______________________________________
      * Hente firmaopplysninger fra spedisjon
      *""""""""""""""""""""""""""""""""""""""
     C                   READ      FIRM
      *_____________________
      * Hente printerstyring
      *"""""""""""""""""""""
     C     KEYP          CHAIN     KODTP
     C     KEYF          CHAIN     KODTFM
      *_______________
      * Printerstyring
      *"""""""""""""""
     C     CALLTYP       IFEQ      'A'
     C                   CALL      'DKI007CA'
     C                   ELSE
     C                   EVAL      UCMD='OVRPRTF FILE(DKI007P) OUTQ('
     C     CALLTYP       IFEQ      'C'
     C                   EVAL      UCMD='OVRPRTF FILE(DKI007P) HOLD(*YES) OUTQ('
     C                   ENDIF
     C     UCMD          CAT       KOPNVN:0      UCMD
     C     UCMD          CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   ENDIF
     C                   OPEN      DKI007P
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Læs varuposter                                       SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     DKIH
     C                   WRITE     DKI007PA

     C     KEYA          SETLL     DKIV
     C     KEYA          READE     DKIV                                   51
     C     *IN51         DOWEQ     *OFF
     C                   EVAL      WRUBR32=DKIV_32
     C                   MOVEL     DKIV_331      WRUBR33A
     C                   MOVE      DKIV_331      WRUBR33B
     C                   EVAL      WRUBR33C=DKIV_332
     C                   EVAL      WRUBR34=DKIV_34
     C                   MOVEL     DKIV_37       WRUBR37A
     C                   MOVE      DKIV_37       WRUBR37B
     C                   EVAL      WRUBR36=DKIV_36
     C                   EVAL      WRUBR39=DKIV_39
     C                   EVAL      WRUBR35=DKIV_35
     C                   EVAL      WRUBR38=DKIV_38
     C                   EVAL      WRUBR31=DKIV_311A
     C*                  EVAL      WRUBR31B=DKIV_315A
     C                   EVAL      WRUBR31B=*BLANKS
     C                   EVAL      ØN154=DKIV_313A
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           WRUBR31B
     C     WRUBR31B      CAT       DKIV_314A:1   WRUBR31B
     C     WRUBR31B      CAT       DKIV_315A:1   WRUBR31B
      *
     C                   EVAL      WRUBR41=DKIV_412
     C                   IF        WRUBR41=999999999
     C                   EVAL      WRUBR41=*ZEROS
     C                   ENDIF
     C                   EVAL      WRUBR46=DKIV_46
     C                   MOVEL     DKIV_446TA    WRUBR44
     C     WRUBR44       CAT       DKIV_312A:1   WRUBR44
     C     WRUBR44       CAT       DKIV_312B:1   WRUBR44

     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKI007PA
     C                   ENDIF
     C                   WRITE     DKI007PB

     C                   EXSR      SRBB
     C                   EXSR      SRBA
     C
     C     KEYA          READE     DKIV                                   51
     C                   ENDDO

     C                   WRITE     DKI007PZ
     C     CALLTYP       IFEQ      'C'
     C                   EXSR      ARKSR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Læs avgifter                                        SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C     KEYB          SETLL     DKIVA
     C     KEYB          READE     DKIVA                                  52
     C     *IN52         DOWEQ     *OFF

     C                   EVAL      WRUBR47A=DKIVA_ABK
     C                   EVAL      WRUBR47B=DKIVA_ABG
     C                   EVAL      WRUBR47C=DKIVA_ABS
     C                   EVAL      WRUBR47D=DKIVA_ABB

     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKI007PA
     C                   ENDIF
     C                   WRITE     DKI007PC
     C
     C     KEYB          READE     DKIVA                                  52
     C                   ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv R44                                           SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR

      * Særskilda upplysningar-1
     C     DKIV_446KA    IFNE      *BLANKS
     C     DKIV_446TA    ORNE      *BLANKS
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKIV_446KA    WRUBR44
     C     WRUBR44       CAT       DKIV_446TA:1  WRUBR44
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKI007PA
     C                   ENDIF
     C                   WRITE     DKI007PD
     C                   ENDIF
      * Særskilda upplysningar-2
     C     DKIV_446TB    IFNE      *BLANKS
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKIV_446TB    WRUBR44
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKI007PA
     C                   ENDIF
     C                   WRITE     DKI007PD
     C                   ENDIF
      * Særskilda upplysningar-3
     C     DKIV_446TC    IFNE      *BLANKS
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKIV_446TC    WRUBR44
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKI007PA
     C                   ENDIF
     C                   WRITE     DKI007PD
     C                   ENDIF
      * Særskilda upplysningar-4
     C     DKIV_446TD    IFNE      *BLANKS
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKIV_446TD    WRUBR44
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKI007PA
     C                   ENDIF
     C                   WRITE     DKI007PD
     C                   ENDIF
      * Container 1 - 4
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKIV_312A     WRUBR44
     C     WRUBR44       CAT       DKIV_312B:1   WRUBR44
     C     WRUBR44       CAT       DKIV_312C:1   WRUBR44
     C     WRUBR44       CAT       DKIV_312D:1   WRUBR44
     C     WRUBR44       IFNE      *BLANKS
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKI007PA
     C                   ENDIF
     C                   WRITE     DKI007PD
     C                   ENDIF
      * Container 5
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKIV_312E     WRUBR44
     C     WRUBR44       IFNE      *BLANKS
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKI007PA
     C                   ENDIF
     C                   WRITE     DKI007PD
     C                   ENDIF
     C
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *    Arkiver                                          ARKSR /
      /////////////////////////////////////////////////////////////
     C     ARKSR         BEGSR
      * SKAL DOKUMENT ARKIVERES ?
     C                   MOVE      'D1'          ARTYPE
     C     FIFIRM        CHAIN     FIRMARC                            34
     C  N34ARTYPE        CHAIN     ARKTXT                             34
     C     *IN(34)       IFEQ      '0'
     C     ARKJN         ANDEQ     'J'

     C                   z-add     DKIH_AVKN     ARKUND
     C                   MOVE      FIFIRM        ARFIRM
     C                   MOVE      UKIH_SYAV     ARAVD
     C                   MOVE      UKIH_SYOP     AROPD
     C                   MOVE      WDT           ARDATE
     C                   MOVE      WTM           ARTIME
     C                   MOVE      ZUSER         ARUSER
     C                   MOVE      UKIH_SYOP     ARUNDE
     C                   MOVEL     'D1:'         ARUNDE
     C                   WRITE     ARKIVR
      *_________________________
      * Aktivera arkivfunktionen  FJERNES SENERE
      *"""""""""""""""""""""""""
     C     DKIH_SYSG     CHAIN     DKTHA
     C                   CALL      'ARC002CLB'
     C                   PARM      'D1'          WATYPE            2
     C                   PARM                    ARUSER
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA10
     C     ØA154D        IFEQ      '0000'                                       <-------------I
     C                   MOVE      ØA154H        ØA10                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA154X        ØA10                                         I
     C                   MOVE      ',    '       ØA10                                         I
     C                   MOVE      ØA154D        ØA10                                         I
     C     Ø10(10)       IFEQ      '0'
     C                   MOVE      ' '           Ø10(10)
     C     Ø10(9)        IFEQ      '0'
     C                   MOVE      ' '           Ø10(9)
     C                   END
     C                   END
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        9             ØX                             <-------------I
     C     Ø10(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø10(ØX)       ØAN
      *
     C                   ENDSR
