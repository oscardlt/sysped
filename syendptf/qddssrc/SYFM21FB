     A*%%TS  SD  20190304  123631  SVEIN       REL-V7R1M0  5770-WDS
N01  A* FADOCN DELT I 2 - KORTET LEDETEKST
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20160914  132738  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  8  2VALUES(' ' '5' 'P' 'K' 'F' '9')
     A            FALI           5S 0H
     A            FASK           1A  O  8  5
     A  50                                  DSPATR(ND)
     A            FAVK           3A  O  8  7
     A  54                                  COLOR(RED)
     A  50                                  DSPATR(ND)
     A            WSVT          20A  O  8 11
     A  54                                  COLOR(RED)
     A  48                                  DSPATR(RI)
     A            WSVAL          3A  O  8 32
     A  66                                  DSPATR(RI)
     A  48                                  DSPATR(UL)
     A            WSBELV        13Y 2O  8 36EDTCDE(M)
     A  48                                  DSPATR(UL)
     A            WSBELN        13Y 2O  8 52EDTCDE(M)
     A  49                                  COLOR(WHT)
     A  48                                  DSPATR(UL)
     A            FAKDM          1A  O  8 68
     A  50                                  DSPATR(ND)
     A            FAKDA          1A  O  8 70
     A  50                                  DSPATR(ND)
     A            FAOPKO         1A  O  8 72
     A  50                                  DSPATR(ND)
     A            FAFAKT         7Y 0O  8 74EDTCDE(4)
     A  48                                  DSPATR(UL)
     A  50                                  DSPATR(ND)
     A N50 56                               DSPATR(RI)
     A            WSDATO         6Y 0O  8 82EDTCDE(4)
     A  48                                  DSPATR(UL)
     A            WSMND          2Y 0O  8 89EDTCDE(4)
     A  48                                  DSPATR(UL)
     A            WSÅR           2Y 0O  8 92EDTCDE(4)
     A  48                                  DSPATR(UL)
     A            WSJOUR         7Y 0O  8 95EDTCDE(4)
     A  48                                  DSPATR(UL)
     A            WSACCN         5Y 0O  8103EDTCDE(4)
     A  48                                  DSPATR(UL)
     A            FAEXRA         7S 4H
     A            FAVAL          3A  H
     A            FAAVD          4S 0H
     A            FAOPD          7S 0H
     A            FABELV        13S 2H
     A            FABELN        13S 2H
     A            FAKDUL         1A  H
     A            FAKDUK         1A  H
     A*           FABELU        13S 2H
     A            FALEVN         8S 0H
     A            FAKUNR         8S 0H
     A            FAJOUR         7S 0H
     A            FAÅR           2S 0H
     A            FAMND          2S 0H
     A            FAFRBN         3S 0H
     A*           WSDATO         6S 0H
     A            WSDAFF         6S 0H
     A            FAAVDR         4S 0H
     A            FAACCN         5S 0H
     A            FADOCN        11A  H
     A            FABÆR          4S 0H
     A            FACD11         1A  H
     A            FACU33         3A  H
N14  A            FAAGRN         9S 0H
     A            WSKUNR         8Y 0O  8109EDTCDE(4)
     A  48                                  DSPATR(UL)
     A            FABELU        13Y 2O  8118EDTCDE(M)
     A  48                                  DSPATR(UL)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20160914  132508  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYFM21B'
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(027 DIV2052 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            HEAVD          4Y 0O  1 48TEXT('Avdeling')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 53'/'
     A            HEOPD          7Y 0O  1 55EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 63MSGCON(004 SHU1589 *LIBL/SYMSGF)
     A            HEPRO          8Y 0O  1 69COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                  1 83MSGCON(010 SHU1590 *LIBL/SYMSGF)
     A            HEKDPL         1A  O  1 95COLOR(WHT)
     A                                  1 97MSGCON(006 SHU1594 *LIBL/SYMSGF)
     A            HEFBV          9Y 0O  1104COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                  1115DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1124TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(013 DIV0134 *LIBL/SYMSGF)
     A            HEKNSF         8Y 0O  2 17TEXT('Kundenr Selg.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENASF        30A  O  2 26TEXT('Navn Selger')
     A                                      COLOR(WHT)
     A                                  2 57MSGCON(008 DIV0131 *LIBL/SYMSGF)
     A            HESG           3A  O  2 66COLOR(WHT)
     A            DSPRCN         4S 0O  2 71SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 83MSGCON(010 SHU1591 *LIBL/SYMSGF)
     A            HERFA         35A  O  2 95COLOR(WHT)
     A                                  3  2MSGCON(013 SHU1385 *LIBL/SYMSGF)
     A            HEKNSA         8Y 0O  3 17TEXT('Kundenr Selger')
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
     A            HENASA        30A  O  3 26TEXT('Navn Selg.fakm')
     A                                      COLOR(RED)
     A                                  3 57MSGCON(008 SHU1588 *LIBL/SYMSGF)
     A            WSDT           6Y 0O  3 66EDTCDE(Y)
     A                                      COLOR(WHT)
     A            HEDTMP         2S 0O  3 76COLOR(WHT)
     A  55                                  DSPATR(RI)
     A            HEDTÅP         2S 0O  3 79COLOR(WHT)
     A  55                                  DSPATR(RI)
     A                                  3 83MSGCON(010 SHU1592 *LIBL/SYMSGF)
     A            HERFK         35A  O  3 95COLOR(WHT)
     A                                  4  2MSGCON(013 DIV0137 *LIBL/SYMSGF)
     A            HEKNKF         8Y 0O  4 17TEXT('Kundenr Kjøp.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAKF        30A  O  4 26TEXT('Navn Kjøper')
     A                                      COLOR(WHT)
     A                                  4 57MSGCON(007 DIV0133 *LIBL/SYMSGF)
     A            HEGN          15A  O  4 66TEXT('Godsnummer')
     A                                      COLOR(WHT)
     A                                  4 83MSGCON(010 SHU1593 *LIBL/SYMSGF)
     A            HEVKT          9Y 0O  4 95EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  4105'/'
     A            HEM3           7Y 3O  4107EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  4116'/'
     A            HELM           4Y 2O  4118EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(013 SHU1386 *LIBL/SYMSGF)
     A            HEKNKA         8Y 0O  5 17TEXT('Kundenr Kjøper')
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
     A            HENAKA        30A  O  5 26TEXT('Navn Kjøp.fakm')
     A                                      COLOR(RED)
     A                                  5 57MSGCON(008 SHU1587 *LIBL/SYMSGF)
     A            HEOT           2A  O  5 66COLOR(WHT)
     A            HEFR           3A  O  5 70TEXT('Frankatur')
     A                                      COLOR(WHT)
     A                                  5 83MSGCON(010 SHU1595 *LIBL/SYMSGF)
     A            WTGX           5A  O  5 95COLOR(WHT)
     A            HESTN6         1A  O  5104COLOR(WHT)
     A                                  6  2MSGCON(090 DIV2006 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S16  A*                                 7  2MSGCON(130 DIV0139 *LIBL/SYMSGF)
S16  A*                                     COLOR(WHT)
N16  A                                  7  2MSGCON(60 DIV0139 *LIBL/SYMSGF)
N16  A                                      COLOR(WHT)
N16  A            FICURR         3      7 63
N16  A                                      COLOR(WHT)
N16  A                                  7 68MSGCON(60 DIV5139 *LIBL/SYMSGF)
N16  A                                      COLOR(WHT)
     A                                  5101'P:'
     A          R BUNN
     A*%%TS  SD  20190304  123631  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF01(01 'Bytt avdlinger')
     A                                      CF02(02 'Gå til valgbilde')
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'Vise fraktbrev')
     A                                      CF07(07 'Vise tollpassfraktbrev')
     A                                      CF08(08 'TRACK AND TRACE')
     A                                      CF09(09 'Vise flyfraktbrev (import)-
     A                                      ')
     A                                      CF10(10 'Vise flyfraktbrev (eksport-
     A                                      )')
     A N35                                  CF11(11 'Vise REKV./BUDSJ.')
     A                                      CF13(13 'Vise TOLLDEKL.)')
     A                                      CF12(12 'Gå til rullbilde')
     A                                      CA15(15 'VISE LAGERFAKT.')
     A                                      CF17(17 'ARKIV')
     A                                      CF24(24 'MORE KEYS)')
     A                                      ROLLUP(25 'Bla frem')
     A                                      ROLLDOWN(26 'Bla tilbake')
     A                                      SLNO(21)
     A                                      CLRL(06)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGLN5         7A  P
     A            MSGLN6         7A  P
     A            MSGLN7         7A  P
     A            MSGLN8         7A  P
     A            MSGLN9         7A  P
     A            MSGLN10        7A  P
     A            MSGLN11        7A  P
     A            MSGLN12        7A  P
     A                                  1  2MSGCON(018 JOV0327 *LIBL/SYMSGF)
     A            WSESTIO       13  2O  1 22EDTCDE(M)
     A                                      COLOR(WHT)
     A                                  1 42MSGCON(018 SHU2015 *LIBL/SYMSGF)
     A            WSESTIT       13  2O  1 62EDTCDE(M)
     A                                      COLOR(WHT)
     A  51 53                           1 88MSGCON(012 JOV0326 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  51        WSGRAN        13Y 2O  1105EDTCDE(M)
     A  51N52                               COLOR(WHT)
     A  51 52                               COLOR(RED)
     A            MSGL1         18A  O  3  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O  3 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O  3 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O  3 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL5         18A  O  3 82MSGID(&MSGLN5 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL6         18A  O  3102MSGID(&MSGLN6 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL7         18A  O  4  2MSGID(&MSGLN7 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL8         18A  O  4 22MSGID(&MSGLN8 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL9         18A  O  4 42MSGID(&MSGLN9 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL10        18A  O  4 62MSGID(&MSGLN10 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL11        18A  O  4 82MSGID(&MSGLN11 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL12        18A  O  4102MSGID(&MSGLN12 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CF01(01 'Bytt avdlinger')
     A                                      CF02(02 'Gå til valgbilde')
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'Vise fraktbrev')
     A                                      CF07(07 'Vise tollpassfraktbrev')
NXX  A                                      CF08(08 'TRACK & TRACE')
     A                                      CF09(09 'Vise flyfraktbrev (import)-
     A                                      ')
     A                                      CF10(10 'Vise flyfraktbrev (eksport-
     A                                      )')
     A N35                                  CF11(11 'Vise TOLLDEKLARASJON)')
     A                                      CF13(13 'Vise TOLLDEKL.')
N08  A                                      CF17(17 'ARKIV')
     A                                      CF24(24 'MORE KEYS')
     A                                      CF12(12 'Gå til rullbilde')
     A                                  1  2'SYSPED/SYFM21B'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(027 DIV0130 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            HEAVD          4Y 0O  1 48TEXT('Avdeling')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HEOPD          7Y 0O  1 53EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(013 DIV0134 *LIBL/SYMSGF)
     A            HEKNSF         8Y 0O  2 17TEXT('Kundenr Selg.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENASF        30A  O  2 26TEXT('Navn Selger')
     A                                      COLOR(WHT)
     A                                  2 57MSGCON(008 DIV0131 *LIBL/SYMSGF)
     A            HESG           3A  O  2 66COLOR(WHT)
     A                                  3  2MSGCON(013 SHU1385 *LIBL/SYMSGF)
     A            HEKNSA         8Y 0O  3 17TEXT('Kundenr Selger')
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
     A            HENASA        30A  O  3 26TEXT('Navn Selg.fakm')
     A                                      COLOR(RED)
     A                                  3 57MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT           6Y 0O  3 66EDTCDE(Y)
     A                                      COLOR(WHT)
     A            HEDTMP         2S 0O  3 76COLOR(WHT)
     A  55                                  DSPATR(RI)
     A            HEDTÅP         2S 0O  3 79COLOR(WHT)
     A  55                                  DSPATR(RI)
     A                                  4  2MSGCON(013 DIV0137 *LIBL/SYMSGF)
     A            HEKNKF         8Y 0O  4 17TEXT('Kundenr Kjøp.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAKF        30A  O  4 26TEXT('Navn Kjøper')
     A                                      COLOR(WHT)
     A                                  4 57MSGCON(007 DIV0133 *LIBL/SYMSGF)
     A            HEGN          15A  O  4 66TEXT('Godsnummer')
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(013 SHU1386 *LIBL/SYMSGF)
     A            HEKNKA         8Y 0O  5 17TEXT('Kundenr Kjøper')
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
     A            HENAKA        30A  O  5 26TEXT('Navn Kjøp.fakm')
     A                                      COLOR(RED)
     A                                  5 57MSGCON(006 DIV0138 *LIBL/SYMSGF)
     A            HEFR           3A  O  5 66TEXT('Frankatur')
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(090 DIV2006 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S16  A*                                 7  2MSGCON(130 DIV0139 *LIBL/SYMSGF)
S16  A*                                     COLOR(WHT)
N16  A                                  7  2MSGCON(60 DIV0139 *LIBL/SYMSGF)
N16  A                                      COLOR(WHT)
N16  A            FICURR         3      7 63
N16  A                                      COLOR(WHT)
N16  A                                  7 68MSGCON(60 DIV5139 *LIBL/SYMSGF)
N16  A                                      COLOR(WHT)
     A            MSGL1         18A  O 22  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 22 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 22 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 22 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL5         18A  O 23  2MSGID(&MSGLN5 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL6         18A  O 23 22MSGID(&MSGLN6 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL7         18A  O 23 42MSGID(&MSGLN7 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL8         18A  O 23 62MSGID(&MSGLN8 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGLN5         7A  P
     A            MSGLN6         7A  P
     A            MSGLN7         7A  P
     A            MSGLN8         7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  1  2MSGCON(076 DIV1009 *LIBL/SYMSGF)
     A            FASK           1A  O  2  2TEXT('Selger/Kjøper S/K')
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            FAVK           3A  O  2  5TEXT('Varekode')
     A                                      COLOR(WHT)
     A            FABELV        13Y 2O  2 13TEXT('Beløp valuta')
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            FAVAL          3A  O  2 30TEXT('Valutakode')
     A                                      COLOR(WHT)
     A            FAKDM          1A  O  2 37TEXT('Momskode J/N')
     A                                      COLOR(WHT)
     A            WSVT          20A  O  2 39TEXT('Varetekst')
     A                                      COLOR(WHT)
     A            FAKDUK         1A  O  2 60TEXT('Utgiftskonto J/N')
     A                                      COLOR(WHT)
     A            FABELU        13Y 2O  2 62TEXT('Beløp')
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(006 DIV1206 *LIBL/SYMSGF)
     A            FALEVN         8Y 0O  3 10COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 22MSGCON(006 DIV2007 *LIBL/SYMSGF)
     A            FAKUNR         8Y 0O  3 30COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 42MSGCON(006 DIV2012 *LIBL/SYMSGF)
     A            FAOPKO         1A  O  3 50COLOR(WHT)
     A                                  3 62MSGCON(007 DIV2011 *LIBL/SYMSGF)
     A            FAÅR           2S 0O  3 70COLOR(WHT)
     A            FAMND          2S 0O  3 73COLOR(WHT)
     A                                  4  2MSGCON(006 DIV2008 *LIBL/SYMSGF)
     A            FAFAKT         7Y 0O  4 12COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 22MSGCON(006 DIV2009 *LIBL/SYMSGF)
     A            WSDATO         6Y 0O  4 30COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                  4 42MSGCON(006 DIV2010 *LIBL/SYMSGF)
     A            WSDAFF         6Y 0O  4 50COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                  4 62MSGCON(007 DIV4240 *LIBL/SYMSGF)
     A            XVKU           7Y 4O  4 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(007 DIV2013 *LIBL/SYMSGF)
     A            FAJOUR         7Y 0O  5 11COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 22MSGCON(006 DIV2014 *LIBL/SYMSGF)
     A            FAFRBN         3Y 0O  5 30COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 42MSGCON(007 DIV2015 *LIBL/SYMSGF)
     A            FAAVDR         4Y 0O  5 50EDTCDE(4)
     A                                      COLOR(WHT)
     A            FAACCN         5S 0O  5 56COLOR(WHT)
     A                                  5 62MSGCON(006 DIV2016 *LIBL/SYMSGF)
     A            FABÆR          4Y 0O  5 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 52MSGCON(004 SHU0308 *LIBL/SYMSGF)
     A            FACD11         1A  O  3 57COLOR(WHT)
     A            FACU33         3A  O  2 78TEXT('Valutakode')
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  6 30COLOR(WHT)
S01  A*           FADOCN        11A  O  6 69COLOR(WHT)
S01  A*                                 6 62MSGCON(006 JOV1467 *LIBL/SYMSGF)
N01  A            FADOCA         4A  O  6 68COLOR(WHT)
N01  A            FADOCB         7A  O  6 73COLOR(WHT)
N01  A                                  6 62MSGCON(005 JOV1467 *LIBL/SYMSGF)
