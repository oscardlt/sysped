      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR05LL MODULE(*LIBL/TJETUR05LL *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 MaxLes/MaxVis innført - hardkodet 10000 / 1000
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FDISP      IF   E           K DISK    USROPN
     FDISPL04   IF   E           K DISK    USROPN
     F                                     RENAME(DISPR:DISPR04)
     FDISPL19   IF   E           K DISK    USROPN
     F                                     RENAME(DISPR:DISPR19)
     FKODTA     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FBILLOF    IF   E           K DISK    USROPN
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D WBETD           S              8  0
     D WBETA           S              8  0
     D WBATD           S              8  0
     D WBATA           S              8  0
     D WSSDF           S              8
     D WSSDT           S              8
     D TstSte          S              8
     D MaxSte          S              8
     D WSSAVD          S              4
     D WBSAVD          S              4  0
     D XAVDA           S              4A
     D XAVDN           S              4  0
     D XOPDA           S              7A
     D XOPDN           S              7  0
     D UdispHea        S             70
     D i               S             10i 0
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
N01  D AntVist         S              9  0
N01  D MaxLes          S              9  0
N01  D MaxVis          S              9  0
     D USER            s             10
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

     D                 DS
     D  PARMOS1                1   1024
     D  OSCHG                  1      1
     D  OSOT16                 2     49
     D  OT                     2     49    DIM(16)
     D  OSST08               150    197
     D  STD                  150    197    DIM(8)
     D  AV                   198    247    DIM(10)
     D  OSLKF16              248    295
     D  LKF                  248    295    DIM(16)
     D  OSLKT16              296    343
     D  LKT                  296    343    DIM(16)
     D  STDT                 344    391    DIM(8)
     D                 DS
     D  HEPOS1                 1      7
     D  POS                    1      7    DIM(7)
     D                 DS
     D  TUTARF                 1     15
     D  TUBLORR                1      1
     D  TUTR                   2      5

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE

     C                   EXSR      INIT

     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬wssdf¬ : ¬'+%trim(WSSDF)+'¬, '
        +'¬wssdt¬ : ¬'+%trim(WSSDT)+'¬, '
        +'¬wssavd¬ : ¬'+%trim(WSSAVD)+'¬, '
        +'¬stdavd¬ : ¬'+%trim(XSTDAVD)+'¬, '
        +'¬error¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     c                   EVAL      JSONstring ='"orderlistlandled" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

      * HOVEDRUTINE
     c     Error         IFEQ      *blanks
      * Vis ordre på lager.
     C                   IF        WSSAVD='ALL' OR
     C                             WSSAVD='IN ' OR WSSAVD='OUT' OR
     C                             WSSAVD='IMP' OR WSSAVD='EXP' OR
     C                             WSSAVD='DOM'
     C     DISPL19K      SETLL     DISPL19
     C     DISPL19K      READE     DISPL19                                35
     C                   ELSE
     C     DISPL04K      SETLL     DISPL04
     C     DISPL04K      READE     DISPL04                                35
     C                   ENDIF
     C                   DOW       *IN35 = *OFF
N01  c                   ADD       1             AntLest
     C                   EXSR      SRFILTER2
     C                   IF        XVIS = 'J'
N01  c                   ADD       1             AntVist
     C                   EXSR      Setrow2
     C                   ENDIF
N01  c                   IF        AntLest >= MaxLes or
N01  c                             AntVist >= MaxVis
N01  c                   leave
N01  c                   endif
     C                   IF        WSSAVD='ALL' OR
     C                             WSSAVD='IN ' OR WSSAVD='OUT' OR
     C                             WSSAVD='IMP' OR WSSAVD='EXP' OR
     C                             WSSAVD='DOM'
     C     DISPL19K      READE     DISPL19                                35
     C                   ELSE
     C     DISPL04K      READE     DISPL04                                35
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF

     c                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
     c
N01  c                   IF        AntLest >= MaxLes or
N01  c                             AntVist >= MaxVis
N01  c                   IF        AntVist >= MaxVis
N01  c                   EVAL      JSONstring =',"maxWarning" : "Max '
N01  c                             +%trim(%editc(MaxVis:'Z'))+ ' is shown. '
N01  c                             +'Please limit your search."'
N01  c                   else
N01  c                   EVAL      JSONstring =',"maxWarning" : "Max '
N01  c                             +%trim(%editc(MaxLes:'Z'))+ ' is searched.'
N01  c                             +'Please limit your search."'
N01  c                   endif
N01  C                   CALLP     updHTMLvar2('JSONstring'
N01  C                                         :%addr(JSONstring)
N01  C                                         :%len(JSONstring))
N01  C                   CALLP     wrtsection('JSONlin')
N01  c                   endif

     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Quit
     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     PARSE         BEGSR
     C                   EVAL      user     = zhbgetvar('user')
     C                   EVAL      WSSDF    = zhbgetvar('WSSDF')
     C     LC:UC         XLATE     WSSDF         WSSDF
     C                   EVAL      WSSDT    = zhbgetvar('WSSDT')
     C     LC:UC         XLATE     WSSDT         WSSDT
     C                   EVAL      WSSAVD   = zhbgetvar('WSSAVD')
     C     LC:UC         XLATE     WSSAVD        WSSAVD                         I fall all,in,out..
     C                   EVAL      WBSAVD   = c2n2(WSSAVD)                      I fall numerisk

     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     BRPARF
     C  N50              CLOSE     DISP
     C  N50              CLOSE     DISPL04
     C  N50              CLOSE     DISPL19
     C  N50              CLOSE     KODTA
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     BILLOF
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *=====================================================================******
      *  FILTRERE POSTER
      *=====================================================================******
     C     SRFILTER2     BEGSR
     C                   MOVE      'N'           XVIS              1
     C     HEADFKLT      CHAIN(N)  HEADF                              35
     C     *in35         CABEQ     '1'           SlFilter2
      *  FRA - Postnr - Landkode/postnr eller SONE (land)
     C                   IF        WSSDF<>*blanks
     C                   IF        %subst(WSSDF:1:1)>='0'
     C                   EVAL      TstSte=HEADS3
     C                   ELSE
     C                   EVAL      TstSte=HELKA+'-'+HEADS3
     C                   ENDIF
     C                   EVAL      MaxSte=%trim(WSSDF)+'99999999'
     C                   IF        TstSte<WSSDF or TstSte>MaxSte
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  TIL - Postnr - Landkode/postnr eller SONE (land)
     C                   IF        WSSDT<>*blanks
     C                   IF        %subst(WSSDT:1:1)>='0'
     C                   EVAL      TstSte=HEADK3
     C                   ELSE
     C                   EVAL      TstSte=HETRI+'-'+HEADK3
     C                   ENDIF
     C                   EVAL      MaxSte=%trim(WSSDT)+'99999999'
     C                   IF        TstSte<WSSDT or TstSte>MaxSte
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  IN - kun inngående avdelinger innland
     C                   IF        WSSAVD = 'IN '
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE <> '2'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  OUT- kun utgående  avdelinger innland
     C                   IF        WSSAVD = 'OUT'
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE <> '1'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  DOM- kun domestic (D/1/2)
     C                   IF        WSSAVD = 'DOM'
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE<>'D' and KOAIE<>'1' and KOAIE<>'2'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  IMP- kun import
     C                   IF        WSSAVD = 'IMP'
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE <> 'I'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  EXP- kun eksport
     C                   IF        WSSAVD = 'EXP'
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE <> 'E'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      * KOMMET GJENNOM ALLE FILTER = J I XVIS
     C                   MOVE      'J'           XVIS              1

     C     SlFilter2     TAG
     C                   ENDSR
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow2       BEGSR

      /free
         if AntVist=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
         endif;
       JSONstring=%trim(JSONstring)+'{'
        +'¬heur¬ : ¬'+%trim(HEUR)+'¬, '
        +'¬heavd¬ : ¬'+%trim(%editc(HEAVD:'Z'))+'¬, '
        +'¬heopd¬ : ¬'+%trim(%editc(HEOPD:'Z'))+'¬, '
        +'¬hesg¬ : ¬'+%trim(HESG)+'¬, '
        +'¬hedtop¬ : ¬'+%trim(%editc(HEDTOP:'Z'))+'¬, '
        +'¬hedtr¬ : ¬'+%trim(%editc(HEDTR:'Z'))+'¬, '
        +'¬hesdff¬ : ¬'+%trim(HESDFF)+'¬, '
        +'¬hesdf¬ : ¬'+%trim(HESDF)+'¬, '
        +'¬hesdt¬ : ¬'+%trim(HESDT)+'¬, '
        +'¬hesdvt¬ : ¬'+%trim(HESDVT)+'¬, '
        +'¬hegn¬ : ¬'+%trim(HEGN)+'¬, '
        +'¬hegnn¬ : ¬'+%trim(HEGNN)+'¬, '
        +'¬hepro¬ : ¬'+%trim(%editc(HEPRO:'Z'))+'¬, '
        +'¬heplan¬ : ¬'+%trim(HEPLAN)+'¬, '
        +'¬trrund¬ : ¬'+%trim(%editc(TRRUND:'Z'))+'¬, '
        +'¬hepos1¬ : ¬'+%trim(HEPOS1)+'¬, '
        +'¬hepos2¬ : ¬'+%trim(%editc(HEPOS2:'Z'))+'¬, '
        +'¬trknfa¬ : ¬'+%trim(%editc(TRKNFA:'Z'))+'¬, '
        +'¬henao¬ : ¬'+%trim(HENAO)+'¬, '
        +'¬hekna¬ : ¬'+%trim(%editc(HEKNA:'Z'))+'¬, '
        +'¬henaa¬ : ¬'+%trim(HENAA)+'¬, '
        +'¬herfa¬ : ¬'+%trim(HERFA)+'¬, '
        +'¬hesaml¬ : ¬'+%trim(HESAML)+'¬, '
        +'¬hehawb¬ : ¬'+%trim(HEHAWB)+'¬, '
        +'¬hesdla¬ : ¬'+%trim(HESDLA)+'¬, '
        +'¬hesdl¬ : ¬'+%trim(HESDL)+'¬, '
        +'¬hevalt¬ : ¬'+%trim(HEVALT)+'¬, '
        +'¬hevalp¬ : ¬'+%trim(%editc(HEVALP:'4'))+'¬, '
        +'¬heknt¬ : ¬'+%trim(%editc(HEKNT:'Z'))+'¬, '
        +'¬helka¬ : ¬'+%trim(HELKA)+'¬, '
        +'¬hekns¬ : ¬'+%trim(%editc(HEKNS:'Z'))+'¬, '
        +'¬henas¬ : ¬'+%trim(HENAS)+'¬, '
        +'¬heads1¬ : ¬'+%trim(HEADS1)+'¬, '
        +'¬heads2¬ : ¬'+%trim(HEADS2)+'¬, '
        +'¬hepns¬ : ¬'+%trim(HEPNS)+'¬, '
        +'¬heads3¬ : ¬'+%trim(HEADS3)+'¬, '
-----   +'¬helks¬ : ¬'+%trim(HELKS)+'¬, '
        +'¬heknh¬ : ¬'+%trim(%editc(HEKNH:'Z'))+'¬, '
-----   +'¬henah¬ : ¬'+%trim(HENAH)+'¬, '
        +'¬headh1¬ : ¬'+%trim(HEADH1)+'¬, '
        +'¬headh2¬ : ¬'+%trim(HEADH2)+'¬, '
        +'¬hepnh¬ : ¬'+%trim(HEPNH)+'¬, '
        +'¬headh3¬ : ¬'+%trim(HEADH3)+'¬, '
        +'¬helkh¬ : ¬'+%trim(HELKH)+'¬, '
        +'¬hehbre¬ : ¬'+%trim(%editc(HEHBRE:'4'))+'¬, '
        +'¬hehlen¬ : ¬'+%trim(%editc(HEHLEN:'4'))+'¬, '
        +'¬hehblk¬ : ¬'+%trim(HEHBLK)+'¬, '
        +'¬hekdfs¬ : ¬'+%trim(HEKDFS)+'¬, '
        +'¬hevals¬ : ¬'+%trim(HEVALS)+'¬, '
        +'¬heknsf¬ : ¬'+%trim(%editc(HEKNSF:'Z'))+'¬, '
        +'¬heans¬ : ¬'+%trim(%editc(HEANS:'Z'))+'¬, '
        +'¬henasf¬ : ¬'+%trim(HENASF)+'¬, '
        +'¬heknk¬ : ¬'+%trim(%editc(HEKNK:'Z'))+'¬, '
        +'¬henak¬ : ¬'+%trim(HENAK)+'¬, '
        +'¬headk1¬ : ¬'+%trim(HEADK1)+'¬, '
        +'¬headk2¬ : ¬'+%trim(HEADK2)+'¬, '
        +'¬hepnk¬ : ¬'+%trim(HEPNK)+'¬, '
        +'¬headk3¬ : ¬'+%trim(HEADK3)+'¬, '
        +'¬helkk¬ : ¬'+%trim(HELKK)+'¬, '
        +'¬heknl¬ : ¬'+%trim(%editc(HEKNL:'Z'))+'¬, '
        +'¬henal¬ : ¬'+%trim(HENAL)+'¬, '
        +'¬headl1¬ : ¬'+%trim(HEADL1)+'¬, '
        +'¬headl2¬ : ¬'+%trim(HEADL2)+'¬, '
        +'¬hepnl¬ : ¬'+%trim(HEPNL)+'¬, '
        +'¬headl3¬ : ¬'+%trim(HEADL3)+'¬, '
        +'¬helkl¬ : ¬'+%trim(HELKL)+'¬, '
        +'¬helbre¬ : ¬'+%trim(%editc(HELBRE:'4'))+'¬, '
        +'¬hellen¬ : ¬'+%trim(%editc(HELLEN:'4'))+'¬, '
        +'¬helblk¬ : ¬'+%trim(HELBLK)+'¬, '
        +'¬hekdfk¬ : ¬'+%trim(HEKDFK)+'¬, '
        +'¬hevalk¬ : ¬'+%trim(HEVALK)+'¬, '
        +'¬heknkf¬ : ¬'+%trim(%editc(HEKNKF:'Z'))+'¬, '
        +'¬heank¬ : ¬'+%trim(%editc(HEANK:'Z'))+'¬, '
        +'¬henakf¬ : ¬'+%trim(HENAKF)+'¬, '
        +'¬heot¬ : ¬'+%trim(HEOT)+'¬, '
        +'¬hekdpl¬ : ¬'+%trim(HEKDPL)+'¬, '
        +'¬hefr¬ : ¬'+%trim(HEFR)+'¬, '
        +'¬hent¬ : ¬'+%trim(%editc(HENT:'Z'))+'¬, '
        +'¬hevs1¬ : ¬'+%trim(HEVS1)+'¬, '
        +'¬hevs2¬ : ¬'+%trim(HEVS2)+'¬, '
        +'¬hegm1¬ : ¬'+%trim(HEGM1)+'¬, '
        +'¬hegm2¬ : ¬'+%trim(HEGM2)+'¬, '
        +'¬hevkt¬ : ¬'+%trim(%editc(HEVKT:'4'))+'¬, '
        +'¬helm¬ : ¬'+%trim(%editc(HELM:'4'))+'¬, '
        +'¬hem3¬ : ¬'+%trim(%editc(HEM3:'4'))+'¬, '
        +'¬hefbv¬ : ¬'+%trim(%editc(HEFBV:'4'))+'¬, '
        +'¬hekf¬ : ¬'+%trim(HEKF)+'¬, '
        +'¬hekft1¬ : ¬'+%trim(HEKFT1)+'¬, '
        +'¬heft1¬ : ¬'+%trim(HEFT1)+'¬, '
        +'¬hedtcp¬ : ¬'+%trim(%editc(HEDTCP:'Z'))+'¬, '
        +'¬hedtap¬ : ¬'+%trim(%editc(HEDTÅP:'Z'))+'¬, '
        +'¬hedtmp¬ : ¬'+%trim(%editc(HEDTMP:'Z'))+'¬, '
        +'¬hekdkk¬ : ¬'+%trim(HEKDKK)+'¬, '
        +'¬hefngk¬ : ¬'+%trim(%editc(HEFNGK:'Z'))+'¬, '
        +'¬hefngs¬ : ¬'+%trim(%editc(HEFNGS:'Z'))+'¬, '
        +'¬hekdtm¬ : ¬'+%trim(HEKDTM)+'¬, '
        +'¬hetrm¬ : ¬'+%trim(%editc(HETRM:'Z'))+'¬, '
        +'¬hetri¬ : ¬'+%trim(HETRI)+'¬, '
        +'¬hetrc¬ : ¬'+%trim(%editc(HETRC:'Z'))+'¬, '
        +'¬hetrcn¬ : ¬'+%trim(HETRCN)+'¬, '
        +'¬hefbk¬ : ¬'+%trim(%editc(HEFBK:'4'))+'¬, '
        +'¬hetle¬ : ¬'+%trim(HETLE)+'¬, '
        +'¬hetll¬ : ¬'+%trim(HETLL)+'¬, '
        +'¬hetlku¬ : ¬'+%trim(HETLKU)+'¬, '
        +'¬hentvs¬ : ¬'+%trim(%editc(HENTVS:'Z'))+'¬, '
        +'¬hevn¬ : ¬'+%trim(%editc(HEVN:'Z'))+'¬, '
        +'¬hentf¬ : ¬'+%trim(%editc(HENTF:'Z'))+'¬, '
        +'¬hebelt¬ : ¬'+%trim(%editc(HEBELT:'4'))+'¬, '
        +'¬hebelm¬ : ¬'+%trim(%editc(HEBELM:'4'))+'¬, '
        +'¬hesgm¬ : ¬'+%trim(HESGM)+'¬, '
        +'¬hedtmo¬ : ¬'+%trim(HEDTMO)+'¬, '
        +'¬heklmo¬ : ¬'+%trim(%editc(HEKLMO:'Z'))+'¬, '
        +'¬hepk1¬ : ¬'+%trim(HEPK1)+'¬, '
        +'¬hepk2¬ : ¬'+%trim(HEPK2)+'¬, '
        +'¬hepk3¬ : ¬'+%trim(HEPK3)+'¬, '
        +'¬hepk4¬ : ¬'+%trim(HEPK4)+'¬, '
        +'¬hepk5¬ : ¬'+%trim(HEPK5)+'¬, '
        +'¬hepk6¬ : ¬'+%trim(HEPK6)+'¬, '
        +'¬hepk7¬ : ¬'+%trim(HEPK7)+'¬, '
        +'¬hepk8¬ : ¬'+%trim(HEPK8)+'¬, '
        +'¬hepk9¬ : ¬'+%trim(HEPK9)+'¬, '
        +'¬hekdl¬ : ¬'+%trim(HEKDL)+'¬, '
        +'¬helb¬ : ¬'+%trim(HELB)+'¬, '
        +'¬helkfr¬ : ¬'+%trim(HELKFR)+'¬, '
        +'¬hesdfr¬ : ¬'+%trim(HESDFR)+'¬, '
        +'¬hestfr¬ : ¬'+%trim(HESTFR)+'¬, '
        +'¬herfk¬ : ¬'+%trim(HERFK)+'¬, '
        +'¬trslag¬ : ¬'+%trim(TRSLAG)+'¬, '
        +'¬travd0¬ : ¬'+%trim(%editc(TRAVD0:'Z'))+'¬, '
        +'¬tropd0¬ : ¬'+%trim(%editc(TROPD0:'Z'))+'¬, '
        +'¬travd1¬ : ¬'+%trim(%editc(TRAVD1:'Z'))+'¬, '
        +'¬tropd1¬ : ¬'+%trim(%editc(TROPD1:'Z'))+'¬, '
        +'¬travd2¬ : ¬'+%trim(%editc(TRAVD2:'Z'))+'¬, '
        +'¬tropd2¬ : ¬'+%trim(%editc(TROPD2:'Z'))+'¬, '
        +'¬trsffd¬ : ¬'+%trim(%editc(TRSFFD:'Z'))+'¬, '
        +'¬trsffk¬ : ¬'+%trim(%editc(TRSFFK:'Z'))+'¬, '
        +'¬trsdfd¬ : ¬'+%trim(%editc(TRSDFD:'Z'))+'¬, '
        +'¬trsdfk¬ : ¬'+%trim(%editc(TRSDFK:'Z'))+'¬, '
        +'¬trsdtd¬ : ¬'+%trim(%editc(TRSDTD:'Z'))+'¬, '
        +'¬trsdtk¬ : ¬'+%trim(%editc(TRSDTK:'Z'))+'¬, '
        +'¬trsvtd¬ : ¬'+%trim(%editc(TRSVTD:'Z'))+'¬, '
        +'¬trsvtk¬ : ¬'+%trim(%editc(TRSVTK:'Z'))+'¬, '
        +'¬trkdak¬ : ¬'+%trim(TRKDAK)+'¬, '
        +'¬trkdff¬ : ¬'+%trim(TRKDFF)+'¬, '
        +'¬trverv¬ : ¬'+%trim(TRVERV)+'¬, '
        +'¬trverb¬ : ¬'+%trim(%editc(TRVERB:'Z'))+'¬, '
        +'¬trettv¬ : ¬'+%trim(TRETTV)+'¬, '
        +'¬trettb¬ : ¬'+%trim(%editc(TRETTB:'Z'))+'¬, '
        +'¬trfrak¬ : ¬'+%trim(TRFRAK)+'¬, '
        +'¬trfrav¬ : ¬'+%trim(TRFRAV)+'¬, '
        +'¬trfrab¬ : ¬'+%trim(%editc(TRFRAB:'4'))+'¬, '
        +'¬trmva¬ : ¬'+%trim(TRMVA)+'¬, '
        +'¬trfa11¬ : ¬'+%trim(TRFA11)+'¬, '
        +'¬trfa12¬ : ¬'+%trim(TRFA12)+'¬, '
        +'¬trflP1¬ : ¬'+%trim(%editc(TRFLP1:'Z'))+'¬, '
        +'¬trfa21¬ : ¬'+%trim(TRFA21)+'¬, '
        +'¬trfa22¬ : ¬'+%trim(TRFA22)+'¬, '
        +'¬trflp2¬ : ¬'+%trim(%editc(TRFLP2:'Z'))+'¬, '
        +'¬trtsta¬ : ¬'+%trim(TRTSTA)+'¬, '
        +'¬trstaf¬ : ¬'+%trim(TRSTAF)+'¬, '
        +'¬trstae¬ : ¬'+%trim(TRSTAE)+'¬, '
        +'¬trsta1¬ : ¬'+%trim(TRSTA1)+'¬, '
        +'¬trsta2¬ : ¬'+%trim(TRSTA2)+'¬, '
        +'¬trsta3¬ : ¬'+%trim(TRSTA3)+'¬, '
        +'¬trsta4¬ : ¬'+%trim(TRSTA4)+'¬, '
        +'¬trkm¬ : ¬'+%trim(%editc(TRKM:'Z'))+'¬, '
        +'¬he01¬ : ¬'+%trim(HE01)+'¬, '
        +'¬hestd¬ : ¬'+%trim(%editc(HESTD:'Z'))+'¬, '
        +'¬hxsndn¬ : ¬'+%trim(HXSNDN)+'¬, '
        +'¬hxpall¬ : ¬'+%trim(%editc(HXPALL:'Z'))+'¬, '
        +'¬hxblgk¬ : ¬'+%trim(HXBLGK)+'¬, '
        +'¬hxLkod¬ : ¬'+%trim(HXLKOD)+'¬, '
        +'¬hxllst¬ : ¬'+%trim(HXLLST)+'¬, '
        +'¬hxedik¬ : ¬'+%trim(HXEDIK)+'¬, '
        +'¬hxteri¬ : ¬'+%trim(HXTERI)+'¬, '
S01     +'¬hehakn¬ : ¬'+%trim(%editc(HEHAKN:'Z'))+'¬, '
S01     +'¬hehaan¬ : ¬'+%trim(%editc(HEHAAN:'Z'))+'¬, '
S01     +'¬helakn¬ : ¬'+%trim(%editc(HELAKN:'Z'))+'¬, '
S01     +'¬helaan¬ : ¬'+%trim(%editc(HELAAN:'Z'))+'¬, '
TMS     +'¬helmla¬ : ¬'+%trim(%editc(HELMLA:'Z'))+'¬, '
TMS     +'¬hepoen¬ : ¬'+%trim(%editc(HEPOEN:'Z'))+'¬, '
TMS     +'¬hestl1¬ : ¬'+%trim(HESTL1)+'¬, '
TMS     +'¬hestl2¬ : ¬'+%trim(HESTL2)+'¬, '
TMS     +'¬hestl3¬ : ¬'+%trim(HESTL3)+'¬, '
TMS     +'¬hestl4¬ : ¬'+%trim(HESTL4)+'¬, '
TMS     +'¬hestn1¬ : ¬'+%trim(HESTN1)+'¬, '
TMS     +'¬hestn2¬ : ¬'+%trim(HESTN2)+'¬, '
TMS     +'¬hestn3¬ : ¬'+%trim(HESTN3)+'¬, '
TMS     +'¬hestn4¬ : ¬'+%trim(HESTN4)+'¬, '
TMS     +'¬hestn5¬ : ¬'+%trim(HESTN5)+'¬, '
TMS     +'¬hestn6¬ : ¬'+%trim(HESTN6)+'¬, '
TMS     +'¬hestn7¬ : ¬'+%trim(HESTN7)+'¬, '
TMS     +'¬hestn8¬ : ¬'+%trim(HESTN8)+'¬, '
TMS     +'¬hestn9¬ : ¬'+%trim(HESTN9)+'¬, '
TMS     +'¬herfed¬ : ¬'+%trim(HERFED)+'¬, '
        +'¬helib¬ : ¬'+%trim(HELIB)+'¬, '
        +'¬hefile¬ : ¬'+%trim(HEFILE)+'¬, '
        +'¬hembr¬ : ¬'+%trim(HEMBR)+'¬, '
        +'¬hetype¬ : ¬'+%trim(HETYPE)+'¬, '
        +'¬helvl¬ : ¬'+%trim(HELVL)+'¬, '
        +'¬hesnn¬ : ¬'+%trim(%editc(HESNN:'Z'))+'¬, '
        +'¬heunik¬ : ¬'+%trim(%editc(HEUNIK:'Z'))+'¬, '
        +'¬hereff¬ : ¬'+%trim(HEREFF)+'¬, '
        +'¬hesgm¬ : ¬'+%trim(HESGM)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    PARMOS1        1024

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      BRPARF
     C  N50              OPEN      DISP
     C  N50              OPEN      DISPL04
     C  N50              OPEN      DISPL19
     C  N50              OPEN      KODTA
     C  N50              OPEN      HEADF
     C  N50              OPEN      BILLOF

     C     HEADFKLT      KLIST
     C                   KFLD                    DIAVD
     C                   KFLD                    DIOPD
     C     DISPL04K      KLIST
     C                   KFLD                    XDISTA1
     C                   KFLD                    XDITUR
     C                   KFLD                    XDIAVD
     C     DISPL19K      KLIST
     C                   KFLD                    XDISTA1
     C                   KFLD                    XDITUR
     C     KOAkey        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    HEAVD
     C                   MOVEL     'A'           KOAUNI
     C     BrParKey      KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR

     C                   MOVE      'TURPL'       PAID
     C  N50BrParKey      CHAIN     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XTURTYPE          1
     C   33              MOVE      'O'           XTURTYPE
     C                   MOVE      ' '           XDISTA1           1
     C                   MOVE      *ZEROS        XDITUR            8 0

     C                   MOVE      'LTAVD'       PAID
     C  N50BrParKey      CHAIN     BRPARF                             33
     C  N33              Z-ADD     PAVRDN        XDIAVD            4 0
     C  N33              MOVE      XDIAVD        XSTDAVD           4
      * select avd oppgitt (annet enn all/in/out..)
     C                   IF        WBSAVD <> 0
     C                             and WSSAVD <> *blanks
     C                   MOVE      WBSAVD        XDIAVD
     C                   ENDIF
      *
N01  C                   z-add     5000          MaxLes
N01  C                   z-add     200           MaxVis

     C                   ENDSR
