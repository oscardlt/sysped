     FDKT056D   CF   E             WORKSTN
     F                                     SFILE(DKT056DA:ZRECNO)
     FDKTHA     UF A E           K DISK
     FDKTHA1    IF   E           K DISK
     F                                     RENAME(DKTHAR:DKTHAR1)
     FKODTSF    IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    KOSFUN        XOSFUN
     C                   EVAL      XOSFUN='SF'
     C     *LIKE         DEFINE    *IN30         WIN30
     C                   MOVEL     '*MSGQ'       WOBJTYP          10
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SFKEY         KLIST
     C                   KFLD                    XOSFUN
     C                   KFLD                    ZKTH_SYSG
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN93=*ON
     C                   WRITE     DKT056DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     DKTHA1
     C                   READ      DKTHA1                                 94

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     DKT056DA
     C                   READ      DKTHA1                                 94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     DKT056DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     DKT056DE
     C                   WRITE     DKT056DØ
     C                   WRITE     DKT056DC
     C                   EXFMT     DKT056DB

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   ENDIF
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     DKT056DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR

     C                   CLEAR                   DKT056DD
     C                   MOVEA     '0000'        *IN(30)

     C     SRBA1         TAG

      *____________
      * Blanke felt
      *""""""""""""
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     DKT056DØ
     C                   EXFMT     DKT056DD
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN05         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'
      *_______
      * F4=Søk
      *"""""""
      *____________
      * Teste input-1
      *""""""""""""
      * signatur må vare utfyllt
     C     ZKTH_SYSG     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'DKT0005'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * signatur må finnes i SYSPED
     C     SFKEY         CHAIN     KODTSF                             30
     C                   IF        *IN30
     C                   EVAL      *IN31=*ON
     C                   MOVE      'DKT0005'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Namn må vare utfyllt
     C     ZKTH_NAMN     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'DKT0006'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Userid må vare utfyllt
     C     ZKTH_USID     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'DKT0007'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Userid må finnes
     C                   CALL      'EDI33C'
     C                   PARM                    ZKTH_USID
     C     *IN30         PARM                    WIN30
     C                   PARM                    WOBJTYP
     C                   IF        *IN30
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'DKT0007'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   EVAL      DKTH_SYSG=ZKTH_SYSG
     C                   EVAL      DKTH_NAMN=ZKTH_NAMN
     C                   EVAL      DKTH_USID=ZKTH_USID
     C                   WRITE     DKTHAR
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   EVAL      ZRECNO=WRECNO
     C                   WRITE     DKT056DA

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    DKT056DA
      *____________
      * Flytte felt
      *""""""""""""
     C     DKTH_SYSG     CHAIN(N)  DKTHA                              53
     C                   MOVEA     '0000'        *IN(30)
     C                   EVAL      ZKTH_SYSG=DKTH_SYSG
     C                   EVAL      ZKTH_NAMN=DKTH_NAMN
     C                   EVAL      ZKTH_USID=DKTH_USID

     C     SRBB1         TAG

     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     DKT056DØ
     C                   EXFMT     DKT056DD
     C                   MOVEA     '0000'        *IN(30)
     C     *IN12         IFEQ      '0'
      *_______
      * F4=Søk
      *"""""""
      *____________
      * Teste input
      *""""""""""""
      * signatur må vare utfyllt
     C     ZKTH_SYSG     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'DKT0005'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * signatur må finnes i SYSPED
     C     SFKEY         CHAIN     KODTSF                             30
     C                   IF        *IN30
     C                   EVAL      *IN31=*ON
     C                   MOVE      'DKT0005'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Namn må vare utfyllt
     C     ZKTH_NAMN     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'DKT0006'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Userid må vare utfyllt
     C     ZKTH_USID     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'DKT0007'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Userid må finnes
     C                   CALL      'EDI33C'
     C                   PARM                    ZKTH_USID
     C     *IN30         PARM                    WIN30
     C                   PARM                    WOBJTYP
     C                   IF        *IN30
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'DKT0007'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     ZKTH_SYSG     CHAIN     DKTHA                              53
     C                   EVAL      DKTH_NAMN=ZKTH_NAMN
     C                   EVAL      DKTH_USID=ZKTH_USID
     C                   UPDATE    DKTHAR

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     DKTH_SYSG     CHAIN     DKTHA                              53
     C                   DELETE    DKTHAR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   DKT056DA
     C                   UPDATE    DKT056DA
     C                   EVAL      *IN95=*ON

     C                   ENDSR
