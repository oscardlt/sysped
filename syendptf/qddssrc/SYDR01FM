     A*%%TS  SD  20170615  065402  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20170613  152154  SVEIN       REL-V7R1M0  5770-WDS
     A            SYJOBQ        10A  H
     A            SYJOBD        10A  H
     A            WSNR           1A  I  6  2VALUES('S' 'A' 'V' 'W' ' ')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            SYAKT          1A  O  6  6
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A            SYBRU          1A  O  6 10
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A            SYJOB         10A  O  6 13
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A            SYPGM         10A  O  6 24
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A            SYLIB         10A  O  6 35
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A            SYJOBT         9A  O  6 46
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A            SYTXT         30A  O  6 56
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A            SYJOBN         6A  O  6 87
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A            SYUSER        10   O  6 94
     A  70                                  COLOR(RED)
     A  71                                  DSPATR(RI)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20170615  065402  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYDR01 '
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(040 SHU1129 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  2 18'SUBSYSTEM:'
     A            USBS          10A  O  2 31COLOR(WHT)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(130 SHU1130 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(129 SHU1731 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  2007  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  1MSGCON(077 SHU1130 *LIBL/SYMSGF)
     A            SYAKT          1A  O  2  7DSPATR(UL)
     A            WSBRU          1   B  2 10
     A                                      DSPATR(PC)
     A            SYJOB         10   O  2 13
     A            SYPGM         10   O  2 24
     A            SYLIB         10   O  2 35
     A            SYTXT         30   O  2 46
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  2007  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2AAVD         4Y 0I  1 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  2007  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'Bekreft sletting')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            SIAVD          4Y 0O  1  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  1  9
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  2007  '
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(075 SHU0983 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYDR01 '
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(040 SHU1129 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  2 18'SUBSYSTEM:'
     A            USBS          10A  O  2 31COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(130 SHU1130 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(129 SHU1731 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2007  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
