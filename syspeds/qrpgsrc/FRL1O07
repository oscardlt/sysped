      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/FRl1O07 MODULE(*LIBL/FRL1O07 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
ODDEV *ODDEV   PTF9 ODDEVEN-logikk
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FPRISLD1   IF   E           K DISK    USROPN
     FPRISLD1N  IF   E           K DISK    USROPN
     F                                     RENAME(PRISLDR:PRISLDRN)
     FPRISLD1E  IF   E           K DISK    USROPN
     F                                     RENAME(PRISLDR:PRISLDRE)
     FPRISLD3   IF   E           K DISK    USROPN
     F                                     RENAME(PRISLDR:PRISLDR3)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * aktiv pristab.nr
     D Prinr           s              5
     D Prinrn          s              5  0
     D Firma           s              2
OddEvD OddEven         s             10
      * Client input variables
     D WSANR           s             15
     D SOKANR          s             15
     D SOKANN          s             15
     D SOKANE          s             14
     D SOKANEN         s             14  0
     D SOKNVN          s             70
     D SOKNVN2         s             70
     D VARI            S             70A   VARYING
     D USER            s             10
     D WSGRP           s              8
     D W2GRP           s              8  0
     D WSPNR           s              8
     D W2PNR           s              8  0
     D MaxV            s              5
     D MaxVis          s              5  0
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D VG              S              5    DIM(10)
     D VE              S              5    DIM(10)
     D                 DS
     D  PAVRDA                 1     50
     D  VV                     1     50    DIM(10)
     D                 DS
     D  HOIREI                 1     15
     D  HOI                    1     15    DIM(15)
     D                 DS
     D  HOIREU                 1     15
     D  HOU                    1     15    DIM(15)
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      SOKANR  = zhbgetvar('SOKANR')
     C                   eval      SOKANN  = zhbgetvar('SOKANN')
     C                   eval      SOKANE  = zhbgetvar('SOKANE')
     C                   eval      SOKANEN    = c2n2(SOKANE)
      * Talgø varenummer
     c                   move      sokanr        hoirei
     c                   exsr      hoiresr
     c                   move      hoireu        sokanr
      * NOBB varenummer
     c                   move      sokann        hoirei
     c                   exsr      hoiresr
     c                   move      hoireu        sokann
      *
     C                   eval      PriNR      = zhbgetvar('PRINR     ')
     C                   eval      Prinrn     = c2n2(PriNR)
     C                   eval      FIRMA      = zhbgetvar('FIRMA     ')
     C                   eval      SOKNVN  = zhbgetvar('SOKNVN')
     C                   eval      SOKNVN  = uppify(SOKNVN)
     C     SOKNVN        IFNE      *BLANKS
     C     ' '           CHECKR(E) SOKNVN        NUM               2 0
     C                   EVAL      %len(VARI) = NUM
     C     NUM           SUBST     SOKNVN:1      VARI
     C                   END
     C                   eval      WSGRP   = zhbgetvar('WSGRP')
     C                   eval      W2GRP   = c2n2(WSGRP)
     C                   eval      WSPNR   = zhbgetvar('WSPNR')
     C                   eval      W2PNR   = c2n2(WSPNR)
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      USER = zhbgetvar('USER')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   close     BRPARF
     C                   CLOSE     PRISLD1
     C                   CLOSE     PRISLD1N
     C                   CLOSE     PRISLD1E
     C                   CLOSE     PRISLD3
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'FRL1O07'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('WSGRP':WSGRP)
     C                   callp     updHTMLvar('WSPNR':WSPNR)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     C                   setoff                                       72
     c                   eval      SOKNVN2   = SOKNVN
     c                   Move      *zeros        AntVist           5 0
      *
     C     soknvn        ifne      *blanks
     C     ANRKEY        setll     PRISLD3
     C     ANRKEY        reade     PRISLD3                                33
     c                   else
     C     sokanr        ifne      *blanks
     C     ANRKEYL       setll     PRISLD1
     C     ANRKEY        reade     PRISLD1                                33
     c                   else
     C     sokann        ifne      *blanks
     C     ANRKEYN       setll     PRISLD1N
     C     ANRKEY        reade     PRISLD1N                               33
     c                   else
     C     sokanen       ifne      *zeros
     C     ANRKEYE       setll     PRISLD1E
     C     ANRKEY        reade     PRISLD1E                               33
     c                   end
     c                   end
     c                   end
     c                   end
     C     *in33         DOWEQ     *OFF
     C                   exsr      exclude
     c     EXCL          IFEQ      'N'
      *
      *
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('PRD_ANRS':PRD_ANRS)
     C                   callp     updHTMLvar('WSANR':PRD_ANRS)
     C*                  callp     updHTMLvar('PRD_ANRK':PRD_ANRK)
     C                   callp     updHTMLvar('PRD_NOBB':PRD_NOBB)
     C                   callp     updHTMLvar('PRD_PTX':PRD_PTX)
     C                   move      PRD_EAN       PRD_EANA         14
     C                   callp     updHTMLvar('PRD_EANA':PRD_EANA)
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C     STLES3        TAG
     C                   END
     C     soknvn        ifne      *blanks
     C     ANRKEY        reade     PRISLD3                                33
     c                   else
     C     sokanr        ifne      *blanks
     C     ANRKEY        reade     PRISLD1                                33
     c                   else
     C     sokann        ifne      *blanks
     C     ANRKEY        reade     PRISLD1N                               33
     c                   else
     C     sokanen       ifne      *zeros
     C     ANRKEY        reade     PRISLD1E                               33
     c                   end
     c                   end
     c                   end
     c                   end
     C                   end
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   Move      'J'           EXCL              1
      * diverse tester - om ikke bestått hopp ut (med J i EXCL)
      * Posten skal med på liste
     C                   Move      'N'           EXCL              1
     C     SOKNVN        ifne      *BLANKS
     C                   move      PRD_PTX       test70           70
     C                   eval      TEST70  = uppify(PRD_PTX)
     C     VARI          SCAN      TEST70        NUM2              2 0    72
     c  N72              MOVE      'J'           EXCL
     C                   END
      *
     C     UTEXCL        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      PRISLD1
     C                   OPEN      PRISLD1N
     C                   OPEN      PRISLD1E
     C                   OPEN      PRISLD3
     C                   open      BRPARF                               50
      * Key for PRISLD
     C     ANRKEYL2      KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    PRINRN
     C                   KFLD                    SOKNVN
     C     ANRKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    PRINRN
      * L1 varenummer
     C     ANRKEYL       KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    PRINRN
     C                   KFLD                    SOKANR
      * NOBB varenummer
     C     ANRKEYN       KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    PRINRN
     C                   KFLD                    SOKANN
      * EAN varenummer
     C     ANRKEYE       KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    PRINRN
     C                   KFLD                    SOKANEN
     c     BRPARFK       klist
     c                   kfld                    PABRID
     c                   kfld                    PAKUNR
     c                   kfld                    PAID
      *
     C                   MOVE      *BLANKS       VG
     C                   MOVE      ' '           XVGNIVÅ           1
      * VAREGRUPPER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
      *
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   EVAL      PAID   = 'LLVG'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'B'           XVGNIVÅ           1
     C                   ELSE
      * VAREGRUPPER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'K'           XVGNIVÅ           1
     C                   ELSE
      * VAREGRUPPER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'F'           XVGNIVÅ           1
     C                   END
     C                   END
     C                   END
      *
     C                   MOVE      *BLANKS       VE
     C                   MOVE      ' '           XVENIVÅ           1
      * VAREEIER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
     C                   EVAL      PAID   = 'LLVE'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'B'           XVENIVÅ           1
     C                   ELSE
      * VAREEIER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'K'           XVENIVÅ           1
     C                   ELSE
      * VAREEIER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'F'           XVENIVÅ           1
     C                   END
     C                   END
     C                   END
      *
     C                   endsr
      *********************************************************************
     C     hoiresr       begsr
      *********************************************************************
     c                   clear                   hoireu
     c                   z-add     15            nr1               2 0
     c                   z-add     15            nr2               2 0
     c                   do        15
     c     hoi(nr1)      ifne      *blanks
     c                   move      hoi(nr1)      hou(nr2)
     c                   sub       1             nr2
     c                   endif
     c                   sub       1             nr1
     c                   enddo
     c                   endsr
