      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED DOKUMENTER                 ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-86 95-97
      ***********************************************************
      *
     FTRUVTD    UF A E           K DISK
     FTRKODL01  IF   E           K DISK
     FTR023D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSSUBN               378    379B 0
     D  WSCLOC               382    383B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UTDT          IFNE      *BLANKS
     C                   MOVE      UTDT          TVTDT
     C                   MOVE      UTDR          TVTDR
     C                   MOVE      UTDSK         TVTDSK
     C                   MOVE      UTDO          TVTDO
     C                   MOVE      UTDOS         TVTDOS
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     TRVTDK        SETLL     TRUVTD
     C     TRVTDK        READE(N)  TRUVTD                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     TRVTDK        READE(N)  TRUVTD                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C  N30              Z-ADD     4             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C   06              DO
     C                   EXSR      CRTTD
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     TRVTDK        SETLL     TRUVTD
     C     TRVTDK        READE     TRUVTD                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    TRUVTDR
     C     TRVTDK        READE     TRUVTD                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UTDT
     C                   MOVE      *BLANKS       UTDR
     C                   MOVE      *BLANKS       UTDSK
     C                   MOVE      *BLANKS       UTDO
     C                   MOVE      *BLANKS       UTDOS
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     TVTDT         IFNE      '*DLT'
     C                   MOVE      TVTDT         UTDT
     C                   MOVE      TVTDR         UTDR
     C                   MOVE      TVTDSK        UTDSK
     C                   MOVE      TVTDO         UTDO
     C                   MOVE      TVTDOS        UTDOS
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     TVTDT         IFNE      '*DLT'
     C     UTDT          IFEQ      *BLANKS
     C                   MOVE      TVTDT         UTDT
     C                   MOVE      TVTDR         UTDR
     C                   MOVE      TVTDSK        UTDSK
     C                   MOVE      TVTDO         UTDO
     C                   MOVE      TVTDOS        UTDOS
     C                   ELSE
     C                   WRITE     TRUVTDR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     TVTDT         COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGTD
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     TVTDT         COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTTD
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C                   MOVE      '014'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSTDT         YKODE
     C     TRK01K        CHAIN     TRKODL01                           90
     C     *IN90         IFEQ      '1'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      'YBC0823'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
      *
     C     WSTDR         COMP      *BLANKS                                89
     C     *IN89         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     12            POSNBR
     C                   MOVE      'YBC0823'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
      *
     C     WSTDSK        IFNE      *BLANKS
     C                   MOVE      '012'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSTDSK        YKODE
     C     TRK01K        CHAIN     TRKODL01                           88
     C     *IN88         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     33            POSNBR
     C                   MOVE      'YBC0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C     WSTDOS        IFNE      *BLANKS
     C                   MOVE      '012'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSTDOS        YKODE
     C     TRK01K        CHAIN     TRKODL01                           87
     C     *IN87         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     63            POSNBR
     C                   MOVE      'YBC0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWTD        BEGSR
     C***********************************************
     C                   MOVE      WSTDT         TVTDT
     C                   MOVE      WSTDR         TVTDR
     C                   MOVE      WSTDSK        TVTDSK
     C                   MOVE      WSTDO         TVTDO
     C                   MOVE      WSTDOS        TVTDOS
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTTD         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSTDT
     C                   MOVE      *BLANKS       WSTDR
     C                   MOVE      *BLANKS       WSTDSK
     C                   MOVE      *BLANKS       WSTDO
     C                   MOVE      *BLANKS       WSTDOS
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCRT         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCRT
     C                   END
     C   12              GOTO      SLCRT
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCRT
      *
     C                   EXSR      MOVWTD
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGTD         BEGSR
     C***********************************************
     C                   MOVE      TVTDT         WSTDT
     C                   MOVE      TVTDR         WSTDR
     C                   MOVE      TVTDSK        WSTDSK
     C                   MOVE      TVTDO         WSTDO
     C                   MOVE      TVTDOS        WSTDOS
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCHG
     C                   EXSR      MOVWTD
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTTD         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       TVTDT
     C                   MOVE      *BLANKS       TVTDR
     C                   MOVEL     '*DELETE'     TVTDR
     C                   MOVE      *BLANKS       TVTDSK
     C                   MOVE      *BLANKS       TVTDO
     C                   MOVE      *BLANKS       TVTDOS
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     11
     C                   MOVE      '014'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSTDT         UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         WSTDT
     C                   Z-ADD     12            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     33
     C     POSNBR        ANDLE     34
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSTDSK        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         WSTDSK
     C                   Z-ADD     36            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     63
     C     POSNBR        ANDLE     64
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSTDOS        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         WSTDOS
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TRVTDK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    ULI
     C     TRK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
     C                   PARM                    UTDT              6
     C                   PARM                    UTDR             20
     C                   PARM                    UTDSK             2
     C                   PARM                    UTDO             26
     C                   PARM                    UTDOS             2
     C     *IN27         PARM      *IN27         UIN27             1
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      UAVD          TVAVD
     C                   MOVE      UTDN          TVTDN
     C                   MOVE      ULI           TVLI
      *
     C                   ENDSR
      *
