********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10005 * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRE FRA 2.0 TIL 4.0
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12001 * 02 YBC PTF12 NYTT FELT FOR MVA-REGISTRTERT
12053 * 03 YBC PTF12 FLERE BELØP-FELTER ER ENDRET FOR Å TA STØRRE VERDI
12281 * 04 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
********************************************************************
      *#############################################################
      * OBS!!!  ALLE ENDRINGER MÅ KOPIERES TIL SADHKO OG SADOMTH
      *#############################################################
     A*__________________________________
     A* SYSPED / Tolldeklarasjoner Import
     A*""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SADHR
     A            SIST      R               REFFLD(ST)
     A            SIAVD          4S 0       COLHDG('AVDELING')
     A            SITDN     R               REFFLD(TDN)
E02  A            SISG      R               REFFLD(SG)
E02  A            SIDT      R               REFFLD(DT)
E02  A                                      COLHDG('Registreringsdato')
      * SIUR = 'F' --> SJØ-OPPD.  SIUR = ' ' --> LAND/FLY-OPPD.
     A            SIUR      R               REFFLD(UR)
     A                                      COLHDG('Oppr.kode')
     A            SIDTY     R               REFFLD(DTY)
     A            SIDP      R               REFFLD(DP)
N04  A            SIDP2          2S 0       COLHDG('Siste 2 siffer pros')
     A            SIKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            SINAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            SIADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            SIADS2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Selger')
     A            SIADS3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Selger')
     A            SINTK     R               REFFLD(NTK)
     A            SISKI     R               REFFLD(KDSKI)
     A            SIKDDK    R               REFFLD(KDDK)
     A            SIVKB     R               REFFLD(VKT9)
     A                                      COLHDG('Bruttovekt')
     A            SIKDC     R               REFFLD(KDC)
     A            SIKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            SIRG          11A         COLHDG('FORETAKSNR.')
N02  A            SIMVA          1A         COLHDG('J/N MVA-REGISTRERT')
     A            SINAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            SIADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            SIADK2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Kjøper')
     A            SIADK3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Kjøper')
     A            SIVAL1    R               REFFLD(VAL)
     A                                      COLHDG('Valutakode Frakt')
     A            SIBEL1    R               REFFLD(BEL11)
     A                                      COLHDG('Beløp tollb.frakt')
     A            SIVAL2    R               REFFLD(VAL)
     A                                      COLHDG('Val.kode A. kost')
     A            SIBEL2    R               REFFLD(BEL11)
     A                                      COLHDG('Beløp A. kost')
     A            SIFTG2    R               REFFLD(KDFT)
     A                                      COLHDG('Fortegn +/- a.kost')
     A            SILKA     R               REFFLD(LKA)
     A                                      COLHDG('Landk avs/utf-land')
     A            SITLF     R               REFFLD(TLF)
     A                                      COLHDG('Telefon Deklarant')
     A            SINAD     R               REFFLD(NA)
     A                                      COLHDG('Navn Deklarant')
     A            SILV      R               REFFLD(LV)
     A            SILVT     R               REFFLD(LVT)
     A            SITRID    R               REFFLD(TRID)
     A            SILKT     R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
     A            SIVAL3    R               REFFLD(VAL)
     A                                      COLHDG('Val.kode Faktsum')
     A            SIBEL3    R               REFFLD(BEL13)
     A                                      COLHDG('Beløp Faktsum')
     A            SIVKU     R               REFFLD(VKU)
S04  A*           SITST     R               REFFLD(TST)
N04  A            SITST          2S 0       COLHDG('Transaksjonstype')
     A            SITRT     R               REFFLD(TRT)
     A* BRUKER POSISJON 1 I SITRT TIL SAMMENSLÅING VARELINJER          )
     A            SITRM     R               REFFLD(TRM)
     A            SIFIF     R               REFFLD(FIF)
     A            SIFID     R               REFFLD(DT)
     A                                      COLHDG('Finans oppl.fakdat')
S03  A*           SIBELT    R               REFFLD(BEL9)
S03  A*                                     COLHDG('Beløp Trafikkavg')
N03  A            SIBELT        11S 0       COLHDG('Beløp Trafikkavg')
     A            SI07           7          COLHDG('*blanks')
     A            SIKTA     R               REFFLD(KTA)
     A            SIKTB     R               REFFLD(KTB)
     A            SIGN          15          COLHDG('GODSNR.')
     A            SIFT1     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-1')
     A            SIFT2     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-2')
     A            SIFT3     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-3')
     A            SIFT4     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-4')
     A            SIDST     R               REFFLD(DST)
S03  A*           SIBEL4    R               REFFLD(BEL9)
S03  A*                                     COLHDG('Beløp CIFsum')
N03  A            SIBEL4        11S 0       COLHDG('Beløp CIFsum')
     A            SIDTG     R               REFFLD(DTG)
     A            SITLL     R               REFFLD(TLL)
     A            SITLE     R               REFFLD(TLE)
     A            SILS      R               REFFLD(LS)
     A            SIKDLS         2A         COLHDG('Lagringssted kode')
S01  A*           SIKDH     R               REFFLD(KDH)
N01  A            SIKDH          4S 0       COLHDG('HAVNEKODE')
OBS01 * SIKDH ER BRUKT TIL Å LAGRE BETALEKODE FOR FORTOLLING SLIK AT
OBS01 * KODEN KAN HENTES VED EVT. OMBEREGNING
OBS01 * 1 = BLANK, 2 = KONTANT, 3 = DAGSOPPGJØR OG 4 = MÅNEDSOPPGJØR
     A            SIKDTR    R               REFFLD(KDTR)
     A            SIAS      R               REFFLD(AS)
     A                                      COLHDG('Avgiftssats transp')
S03  A*           SIBEL5    R               REFFLD(BEL9)
S03  A*                                     COLHDG('Beløp Tot Toll')
N03  A            SIBEL5        11S 0       COLHDG('Beløp Tot Toll')
S03  A*           SIBEL6    R               REFFLD(BEL9)
S03  A*                                     COLHDG('Beløp Tot MVA')
N03  A            SIBEL6        11S 0       COLHDG('Beløp Tot MVA')
     A            SIBEL7    R               REFFLD(BEL110)
     A                                      COLHDG('Beløp Tot avg')
     A            SIBEL8    R               REFFLD(BEL11)
     A                                      COLHDG('Frakt omberegnet')
     A            SIBEL9    R               REFFLD(BEL11)
     A                                      COLHDG('Andre kost.omber')
S03  A*           SIBELA    R               REFFLD(BEL7)
S03  A*                                     COLHDG('Beløp Forsikring')
N03  A            SIBELA         9S 0       COLHDG('Beløp Forsikring')
     A            SILV2     R               REFFLD(LV)
     A            SIPOS     R               REFFLD(POS9)
     A            SITARF        16          COLHDG('TARIFFØRSNAVN')
     A            SIVALB    R               REFFLD(VAL)
     A                                      COLHDG('Valutakode Work')
     A            SIBELB    R               REFFLD(BEL5S)
     A                                      COLHDG('Beløp Work')
     A            SIMID     R               REFFLD(MID)
     A            SIMIDK    R               REFFLD(MIDK)
     A            SIMF      R               REFFLD(MF)
     A            SIMP      R               REFFLD(MP)
     A            SIDTØ     R               REFFLD(DT)
     A                                      COLHDG('Ønsket Beh.dato')
     A            SIMI      R               REFFLD(MI)
     A            SIBELD    R               REFFLD(BEL10S)
     A                                      COLHDG('Beløp Depositum')
     A            SIPKL     R               REFFLD(PK)
     A                                      COLHDG('Print ved linje ?')
      * SIKDV ER BRUKT -> LAGRE SISTE STATUS VED MOTTAK AV FEILMELDING FRA TVINN
     A            SIKDV     R               REFFLD(KDV)
     A*
     A*** SI0035 + SI27 = SI28  FRA FØR 300590
     A*** BARE AIR CONTACT, NTH OG NORGIPS BRUKER SI28
     A*** FOR Å UNNGÅ KONVERTERING AV REGISTER (SADH) GJØR VI EN
     A*** DOBBEL DEFINEISJON I PROGRAMMER.
     A*** FORHOLDET MELLOM SI28 OG SI27, SI0035 ER FØLGENDE:
     A*                                       1  28 SI28
     A*                                       1  112SIBELC
     A*                                      12  152SIRAB
     A*                                       1  27 SI27
     A*                                      28  28 SI0035
      * SI0035 --> 1. OM DET ER EN TEST SENDING VED UTGÅENDE
      *            2. OM DET ER DOKUMENT/VAREKONTROLL VED INNGÅENDE
     A            SI0035    R               REFFLD(N0035)
     A            SI27          27          COLHDG('*blanks')
     A*
     A            SIOPD     R               REFFLD(OPD)
     A            SIFRBN    R               REFFLD(FRBN)
     A            SIWS      R               REFFLD(WS)
     A            SIKTC          1A         COLHDG('IKKE TOLLKREDITT')
     A            SIVALR    R               REFFLD(VAL)
     A            SIBELR    R               REFFLD(BEL11)
     A                                      COLHDG('Bearbeidskostnad')
     A            SIBELS    R               REFFLD(BEL9)
     A                                      COLHDG('CIFsum PROS=64/65')
     A          K SIAVD
     A          K SITDN
