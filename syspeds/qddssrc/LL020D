     A*%%TS  SD  20140819  153214  JOVO        REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 SØK PÅ LEVERANDØRS VARENR
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 OPSJON D = VIS SAMMENSLÅINGS DETALJE
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/LLAGJF LLAGJR)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR LL020PNL)
     A                                      HLPTITLE('ARBEIDE MED SERIENR')
     A                                      ALTHELP
     A                                      HELP
     A                                      CA02(02 'SPØR')
      *
     A          R BILDE01
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      CF04(04 'Spørr')
     A                                      CF10(10 'HENT FRA L1')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(LL020010 LL020PNL)
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(LL020020 LL020PNL)
     A                                      HLPARA(02 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL020 '
     A                                      COLOR(BLU)
     A N27                              1 20MSGCON(50 YBC1151 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(50 YBC1152 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  3  3MSGCON(30 YBC1153 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  3MSGCON(75 YBC1154 SYMSGF)
     A                                  6  3MSGCON(75 YBC1155 SYMSGF)
     A                                  8  3MSGCON(15 YBC1156 SYMSGF)
     A            SEGRUP         8Y 0O  8 20DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A N49        NVNGRU        30   O  8 29COLOR(WHT)
     A  49                              8 29MSGCON(40 YBC1157 SYMSGF)
     A                                  9  3MSGCON(15 YBC1158 SYMSGF)
     A            SEKJED         8Y 0B  9 20DSPATR(HI)
     A                                      EDTCDE(4)
     A            NVNKJE        30A  O  9 29COLOR(WHT)
     A                                 10 39MSGCON(20 YBC1159 SYMSGF)
N01  A                                 10 62MSGCON(15 YBC1970 SYMSGF)
     A                                 11  3MSGCON(15 YBC1160 SYMSGF)
     A            SEARTN        15A  B 11 20DSPATR(HI)
     A            SEXART         1A  B 11 47DSPATR(HI)
N01  A            SEARTL        15A  B 11 62DSPATR(HI)
     A                                 12  3MSGCON(15 TRO0452 SYMSGF)
     A            SEDATF         6Y 0B 12 20DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 12 27'('
     A            SEDATT         6Y 0B 12 29DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                 12 36')'
     A                                 12 39MSGCON(06 YBC1161 SYMSGF)
     A            SEXDAT         1A  B 12 47DSPATR(HI)
     A                                 13  3MSGCON(15 YBC1162 SYMSGF)
     A            SELOK1         5A  B 13 20DSPATR(HI)
     A            SELOK2        20A  B 13 26DSPATR(HI)
     A            SEXLOK         1A  B 13 47DSPATR(HI)
     A                                 14  3MSGCON(15 YBC1163 SYMSGF)
     A            SESENR        25A  B 14 20DSPATR(HI)
     A            SEXSEN         1A  B 14 47DSPATR(HI)
     A                                 15  3MSGCON(15 YBC1164 SYMSGF)
     A            SEAG           5A  B 15 20DSPATR(HI)
     A                                 16  3MSGCON(15 YBC1165 SYMSGF)
     A            SEMRNR         5  0B 16 20DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 16 27MSGCON(13 YBC1166 SYMSGF)
     A            SEKREF        25A  B 16 41DSPATR(HI)
     A                                 17 27MSGCON(13 YBC1167 SYMSGF)
     A            SELREF        25A  B 17 41DSPATR(HI)
     A                                 18  3MSGCON(15 YBC1168 SYMSGF)
     A                                 18 27'Godsnr:'
     A            SEGN          15A  B 18 35DSPATR(HI)
     A                                 18 52'MRN-nr:'
     A            SETRNR        18A  B 18 60DSPATR(HI)
     A                                 19  3MSGCON(15 YBC1169 SYMSGF)
     A            SESAL          1A  B 19 20DSPATR(HI)
     A                                 19 23MSGCON(40 YBC1170 SYMSGF)
     A                                 20 23MSGCON(40 YBC1171 SYMSGF)
     A                                 21 23MSGCON(40 YBC1172 SYMSGF)
     A                                 22  3MSGCON(18 YBC1173 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  3MSGCON(10 TRO0002 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 20MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40'F10=Hent bestilling'
     A                                      COLOR(BLU)
     A                                 23 65MSGCON(15 TRO0424 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20140819  153214  JOVO        REL-V7R1M0  5770-WDS
     A            VAMRNR         5Y 0H
     A            VAMN           7Y 0H
     A            VAVARE        35A  H
     A            ARTDS         35A  H
     A            VAANR         15A  H
     A            VALLOK        25A  H
     A            VASENR        25A  H
     A            VASTAT         1A  H
     A            VAPLID        20A  H
     A            VAST2          5A  H
     A            WSNR           1A  B  7  2
     A  30                                  DSPATR(PC)
     A                                      VALUES(' ' '2' '4' '5' '6' '8' '9' -
S02  A*                                     'A' 'S' 'V' 'L' 'X' 'E' 'M')
N02  A                                      'A' 'S' 'V' 'L' 'X' 'E' 'M' 'D')
     A            XXVARE        35A  O  7  5
     A  51                                  COLOR(WHT)
     A            VADATO    R        O  7 41EDTWRD('    .  .  ')
     A  48                                  COLOR(YLW)
     A            VAAVD     R        O  7 52EDTCDE(4)
     A            VAOPD     R        O  7 57EDTCDE(4)
     A            VAOANT    R        O  7 65EDTCDE(4)
     A            VASALD    R        O  7 73EDTCDE(4)
     A                                      COLOR(WHT)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20140819  153214  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0015)
     A  *DS4                                SFLSIZ(0015)
     A  *DS3                                SFLPAG(0014)
     A  *DS4                                SFLPAG(0014)
     A                                      CF03(03 'Avslutt')
     A                                      CA12(12 'Cancel')
     A                                      CF07(07 'ART/VARESLAG/BATCHNR')
     A                                      CA05(05 'FORNY')
     A N27N28                               CF06(06 'Lag ny')
     A N27N28                               CF16(16 'Lag ny heading')
     A N27N28                               CF11(11 'Mottaksrapport')
     A N28                                  CF08(08 'Søk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  31                                  SFLMSG('Tastet valg er ikke gyldig')
     A          H                           HLPPNLGRP(LL020030 LL020PNL)
     A  *DS3                                HLPARA(01 001 03 080)
     A  *DS4                                HLPARA(01 001 03 080)
     A          H                           HLPPNLGRP(LL020040 LL020PNL)
     A  *DS3                                HLPARA(04 001 21 080)
     A  *DS4                                HLPARA(04 001 21 080)
     A          H                           HLPPNLGRP(LL020050 LL020PNL)
     A  *DS3                                HLPARA(22 001 24 080)
     A  *DS4                                HLPARA(22 001 24 080)
     A                                  1  2'SYSPED/LL020 '
     A                                      COLOR(BLU)
     A N27N28                           1 20MSGCON(040 YBC1174 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  28
     AO 27                              1 20MSGCON(040 YBC1175 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(014 YBC1176 *LIBL/SYMSGF)
     A            VAGRUP    R        O  2 17COLOR(WHT)
     A                                      EDTCDE(4)
     A            NVNGRU        30A  O  2 27COLOR(WHT)
     A            ZRECNO         4S 0O  2 56SFLRCDNBR
     A                                  2 72TIME
     A                                  3  2MSGCON(014 YBC1177 *LIBL/SYMSGF)
     A            VAKJED    R        O  3 17COLOR(WHT)
     A                                      EDTCDE(4)
     A            NVNKJE        30A  O  3 27COLOR(WHT)
     A                                  3 58MSGCON(021 YBC1182 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(018 YBC1178 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 58MSGCON(009 YBC1179 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 34MSGCON(010 YBC1180 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           4 45MSGCON(010 YBC1181 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 57'L=Lisens'
     A                                      COLOR(BLU)
     A                                  4 67'E=Status'
     A                                      COLOR(BLU)
     A N27N28                           5  4MSGCON(007 TRO0007 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           5 14MSGCON(008 TRO0009 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 25MSGCON(008 TRO0010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           5 34MSGCON(020 YBC1183 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           5 57MSGCON(011 YBC1184 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 69MSGCON(008 YBC1185 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N42N43                           6  2MSGCON(078 YBC1186 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  42N43                           6  2MSGCON(078 YBC1187 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  42 43                           6  2MSGCON(078 YBC1188 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 21MSGCON(012 JOV1639 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R VARELI
     A*%%TS  SD  20140624  204608  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Exit')
     A                                      CF04(04 'Søk')
     A  39                                  CF07(07 'Hent ny forholdstall')
     A                                      CA12(12 'Cancel')
     A                                      CF23(23 'AKSEPTER FEIL')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(LL020060 LL020PNL)
     A  *DS3                                HLPARA(01 001 01 080)
     A  *DS4                                HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(LL020070 LL020PNL)
     A  *DS3                                HLPARA(02 001 06 080)
     A  *DS4                                HLPARA(02 001 06 080)
     A          H                           HLPPNLGRP(LL020080 LL020PNL)
     A  *DS3                                HLPARA(07 001 07 080)
     A  *DS4                                HLPARA(07 001 07 080)
     A          H                           HLPPNLGRP(LL020090 LL020PNL)
     A  *DS3                                HLPARA(08 001 14 080)
     A  *DS4                                HLPARA(08 001 14 080)
     A          H                           HLPPNLGRP(LL020100 LL020PNL)
     A  *DS3                                HLPARA(15 001 22 080)
     A  *DS4                                HLPARA(15 001 22 080)
     A          H                           HLPPNLGRP(LL020110 LL020PNL)
     A  *DS3                                HLPARA(23 001 24 080)
     A  *DS4                                HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WRVKF0    R        H      REFFLD(LLAGJR/VAVKF0 *LIBL/LLAGJF)
     A            WRKUF0    R        H      REFFLD(LLAGJR/VAKUF0 *LIBL/LLAGJF)
     A            WRVKF1    R        H      REFFLD(LLAGJR/VAVKF1 *LIBL/LLAGJF)
     A            WRKUF1    R        H      REFFLD(LLAGJR/VAKUF1 *LIBL/LLAGJF)
     A            WRVKF2    R        H      REFFLD(LLAGJR/VAVKF2 *LIBL/LLAGJF)
     A            WRKUF2    R        H      REFFLD(LLAGJR/VAKUF2 *LIBL/LLAGJF)
     A            WRVKF3    R        H      REFFLD(LLAGJR/VAVKF3 *LIBL/LLAGJF)
     A            WRKUF3    R        H      REFFLD(LLAGJR/VAKUF3 *LIBL/LLAGJF)
     A                                  1  2'SYSPED/LL020 '
     A                                      COLOR(BLU)
     A N27                              1 20MSGCON(040 YBC1174 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(040 YBC1175 *LIBL/SYMSGF)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  2  3MSGCON(015 YBC1189 *LIBL/SYMSGF)
     A            WRMRNR         5Y 0O  2 20DSPATR(HI)
     A                                      EDTCDE(4)
     A  29                              2 26'Status:'
     A  29        WRST2          5A  O  2 34DSPATR(HI)
     A                                      COLOR(YLW)
     A  29        WRST2N        30A  O  2 40DSPATR(HI)
     A                                      COLOR(YLW)
     A                                  3  3MSGCON(015 YBC1190 *LIBL/SYMSGF)
     A            WRGRUP         8Y 0O  3 20DSPATR(HI)
     A                                      EDTCDE(4)
     A            NVNGRU        30A  O  3 29COLOR(WHT)
     A                                  3 60'GNR:'
     A            WRGN          15A  O  3 65COLOR(RED)
     A                                  4  3MSGCON(015 YBC1191 *LIBL/SYMSGF)
     A            WRKJED         8Y 0O  4 20DSPATR(HI)
     A                                      EDTCDE(4)
     A            NVNKJE        30A  O  4 29COLOR(WHT)
     A            WRTRNR        18A  O  4 60COLOR(RED)
     A                                  5  3MSGCON(015 YBC1192 *LIBL/SYMSGF)
     A            WRANR         15A  B  5 20DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                  5 36'Le.Art:'
     A            WRANRL        15A  B  5 44DSPATR(HI)
     A  41                                  DSPATR(PR)
     A            WRMOTT        15A  O  5 60COLOR(RED)
     A                                  6  3MSGCON(015 YBC1194 *LIBL/SYMSGF)
     A            WRVARE    R        B  6 20REFFLD(LLAGJR/VAVARE *LIBL/LLAGJF)
     A  41                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A                                  7  3MSGCON(015 YBC1195 *LIBL/SYMSGF)
     A            WRLLOK    R        H      REFFLD(LLAGJR/VALLOK *LIBL/LLAGJF)
     A            WRLOK1         5A  B  7 20
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A            WRLOK2        20A  B  7 26
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                  7 52MSGCON(005 YBC1196 *LIBL/SYMSGF)
     A            WRLLT          3A  B  7 58
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                  8 50MSGCON(008 YBC1197 *LIBL/SYMSGF)
     A                                  8 59MSGCON(008 YBC1198 *LIBL/SYMSGF)
     A                                  8 68MSGCON(010 YBC1199 *LIBL/SYMSGF)
     A            WROANT    R        B  9 20REFFLD(LLAGJR/VAOANT *LIBL/LLAGJF)
     A                                      DSPATR(HI)
     A  41                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  9 28MSGCON(006 YBC1201 *LIBL/SYMSGF)
     A  41        VASALD    R        O  9 35EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  9 43MSGCON(005 YBC1202 *LIBL/SYMSGF)
     A            WRVKTS    R        O  9 50REFFLD(LLAGJR/VAVKTS *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRKUBS    R        O  9 59REFFLD(LLAGJR/VAKUBS *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  9 68MSGCON(013 YBC1203 *LIBL/SYMSGF)
     A                                 10  4MSGCON(030 YBC1204 *LIBL/SYMSGF)
     A            WRSAE0    R        B 10 35REFFLD(LLAGJR/VASAE0 *LIBL/LLAGJF)
     A                                      DSPATR(HI)
     A  41N46                               DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 10 43MSGCON(005 YBC1206 *LIBL/SYMSGF)
     A            WRVKT0    R        O 10 50REFFLD(LLAGJR/VAVKT0 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRKUB0    R        O 10 59REFFLD(LLAGJR/VAKUB0 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 11 10MSGCON(024 YBC1205 *LIBL/SYMSGF)
     A            WRSAE1    R        B 11 35REFFLD(LLAGJR/VASAE1 *LIBL/LLAGJF)
     A                                      DSPATR(HI)
     A  41N46                               DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 11 43MSGCON(005 YBC1206 *LIBL/SYMSGF)
     A            WRVKT1    R        O 11 50REFFLD(LLAGJR/VAVKT1 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRKUB1    R        O 11 59REFFLD(LLAGJR/VAKUB1 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRAN01    R        O 11 68REFFLD(LLAGJR/VAAN01 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRAN00    R        O 11 74REFFLD(LLAGJR/VAAN01 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 12 10MSGCON(024 YBC1207 *LIBL/SYMSGF)
     A            WRSAE2    R        B 12 35REFFLD(LLAGJR/VASAE2 *LIBL/LLAGJF)
     A                                      DSPATR(HI)
     A  41N46                               DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 12 43MSGCON(005 YBC1206 *LIBL/SYMSGF)
     A            WRVKT2    R        O 12 50REFFLD(LLAGJR/VAVKT2 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRKUB2    R        O 12 59REFFLD(LLAGJR/VAKUB2 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRAN12    R        O 12 68REFFLD(LLAGJR/VAAN12 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRAN02    R        O 12 74REFFLD(LLAGJR/VAAN02 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 13 10MSGCON(024 YBC1208 *LIBL/SYMSGF)
     A            WRSAE3    R        B 13 35REFFLD(LLAGJR/VASAE3 *LIBL/LLAGJF)
     A                                      DSPATR(HI)
     A  41N46                               DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 13 43MSGCON(005 YBC1206 *LIBL/SYMSGF)
     A            WRVKT3    R        O 13 50REFFLD(LLAGJR/VAVKT3 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRKUB3    R        O 13 59REFFLD(LLAGJR/VAKUB3 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRAN23    R        O 13 68REFFLD(LLAGJR/VAAN23 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WRAN03    R        O 13 74REFFLD(LLAGJR/VAAN03 *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 15MSGCON(019 YBC1209 *LIBL/SYMSGF)
     A            WRMIN     R        O 14 35REFFLD(LLAGJR/VAMIN *LIBL/LLAGJF)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 43MSGCON(015 YBC1210 *LIBL/SYMSGF)
     A            WRDTV     R        B 14 59REFFLD(LLAGJR/VADTV *LIBL/LLAGJF)
     A  41                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 16  3MSGCON(015 YBC1211 *LIBL/SYMSGF)
     A            WRVERD    R        B 16 20REFFLD(LLAGJR/VAVERD *LIBL/LLAGJF)
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16 31'Valuta:'
     A            WRVAL          3A  B 16 39
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                 16 43'Salgspris:'
     A            WRSPRI    R        B 16 57REFFLD(LLAGJR/VASPRI *LIBL/LLAGJF)
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17  3MSGCON(015 YBC1212 *LIBL/SYMSGF)
     A            WRULDT    R        B 17 20REFFLD(LLAGJR/VAULDT *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  41                                  DSPATR(PR)
     A                                 17 31'Pro.dato:'
     A            WRPRDT    R        B 17 41REFFLD(LLAGJR/VAPRDT *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  41                                  DSPATR(PR)
     A                                 17 51'Pallid:'
     A            WRPLID        20A  B 17 59
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                 18  3MSGCON(015 YBC1213 *LIBL/SYMSGF)
     A            WRSENR    R        B 18 20REFFLD(LLAGJR/VASENR *LIBL/LLAGJF)
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                 18 61MSGCON(018 YBC1214 *LIBL/SYMSGF)
     A  41                             19  3MSGCON(025 YBC1215 *LIBL/SYMSGF)
     A                                 19 29MSGCON(009 YBC1216 *LIBL/SYMSGF)
     A            WRAVD          4Y 0B 19 39EDTCDE(4)
     A  41                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A  41                             19 61MSGCON(007 YBC1217 *LIBL/SYMSGF)
     A  41        VABUSE    R        O 19 69COLOR(WHT)
     A                                 20  3'Mottatte stykk:'
     A            WRANKT    R        B 20 20REFFLD(LLAGJR/VAANKT *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  41                                  DSPATR(PR)
     A                                 20 29MSGCON(009 YBC1218 *LIBL/SYMSGF)
     A            WROPD          7Y 0B 20 39EDTCDE(4)
     A  41                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A  41                             20 61MSGCON(007 YBC1219 *LIBL/SYMSGF)
     A  41        VABDAT    R        O 20 69EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                 21 29MSGCON(009 YBC1220 *LIBL/SYMSGF)
     A            WRFBNR         3Y 0B 21 39EDTCDE(4)
     A  41                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A  41                             21 61MSGCON(007 YBC1221 *LIBL/SYMSGF)
     A  41        VABKL     R        O 21 69EDTWRD('  :  :  ')
     A                                      COLOR(WHT)
     A N41N45                          22  3MSGCON(025 YBC1222 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  41N45                          22  3MSGCON(025 YBC1223 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22 61MSGCON(007 YBC1224 *LIBL/SYMSGF)
     A            WRDATO    R        O 22 69REFFLD(LLAGJR/VADATO *LIBL/LLAGJF)
     A                                      EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                 23  3MSGCON(025 YBC1225 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N41
     AO 41 46                          23 30MSGCON(007 YBC1226 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  39                             23 39MSGCON(023 YBC1228 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                 23 65MSGCON(015 TRO0424 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  3MSGCON(015 YBC1200 *LIBL/SYMSGF)
     A                                 15  3'Frakt utenf.EU:'
     A            WRFRIE         9Y 4B 15 57
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15 31'Valuta:'
     A            WRFVIE         3A  B 15 76
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                 15 43'Innenfor EU:'
     A            WRFRUE         9Y 4B 15 20
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            WRFVUE         3A  B 15 39
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                 15 68'Valuta:'
     A            WROPLK         2A  B  6 76
     A  41N46                               DSPATR(PR)
     A N46                                  COLOR(WHT)
     A                                  6 68'Oppr.LK'
     A                                 22 43'RecordNo'
     A            VAMN      R        O 22 52EDTCDE(Z)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      SLNO(22)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(25 YBC1225 SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           1 27MSGCON(010 TRO0422 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 39MSGCON(09 YBC1227 SYMSGF)
     A                                      COLOR(BLU)
     A N28                              1 49MSGCON(010 TRO0432 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  42N43                           1 61MSGCON(13 YBC1229 SYMSGF)
     A                                      COLOR(BLU)
     A N42N43                           1 61MSGCON(13 YBC1230 SYMSGF)
     A                                      COLOR(BLU)
     A  42 43                           1 61MSGCON(13 YBC1231 SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           2  2'F6=Lag ny   F16=Lag/velg Mot.rap. -
     A                                        F11=Utskrift Mot.rap.'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NOREC
     A                                      CF03(03 'Avslutt')
     A                                      CA12(12 'Cancel')
     A                                      CA05(05 'FORNY')
     A N27N28                               CF06(06 'Lag ny')
     A N27N28                               CF16(16 'Lag ny heading')
     A                                      CF07(07 'ART/VARESLAG')
     A N28                                  CF08(08 'Søk')
     A                                      OVERLAY
     A                                      PUTOVR
     A          H                           HLPPNLGRP(LL020030 LL020PNL)
     A                                      HLPARA(01 001 03 080)
     A          H                           HLPPNLGRP(LL020040 LL020PNL)
     A                                      HLPARA(04 001 21 080)
     A          H                           HLPPNLGRP(LL020050 LL020PNL)
     A                                      HLPARA(22 001 24 080)
     A                                  1  2'SYSPED/LL020 '
     A                                      COLOR(BLU)
     A N27N28                           1 20MSGCON(40 YBC1174 SYMSGF)
     A                                      COLOR(WHT)
     A  28
     AO 27                              1 20MSGCON(40 YBC1175 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(14 YBC1176 SYMSGF)
     A            VAGRUP    R        O  2 17COLOR(WHT)
     A                                      EDTCDE(4)
     A            NVNGRU        30A  O  2 27COLOR(WHT)
     A                                  2 72TIME
     A                                  3  2MSGCON(14 YBC1177 SYMSGF)
     A            VAKJED    R        O  3 17COLOR(WHT)
     A                                      EDTCDE(4)
     A            NVNKJE        30A  O  3 27COLOR(WHT)
     A                                  3 57MSGCON(21 YBC1182 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           5  4MSGCON(007 TRO0007 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           5 14MSGCON(008 TRO0009 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 25MSGCON(008 TRO0010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           5 57MSGCON(11 YBC1184 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 69MSGCON(08 YBC1185 SYMSGF)
     A                                      COLOR(BLU)
     A N42N43                           6  2MSGCON(78 YBC1186 SYMSGF)
     A                                      COLOR(WHT)
     A  42N43                           6  2MSGCON(78 YBC1187 SYMSGF)
     A                                      COLOR(WHT)
     A  42 43                           6  2MSGCON(78 YBC1188 SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  3MSGCON(75 YBC1232 SYMSGF)
     A N27N28                          22  2'F6=Lag ny   F16=Lag/velg Mot.rap.'
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(25 YBC1225 SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                          23 27MSGCON(010 TRO0422 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N28                             23 49MSGCON(010 TRO0432 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  42N43                          23 61MSGCON(13 YBC1229 SYMSGF)
     A                                      COLOR(BLU)
     A N42N43                          23 61MSGCON(13 YBC1230 SYMSGF)
     A                                      COLOR(BLU)
     A  42 43                          23 61MSGCON(13 YBC1231 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R ALARM
     A                                      WINDOW(7 25 4 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2MSGCON(28 YBC1233 SYMSGF)
     A                                      COLOR(YLW)
     A                                  2  2MSGCON(28 YBC1234 SYMSGF)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(28 YBC1235 SYMSGF)
     A            WSSVAR         1A  B  4 10VALUES('J' 'N')
     A                                  4 15'(J/N)'
      *
     A          R ALARM2
     A                                      WINDOW(7 10 4 60 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2MSGCON(58 YBC1236 SYMSGF)
     A                                      COLOR(YLW)
     A                                  2  2MSGCON(58 YBC1237 SYMSGF)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(58 YBC1238 SYMSGF)
     A                                      COLOR(RED)
     A                                  4  2MSGCON(58 YBC1239 SYMSGF)
     A                                      COLOR(RED)
      *
     A          R MOTRAP
     A                                      WINDOW(7 30 4 30 *NOMSGLIN)
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                  1  2'Utskrift av mottaksrapport'
     A                                      COLOR(YLW)
     A                                  2  2'Mot.rap.nr:'
     A            WRMRNR         5Y 0O  2 14DSPATR(HI)
     A N90                              3  2'Prøveutsk.:'
     A  90                              3  2'Kopiutskrift'
     A                                      COLOR(RED)
     A N90        WRPRØV         1A  B  3 14DSPATR(UL)
     A                                      VALUES('J' 'N')
     A N90                              3 17'(J/N)'
     A                                  4  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R CHGST2
     A                                      WINDOW(*DFT 4 55 *NOMSGLIN)
     A                                      CF04(04 'Spørr')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                  1  2'Endre status'
     A                                      COLOR(YLW)
     A                                  2  2'Status nå:'
     A            WGST2          5A  O  2 14DSPATR(UL)
     A            WGST2N        30A  O  2 20
     A                                  3  2'Ny status:'
     A            WNST2          5A  B  3 14DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(HI)
     A            WNST2N        30A  O  3 20
     A N90                              4  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  90        MSGTX2        50A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
