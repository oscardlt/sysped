********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10004 * 01 YBC PTF10 LEVERANDØRSNR ER ØKT TIL 5.0
10027 * 02 YBC PTF10 NYTT FELT FOR TYPE AV ORDRE
********************************************************************
      *__________________________
      * ARBEIDSFIL FOR MANIFEST
      *""""""""""""""""""""""""""
     A          R GSS022WR
     A            ORONO          7S 0       COLHDG('ORDRENR.')
     A            ORST           1A         COLHDG('STATUS')
      *             BLANK = PÅ LAGER
      *             1     = MED PACKINGLISTE
      *             2     = MED BEKREFTET PACKINGLISTE
      *             3     = MED PROFORMA
      *             4     = MED BEKREFTET PROFORMA
      *             5     = MED FAKTURAGRUNNLAG
      *             6     = MED BEKREFTET FAKTURAGRUNNLAG
      *             S     = SLETTET
N02  A            ORTYPE         1A         COLHDG('ORDRETYPE')
     A            ORTNO         11S 0       COLHDG('TURNR. AVD/OPD')
     A            ORTNO2        11S 0       COLHDG('TURNR. AVD/OPD')
     A            ORTNO3        11S 0       COLHDG('TURNR. AVD/OPD')
     A            ORCNO          8S 0       COLHDG('KUNDENR.')
S01  A*           ORSNO          3S 0       COLHDG('LEVERANDØRSNR.')
N01  A            ORSNO          5S 0       COLHDG('LEVERANDØRSNR.')
     A            ORSNM         30A         COLHDG('LEV.NAVN')
     A            ORBCD          5A         COLHDG('BÅTKODE')
     A            ORCLI          5P 0       COLHDG('ANTALL KOLLI')
     A            ORWT           7P 1       COLHDG('VEKT - KG')
     A            ORVOL          5P 2       COLHDG('VOLUM - M3')
     A            ORDSC         30A         COLHDG('VAREBESKRIVELSE')
     A            ORCURC         3A         COLHDG('VALUTAKODE')
     A            ORAMT         11P 2       COLHDG('BELØP')
     A            ORCLNF         3P 0       COLHDG('KOLLINR. FRA')
     A            ORCLNT         3P 0       COLHDG('KOLLINR. TIL')
     A            ORDTWL         8S 0       COLHDG('ØNSKET LEV.DATO')
     A            ORDTL          8S 0       COLHDG('DATO LEVERT')
     A            ORDTL2         8S 0       COLHDG('DATO LEVERT')
     A            ORDTL3         8S 0       COLHDG('DATO LEVERT')
     A            ORLC           5A         COLHDG('LOKASJON LAND/STD')
     A            ORLC2          5A         COLHDG('LOK. LAND/STD 2')
     A            ORLC3          5A         COLHDG('LOK. LAND/STD 3')
     A            ORPOS1         5A         COLHDG('LAGERPOSISJON 1')
     A            ORPOS2         5A         COLHDG('LAGERPOSISJON 2')
     A            ORPOS3         5A         COLHDG('LAGERPOSISJON 3')
     A            ORFTCD         2A         COLHDG('FRITEKST KODE')
     A            ORFT1         30A         COLHDG('FRITEKST 1')
     A            ORFT2         30A         COLHDG('FRITEKST 2')
     A            ORFT3         30A         COLHDG('FRITEKST 3')
     A            ORFT4         30A         COLHDG('FRITEKST 4')
     A            OROT           2A         COLHDG('OPPDRAGSTYPE')
     A            ORFR           3A         COLHDG('FRANKATUR')
     A            ORDTR          8S 0       COLHDG('REG.DATO')
     A          K ORBCD
     A          K ORCLNF
     A          K ORCLNT
