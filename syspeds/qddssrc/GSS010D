********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10004 * 01 YBC PTF10 LEVERANDØRSNR ER ØKT TIL 5.0
      *              INGEN MERKING
      * 02 SH  PTF10 INNFØRT NY DANGEROUS GOODS
10XXX * 03 YBC PTF10 RETT FEIL I BILDE01
10XXX * 04 YBC PTF10 NY OPSJON FOR SIMULERING AV UTLEGG
10027 * 05 YBC PTF10 VIS ORDRETYPE
10XXX * 06 YBC PTF10 OPSJON 8 OG 9 BRUKES IKKE FJERNES
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 07 YBC PTF11 SØK PÅ ORDRE PÅ OPPRINNELIG LOKASJON
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      ALTHELP
     A                                      HELP
      *
     A          R GSS010DX
     A                                      CF04(04 'Søking')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SHIPSYS/GSS010D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with orders'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  4  2'Status . . . . . . . .:'
     A            WSST           1A  B  4 28DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      VALUES('*' '0' '1' '2' '3' '4' '5' -
     A                                      '6' 'D' 'P' 'X' 'I' 'S' 'E' 'B')
     A                                  4 40'* = All status'
     A                                  4 60'X = In work'
     A                                  5 40'0 = On stock'
     A                                  6 40'1 = Packinglist'
     A                                  6 60'2 = Confirmed pack.'
     A                                  7 40'3 = Proforma'
     A                                  7 60'4 = Confirmed prof.'
     A                                  8 40'5 = Invioce basis'
     A                                  8 60'6 = Confirmed inv.b.'
     A                                  9 40'D = Deleted'
     A                                  9 60'P = Pending'
     A                                 11  2'Vessel . . . . . . . .:'
     A            WSBCD          5A  B 11 28DSPATR(UL)
     A                                 11 35'Port'
     A            WSPORT         5   B 11 40
     A                                 12  2'Location . . . . . . .:'
     A            WSLC           5A  B 12 28DSPATR(UL)
     A                                 13  2'Clients order No.  . .:'
     A            WSCONO        20A  B 13 28DSPATR(UL)
     A                                 14  2'Clients No.  . . . . .:'
     A            WSCNO          8Y 0B 14 28DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15  2'Supplier No. . . . . .:'
     A            WSSNO          5Y 0B 15 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 16  2'Date delivered . . . .:'
     A            WSDTL          6Y 0B 16 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 16 40'(DDMMYY)'
     A                                 17  2'Order No.  . . . . . .:'
     A            WSONO          7Y 0B 17 28DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 18  2'Supplier name:'
     A            WSSNMS        30A  B 18 18DSPATR(UL)
     A                                 18 50'Date F/T:'
     A            WSDTLF         6Y 0B 18 60DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 18 67'/'
     A            WSDTLT         6Y 0B 18 69DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 19  2'Return goods/Strategic:'
     A            WSRTNG         1A  B 19 28DSPATR(UL)
     A                                      VALUES('R' 'S' 'N' ' ')
     A                                 19 40'(R/S/N)'
     A                                 20  2'Reference  . . . . . .:'
     A            WSKD           5A  B 20 28DSPATR(UL)
     A            WSREF         35A  B 20 34DSPATR(UL)
N07  A                                 21  2'Origin location. . . .:'
N07  A            WSLCOR         5A  B 21 28DSPATR(UL)
N07  A                                 21 35'From date:'
N07  A            WSDTLOR        6Y 0B 21 46DSPATR(UL)
N07  A                                      EDTCDE(4)
N07  A                                 21 52'(DDMMYY)'
N07  A                                 21 62'Location:'
N07  A            WSLCOR2        5A  B 21 72DSPATR(UL)
     A                                 23  2'F3=End  F4=Query'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R GSS010DA                  SFL
S06  A*           WSNR           1A  B  7  2VALUES(' ' '1' '2' '3' '4' '5' '8' -
S06  A*                                     '9' 'P' 'C' 'N' 'L' 'I' 'S' 'E' -
S02  A*                                     'T' 'A' 'B' 'Q' 'U')
S04  A*                                     'T' 'A' 'B' 'Q' 'U' 'D')
S06  A*                                     'T' 'A' 'B' 'Q' 'U' 'D' 'G')
N06  A            WSNR           1A  B  7  2VALUES(' ' '1' '2' '3' '4' '5' -
N06  A                                      'P' 'C' 'N' 'L' 'I' 'S' 'E' -
N06  A                                      'T' 'A' 'B' 'Q' 'U' 'D' 'G')
     A            ORONO          7Y 0O  7  4EDTCDE(4)
     A  50                                  DSPATR(RI)
     A  56
     AO 46                                  DSPATR(HI)
S02  A* 27                                  COLOR(RED)
N02  A  28
N02  AO 27                                  COLOR(RED)
S02  A* 28                                  COLOR(YLW)
N02  A  29                                  COLOR(YLW)
     A            ORST           1A  O  7 12
     A  41                                  COLOR(WHT)
     A            ORTNO         11Y 0O  7 14EDTCDE(4)
     A            ORCNO          8Y 0O  7 26EDTCDE(4)
     A  43                                  COLOR(WHT)
     A            ORSNO          5Y 0O  7 35EDTCDE(4)
     A  44                                  COLOR(WHT)
     A            ORBCD          5A  O  7 41
     A  42                                  COLOR(WHT)
N04  A  39                                  COLOR(YLW)
     A            ORDTL          8Y 0H
     A            ORTNO2        11Y 0H
     A            ORTNO3        11Y 0H
     A            WXDTL          6Y 0O  7 47EDTCDE(4)
     A  45                                  COLOR(WHT)
     A            ORLC           5A  O  7 54
     A  47                                  COLOR(WHT)
     A            POCONO        20A  H
     A            XPOCONO       19A  O  7 60
     A  48                                  COLOR(WHT)
     A            PAPKD          1A  O  7 80COLOR(YLW)
     A            ORRTNG         1A  O  8  3DSPATR(UL)
     A            ORPOS1         5A  O  8  5DSPATR(UL)
     A            WXSNM         10A  O  8 11DSPATR(UL)
     A  49
     AO 59                                  COLOR(WHT)
     A  35                                  COLOR(YLW)
     A            ORCLI          5Y 0O  8 22EDTCDE(4)
     A                                      DSPATR(UL)
     A            ORWT           7Y 1O  8 28EDTCDE(4)
     A                                      DSPATR(UL)
     A            ORVOL          5Y 2O  8 37EDTCDE(4)
     A                                      DSPATR(UL)
S05  A*           WXDSC         18A  O  8 44DSPATR(UL)
N05  A            WXDSC         16A  O  8 44DSPATR(UL)
     A  40                                  COLOR(WHT)
     A  29                                  COLOR(YLW)
N02  A  28                                  COLOR(RED)
E05  A            ORAMT         11Y 2O  8 61DSPATR(UL)
     A                                      EDTCDE(4)
E05  A            ORCURC         3A  O  8 74DSPATR(UL)
N05  A            ORTYPE         1A  O  8 78DSPATR(UL)
N05  A                                      COLOR(YLW)
      *
     A          R GSS010DB                  SFLCTL(GSS010DA)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE EN NY')
     A                                      CF08(08 'SØK')
N04  A                                      CF10(10 'TIL PRØVE-FAKTURA')
     A                                      CF12(12 'SØK')
     A                                      CF23(23 'MORE OPTIONS')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                  1  2'SHIPSYS/GSS010D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with orders'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A  41                              2  2'Status . . . . .:'
     A  41        WSST           1A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A  42
     AO 52                              2  2'Vessel . . . . .:'
     A  42
     AO 52        WSBCD          5A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A  42
     AO 52        CBNAB         30A  O  2 26DSPATR(UL)
     A                                      COLOR(WHT)
     A  43
     AO 53                              2  2'Clients No.  . .:'
     A  43
     AO 53        WSCNO          8Y 0O  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  43
     AO 53        KNAVN         30A  O  2 29DSPATR(UL)
     A                                      COLOR(WHT)
     A  44
     AO 54                              2  2'Supplier No. . .:'
     A  44
     AO 54        WSSNO          5Y 0O  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  44
     AO 54        WSSNM         30A  O  2 26DSPATR(UL)
     A                                      COLOR(WHT)
     A  45
     AO 55                              2  2'Date delivered .:'
     A  45
     AO 55        WSDTL          6Y 0O  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  46
     AO 56                              2  2'Order No.  . . .:'
     A  46
     AO 56        WSONO          7Y 0O  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  47
     AO 57                              2  2'Location   . . .:'
     A  47
     AO 57        WSLC           5A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A  48                              2  2'Clients order No:'
     A  48        WSCONO        20A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A  42
     AO 52                              2 60'Tot. weight:'
     A  42
     AO 52        WSWT           7Y 1O  2 73DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
     A N95                              3  2'Options:  1=Crt 2=Chg 3=Cpy 4=Dlt -
N06  A                                      5=Dsp P=Okp C=Cmp N=No cmp L=Lok'
S06  A*                                     5=Dsp 8=Rls 9=Pck P=Okp C=Cmp N=No -
S06  A*                                     cmp L=Lok'
     A                                      COLOR(BLU)
     A  95                              3  2'Options:  I=Inv. S=Sysped order -
     A                                       E=Email order A=Arch. B=Barc -
     A                                       Q=ISO D=DG'
     A                                      COLOR(BLU)
     A                                  4  2'Op Ord.No St Trip No... Clien.No S-
     A                                      upNo Boatc D.date Locat Clients ord-
     A                                      er No.  P'
     A                                      COLOR(WHT)
     A N40                              5  2'   Pos.1 Supplier   Colli Weight  -
N05  A                                       Volume Goods descriptio Values    -
N05  A                                         Cur T'
S05  A*                                      Volume Goods description  Values  -
S05  A*                                          Cur'
     A                                      COLOR(WHT)
     A  40                              5  2'   Pos.1 Supplier   Colli Weight  -
N05  A                                       Volume References       Values    -
N05  A                                         Cur T'
S05  A*                                      Volume References         Values  -
S05  A*                                          Cur'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  6  2DSPATR(UL)
     A            W2ONO          7Y 0B  6  4DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2ST           1A  B  6 12DSPATR(UL)
     A                                      VALUES('*' '0' '1' '2' '3' '4' '5' -
     A                                      '6' 'D' 'P')
     A            W2CNO          8Y 0B  6 26DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SNO          5Y 0B  6 35DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2BCD          5A  B  6 41DSPATR(UL)
     A            W2DTL          6Y 0B  6 47DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2LC           5A  B  6 54DSPATR(UL)
     A            W2CONO        20A  B  6 60DSPATR(UL)
      *
     A          R GSS010DC
     A                                      SLNO(23)
     A                                  1  2'F3=End   F5=Refresh   F6=Create   -
     A                                      F8=Search  F23=More options'
     A                                      COLOR(BLU)
N04  A                                  1 65'F10=Inq. price'
N04  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS010DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE EN NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'SØK')
     A                                      CF23(23 'more options')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS010D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with orders'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A  41                              2  2'Status . . . . .:'
     A  41        WSST           1A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A  42
     AO 52                              2  2'Vessel . . . . .:'
     A  42
     AO 52        WSBCD          5A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A  42
     AO 52        CBNAB         30   O  2 26DSPATR(UL)
     A                                      COLOR(WHT)
     A  43
     AO 53                              2  2'Clients No.  . .:'
     A  43
     AO 53        WSCNO          8Y 0O  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  43
     AO 53        KNAVN         30   O  2 29DSPATR(UL)
     A                                      COLOR(WHT)
     A  44
     AO 54                              2  2'Supplier No. . .:'
     A  44
     AO 54        WSSNO          5Y 0O  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  44
     AO 54        WSSNM         30   O  2 26DSPATR(UL)
     A                                      COLOR(WHT)
     A  45
     AO 55                              2  2'Date delivered .:'
     A  45
     AO 55        WSDTL          6Y 0O  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  46
     AO 56                              2  2'Order No.  . . .:'
     A  46
     AO 56        WSONO          7Y 0O  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  47
     AO 57                              2  2'Locartion  . . .:'
     A  47
     AO 57        WSLC           5A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A  48                              2  2'Clients order No:'
     A  48        WSCONO        20A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'Options:  1=Crt 2=Chg 3=Cpy 4=Dlt -
N06  A                                      5=Dsp P=Okp C=Cmp N=No cmp L=Lok'
S06  A*                                     5=Dsp 8=Rls 9=Pck P=Okp C=Cmp N=No -
S06  A*                                     cmp L=Lok'
     A                                      COLOR(BLU)
     A                                  4  2'Op Ord.No St Trip No... Clien.No S-
     A                                      upNo Boatc D.date Locat Clients ord-
     A                                      er No.   '
     A                                      COLOR(WHT)
     A N40                              5  2'   Pos.1 Supplier   Colli Weight  -
     A                                       Volume Goods description  Values  -
     A                                           Cur'
     A                                      COLOR(WHT)
     A  40                              5  2'   Pos.1 Supplier   Colli Weight  -
     A                                       Volume References         Values  -
     A                                           Cur'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  6  2DSPATR(UL)
     A            W2ONO          7Y 0B  6  4DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2ST           1A  B  6 12DSPATR(UL)
     A                                      VALUES('*' '0' '1' '2' '3' '4' 'D' -
     A                                      'P' '5' '6')
     A            W2CNO          8Y 0B  6 26DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SNO          5Y 0B  6 35DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2BCD          5A  B  6 41DSPATR(UL)
     A            W2DTL          6Y 0B  6 47DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2LC           5A  B  6 54DSPATR(UL)
     A            W2CONO        20A  B  6 60DSPATR(UL)
     A                                  8  2'No order found'
     A                                      COLOR(RED)
     A                                 23  2'F3=End   F5=Refresh   F6=Create   -
     A                                      F8=Search'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A* MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
S03  A*         R WINDOWSZ
S03  A*                                     OVERLAY
S03  A*                                     WINDOW(7 25 5 30 *NOMSGLIN)
      *
     A          R BILDE01
S03  A*                                     WINDOW(WINDOWSZ)
N03  A                                      WINDOW(7 25 5 30 *NOMSGLIN)
     A                                      CF12(12 'tilbake')
     A                                      OVERLAY
     A                                  1  2'1=select order subs(B,D,G)  '
     A                                      COLOR(WHT)
     A            SYVEL1         1A  B  2  2
     A            SYDEL1         1A  B  2  5VALUES(' ' 'B' 'D' 'G')
     A            SYDEN1         1A  O  2  8
     A            SYAVD1         4S 0O  2 10
     A            SYORD1         7S 0O  2 16
     A            SYVEL2         1A  B  3  2
     A            SYDEL2         1A  B  3  5VALUES(' ' 'B' 'D' 'G')
     A            SYDEN2         1A  O  3  8
     A            SYAVD2         4S 0O  3 10
     A            SYORD2         7S 0O  3 16
     A            SYVEL3         1A  B  4  2
     A            SYDEL3         1A  B  4  5VALUES(' ' 'B' 'D' 'G')
     A            SYDEN3         1A  O  4  8
     A            SYAVD3         4S 0O  4 10
     A            SYORD3         7S 0O  4 16
     A                                  5  2'F12=return'
     A                                      COLOR(BLU)
      *
     A          R SLETT
     A                                      WINDOW(*DFT 6 40 *NOMSGLIN)
     A                                      CF23(23 'BEKREFT')
     A                                      OVERLAY
     A                                  2  2'F23 = CONFIRM DELETION'
     A                                      COLOR(RED)
     A                                      DSPATR(RI)
     A                                  3  2'ORDERNO:'
     A                                      COLOR(RED)
     A            ORONO          7S 0O  3 11DSPATR(UL)
     A                                      COLOR(RED)
     A                                  4  2'TRIPNO.:'
     A                                      COLOR(RED)
     A            ORTNO         11S 0O  4 11DSPATR(UL)
     A                                      COLOR(RED)
     A                                  5  2'F23=DELETE ORDER'
     A                                      COLOR(BLU)
