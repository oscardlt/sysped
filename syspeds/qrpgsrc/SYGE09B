      *
      ***********************************************************
      *** PROGRAM FOR BEREGNING AV MODULUS 11    MAX LENGDE 21 TEGN
      *** PARAMETER KID MÅ HA HØYRESTILLT VERDI
      *** MED FORANSTÅENDE NULLER
      *****************************************************************
      *
     D A21             S              2  0 DIM(21)
     D WA21            S              2    DIM(21)
     D S21             S              1  0 DIM(42)
     D WS21            S              1    DIM(42)
     D T21             S              1  0 DIM(21) CTDATA PERRCD(21)
      *
     D                 DS
     D  WK21                   1     21  0
     D                                     DIM(21)
     D  UK21                   1     21
      *
     C     *ENTRY        PLIST
     C                   PARM                    UKID             21
     C                   PARM                    UMOD              1 0
     C*
     C     UKID          IFNE      *BLANKS
     C                   EXSR      MODSR
     C                   END
     C                   SETON                                        LR
     C                   RETURN
     C*
     C****************************************************************    D150
     C*       MODSR     -    UTREGNING AV MOD-11 KONTROLLFELT             D150
     C****************************************************************    D150
     C     MODSR         BEGSR                                                                 D150
     C*---------------------------------------------------------------    D150
     C                   MOVEL     UKID          UK21
     C     T21           MULT      WK21          A21                                           D150
     C                   MOVE      A21           WA21                                          D150
     C     1             DO        21            XN                3 0
     C                   ADD       A21(XN)       XBAK              5 0
     C                   ENDDO                                                                 D150
     C     XBAK          DIV       11            XBAK2             5 0                         D150
     C                   MVR                     XBAK3             3 0                         D150
     C                   SELECT
     C     XBAK3         WHENEQ    0
     C                   MOVE      0             UMOD                                          D150
     C     XBAK3         WHENEQ    1
     C                   Z-ADD     -1            UMOD                                          D150
     C                   OTHER
     C     11            SUB       XBAK3         UMOD                                          D150
     C                   ENDSL
     C                   ENDSR                                                                 D150
     C*
      *
**
432765432765432765432
