********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12338 * 01 CB  PTF12 Stoppet på grunn av feil variabelbruk.
      ***********************************************************
      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE182                   ***
      *** Release for transit                                       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH05  UF   E           K DISK
     FTVINF     O  A E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM2     IF   E           K DISK    RENAME(EDIMR:EDIMR2)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104rb
     d ncts104rb       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104rb       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d x_thtrnr        s                   like(thtrnr)
     d x_indt          s              8
     d x_intm          s              6
     d x_indttm        s             19
     d x_avgstd        s              8
     d x_incstd        s              8
     d x_IE182         s              1a
     d wdt             s              8
     d wtm             s              6
     d                 ds
     d x_txt                   1    512
     d x_tx                    1    490    dim(7)
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
      *----------------------------------------------------------------------
     IEDIMR2
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC182C';
             x_ie182 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'CustomsOfficeOfDeparture';
             exsr srxml2;
           when xrnv = 'CustomsOfficeOfIncidentRegistration';
             exsr srxml3;
           when xrnv = 'Consignment';
             exsr srxml4;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie182 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
           when xrnv = 'incidentNotificationDateAndTime';
             x_indttm = xrverd;
             x_indt = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
             x_intm = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                   + %subst(xrverd:18:2);
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_avgstd = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_incstd = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;
         x_txt = *blanks;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'Incident';
             exsr srxml41;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         f0077 = 'INF';
         ft(1) = 'Incident notification date and time: '
                + %subst(x_indttm:1:10) + '  ' + %subst(x_indttm:12:8);
         ft(2) = 'Customs office of departure: ' + %trim(x_avgstd);
         ft(3) = 'Customs office of incident registration: ' + %trim(x_incstd);
         ft(4) = %subst(x_txt:1:70);
         ft(5) = %subst(x_txt:71:70);
         write tvinff;

       endsr;

       //**********************************************
       begsr srxml41;
       //**********************************************

         x3_xrlev1 = xrlev1;
         x3_xrlev2 = xrlev2;
         x3_xrlev3 = 1;
         x3_xrlev4 = 0;

         chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'sequenceNumber';
           when xrnv = 'code';
           when xrnv = 'text';
             x_txt = xrverd;
             leave;
           when xrnv = 'Endorsement';
             //exsr srxml411;
           when xrnv = 'Location';
             //exsr srxml412;
           when xrnv = 'TransportEquipment';
             //exsr srxml413;
           when xrnv = 'Transhipment';
             //exsr srxml414;
           endsl;

           x3_xrlev3 = x3_xrlev3 + 1;
           chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

//S01  //chain thtrnr nctseh05;
N01      chain x_thtrnr nctseh05;
         if %found;
           thst182 = 'J';
           update nctsehr;
         endif;

//S01  //setll (thavd: thtdn: 999999999) edim2;
N01      setll (thavd: thtdn: 99999999) edim2;
         readpe (thavd: thtdn) edim2;

         chain 1 edic;
         cmn = cmn + 1;
         update edicr;
         mmn = cmn;
         m0065  = 'XMLDEC';
         m0068  = x0068;
         m1001  = 'TET';
         m1225  = '182';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '182';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = '182';
         ttdate = %float(x_indt);
         tttime = %float(x_intm);
         ttdatl = %float(wdt);
         tttiml = %float(wtm);
         ttuser = zuser;
         tttext = 'Forwarded Incident Notification To ED: '
                  + %trim(x_indttm)+ ' '  + %trim(thtrnr);
         tttexl = 'Videresendt hendelsesmelding til ED: '
                  + %trim(x_indttm)+ ' '  + %trim(thtrnr);
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
