     A*%%TS  SD  20150217  105349  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261A* 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
     A*        BRINGTMS-PROSJEKT
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYTR88DPNL)
     A                                      HLPTITLE('OMDISPONERING AV OPPDRAG -
     A                                      (TRANSPORTMODULEN)')
     A                                      ALTHELP
     A                                      HELP
     A          R SUBFIL                    SFL
     A            DITUAV         4Y 0H
     A            DITUR          8Y 0H
     A            TURUND         8Y 0H
     A            WSNR           1A  I  8  2VALUES(' ' '1' '5')
     A  55                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
     A  98                                  DSPATR(RI)
     A            IMCO           1   O  9  4
     A                                      COLOR(RED)
     A            HEAVD          4Y 0O  8  6EDTCDE(4)
     A            HEOPD          7Y 0O  8 11EDTCDE(4)
     A            HENAS         30A  O  8 19
     A  55                                  DSPATR(RI)
     A            HENAK         30A  O  8 50
     A            HESDF          5A  O  9  6
     A            WRSDFD         6S 0O  9 12
     A            TRSTAF         1A  O  9 19DSPATR(HI)
     A            HESDT          5A  O  9 21
     A            WRSDTD         6Y 0O  9 27
     A            TRSTAE         1A  O  9 34DSPATR(HI)
     A            HENT4          4Y 0O  9 36EDTCDE(4)
     A            HEVS10        10A  O  9 41
     A            HEVKTW         5Y 0O  9 52EDTCDE(4)
     A            WSM3SU         5Y 2O  9 58EDTCDE(4)
     A            HELM           4Y 2O  9 65EDTCDE(4)
     A            HEOT           2A  O  9 71
     A  59                                  DSPATR(RI)
     A            HEFR           3A  O  9 74
     A            ETTE           1   O  9 78COLOR(RED)
     A                                 10  1MSGCON(004 DIV4065 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            TUSTEF    R        O 10  6REFFLD(TURERR/TUSTEF *LIBL/TURER)
     A                                      DSPATR(UL)
     A            TUDAFR         6S 0O 10 12DSPATR(UL)
     A            TUSTET    R        O 10 21REFFLD(TURERR/TUSTET *LIBL/TURER)
     A                                      DSPATR(UL)
     A            TUDATI         6S 0O 10 27DSPATR(UL)
     A            TUAO      R        O 10 35REFFLD(TURERR/TUAO *LIBL/TURER)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            DISTA1         1A  O 10 41DSPATR(BL)
     A                                      DSPATR(HI)
     A            TUPRO     R        O 10 43REFFLD(TURERR/TUPRO *LIBL/TURER)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
S01  A*           TUTVKT    R        O 10 52REFFLD(TURERR/TUTVKT *LIBL/TURER)
N01  A            TUTVKT    R        H      REFFLD(TURERR/TUTVKT *LIBL/TURER)
N01  A            XTUTVKT        5  0O 10 52
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
S01  A*           TUTM3     R        O 10 58REFFLD(TURERR/TUTM3 *LIBL/TURER)
N01  A            TUTM3     R        H      REFFLD(TURERR/TUTM3 *LIBL/TURER)
N01  A            XTUTM3         5  2O 10 58
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            TUTLM     R        O 10 65REFFLD(TURERR/TUTLM *LIBL/TURER)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            HEHAWB    R        O 10 71REFFLD(HEADFR/HEHAWB *LIBL/HEADF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0004)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  3'SYSPED/SYTR88D'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(030 DIV4064 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 22MSGCON(011 DIV4023 *LIBL/SYMSGF)
     A            UPRO           8Y 0O  2 34EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 35SFLRCDNBR
     A                                      DSPATR(ND)
     A            URUND          8Y 0O  2 43EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 53MSGCON(006 DIV0683 *LIBL/SYMSGF)
     A            USDF          20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(009 DIV4021 *LIBL/SYMSGF)
     A            UBILN     R        O  3 12REFFLD(TURERR/TUBILN *LIBL/TURER)
     A                                      COLOR(WHT)
     A            ULK       R        O  3 23REFFLD(TURERR/TULK *LIBL/TURER)
     A                                      COLOR(WHT)
     A            UHENG     R        O  3 27REFFLD(TURERR/TUHENG *LIBL/TURER)
     A                                      COLOR(WHT)
     A            ULKH      R        O  3 38REFFLD(TURERR/TULKH *LIBL/TURER)
     A                                      COLOR(WHT)
     A                                  3 53MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            USDT          20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            UKNT           8Y 0O  4 12EDTCDE(4)
     A                                      COLOR(WHT)
     A            UNAT          30A  O  4 21COLOR(WHT)
     A                                  4 53MSGCON(006 DIV4022 *LIBL/SYMSGF)
     A            UDAFR          6S 0O  4 60COLOR(WHT)
     A                                  4 67'----->'
     A            UDATI          6S 0O  4 74COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(078 DIV0689 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  1MSGCON(078 DIV4020 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 36MSGCON(015 DIV2237 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A*%%TS  SD  20150217  105349  SVEIN       REL-V7R1M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF08(08 'SØK FRA NY AVD.')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSGNR          7A  P
S01  A*                                 1  2MSGCON(007 DIV4000 *LIBL/SYMSGF)
S01  A*           UTS            5Y 0O  1 10COLOR(WHT)
     A                                  1  2'Kll'
     A            UTS            7Y 0O  1  6COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 1 16MSGCON(006 DIV0694 *LIBL/SYMSGF)
S01  A*           UTVKT          5Y 0O  1 23COLOR(WHT)
     A                                  1 14'Vkt'
     A            UTVKT          9Y 0O  1 18COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           UTARA          5Y 0O  1 29COLOR(WHT)
     A            UTARA          5Y 0O  1 28COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           WSBER1         5Y 0O  1 36COLOR(WHT)
     A            WSBER1         9Y 0O  1 34COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 1 42MSGCON(006 DIV0695 *LIBL/SYMSGF)
S01  A*           ULM3           5Y 2O  1 49COLOR(WHT)
     A                                  1 44'M3'
     A            UTM3           7Y 3O  1 47COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 57'LML'
     A            UTLM           4  2O  1 61COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 67'LMF'
     A            UTLMLA         4  2O  1 71COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 77MSGCON(005 SHU1511 *LIBL/SYMSGF)
     A            UTPOEN         5  0O  1 83COLOR(WHT)
     A                                  1 90MSGCON(006 DIV0697 *LIBL/SYMSGF)
     A            UAO            5Y 0O  1 97COLOR(WHT)
     A                                      EDTCDE(4)
     A            UKVKT          9Y 0O  2 18COLOR(WHT)
     A                                      EDTCDE(4)
     A            TUBER2         9Y 0O  2 34COLOR(WHT)
     A                                      EDTCDE(4)
     A            UKAM3          7Y 3O  2 47COLOR(WHT)
     A                                      EDTCDE(4)
     A            UKALM          4Y 2O  2 61COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0701 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'Avslutt')
     A                                      CF08(08 'SØK FRA NY AVD.')
     A                                      CF12(12 'FORRIGE')
     A                                  1  3'SYSPED/SYTR88D'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(030 DIV4064 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 22MSGCON(011 DIV4023 *LIBL/SYMSGF)
     A            UPRO           8Y 0O  2 34EDTCDE(4)
     A                                      COLOR(WHT)
     A            URUND          8Y 0O  2 43EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 53MSGCON(006 DIV0683 *LIBL/SYMSGF)
     A            USDF          20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(009 DIV4021 *LIBL/SYMSGF)
     A            UBILN     R        O  3 12REFFLD(TURERR/TUBILN *LIBL/TURER)
     A                                      COLOR(WHT)
     A            ULK       R        O  3 23REFFLD(TURERR/TULK *LIBL/TURER)
     A                                      COLOR(WHT)
     A            UHENG     R        O  3 27REFFLD(TURERR/TUHENG *LIBL/TURER)
     A                                      COLOR(WHT)
     A            ULKH      R        O  3 38REFFLD(TURERR/TULKH *LIBL/TURER)
     A                                      COLOR(WHT)
     A                                  3 53MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            USDT          20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            UKNT2          8Y 0O  4 12EDTCDE(4)
     A                                      COLOR(WHT)
     A            UNAT          30A  O  4 21COLOR(WHT)
     A                                  4 53MSGCON(006 DIV4022 *LIBL/SYMSGF)
     A            UDAFR          6S 0O  4 60COLOR(WHT)
     A                                  4 67'----->'
     A            UDATI          6S 0O  4 74COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(078 DIV0689 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  1MSGCON(078 DIV4020 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S01  A*                                22  2MSGCON(015 DIV0691 *LIBL/SYMSGF)
S01  A*           UTS            5Y 0O 22 18COLOR(WHT)
N01  A                                 22  2'Kll'
N01  A            UTS            7Y 0O 22  6COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                22 26MSGCON(006 DIV0694 *LIBL/SYMSGF)
S01  A*           UTVKT          5Y 0O 22 33COLOR(WHT)
N01  A                                 22 14'Vkt'
N01  A            UTVKT          9Y 0O 22 18COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                22 41MSGCON(006 DIV0695 *LIBL/SYMSGF)
S01  A*           UTM3           5Y 2O 22 48COLOR(WHT)
N01  A                                 22 44'M3'
N01  A            UTM3           7Y 3O 22 47COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 56MSGCON(006 DIV0696 *LIBL/SYMSGF)
     A            UTLM           4Y 2O 22 63EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 22 69MSGCON(006 DIV0697 *LIBL/SYMSGF)
     A            UAO            5Y 0O 22 76COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0701 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 35MSGCON(018 DIV0700 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SØKOPD
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(24)
     A                                      CF12(12 'FORRIGE')
     A                                      CF03(12)
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            WSAVD          4Y 0B  1  2DSPATR(UL)
     A                                      DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSOPDT         2   B  1  9COLOR(YLW)
     A            WSDATO         6Y 0B  1 13EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  1 20MSGCON(040 DIV4025 *LIBL/SYMSGF)
     A            WSSTEF         5A  B  2  2COLOR(YLW)
     A            WSSTET         5A  B  2  9COLOR(YLW)
     A                                  2 20MSGCON(030 DIV3805 *LIBL/SYMSGF)
     A            WSSONF         5A  B  3  2COLOR(YLW)
     A            WSSONT         5A  B  3  9COLOR(YLW)
     A                                  3 20MSGCON(030 DIV3806 *LIBL/SYMSGF)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
