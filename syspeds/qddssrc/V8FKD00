     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A          R BILDE01
     A                                      CA03(03 'Avslutt program.')
     A                                      CF10(10 'Submit job')
     A                                  1  3'V8FKC'
     A                                  1 32'KONVERTERING AV DATA'
     A                                      DSPATR(HI)
     A                                  1 69DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 14SYSNAME
     A                                  2 69TIME
     A                                  4  4'Tast ønskede biblioteker og trykk -
     A                                      Enter for å kjøre jobben interaktiv-
     A                                      t'
     A                                      COLOR(BLU)
     A                                  5  4'eller trykk F10 for å submitte job-
     A                                      ben til QBATCH'
     A                                      COLOR(BLU)
     A                                  7 11'Fra bibliotek'
     A            FRALIB        10A  B  7 28
     A  30                                  DSPATR(RI)
     A                                      COMP(NE ' ')
     A                                      DSPATR(MDT)
     A                                  7 42'(bibliotek som inneholder data)'
     A                                  9 11'Historikkdata'
     A            HIST           1A  B  9 28
     A  31                                  DSPATR(RI)
     A                                      VALUES('J' 'N')
     A                                      DSPATR(MDT)
     A                                  9 42'(J=Historikk  N=Drift)'
     A                                 10 11'KUN SAD/TVINN'
     A            SAD            1A  B 10 28
     A  31                                  DSPATR(RI)
     A                                      VALUES('J' 'N')
     A                                      DSPATR(MDT)
     A                                 10 42'(J=SAD/TVINN  N=SYSPED)'
     A                                 11 11'Tømme filer i fra-biblioteket'
     A                                 12 11'etterhvert som de kopieres til'
     A                                 13 11'til-biblioteket'
     A            TOMME          1A  B 13 28VALUES('J' 'N')
     A                                      DSPATR(MDT)
     A                                 13 31'(J/N)'
     A                                 13 42'(Svar J ved dårlig plass på disk)'
     A                                 23  2'F3=Gå ut'
     A                                      COLOR(BLU)
     A                                 23 35'F10=Submit job'
     A                                      COLOR(BLU)
