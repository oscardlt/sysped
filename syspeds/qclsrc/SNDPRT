/*********************************************************************/
/*                                                                   */
/*  ****  STARTE "DRIVER" FOR FJERNPRINTER  ****                     */
/* (Startes fra CL STRSNDPRT, 1 job pr utkø som skal "drives")       */
/*                                                                   */
/*  PARAMETER 1=USERID ("Printer") PÅ SYSTEMET SOM MOTTAR PRINTEN.   */
/*                                                                   */
/*  PARAMETER 2=ADRESSEN/SYSTEMNAVN(DISTRIB.KØ) EN SENDER TIL.       */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&OUTQ &ADRESSE)

             DCL        VAR(&OUTQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ADRESSE) TYPE(*CHAR) LEN(8)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10) VALUE(*LIBL)
             DCL        VAR(&FLDLEN) TYPE(*DEC) LEN(5 0) VALUE(128)
             DCL        VAR(&FIELD) TYPE(*CHAR) LEN(128)
             DCL        VAR(&WAIT) TYPE(*DEC) LEN(5 0) VALUE(-1)
             DCL        VAR(&A) TYPE(*CHAR) LEN(10)
             DCL        VAR(&B) TYPE(*CHAR) LEN(10)
             DCL        VAR(&C) TYPE(*CHAR) LEN(10)
             DCL        VAR(&D) TYPE(*CHAR) LEN(6)
             DCL        VAR(&E) TYPE(*CHAR) LEN(4)
             DCL        VAR(&EBIN) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&ECHR) TYPE(*CHAR) LEN(4)


             DLTDTAQ    DTAQ(&OUTQ)
             MONMSG     MSGID(CPF0000)
             CRTDTAQ    DTAQ(SYENDRE/&OUTQ) MAXLEN(128)

 START:      CALL       PGM(QRCVDTAQ) PARM(&OUTQ &LIB &FLDLEN +
                          &FIELD &WAIT)

             CHGVAR     VAR(&A) VALUE(%SUBSTRING(&FIELD 39 10))
             CHGVAR     VAR(&B) VALUE(%SUBSTRING(&FIELD 13 10))
             CHGVAR     VAR(&C) VALUE(%SUBSTRING(&FIELD 23 10))
             CHGVAR     VAR(&D) VALUE(%SUBSTRING(&FIELD 33 6))
             CHGVAR     VAR(&E) VALUE(%SUBSTRING(&FIELD 49 4))
             CHGVAR     VAR(&EBIN) VALUE(%BINARY(&E 1 4))
             CHGVAR     VAR(&ECHR) VALUE(&EBIN)

  /*SEND FILA */
             SNDNETSPLF FILE(&A) TOUSRID((&OUTQ &ADRESSE)) +
                          JOB(&D/&C/&B) SPLNBR(&ECHR) DTAFMT(*ALLDATA)
             MONMSG     MSGID(CPF0000)

  /*SLETT SENDT FIL */
             DLTSPLF    FILE(&A) JOB(&D/&C/&B) SPLNBR(&ECHR)
             MONMSG     MSGID(CPF0000)

  /*GÅ OG VENT PÅ NESTE DATAKØ-POST */
             GOTO START

             ENDPGM
