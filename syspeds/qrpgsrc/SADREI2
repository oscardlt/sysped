********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NY FIL, SADHMD
********************************************************************
     FEDIM2     UF   E           K DISK
     FEDIS      UF   E           K DISK
     FSADHD     UF   E           K DISK
N01  FSADHMD    UF   E           K DISK
     FSADHCMP   O    E           K DISK
     FSADV      UF   E           K DISK
     FSADF      UF   E           K DISK
     FSADW      UF   E           K DISK
     FTVINF     UF   E           K DISK
     D                 DS
     D  XDT2                   1      6  0
     D  XD                     1      2  0
     D  XM                     3      4  0
     D  XÅ                     5      6  0
     D                 DS
     D  WDT                    1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *___________
      * KEY FIELDS
      *"""""""""""
     C     SADHK         KLIST
     C                   KFLD                    SIAVD
     C                   KFLD                    SITDN
      *____________
      * WORK FIELDS
      *""""""""""""
     C     *LIKE         DEFINE    SIAVD         XAVD
     C     *LIKE         DEFINE    XDT2          XDT
      *_______________
      * PARAMETER LIST
      *"""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    XAVD
     C     XDT2          PARM                    XDT
     C                   PARM                    XDLT              1
      *__________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADHD
      *""""""""""""""""""""""""""""""""""""""""""
     C                   Z-ADD     XÅ            WÅ
     C                   Z-ADD     XM            WM
     C                   Z-ADD     XD            WD
     C     XÅ            IFGT      50
     C                   MOVEL     '19'          WÅ
     C                   ELSE
     C                   MOVEL     '20'          WÅ
     C                   END
     C     SIDT          SETLL     SADHD
     C                   READ      SADHD                                  51
     C  N51SIDT          COMP      WDT                                51
     C     *IN51         DOWEQ     '0'                                          <----------I
     C     XAVD          IFEQ      SIAVD                                         <--------II
     C     XAVD          OREQ      0
     C                   EXSR      SRA                                          SADV
     C                   EXSR      SRB                                          SADF
     C                   EXSR      SRC                                          SADW
     C     XDLT          IFEQ      'J'
     C                   EXSR      SRD                                          EDIM2/EDIS/TVINF
     C                   END
N01  C                   EXSR      SRE                                          SADHMD
     C                   WRITE     SADHRCMP
     C                   DELETE    SADHR
     C                   END                                                     <--------II
     C                   READ      SADHD                                  51
     C  N51SIDT          COMP      WDT                                51
     C  N51              END                                                    <----------I
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADV
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C     SADHK         SETLL     SADV
     C     SADHK         READE     SADV                                   52
     C     *IN52         DOWEQ     '0'
     C                   DELETE    SADVR
     C     SADHK         READE     SADV                                   52
     C                   END
     C                   ENDSR
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADF
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRB           BEGSR
     C     SADHK         SETLL     SADF
     C     SADHK         READE     SADF                                   52
     C     *IN52         DOWEQ     '0'
     C                   DELETE    SADFR
     C     SADHK         READE     SADF                                   52
     C                   END
     C                   ENDSR
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADW
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRC           BEGSR
     C     SADHK         SETLL     SADW
     C     SADHK         READE     SADW                                   52
     C     *IN52         DOWEQ     '0'
     C                   DELETE    SADWR
     C     SADHK         READE     SADW                                   52
     C                   END
     C                   ENDSR
      *_____________________________________________________
      * SETLL, READ AND DELETE RECORDS FROM EDIM2/EDIS/TVINF
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   MOVE      '0'           *IN52
     C     SADHK         SETLL     EDIM2
     C     SADHK         READE     EDIM2                                  52
      *
     C     *IN52         DOWEQ     '0'                                          <----------I
     C     M1001         IFEQ      '929'                                         <--------II
     C     MMN           SETLL     TVINF                                                  II
     C     MMN           READE     TVINF                                  53              II
     C     *IN53         DOWEQ     '0'                                            <------III
     C                   DELETE    TVINFF                                                III
     C     MMN           READE     TVINF                                  53             III
     C  N53              END                                                      <------III
     C     MSN           CHAIN     EDIS                               53                  II
     C  N53              DELETE    EDISR                                                  II
     C                   DELETE    EDIMR                                                  II
     C                   ELSE                                                             II
     C                   UPDATE    EDIMR                                                  II
     C                   END                                                     <--------II
     C     SADHK         READE     EDIM2                                  52               I
     C  N52              END                                                    <----------I
     C                   ENDSR
N01   *___________________________________________
N01   * SETLL, READ AND DELETE RECORDS FROM SADHMD
N01   *"""""""""""""""""""""""""""""""""""""""""""
N01  C     SRE           BEGSR
N01  C     SADHK         CHAIN     SADHMD                             52
N01  C                   IF        *IN52 = *OFF
N01  C                   DELETE    SADHMDR
N01  C                   END
N01  C                   ENDSR
