      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10244 * 01 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A
      *              MYE ENDRING. INGEN MERKING
10244 * 02 SH  PTF10 18 POSTER I SUBFIL
********************************************************************
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KUNDERS VAREREGISTER      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-85 90 95 96 ***
      ***********************************************************
      *
     FCUNDL01   IF   E           K DISK
     FTARI      IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FKODTSB    IF   E           K DISK
     FKODTSE    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSADL      UF A E           K DISK
     FSAD004FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  SLALFA                 1     28
     D  XDLT                   1      4
     D                 DS
     D  SLTXT                  1     25
     D  SLTXT2                 1     20
     D  SLR31                 21     21
     D  SLPRE                 22     22
     D  SLMFR                 23     23
     D  SLLEDI                24     25
      *
     C                   EXSR      INIT
      *
      *** LES KUNDENR.
     C     ST            TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       3099
      *
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKUNR
     C     KUND1K        CHAIN     CUNDL01                            99
     C  N99              MOVE      *BLANKS       W2ALFA
     C  N99              GOTO      START
     C                   MOVE      *ZEROS        WSKUNR
     C                   SETOFF                                       99
     C                   GOTO      ST
     C                   END
     C     KUND1K        CHAIN     CUNDL01                            99
     C   99              DO
     C                   MOVE      'SPK0001'     MSGNR
     C                   SETON                                        30
     C                   GOTO      ST
     C                   END
     C                   MOVE      *BLANKS       W2ALFA
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2ALFA        YALFA            28
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SADLK1        SETLL     SADL
     C     START1        TAG
     C     WSKUNR        READE     SADL                                   94
     C   94              MOVE      *BLANKS       SLALFA
     C   94              MOVE      *BLANKS       WSNR
     C  N94              UPDATE    SADLR
      *
     C     START2        TAG
      *
     C  N94              DO
E02  C                   DO        18
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C     WSKUNR        READE     SADL                                   94
     C  N94              UPDATE    SADLR
     C  N94              MOVE      SLALFA        YALFA
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   SETOFF                                       98
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     NYVARE
     C                   GOTO      ST41
     C                   ELSE
     C                   END
     C                   WRITE     SUBCTL
      *
      *** AVSLUTT
     C     ST41          TAG
     C                   EXFMT     BUNN
     C                   SETOFF                                       9830
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY VARE
     C   06              GOTO      START5
      *** SØK FRA VARE
     C   08              GOTO      START6
      *** TIL BILDE SOM HENTER KUNDENR
     C   12              GOTO      ST
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94SADLK3        SETLL     SADL
     C  N94WSKUNR        READE     SADL                                   94
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
      *
      *** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     SADLK2        CHAIN     SADL                               33
     C  N33              DELETE    SADLR
     C                   EXSR      MOVB1
     C                   MOVEL     '*DLT'        SLALFA
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
      *
     C*** LAGE/ENDRE VARE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(81)
     C                   MOVEA     '000'         *IN(87)
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
      *
      *** SØK FRA UTSKRIFTTYPE
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
      *
      * TEST OM SØKETEKST ER TASTET
     C  N40              DO
     C     WSALFA        COMP      *BLANKS                                89
     C   89              MOVE      'SPF0002'     MSGNR
     C   89              SETON                                            30
     C   89              GOTO      SLT1
     C                   END
      *
      * TEST OM OPPRINNELSELAND/FYLKEKODE ER RIKTIG
     C     WSOPPL        IFGE      '01'
     C     WSOPPL        ANDLE     '99'
     C     WSOPPL        CHAIN     KODTSE                             88
     C   88              MOVE      'YBC1824'     MSGNR
     C                   ELSE
     C     KODS2K        CHAIN     KODTS2                             88
     C   88              MOVE      'SPL0001'     MSGNR
     C                   END
     C   88              SETON                                        30
     C   88              GOTO      SLT1
      *
      * TEST TARIFFNR.
     C     TARIK         CHAIN     TARI                               87
     C   87              MOVE      'SPV0003'     MSGNR
     C   87              SETON                                        30
     C   87              GOTO      SLT1
      * TEST KODE FOR DOKUMENT/SERTIFIKAT
     C     WSCREF        IFNE      *BLANKS
     C     WSCREF        CHAIN     KODTSB                             82
     C     *IN82         IFEQ      *ON
     C     *IN30         ANDEQ     *OFF
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SPC0006'     MSGNR
     C                   GOTO      SLT1
     C                   ENDIF
     C                   ENDIF
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     SADLK         CHAIN     SADL                               33
     C  N33
     CANN40              GOTO      SLT1
     C                   EXSR      MOVWSL
     C  N33              UPDATE    SADLR
     C   33              WRITE     SADLR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
      *
     C     SLT1          ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     XDLT          COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVSLW
     C                   MOVE      ' '           WSNR
     C                   SETON                                        40
     C                   SETOFF                                       999789
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     XDLT          COMP      '*DLT'                                 98
     C                   GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVWSL        BEGSR
      ***********************************************
     C                   MOVE      WSKUNR        SLKNR
     C                   MOVE      WSALFA        SLALFA
     C                   MOVE      WSTXT2        SLTXT2
     C                   MOVE      WSR31         SLR31
     C                   MOVE      WSOPPL        SLOPPL
     C                   MOVE      WSTANR        SLTANR
     C                   MOVE      WSTN          SLTN
     C                   MOVE      WSPRE         SLPRE
     C                   MOVE      WSVEKT        SLVEKT
     C                   MOVE      WSPVA         SLPVA
     C                   MOVE      WSSATS        SLSATS
     C                   MOVE      WSMFR         SLMFR
     C                   MOVEL     WSKDAÆ        SLKDAÆ
     C                   MOVEL     WSKDSÆ        SLKDSÆ
     C                   MOVEL     WSTO          SLTO
     C                   MOVEL     WSCREF        SLCREF
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVSLW        BEGSR
      ***********************************************
     C                   MOVE      SLALFA        WSALFA
     C                   MOVE      SLTXT2        WSTXT2
     C                   MOVE      SLR31         WSR31
     C                   MOVE      SLOPPL        WSOPPL
     C                   MOVE      SLTANR        WSTANR
     C                   MOVE      SLTN          WSTN
     C                   MOVE      SLPRE         WSPRE
     C                   MOVE      SLVEKT        WSVEKT
     C                   MOVE      SLPVA         WSPVA
     C                   MOVE      SLSATS        WSSATS
     C                   MOVE      SLMFR         WSMFR
     C                   MOVEL     SLKDAÆ        WSKDAÆ
     C                   MOVEL     SLKDSÆ        WSKDSÆ
     C                   MOVEL     SLTO          WSTO
     C                   MOVEL     SLCREF        WSCREF
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB1         BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       SLALFA
     C                   MOVE      *BLANKS       SLTXT2
     C                   MOVE      *BLANKS       SLR31
     C                   MOVE      *BLANKS       SLOPPL
     C                   MOVE      *ZEROS        SLTANR
     C                   MOVE      *BLANKS       SLTN
     C                   MOVE      *BLANKS       SLPRE
     C                   MOVE      *ZEROS        SLVEKT
     C                   MOVE      *BLANKS       SLPVA
     C                   MOVE      *ZEROS        SLSATS
     C                   MOVE      *BLANKS       SLMFR
     C                   MOVE      *BLANKS       SLKDAÆ
     C                   MOVE      *BLANKS       SLKDSÆ
     C                   MOVE      *BLANKS       SLTO
     C                   MOVE      *BLANKS       SLCREF
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB2         BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       WSALFA
     C                   MOVE      *BLANKS       WSTXT2
     C                   MOVE      *BLANKS       WSR31
     C                   MOVE      *BLANKS       WSOPPL
     C                   MOVE      *ZEROS        WSTANR
     C                   MOVE      *BLANKS       WSTN
     C                   MOVE      *BLANKS       WSPRE
     C                   MOVE      *ZEROS        WSVEKT
     C                   MOVE      *BLANKS       WSPVA
     C                   MOVE      *ZEROS        WSSATS
     C                   MOVE      *BLANKS       WSMFR
     C                   MOVE      *BLANKS       WSKDAÆ
     C                   MOVE      *BLANKS       WSKDSÆ
     C                   MOVE      *BLANKS       WSTO
     C                   MOVE      *BLANKS       WSCREF
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SADLK         KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSALFA
     C     SADLK1        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2ALFA
     C     SADLK2        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    SLALFA
     C     SADLK3        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    YALFA
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     TARIK         KLIST
     C                   KFLD                    WSTANR
     C                   KFLD                    WSTAR
     C     KODS2K        KLIST
     C                   KFLD                    WS2UNI
     C                   KFLD                    WSOPPL
      *
     C                   MOVE      'S2 '         WS2UNI            3
     C                   MOVE      *BLANKS       W2ALFA
     C                   MOVE      *BLANKS       WSTAR             3
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
      ***********************************************
     C     SØKUNR        BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       USØKKN
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
      *
     C                   ENDSR
      *
