********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('SYFA5901' 'SYFA59 ' 'SY400-
S01  A*                                     LO/MAINT')
N01  A                                      HLPPNLGRP(HLPSCR SYFA59PNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD AV AVDELINGSG-
N01  A                                      RUPPER STATISTIKK')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R B10
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'SØK')
     A  89                                  CF11(11 'DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(01)
     A                                      CLRL(24)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYFA59 '
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(033 TBR0245 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            WMODE          7A  O  4 32MSGID(&WMODNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  3MSGCON(020 TBR0246 *LIBL/SYMSGF)
     A            AVKODE         7   B  8 27
     A                                 10  3MSGCON(020 TBR0232 *LIBL/SYMSGF)
     A            AVTEXT         4Y 0B 10 27EDTCDE(4)
     A                                 23  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 70MSGCON(010 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            WMODNR         7A  P
     A          R B20
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYFA59 '
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(033 TBR0245 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  4 40'  2=Endre  4=Slette'
     A                                      COLOR(BLU)
     A                                  6  3'Valg'
     A                                      COLOR(WHT)
     A                                  6  9MSGCON(015 TBR0246 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            FVKODE         7   B  7  9
     A                                 10 10'(Ingen poster i filen)   '
     A  30        MSGTXT        70A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R B30
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'SØK')
     A  89                                  CF11(11 'DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(01)
     A                                      CLRL(24)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYFA59 '
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(033 TBR0245 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            WMODE          7A  O  4 32MSGID(&WMODNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  3MSGCON(020 TBR0246 *LIBL/SYMSGF)
     A            AVKODE         7A  O  8 27
     A                                 10  3MSGCON(020 TBR0232 *LIBL/SYMSGF)
     A  80        BVTEXT         4  0O 10 27
     A N80        AVTEXT         4  0B 10 27
     A  80                             12  8MSGCON(040 TBR0110 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 70MSGCON(010 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            WMODNR         7A  P
     A          R B40
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A N31                                  CF05(05 'Forny')
     A                                      CF06(06 'Ny linje')
     A  31                                  CF12(12 'Avbryt')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(010 TBR0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(010 TBR0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R H50                       SFL
     A            SFRRN         10S 0H
     A            WSNR           2A  I  8  3VALUES('  ' '2 ' '02' ' 2' '4 ' '04-
     A                                      ' ' 4')
     A            SVKODE         7   O  8  9
     A            SVTEXT         4  0O  8 32
     A            BESKR         33   O  8 43
     A*
     A          R K60                       SFLCTL(H50)
     A                                      SFLSIZ(2000)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  3'SYSPED/SYFA59 '
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(033 TBR0245 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  4 40'  2=Endre  4=Slette'
     A                                      COLOR(BLU)
     A                                  6  2'Valg'
     A                                      COLOR(WHT)
     A                                  6 43MSGCON(020 TBR0105 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  9MSGCON(015 TBR0246 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 30MSGCON(008 TBR0232 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            FVKODE         7   B  7  9
