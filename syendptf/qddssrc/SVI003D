     A*%%TS  SD  20160219  091254  CB          REL-V7R1M0  5770-WDS
     A*
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 CB  KODE TIL SÄRSKILDA UPPLYSNINGAR
11XXXA* 02 CB  STATUSKODE
P12063* 03 CB  Testa att en del numerisk inte ær negativa
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A                                      HLPPNLGRP(HLPSCR SVI001PNL)
     A                                      HLPTITLE('ED För Import')
     A          R SVI003DA                  SFL
     A*%%TS  SD  20160219  091254  CB          REL-V7R1M0  5770-WDS
     A            SVIV_SYLI      5S 0H
     A            SVIV_STVA     11S 0H
     A            SVIV_SUTX     70A  H
     A            SVIV_KVOT      6A  H
     A            SVIV_NETO     14S 3H
     A            SVIV_ANKV      9Y 0H
     A            SVIV_EUP2      3A  H
     A            ZOP            1A  I  8  2VALUES(' ' 'K' '2' '4' '5')
     A            SVIV_ULKD      2A  O  8  4
     A            SVIV_VATA     10A  O  8  7
     A            SVIV_VATI      4A  O  8 18
     A            SVIV_FOKD      3A  O  8 23
     A            SVIV_EUP1      4A  O  8 27
     A            SVIV_BRUT     14Y 3O  8 32EDTCDE(4)
     A            SVIV_VASL     70A  O  8 48
     A            SVIV_ERR       1A  O  8121
     A            SVIV_VANO      5Y 0O  8128EDTCDE(Z)
     A          R SVI003DB                  SFLCTL(SVI003DA)
     A*%%TS  SD  20160219  091254  CB          REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF11(11 'K.VARER')
     A                                      CF12(12 'Förra')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI003'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 46'Underhåll av Ärende - Varuposter i-
     A                                      mport'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 77SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 94'Antal'
     A                                  2110'B.vikt'
     A                                  2127'F.bel'
     A                                  3  2'Avd/ärende/sign:'
     A            ZVIV_SYAV      4Y 0O  3 19EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZVIV_SYOP      7Y 0O  3 24EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZVIV_SYSG      3A  O  3 32COLOR(WHT)
     A                                  3 82'Huvud:'
     A            SVIH_KOTA      8Y 0O  3 89EDTCDE(K)
     A            SVIH_BRUT     12Y 3O  3101EDTCDE(K)
     A            SVIH_FABL     11Y 3O  3118EDTCDE(K)
     A                                  4 82'Linje:'
     A            WVIH_KOTA      8Y 0O  4 89EDTCDE(K)
     A            WVIH_BRUT     12Y 3O  4101EDTCDE(K)
     A            WVIH_FABL     11Y 3O  4118EDTCDE(K)
     A                                  5  2'Välj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5 82'Diff.:'
     A            XVIH_KOTA      8Y 0O  5 89EDTCDE(K)
     A            XVIH_BRUT     12Y 3O  5101EDTCDE(K)
     A            XVIH_FABL     11Y 3O  5118EDTCDE(K)
     A                                  6  4'2=Ändra   4=Radera   5=Visa'
     A                                      COLOR(BLU)
     A                                  6 34'K=Lægg till i kunders varuregister'
     A                                      COLOR(BLU)
     A                                  7  4'Ul'
     A                                      COLOR(WHT)
     A                                  7  7'Varukod'
     A                                      COLOR(WHT)
     A                                  7 18'33:3'
     A                                      COLOR(WHT)
     A                                  7 23'Fm'
     A                                      COLOR(WHT)
     A                                  7 27'Førf'
     A                                      COLOR(WHT)
     A                                  7 37'Bruttovikt'
     A                                      COLOR(WHT)
     A                                  7 48'Varuslag'
     A                                      COLOR(WHT)
     A                                  7121'Status'
     A                                      COLOR(WHT)
     A                                  7128'Linje'
     A                                      COLOR(WHT)
     A          R SVI003DC
     A                                      OVERLAY
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 26 24'F6=Skapa'
     A                                      COLOR(BLU)
     A                                 26 35'F11=K.varer'
     A                                      COLOR(BLU)
     A                                 26 49'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVI003DD
     A*%%TS  SD  20160219  091254  CB          REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF09(09 'XTRA R44')
     A                                      CF10(10 'AUTOVIKT')
     A                                      CF11(11 'K.VARER')
     A                                      CF12(12 'Forrige')
     A                                      CF14(14 'R31')
     A                                      CF15(15 'Mer SU')
     A                                      CF16(16 'BILH')
     A                                      CF17(17 'CONT')
     A                                      CF18(18 'TIDH')
     A                                      CF19(19 'LAGER')
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SVI0010340 SVI001PNL)
     A                                      HLPARA(19 004 19 005)
     A          H                           HLPPNLGRP(SVI0010330 SVI001PNL)
     A                                      HLPARA(19 007 19 016)
     A          H                           HLPPNLGRP(SVI0010331 SVI001PNL)
     A                                      HLPARA(19 018 19 021)
     A          H                           HLPPNLGRP(SVI0010360 SVI001PNL)
     A                                      HLPARA(19 023 19 025)
     A          H                           HLPPNLGRP(SVI0010371 SVI001PNL)
     A                                      HLPARA(19 027 19 030)
     A          H                           HLPPNLGRP(SVI0010373 SVI001PNL)
     A                                      HLPARA(19 032 19 034)
     A          H                           HLPPNLGRP(SVI0010350 SVI001PNL)
     A                                      HLPARA(19 033 19 041)
     A          H                           HLPPNLGRP(SVI0010380 SVI001PNL)
     A                                      HLPARA(19 047 19 055)
     A          H                           HLPPNLGRP(SVI0010410 SVI001PNL)
     A                                      HLPARA(19 057 19 065)
     A          H                           HLPPNLGRP(SVI0010420 SVI001PNL)
     A                                      HLPARA(19 068 19 079)
     A          H                           HLPPNLGRP(SVI0010310 SVI001PNL)
     A                                      HLPARA(20 004 20 078)
     A          H                           HLPPNLGRP(SVI0010440 SVI001PNL)
     A                                      HLPARA(21 004 21 043)
     A          H                           HLPPNLGRP(SVI0010441 SVI001PNL)
     A                                      HLPARA(21 048 21 058)
     A          H                           HLPPNLGRP(SVI0010442 SVI001PNL)
     A                                      HLPARA(21 060 21 074)
     A          H                           HLPPNLGRP(SVI0010390 SVI001PNL)
     A                                      HLPARA(22 004 22 014)
     A          H                           HLPPNLGRP(SVI0010440 SVI001PNL)
     A                                      HLPARA(22 031 22 079)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                 21  4'Ul'
     A                                      COLOR(WHT)
     A                                 21  7'Varukod'
     A                                      COLOR(WHT)
     A                                 21 18'Fm'
     A                                      COLOR(WHT)
     A                                 21 22'Förf'
     A                                      COLOR(WHT)
     A                                 21 32'Bruttovikt'
     A                                      COLOR(WHT)
     A                                 21 49'Nettovikt'
     A                                      COLOR(WHT)
     A                                 21 61'X_mängd'
     A                                      COLOR(WHT)
     A                                 21 74'Fa.bel:'
     A                                      COLOR(WHT)
     A                                 21 83'Stat.värde'
     A                                      COLOR(WHT)
     A                                 21 96'Tullvärde'
     A                                      COLOR(WHT)
     A                                 21106'Call me'
     A                                      COLOR(WHT)
     A            ZVIV_ULKD      2A  B 22  4COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 31                               DSPATR(HI)
     A            ZVIV_VATA     10A  B 22  7COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 32                               DSPATR(HI)
     A            ZVIV_FOKD      3A  B 22 18COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 34                               DSPATR(HI)
     A            ZVIV_EUP1      4A  B 22 22COLOR(YLW)
     A  27                                  DSPATR(PR)
     A            ZVIV_BRUT     14Y 3B 22 27EDTCDE(4)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A  30 36                               DSPATR(HI)
N03  A                                      COMP(GE 0)
     A            ZVIV_NETO     14Y 3B 22 43EDTCDE(4)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A  30 37                               DSPATR(HI)
N03  A                                      COMP(GE 0)
     A            ZVIV_ANKV      9Y 0B 22 59EDTCDE(4)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A  30 38                               DSPATR(HI)
N03  A                                      COMP(GE 0)
     A            ZVIV_FABL     11Y 3B 22 69EDTCDE(4)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A  30 44                               DSPATR(HI)
N03  A                                      COMP(GE 0)
     A            ZVIV_STVA     11Y 0B 22 82EDTCDE(4)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
N03  A                                      COMP(GE 0)
     A            ZVIV_STVA2    11Y 0B 22 94EDTCDE(4)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
N03  A                                      COMP(GE 0)
     A            ZVIV_CALL      2A  B 22106COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 47                               DSPATR(HI)
     A            WVIV_SYLI      5Y 0O 22128EDTCDE(Z)
     A                                 23  4'R31:'
     A                                      COLOR(WHT)
     A            ZVIV_KOTA      5Y 0B 23  9EDTCDE(4)
     A  27                                  DSPATR(PR)
     A                                      COMP(GE 0)
     A                                      COLOR(YLW)
     A            ZVIV_KOSL      4A  B 23 15COLOR(YLW)
     A  27                                  DSPATR(PR)
     A            ZVIV_VASL     70A  B 23 20COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 43                               DSPATR(HI)
     A                                      CHECK(LC)
     A            ZVIV_GODM     42A  B 23 91COLOR(YLW)
     A  27                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 24  4'Su.:'
     A                                      COLOR(WHT)
     A            ZVIV_SUKO      5A  B 24  9COLOR(YLW)
     A  27                                  DSPATR(PR)
     A            ZVIV_SUTX     70A  B 24 15COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 39                               DSPATR(HI)
     A                                      CHECK(LC)
     A                                 24 88'R33:3'
     A                                      COLOR(WHT)
     A                                 24 94'R33:4'
     A                                      COLOR(WHT)
     A                                 24100'R33:5'
     A                                      COLOR(WHT)
     A                                 24106'R37:2'
     A                                      COLOR(WHT)
     A                                 25  4'Kvot'
     A                                      COLOR(WHT)
     A            ZVIV_KVOT      6A  B 25  9COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 42                               DSPATR(HI)
     A                                 25 18'Avg.ber:'
     A                                      COLOR(WHT)
     A            ZVIV_AVGB      1A  B 25 27COLOR(YLW)
     A  27                                  DSPATR(PR)
     A                                 25 31'B.handl:'
     A                                      COLOR(WHT)
     A            ZVIV_BIT1      4A  B 25 40COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 45                               DSPATR(HI)
     A            ZVIV_BII1     35A  B 25 45COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 46                               DSPATR(HI)
     A                                      CHECK(LC)
     A            ZVIV_VATI      4A  B 25 88COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 33                               DSPATR(HI)
     A            ZVIV_VAT4      4A  B 25 94COLOR(YLW)
     A  27                                  DSPATR(PR)
     A            ZVIV_VAT5      4A  B 25100COLOR(YLW)
     A  27                                  DSPATR(PR)
     A            ZVIV_EUP2      3A  B 25106COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  30 35                               DSPATR(HI)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 26 24'F10=Autovikt'
     A                                      COLOR(BLU)
     A                                 26 38' F11=K_varer'
     A                                      COLOR(BLU)
     A                                 26 53'F12=Avbryt'
     A                                      COLOR(BLU)
     A                                 26 66'F14=R31'
     A                                      COLOR(BLU)
     A                                 26 76'F15=SU'
     A                                      COLOR(BLU)
     A                                 26 85'F16=BH'
     A                                      COLOR(BLU)
     A                                 26 94'F17=Cont'
     A                                      COLOR(BLU)
     A                                 26105'F18=Tid_H'
     A                                      COLOR(BLU)
     A                                 26117'F19=Lager'
     A                                      COLOR(BLU)
     A          R SVI003DE
     A                                 26  2' '
     A                                 27  2' '
     A          R SVI003DF
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF11(11 'K.VARER')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI003'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 46'Underhåll av Ärende - Varuposter i-
     A                                      mport'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A                                  3  2'Avd/ärende/sign:'
     A            ZVIV_SYAV      4Y 0O  3 19EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZVIV_SYOP      7Y 0O  3 24EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZVIV_SYSG      3A  O  3 32COLOR(WHT)
     A                                  3101'Fakturabelopp. :'
     A            SVIH_FABL     11Y 3O  3118EDTCDE(K)
     A                                      COLOR(WHT)
     A                                  4101'Varulinjebelopp:'
     A            WVIH_FABL     11Y 3O  4118EDTCDE(K)
     A                                      COLOR(WHT)
     A                                  5  2'Välj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5101'Differans......:'
     A            XVIH_FABL     11Y 3O  5118EDTCDE(K)
     A                                      COLOR(WHT)
     A                                  6  4'2=Ändra   4=Radera'
     A                                      COLOR(BLU)
     A                                  8  4'Ul'
     A                                      COLOR(WHT)
     A                                  8  7'Varukod'
     A                                      COLOR(WHT)
     A                                  8 18'Tk'
     A                                      COLOR(WHT)
     A                                  8 23'Fm'
     A                                      COLOR(WHT)
     A                                  8 27'Proc'
     A                                      COLOR(WHT)
     A                                  8 32'Bruttovikt'
     A                                      COLOR(WHT)
     A                                  8 43'Nettovikt'
     A                                      COLOR(WHT)
     A                                  8 55'X_mängd'
     A                                      COLOR(WHT)
     A                                  8 63'Varuslag'
     A                                      COLOR(WHT)
     A          R SVI003DG
     A                                      WINDOW(*DFT 17 44 *NOMSGLIN)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                  1  1'Fakturabelopp Huvudnivå...:'
     A            SVIH_FABL     11Y 3O  1 30EDTCDE(K)
     A                                      COLOR(WHT)
     A                                  2  1'Fakturabelopp Varupostnivå:'
     A            WVIH_FABL     11Y 3O  2 30EDTCDE(K)
     A                                      COLOR(WHT)
     A                                  3  1'Fakturabelopp Differens...:'
     A            XVIH_FABL     11  3O  3 30EDTCDE(K)
     A N71                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A                                  5  1'Bruttovikt Huvudnivå......:'
     A            SVIH_BRUT     12Y 3O  5 29EDTCDE(K)
     A                                      COLOR(WHT)
     A                                  6  1'Bruttovikt Varupostnivå...:'
     A            WVIH_BRUT     12Y 3O  6 29EDTCDE(K)
     A                                      COLOR(WHT)
     A                                  7  1'Bruttovikt Differens......:'
     A            XVIH_BRUT     12  3O  7 29EDTCDE(K)
     A N72                                  COLOR(WHT)
     A  72                                  COLOR(RED)
     A                                  9  1'Antal Huvudnivå...........:'
     A            SVIH_KOTA      8Y 0O  9 30EDTCDE(K)
     A                                      COLOR(WHT)
     A                                 10  1'Antal Varupostnivå........:'
     A            WVIH_KOTA      8Y 0O 10 30EDTCDE(K)
     A                                      COLOR(WHT)
     A                                 11  1'Antal Differens...........:'
     A            XVIH_KOTA      8  0O 11 30EDTCDE(K)
     A N73                                  COLOR(WHT)
     A  73                                  COLOR(RED)
     A                                 13  1'OBS ! Det är differens mellan'
     A                                 14  7'huvudnivå och varupostnivå'
     A                                 15  7'Vill ni fortsätta ?'
     A            XVIH_DIJN      1A  B 17 21COLOR(YLW)
     A                                      VALUES('J' 'N')
     A                                 17 23'J/N'
     A          R SVI003DI
     A*%%TS  SD  20130314  110545  CB          REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF12(12 'Förra')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI003'
     A                                      COLOR(BLU)
     A                                  1 16'DI'
     A                                      COLOR(BLU)
     A                                  1 46'Underhåll av Ärende - Varuposter i-
     A                                      mport'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A                                  2 63'Mer R.31'
     A                                      COLOR(WHT)
     A                                  4  2'Kollital'
     A                                      COLOR(WHT)
     A                                  4 11'Kollislag'
     A                                      COLOR(WHT)
     A                                  4 21'Godsmärkning'
     A                                      COLOR(WHT)
     A            ZVIV_KOT1      5Y 0B  5  2COLOR(YLW)
     A                                      COMP(GE 0)
     A                                      EDTCDE(4)
     A            ZVIV_KOS1      4A  B  5 11COLOR(YLW)
     A            ZVIV_GODM     42A  B  5 21COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_KOT2      5Y 0B  6  2COLOR(YLW)
     A                                      COMP(GE 0)
     A                                      EDTCDE(4)
     A            ZVIV_KOS2      4A  B  6 11COLOR(YLW)
     A            ZVIV_GOD2     42A  B  6 21COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_KOT3      5Y 0B  7  2COLOR(YLW)
     A                                      COMP(GE 0)
     A                                      EDTCDE(4)
     A            ZVIV_KOS3      4A  B  7 11COLOR(YLW)
     A            ZVIV_GOD3     42A  B  7 21COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_KOT4      5Y 0B  8  2COLOR(YLW)
     A                                      COMP(GE 0)
     A                                      EDTCDE(4)
     A            ZVIV_KOS4      4A  B  8 11COLOR(YLW)
     A            ZVIV_GOD4     42A  B  8 21COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_KOT5      5Y 0B  9  2COLOR(YLW)
     A                                      COMP(GE 0)
     A                                      EDTCDE(4)
     A            ZVIV_KOS5      4A  B  9 11COLOR(YLW)
     A            ZVIV_GOD5     42A  B  9 21COLOR(YLW)
     A                                      CHECK(LC)
     A                                 11  2'Varubeskrivning'
     A                                      COLOR(WHT)
     A            ZVIV_VAS1     70A  B 12  2COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_VAS2     70A  B 13  2COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_VAS3     70A  B 14  2COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_VAS4     70A  B 15  2COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_VAS5     70A  B 16  2COLOR(YLW)
     A                                      CHECK(LC)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 26 24'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVI003DJ
     A*%%TS  SD  20130314  080910  CB          REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF12(12 'Förra')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI003'
     A                                      COLOR(BLU)
     A                                  1 16'DJ'
     A                                      COLOR(BLU)
     A                                  1 46'Underhåll av Ärende - Varuposter i-
     A                                      mport'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A                                  2 54'Särskilda upplysningar'
     A                                      COLOR(WHT)
     A                                  4  2'Kod'
     A                                      COLOR(WHT)
     A                                  4  8'Text'
     A                                      COLOR(WHT)
     A            ZVIV_SUK1      5A  B  5  2COLOR(YLW)
     A            ZVIV_SUT1     70A  B  5  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUT2     70A  B  6  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUT3     70A  B  7  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUT4     70A  B  8  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUT5     70A  B  9  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUK6      5A  B 10  2COLOR(YLW)
     A            ZVIV_SUT6     70A  B 10  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUT7     70A  B 11  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUT8     70A  B 12  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUT9     70A  B 13  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUTA     70A  B 14  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUKB      5A  B 15  2COLOR(YLW)
     A            ZVIV_SUTB     70A  B 15  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUTC     70A  B 16  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUTD     70A  B 17  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUTE     70A  B 18  8COLOR(YLW)
     A                                      CHECK(LC)
     A            ZVIV_SUTF     70A  B 19  8COLOR(YLW)
     A                                      CHECK(LC)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 26 24'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVI003DK
     A*%%TS  SD  20130314  091347  CB          REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF12(12 'Förra')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI003'
     A                                      COLOR(BLU)
     A                                  1 16'DK'
     A                                      COLOR(BLU)
     A                                  1 46'Underhåll av Ärende - Varuposter i-
     A                                      mport'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A                                  2 57'Bilagda Handlingar'
     A                                      COLOR(WHT)
     A                                  4  2'Typ'
     A                                      COLOR(WHT)
     A                                  4  7'Identitet'
     A                                      COLOR(WHT)
     A            ZVIV_BIT1      4A  B  5  2COLOR(YLW)
     A            ZVIV_BII1     35A  B  5  7COLOR(YLW)
     A            ZVIV_BIT2      4A  B  6  2COLOR(YLW)
     A            ZVIV_BII2     35A  B  6  7COLOR(YLW)
     A            ZVIV_BIT3      4A  B  7  2COLOR(YLW)
     A            ZVIV_BII3     35A  B  7  7COLOR(YLW)
     A            ZVIV_BIT4      4A  B  8  2COLOR(YLW)
     A            ZVIV_BII4     35A  B  8  7COLOR(YLW)
     A            ZVIV_BIT5      4A  B  9  2COLOR(YLW)
     A            ZVIV_BII5     35A  B  9  7COLOR(YLW)
     A            ZVIV_BIT6      4A  B 10  2COLOR(YLW)
     A            ZVIV_BII6     35A  B 10  7COLOR(YLW)
     A            ZVIV_BIT7      4A  B 11  2COLOR(YLW)
     A            ZVIV_BII7     35A  B 11  7COLOR(YLW)
     A            ZVIV_BIT8      4A  B 12  2COLOR(YLW)
     A            ZVIV_BII8     35A  B 12  7COLOR(YLW)
     A            ZVIV_BIT9      4A  B 13  2COLOR(YLW)
     A            ZVIV_BII9     35A  B 13  7COLOR(YLW)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 26 24'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVI003DL
     A*%%TS  SD  20130314  213230  CB          REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF12(12 'Förra')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI003'
     A                                      COLOR(BLU)
     A                                  1 16'DL'
     A                                      COLOR(BLU)
     A                                  1 46'Underhåll av Ärende - Varuposter i-
     A                                      mport'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A                                  2 57'Containeruppgifter'
     A                                      COLOR(WHT)
     A                                  4  2'Containernummer'
     A                                      COLOR(WHT)
     A            ZVIV_CO01     17A  B  5  2COLOR(YLW)
     A            ZVIV_CO02     17A  B  5 20COLOR(YLW)
     A            ZVIV_CO03     17A  B  5 38COLOR(YLW)
     A            ZVIV_CO04     17A  B  5 56COLOR(YLW)
     A            ZVIV_CO05     17A  B  6  2COLOR(YLW)
     A            ZVIV_CO06     17A  B  6 20COLOR(YLW)
     A            ZVIV_CO07     17A  B  6 38COLOR(YLW)
     A            ZVIV_CO08     17A  B  6 56COLOR(YLW)
     A            ZVIV_CO09     17A  B  7  2COLOR(YLW)
     A            ZVIV_CO10     17A  B  7 20COLOR(YLW)
     A            ZVIV_CO11     17A  B  7 38COLOR(YLW)
     A            ZVIV_CO12     17A  B  7 56COLOR(YLW)
     A            ZVIV_CO13     17A  B  8  2COLOR(YLW)
     A            ZVIV_CO14     17A  B  8 20COLOR(YLW)
     A            ZVIV_CO15     17A  B  8 38COLOR(YLW)
     A            ZVIV_CO16     17A  B  8 56COLOR(YLW)
     A            ZVIV_CO17     17A  B  9  2COLOR(YLW)
     A            ZVIV_CO18     17A  B  9 20COLOR(YLW)
     A            ZVIV_CO19     17A  B  9 38COLOR(YLW)
     A            ZVIV_CO20     17A  B  9 56COLOR(YLW)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 26 24'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVI003DM
     A*%%TS  SD  20130315  083649  CB          REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF12(12 'Förra')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI003'
     A                                      COLOR(BLU)
     A                                  1 16'DM'
     A                                      COLOR(BLU)
     A                                  1 46'Underhåll av Ärende - Varuposter i-
     A                                      mport'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A                                  2 57'Tidigare handlingar'
     A                                      COLOR(WHT)
     A                                  4  2'Kategori'
     A                                      COLOR(WHT)
     A                                  4 11'Typ'
     A                                      COLOR(WHT)
     A                                  4 15'Identitet'
     A                                      COLOR(WHT)
     A            ZVIV_TIK1      1A  B  5  2COLOR(YLW)
     A            ZVIV_TIT1      3A  B  5 11COLOR(YLW)
     A            ZVIV_TIX1     35A  B  5 15COLOR(YLW)
     A            ZVIV_TIK2      1A  B  6  2COLOR(YLW)
     A            ZVIV_TIT2      3A  B  6 11COLOR(YLW)
     A            ZVIV_TIX2     35A  B  6 15COLOR(YLW)
     A            ZVIV_TIK3      1A  B  7  2COLOR(YLW)
     A            ZVIV_TIT3      3A  B  7 11COLOR(YLW)
     A            ZVIV_TIX3     35A  B  7 15COLOR(YLW)
     A            ZVIV_TIK4      1A  B  8  2COLOR(YLW)
     A            ZVIV_TIT4      3A  B  8 11COLOR(YLW)
     A            ZVIV_TIX4     35A  B  8 15COLOR(YLW)
     A            ZVIV_TIK5      1A  B  9  2COLOR(YLW)
     A            ZVIV_TIT5      3A  B  9 11COLOR(YLW)
     A            ZVIV_TIX5     35A  B  9 15COLOR(YLW)
     A            ZVIV_TIK6      1A  B 10  2COLOR(YLW)
     A            ZVIV_TIT6      3A  B 10 11COLOR(YLW)
     A            ZVIV_TIX6     35A  B 10 15COLOR(YLW)
     A            ZVIV_TIK7      1A  B 11  2COLOR(YLW)
     A            ZVIV_TIT7      3A  B 11 11COLOR(YLW)
     A            ZVIV_TIX7     35A  B 11 15COLOR(YLW)
     A            ZVIV_TIK8      1A  B 12  2COLOR(YLW)
     A            ZVIV_TIT8      3A  B 12 11COLOR(YLW)
     A            ZVIV_TIX8     35A  B 12 15COLOR(YLW)
     A            ZVIV_TIK9      1A  B 13  2COLOR(YLW)
     A            ZVIV_TIT9      3A  B 13 11COLOR(YLW)
     A            ZVIV_TIX9     35A  B 13 15COLOR(YLW)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 26 24'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVI003DN
     A*%%TS  SD  20130315  083649  CB          REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF12(12 'Förra')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVI003'
     A                                      COLOR(BLU)
     A                                  1 16'DN'
     A                                      COLOR(BLU)
     A                                  1 46'Underhåll av Ärende - Varuposter i-
     A                                      mport'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A                                  2 57'Identifiering av lager'
     A                                      COLOR(WHT)
     A                                  4  2'Identitetsnummer'
     A                                      COLOR(WHT)
     A                                  4 19'Typ'
     A                                      COLOR(WHT)
     A                                  4 23'Landkod'
     A                                      COLOR(WHT)
     A            ZVIV_LAGI     14A  B  5  2COLOR(YLW)
     A            ZVIV_LAGT      1A  B  5 19COLOR(YLW)
     A            ZVIV_LAGL      2A  B  5 23COLOR(YLW)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Sök'
     A                                      COLOR(BLU)
     A                                 26 24'F12=Avbryt'
     A                                      COLOR(BLU)
     A          R SVI003DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
