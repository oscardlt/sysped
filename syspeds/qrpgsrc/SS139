      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/SS139   MODULE(*LIBL/SS139 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FBRPARF    if   e           k disk    usropn
     Fgsstrl11  if   e           K DISK    usropn
     Fgssorl21  if   e           K DISK    usropn
     FGSSBXF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D UserId          s             10
     D User            s             10
     D UsrSpcName      s             10
     D TUR             S             11
     D TURN            S             11  0
      * andre variabler
     D TotPos          S              5  0
     D TotCll          S              5  0
     D TotVkt          S              7  1
     D TotM3           S              7  2
     D TotLM           S              7  2
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  ORFT4                  1     30
     D  ORLMA                 11     14
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      userid = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      TUR = zhbgetvar('TUR')
     C                   eval      TURN = c2n2(TUR)
      * Set libl/open files
     C                   exsr      Init
      * Hvis turnr for oppdatering er gitt, oppdater denne først
     c     Turn          ifne      *zeros
     c                   move      Turn          UUTUR             9
     c                   move      'IN '         LocInOut          3
     c                   move      HUBCODE       LocCode           5
     c                   call      'STS113'
     c                   parm                    UUTUR             9
     c                   parm                    LocInOut
     c                   parm                    LocCode
     C                   PARM                    USERID
     c                   end
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Hent/vis TURER på vei inn mot hub.
     C                   exsr      VIS
      *
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     gsstrl11
     C                   close     gssorl21
     C                   close     GSSBXF
     C                   eval      *inlr = *on
     C                   RETURN
      *
     C***********************************************
     C     VIS           BEGSR
     C***********************************************
      * Repeat UserId for the next invocation
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':userid)
     C                   callp     updhtmlvar('UsrSpcName':UsrSpcName)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('WSNA':PAVRDA)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     wrtsection('top')
     C     *loval        SETLL     gsstrl11
     C                   READ      gsstrl11                               94
     C     *IN94         DOWEQ     '0'
     C     TRLOCT        cabne     HUBCODE       NextRec
      * post som skal vises i lista...
     C                   callp     updhtmlvar('TUR':
     C                             %trim(%editc(TRTNO:'Z')))
     C                   callp     updhtmlvar('DATE':
     C                             %trim(%editw(TRDTS:'    .  .  ')))
     C                   callp     updhtmlvar('FROMHUB':TRLOCF)
     C                   callp     updhtmlvar('TOHUB':TRLOCT)
     c                   exsr      AkkumTur
     C                   callp     updhtmlvar('TotPos':
     C                             %trim(%editc(TotPos:'Z')))
     C                   callp     updhtmlvar('TotCll':
     C                             %trim(%editc(TotCll:'Z')))
     C                   callp     updhtmlvar('TotVkt':
     C                             %trim(%editc(TotVkt:'M')))
     C                   callp     updhtmlvar('TotM3':
     C                             %trim(%editc(TotM3:'M')))
     C                   callp     updhtmlvar('TotLM':
     C                             %trim(%editc(TotLM:'M')))
     C                   callp     updhtmlvar('Units':units)
     C                   callp     wrtsection('tablerow')
      *
     C     NextRec       Tag
     C                   READ      gsstrl11                               94
     C                   end
      *
     C                   callp     wrtsection('tablebot')
      *
     C                   ENDSR
      *
      *=====================================================================******
      * Akkumuler totaler pr tur
      *=====================================================================******
     C     AkkumTur      begsr
     c                   move      *zeros        TotPos
     c                   move      *zeros        TotCll
     c                   move      *zeros        TotVkt
     c                   move      *zeros        TotM3
     c                   move      *zeros        TotLM
      *------------------
     C     TRTNO         SETLL     gssorl21
     C     TRTNO         READE     gssorl21                               33
     C     *IN33         DOWEQ     '0'
     C     ORST          IFNE      'D'
     c     ORLMA         IFEQ      *BLANKS
     c                   MOVE      *ZEROS        ORLM              4 2
     c                   ELSE
     c                   MOVE      ORLMA         ORLM
     c                   END
     c                   add       1             TotPos
     c                   add       ORCLI         TotCll
     c                   add       ORWT          TotVkt
     c                   add       ORVOL         TotM3
     c                   add       ORLM          TotLM
     C                   end
     C     TRTNO         READE     gssorl21                               33
     C                   end
      *------------------
      * List hvilke Units som inngår i Sh.listen
     c                   move      *blanks       Units            70
      *
     C     TRTNO         SETLL     GSSBXF
     C     TRTNO         READE     GSSBXF                                 33
     C     *IN33         DOWEQ     '0'
     C     Units         IFEQ      *blanks
     C                   Movel     BXID          Units
     C                   ELSE
     C                   cat       ',':0         Units
     C                   cat       BXID:1        Units
     C                   END
     C     TRTNO         READE     GSSBXF                                 33
     C                   ENDDO
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SS139'
      *------------------
     C                   endsr
      *=====================================================================******
      * Set Keys mm
      *=====================================================================******
     C     init          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              30
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      BRPARF
     C                   open      gsstrl11
     C                   open      gssorl21
     C                   open      GSSBXF
      *
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Hent HUb-kode  / Navn
     c                   Move      *blanks       HUBCODE           5
     C                   MOVE      'SSFAB'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        HUBCODE
     C                   endsr
