     H DATEDIT(*YMD)
     FEDIS      UF   E           K DISK
     FEDIM6     IF   E           K DISK
     FCNFF      UF   E             DISK
     FLOGF      O    E             DISK
     FFIRM      IF   E             DISK
     FSYFIRMF   IF   E           K DISK
     D                 DS
     D  SSNZ                   1      9  0
      * Input parametrer
     C     *ENTRY        PLIST
     C                   PARM                    SSN
     C                   PARM                    WMBRS            10 0
      * Definiere Workfelt
     C     *IN99         IFEQ      *OFF
     C                   MOVEL     'SMADTATQ'    QTNAME           10
     C                   MOVEL     '*LIBL'       QTLIB            10
     C                   Z-ADD     9             QTLEN             5 0
     C                   MOVEL     *BLANKS       QTFIEL            9
     C                   MOVE      *ON           *IN99
     C                   ENDIF
      * Hent firmaopplysninger
     C                   READ      FIRM
     C     FIFIRM        CHAIN     SYFIRMF
      * Oppdater logg status til 'B'
     C     SSN           CHAIN     EDIS                               51
     C     *IN51         CABEQ     *ON           SLUTT
     C     *IN51         IFEQ      '0'
     C                   MOVE      'B'           SST
     C     WMBRS         MULT      80            SMBRS
     C                   UPDATE    EDISR
     C                   ENDIF
      * Hent meldingstype i EDIM6.
      * For sending i TdV IS sendes det en melding per sending
     C     SSN           CHAIN     EDIM6                              51
     C     *IN51         CABEQ     *ON           SLUTT
      * Hent transaksjonsnummer fra CNFF
     C     1             CHAIN     CNFF                               51
     C     *IN51         CABEQ     *ON           SLUTT
     C                   ADD       1             CNRECN
     C                   UPDATE    CNFR
      * Skriv til TdV IS logg fil
     C                   CLEAR                   LOGR
     C                   MOVEL     S0010         LGIDNE
     C                   Z-ADD     CNRECN        LGRECN
     C                   Z-ADD     CNRECN        LGTRID
     C                   MOVEL     SYFUSRID      LGSID
     C                   MOVEL     M0065         LGMSID
     C                   MOVEL     'T'           LGTR
     C                   MOVEL     'A'           LGSTAT
     C                   MOVEL     UDATE         LGDATE
     C                   Z-ADD     SSN           SSNZ
     C                   MOVEL     SSNZ          LGREF2
     C                   MOVEL     SLIB          LGLIB
     C                   MOVEL     SFILE         LGFIL
     C                   MOVEL     SMBR          LGMBR
     C                   TIME                    LGTIME
     C     LGLIB         IFEQ      '*LIBL'
     C                   MOVEL     FILIBF        LGLIB
     C                   ENDIF
     C     LGLIB         IFEQ      *BLANKS
     C     LGFIL         OREQ      *BLANKS
     C     LGMBR         OREQ      *BLANKS
     C                   GOTO      SLUTT
     C                   ENDIF
      *
     C                   WRITE     LOGR
      *
      * Send post til datakø
     C                   MOVEL     LGRECN        QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM                    QTNAME
     C                   PARM                    QTLIB
     C                   PARM                    QTLEN
     C                   PARM                    QTFIEL
      *
     C     SLUTT         TAG
     C                   RETURN
