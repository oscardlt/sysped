     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      HLPPNLGRP(HLPSCR TVI65PNL)
     A                                      HLPTITLE('EDI UTVEKSLINGER')
     A                                      HELP
     A                                      ALTHELP
     A                                      CF03(03 'Avslutt program')
     A                                      PRINT
     A          R TVI70DA
     A                                      OVERLAY
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 31'Statistikk meldinger'
     A                                      COLOR(WHT)
     A                                  1 69'TVI70D A'
     A                                      COLOR(BLU)
     A                                  2 38'Start'
     A                                      COLOR(WHT)
     A                                  4  2'Velg og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  6  2'Avdeling . . . . . . . . . . . . .-
     A                                       . :'
     A            ZAVD      R        B  6 43REFFLD(MAVD EDIM)
     A  31                                  COLOR(RED)
     A                                  6 54'0=Alle'
     A                                  8  2'Fra og med transaksjonsdato  . . .-
     A                                       . :'
     A            ZDTF      R     D  B  8 43REFFLD(MDT)
     A  32                                  COLOR(RED)
     A                                  8 54'≈≈≈≈MMDD'
     A                                  9  4'Tid  . . . . . . . . . . . . . . .-
     A                                       :'
     A            ZTMF      R     D  B  9 43REFFLD(M0068B)
     A  33                                  DSPATR(PC)
     A  33                                  COLOR(RED)
     A                                  9 54'TTMMSS'
     A                                 11  2'Til og med transaksjonsdato  . . .-
     A                                       . :'
     A            ZDTT      R     D  B 11 43REFFLD(MDT)
     A  34                                  COLOR(RED)
     A                                 11 54'≈≈≈≈MMDD'
     A                                 12  4'Tid  . . . . . . . . . . . . . . .-
     A                                       :'
     A            ZTMT      R     D  B 12 43REFFLD(M0068B)
     A  35                                  DSPATR(PC)
     A  35                                  COLOR(RED)
     A                                 12 54'TTMMSS'
     A                                 14  2'Meldingstype (007/015/044) . . . .-
     A                                       . :'
     A            ZTYPE1         3A  B 14 43VALUES('007' '015' '044' '   ')
     A            ZTYPE2         3A  B 14 48VALUES('007' '015' '044' '   ')
     A            ZTYPE3         3A  B 14 53VALUES('007' '015' '044' '   ')
     A                                 23  2'F3=GÂ ut'
     A                                      COLOR(BLU)
     A          R TVI70DB                   SFL
     A            MAVD      R        O 10  2EDTCDE(4)
     A            MTDN      R        O 10  7EDTCDE(4)
     A            M0068A    R        H
     A            M068A6         6  0O 10 15
     A            M0068B    R        O 10 22EDTCDE(4)
     A            M0068C    R        O 10 29EDTCDE(4)
     A            WSIMEX         3A  O 10 32COLOR(PNK)
     A            X1N07          3A  O 10 36COLOR(PNK)
     A            XDT       R        H      REFFLD(MDT)
     A            XDT6           6Y 0O 10 40
     A                                      COLOR(PNK)
     A            XTM            4Y 0O 10 47COLOR(PNK)
     A                                      EDTWRD('  :  ')
     A            Y1N07          3A  O 10 53COLOR(TRQ)
     A            YDT       R        H      REFFLD(MDT)
     A            YDT6           6Y 0O 10 57
     A                                      COLOR(TRQ)
     A            YTM            4Y 0O 10 64COLOR(TRQ)
     A                                      EDTWRD('  :  ')
     A            ≈DGR           3Y 0O 10 71COLOR(YLW)
     A                                      EDTWRD(' 0 ')
     A            ≈TIM           3Y 0O 10 75COLOR(YLW)
     A                                      EDTWRD(' 0 ')
     A            ≈MIN           2Y 0O 10 79COLOR(YLW)
     A                                      EDTWRD('0 ')
     A          R TVI70DC                   SFLCTL(TVI70DB)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      CF13(13 'Skriv ut')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 31'Statistikk meldinger'
     A                                      COLOR(WHT)
     A                                  1 73'TVI70D B'
     A                                      COLOR(BLU)
     A                                  2 38'Valg'
     A                                      COLOR(WHT)
     A                                  4  2'Avdeling:'
     A            ZAVD      R        O  4 12REFFLD(MAVD EDIM)
     A                                      EDTCDE(4)
     A                                  4 18'Fra:'
     A            ZDTF      R        O  4 23REFFLD(MDT)
     A            ZTMF      R        O  4 32REFFLD(MTM)
     A                                      EDTWRD('0 :  :  ')
     A                                  4 42'Til:'
     A            ZDTT      R        O  4 47REFFLD(MDT)
     A            ZTMT      R        O  4 56REFFLD(MTM)
     A                                      EDTWRD('0 :  :  ')
     A                                  6  2'Trykk Enter for Â fortsette:'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  7 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  8 30'_______Sent________'
     A                                      COLOR(PNK)
     A                                  8 52'_____Mottatt____'
     A                                      COLOR(TRQ)
     A                                  8 69'--Svarstid--'
     A                                      COLOR(YLW)
     A                                  9  3'Avd'
     A                                      COLOR(WHT)
     A                                  9  8'Tdeknr'
     A                                      COLOR(WHT)
     A                                  9 15'Dato'
     A                                      COLOR(WHT)
     A                                  9 21'Sekvens'
     A                                      COLOR(WHT)
     A                                  9 29'V'
     A                                      COLOR(WHT)
     A                                  9 32'I/E'
     A                                      COLOR(PNK)
     A                                  9 36'Fun'
     A                                      COLOR(PNK)
     A                                  9 40'Dato'
     A                                      COLOR(PNK)
     A                                  9 47'Tid'
     A                                      COLOR(PNK)
     A                                  9 53'Fun'
     A                                      COLOR(TRQ)
     A                                  9 57'Dato'
     A                                      COLOR(TRQ)
     A                                  9 64'Tid'
     A                                      COLOR(TRQ)
     A                                  9 70'Dag'
     A                                      COLOR(YLW)
     A                                  9 74'Tim'
     A                                      COLOR(YLW)
     A                                  9 78'Min'
     A                                      COLOR(YLW)
     A          R TVI70DD
     A                                      OVERLAY
     A                                 23  2'F3=GÂ ut   F5=Forny   F12=Annuller-
     A                                         F13=Skriv ut'
     A                                      COLOR(BLU)
     A          R TVI70Dÿ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
