      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LLAGJD LLAGJDR)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR LL021PNL)
     A                                      HLPTITLE('ARBEIDE MED UTTAKSLINJER -
     A                                      (MOTLINJER)')
     A                                      ALTHELP
     A                                      HELP
     A                                      CA02(02 'SPØR')
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 68 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            VDGRUP         8Y 0H
     A            VDKJED         8Y 0H
     A            VDMN           7Y 0H
     A            VDVN           3Y 0H
     A            WSNR           1A  I  8  2VALUES(' ' '2' '4' '5')
     A            VDLDAT    R        O  8  4
     A            VDSAE0    R        H
     A            VDSAE1    R        H
     A            VDSAE2    R        H
     A            VDSAE3    R        H
     A            VDLTYP    R        H
     A            VDST      R        H
     A            VDAVD     R        O  8 13EDTCDE(4)
     A            VDOPD     R        O  8 18EDTCDE(4)
     A            VDFBNR    R        O  8 26EDTCDE(4)
     A            VDTEXT    R        H
     A            LHPAK         17   H
     A            WXTEXT        30   O  8 30
     A            VDLANT    R        O  8 61EDTCDE(M)
     A  41                                  COLOR(RED)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'LAG NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(LL021010 LL021PNL)
     A                                      HLPARA(01 001 01 065)
     A          H                           HLPPNLGRP(LL021020 LL021PNL)
     A                                      HLPARA(02 001 18 065)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL021R '
     A                                      COLOR(BLU)
     A N27                              1 11MSGCON(40 YBC1240 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 11MSGCON(40 YBC1241 SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0B  1 42SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(08 YBC1242 SYMSGF)
     A            VAANR         15A  O  3 11COLOR(WHT)
     A                                  3 27MSGCON(07 YBC1243 SYMSGF)
     A            VADATO         8Y 0O  3 35EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                  3 46MSGCON(13 YBC1244 SYMSGF)
     A            VAOANT         7Y 0O  3 60COLOR(WHT)
     A                                      EDTCDE(M)
     A            VAHK      R        O  4 11REFFLD(LLAGJR/VAHK *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A            VAFAR     R        O  4 22REFFLD(LLAGJR/VAFAR *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A            VASIZ     R        O  4 39REFFLD(LLAGJR/VASIZ *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A                                  4 46MSGCON(13 YBC1245 SYMSGF)
     A            VASALD         7Y 0O  4 60COLOR(WHT)
     A                                      EDTCDE(M)
     A N27                              6  2MSGCON(50 YBC1246 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              6  2MSGCON(50 YBC1247 SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(67 YBC1248 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(18)
     A                                      CLRL(01)
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'LAG NY')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N27N30                           1  2MSGCON(39 YBC1249 SYMSGF)
     A                                      COLOR(BLU)
     A  27N30                           1  2MSGCON(39 YBC1250 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 42MSGCON(18 YBC1251 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(LL021010 LL021PNL)
     A                                      HLPARA(01 001 01 065)
     A          H                           HLPPNLGRP(LL021020 LL021PNL)
     A                                      HLPARA(02 001 18 065)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'LL021R '
     A                                      COLOR(BLU)
     A N27                              1 11MSGCON(40 YBC1240 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 11MSGCON(40 YBC1241 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(08 YBC1242 SYMSGF)
     A            VAANR         15A  O  3 11COLOR(WHT)
     A                                  3 27MSGCON(07 YBC1243 SYMSGF)
     A            VADATO         8Y 0O  3 35EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                  3 46MSGCON(13 YBC1244 SYMSGF)
     A            VAOANT         7Y 0O  3 60COLOR(WHT)
     A                                      EDTCDE(M)
     A            VAHK      R        O  4 11REFFLD(LLAGJR/VAHK *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A            VAFAR     R        O  4 22REFFLD(LLAGJR/VAFAR *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A            VASIZ     R        O  4 39REFFLD(LLAGJR/VASIZ *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A                                  4 46MSGCON(13 YBC1245 SYMSGF)
     A            VASALD         7Y 0O  4 60COLOR(WHT)
     A                                      EDTCDE(M)
     A N27                              6  2MSGCON(50 YBC1246 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              6  2MSGCON(50 YBC1247 SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(67 YBC1248 SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(60 YBC1252 SYMSGF)
     A N27N30                          18  2MSGCON(50 YBC1249 SYMSGF)
     A                                      COLOR(BLU)
     A  27N30                          18  2MSGCON(39 YBC1250 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 18 42MSGCON(18 YBC1251 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BILDE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A N51                                  CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(LL021030 LL021PNL)
     A                                      HLPARA(01 001 16 065)
     A          H                           HLPPNLGRP(LL021040 LL021PNL)
     A                                      HLPARA(17 001 18 065)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'LL021R '
     A                                      COLOR(BLU)
     A N27                              1 11MSGCON(040 YBC1240 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 11MSGCON(040 YBC1241 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(008 YBC1242 *LIBL/SYMSGF)
     A            VAANR         15A  O  3 11COLOR(WHT)
     A                                  3 27MSGCON(007 YBC1243 *LIBL/SYMSGF)
     A            VADATO         8Y 0O  3 35EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                  3 46MSGCON(013 YBC1244 *LIBL/SYMSGF)
     A            VAOANT         7Y 0O  3 60COLOR(WHT)
     A                                      EDTCDE(4)
     A            VAHK      R        O  4 11REFFLD(LLAGJR/VAHK *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A            VAFAR     R        O  4 22REFFLD(LLAGJR/VAFAR *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A            VASIZ     R        O  4 39REFFLD(LLAGJR/VASIZ *LIBL/LLAGJF)
     A                                      COLOR(WHT)
     A                                  4 46MSGCON(013 YBC1245 *LIBL/SYMSGF)
     A            VASALD         7Y 0O  4 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(013 YBC1253 *LIBL/SYMSGF)
     A            WSLANT    R        B  7 16REFFLD(LLAGJDR/VDLANT *LIBL/LLAGJD)
     A                                      EDTCDE(L)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                  7 40MSGCON(008 YBC1254 *LIBL/SYMSGF)
     A                                  7 49MSGCON(008 YBC1255 *LIBL/SYMSGF)
     A            WSSAE0    R        B  7 60REFFLD(LLAGJDR/VDSAE0 *LIBL/LLAGJD)
     A                                      EDTCDE(L)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                  8  2MSGCON(013 YBC1256 *LIBL/SYMSGF)
     A            WSLDAT    R        B  8 16REFFLD(LLAGJDR/VDLDAT *LIBL/LLAGJD)
     A  51                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  51                                  COLOR(WHT)
     A                                  8 26MSGCON(010 YBC1257 *LIBL/SYMSGF)
     A                                  8 49MSGCON(010 YBC1258 *LIBL/SYMSGF)
     A            WSSAE1    R        B  8 60REFFLD(LLAGJDR/VDSAE1 *LIBL/LLAGJD)
     A                                      EDTCDE(L)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                  9  2MSGCON(013 YBC1259 *LIBL/SYMSGF)
     A            WSLTYP    R        B  9 16REFFLD(LLAGJDR/VDLTYP *LIBL/LLAGJD)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                  9 49MSGCON(010 YBC1261 *LIBL/SYMSGF)
     A            WSSAE2    R        B  9 60REFFLD(LLAGJDR/VDSAE2 *LIBL/LLAGJD)
     A                                      EDTCDE(L)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                 10  2MSGCON(013 YBC1260 *LIBL/SYMSGF)
     A            WSTEXT    R        B 10 16REFFLD(LLAGJDR/VDTEXT *LIBL/LLAGJD)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                 10 49MSGCON(010 YBC1262 *LIBL/SYMSGF)
     A            WSSAE3    R        B 10 60REFFLD(LLAGJDR/VDSAE3 *LIBL/LLAGJD)
     A                                      EDTCDE(L)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                 11  2MSGCON(013 YBC1263 *LIBL/SYMSGF)
     A            WSDAGE    R        B 11 16REFFLD(LLAGJDR/VDDAGE *LIBL/LLAGJD)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A  51                             13 47MSGCON(018 YBC1264 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(022 YBC1265 *LIBL/SYMSGF)
     A                                 14 28MSGCON(009 YBC1266 *LIBL/SYMSGF)
     A            WSAVD     R        B 14 38REFFLD(LLAGJDR/VDAVD *LIBL/LLAGJD)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A  51                             14 47MSGCON(007 YBC1267 *LIBL/SYMSGF)
     A  51        VDBUSE    R        O 14 55COLOR(WHT)
     A                                 15 28MSGCON(009 YBC1268 *LIBL/SYMSGF)
     A            WSOPD     R        B 15 38REFFLD(LLAGJDR/VDOPD *LIBL/LLAGJD)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A  51                             15 47MSGCON(007 YBC1269 *LIBL/SYMSGF)
     A  51        VDBDAT    R        O 15 55EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                 16 28MSGCON(009 YBC1270 *LIBL/SYMSGF)
     A            WSFBNR    R        B 16 38REFFLD(LLAGJDR/VDFBNR *LIBL/LLAGJD)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A  51                             16 47MSGCON(007 YBC1271 *LIBL/SYMSGF)
     A  51        VDBKL     R        O 16 55EDTWRD('  :  :  ')
     A                                      COLOR(WHT)
     A  52                             17  3MSGCON(040 YBC1697 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  30        MSGTXT        60A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30 51                          18  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30N51                          18  2MSGCON(030 YBC1272 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            LHALN          7Y 0O  9 26COLOR(WHT)
     A                                      EDTCDE(Z)
     A            LHPAK         17A  O 12 16COLOR(WHT)
     A            LHORDR        17A  O 12 34COLOR(WHT)
     A            LHFLN         17A  O 12 52COLOR(WHT)
     A                                 12  2'Pakksed.Kref:'
     A            LHBNA         30A  O 13 16COLOR(WHT)
     A                                 17 47'Recordn'
     A            VAMN           7Y 0O 17 56COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                 13  2'Pak.Mottaker:'
