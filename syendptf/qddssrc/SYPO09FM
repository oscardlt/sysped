     A*%%TS  SD  20151020  084640  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0511A* 01 JOV PTF5 F7=VEDL. FR.TILLEG (VAL/OLJE..)
P6XXXA* 02 YBC PTF6 ÅPNE MIN/MAX FOR TOLLPOST FRAKTSONE
P7075A* 03 JOV PTF7 TILLATE/VISE NEGATIV RABATT (= PÅSLAG)
P7075A* 04 SH  PTF7 AFGREN MANGLET I SUBFIL
PXXXXA* 05 JOV PTF8 VIS M3/LM MM. VED %% OG T
P8068A* 06 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYPO09PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV KUNDERS FR-
     A                                      AKTAVTALER')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPO09'
     A                                      COLOR(BLU)
     A  47                              1 21MSGCON(040 DIV1632 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  46                              1 21MSGCON(040 SHU0449 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  45                              1 21MSGCON(040 SHU1462 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                 11 21MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 36DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 21MSGCON(050 SHU0420 *LIBL/SYMSGF)
     A                                 23 21MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(078 SPA0001 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BILDE02
     A*%%TS  SD  20150730  162757  JOVO        REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF02(02 'Kopi avtaler')
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYPO09'
     A                                      COLOR(BLU)
     A  47                              1 21MSGCON(040 DIV1632 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  46                              1 21MSGCON(040 SHU0449 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  45                              1 21MSGCON(040 SHU1462 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  7  2MSGCON(020 DIV1633 *LIBL/SYMSGF)
     A            WSKUND         8Y 0B  7 24DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                 11  2MSGCON(060 DIV1649 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A                                 13  2MSGCON(020 DIV1633 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A            KPKUNR         8Y 0B 13 24DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A  27                                  DSPATR(ND)
     A  45                             13 37MSGCON(024 JOV1687 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A  47                             13 37MSGCON(024 JOV1686 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A  45        WSKOPKU        1A  B 13 62
     A  27                                  DSPATR(PR)
     A  27                                  DSPATR(ND)
     A  47        WSKOPLE        1A  B 13 62
     A  27                                  DSPATR(PR)
     A  27                                  DSPATR(ND)
     A                                 13 65MSGCON(007 JOV1688 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A                                 14  2MSGCON(020 JOV1300 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A            WSBEHO         1A  B 14 24
     A  27                                  DSPATR(PR)
     A  27                                  DSPATR(ND)
     A                                 14 27MSGCON(040 JOV1301 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A                                 15  2MSGCON(020 SHU1418 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A            WSDABA        10A  B 15 24
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A  27                                  DSPATR(ND)
     A                                 16  2MSGCON(077 JOV1302 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A                                 17  2MSGCON(077 JOV1303 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A                                 18  2MSGCON(077 JOV1304 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A                                 19  2MSGCON(077 JOV1305 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A                                 20  2MSGCON(077 JOV1306 *LIBL/SYMSGF)
     A  27                                  DSPATR(ND)
     A                                 23  2MSGCON(018 DIV1638 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
SV5  A*           AFAKAR    R        H      REFFLD(AVFRHR/AFAKAR *LIBL/AVFRH)
SV5  A*           AFAKMN    R        H      REFFLD(AVFRHR/AFAKMN *LIBL/AVFRH)
SV5  A*           AFAKDG    R        H      REFFLD(AVFRHR/AFAKDG *LIBL/AVFRH)
     A          R SUBFIL                    SFL
     A            AFAKDT    R        H      REFFLD(AVFRHR/AFAKDT *LIBL/AVFRH)
     A            AFGJDT    R        H      REFFLD(AVFRHR/AFGJDT *LIBL/AVFRH)
     A            AFFRTX    R        H      REFFLD(AVFRHR/AFFRTX *LIBL/AVFRH)
     A            AFGE01    R        H      REFFLD(AVFRHR/AFGE01 *LIBL/AVFRH)
     A            AFGE02    R        H      REFFLD(AVFRHR/AFGE02 *LIBL/AVFRH)
     A            AFGE03    R        H      REFFLD(AVFRHR/AFGE03 *LIBL/AVFRH)
     A            AFGE04    R        H      REFFLD(AVFRHR/AFGE04 *LIBL/AVFRH)
     A            AFGE05    R        H      REFFLD(AVFRHR/AFGE05 *LIBL/AVFRH)
     A            AFGE06    R        H      REFFLD(AVFRHR/AFGE06 *LIBL/AVFRH)
     A            AFGE07    R        H      REFFLD(AVFRHR/AFGE07 *LIBL/AVFRH)
     A            AFGE08    R        H      REFFLD(AVFRHR/AFGE08 *LIBL/AVFRH)
     A            AFGE09    R        H      REFFLD(AVFRHR/AFGE09 *LIBL/AVFRH)
     A            AFGE10    R        H      REFFLD(AVFRHR/AFGE10 *LIBL/AVFRH)
     A            AFMIMA    R        H      REFFLD(AVFRHR/AFMIMA *LIBL/AVFRH)
     A            AFGRPR    R        H      REFFLD(AVFRHR/AFGRPR *LIBL/AVFRH)
     A            AFOMRL    R        H      REFFLD(AVFRHR/AFOMRL *LIBL/AVFRH)
     A            AFOMRM    R        H      REFFLD(AVFRHR/AFOMRM *LIBL/AVFRH)
     A            AFPFR1    R        H      REFFLD(AVFRHR/AFPFR1 *LIBL/AVFRH)
     A            AFSFR1    R        H      REFFLD(AVFRHR/AFSFR1 *LIBL/AVFRH)
     A            AFPFRA    R        H      REFFLD(AVFRHR/AFPFRA *LIBL/AVFRH)
     A            AFSFRA    R        H      REFFLD(AVFRHR/AFSFRA *LIBL/AVFRH)
     A            AFPFR2    R        H      REFFLD(AVFRHR/AFPFR2 *LIBL/AVFRH)
     A            AFSFR2    R        H      REFFLD(AVFRHR/AFSFR2 *LIBL/AVFRH)
     A            AFMINI    R        H      REFFLD(AVFRHR/AFMINI *LIBL/AVFRH)
     A            AFMAXI    R        H      REFFLD(AVFRHR/AFMAXI *LIBL/AVFRH)
N04  A            AFGREN    R        H      REFFLD(AVFRHR/AFGREN *LIBL/AVFRH)
     A            WSNR           1A  I  7  3VALUES(' ' '2' '3' '4' '5' '9')
     A            AFLKFR    R        O  7  5REFFLD(AVFRHR/AFLKFR *LIBL/AVFRH)
     A            AFPNF1    R        O  7  8REFFLD(AVFRHR/AFPNF1 *LIBL/AVFRH)
     A            AFPNF2    R        O  7 14REFFLD(AVFRHR/AFPNF2 *LIBL/AVFRH)
     A                                  7 20'->'
     A            AFLKTI    R        O  7 23REFFLD(AVFRHR/AFLKTI *LIBL/AVFRH)
     A            AFPNT1    R        O  7 26REFFLD(AVFRHR/AFPNT1 *LIBL/AVFRH)
     A            AFPNT2    R        O  7 32REFFLD(AVFRHR/AFPNT2 *LIBL/AVFRH)
     A            AFFRAN    R        O  7 38REFFLD(AVFRHR/AFFRAN *LIBL/AVFRH)
     A            AFGEBY    R        O  7 42REFFLD(AVFRHR/AFGEBY *LIBL/AVFRH)
     A            AFVALK    R        O  7 46REFFLD(AVFRHR/AFVALK *LIBL/AVFRH)
     A            AFPROD    R        O  7 50REFFLD(AVFRHR/AFPROD *LIBL/AVFRH)
     A            AFAVDE    R        O  7 52REFFLD(AVFRHR/AFAVDE *LIBL/AVFRH)
     A            AFBFR1    R        O  7 58REFFLD(AVFRHR/AFBFR1 *LIBL/AVFRH)
     A            AFBFRA    R        O  7 62REFFLD(AVFRHR/AFBFRA *LIBL/AVFRH)
     A            AFBFR2    R        O  7 66REFFLD(AVFRHR/AFBFR2 *LIBL/AVFRH)
     A            AFTYPE    R        O  7 69REFFLD(AVFRHR/AFTYPE *LIBL/AVFRH)
     A            AFAVNR    R        O  7 71REFFLD(AVFRHR/AFAVNR *LIBL/AVFRH)
     A                                      EDTCDE(4)
     A            AFFR30        30   O  7 81
     A            WSFRAD         6Y 0O  7112EDTWRD('  .  .  ')
     A            WSTILD         6Y 0O  7123EDTWRD('  .  .  ')
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20151020  084640  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPPNLGRP(SYPO09020 SYPO09PNL)
     A                                      HLPARA(01 001 20 080)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYPO09'
     A                                      COLOR(BLU)
     A  47                              1 21MSGCON(040 DIV1632 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  46                              1 21MSGCON(040 SHU0449 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  45                              1 21MSGCON(040 SHU1462 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            AFKUND    R        O  2 19REFFLD(AVFRHR/AFKUND *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 29COLOR(WHT)
     A            ADR3          24A  O  3 29COLOR(WHT)
     A N27                              4 34MSGCON(030 DIV0034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              4 34MSGCON(030 SHU0450 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 65'9=Snu'
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(130 SHU1607 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF07(07 'FR,TILLEGG')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      CF10(10 'gebyravtaler')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N31                              3  3MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              3 19MSGCON(008 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              3 33MSGCON(010 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              3 50MSGCON(006 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  31                              3 42MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62'F10=Gebyravtale'
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                              2 50MSGCON(010 JOV1028 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3 70MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            W2LKFR    R        I  3  8REFFLD(AVFRHR/AFLKFR *LIBL/AVFRH)
     A            W2PNF1         5   I  3 11
     A            W2PNF2         5   I  3 17
     A            W2LKT1    R        I  3 29REFFLD(AVFRHR/AFLKFR *LIBL/AVFRH)
     A            W2PNT1         5   I  3 32
     A            W2PNT2         5   I  3 38
     A            W2FRAN         3   I  3 45
     A            W2GEBY         3   I  3 51
     A                                  2  2'SØK:  Lk Fra   Til         Lk Fra -
     A                                        Til    Fran  Geb'
     A                                  3 24'-->'
      *
     A          R VAREL2A
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(035 SHU0102 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2AVDE    R        B  2 42REFFLD(AVFRHR/AFAVDE *LIBL/AVFRH)
     A                                      EDTCDE(4)
     A            W2PROD    R        B  2 39REFFLD(AVFRHR/AFPROD *LIBL/AVFRH)
     A            W2LKFR    R        B  2 48REFFLD(AVFRHR/AFLKFR *LIBL/AVFRH)
     A            W2PNF1    R        B  2 52REFFLD(AVFRHR/AFPNF1 *LIBL/AVFRH)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  3MSGCON(070 SHU0101 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A            AFLKFR    R        O  2  5REFFLD(AVFRHR/AFLKFR *LIBL/AVFRH)
     A            AFPNF1    R        O  2  8REFFLD(AVFRHR/AFPNF1 *LIBL/AVFRH)
     A            AFPNF2    R        O  2 14REFFLD(AVFRHR/AFPNF2 *LIBL/AVFRH)
     A                                  2 20'->'
     A            AFLKTI    R        O  2 23REFFLD(AVFRHR/AFLKTI *LIBL/AVFRH)
     A            AFPNT1    R        O  2 26REFFLD(AVFRHR/AFPNT1 *LIBL/AVFRH)
     A            AFPNT2    R        O  2 32REFFLD(AVFRHR/AFPNT2 *LIBL/AVFRH)
     A            AFFRAN    R        O  2 38REFFLD(AVFRHR/AFFRAN *LIBL/AVFRH)
     A            AFGEBY    R        O  2 42REFFLD(AVFRHR/AFGEBY *LIBL/AVFRH)
     A            AFVALK    R        O  2 46REFFLD(AVFRHR/AFVALK *LIBL/AVFRH)
     A            AFPROD    R        O  2 50REFFLD(AVFRHR/AFPROD *LIBL/AVFRH)
     A            AFAVDE    R        O  2 52REFFLD(AVFRHR/AFAVDE *LIBL/AVFRH)
     A            AFBFR1    R        O  2 58REFFLD(AVFRHR/AFBFR1 *LIBL/AVFRH)
     A            AFBFRA    R        O  2 62REFFLD(AVFRHR/AFBFRA *LIBL/AVFRH)
     A            AFBFR2    R        O  2 66REFFLD(AVFRHR/AFBFR2 *LIBL/AVFRH)
     A            AFTYPE    R        O  2 69REFFLD(AVFRHR/AFTYPE *LIBL/AVFRH)
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SYPO09'
     A                                      COLOR(BLU)
     A  47                              1 21MSGCON(040 DIV1632 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  46                              1 21MSGCON(040 SHU0449 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  45                              1 21MSGCON(040 SHU1462 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(020 DIV1633 *LIBL/SYMSGF)
     A            AFKUND    R        O  6 25REFFLD(AVFRHR/AFKUND *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            KNAVN         30   O  7 25
     A            ADR1          30   O  8 25
     A            ADR2          30   O  9 25
     A            POSTNR         4  0O 10 25
     A            ADR3          24   O 10 31
     A                                 15 10MSGCON(060 SHU0100 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A N31                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                             23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                             23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  31                             23  2MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  31                             23 22MSGCON(018 DIV1965 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  31                             23 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R VARELI
     A                                      CF02(02 'Kopi avtaler')
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
S06  A*         H                           HLPDOC('SYPO09020' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09020 SYPO09PNL)
     A                                      HLPARA(01 001 05 080)
S06  A*         H                           HLPDOC('SYPO09030' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09030 SYPO09PNL)
     A                                      HLPARA(06 001 06 080)
S06  A*         H                           HLPDOC('SYPO09040' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09040 SYPO09PNL)
     A                                      HLPARA(07 001 07 080)
S06  A*         H                           HLPDOC('SYPO09050' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09050 SYPO09PNL)
     A                                      HLPARA(08 001 08 080)
S06  A*         H                           HLPDOC('SYPO09060' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09060 SYPO09PNL)
     A                                      HLPARA(09 001 09 030)
S06  A*         H                           HLPDOC('SYPO09070' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09070 SYPO09PNL)
     A                                      HLPARA(09 031 09 080)
S06  A*         H                           HLPDOC('SYPO09080' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09080 SYPO09PNL)
     A                                      HLPARA(10 001 10 080)
S06  A*         H                           HLPDOC('SYPO09090' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09090 SYPO09PNL)
     A                                      HLPARA(11 001 11 080)
S06  A*         H                           HLPDOC('SYPO09100' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09100 SYPO09PNL)
     A                                      HLPARA(12 001 12 080)
S06  A*         H                           HLPDOC('SYPO09110' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09110 SYPO09PNL)
     A                                      HLPARA(13 001 13 080)
S06  A*         H                           HLPDOC('SYPO09120' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09120 SYPO09PNL)
     A                                      HLPARA(14 001 14 080)
S06  A*         H                           HLPDOC('SYPO09130' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09130 SYPO09PNL)
     A                                      HLPARA(15 001 15 080)
S06  A*         H                           HLPDOC('SYPO09140' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09140 SYPO09PNL)
     A                                      HLPARA(16 001 16 080)
S06  A*         H                           HLPDOC('SYPO09150' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09150 SYPO09PNL)
     A                                      HLPARA(17 001 17 080)
S06  A*         H                           HLPDOC('SYPO09160' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09160 SYPO09PNL)
     A                                      HLPARA(18 001 18 080)
S06  A*         H                           HLPDOC('SYPO09170' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09170 SYPO09PNL)
     A                                      HLPARA(19 001 19 080)
S06  A*         H                           HLPDOC('SYPO09180' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09180 SYPO09PNL)
     A                                      HLPARA(20 001 20 080)
S06  A*         H                           HLPDOC('SYPO09190' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09190 SYPO09PNL)
     A                                      HLPARA(21 001 21 080)
S06  A*         H                           HLPDOC('SYPO09200' 'SYPO09' 'SY400L-
S06  A*                                     O/MAINT')
N06  A          H                           HLPPNLGRP(SYPO09200 SYPO09PNL)
     A                                      HLPARA(22 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPO09'
     A                                      COLOR(BLU)
     A  47                              1 21MSGCON(040 DIV1632 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  46                              1 21MSGCON(040 SHU0449 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  45                              1 21MSGCON(040 SHU1462 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(020 DIV1633 *LIBL/SYMSGF)
     A            WSKUND    R        O  2 24REFFLD(AVFRHR/AFKUND *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 34COLOR(WHT)
     A                                  2 70TIME
     A                                  3  2MSGCON(020 SHU0098 *LIBL/SYMSGF)
     A            AFAVNR    R        O  3 23REFFLD(AVFRHR/AFAVNR *LIBL/AVFRH)
     A                                      COLOR(WHT)
     A            ADR1          30A  O  3 34COLOR(WHT)
     A            ADR2          30A  O  4 34COLOR(WHT)
     A            POSTNR         4S 0O  5 34COLOR(WHT)
     A            ADR3          24A  O  5 40COLOR(WHT)
     A                                  6  2MSGCON(020 SHU0141 *LIBL/SYMSGF)
     A            WSFRTX        50A  B  6 23CHECK(LC)
     A  27                                  DSPATR(PR)
     A*                                 7  2MSGCON(020 SHU0083 *LIBL/SYMSGF)
     A  28                              7  2MSGCON(020 SHU0083 *LIBL/SYMSGF)
     A N28                              7  2MSGCON(020 JOV0778 *LIBL/SYMSGF)
     A            WSLKFR    R        B  7 23REFFLD(AVFRHR/AFLKFR *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A            WSPNF1    R        B  7 26REFFLD(AVFRHR/AFPNF1 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A            WSPNF2    R        B  7 32REFFLD(AVFRHR/AFPNF2 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A  28                              8  2MSGCON(020 SHU0084 *LIBL/SYMSGF)
     A  28        WSLKTI    R        B  8 23REFFLD(AVFRHR/AFLKTI *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A  28        WSPNT1    R        B  8 26REFFLD(AVFRHR/AFPNT1 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A  28        WSPNT2    R        B  8 32REFFLD(AVFRHR/AFPNT2 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A  28                              9  2MSGCON(020 SHU0085 *LIBL/SYMSGF)
     A  28        WSFRAN    R        B  9 23REFFLD(AVFRHR/AFFRAN *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
S05  A*N28                              9  2MSGCON(075 JOV0779 *LIBL/SYMSGF)
N05  A N28N29                           9  2MSGCON(075 JOV0779 *LIBL/SYMSGF)
N05  A N28 29                           9  2MSGCON(075 JOV1339 *LIBL/SYMSGF)
     A  28                              9 31MSGCON(020 YBC0061 *LIBL/SYMSGF)
     A  28        WSGEBY    R        B  9 52REFFLD(AVFRHR/AFGEBY *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A                                 10  2MSGCON(020 SHU0086 *LIBL/SYMSGF)
     A            WSBFRA    R        B 10 23REFFLD(AVFRHR/AFBFRA *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A            WSPFRA    R        B 10 28REFFLD(AVFRHR/AFPFRA *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
S03  A*                                     EDTCDE(4)
S03  A*                                10 35'%'
N03  A                                      EDTCDE(M)
N03  A                                 10 36'%'
     A  28                             10 39MSGCON(012 SHU0087 *LIBL/SYMSGF)
     A  28        WSSFRA    R        B 10 52REFFLD(AVFRHR/AFSFRA *LIBL/AVFRH)
     A  27                                  DSPATR(PR)
     A  67                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  28                             11  2MSGCON(020 SHU0088 *LIBL/SYMSGF)
     A  28        WSBFR1    R        B 11 23REFFLD(AVFRHR/AFBFR1 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  85                                  DSPATR(RI)
     A  28        WSPFR1    R        B 11 28REFFLD(AVFRHR/AFPFR1 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
S03  A*                                     EDTCDE(4)
S03  A* 28                             11 35'%'
N03  A                                      EDTCDE(M)
N03  A  28                             11 36'%'
     A  28                             11 39MSGCON(012 SHU0087 *LIBL/SYMSGF)
     A  28        WSSFR1    R        B 11 52REFFLD(AVFRHR/AFSFR1 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  66                                  DSPATR(RI)
     A  28                             12  2MSGCON(020 SHU0089 *LIBL/SYMSGF)
     A  28        WSBFR2    R        B 12 23REFFLD(AVFRHR/AFBFR2 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  84                                  DSPATR(RI)
     A  28        WSPFR2    R        B 12 28REFFLD(AVFRHR/AFPFR2 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
S03  A*                                     EDTCDE(4)
S03  A* 28                             12 35'%'
S03  A                                      EDTCDE(M)
S03  A  28                             12 36'%'
     A  28                             12 39MSGCON(012 SHU0087 *LIBL/SYMSGF)
     A  28        WSSFR2    R        B 12 52REFFLD(AVFRHR/AFSFR2 *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  65                                  DSPATR(RI)
     A  28                             13  2MSGCON(020 SHU0090 *LIBL/SYMSGF)
     A  28        WSVALK    R        B 13 23REFFLD(AVFRHR/AFVALK *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  83                                  DSPATR(RI)
S02  A* 28                             14  2MSGCON(020 SHU0091 *LIBL/SYMSGF)
S02  A* 28        WSMINI    R        B 14 23REFFLD(AVFRHR/AFMINI *LIBL/AVFRH)
N02  A                                 14  2MSGCON(020 SHU0091 *LIBL/SYMSGF)
N02  A            WSMINI    R        B 14 23REFFLD(AVFRHR/AFMINI *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
S02  A* 28                             14 34'/'
S02  A* 28        WSMAXI    R        B 14 36REFFLD(AVFRHR/AFMAXI *LIBL/AVFRH)
N02  A                                 14 34'/'
N02  A            WSMAXI    R        B 14 36REFFLD(AVFRHR/AFMAXI *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  28                             14 47'/'
     A  28        WSGRPR    R        B 14 49REFFLD(AVFRHR/AFGRPR *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(020 SHU0092 *LIBL/SYMSGF)
     A            WSPROD    R        B 15 23REFFLD(AVFRHR/AFPROD *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  82                                  DSPATR(RI)
     A                                 16  2MSGCON(020 SHU0093 *LIBL/SYMSGF)
     A            WSAVDE    R        B 16 23REFFLD(AVFRHR/AFAVDE *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  81                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A  28                             17  2MSGCON(020 SHU0094 *LIBL/SYMSGF)
     A  28        WSTYPE    R        B 17 23REFFLD(AVFRHR/AFTYPE *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  80                                  DSPATR(RI)
     A  28                             17 26'(X,L,M,V,B)  '
     A  28                             18  2MSGCON(020 SHU0095 *LIBL/SYMSGF)
     A  28        WSMIMA    R        B 18 23REFFLD(AVFRHR/AFMIMA *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  28        WSGREN    R        B 18 25REFFLD(AVFRHR/AFGREN *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
S05  A* 28                             19  2MSGCON(020 SHU0096 *LIBL/SYMSGF)
S05  A* 28        WSOMRL    R        B 19 23REFFLD(AVFRHR/AFOMRL *LIBL/AVFRH)
N05  A  29
S05  AO 28                             19  2MSGCON(020 SHU0096 *LIBL/SYMSGF)
N05  A  29
N05  AO 28        WSOMRL    R        B 19 23REFFLD(AVFRHR/AFOMRL *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
S05  A* 28                             20  2MSGCON(020 SHU0097 *LIBL/SYMSGF)
S05  A* 28        WSOMRM    R        B 20 23REFFLD(AVFRHR/AFOMRM *LIBL/AVFRH)
N05  A  29
N05  AO 28                             20  2MSGCON(020 SHU0097 *LIBL/SYMSGF)
N05  A  29
N05  AO 28        WSOMRM    R        B 20 23REFFLD(AVFRHR/AFOMRM *LIBL/AVFRH)
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
S05  A* 28                             20 29MSGCON(030 SHU0426 *LIBL/SYMSGF)
S05  A* 28        WSOMR1         5Y 0B 20 61EDTCDE(4)
S05  A* 27                                  DSPATR(PR)
S05  A* 28        WSOMR2         5Y 0B 20 69EDTCDE(4)
S05  A* 27                                  DSPATR(PR)
N05  A  29
N05  AO 28                             20 29MSGCON(030 SHU0426 *LIBL/SYMSGF)
N05  A  29
N05  AO 28        WSOMR1         5Y 0B 20 61EDTCDE(4)
N05  A  27                                  DSPATR(PR)
N05  A  29
N05  AO 28        WSOMR2         5Y 0B 20 69EDTCDE(4)
N05  A  27                                  DSPATR(PR)
     A  28                             21  2MSGCON(020 SHU0121 *LIBL/SYMSGF)
     A  28        WSGE01         3A  B 21 23DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  79                                  DSPATR(RI)
     A  28        WSGE02         3A  B 21 27DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  78                                  DSPATR(RI)
     A  28        WSGE03         3A  B 21 31DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  77                                  DSPATR(RI)
     A  28        WSGE04         3A  B 21 35DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  76                                  DSPATR(RI)
     A  28        WSGE05         3A  B 21 39DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  75                                  DSPATR(RI)
     A  28        WSGE06         3A  B 21 43DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  74                                  DSPATR(RI)
     A  28        WSGE07         3A  B 21 47DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  73                                  DSPATR(RI)
     A  28        WSGE08         3A  B 21 51DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  72                                  DSPATR(RI)
     A  28        WSGE09         3A  B 21 55DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  71                                  DSPATR(RI)
     A  28        WSGE10         3A  B 21 59DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  70                                  DSPATR(RI)
     A  28                             22  2MSGCON(020 SHU0140 *LIBL/SYMSGF)
     A  28        WSFDAT         6Y 0B 22 23EDTCDE(4)
     A  27                                  DSPATR(PR)
     A  69                                  DSPATR(RI)
     A  28        WSTDAT         6Y 0B 22 32EDTCDE(4)
     A  27                                  DSPATR(PR)
     A  68                                  DSPATR(RI)
     A                                 23 22MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BEKREFT
     A                                      WINDOW(10 28 6 42 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF23(23 'BEKREFTE')
     A                                      CF12(12)
     A                                  1  2'SYSPED/SYPO09'
     A                                      COLOR(BLU)
     A  90                              3  2MSGCON(040 YBC0381 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A  89                              3  2MSGCON(040 YBC0383 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A  88                              3  2MSGCON(040 YBC0384 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  4  2MSGCON(040 YBC0382 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  6  2MSGCON(036 YBC0366 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
