********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB PTF12 Feil filnavn
********************************************************************
     H DFTACTGRP(*NO)
      *
      ***********************************************************
      *** PROGRAM SKAL KONVERTER FILER FRA TVINN              ***
      *** 1. Inn parameter UPGM -> Bestmme path og konv.pgm   ***
      *** 2. Ut paramter UPATH og UPGM (pgm for konvertering) ***
      *** 3. Oppdat post i EDIS (sendingslogg) og EDISX (path)***
      ***********************************************************
      *
     FEDIC      UF   E             DISK
     FEDIS      O  A E             DISK
     FEDISX     O  A E             DISK
      *
     D                sds
     D ZUSER                 254    263
     D                 ds
     D XMBR                    1     10
     D XKD                     1      1
     D XSN                     2     10  0
     D XSN2                    2     10
      *  Prototype for syti00r
     d syti00r         pr
     d usn_                                like(usn)
     d upath_                              like(upath)
     d upgm_                               like(upgm)
      *  *ENTRY Interface for Main Procedure
     d syti00r         pi
     d usn                            9a
     d upath                        256a
     d upgm                          10a
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d wdt             s              8
     d wtm             s              6

     D EDIDTA          DS           256    DTAARA('EDIDTA')
     D  UELIBF               171    180

      *----------------------------------------------------------------------
      /free

       exsr init;

       chain 1 edic;
       csn = csn + 1;
       update edicr;
       xkd = 'K';
       xsn = csn;

       sxprodm  = xmbr;
       sxifsobj = upath;
       //write edisr;

       s0004  = 'TVINN';
       s0010  = 'SYSPED';
       select;
       when upgm = 'SYTI01C1';
         s0020  = 'VALUTAKURS';
       when upgm = 'SYTI02C1';
         s0020  = 'FF-AVGIFT';
       when upgm = 'SYTI03C1';
         s0020  = 'REF.KODE-IMP';
       when upgm = 'SYTI04C1';
         s0020  = 'LANDKODE';
       when upgm = 'SYTI05C1';
         s0020  = 'LANDGRUPPE';
       when upgm = 'SYTI06C1';
         s0020  = 'RÅVARETOLL';
       when upgm = 'SYTI07C1';
         s0020  = 'TOLLSATS-IMP';
       when upgm = 'SYTI08C1';
         s0020  = 'AVGIFT-IMP';
       when upgm = 'SYTI09C1';
         s0020  = 'REF.KODE-EKSP';
       endsl;
       s0026  = s0020;
       s0036  = 1;
       sdt    = %dec(wdt:8:0);
       stm    = %dec(wtm:6:0);
       slib   = '*LIBL';
       sfile  = '*IFS';
       smbr   = xmbr;
       ssn    = csn;
       ssr    = 'R';
       sst    = 'C';
       write edisr;

       sxprodm  = xmbr;
       sxifsobj = upath;
       write edisxr;

       usn    = xsn2;

       *inlr = *on;
       return;

       //**********************************************
       begsr init;
       //**********************************************

         in edidta;
         select;
         when upgm = 'SYTI01C1';
           upath = '/' + %trim(UELIBF) + '/TOLL/valutakurs.xml';
         when upgm = 'SYTI02C1';
           upath = '/' + %trim(UELIBF) + '/TOLL/utfoerselsavgift.xml';
         when upgm = 'SYTI03C1';
           upath = '/' + %trim(UELIBF) + '/TOLL/innfoerselsreferanse.xml';
         when upgm = 'SYTI04C1';
           upath = '/' + %trim(UELIBF) + '/TOLL/medlemsland.xml';
         when upgm = 'SYTI05C1';
           upath = '/' + %trim(UELIBF) + '/TOLL/landgruppe.xml';
         when upgm = 'SYTI06C1';
S01//      upath = '/' + %trim(UELIBF) + '/TOLL/raavaretollsats.xml';
N01        upath = '/' + %trim(UELIBF) + '/TOLL/raavaretollavgiftssats.xml';
         when upgm = 'SYTI07C1';
S01//      upath = '/' + %trim(UELIBF) + '/TOLL/tollsats.xml';
N01        upath = '/' + %trim(UELIBF) + '/TOLL/tollavgiftssats.xml';
         when upgm = 'SYTI08C1';
           upath = '/' + %trim(UELIBF) + '/TOLL/innfoerselsavgift.xml';
         when upgm = 'SYTI09C1';
           upath = '/' + %trim(UELIBF) + '/TOLL/utfoerselsreferanse.xml';
         endsl;

         syge15c ( wdt : wtm );

       endsr;

      /end-free
