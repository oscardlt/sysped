      *
      ***********************************************************
      *** PROGRAM FOR Å SØKE BÅ MERKE/MODELL I BILFORTOLLING  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 02 04 07-11 13-24
      *** LEDIGE INDIKATORER 31-90 95-97 99
      ***********************************************************
      *
     FSADBIL    UF   E           K DISK
     FSAD061FQ  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      * BEVAR SIST BENYTTEDE SØK .....
     C     W2MERK        IFNE      *BLANKS
     C                   MOVE      W2MERK        GMMERK            7
     C                   MOVE      W2MOD1        GMMOD1           10
     C                   ELSE
      * NYTT SØK IKKE ANGITT (FRISK OPP E.L.) - BENYTT SISTE SØK...
     C                   MOVE      GMMERK        W2MERK
     C                   MOVE      GMMOD1        W2MOD1
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LESKEY        SETLL     SADBIL
     C     START1        TAG
     C                   READ(N)   SADBIL                                 94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        5
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ(N)   SADBIL                                 94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     *IN29         IFEQ      '0'
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   MOVE      '1'           *IN29
     C                   ELSE
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C*
     C                   SETOFF                                       9830
     C   03
     COR 12              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      * NYTT SØK..
     C     W2MERK        IFNE      *BLANKS
     C                   GOTO      START
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   99              GOTO      START
     C   98              GOTO      START3
     C                   GOTO      SLUTT
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       99
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'
     C                   MOVE      SBPACB        UMERK
     C                   MOVE      SBTX1C        UMOD1
     C                   MOVE      SBTX2C        UMOD2
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     MALKEY        CHAIN     SADBIL                             33
     C  N33              DELETE    SADBILR
     C                   SETON                                        99
     C                   END
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     LESKEY        KLIST
     C                   KFLD                    W2MERK           15
     C                   KFLD                    W2MOD1           10
     C*
     C     MALKEY        KLIST
     C                   KFLD                    SBPACB
     C                   KFLD                    SBTX1C
     C                   KFLD                    SBTX2C
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UMERK            15
     C                   PARM                    UMOD1            10
     C                   PARM                    UMOD2            10
      *
     C                   MOVE      UMERK         W2MERK
     C                   MOVE      UMOD1         W2MOD1
      *
     C                   WRITE     WINDOWSZ
     C*
     C                   ENDSR
     C*
