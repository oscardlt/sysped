********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF12 HERFK innført
12XXX * 02 JOV PTF12 kjøper bet.og HERFK IKKE md i layout -HERFK->HERFA (= som før HERFA=fakturaref)
********************************************************************
     FHEADF     IF   E           K DISK
     FCUFLD     IF   E           K DISK
     FCUFLD1    IF   E           K DISK
     F                                     RENAME(CUFLDR:CUFLD1R)
     FSYFLD     IF   E           K DISK
     FFAK8      IF   E           K DISK
     FKODTOTY   IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FCUSAPR    IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODWL01   IF   E           K DISK
      *ALLE TENKELIGE FELT SOM KAN INNGÅ I UTSKRIFTEN (MAX FELTLEN. 79)
     D FL              S             79    DIM(100)
      *ARBEIDSFELT FOR MOVEA OPERASJON INN TIL LINJA
      *ARBEIDSFELT FOR EDITERING AV FELT
      *ARBEIDSFELT MOTTA DEN FERDIGE EDITERTE STRENGEN
     D WN              S              1    DIM(79)                              WORKFIELD EDIT
      *FERDIG KOMPONERTE LINJER 1,2,3
     D LI              S             79    DIM(3)                               Output string
     D                 DS
     D  WL                     1     79
     D                                     DIM(79)                              WORK LINE
     D  WORKL                  1     79
     D                 DS
     D  WE                     1     79
     D                                     DIM(79)                              WORKFIELD EDIT
     D  WORKE                  1     79
     D                 DS
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D  HEDTOP                 1      8  0
     D                 DS
     D  TRSFÅ                  1      4  0
     D  TRSFM                  5      6  0
     D  TRSFD                  7      8  0
     D  TRSDFD                 1      8  0
     D                 DS
     D  TRSTÅ                  1      4  0
     D  TRSTM                  5      6  0
     D  TRSTD                  7      8  0
     D  TRSDTD                 1      8  0
     D                 DS
     D  XDTD                   1      2  0
     D  XDTM                   3      4  0
     D  XDTÅ                   5      6  0
     D  DSDATO                 1      6  0
     D                 DS
     D  FADATÅ                 1      4  0
     D  FADATM                 5      6  0
     D  FADATD                 7      8  0
     D  FADATO                 1      8  0
     D                 DS
     D  HXSNDN                 1     20
     D  HXSND04_20             4     20
     D  HXSND11_20            11     20
      *
     C                   EXSR      INIT
      *
     C     HEADFK        CHAIN     HEADF                              33
     C     *IN33         IFEQ      *OFF
     C                   EXSR      INIARR
     C                   EXSR      MAIN
     C                   END
      *
      * Utparam. 3 linjer med ferdige data
     C                   MOVE      LI(1)         ULI1
     C                   MOVE      LI(2)         ULI2
     C                   MOVE      LI(3)         ULI3
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UKUNDE
     C                   PARM                    UDOKUM
     C                   PARM                    UAVD
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFAKT
     C                   PARM                    UVAL
     C                   PARM                    ULI1             79
     C                   PARM                    ULI2             79
     C                   PARM                    ULI3             79
     C                   PARM                    UKUNBEL
     C                   PARM                    Innland           1
     C                   PARM                    UFlag1            1
     C                   PARM                    UFlag2            1
      *
     C                   CLEAR                   HEADFR
     C                   CLEAR                   CUFLDR
     C                   CLEAR                   KOOTYR
     C                   CLEAR                   KOFRR
     C                   MOVEA     *BLANKS       LI
     C                   MOVE      *ZEROS        FADATO
      *
     C     *LIKE         DEFINE    CFKEY1        UKUNDE
     C     *LIKE         DEFINE    CFKEY1        UKUNBEL
     C     *LIKE         DEFINE    CFKEY2        UDOKUM
     C     *LIKE         DEFINE    HEAVD         UAVD
     C     *LIKE         DEFINE    FAFAKT        UFAKT
     C     *LIKE         DEFINE    HEVALK        UVAL
     C     *LIKE         DEFINE    FITAX         WITAX
     C     *LIKE         DEFINE    TONOMO        WONOMO
     C     *LIKE         DEFINE    FABELV        DNOPL
      *
     C                   Z-ADD     UAVD          HEAVD
     C                   Z-ADD     UOPD          HEOPD
     C                   MOVE      UKUNDE        CFKEY1
     C                   MOVE      UDOKUM        CFKEY2
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     CUFLDK        KLIST
     C                   KFLD                    CFKEY1
     C                   KFLD                    CFKEY2
     C                   KFLD                    CFL
     C     CUFL1K        KLIST
     C                   KFLD                    CFKEY1
     C                   KFLD                    CFKEY2
     C                   KFLD                    CFN
      *
     C     FAK8K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    UFAKT
     C                   KFLD                    UKUNBEL
     C                   KFLD                    UVAL
     C     KODOTK        KLIST
     C                   KFLD                    YOTUNI
     C                   KFLD                    HEOT
     C                   MOVE      'OTY'         YOTUNI            3
     C     KODFRK        KLIST
     C                   KFLD                    YFRUNI
     C                   KFLD                    HEFR
     C                   MOVE      'FR'          YFRUNI            2
     C     FRIKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YFSKOD            3
     C     KODWKY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WMVAKD
     C                   KFLD                    FAKDM
     C                   MOVE      *BLANKS       WMVAKD            2
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
      *
     C                   ENDSR
      *
     C*-------------------------------------------
     C     INIARR        BEGSR
     C*"""""""""""""""""""""""""""""""""""""""""""
N02   * Kjøper betaler og HERFK  IKKE definert i spesial-layout = Vis i HERFA (som før V12..)
N02  c                   if        HEUR = 'H' and HERFK<> *blanks
N02  C     FAK8K         Chain     FAK8
N02  c                   if        %found and FASK = 'K'
N02  C                   Z-ADD     17            CFN                            HERFK
N02  C     CUFL1K        CHAIN     CUFLD1
N02  c                   if        not %found
N02  c                   eval      HERFA = HERFK
N02  c                   endif
N02  c                   endif
N02  c                   endif
N02
     C                   MOVE      HEDTD         XDTD
     C                   MOVE      HEDTM         XDTM
     C                   MOVE      HEDTÅ         XDTÅ
     C                   MOVEA     *BLANKS       FL
      * Rene alfa-felt
     C                   MOVEL     HESG          FL(6)
     c     HXSND11_20    ifne      *Blanks
     C                   MOVEL     HXSND04_20    FL(7)
     C                   else
     C                   MOVEL     HXSNDN        FL(7)
     C                   end
     C                   MOVEL     HEGN          FL(10)
     C                   MOVEL     HEPOS1        FL(12)
     C                   MOVEL     HERFA         FL(16)
N01  C                   MOVEL     HERFK         FL(17)
     C                   MOVEL     HESDF         FL(20)
     C                   MOVEL     HESDT         FL(22)
     C                   MOVEL     HEGM1         FL(34)
     C                   MOVEL     HEGM2         FL(36)
     C                   MOVEL     HEVS1         FL(38)
     C                   MOVEL     HEVS2         FL(40)
     C                   MOVEL     HEOT          FL(42)
     C     KODOTK        CHAIN     KODTOTY                            33
     C                   MOVEL     KO1NTX        FL(44)
     C                   MOVEL     HEFR          FL(46)
     C     KODFRK        CHAIN     KODTFR                             33
     C                   MOVEL     KFRNTX        FL(48)
     C                   MOVEL     HENAS         FL(50)
     C                   MOVEL     HEADS1        FL(52)
     C                   MOVEL     HEADS2        FL(54)
     C                   MOVEL     HEADS3        FL(56)
     C                   MOVEL     HENASF        FL(58)
     C                   MOVEL     HENAK         FL(60)
     C                   MOVEL     HEADK1        FL(62)
     C                   MOVEL     HEADK2        FL(64)
     C                   MOVEL     HEADK3        FL(66)
     C                   MOVEL     HENAKF        FL(68)
     C                   MOVEL     HEHAWB        FL(70)
     C                   MOVEL     HETLE         FL(74)
     C                   MOVEL     HETLL         FL(76)
     C                   MOVEL     HEDTMO        FL(78)
     C                   MOVEL     HESGM         FL(80)
     C                   MOVEL     TRVERV        FL(81)
     C                   MOVEL     HESDL         FL(82)
      * Fri søkevei
     C     UKUNDE        CHAIN     CUSAPR                             33
     C                   MOVE      *BLANKS       GMFSKO            3
      *     -  MAX 5 STK. (HVIS LIK FOREGÅENDE - LES VIDERE..)
     C     CPFS01        IFNE      *BLANKS
     C                   MOVE      CPFS01        YFSKOD
     C     FRIKEY        CHAIN     FRISOK                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      YFSKOD        GMFSKO
     C                   MOVEL     FSSOK         FL(95)
     C                   END
     C                   END
     C     CPFS02        IFNE      *BLANKS
     C                   MOVE      CPFS02        YFSKOD
     C     YFSKOD        IFEQ      GMFSKO
     C     FRIKEY        READE     FRISOK                                 33
     C                   ELSE
     C     FRIKEY        CHAIN     FRISOK                             33
     C                   END
     C     *IN33         IFEQ      '0'
     C                   MOVE      YFSKOD        GMFSKO
     C                   MOVEL     FSSOK         FL(96)
     C                   END
     C                   END
     C     CPFS03        IFNE      *BLANKS
     C                   MOVE      CPFS03        YFSKOD
     C     YFSKOD        IFEQ      GMFSKO
     C     FRIKEY        READE     FRISOK                                 33
     C                   ELSE
     C     FRIKEY        CHAIN     FRISOK                             33
     C                   END
     C     *IN33         IFEQ      '0'
     C                   MOVE      YFSKOD        GMFSKO
     C                   MOVEL     FSSOK         FL(97)
     C                   END
     C                   END
     C     CPFS04        IFNE      *BLANKS
     C                   MOVE      CPFS04        YFSKOD
     C     YFSKOD        IFEQ      GMFSKO
     C     FRIKEY        READE     FRISOK                                 33
     C                   ELSE
     C     FRIKEY        CHAIN     FRISOK                             33
     C                   END
     C     *IN33         IFEQ      '0'
     C                   MOVE      YFSKOD        GMFSKO
     C                   MOVEL     FSSOK         FL(98)
     C                   END
     C                   END
     C     CPFS05        IFNE      *BLANKS
     C                   MOVE      CPFS05        YFSKOD
     C     YFSKOD        IFEQ      GMFSKO
     C     FRIKEY        READE     FRISOK                                 33
     C                   ELSE
     C     FRIKEY        CHAIN     FRISOK                             33
     C                   END
     C     *IN33         IFEQ      '0'
     C                   MOVE      YFSKOD        GMFSKO
     C                   MOVEL     FSSOK         FL(99)
     C                   END
     C                   END
      * "Rene" numeriske felt (uten fortegn)
     C                   MOVEL     HEAVD         FL(2)
     C                   MOVEL     HEOPD         FL(4)
     C                   MOVEL     HEPRO         FL(8)
     C                   MOVEL     HEPOS2        FL(14)
     C                   MOVEL     DSDATO        FL(18)
     C                   MOVE      TRSFD         XDTD
     C                   MOVE      TRSFM         XDTM
     C                   MOVE      TRSFÅ         XDTÅ
     C                   MOVEL     DSDATO        FL(19)
     C                   MOVE      TRSTD         XDTD
     C                   MOVE      TRSTM         XDTM
     C                   MOVE      TRSTÅ         XDTÅ
     C                   MOVEL     DSDATO        FL(21)
     C                   MOVEL     HEVKT         FL(24)
     C                   MOVEL     HEFBV         FL(26)
     C                   MOVEL     HEM3          FL(28)
     C                   MOVEL     HELM          FL(30)
     C                   MOVEL     HENT          FL(32)
     C                   MOVEL     HEDTR         FL(72)
     C                   MOVEL     TRVERB        FL(83)
      * Numeriske felt som kan ha fortegn
      * Div. beløp fra fakturalinjene, hente kun hvis brukt på kunden..
      *
     C                   Z-ADD     84            CFN
     C     CUFL1K        CHAIN     CUFLD1                             33
     C   33              Z-ADD     86            CFN
     C   33CUFL1K        CHAIN     CUFLD1                             33
     C   33              Z-ADD     88            CFN
     C   33CUFL1K        CHAIN     CUFLD1                             33
     C   33              Z-ADD     90            CFN
     C   33CUFL1K        CHAIN     CUFLD1                             33
     C   33              Z-ADD     92            CFN
     C   33CUFL1K        CHAIN     CUFLD1                             33
     C                   MOVE      *ZEROS        CFL
     C     *IN33         IFEQ      *ON
     C                   MOVE      *ZEROS        FL(84)
     C                   MOVE      *ZEROS        FL(86)
     C                   MOVE      *ZEROS        FL(88)
     C                   MOVE      *ZEROS        FL(90)
     C                   MOVE      *ZEROS        FL(92)
     C                   ELSE
      * I ficurr
     C                   MOVE      *ZEROS        TONOPL           13 2           *PLIKTIG
     C                   MOVE      *ZEROS        TONOFR           13 2           *FRI
     C                   MOVE      *ZEROS        TONONE           13 2           *NETTO(P+F)
     C                   MOVE      *ZEROS        TONOMO           13 2           *MOMS
     C                   MOVE      *ZEROS        TONOBR           13 2           *TOT(P+F+M)
      *************************************************************************
      *   start leseloop på fakturafil                                        *
      *************************************************************************
     C     FAK8K         SETLL     FAK8
     C     FAK8K         READE     FAK8                                   34
     C     *IN34         DOWEQ     '0'
      *
      * START MED MOMS (PLIKTIG)
      *
     C     FAKDM         IFNE      ' '
     C     FAKDM         andne     'N'
     C*          TONOPL    IFEQ *ZEROS
     C                   EXSR      GETTAX
     C*                    END
      *
      * valuta eller ikke
      *
     C     UVAL          IFEQ      *BLANKS
     C     uval          oreq      ficurr
     C                   ADD       FABELN        TONOPL                         NOK-pliktig
     C                   Z-ADD     FABELN        DNOPL                          NOK-pliktig
     C                   ELSE
     C                   ADD       FABELV        TONOPL                         VAL-pliktig
     C                   Z-ADD     FABELV        DNOPL                          NOK-pliktig
     C                   END
      * slutt valuta
      *
      * START UTEN MOMS  (FRITT)
     C                   ELSE
     C     UVAL          IFEQ      *BLANKS
     C     uval          oreq      ficurr
     C                   ADD       FABELN        TONOFR                         NOK-FRITT
     C                   Z-ADD     0             DNOPL                          NOK-pliktig
     C                   ELSE
     C                   ADD       FABELV        TONOFR                         VAL-FRITT
     C                   Z-ADD     0             DNOPL                          NOK-pliktig
     C                   END
     C                   END
      *
      * SLUTT SUMMERING
      *
     C     UVAL          IFEQ      *BLANKS
     C     uval          oreq      ficurr
     C                   ADD       FABELN        TONONE                         NOK-TOTnetto
     C                   ELSE
     C                   ADD       FABELV        TONONE                         VAL-TOTnetto
     C                   END
     C     DNOPL         MULT      WITAX         WONOMO                         NOK/VAL-MOMS
     C                   ADD       WONOMO        TONOMO                         NOK/VAL-MOMS
      *
     C     FAK8K         READE     FAK8                                   34
     C                   END
      *
      *
     C     TONONE        ADD       TONOMO        TONOBR                         NOK/VAL-Btot
      *
      * OBS! Må ha positiv numerisk verdi før move til alfafelt...
      *      Hvis ikke får en grums (å) i stedet for siste siffer.
      * Fortegn ?
     C     TONOFR        IFLT      0
     C                   MULT      -1            TONOFR
     C                   MOVE      '-'           FELT14
     C                   ELSE
     C                   MOVE      ' '           FELT14
     C                   END
     C                   MOVEL     TONOFR        FELT14           14
     C                   MOVEL     FELT14        FL(84)
      * Fortegn ?
     C     TONOPL        IFLT      0
     C                   MULT      -1            TONOPL
     C                   MOVE      '-'           FELT14
     C                   ELSE
     C                   MOVE      ' '           FELT14
     C                   END
     C                   MOVEL     TONOPL        FELT14
     C                   MOVEL     FELT14        FL(86)
      * Fortegn ?
     C     TONONE        IFLT      0
     C                   MULT      -1            TONONE
     C                   MOVE      '-'           FELT14
     C                   ELSE
     C                   MOVE      ' '           FELT14
     C                   END
     C                   MOVEL     TONONE        FELT14
     C                   MOVEL     FELT14        FL(88)
      * Fortegn ?
     C     TONOMO        IFLT      0
     C                   MULT      -1            TONOMO
     C                   MOVE      '-'           FELT14
     C                   ELSE
     C                   MOVE      ' '           FELT14
     C                   END
     C                   MOVEL     TONOMO        FELT14
     C                   MOVEL     FELT14        FL(90)
      * Fortegn ?
     C     TONOBR        IFLT      0
     C                   MULT      -1            TONOBR
     C                   MOVE      '-'           FELT14
     C                   ELSE
     C                   MOVE      ' '           FELT14
     C                   END
     C                   MOVEL     TONOBR        FELT14
     C                   MOVEL     FELT14        FL(92)
      *
     C                   END
      *
     C                   ENDSR
      *
     C*-------------------------------------------
     C     GETTAX        BEGSR
     C*"""""""""""""""""""""""""""""""""""""""""""
      *
     C     FADATO        IFEQ      *ZEROS
     C                   MOVEL     20            FADATÅ
     C                   MOVE      UYEAR         FADATÅ
     C                   MOVE      UMONTH        FADATM
     C                   MOVE      UDAY          FADATD
     C                   END
      * VARIABLE VAT
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     FADATO        ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAX
     C                   END
      * sjekk momskoder fra Løsning1                                      34
     C                   Z-ADD     0             WITAX
     C     FAKDM         IFEQ      'J'
     C                   Z-ADD     FITAX         WITAX
     C                   ELSE
     C     KODWKY        CHAIN     KODWL01                            84
     C     *IN84         IFEQ      *OFF
     C     MVAAVI        ANDNE     *ZERO
     C     MVAAVI        DIV       100           WITAX
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
     C*-------------------------------------------
     C     MAIN          BEGSR
     C*"""""""""""""""""""""""""""""""""""""""""""
      * OBS! Hvis element/felt-lengden i array FL er MINDRE enn lengden
      *      på feltet/(array WL) den flyttes inn i, vil NESTE arr-elem
      *      fra FL bli med over i MOVEA. (Når faktor2 i MOVEA er
      *      indexert betyr det jo fra og med.. Resfelt indexsert: legg
      *      inn begynnende fra og med.. ->pøs på alle data (også neste
      *      element hvis det er plass i mottakende array)
      * HVIS DET ER ØNSKELIG Å HA MINDRE FELTLENGDE ENN MAX LINJELENGDE
      * (FOR Å SPARE PLASS HVIS ARRAYET BLIR SVÆRT!) MÅ EN FØRST FLYTTE
      * FL,XX INN I EN VARIABEL, SÅ FLYTTE VARIABELEN TIL WL,YY!!!!!!!!
      *
      * KOMPONER (MAX 3) LINJER A 79 TEGN...
      * Hent felt for felt i kundens profil.
      * Sjekk om lengde skal justeres i forhold til standard.
      * Hvis numerisk - ta hensyn til editkode (og lengdejustering)
      * Hvis alfa - ta hensyn til lengdejustering
      *
     C                   MOVEA     *BLANKS       LI
     C                   Z-ADD     1             CFL
     C     CFL           DOWLE     3                                            B1
     C                   MOVE      *BLANKS       WORKL
     C     CUFLDK        SETLL     CUFLD
     C     CUFLDK        READE     CUFLD                                  50
     C     *IN50         DOWEQ     '0'                                           B2
     C     CFN           CHAIN     SYFLD                              33
     C     CFLEN         SUB       SFLEN         LENJUS            3 0
     C     CFEDIT        IFGE      '0'
     C     CFEDIT        ANDLE     '9'
     C                   EXSR      NUMEDT
     C                   ELSE
     C                   EXSR      ALFA
     C                   END
     C                   MOVEA     WORKE         WL(CFP)
     C     CUFLDK        READE     CUFLD                                  50
     C                   END                                                     E2
     C* LEGG LINJA I ARRAY FOR UTSKRIFTEN ... FLERE LINJER???
     C                   MOVE      WORKL         LI(CFL)
     C                   ADD       1             CFL
     C                   END                                                    E1
      *
     C                   ENDSR
      *
     C*-------------------------------------------
     C     NUMEDT        BEGSR
     C*"""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      SFDEC         SFDECN            1 0
     C                   MOVE      SFDEC         DECANT            1 0
     C                   MOVE      CFDEC         CFDECN            1 0
     C                   MOVE      FL(CFN)       WORKE
     C                   Z-ADD     SFLEN         EE                3 0
     C                   Z-ADD     79            NN                3 0
     C                   MOVE      *ZEROS        TUSDEL            1 0
     C                   MOVE      *ZEROS        DATTID            1 0
     C*
     C* Fortegn?? (ligger i så fall klar i posisjonen bak feltet)
     C     CFEDIT        IFEQ      '2'
     C     CFEDIT        OREQ      '4'
     C                   ADD       1             EE
     C                   MOVE      WE(EE)        WN(NN)
     C                   SUB       1             EE
     C                   SUB       1             NN
     C                   END
     C* Desimaler angitt ??
     C* I FL (->WE) er komma plassert i.h.t. SYFLD desimaldef. Hvis det
     C* er definert ferre desim. i CUFLD ikke legg ut de "bakerste"...
     C     SFDECN        IFNE      0
     C                   DO        SFDECN
     C     DECANT        IFLE      CFDECN
     C                   MOVE      WE(EE)        WN(NN)
     C                   SUB       1             NN
     C                   END
     C                   SUB       1             EE
     C                   END
     C* Legg inn desimal.komma (men IKKE hvis overstyrt til 0 i CUFLD)
     C     CFDECN        IFNE      0
     C                   MOVE      ','           WN(NN)
     C                   SUB       1             NN
     C                   END
     C                   END
     C*
     C* Resten av strengen takles i denne loopen......
     C     EE            DOWNE     0
     C*     tusendeling???
     C     CFEDIT        IFEQ      '3'
     C     CFEDIT        OREQ      '4'
     C     TUSDEL        IFEQ      3
     C                   MOVE      '.'           WN(NN)
     C                   SUB       1             NN
     C                   MOVE      *ZEROS        TUSDEL
     C                   END
     C                   ADD       1             TUSDEL
     C                   ELSE
     C*     dato/tid-editering???
     C     CFEDIT        IFEQ      '6'
     C     DATTID        IFEQ      2
     C                   MOVE      '.'           WN(NN)
     C                   SUB       1             NN
     C                   MOVE      *ZEROS        DATTID
     C                   END
     C                   ADD       1             DATTID
     C                   END
     C                   END
     C* ... evt editeringstegn er nå gjort - flytt selve tegnet/tallet
     C                   MOVE      WE(EE)        WN(NN)
     C                   SUB       1             EE
     C                   SUB       1             NN
     C                   END
     C*
     C* NN står nå en foran feltet..
     C                   ADD       1             NN
     C*
     C* Feltet ligger nå i WN fra posisjon NN -> 79
      * Skal lengden justeres (-4 i justering vil si øke NN med 4)
      * (EFFEKTIV sifferlengde - editeringskode m.m. telles ikke med)
     C     LENJUS        IFLT      0
     C     LENJUS        DOWLT     0
     C     WN(NN)        IFGE      '0'
     C     WN(NN)        ANDLE     '9'
     C                   ADD       1             LENJUS
     C                   END
     C                   ADD       1             NN
     C                   END
     C                   END
      * Står en etter lengdejustering med tusendelingstegn som første?
     C     WN(NN)        IFEQ      '.'
     C                   ADD       1             NN
     C                   END
     C* Skal foranstilte 0 fjernes??
     C     CFEDIT        IFGE      '1'
     C     CFEDIT        ANDLE     '4'
     C                   MOVE      NN            N2                3 0
      *
     C     STNULL        TAG
     C     WN(N2)        IFEQ      '0'
     C     WN(N2)        OREQ      '.'
     C                   MOVE      ' '           WN(N2)
     C                   ADD       1             N2
     C     N2            IFLT      79
     C                   GOTO      STNULL
     C                   END
     C                   END
      *
     C* Gjenstår  ,00 (eller ,50....) eller 0 med "fortegn" (= blank)
     C     WN(N2)        IFEQ      ','
     C     WN(N2)        OREQ      ' '
     C                   SUB       1             N2
     C                   MOVE      '0'           WN(N2)
     C                   END
      *
     C                   END
     C*
     C* Flytt det editerte feltet tilbake til array FL
     C                   MOVE      *BLANKS       HLPFLT           79
     C                   MOVEA     *BLANKS       WE
     C                   MOVEA     WN(NN)        WORKE
     C*
     C                   ENDSR
     C*
     C*-------------------------------------------
     C     ALFA          BEGSR
     C*"""""""""""""""""""""""""""""""""""""""""""
      *Legg tekstkonstant eller valgt felt i arbeidsarray
     C     SFEDIT        IFEQ      'T'
     C                   MOVE      *BLANKS       WORKE
     C                   MOVEL     CFOVER        WORKE
     C                   ELSE
     C                   MOVE      FL(CFN)       WORKE
     C                   END
      *Tegnene utover kundens ønskede lengde blankes
     C     CFLEN         ADD       1             EE
     C                   MOVEA     *BLANKS       WE(EE)
     C*
     C                   ENDSR
     C*
