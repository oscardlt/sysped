*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTP)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20100615  110529  SVEIN       REL-V5R4M0  5722-WDS
     A            W2NR           1A  I  6  2VALUES(' ' '2' '9')
     A            KOPNVN        10A  O  6  5DSPATR(HI)
     A            KOPLAS         1A  O  6 17
     A            KOPTY         10A  O  6 21
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20100615  110529  SVEIN       REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA75'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(035 SHU1276 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 56DATE
     A                                      EDTCDE(Y)
     A                                  1 66TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 SHU1277 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF05(05 'Frisk')
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'Forrige')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
