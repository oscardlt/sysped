      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE Gebyrer     på en varelinje  ***
      **************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 Ved tillegg som høyde/farlig gods mm - MOMSKODE etter fraktlinja
********************************************************************
      * BRINGTMS-prosjekt.
     FDOKUFV    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FDOKUFVG   UF A E           K DISK
     FFAKT      UF A E           K DISK
     FFIRM      IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FSYFA10GD  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   move      *zeros        SumBEL
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LinKey        SETLL     DOKUFVG
     C     LinKey        READE(N)  DOKUFVG                                94
     C     *in94         DOWEQ     '0'
     c                   eval      SFVT = FGVT
     c                   if        SFVT=*blanks
     c     KGEkey        chain     KODTGE
     c                   movel     KGENOT        SFVT
     c                   endif
     c                   move      '0'           *in50
     c                   move      ' '           FAOPKO
     c                   if        FGFALI <> 0
     c     FaktKey       chain     FAKT
     c     FAOPKO        comp      'C'                                50        protect
     c                   endif
     c                   add       FGBELN        SUMBEL
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     LinKey        READE(N)  DOKUFVG                                94
     C  N94              ENDDO
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   clear                   VARELI
     c                   move      SKPART        WSSKX
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      START5
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   GOTO      START3
     C                   END
     c                   goto      start                                        alltid refreh
     c                   endif
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       888786
     c     *in04         ifeq      '1'
     c                   goto      START5
     c                   endif
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C* Ok - refresh
     C                   GOTO      START                                        alltid refresh
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     c     *IN40         ifeq      *off
     C                   MOVE      *ZEROS        FGLIN2
     C     LinKey2Max    SETGT     DOKUFVG
     C     LinKey        READPE(N) DOKUFVG                                33
     C     FGLIN2        ADD       1             WSLIN2            2 0
     c                   endif
     C     LinKey2       CHAIN     DOKUFVG                            33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    DOKUFVGR
     C   33              WRITE     DOKUFVGR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXSR      MOVSIW
     C     LinKey2       CHAIN(N)  DOKUFVG                            33
     C                   EXFMT     VAREL3
     C     *IN11         IFEQ      '1'
     C     LinKey2       CHAIN     DOKUFVG                            33
     C  N33              DELETE    DOKUFVGR
     C* Slett også i FAKT..
     c     FGFALI        ifne      *zeros
     c     FaktKey       chain     FAKT                               34
     c  N34FAOPKO        ifle      'C'
     c                   delete    FAKTR
     c                   else
     c                   update    FAKTR
     c                   endif
     c                   endif
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     c     *in04         ifeq      '1'
     c                   goto      STCHG
     c                   endif
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
      * BLANK (I SUBFIL) VED DELETE
     C*
     C                   MOVE      *BLANKS       FGSKX
     C                   MOVE      *BLANKS       FGVK
     C                   MOVE      *Zeros        FGBELV
     C                   MOVE      *BLANKS       FGVAL
     C                   MOVE      *BLANKS       FGKDM
     C                   MOVE      *Zeros        FGBELN
     C                   MOVEL     *BLANKS       FGVT
     C                   MOVEL     '*DELETE*  '  FGVT
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
      * BLANK NÅR EN GÅR UT AV VARELINJEVEDLIKEHOLD
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSLIN2
     C                   MOVE      SKPART        WSSKX
     C                   MOVE      *BLANKS       WSVK
     C                   MOVE      *Zeros        WSBELV
     C                   MOVE      *BLANKS       WSVAL
     C                   MOVE      *BLANKS       WSKDM
     C                   MOVE      *Zeros        WSBELN
     C                   MOVEL     *BLANKS       WSVT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
      * HENTE DATA FOR ENDRING AV LINJE..
     C                   MOVE      FGLIN2        WSLIN2
     C                   MOVE      FGSKX         WSSKX
     C                   MOVE      FGVK          WSVK
     C                   MOVE      FGBELV        WSBELV
     C                   MOVE      FGVAL         WSVAL
     C                   MOVE      FGKDM         WSKDM
     C                   MOVE      FGBELN        WSBELN
     C                   MOVE      FGVT          WSVT
     C                   SETON                                        40
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     21            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
      * FLYTTE DATA ETTER ENDRING AV LINJE..
     C   33              DO
     C                   MOVE      UAVD          FGAVD
     C                   MOVE      UOPD          FGOPD
     C                   MOVE      UFBNR         FGFBNR
     C                   MOVE      ULINR         FGLINR
     C                   MOVE      WSLIN2        FGLIN2
     C                   MOVE      *zeros        FGFALI
     C                   END
     C                   MOVE      WSSKX         FGSKX
     C                   MOVE      WSVK          FGVK
     C                   MOVE      WSBELV        FGBELV
     C                   if        WSVAL = *blanks
     c                   eval      WSVAL = FICURR
     c                   endif
     C                   MOVE      WSVAL         FGVAL
     C                   if        WSKDM = *blanks
N01  C                   if        FRAKTkdm<>*blanks
N01  c                   eval      WSKDM = FRAKTkdm
N01  c                   else
     c                   eval      WSKDM = 'J'
N01  c                   endif
     c                   endif
     C                   MOVE      WSKDM         FGKDM
     C                   if        WSVAL = FICURR
     c                   eval      WSBELN= WSBELV
     c                   else
     c     ValKey        chain     VALUL01
     c                   eval(H)   WSBELN=(WSBELV * VALKU1 / OMRFAK) + 0.50
     c                   move      00            WSBELN
     c                   endif
     C                   MOVE      WSBELN        FGBELN
     C                   MOVE      WSVT          FGVT
     c                   eval      SFVT = FGVT
     c                   if        SFVT=*blanks
     c     KGEkey        chain     KODTGE
     c                   movel     KGENOT        SFVT
     c                   endif
      * M=MANUELT SKAPT I AUTO-MODE
     c                   MOVE      'M'           FGKDA
     c  N33              MOVE      'E'           FGKDA
     C*
     C* Skriv også til FAKT..
     c     FGFALI        ifne      *zeros
     c     FaktKey       chain     FAKT                               34
     c  N34FAOPKO        cabgt     'C'           SkipUpd
     c                   else
     c                   seton                                        34
     c                   endif
     c   34              CLEAR                   FAKTR
     C                   MOVE      TRKDFF        FASK
     C                   MOVE      FGAVD         FAAVD
     C                   MOVE      FGOPD         FAOPD
     C                   MOVE      FGVK          FAVK
     C                   MOVE      FGVAL         FAVAL
N02  C                   MOVE      FraktBet      FAKUNR
     C                   MOVE      FGKDM         FAKDM
     C                   MOVE      'M'           FAKDA
     C                   eval      FABELV = FGBELV
     C                   eval      FABELN = FGBELN
     C                   move      FGVT          FAVT
     C     SkipUpd       Tag
     C     *in34         ifeq      '1'
     C                   CALL      'SYGE06'
     C                   PARM                    FAAVD
     C                   PARM                    FAOPD
     C                   PARM                    FALI
     C                   WRITE     FAKTR
     C                   else
     C                   UPDATE    FAKTR
     C                   endif
      * bevar peker til aktulle fakturalinje
     C                   MOVE      FALI          FGFALI
     C
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFBNR             3 0
     C                   PARM                    ULINR             2 0
     C                   PARM                    UFLAGG            1
     C     HeaKey        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     LinKey        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C     LinKey2Max    KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C                   KFLD                    maxSub
     C                   z-add     99            maxSub            2 0
     C     LinKey2       KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C                   KFLD                    WSLIN2
     C     FaktKey       KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    FGFALI
N01   * hent dft momskode fra fakt linje nr 0 = Hovedfrakten
N01  c     FaktKey       chain(N)  FAKT                               33
N01  c  N33              move      FAKDM         FRAKTkdm          1
     C*
     C     DokufvKey     KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C                   move      'F'           FVRI
     c     DokufvKey     chain     DOKUFV                             33
     c     *in33         ifeq      '0'
     c                   eval      FVBESKR = %trim(%editc(FVANT:'4'))+' '
     c                             + %trim(FVPAKN)+' '+%trim(FVVT)+'    '
     c                             + %trim(%editc(FVVKT:'4'))+' Kg'
     c                   endif
     C*
     C*
     C     KGEKey        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FGVK
     C                   move      'GE'          KGEUNI
     c     *loval        setll     FIRM
     c                   read      FIRM
     C     ValKey        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSVAL
     C*
     c     Heakey        chain     Headf                              33
     c                   move      *zeros        FraktBet          8 0
     c                   move      TRKDFF        SKPART            1
     c                   if        SKPART = 'S'
     c                   eval      FraktBet = HEKNSF
     c                   else
     c                   eval      FraktBet = HEKNKF
     c                   endif
     C*
     C                   WRITE     WINDOWSZ
     C*
     C                   ENDSR
