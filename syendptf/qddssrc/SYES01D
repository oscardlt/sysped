     A*%%TS  SD  20191204  201458  JOVO        REL-V7R1M0  5770-WDS
N05  A* KUNDEGRUPPE I SUBFIL
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '3' '4' '5' '8')
     A            BIAKT          1A  O  5  5
     A            BIBRID        10A  O  5  8
     A            BIBESK        30A  O  5 19
     A            BIFIRM         2A  O  5 50
     A            BIKUNR         8Y 0O  5 53EDTCDE(4)
S05  A*           BILIBL        10A  O  5 62
S05  A*           BIEIER         5A  O  5 73
N05  A            BIKNG          8Y 0O  5 64EDTCDE(Z)
     A            BISTDL         1A  H
     A            BIWELC        50A  H
     A            BIANTF         1Y 0H
     A            BIDTV          8Y 0H
S05  A*           BIKNG          8Y 0H
     A            BITUT          2Y 0H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYES01R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(023 DIV2597 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(065 DIV2598 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S05  A*                                 4  2MSGCON(078 DIV2599 *LIBL/SYMSGF)
N05  A                                  4  2MSGCON(078 JOV1784 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  3  2MSGCON(042 DIV2600 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R VARELI
     A*%%TS  SD  20191204  201458  JOVO        REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF09(09 'SØKING')
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'BL.PW.OK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYES01R'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            WSBRID        10A  B  3 14DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A            WSBESK        30A  B  3 48DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            WSPO          10A  B  4 14DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSDTV          6Y 0O  4 48DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A            WSTIDV         6Y 0O  4 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A            WSAKT          1A  B  5 14DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSINN          1A  B  4 68COLOR(YLW)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A            WSFIRM         2A  O  6 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSSTDL         1A  B  7 14DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLIBL        10A  B  7 48DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSWELC        50A  B  8 14COLOR(WHT)
     A                                      DSPATR(PR)
     A            WSKUNR         8Y 0B  9 14DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKNG          8Y 0B  9 48DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSEIER         5A  B 10 14DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFARG        10A  B 11 14DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSANTF         1Y 0B  5 78DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSDTF          6Y 0O  5 48DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A            WSTIDF         6Y 0O  5 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 16'(1=STD)'
     A                                  1 20MSGCON(023 DIV2597 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(010 DIV2601 *LIBL/SYMSGF)
     A                                  3 26MSGCON(013 DIV2602 *LIBL/SYMSGF)
     A                                  4  2MSGCON(011 DIV2603 *LIBL/SYMSGF)
     A                                  4 26MSGCON(011 DIV2604 *LIBL/SYMSGF)
     A                                  5  2MSGCON(011 DIV2605 *LIBL/SYMSGF)
     A                                  6  2MSGCON(011 DIV2606 *LIBL/SYMSGF)
     A                                  7  2MSGCON(011 DIV2607 *LIBL/SYMSGF)
     A                                  7 26MSGCON(011 DIV2608 *LIBL/SYMSGF)
     A                                  9  2MSGCON(011 DIV2609 *LIBL/SYMSGF)
     A                                  9 26MSGCON(012 DIV2610 *LIBL/SYMSGF)
     A                                  9 58MSGCON(016 DIV2611 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 10  2MSGCON(011 DIV2612 *LIBL/SYMSGF)
     A                                 11  2MSGCON(011 DIV2613 *LIBL/SYMSGF)
     A                                 11 26MSGCON(053 DIV2614 *LIBL/SYMSGF)
     A                                 12 26MSGCON(053 DIV2615 *LIBL/SYMSGF)
     A                                 13 26MSGCON(053 DIV2616 *LIBL/SYMSGF)
     A                                 14 26MSGCON(053 DIV2617 *LIBL/SYMSGF)
     A                                  5 26MSGCON(020 JOV1608 *LIBL/SYMSGF)
     A                                  5 67MSGCON(010 DIV2619 *LIBL/SYMSGF)
     A                                 22  2MSGCON(066 DIV2620 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(020 DIV2621 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(020 DIV2622 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 44MSGCON(036 DIV2623 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N06  A                                 16  2'Langt Passord:'
N06  A            WSPOL         70A  B 17  2DSPATR(UL)
N06  A  83                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                      CHECK(LC)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SYES01R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med web-brukerID'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            BIBRID        10A  O  3 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BIBESK        30A  O  3 48DSPATR(UL)
     A                                      COLOR(WHT)
     A            BIPO          10A  O  4 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSDTV          6Y 0O  4 48DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A            BITIDV         6Y 0O  4 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A            BIAKT          1A  O  5 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BIFIRM         2A  O  6 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BISTDL         1A  O  7 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BILIBL        10A  O  7 48DSPATR(UL)
     A                                      COLOR(WHT)
     A            BIWELC        50A  O  8 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BIKUNR         8Y 0O  9 14DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BIKNG          8Y 0O  9 48DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BIEIER         5A  O 10 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BIFARG        10A  O 11 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BIANTF         1Y 0O  5 79DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSDTF          6Y 0O  5 48DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A            BITIDF         6Y 0O  5 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
N06  A                                 16  2'Langt Passord:'
N06  A            BIPOL         70A  B 17  2DSPATR(UL)
N06  A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7 16'(1=STD)'
     A                                  3  2MSGCON(011 DIV2601 *LIBL/SYMSGF)
     A                                  3 26MSGCON(011 DIV2602 *LIBL/SYMSGF)
     A                                  4  2MSGCON(011 DIV2603 *LIBL/SYMSGF)
     A                                  4 26MSGCON(011 DIV2604 *LIBL/SYMSGF)
     A                                  5  2MSGCON(011 DIV2605 *LIBL/SYMSGF)
     A                                  6  2MSGCON(011 DIV2606 *LIBL/SYMSGF)
     A                                  7  2MSGCON(011 DIV2607 *LIBL/SYMSGF)
     A                                  7 26MSGCON(011 DIV2608 *LIBL/SYMSGF)
     A                                  9  2MSGCON(011 DIV2609 *LIBL/SYMSGF)
     A                                  9 26MSGCON(011 DIV2610 *LIBL/SYMSGF)
     A                                 10  2MSGCON(011 DIV2612 *LIBL/SYMSGF)
     A                                 11  2MSGCON(011 DIV2613 *LIBL/SYMSGF)
     A                                  5 26MSGCON(020 JOV1608 *LIBL/SYMSGF)
     A                                  5 68MSGCON(010 DIV2619 *LIBL/SYMSGF)
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØKING')
     A                                      CF09(09 'SØKING')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2BRID        10A  I  2  8DSPATR(UL)
     A            W2BESK        30A  I  2 19DSPATR(UL)
     A                                      CHECK(LC)
     A            W2KUNR         8Y 0B  2 53EDTCDE(4)
     A            W2KNG          8Y 0B  2 64EDTCDE(4)
     A                                  1  2MSGCON(072 DIV2624 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(005 DIV2625 *LIBL/SYMSGF)
     A                                  3  2MSGCON(010 DIV2626 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 53MSGCON(010 DIV2627 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 64MSGCON(010 DIV2628 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYES01R'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 20MSGCON(023 DIV2597 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S05  A*                                 4  2MSGCON(078 DIV2599 *LIBL/SYMSGF)
N05  A                                  4  2MSGCON(078 JOV1784 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(042 DIV2600 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
