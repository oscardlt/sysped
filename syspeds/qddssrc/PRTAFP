    *
      *
      *
      * DESCRIPTION: SAMPLE AFP KEYWORD USAGE
      *
      * CREATE COMMAND: CRTPRTF FILE(MYLIB/PRINTFILE)
      *                         SRCFILE(MYLIB/QDDSSRC)
      *                         DEVTYPE(*AFPDS)
      *
      * DRAW A BOX
      *
     A          R DRAWBOX
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      BOX(&STRY &STRX &ENDY &ENDX &LW)
     A            STRY           5S 3P
     A            STRX           5S 3P
     A            ENDY           5S 3P
     A            ENDX           5S 3P
     A            LW             5S 3P
      *
      * DRAW A HORIZONTAL LINE
      *
     A          R HRZLINE
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      LINE(&STRY &STRX &LLEN *HRZ &LW)
     A            STRY           5S 3P
     A            STRX           5S 3P
     A            LLEN           5S 3P
     A            LW             5S 3P
      *
      * DRAW A VERTICAL LINE
      *
     A          R VRTLINE
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      LINE(&STRY &STRX &LLEN *VRT &LW)
     A            STRY           5S 3P
     A            STRX           5S 3P
     A            LLEN           5S 3P
     A            LW             5S 3P
      *
      * DRAW TEXT USING A CODED FONT
      *
     A          R TEXTFONT
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A            P1PRJ         30A
     A                                      FONT(16951 (*POINTSIZE 36))
     A                                      POSITION(&Y &X)
     A            Y              5S 3P
     A            X              5S 3P
      *
      * DRAW TEXT ROTATED 90 DEGREES
      *
     A          R ROTTEXT
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A            TEXT          30A
     A                                      TXTRTT(90)
     A                                      FONT(2304 (*POINTSIZE 16))
     A                                      POSITION(&Y &X)
     A            Y              5S 3P
     A            X              5S 3P
     A*
     A*  REPORT
     A*
     A          R REPORT
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%FS 004
     A*%%***********************************************************************
     A                                      FONT(2304 (*POINTSIZE 15))
     A                                  5  8
     A                                      'CODE400 IT PROJECTS'
     A*
     A                                      FONT(2304 (*POINTSIZE 20))
     A            P1PROJECT      6A  O  5 65
     A*
     A* PROGRAMMER
     A*
     A                                      FONT(2304 (*POINTSIZE 20))
     A                                 12  8
     A                                      'ASSIGNED TO:'
     A                                      FONT(2304 (*POINTSIZE 15))
     A            P1USER        10A  O 12 21
     A                                      FONT(2304 (*POINTSIZE 15))
     A*
     A* NOTES:
     A*
     A                                 14  8
     A                                      'NOTES:'
     A                                      FONT(16951 (*POINTSIZE 25))
     A            P1NOTE1       50A  O 16  8
     A                                      FONT(16951 (*POINTSIZE 15))
     A            P1NOTE2       50A  O 17  8
     A                                      FONT(16951 (*POINTSIZE 15))
     A            P1NOTE3       50A  O 18  8
     A                                      FONT(16951 (*POINTSIZE 15))
     A            P1NOTE4       50A  O 19  8
     A                                      FONT(16951 (*POINTSIZE 15))
     A            P1NOTE5       50A  O 20  8
     A                                      FONT(16951 (*POINTSIZE 15))
     A            P1NOTE6       50A  O 21  8
     A                                      FONT(16951 (*POINTSIZE 15))
     A*
     A*
     A* START THE DEFINING FIELDS SECTION
     A*
     A                                 26  8
     A                                      'PROJECT DEFINITION'
     A                                      FONT(16951 (*POINTSIZE 25))
     A                                 26 42
     A                                      'COMMENTS:'
     A                                      FONT(16951 (*POINTSIZE 25))
     A*
     A*  PROJECT DEFINITION - ALL FIELDS:
     A*
     A                                 29  8
     A                                      'CATEGORY . . . . :'
     A                                      FONT(2304 (*POINTSIZE 10))
     A            P1CAT         10A  O 29 20
     A                                      FONT(2304 (*POINTSIZE 10))
     A*
     A                                 31  8
     A                                      'DEPARTMENT . . . :'
     A                                      FONT(2304 (*POINTSIZE 10))
     A            P1DEPT        10A  O 31 20
     A                                      FONT(2304 (*POINTSIZE 10))
     A*
     A                                 33  8
     A                                      'DATE ASSIGNED  . :'
     A                                      FONT(2304 (*POINTSIZE 10))
     A            P1ADATE        8  0O 33 20
     A                                      FONT(2304 (*POINTSIZE 10))
     A                                      EDTWRD('  /  /    ')
     A*
     A                                 35  8
     A                                      'DATE DUE . . . . :'
     A                                      FONT(2304 (*POINTSIZE 10))
     A            P1DDATE        8  0O 35 20
     A                                      FONT(2304 (*POINTSIZE 10))
     A                                      EDTWRD('  /  /    ')
     A*
     A                                 37  8
     A                                      'PRIORITY . . . . :'
     A                                      FONT(2304 (*POINTSIZE 10))
     A            P1PTY          2  0O 37 20
     A                                      FONT(2304 (*POINTSIZE 10))
     A*
     A* THE LAST 4 APPROVAL FIELDS
     A*
     A                                 40  7
     A                                      'DATE COMPLETE:'
     A                                      FONT(2304 (*POINTSIZE 08))
     A*
     A                                 43  7
     A                                      'QC APPROVED:'
     A                                      FONT(2304 (*POINTSIZE 08))
     A*
     A                                 46  7
     A                                      'USER APPROVAL:'
     A                                      FONT(2304 (*POINTSIZE 08))
     A*
     A                                 49  7
     A                                      'DATE INSTALLED:'
     A                                      FONT(2304 (*POINTSIZE 08))
     A*%%***********************************************************************
     A*%%SS
     A*%%SN P1PROJECT  XXXXXX
     A*%%CL 007
     A*%%CL 002
     A*%%CL 002
     A*%%CL 001
     A*%%CL 001
     A*%%CL 001
     A*%%CL 001
     A*%%CL 001
     A*%%CL 005
     A*%%CL 003
     A*%%CL 002
     A*%%CL 002
     A*%%***********************************************************************
     A*
      *
      * EJECT THE CURRENT PAGE
      *
     A          R PAGEND
     A*%%***********************************************************************
     A*%%RI 00000
     A*%%***********************************************************************
     A                                      ENDPAGE
     A*%%***********************************************************************
     A*%%CP+999CRTPRTF
     A*%%CP+    FILE(JJFLIB/APFP)
     A*%%CP+    DEVTYPE(*SCS)
     A*%%CP     PAGESIZE(*N       198      *N     )
     A*%%***********************************************************************
