     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  5  2VALUES(' ' '2' '3' '4' '5' '9')
     A            TGVK           3A  H
     A            TGGBL         11Y 0H
     A            TGGBLB        11Y 0H
     A            TGDT           8Y 0H
     A            TGDTR          8Y 0H
     A            TGKNA          8Y 0H
     A            TGNAA         30A  H
     A            TGADA1        30A  H
     A            TGPNA          9A  H
     A            TGPSA         24A  H
     A            TGLKA          2A  H
     A            TGUSR         10A  H
     A            TGRN          11Y 0H
     A            TGGTY          1Y 0H
     A            TGPRM          3Y 0H
     A            TGGNR         24A  O  5  5TEXT('GARANTINR')
     A            TGTSD          8A  O  5 30TEXT('TOLLSTED')
     A            WSDT           6S 0O  5 39TEXT('Registreringsdato')
     A            TGST           1A  O  5 48TEXT('Status')
     A            TGTINA        17A  O  5 51TEXT('HOVEDANSVARLIG')
     A            TGAKNY         4A  O  5 69TEXT('ADGANSKODE NY')
     A            TGAKGM         4A  O  5 74TEXT('ADGANSKODE GAMMEL')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt program')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'Lag ny')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SVX030R'
     A                                      COLOR(BLU)
     A                                  1 26'Underhåll garanti  '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Vælj:  2=Ændra  3=Kopiera 4=Radera-
     A                                       5=Visa 9=Logg    '
     A                                      COLOR(BLU)
     A                                  4  2'Op Garantinr                Tullko-
     A                                      nt Sænd.dat St Huvudansvarig     Ad-
     A                                      g.kd(N/G)'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F3=Avsluta   F5=Uppdatera  F6=Skap-
     A                                      a ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CA03(03 'Avslutt program')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'Lag ny')
     A                                  1  2'SYSPED/SVX030R'
     A                                      COLOR(BLU)
     A                                  1 20'Underhåll garanti  '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Velg:  2=Ændra  3=Kopiera 4=Radera-
     A                                       5=Visa 9=EDI-logg'
     A                                      COLOR(BLU)
     A                                  4  2'Op Garantinr                Tullko-
     A                                      nt Sænd.dat St Huvudansvarig     Ad-
     A                                      g.kd(N/G)'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Avsluta   F5=Uppdatera  F6=SKap-
     A                                      a ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
