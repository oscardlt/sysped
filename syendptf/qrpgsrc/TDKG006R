      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKG006R MODULE(*LIBL/TDKG006R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKTARA    IF   E           K DISK    USROPN
     FDKTARD    IF   E           K DISK    USROPN
     FDKTARE    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WIE             S              1
     D WKOD            S             10
     D STOPP           S             10
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D ZB              S              1    DIM(10)
     D POS             S             10I 0
     D                 DS
     D  WALF74                 1      7
     D  WNUM74                 1      7S 4

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬ie¬ : ¬'+%trim(WIE)+'¬, '
        +'¬kod¬ : ¬'+%trim(WKOD)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"tariclist" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta varukod/varukoder
      *
     c     Error         ifeq      *blanks
     C     KEY1          SETLL     DKTARA
     C                   READ      DKTARA                                 55
N01  C  N55DKTARA02      CABGT     STOPP         UTLES
     C     *IN55         DOWEQ     *OFF

     C*    DKTARA07      CABEQ     'TN701'       IKKEMED
     C*    DKTARA07      CABEQ     'TN702'       IKKEMED
     C     DKTARA02      CHAIN     DKTARD                             20
     C     *IN20         CABEQ     *ON           IKKEMED

     C                   MOVE      DKTARA05      WD1               8
     C                   MOVE      DKTARA06      WD2               8
     C                   MOVEL     DKTARA05      WD3               2
     C                   IF        WD3 > '50'
     C                   MOVEL     '19'          WD1
     C                   ELSE
     C                   MOVEL     '20'          WD1
     C                   ENDIF
     C                   MOVEL     DKTARA06      WD3               2
     C                   IF        WD3 > '50'
     C                   MOVEL     '19'          WD2
     C                   ELSE
     C                   MOVEL     '20'          WD2
     C                   ENDIF
     C                   IF        WD3 = '99'
     C                   MOVEL     '20'          WD2
     C                   ENDIF
     C     WD1           IFLE      WDT
     C     DKTARA06      ANDGE     WDT
     C                   MOVEL     DKTARA64      W60              60
      /free
         pos = 1;
               dou pos <= 0;

                 pos = %scan(%trim(':'):w60:pos);
                 if (pos = 0);
                     leave;
                 endif;
                 w60 = %replace( %trim('.')
                                    : w60
                                    : pos
                                    : %len(%trim(':')));
                 if (pos + %len(%trim('.')) > %len(w60));
                     leave;
                 endif;
               enddo;
      /end-free
     C                   IF        WIE='E'
     C                   MOVE      '  '          DKTARA02
     C                   ENDIF
     C                   EVAL      WALF74=DKTARA15
     C                   IF        WALF74=*BLANKS
     C                   EVAL      WALF74='0000000'
     C                   ENDIF
     C                   EVAL      DKTARA63=DKTARD47
     C                   EVAL      DKTARA57=*BLANKS
     C                   IF        WIE='E'
     C                   MOVEL     DKTARA02      WKEY08            8
     C     WKEY08        CHAIN     DKTARE                             21
     C                   IF        *IN21=*OFF
     C                   EVAL      DKTARA57='X'
     C                   ENDIF
     C                   ENDIF

     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬dktara02¬ : ¬'+%trim(DKTARA02)+'¬, '
          +'¬dktara15¬ : ¬'+%trim(DKTARA15)+'¬, '
          +'¬dktara54¬ : ¬'+%trim(DKTARA54)+'¬, '
          +'¬dktara55¬ : ¬'+%trim(DKTARA55)+'¬, '
          +'¬dktara56¬ : ¬'+%trim(DKTARA56)+'¬, '
          +'¬dktara57¬ : ¬'+%trim(DKTARA57)+'¬, '
          +'¬dktara58¬ : ¬'+%trim(DKTARA58)+'¬, '
          +'¬dktara59¬ : ¬'+%trim(DKTARA59)+'¬, '
          +'¬dktara60¬ : ¬'+%trim(DKTARA60)+'¬, '
          +'¬dktara61¬ : ¬'+%trim(DKTARA61)+'¬, '
          +'¬dktara62¬ : ¬'+%trim(DKTARA62)+'¬, '
          +'¬dktara63¬ : ¬'+%trim(DKTARA63)+'¬, '
          +'¬dktara64¬ : ¬'+%trim(W60)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C                   ENDIF

     C     IKKEMED       TAG

     C                   READ      DKTARA                                 55
N01  C  N55DKTARA02      CABGT     STOPP         UTLES
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKTARA
     C  N50              CLOSE     DKTARD
     C  N50              CLOSE     DKTARE

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WIE = zhbgetvar('IE')
     C                   EVAL      WKOD = zhbgetvar('KOD')
     C                   MOVEA     WKOD          ZB
     C     ZB(1)         IFNE      ' '
     C                   Z-ADD     0             ZAX               2 0
     C     1             DO        10            ZAX               2 0
     C     11            SUB       ZAX           WAX               2 0
     C     ZB(WAX)       CABNE     *BLANKS       SRB3
     C                   MOVE      '9'           ZB(WAX)
     C                   ENDDO
     C                   ENDIF
     C     SRB3          TAG
     C                   MOVEA     ZB            STOPP
     C
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     KEY1          KLIST
     C                   KFLD                    WKOD

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKTARA
     C  N50              OPEN      DKTARD
     C  N50              OPEN      DKTARE
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT6              6

     c                   endsr
