     A*%%TS  SD  20200124  084517  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10004A* 01 YBC PTF10 LEVERANDØRSNR ER ØKT TIL 5.0
     A*              INGEN MERKING
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R GSS024DA                  SFL
     A*%%TS  SD  20200124  084517  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  5  2VALUES(' ' '1' '5' 'P' 'T')
     A            ORONO          7  0O  5  5EDTCDE(4)
     A  27                                  COLOR(RED)
     A  29                                  COLOR(YLW)
     A            POCONO        20A  H
     A            POCONO18      18A  O  5 13
     A  38                                  COLOR(YLW)
     A  39                                  COLOR(RED)
     A            ORSNO          5Y 0O  5 32EDTCDE(4)
     A            ORCLI          5Y 0O  5 38EDTCDE(4)
     A            ORWT           7Y 1O  5 44EDTCDE(4)
     A            ORVOL          5Y 2O  5 53EDTCDE(4)
     A            ORPOS1         5A  O  5 60
     A            ORLC           5A  H
     A            ORBCD          5A  H
     A            XLCBCD         5A  O  5 66
     A            ORCLNF         3Y 0O  5 72EDTCDE(4)
     A            PAPKD          1A  O  5 76COLOR(YLW)
     A            ORPUKE         2   O  5 78
     A            ORTNO2        11Y 0O  5 82EDTCDE(Z)
     A                                      COLOR(RED)
     A*
     A          R GSS024DB                  SFLCTL(GSS024DA)
     A*%%TS  SD  20200124  084517  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS024D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Pick up orders on trip'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Trip No.'
     A            TRTNO         11Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 24'Clients No.'
     A            TRCNO          8Y 0O  2 36EDTCDE(4)
     A                                      COLOR(WHT)
     A N40                              2 45'Vessel'
     A N40        TRBCD          5A  O  2 52COLOR(WHT)
     A  40                              2 45'locat.'
     A  40        TRLOCF         5A  O  2 52COLOR(WHT)
     A                                  2 60'Tot. weight:'
     A            UWT            7Y 1O  2 73DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
     A  40                              3  2'Type options:    1=Pick up  5=Disp-
     A                                      lay'
     A                                      COLOR(BLU)
     A N40                              3  2'Type options:    1=Pick up  5=Disp-
     A                                      lay   P=Orders with P.O.   T=All or-
     A                                      ders'
     A                                      COLOR(BLU)
     A N40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Locat-
     A                                       Cln P     Tr.trip'
     A                                      DSPATR(HI)
     A  40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Vesse-
     A                                       Cln P     Tr.trip       '
     A                                      DSPATR(HI)
      *
     A          R GSS024DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF07(07 'PLUKK TOM UKE')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'Print')
     A                                      CF12(12 'Cancel')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=End  F5=Refresh  F8=Search  F10-
     A                                      =Print packinglist  F12=Cancel  F7=-
     A                                      Plukk t.o.m. uke'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS024DE
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Search from --> '
     A N40                              2 19'Order No.'
     A N40        W2ONO          7Y 0I  2 29DSPATR(UL)
     A                                      DSPATR(PC)
     A  40                              2 19'Vessel:'
     A  40        W2BCD          5A  I  2 29DSPATR(UL)
     A                                      DSPATR(PC)
     A N40                              2 39'or location:'
     A N40        W2LC           5A  I  2 52DSPATR(UL)
     A                                  3  2'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R GSS024DG
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'Print')
     A                                      CF12(12 'Cancel')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS024D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Pick up orders on trip'
     A                                      DSPATR(HI)
     A                                  2  2'Trip No.'
     A            TRTNO         11Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 24'Clients No.'
     A            TRCNO          8Y 0O  2 36EDTCDE(4)
     A                                      COLOR(WHT)
     A N40                              2 45'Vessel'
     A N40        TRBCD          5A  O  2 52COLOR(WHT)
     A  40                              2 45'locat.'
     A  40        TRLOCF         5A  O  2 52COLOR(WHT)
     A                                  2 60'Tot. weight:'
     A            UWT            7Y 1O  2 73DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
     A N40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Locat-
     A                                       Cln P'
     A                                      DSPATR(HI)
     A  40                              4  2'Op OrderNo Clients order No.    SN-
     A                                      o Colli Weight   Volume Pos.1 Vesse-
     A                                       Cln P'
     A                                      DSPATR(HI)
     A                                  6  6'No order found'
     A                                      COLOR(RED)
     A                                 23  2'F3=End  F5=Refresh  F8=Search  F10-
     A                                      =Print packinglist  F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS024DP
     A                                      WINDOW(*DFT 7 40 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Plukk t.o.m. uke'
     A                                      COLOR(WHT)
     A                                  3  2'År    :'
     A            WSPÅR          2  0B  3 10DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4  2'Uke   :'
     A            WSPUKE         2  0B  4 10DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  5  2'Område:'
     A            WSAREA         4   B  5 10DSPATR(UL)
     A                                  6  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99                              7  2MSGCON(38 SPU0101 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R GSS024DX
     A                                      WINDOW(*DFT 7 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print packinglist'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRT         10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'No. of copies:'
     A            WSCPY          1  0B  4 17DSPATR(UL)
     A                                  5  2'Send fax ?    '
     A            WSFAX          1A  B  5 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  5 20'Y/N'
     A                                  6  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99        WSMSGN        25A     7  2MSGID(GSS0042 GSSMSGF)
     A                                      COLOR(WHT)
