      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SAKSBEHANDLER(SIGNATUR)   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FKUOPDTY   UF A E           K DISK
     FKODTOTY   IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYFA23FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C*
     C                   EXSR      INIT
     C*
     C*
     C     START0        TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       9930
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKUNR
     C     KUND1K        CHAIN     CUNDL01                            99
     C  N99              GOTO      START
     C                   MOVE      *ZEROS        WSKUNR
     C                   SETOFF                                       99
     C                   GOTO      START0
     C                   END
     C     STARTX        TAG
     C     KUND1K        CHAIN     CUNDL01                            99
     C   99              MOVE      'SPK0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START0
     C                   MOVE      WSKUNR        KUOKUN
     C*
      * START PÅ DETALJER PÅ KUNDEN
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2ODEL        YSODEL            1
     C                   MOVE      W2OAVD        YSOAVD            4 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSF1        SETLL     KUOPDTY
     C     START1        TAG
     C     WSKUNR        READE(N)  KUOPDTY                                94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     WSKUNR        READE(N)  KUOPDTY                                94
     C  N94              MOVE      KUODEL        YSODEL
     C  N94              MOVE      KUOAVD        YSOAVD
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      START0
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     11            POSNBR
     C                   MOVE      *BLANKS       WSODEL
     C                   MOVE      *ZEROS        WSOAVD
     C                   MOVE      *BLANKS       WSOOTY
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSF3        SETLL     KUOPDTY
     C  N94WSKUNR        READE(N)  KUOPDTY                                94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
      * TESTER VED NY
     C  N40              DO
     C     WSODEL        COMP      *BLANKS                                90
     C   90              MOVE      'SHU0534'     MSGNR
     C   90              Z-ADD     11            POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C     KODTKE        CHAIN     KODTOTY                            89
     C   89
     CANN30              MOVE      'SPO0007'     MSGNR
     C   89
     CANN30              Z-ADD     32            POSNBR
     C   89              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSFK        CHAIN     KUOPDTY                            33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    KUOPDTYR
     C   33              WRITE     KUOPDTYR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KUOOTY        COMP      '*D'                                   98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KUOOTY        COMP      '*D'                                   98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODDEL        CHAIN     KUOPDTY                            33
     C  N33              DELETE    KUOPDTYR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KUODEL
     C                   MOVE      *ZEROS        KUOAVD
     C                   MOVEL     '*D'          KUOOTY
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSODEL
     C                   MOVE      *ZEROS        WSOAVD
     C                   MOVE      *BLANKS       WSOOTY
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      KUODEL        WSODEL
     C                   MOVE      KUOAVD        WSOAVD
     C                   MOVE      KUOOTY        WSOOTY
     C                   SETON                                        40
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     32            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSKUNR        KUOKUN
     C                   MOVE      WSODEL        KUODEL
     C                   MOVE      WSOAVD        KUOAVD
     C                   END
     C*
     C                   MOVE      WSOOTY        KUOOTY
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODDEL        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    KUODEL
     C                   KFLD                    KUOAVD
     C     KODSFK        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSODEL
     C                   KFLD                    WSOAVD
     C     KODSF1        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2ODEL
     C                   KFLD                    W2OAVD
     C     KODSF3        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    YSODEL
     C                   KFLD                    YSOAVD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     KODTKE        KLIST
     C                   KFLD                    YSFUN2
     C                   KFLD                    WSOOTY
     C                   MOVE      'OTY'         YSFUN2            3
     C*
     C*
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C*
