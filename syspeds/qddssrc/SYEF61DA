     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/AIRNRD)
     A                                      PRINT
     A                                      HLPDOC('SYEF03010' 'SYEF03 ' 'SY400-
     A                                      LO/MAINT')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            XDLT           7A  H
     A            WSNR           1A  I  6  2
     A            AIRCCP    R        O  6  5REFFLD(AIRNRDR/AIRCCP)
     A            AIRCOD    R        O  6  7REFFLD(AIRNRDR/AIRCOD)
     A            AIRKN     R        O  6 12REFFLD(AIRNRDR/AIRKN)
     A                                      EDTCDE(4)
     A            AIRCOM    R        O  6 21REFFLD(AIRNRDR/AIRCOM)
     A            AIRWEI    R        O  6 26REFFLD(AIRNRDR/AIRWEI)
     A                                      EDTCDE(4)
     A            AIRCP     R        O  6 33REFFLD(AIRNRDR/AIRCP)
     A            AIRTRC    R        O  6 37REFFLD(AIRNRDR/AIRTRC)
     A            AIRSRT    R        O  6 40REFFLD(AIRNRDR/AIRSRT)
     A            AIRGCD    R        O  6 45REFFLD(AIRNRDR/AIRGCD)
     A            AIRGRN    R        O  6 48REFFLD(AIRNRDR/AIRGRN)
     A                                      EDTCDE(4)
     A            AIRACD    R        O  6 63REFFLD(AIRNRDR/AIRGCD)
     A            AIRAWB    R        O  6 65REFFLD(AIRNRDR/AIRAWB)
     A                                      EDTCDE(4)
     A            AIRGC2    R        O  7 45REFFLD(AIRNRDR/AIRGC2)
     A            AIRGR2    R        O  7 48REFFLD(AIRNRDR/AIRGR2)
     A                                      EDTCDE(4)
     A            AIRAC2    R        O  7 63REFFLD(AIRNRDR/AIRGCD)
     A            AIRAW2    R        O  7 65REFFLD(AIRNRDR/AIRAWB)
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF03(03 'AVSLUTT')
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF09(09 'VEDL. FREIGHTNR.')
     A  91N93                               CF12(12 'FORRIGE')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A          H                           HLPDOC('SYEF03020' 'SYEF03' 'SY400L-
     A                                      O/MAINT')
     A                                      HLPARA(01 001 20 080)
     A                                  1  2'SYSPED/SYEF61RA'
     A                                      COLOR(BLU)
     A                                  1 21'Vedlikehold flyfraktrate (netto)'
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ULIN      R        O  3 33REFFLD(AIRNRDR/AIRLIN)
     A                                      COLOR(WHT)
     A            USDT          30A  O  3 38COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            UPFR      R        O  4 33REFFLD(AIRNRDR/AIRPFR)
     A                                      COLOR(WHT)
     A                                  4 37'-->'
     A            UPTO      R        O  4 41REFFLD(AIRNRDR/AIRPTO)
     A                                      COLOR(WHT)
     A                                  5  2'Op RateKl Kundenr. Com. Vektgr C/P-
     A                                       Tr Sort Kode gr.pris/min. Kode    -
     A                                      Kilopris'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                 23  2'F3=Avslutt   F5=Forny   F6=Lage   -
     A                                      F9=Vedl. freightnr.   F12=Avbryt'
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF03(03 'AVSLUTT')
     A N40                                  CF04(04 'Søk')
     A N40                                  CF09(09 'VEDL. FREIGHTNR.')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                      PROTECT
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPDOC('SYEF03030' 'SYEF03' 'SY400L-
     A                                      O/MAINT')
     A                                      HLPARA(02 001 02 008)
     A          H                           HLPDOC('SYEF03040' 'SYEF03' 'SY400L-
     A                                      O/MAINT')
     A                                      HLPARA(02 009 02 019)
     A          H                           HLPDOC('SYEF03050' 'SYEF03' 'SY400L-
     A                                      O/MAINT')
     A                                      HLPARA(02 020 02 035)
     A          H                           HLPDOC('SYEF03060' 'SYEF03' 'SY400L-
     A                                      O/MAINT')
     A                                      HLPARA(02 036 02 050)
     A          H                           HLPDOC('SYEF03070' 'SYEF03' 'SY400L-
     A                                      O/MAINT')
     A                                      HLPARA(02 051 02 065)
     A          H                           HLPDOC('SYEF03080' 'SYEF03' 'SY400L-
     A                                      O/MAINT')
     A                                      HLPARA(02 066 02 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   RateKl Kundenr. Com. Vektgr C/P-
     A                                       Tr Sort Kode gr.pris/min. Kode    -
     A                                      Kilopris'
     A                                      COLOR(WHT)
     A            WSRCCP    R        B  2  5REFFLD(AIRNRDR/AIRCCP)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSRCOD    R        B  2  7REFFLD(AIRNRDR/AIRCOD)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A  90                                  DSPATR(RI)
     A            WSRKN     R        B  2 12REFFLD(AIRNRDR/AIRKN)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSRCOM    R        B  2 21REFFLD(AIRNRDR/AIRCOM)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSRWEI    R        B  2 26REFFLD(AIRNRDR/AIRWEI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSRCP     R        B  2 33REFFLD(AIRNRDR/AIRCP)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSRTRC    R        B  2 37REFFLD(AIRNRDR/AIRTRC)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSRSRT    R        B  2 40REFFLD(AIRNRDR/AIRSRT)
     A                                      EDTCDE(4)
     A            WSRGCD    R        B  2 45REFFLD(AIRNRDR/AIRGCD)
     A  87                                  DSPATR(RI)
     A                                      VALUES(' ' 'N' 'P')
     A            WSRGRN    R        B  2 48REFFLD(AIRNRDR/AIRGRN)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSRACD    R        B  2 63REFFLD(AIRNRDR/AIRACD)
     A  85                                  DSPATR(RI)
     A            WSRAWB    R        B  2 65REFFLD(AIRNRDR/AIRAWB)
     A  84                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSRGC2    R        B  3 45REFFLD(AIRNRDR/AIRGC2)
     A  83                                  DSPATR(RI)
     A                                      VALUES(' ' 'N' 'P')
     A            WSRGR2    R        B  3 48REFFLD(AIRNRDR/AIRGR2)
     A  82                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSRAC2    R        B  3 63REFFLD(AIRNRDR/AIRAC2)
     A  81                                  DSPATR(RI)
     A            WSRAW2    R        B  3 65REFFLD(AIRNRDR/AIRAW2)
     A  80                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N40                              4 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N40                              4 42'F9=Vedl. freightnr.'
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF09(09 'VEDL. FREIGHTNR.')
     A                                      CF12(12 'Forrige')
     A          H                           HLPDOC('SYEF03020' 'SYEF03' 'SY400L-
     A                                      O/MAINT')
     A                                      HLPARA(01 001 20 080)
     A                                  1  2'SYSPED/SYEF61RA'
     A                                      COLOR(BLU)
     A                                  1 21'Vedlikehold flyfraktrate (netto)'
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ULIN      R        O  3 33REFFLD(AIRNRDR/AIRLIN)
     A                                      COLOR(WHT)
     A            USDT          30A  O  3 38COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            UPFR      R        O  4 33REFFLD(AIRNRDR/AIRPFR)
     A                                      COLOR(WHT)
     A                                  4 37'-->'
     A            UPTO      R        O  4 41REFFLD(AIRNRDR/AIRPTO)
     A                                      COLOR(WHT)
     A                                  5  2'Op RateKl Kundenr. Com. Vektgr C/P-
     A                                       Tr Sort Kode gr.pris/min. Kode    -
     A                                      Kilopris'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Avslutt   F5=Forny   F6=Lage   -
     A                                      F9=Vedl. freightnr.   F12=Avbryt'
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2'   RateKl Kundenr. Com. Vektgr C/P-
     A                                       Tr Sort Kode gr.pris/min. Kode    -
     A                                      Kilopris'
     A                                      COLOR(WHT)
     A            AIRCOD    R        O  2  5REFFLD(AIRNRDR/AIRCOD)
     A                                      DSPATR(PC)
     A            AIRKN     R        O  2 12REFFLD(AIRNRDR/AIRKN)
     A                                      EDTCDE(4)
     A            AIRCOM    R        O  2 21REFFLD(AIRNRDR/AIRCOM)
     A            AIRWEI    R        O  2 26REFFLD(AIRNRDR/AIRWEI)
     A                                      EDTCDE(4)
     A            AIRCP     R        O  2 33REFFLD(AIRNRDR/AIRCP)
     A            AIRTRC    R        O  2 37REFFLD(AIRNRDR/AIRTRC)
     A            AISRT     R        O  2 40REFFLD(AIRNRDR/AIRSRT)
     A                                      EDTCDE(4)
     A            AIRGCD    R        O  2 45REFFLD(AIRNRDR/AIRGCD)
     A            AIRGRN    R        O  2 48REFFLD(AIRNRDR/AIRGRN)
     A                                      EDTCDE(4)
     A            AIRACD    R        O  2 63REFFLD(AIRNRDR/AIRACD)
     A            AIRAWB    R        O  2 65REFFLD(AIRNRDR/AIRAWB)
     A                                      EDTCDE(4)
     A            AIRGC2    R        O  3 45REFFLD(AIRNRDR/AIRGC2)
     A            AIRGR2    R        O  3 48REFFLD(AIRNRDR/AIRGR2)
     A                                      EDTCDE(4)
     A            AIRAC2    R        O  3 63REFFLD(AIRNRDR/AIRAC2)
     A            AIRAW2    R        O  3 65REFFLD(AIRNRDR/AIRAW2)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
