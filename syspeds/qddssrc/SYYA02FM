********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØK KUNDE')
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYYA02'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(033 DIV0756 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4  2MSGCON(015 DIV0757 *LIBL/SYMSGF)
     A            WSKNF          8Y 0B  4 18DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4 28MSGCON(027 DIV0758 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(015 DIV0759 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  5 18DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(015 DIV2536 *LIBL/SYMSGF)
     A            WSOPDT         2A  B  7 18COLOR(YLW)
     A                                  7 28MSGCON(018 DIV0761 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  9  2MSGCON(015 DIV5089 *LIBL/SYMSGF)
     A            WSCMP          1A  B  9 18DSPATR(UL)
     A                                      COLOR(YLW)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      VALUES(' ' 'J' 'N')
     A                                  9 28'(J/N)'
     A                                      COLOR(BLU)
     A                                 10  2MSGCON(015 DIV2537 *LIBL/SYMSGF)
     A                                 11  2MSGCON(015 DIV2542 *LIBL/SYMSGF)
     A            WSHEAD         1A  B 11 18COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 11 28'(J/N)'
     A                                      COLOR(BLU)
     A                                 13  2MSGCON(015 DIV0760 *LIBL/SYMSGF)
     A            WSSDF          5A  B 13 18DSPATR(UL)
     A  97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13 28MSGCON(018 DIV0761 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 14  2MSGCON(015 DIV0762 *LIBL/SYMSGF)
     A            WSSDT          5A  B 14 18DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14 28MSGCON(018 DIV0761 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 16  2MSGCON(015 JOV0035 *LIBL/SYMSGF)
     A            WSLK           2A  B 16 18COLOR(YLW)
     A                                 16 28MSGCON(018 DIV0761 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 18  2MSGCON(015 DIV0763 *LIBL/SYMSGF)
     A            WSVALG         1A  B 18 18DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'U' 'G')
     A                                 18 28MSGCON(030 DIV0764 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 19 28MSGCON(030 DIV0765 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 20 28MSGCON(030 DIV0766 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 21  2MSGCON(015 DIV0767 *LIBL/SYMSGF)
     A            WSDT           6Y 0B 21 18DSPATR(UL)
     A  95                                  DSPATR(PC)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 21 28MSGCON(030 DIV0768 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0636 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A  30                                  COLOR(WHT)
