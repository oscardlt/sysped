********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11086 * 01 CB  PTF11 max 6 repetisjoner for ACC (Accounting inf)
      ***********************************************************
      *** PROGRAMMET LEGGER KLART HWB TIL SENDING SOM AZB.    ***
      *** SENDER KUN EN OG EN AV GANGEN.                      ***
      *** LR SETTES IKKE PÅ.                                  ***
      *** KODETEKSTFILENE ++ ER DEFINERT SOM OUTPUT FILER     ***
      *** KUN FOR Å FÅ TATT CLEAR PÅ RECORDFORMATET.          ***
      ***********************************************************
     H DATEDIT(*YMD)
     FFIRM      IF   E             DISK
     FKODTA     IF A E           K DISK
     FSTED2     IF A E           K DISK
     FKODTSF    IF A E           K DISK
     FKODTFS    IF A E           K DISK
     FKODTG2    IF A E           K DISK
     FKODTDIS   IF A E           K DISK
     FHEADF     IF A E           K DISK
     FHEAD12    IF   E           K DISK
     F                                     RENAME(HEADFR:HE12R)
     FDOKEF     IF A E           K DISK
     FDOKEQ     IF A E           K DISK
     FCUNDL01   IF A E           K DISK
     FFAK2      IF A E           K DISK
     FCNFF      UF   E             DISK
     FLOCL01    IF   E           K DISK
     FLOGF      O    E             DISK
     FFZB00F    O    E             DISK
     FFZB05F    O    E             DISK
     FFZB06F    O    E             DISK
     FFZB10F    O    E             DISK
     FFZB12F    O    E             DISK
     FFZB13F    O    E             DISK
      *
     D KA1             S              4  0 DIM(6)
     D FK              S              7    DIM(9)
     D FN              S             10  2 DIM(9)
     D FL              S              1    DIM(3)
     D MND             S              2  0 DIM(12) CTDATA PERRCD(1)
     D MNA             S              3    DIM(12) ALT(MND)
     D WR1             S              1    DIM(9)
     D WR2             S              1    DIM(17)
     D WR3             S              1    DIM(30)
     D AR1             S              1    DIM(30)
     D AR2             S              1    DIM(30)
      *
     D                 DS
     D  KA                     1     24
     D                                     DIM(6)
     D  DFNT1                  1      4
     D  DFNT2                  5      8
     D  DFNT3                  9     12
     D  DFNT4                 13     16
     D  DFNT5                 17     20
     D  DFNT6                 21     24
     D                 DS
     D  VE                     1     42  1
     D                                     DIM(6)
     D  DFVKT1                 1      7  1
     D  DFVKT2                 8     14  1
     D  DFVKT3                15     21  1
     D  DFVKT4                22     28  1
     D  DFVKT5                29     35  1
     D  DFVKT6                36     42  1
     D                 DS
     D  RA                     1      6
     D                                     DIM(6)
     D  DFRC1                  1      1
     D  DFRC2                  2      2
     D  DFRC3                  3      3
     D  DFRC4                  4      4
     D  DFRC5                  5      5
     D  DFRC6                  6      6
     D                 DS
     D  IT                     1     24
     D                                     DIM(6)
     D  DFCOM1                 1      4
     D  DFCOM2                 5      8
     D  DFCOM3                 9     12
     D  DFCOM4                13     16
     D  DFCOM5                17     20
     D  DFCOM6                21     24
     D                 DS
     D  FV                     1     42  1
     D                                     DIM(6)
     D  DFFBV1                 1      7  1
     D  DFFBV2                 8     14  1
     D  DFFBV3                15     21  1
     D  DFFBV4                22     28  1
     D  DFFBV5                29     35  1
     D  DFFBV6                36     42  1
     D                 DS
     D  MI                     1      6
     D                                     DIM(6)
     D  DFRK1                  1      1
     D  DFRK2                  2      2
     D  DFRK3                  3      3
     D  DFRK4                  4      4
     D  DFRK5                  5      5
     D  DFRK6                  6      6
     D                 DS
     D  PR                     1     48  2
     D                                     DIM(6)
     D  DFRPR1                 1      8  2
     D  DFRPR2                 9     16  2
     D  DFRPR3                17     24  2
     D  DFRPR4                25     32  2
     D  DFRPR5                33     40  2
     D  DFRPR6                41     48  2
     D                 DS
     D  BE                     1     60  2
     D                                     DIM(6)
     D  DFBLT1                 1     10  2
     D  DFBLT2                11     20  2
     D  DFBLT3                21     30  2
     D  DFBLT4                31     40  2
     D  DFBLT5                41     50  2
     D  DFBLT6                51     60  2
     D                 DS
     D  VA                     1    250
     D                                     DIM(10)
     D  DFVS1                  1     25
     D  DFVS2                 26     50
     D  DFVS3                 51     75
     D  DFVS4                 76    100
     D  DFVS5                101    125
     D  DFVS6                126    150
     D  DFVS7                151    175
     D  DFVS8                176    200
     D  DFVS9                201    225
     D  DFVS10               226    250
     D                 DS
     D  FIN                    1    245
     D                                     DIM(7)
     D  DFINF1                 1     35
     D  DFINF2                36     70
     D  DQINF3                71    105
     D  DQINF4               106    140
     D  DQINF5               141    175
     D  DQINF6               176    210
     D  DQINF7               211    245
     D                 DS
     D  ROUTIN                 1     15
     D  WWA                    1      3
     D  WWB                    5     10
     D  WWC                   11     11
     D  WWD                   12     13
     D                 DS
     D  DS142                  1     31
     D  HEAVD                  1      4  0
     D  SLASJ                  5      5
     D  HEOPD                  6     12  0
     D  SLASJ2                13     13
     D  HESG                  14     16
     D  SLASJ3                17     17
     D  HESAML                18     23
     D                 DS
     D  WRK2T3                 1    142
     D  WRK2A                  1     65
     D  WRK2B                 66    130
     D  WRK2C                131    142
     D                 DS
     D  O12DS                  1      1
     D  O12DS1                 1      3
     D  O12DS2                 4     20
     D  O12115                 1     20
     D                 DS
     D  DFCCPP                 1      2
     D  WDFCP1                 1      1
     D  WDFCP2                 2      2
     D                 DS
     D  WRKALF                 1     20
     D  WRKA17                 1     17
     D  WRKAL3                18     20
     D                 DS
     D  WFRO3                  1     17
     D  DFROA3                 1      3
     D  DFROB3                 5     11
     D  DFROC3                13     14  0
     D                 DS
     D  WARECA                 1      9  0
     D                 DS            14
     D PRICAR          C                   CONST('PRIORITY CARGO')
     D                 DS            11
     D NUM             C                   CONST(' 0123456789')
     D                SDS
     D  WUSER                254    263
      ***********************************************
      *
     C                   EXSR      INIT
      *
     C     *IN90         CABEQ     *ON           ENDPGM
     C     *IN90         IFEQ      '0'
     C                   EXSR      FINVER
     C                   EXSR      WRTINH                                       <Write inhouse
     C     *IN90         IFEQ      '0'
     C                   EXSR      WRTLOG                                       <Write log
     C                   EXSR      SNDTAQ                                       <Send dataq
     C                   ENDIF                                                  <*IN90
     C                   ENDIF                                                  <*IN90
      *
     C     ENDPGM        TAG
     C                   RETURN
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    AVD
     C                   PARM                    OPD
     C                   PARM                    HWB
     C                   PARM                    PGID
     C                   PARM                    PGIDNE
     C                   PARM                    PGPACP
     C                   PARM                    PGPACA
     C                   PARM                    PGCPAI
     C                   PARM                    PGCPAA
     C                   PARM                    PGMTM1
     C                   PARM                    PGMTM2
     C                   PARM                    PGMTM3
      *
     C     *LIKE         DEFINE    HEHAWB        HWB
     C     *LIKE         DEFINE    HEHAWB        WHWB
     C     *LIKE         DEFINE    HEAVD         AVD
     C     *LIKE         DEFINE    HEOPD         OPD
     C     *LIKE         DEFINE    HEAVD         WAVD
     C     *LIKE         DEFINE    HEOPD         WOPD
     C     *LIKE         DEFINE    LGID          PGID
     C     *LIKE         DEFINE    LGIDNE        PGIDNE
     C     *LIKE         DEFINE    LGPACP        PGPACP
     C     *LIKE         DEFINE    LGPACA        PGPACA
     C     *LIKE         DEFINE    LGCPAI        PGCPAI
     C     *LIKE         DEFINE    LGCPAA        PGCPAA
     C     *LIKE         DEFINE    LGMTM1        PGMTM1
     C     *LIKE         DEFINE    LGMTM2        PGMTM2
     C     *LIKE         DEFINE    LGMTM3        PGMTM3
      *
     C                   Z-ADD     AVD           WAVD
     C                   Z-ADD     OPD           WOPD
     C                   MOVEL     HWB           WHWB
      *
     C     OPDKEY        KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WOPD
     C     HWBKEY        KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WHWB
     C     DOKKEY        KLIST
     C                   KFLD                    WFRI
     C                   KFLD                    WAVD
     C                   KFLD                    WOPD
     C     DOKEQK        KLIST
     C                   KFLD                    WQRI
     C                   KFLD                    WAVD
     C                   KFLD                    WOPD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KOAKNR
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    WAVD
     C     KODSFK        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    HESG
     C     KODFSK        KLIST
     C                   KFLD                    YFSUNI
     C                   KFLD                    DFFSN
     C     FAK2K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YSK
     C     KODG2K        KLIST
     C                   KFLD                    YG2UNI
     C                   KFLD                    FAVK
      *
     C                   MOVE      '/'           SLASJ             1
     C                   MOVE      '/'           SLASJ2            1
     C                   MOVE      '/'           SLASJ3            1
     C                   MOVE      'F'           WFRI              1
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      'Q'           WQRI              1
     C                   MOVE      'SF'          YSFUNI            2
     C                   MOVE      'FS'          YFSUNI            2
     C                   MOVE      ' '           YSK               1
     C                   MOVE      'G2'          YG2UNI            2
     C                   MOVEL     WUSER         WWUSER           17
      * Nullstilling av recordformater og variabler siden ikke LR
     C                   CLEAR                   KOAR
     C                   CLEAR                   STED2R
     C                   CLEAR                   KOSFR
     C                   CLEAR                   KODTFSR
     C                   CLEAR                   KODTDISR
     C                   CLEAR                   KOSFR
     C                   CLEAR                   KOG2R
     C                   CLEAR                   HEADFR
     C                   CLEAR                   DOKEFR
     C                   CLEAR                   DOKEQR
     C                   CLEAR                   KUNDR
     C                   CLEAR                   FAKTR
     C                   Z-ADD     0             XBELTO
     C                   Z-ADD     0             XCACOL
     C                   Z-ADD     0             XCAPRE
     C                   MOVE      *BLANKS       XFRAF2
     C                   Z-ADD     0             XTKJØ
      *
     C     *IN99         IFEQ      *OFF
     C                   READ      FIRM                                   90
     C                   MOVE      *ON           *IN99
     C                   ENDIF
     C     WAVD          IFEQ      *ZEROS
     C                   SETON                                        90
     C                   GOTO      INIT99
     C                   ENDIF
     C     WOPD          IFEQ      *ZEROS
     C     WHWB          ANDEQ     *BLANKS
     C                   SETON                                        90
     C                   GOTO      INIT99
     C                   ENDIF
     C     WOPD          IFNE      *ZEROS
     C     OPDKEY        CHAIN     HEADF                              90
     C                   ELSE
     C     HWBKEY        CHAIN     HEAD12                             90
     C                   ENDIF
     C     *IN90         CABEQ     '1'           INIT99
     C     DOKKEY        CHAIN     DOKEF                              90
     C     *IN90         CABEQ     '1'           INIT99
     C     DOKEQK        CHAIN     DOKEQ                              90
     C     KODSFK        CHAIN     KODTSF                             90
     C     KODFSK        CHAIN     KODTFS                             90
     C     WWUSER        CHAIN     LOCL01                             9090
     C     *IN90         CABEQ     '1'           INIT99
     C     1             CHAIN     CNFF                               9090
     C     *IN90         CABEQ     '1'           INIT99
      * HENTER NESTE LEDIGE TRANSAKSJONS ID
     C                   ADD       1             CNRECN
     C                   UPDATE    CNFR
      * HVIS ALT GIKK GREIT KAN RUTINEN UTFØRES
      * Dummy write
     C     *IN90         IFEQ      *OFF
     C     *IN90         ANDEQ     *ON
     C                   WRITE     KOAR
     C                   WRITE     STED2R
     C                   WRITE     KOSFR
     C                   WRITE     KODTFSR
     C                   WRITE     KOSFR
     C                   WRITE     KODTDISR
     C                   WRITE     KOG2R
     C                   WRITE     HEADFR
     C                   WRITE     DOKEFR
     C                   WRITE     DOKEQR
     C                   WRITE     KUNDR
     C                   WRITE     FAKTR
     C                   ENDIF
     C     INIT99        TAG
     C                   ENDSR
      ***********************************************
     C     FINVER        BEGSR
      ***********************************************
      * Finner en del nøkkeltall som ellers blir beregnet av SYEF14
     C     'DISPU'       CHAIN     KODTDIS                            33
     C                   MOVE      *ZEROS        XTDUA             9 2
     C     DFCCPP        IFEQ      'CC'
     C                   MOVE      *ON           *IN70
     C                   ELSE
     C                   MOVE      *OFF          *IN70
     C                   ENDIF
     C     *IN70         IFEQ      *ON
     C                   EXSR      SUM
     C                   Z-ADD     XTKJØ         XTDUA
     C                   ELSE
     C                   Z-ADD     KDSBL4        XTDUA
     C     XTDUA         IFEQ      0
     C                   EXSR      SUM3
     C                   ENDIF
     C                   ENDIF
      *
     C     HESDF         CHAIN     STED2                              90
     C     *IN90         IFEQ      *ON
     C                   MOVEL     *BLANKS       XFRAF2
     C                   ENDIF
      *
     C     *IN70         IFEQ      *ON
     C     DFKDDC        ANDEQ     'P'
     C                   Z-ADD     DFBLDC        XCAPRE            9 2
     C                   ENDIF
      *
     C     *IN70         IFEQ      *OFF
     C     DFKDDC        ANDEQ     'C'
     C                   Z-ADD     DFBLDC        XCACOL            9 2
     C                   ENDIF
      *
     C     DFBLT         ADD       DFBLDC        XBELTO            9 2
     C                   ADD       DFBLA         XBELTO
     C                   ADD       DFBLAV        XBELTO
     C                   ADD       XTDUA         XBELTO
      *
     C                   EXSR      MOVKA
      *
     C                   ENDSR
      ***********************************************
     C     WRTINH        BEGSR
      ***********************************************
      * SKRIVER POST TIL FZB00F
     C                   CLEAR                   FZB00R
     C                   Z-ADD     CNRECN        O00REC
     C                   MOVEL     DS142         O0142
     C                   MOVEL     *BLANKS       WRK
     C     11            SUBST     HEGN:5        WRK
     C                   EXSR      CHKNUM
     C     *IN91         IFEQ      '0'
     C     *IN92         ANDEQ     '0'
     C     3             SUBST     HEGN:5        W0222             3
     C                   MOVEL     W0222         O0222
     C     8             SUBST     HEGN:8        W0224             8
     C                   MOVEL     W0224         O0224
     C                   ELSE
     C                   MOVE      *ON           *IN90
     C                   GOTO      WRT99
     C                   ENDIF
     C                   MOVEL     HEHAWB        O0232
     C                   MOVEL     HESDF         O0242
     C                   MOVEL     HESDT         O0243
     C                   Z-ADD     DFNTT         O0253
     C                   MOVEL     'K'           O0254
     C                   Z-ADD     DFVKTT        O0255
     C                   MOVEL     DFROB1        O0322A
     C     4             SUBST     DFROB1:3      O0323A
     C                   Z-ADD     DFROC1        O0325A
     C                   MOVEL     DFROB2        O0322B
     C     4             SUBST     DFROB2:3      O0323B
     C                   Z-ADD     DFROC2        O0325B
     C                   MOVEL     DFROA1        O0422
     C                   MOVEL     DFROB1        O0423
     C                   MOVEL     DFROA2        O0432A
     C                   MOVEL     DFROB2        O0433A
     C                   MOVEL     DFROA3        O0432B
     C                   MOVEL     DFROB3        O0433B
     C                   MOVEL     *BLANKS       O0522
     C                   MOVEL     *BLANKS       O0542
     C                   MOVEL     HENAS         O0542
     C                   MOVEL     *BLANKS       O0552
     C                   MOVEL     HEADS1        O0552
     C                   Z-ADD     1             XW                3 0
     C                   Z-ADD     0             MY                3 0
     C                   MOVEA     *BLANKS       WR3
     C                   MOVEA     *BLANKS       WR2
     C                   MOVEA     *BLANKS       WR1
     C                   MOVEA     HEADS3        WR3
     C     WR3(XW)       DOWNE     *BLANK
     C     XW            ANDLE     9
     C                   MOVE      WR3(XW)       WR1(XW)
     C                   ADD       1             XW
     C                   ENDDO
     C                   DO        17
     C                   ADD       1             XW
     C                   ADD       1             MY
     C                   MOVE      WR3(XW)       WR2(MY)
     C                   ENDDO
     C                   MOVEA     WR2           O0562
     C                   MOVEL     *BLANKS       O0564
     C                   MOVEL     HELKS         O0572
     C     O0572         IFEQ      *BLANKS
     C     HESDF         CHAIN     STED2                              66
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     ST2LK         O0572
     C                   ENDIF
     C                   ENDIF
     C                   MOVEA     WR1           O0574
     C     DFTLFS        IFNE      *BLANKS
     C                   Z-ADD     CNRECN        O05REC
     C                   MOVEL     'TE'          O0582
     C                   MOVEL     DFTLFS        O0584
     C                   WRITE     FZB05R
     C                   ENDIF
     C                   MOVEL     *BLANKS       O0622
     C                   MOVEL     *BLANKS       O0642
     C                   MOVEL     HENAK         O0642
     C                   MOVEL     *BLANKS       O0652
     C                   MOVEL     HEADK1        O0652
     C                   Z-ADD     1             XW                3 0
     C                   Z-ADD     0             MY
     C                   MOVEA     *BLANKS       WR3
     C                   MOVEA     *BLANKS       WR2
     C                   MOVEA     *BLANKS       WR1
     C                   MOVEA     HEADK3        WR3
     C     WR3(XW)       DOWNE     *BLANK
     C     XW            ANDLE     9
     C                   MOVE      WR3(XW)       WR1(XW)
     C                   ADD       1             XW
     C                   ENDDO
     C                   DO        17
     C                   ADD       1             XW
     C                   ADD       1             MY
     C                   MOVE      WR3(XW)       WR2(MY)
     C                   ENDDO
     C                   MOVEA     WR2           O0662
     C     O0662         IFEQ      *BLANKS
     C                   MOVEL     HEADK2        O0662
     C                   ENDIF
     C                   MOVEL     *BLANKS       O0664
     C                   MOVEL     HELKK         O0672
     C     O0672         IFEQ      *BLANKS
     C                   MOVEL     HELKA         O0672
     C                   ENDIF
     C     O0672         IFEQ      *BLANKS
     C     HESDT         CHAIN     STED2                              90
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     ST2LK         O0672
     C                   ENDIF
     C                   ENDIF
     C                   MOVEA     WR1           O0674
     C     DFTLFK        IFNE      *BLANKS
     C                   Z-ADD     CNRECN        O06REC
     C                   MOVEL     'TE'          O0682
     C                   MOVEL     DFTLFK        O0684
     C                   WRITE     FZB06R
     C                   ENDIF
     C                   MOVEL     *BLANKS       O0722
     C     KODTAK        CHAIN     KODTA                              90
     C     *IN90         IFEQ      *OFF
     C     KUND1K        CHAIN     CUNDL01                            90
     C                   ENDIF
     C                   MOVEL     KNAVN         O0742
     C                   MOVEL     ADR3          O0752
     C                   MOVE      ADR3          XFRAF2           15
     C                   MOVEL     DQINFD        O083A
     C                   MOVEL     DFVAL         O113
     C                   MOVEL     WDFCP1        O1152
     C                   MOVEL     WDFCP2        O1153
     C                   MOVEL     *BLANKS       WRK
     C                   MOVEL     DFDVCA        WRK
     C                   EXSR      CHKNUM
     C     *IN91         IFEQ      '0'
     C     *IN92         ANDEQ     '0'
     C                   MOVE      *BLANKS       WRKALF
     C                   MOVE      WRK           WRKA17
     C                   MOVE      *ZEROS        WRKAL3
     C                   MOVE      WRKALF        O1162
     C                   ELSE
     C                   Z-ADD     0             O1162
     C                   END                                                    <*IN91
     C                   MOVEL     *BLANKS       WRK
     C                   MOVEL     DFDVCU        WRK
     C                   EXSR      CHKNUM
     C     *IN91         IFEQ      '0'
     C     *IN92         ANDEQ     '0'
     C                   MOVE      *BLANKS       WRKALF
     C                   MOVE      WRK           WRKA17
     C                   MOVE      *ZEROS        WRKAL3
     C                   MOVE      WRKALF        O1172
     C                   ELSE
     C                   Z-ADD     0             O1172
     C                   END                                                    <*IN90
     C                   Z-ADD     DFBELF        O1182
     C     O1152         IFEQ      'P'
     C                   Z-ADD     DFBLT         O1423
     C                   Z-ADD     DFBLA         O1433
     C                   Z-ADD     DFBLAV        O1443
     C                   ENDIF                                                  <W1171
     C     O1153         IFEQ      'P'
     C                   Z-ADD     XTDUA         O1463
     C     DFBLDC        IFNE      0
     C                   Z-ADD     DFBLDC        O1473
     C                   ELSE                                                   <DFBLDC
     C                   Z-ADD     XCAPRE        O1473
     C                   END                                                    <DFBLDC
     C     XBELTO        IFNE      0
     C                   Z-ADD     XBELTO        O1483A
     C                   ELSE                                                   <DFBLDC
     C                   Z-ADD     XCAPRE        O1483A
     C                   END                                                    <XBELTO
     C                   ENDIF                                                  <W1172
      * Hvis MI er lik 'A' (As Agreed) nullstilles beløpsfelt
     C     MI(1)         IFEQ      'A'
     C     O1483A        ANDGT     0
     C                   Z-ADD     0             O1423
     C                   Z-ADD     0             O1433
     C                   Z-ADD     0             O1443
     C                   Z-ADD     0             O1463
     C                   Z-ADD     0             O1473
     C                   Z-ADD     0             O1483A
     C                   MOVE      'A'           O1483B
     C                   ENDIF
     C     O1152         IFEQ      'C'
     C                   Z-ADD     DFBLT         O1523
     C                   Z-ADD     DFBLA         O1533
     C                   Z-ADD     DFBLAV        O1543
     C                   ENDIF                                                  <W1171
     C     O1153         IFEQ      'C'
     C                   Z-ADD     XTDUA         O1563
     C     DFBLDC        IFNE      0
     C                   Z-ADD     DFBLDC        O1573
     C                   ELSE                                                   <DFBLDC
     C                   Z-ADD     XCACOL        O1573
     C                   END                                                    <DFBLDC
     C     XBELTO        IFNE      0
     C                   Z-ADD     XBELTO        O1583A
     C                   ELSE                                                   <DFBLDC
     C                   Z-ADD     XCACOL        O1583A
     C                   END                                                    <XBELTO
     C                   ENDIF                                                  <W1172
      * Hvis MI er lik 'A' (As Agreed) nullstilles beløpsfelt
     C     MI(1)         IFEQ      'A'
     C     O1583A        ANDGT     0
     C                   Z-ADD     0             O1523
     C                   Z-ADD     0             O1533
     C                   Z-ADD     0             O1543
     C                   Z-ADD     0             O1563
     C                   Z-ADD     0             O1573
     C                   Z-ADD     0             O1583A
     C                   MOVE      'A'           O1583B
     C                   ENDIF
      *
     C                   MOVEL     KNAVN         O163
     C                   MOVE      UDAY          O1722
     C                   Z-ADD     1             MX                3 0
     C     UMONTH        LOOKUP    MND(MX)                                93
     C   93              MOVE      MNA(MX)       O1723
     C                   MOVE      UYEAR         O1724
     C                   MOVEL     XFRAF2        O1726
     C                   MOVEL     KOSFNV        O1732
     C                   MOVEL     DFINFA        O183A
     C                   MOVEL     DFINFB        O183B
     C                   MOVEL     DFINFC        O183C
     C     O183A         IFEQ      *BLANKS
     C                   MOVEL     O183B         O183A
     C                   MOVEL     O183C         O183B
     C                   MOVEL     *BLANKS       O183C
     C                   ENDIF
     C     O183A         IFEQ      *BLANKS
     C                   MOVEL     O183B         O183A
     C                   MOVEL     *BLANKS       O183B
     C                   ENDIF
     C     WFRO3         IFNE      *BLANKS
     C     O183C         ANDEQ     *BLANKS
     C                   MOVEL     O183B         O183C
     C                   MOVEL     O183A         O183B
     C                   MOVEL     *BLANKS       O183A
     C                   MOVEL     WFRO3         O183A
     C                   ENDIF
      * Agenten er lest inn tidligere for utfylling av 742
     C                   MOVEL     KNAVN         O2022
     C                   MOVEL     ADR1          O2032
     C                   MOVEL     ADR3          O2042
     C                   MOVEL     *BLANKS       O2044
     C                   MOVEL     HELKS         O2052
     C     O2052         IFEQ      *BLANKS
     C     HESDF         CHAIN     STED2                              66
     C     *IN90         IFEQ      *OFF
     C                   MOVEL     ST2LK         O2052
     C                   ENDIF
     C                   ENDIF
     C     POSTNR        IFNE      *ZEROS
     C                   MOVEL     POSTNR        O2054
     C                   ELSE
     C                   MOVEL     SYPOGE        O2054
     C                   ENDIF
     C                   WRITE     FZB00R
      *
      * SKRIVER POSTER TIL FZB10F (ACCOUNTING INFORMATION)
     C                   Z-ADD     1             MX
S01  C*                  DO        7
N01  C                   DO        6
     C     FIN(MX)       IFNE      *BLANKS
     C                   CLEAR                   FZB10R
     C                   Z-ADD     CNRECN        O10REC                         <FELLES
     C                   MOVEL     'GEN'         O1022                          <FELLES
     C                   MOVEL     FIN(MX)       O1024
     C                   WRITE     FZB10R
     C                   ENDIF
     C                   ADD       1             MX
     C                   ENDDO                                                  <FIN,MX
      *
      * SKRIVER POSTER TIL FZB12F (RATE DESCRIPTION)
     C                   Z-ADD     1             MX
     C                   DO        6
     C     KA1(MX)       IFNE      *ZEROS
     C     KA(MX)        ORNE      *BLANKS
     C     RA(MX)        ORNE      *BLANKS
     C     VE(MX)        ORNE      *ZEROS
     C     MI(MX)        ORNE      *BLANKS
     C     IT(MX)        ORNE      *BLANKS
     C     FV(MX)        ORNE      *ZEROS
     C     PR(MX)        ORNE      *ZEROS
     C     BE(MX)        ORNE      *ZEROS
     C     VA(MX)        ORNE      *BLANKS
     C                   CLEAR                   FZB12R
     C                   Z-ADD     CNRECN        O12REC                         <FELLES
     C     KA1(MX)       IFGT      *ZEROS
     C                   Z-ADD     KA1(MX)       O1233A
     C                   MOVE      *BLANKS       O1233B
     C                   ELSE                                                   <KA1,MX
     C                   MOVEL     KA(MX)        O1233B
     C                   Z-ADD     0             O1233A
     C                   END                                                    <KA1,MX
     C                   Z-ADD     VE(MX)        O1243
     C     O1243         IFGT      0
     C                   MOVE      'K'           O1242
     C                   ELSE
     C                   MOVE      *BLANK        O1242
     C                   ENDIF
     C                   MOVEL     RA(MX)        O1253
     C                   MOVEL     *BLANKS       WRK
     C                   MOVEL     IT(MX)        WRK
     C                   EXSR      CHKNUM
     C     *IN91         IFEQ      '0'
     C     *IN92         ANDEQ     '0'
     C                   MOVE      WRK           O1263A
     C                   ENDIF
     C                   Z-ADD     FV(MX)        O1273
     C                   Z-ADD     PR(MX)        O1283
     C                   Z-ADD     BE(MX)        O1293A
     C     O1283         IFEQ      0
     C                   Z-ADD     O1293A        O1283
     C                   ENDIF
     C                   MOVEL     *BLANKS       O12115
     C                   MOVEL     VA(MX)        O12115
     C     O12DS         IFGE      '0'
     C     O12DS         ANDLE     '9'                                          <DIM. INFO
     C                   MOVEL     *BLANKS       O12115
     C                   MOVE      'DIM'         O12DS1
     C                   MOVEL     VA(MX)        O12DS2
     C                   ENDIF                                                  <W12DS
      * Hvis MI er lik 'P' legger 'J' på ratelinje 1.
     C     MI(1)         IFEQ      'P'
     C     MX            ANDEQ     1
     C                   MOVE      'J'           O12192
     C                   ENDIF
      * Hvis MI er lik 'A' (As Agreed) nullstilles beløpsfelt
     C     MI(1)         IFEQ      'A'
     C                   Z-ADD     0             O1273
     C                   Z-ADD     0             O1283
     C                   Z-ADD     0             O1293A
     C                   MOVE      'A'           O1293B
     C                   ENDIF
     C                   WRITE     FZB12R
     C                   ENDIF                                                  <KA1,MX
     C                   ADD       1             MX
     C                   ENDDO                                                  <DO 6
      * Sjekker om det er flere tekstlinjer som skal sendes.           e
     C                   DO        4
     C     VA(MX)        IFNE      *BLANKS
     C                   CLEAR                   FZB12R
     C                   Z-ADD     CNRECN        O12REC                         <FELLES
     C                   MOVEL     VA(MX)        O12115
     C                   WRITE     FZB12R
     C                   ADD       1             MX
     C                   ENDIF
     C                   ENDDO                                                  <DO 6
      * SKRIVER POSTER TIL FZB13F (OTHER CHARGES)
     C                   CLEAR                   FZB13R
     C                   Z-ADD     CNRECN        O13REC                         <FELLES
     C                   MOVE      O1153         O1322
      *
     C     O1463         IFGT      0
     C     O1563         ORGT      0
     C                   MOVE      'MA'          O1331A
     C                   MOVE      'A'           O1332A
     C     O1322         IFEQ      'P'
     C                   Z-ADD     O1463         O1333A
     C                   ELSE
     C                   Z-ADD     O1563         O1333A
     C                   ENDIF
     C                   ENDIF
      *
     C     O1473         IFGT      0
     C     O1573         ORGT      0
     C                   MOVE      'MC'          O1331B
     C                   MOVE      'C'           O1332B
     C     O1322         IFEQ      'P'
     C                   Z-ADD     O1473         O1333B
     C                   ELSE
     C                   Z-ADD     O1573         O1333B
     C                   ENDIF
     C                   ENDIF
      *
     C                   WRITE     FZB13R
      *
     C     WRT99         ENDSR
      ***********************************************
     C     WRTLOG        BEGSR
      ***********************************************
     C                   CLEAR                   LOGR
     C                   MOVE      PGID          LGID
     C                   MOVE      PGIDNE        LGIDNE
     C                   MOVE      PGPACP        LGPACP
     C                   MOVE      PGPACA        LGPACA
     C                   MOVE      PGCPAI        LGCPAI
     C                   MOVE      PGCPAA        LGCPAA
     C                   MOVE      PGMTM1        LGMTM1
     C                   MOVE      PGMTM2        LGMTM2
     C                   MOVE      PGMTM3        LGMTM3
     C                   Z-ADD     CNRECN        LGRECN
     C                   Z-ADD     CNRECN        LGTRID
     C                   MOVEL     LOID          LGSID
     C                   MOVEL     'FZB'         LGMSID
     C                   MOVEL     'T'           LGTR
     C                   MOVEL     'A'           LGSTAT
     C                   MOVEL     UDATE         LGDATE
     C                   TIME                    LGTIME
      *
     C                   WRITE     LOGR
      *
     C                   ENDSR
      ***********************************************
     C     SNDTAQ        BEGSR
      ***********************************************
     C                   MOVEL     'SMADTATQ'    QTNAME           10
     C                   MOVEL     '*LIBL'       QTLIB            10
     C                   Z-ADD     9             QTLEN             5 0
     C                   MOVEL     *BLANKS       QTFIEL            9
     C                   MOVEL     LGRECN        QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM                    QTNAME
     C                   PARM                    QTLIB
     C                   PARM                    QTLEN
     C                   PARM                    QTFIEL
      *
     C                   ENDSR
     C******************************************
     C     MOVKA         BEGSR
     C******************************************
     C                   Z-ADD     1             X3                1 0
     C     X3            DOWLE     6
     C                   MOVE      KA(X3)        TESTSH            1
     C     TESTSH        IFGE      '0'
     C     TESTSH        ANDLE     '9'
     C                   MOVE      KA(X3)        KA1(X3)
     C                   ELSE
     C                   MOVE      *ZEROS        KA1(X3)
     C                   END
     C                   ADD       1             X3
     C                   END
     C*
     C                   ENDSR
     C***********************************************
     C     CHKNUM        BEGSR
     C***********************************************
     C                   MOVE      *OFF          *IN91
     C                   Z-ADD     30            ÆX                3 0
     C                   Z-ADD     30            ÆY                3 0
     C                   MOVEA     *BLANKS       AR1
     C                   MOVEA     *BLANKS       AR2
     C                   MOVEA     WRK           AR1
     C     WRK           CABEQ     *BLANKS       CHK99                    92
      * POSISJONER TIL SISTE TEGN I FELTET
     C     AR1(ÆX)       DOWEQ     *BLANK
     C                   SUB       1             ÆX
     C                   ENDDO
      * ERSTATT BLANK MED NULL OG SJEKK OM DET BARE ER TALL
     C     ÆX            DOWGT     0
     C     AR1(ÆX)       IFEQ      *BLANK
     C                   MOVE      '0'           AR1(ÆX)
     C                   ENDIF
     C     AR1(ÆX)       IFGE      '0'
     C     AR1(ÆX)       ANDLE     '9'
     C                   MOVE      AR1(ÆX)       AR2(ÆY)
     C                   SUB       1             ÆX
     C                   SUB       1             ÆY
     C                   ELSE                                                   <AR1,ÆX
      * FELTET INNEHOLDER IKKE BARE TALL. 91 BLIR SATT PÅ
     C                   MOVE      *ON           *IN91
     C                   GOTO      CHK99
     C                   END                                                    <AR1,ÆX
     C                   ENDDO                                                  <ÆX
      * FYLL OPP RESTEN AV FELTET MED NULL
     C     ÆY            DOWGT     0
     C                   MOVE      '0'           AR2(ÆY)
     C                   SUB       1             ÆY
     C                   ENDDO
      *
     C                   MOVEA     AR2           WRK              30
      *
     C     CHK99         ENDSR
     C******************************************
     C     SUM           BEGSR
     C******************************************
     C                   MOVE      *BLANKS       FK
     C                   MOVE      *ZEROS        FN
     C                   Z-ADD     1             XN2               2 0
     C*
     C                   MOVE      'K'           YSK
     C     HEVALK        COMP      FICURR                             7171
     C     FAK2K         SETLL     FAK2
     C                   EXSR      SUM2
     C                   Z-ADD     XBRUTT        XTKJØ             9 2
     C                   MOVEL     XMOMS         XKMOMS            9 0
     C*
     C                   ENDSR
     C******************************************
     C     SUM2          BEGSR
     C******************************************
     C                   MOVE      *BLANKS       FK
     C                   MOVE      *ZEROS        FN
     C                   MOVE      *ZEROS        XTOTAL            9 2
     C                   MOVE      *ZEROS        XMVAGR            9 2
     C                   SETOFF                                       33
     C*
     C     STVALI        TAG
     C     FAK2K         READE     FAK2                                   33
     C  N33FAKDM         COMP      'J'                                    34
     C  N33FAVK          IFNE      'FFE'                                        1<-B
     C     FAVK          IFNE      'MC '                                         2<-B
     C   34
     CANN71              ADD       FABELN        XMVAGR            9 2
     C   34
     CAN 71              ADD       FABELV        XMVAGR
     C  N71              ADD       FABELN        XTOTAL
     C   71              ADD       FABELV        XTOTAL
     C     XN2           IFLE      9                                              3<-B
     C     KODG2K        CHAIN     KODTG2                             42
     C  N42              MOVE      KG2KRT        FK(XN2)
     C*  UNDERTRYKKE DESIMALER - FARLIG VED INSTALLASJON I F.EKS USA. (DOLLAR)
     C  N71              Z-ADD     FABELN        FN(XN2)
     C   71              Z-ADD     FABELV        FN(XN2)
     C                   ADD       1             XN2
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C  N33              GOTO      STVALI
     C     XMVAGR        MULT(H)   FITAX         XMOMS             8 1
     C     XTOTAL        ADD       XMOMS         XBRUTT            9 2
     C*
     C                   ENDSR
     C*
     C******************************************
     C     SUM3          BEGSR
     C******************************************
     C                   MOVE      'S'           YSK
     C     FAK2K         SETLL     FAK2
     C                   SETOFF                                       33
     C*
     C     STVAL2        TAG
     C     FAK2K         READE     FAK2                                   33
     C  N33FAVK          IFEQ      'FFE'
     C     DFBLT         IFLT      FABELN
     C     FABELN        SUB       DFBLT         KDSBL4
     C                   Z-ADD     KDSBL4        XTDUA
     C                   GOTO      TAGSK
     C                   END
     C                   END
     C  N33              GOTO      STVAL2
     C*
     C     TAGSK         ENDSR
**
01JAN
02FEB
03MAR
04APR
05MAY
06JUN
07JUL
08AUG
09SEP
10OCT
11NOV
12DEC
