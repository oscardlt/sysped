      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS182 MODULE(*LIBL/SS182
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FWRKCGISS  UF A E           K DISK    USROPN
     FCDBOAT    IF   E           K DISK    USROPN
     FGSSORL17  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR17)
     FGSSORL18  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR18)
     FGSSORL19  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR19)
     FGSSORL20  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR20)
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSRFF    IF   E           K DISK    USROPN
     FGSSLGL01  IF   E           K DISK    USROPN
     FVADR      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WINT            s              6
     D WINTEG          s              6  0
     D MaxSN           s              5
     D MaxRC           s              5
     D MaxSN2          s              5  0
     D MaxRC2          s              5  0
     D AntSn           s              5  0
     D AntRC           s              5  0
     D XSN             s              5  0
     D WSCNO           s              8
     D WSCNO8          s              8  0
     D WSPRO           s              4
     D WSSAL           s             10
     D WSSALMAX        s             10
     D WSBCD           s              5
     D WSLC            s              5
     D WSDTL           s              8
     D WSDTL8          s              8  0
     D WSAP            s             12
     D WSPO            s             20
     D WSPOMAX         s             20
     D WSACC           s             20
     D WSACCMAX        s             20
     D WSMS01          s              1
     D WSMS02          s              1
     D WSMS03          s              1
     D WSMS04          s              1
     D WSMS05          s              1
     D WSMS06          s              1
     D WSMS07          s              1
     D WSMS08          s              1
     D WSMS09          s              1
     D WSMS10          s              1
     D WSINKL          s              1
     D W2AP            s             12
     D W2SAL           s             35
     D W2ACC           s             35
     D W2CNA           s             61
     D lstbrd          s              1
     D NIER            s             30a   inz('999999999999999999999999999999')
     D XKN             s              8  0 DIM(100)
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D ORBCD                   1      5
     D ORBCD2                  2      5
     D                 DS
     D ORFT4                   1     30
     D ORAREA                  1      2
     D ORAAR                   4      7
     D ORMND                   8      9
     D ORAARMND                4      9
     D ORLMA                  11     14
     D                 DS
     D LGANY                   1     35
     D LGANY01                 1      7
     D LGANY02                 9     35
     D LGANY11                 1      5
     D LGANY12                 7     35
     D                 DS
     D VADRN3                  1     30
     D VADRN32                 1     10
     D                 DS
     D WKEY1                   1    256
     D KEY1                    1    256    DIM(256)
     D                 DS
     D WKEY2                   1    256
     D KEY                     1    256    DIM(256)
     D                 DS
     D                 DS
     D WKEY3                   1    256
     D KEY3                    1    256    DIM(256)
     D                 DS
     D WDATA                   1   1024
     D DATA                    1   1024    DIM(1024)
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   exsr      GetData
     C                   IF        XFOUND = 'N'
     C                   callp     wrtsection('0found')
     C                   ELSE
     C                   exsr      SetData
     C                   callp     wrtsection('foundsome')
     C                   END
      *
     C                   exsr      SetEnd
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      WKEY1   = zhbgetvar('WKEY')
     C                   IF        WKEY1   = *BLANKS
     C                   EVAL      WINT    = zhbgetvar('WINT')
     C                   eval      WINTEG  = c2n2(WINT)
     C                   eval      MaxSn   = zhbgetvar('MaxSn')
     C                   eval      MaxSn2  = c2n2(MaxSn)
     C                   eval      MaxRc   = zhbgetvar('MaxRc')
     C                   eval      MaxRc2  = c2n2(MaxRc)
     C                   EVAL      WSCNO   = zhbgetvar('WSCNO')
     C                   eval      WSCNO8  = c2n2(WSCNO)
     C                   EVAL      WSPRO   = zhbgetvar('WSPRO')
     C                   eval      WSPRO   = uppify(WSPRO)
     C                   EVAL      WSSAL   = zhbgetvar('WSSAL')
     C                   eval      WSSAL   = uppify(WSSAL)
     C                   eval      WSSALMAX= WSSAL
     C                   CAT       NIER:0        WSSALMAX
     C                   EVAL      WSBCD   = zhbgetvar('WSBCD')
     C                   eval      WSBCD   = uppify(WSBCD)
     C                   EVAL      WSLC    = zhbgetvar('WSLC')
     C                   eval      WSLC    = uppify(WSLC)
     C                   EVAL      WSDTL   = zhbgetvar('WSDTL')
     C                   eval      WSDTL8  = c2n2(WSDTL)
     C                   EVAL      WSAP    = zhbgetvar('WSAP')
     C                   eval      WSAP    = uppify(WSAP)
     C                   EVAL      WSPO    = zhbgetvar('WSPO')
     C                   eval      WSPO    = uppify(WSPO)
     C                   eval      WSPOMAX = WSPO
     C                   CAT       NIER:0        WSPOMAX
     C                   EVAL      WSACC   = zhbgetvar('WSACC')
     C                   eval      WSACC   = uppify(WSACC)
     C                   eval      WSACCMAX= WSACC
     C                   CAT       NIER:0        WSACCMAX
     C                   EVAL      WSMS01  = zhbgetvar('WSMS01')
     C                   EVAL      WSMS02  = zhbgetvar('WSMS02')
     C                   EVAL      WSMS03  = zhbgetvar('WSMS03')
     C                   EVAL      WSMS04  = zhbgetvar('WSMS04')
     C                   EVAL      WSMS05  = zhbgetvar('WSMS05')
     C                   EVAL      WSMS06  = zhbgetvar('WSMS06')
     C                   EVAL      WSMS07  = zhbgetvar('WSMS07')
     C                   EVAL      WSMS08  = zhbgetvar('WSMS08')
     C                   EVAL      WSMS09  = zhbgetvar('WSMS09')
     C                   EVAL      WSMS10  = zhbgetvar('WSMS10')
     C                   EVAL      WSINKL  = zhbgetvar('WSINKL')
     C                   MOVE      'X'           KEY1(1)
     C                   MOVEA     WSCNO         KEY1(2)
     C                   MOVEA     WSPRO         KEY1(10)
     C                   MOVEA     WSSAL         KEY1(14)
     C                   MOVEA     WSBCD         KEY1(24)
     C                   MOVEA     WSLC          KEY1(29)
     C                   MOVEA     WSDTL         KEY1(34)
     C                   MOVEA     WSAP          KEY1(42)
     C                   MOVEA     WSPO          KEY1(54)
     C                   MOVEA     WSACC         KEY1(74)
      * POS. 94 - 98 LEDIGE
     C                   MOVE      WSINKL        KEY1(99)
     C                   MOVE      WSMS01        KEY1(100)
     C                   MOVE      WSMS02        KEY1(101)
     C                   MOVE      WSMS03        KEY1(102)
     C                   MOVE      WSMS04        KEY1(103)
     C                   MOVE      WSMS05        KEY1(104)
     C                   MOVE      WSMS06        KEY1(105)
     C                   MOVE      WSMS07        KEY1(106)
     C                   MOVE      WSMS08        KEY1(107)
     C                   MOVE      WSMS09        KEY1(108)
     C                   MOVE      WSMS10        KEY1(109)
     C                   MOVEA     WINT          KEY1(110)
     C                   MOVEA     MaxSn         KEY1(116)
     C                   MOVEA     MaxRc         KEY1(121)
     C                   MOVEA     'J'           KEY1(256)
     C                   ELSE
     C                   MOVEA     KEY1(2)       WSCNO
     C                   eval      WSCNO8  = c2n2(WSCNO)
     C                   MOVEA     KEY1(10)      WSPRO
     C                   MOVEA     KEY1(14)      WSSAL
     C                   eval      WSSALMAX= WSSAL
     C                   CAT       NIER:0        WSSALMAX
     C                   MOVEA     KEY1(24)      WSBCD
     C                   MOVEA     KEY1(29)      WSLC
     C                   MOVEA     KEY1(34)      WSDTL
     C                   eval      WSDTL8  = c2n2(WSDTL)
     C                   MOVEA     KEY1(42)      WSAP
     C                   MOVEA     KEY1(54)      WSPO
     C                   eval      WSPOMAX = WSPO
     C                   CAT       NIER:0        WSPOMAX
     C                   MOVEA     KEY1(74)      WSACC
     C                   eval      WSACCMAX= WSACC
     C                   CAT       NIER:0        WSACCMAX
      * POS. 94 - 98 LEDIGE
     C                   MOVEA     KEY1(99)      WSINKL
     C                   MOVEA     KEY1(100)     WSMS01
     C                   MOVEA     KEY1(101)     WSMS02
     C                   MOVEA     KEY1(102)     WSMS03
     C                   MOVEA     KEY1(103)     WSMS04
     C                   MOVEA     KEY1(104)     WSMS05
     C                   MOVEA     KEY1(105)     WSMS06
     C                   MOVEA     KEY1(106)     WSMS07
     C                   MOVEA     KEY1(107)     WSMS08
     C                   MOVEA     KEY1(108)     WSMS09
     C                   MOVEA     KEY1(109)     WSMS10
     C                   MOVEA     KEY1(110)     WINT
     C                   eval      WINTEG  = c2n2(WINT)
     C                   MOVEA     KEY1(116)     MaxSn
     C                   eval      MaxSn2  = c2n2(MaxSn)
     C                   MOVEA     KEY1(121)     MaxRc
     C                   eval      MaxRc2  = c2n2(MaxRc)
     C                   END
     C                   eval      W2AP    = WSAP
     C                   cat       '9999999999':0W2AP
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcname)
     C                   callp     updHtmlVar('MaxSn':MaxSn)
     C                   callp     updHtmlVar('MaxRc':MaxRc)
     C                   callp     updhtmlvar('WKEY':WKEY1)
     C                   eval      WKEY3 = WKEY1
     C                   MOVEA     '          '  KEY3(100)
     C                   MOVE      'o'           KEY3(107)
     C                   callp     updhtmlvar('WKEY01':WKEY3)
     C                   MOVE      ' '           KEY3(107)
     C                   MOVE      'o'           KEY3(108)
     C                   callp     updhtmlvar('WKEY02':WKEY3)
     C                   MOVE      ' '           KEY3(108)
     C                   MOVE      'o'           KEY3(101)
     C                   callp     updhtmlvar('WKEY03':WKEY3)
     C                   MOVE      ' '           KEY3(101)
     C                   MOVE      'o'           KEY3(104)
     C                   callp     updhtmlvar('WKEY04':WKEY3)
     C                   MOVE      ' '           KEY3(104)
     C                   MOVE      'o'           KEY3(103)
     C                   callp     updhtmlvar('WKEY05':WKEY3)
     C                   MOVE      ' '           KEY3(103)
     C                   MOVE      'o'           KEY3(106)
     C                   callp     updhtmlvar('WKEY06':WKEY3)
     C                   MOVE      ' '           KEY3(106)
     C                   MOVE      'o'           KEY3(105)
     C                   callp     updhtmlvar('WKEY07':WKEY3)
     C                   MOVE      ' '           KEY3(105)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS182'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  Hent data til arbeidsfil
      *=====================================================================******
     C     GetData       begsr
      *
      * Slett gamle data
     C     WINTEG        SETLL     WRKCGISS
     C     WINTEG        READE     WRKCGISS                               33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    WRKCGISR
     C     WINTEG        READE     WRKCGISS                               33
     C  N33              ENDDO
     C                   MOVE      *BLANKS       WKEY2
     C                   MOVE      *BLANKS       WDATA
      *
      * Oppdater ny data
     C                   MOVE      'N'           XFOUND            1
      *
     C                   IF        WSCNO8 < 99999999
     C                   MOVE      WSCNO8        XKN(1)
     C                   ELSE
     C     WSUSER        SETLL     BRPARF
     C     WSUSER        READE     BRPARF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        PAID = 'SSCUS'
     C                   ADD       1             XN                3 0
     C                   Z-ADD     PAVRDN        XKN(XN)
     C                   END
     C     WSUSER        READE     BRPARF                                 33
     C  N33              ENDDO
     C                   END
      *
     C                   IF        WSINKL = ' '
     C                   MOVE      'N'           XACT              1
     C                   ELSE
     C                   MOVE      'J'           XACT
     C                   END
     C                   MOVE      'J'           XPOD              1
      *
     C     WSUSER        SETLL     BRPARF
     C     STGETDTA      TAG
     C     WSUSER        READE     BRPARF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        PAID = 'SSLOC'
     C                   Z-ADD     PAVRDN        XXKN              8 0
     C                   MOVEL     PAVRDA        XXLC              5
     C                   LEAVE
     C                   END
     C     WSUSER        READE     BRPARF                                 33
     C  N33              ENDDO
     C*    *IN33         CABEQ     *ON           SLGETDTA
     C   33              IF        XACT = 'J'
     C                   IF        XPOD = 'J'
     C                   MOVE      'N'           XPOD
     C     WSUSER        SETLL     BRPARF
     C                   GOTO      STGETDTA
     C                   ELSE
     C                   GOTO      SLGETDTA
     C                   END
     C                   ELSE
     C                   GOTO      SLGETDTA
     C                   END
      *
     C                   IF        XPOD = 'J'
     C                   SELECT
     C                   WHEN      XXKN = 0
     C     XXLC          CHAIN     CDBOAT                             33
     C     GSSOR20K      SETLL     GSSORL20
     C     GSSOR20K      READE     GSSORL20                               33
     C                   WHEN      ((XXKN >= 99999999) AND (XXLC<>'99999'))
     C     XXLC          SETLL     GSSORL19
     C     XXLC          READE     GSSORL19                               33
     C                   WHEN      ((XXKN >= 99999999) AND (XXLC='99999'))
     C     *LOVAL        SETLL     GSSORL19
     C                   READ      GSSORL19                               33
     C                   WHEN      XXLC = '99999'
     C     XXKN          SETLL     GSSORL20
     C     XXKN          READE     GSSORL20                               33
     C                   OTHER
     C     GSSOR19K      SETLL     GSSORL19
     C     GSSOR19K      READE     GSSORL19                               33
     C                   ENDSL
     C                   ELSE
     C                   SELECT
     C                   WHEN      XXKN = 0
     C     XXLC          CHAIN     CDBOAT                             33
     C     GSSOR20K      SETLL     GSSORL18
     C     GSSOR20K      READE     GSSORL18                               33
     C                   WHEN      ((XXKN >= 99999999) AND (XXLC<>'99999'))
     C     XXLC          SETLL     GSSORL17
     C     XXLC          READE     GSSORL17                               33
     C                   WHEN      ((XXKN >= 99999999) AND (XXLC='99999'))
     C     *LOVAL        SETLL     GSSORL17
     C                   READ      GSSORL17                               33
     C                   WHEN      XXLC = '99999'
     C     XXKN          SETLL     GSSORL18
     C     XXKN          READE     GSSORL18                               33
     C                   OTHER
     C     GSSOR19K      SETLL     GSSORL17
     C     GSSOR19K      READE     GSSORL17                               33
     C                   ENDSL
     C                   END
      *
     C                   DOW       *IN33 = *OFF
     C     ORST          CABEQ     'D'           LESNESTE
     C                   EVAL      AntRc = AntRc + 1
      *
     C     1             DO        100           XN
     C                   IF        ORCNO = XKN(XN)
     C                   LEAVE
     C                   END
     C     XKN(XN)       CABEQ     0             LESNESTE
     C                   ENDDO
      *
     C                   IF        WSBCD <> *BLANKS
     C     WSBCD         CABNE     ORBCD         LESNESTE
     C                   END
      *
     C                   IF        WSLC <> *BLANKS
     C     WSLC          CABNE     ORLC          LESNESTE
     C                   END
      *
     C                   IF        WSDTL8 <> 0
     C     WSDTL8        CABNE     ORDTL         LESNESTE
     C                   END
      *
     C                   MOVEL     ORAREA        XXAP             12
     C                   MOVE      ORBCD2        VADRNR
     C     VADRK         CHAIN     VADR                               34
     C                   IF        *IN34
     C     ORBCD         CHAIN     CDBOAT                             34
     C                   EVAL      VADRNA = CBNAB
     C                   EVAL      VADRN3 = ORBCD
     C                   END
     C                   MOVE      VADRN32       XXAP
     C                   IF        WSAP <> *BLANKS
     C                   IF        ((XXAP < WSAP) OR (XXAP > W2AP))
     C                   GOTO      LESNESTE
     C                   END
     C                   END
      *
     C     ORONO         SETLL     GSSPOF
     C     ORONO         READE     GSSPOF                                 34
     C                   IF        *IN34
     C     WSPRO         CABNE     *BLANKS       LESNESTE
     C     WSPO          CABNE     *BLANKS       LESNESTE
     C                   MOVE      *BLANKS       POCONO
     C                   END
     C                   IF        WSPRO <> *BLANKS
     C                   MOVEL     POCONO        XAN04             4
     C     XAN04         CABNE     WSPRO         LESNESTE
     C                   END
     C                   IF        WSPO <> *BLANKS
     C     POCONO        CABLT     WSPO          LESNESTE
     C     POCONO        CABGT     WSPOMAX       LESNESTE
     C                   END
      *
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 34
     C                   IF        *IN34
     C     WSSAL         CABNE     *BLANKS       LESNESTE
     C                   MOVE      *BLANKS       RFREF
     C                   ELSE
     C                   DOW       *IN34 = *OFF
     C                   IF        RFKD = 'SAL'
     C                   IF        WSSAL <> *BLANKS
     C                   MOVEL     RFREF         XAN10            10
     C                   IF        ((WSSAL <= XAN10) AND (WSSALMAX > XAN10))
     C                   LEAVE
     C                   END
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   END
     C     ORONO         READE     GSSRFF                                 34
     C  N34              ENDDO
     C                   IF        *IN34
     C     WSSAL         CABNE     *BLANKS       LESNESTE
     C                   END
     C                   END
     C                   MOVE      RFREF         W2SAL
      *
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 34
     C                   IF        *IN34
     C     WSACC         CABNE     *BLANKS       LESNESTE
     C                   MOVE      *BLANKS       RFREF
     C                   ELSE
     C                   DOW       *IN34 = *OFF
     C                   IF        RFKD = 'CPO'
     C                   IF        WSACC <> *BLANKS
     C                   MOVEL     RFREF         XAN20            20
     C                   IF        ((WSACC <= XAN20) AND (WSACCMAX > XAN20))
     C                   LEAVE
     C                   END
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   END
     C     ORONO         READE     GSSRFF                                 34
     C  N34              ENDDO
     C     *IN34         CABEQ     *ON           LESNESTE
     C                   END
     C                   MOVE      RFREF         W2ACC
      *
      * ORDNE NØKKEL
     C                   SELECT
     C                   WHEN      WSMS01 = 'o'
     C                   MOVEL     ORCNO         XAN08             8
     C                   MOVEA     XAN08         KEY(1)
     C                   WHEN      WSMS02 = 'o'
     C                   MOVEL     POCONO        XAN04             4
     C                   MOVEA     XAN04         KEY(1)
     C                   WHEN      WSMS03 = 'o'
     C                   MOVEL     W2SAL         XAN10            10
     C                   MOVEA     XAN10         KEY(1)
     C                   WHEN      WSMS04 = 'o'
     C                   MOVEA     ORBCD         KEY(1)
     C                   WHEN      WSMS05 = 'o'
     C                   MOVEA     ORLC          KEY(1)
     C                   WHEN      WSMS06 = 'o'
     C                   MOVEL     ORDTL         XAN08             8
     C                   MOVEA     XAN08         KEY(1)
     C                   WHEN      WSMS07 = 'o'
     C                   MOVEA     XXAP          KEY(1)
     C                   WHEN      WSMS08 = 'o'
     C                   MOVEA     POCONO        KEY(1)
     C                   WHEN      WSMS09 = 'o'
     C                   MOVEA     W2ACC         KEY(1)
     C                   WHEN      WSMS10 = 'o'
     C                   MOVEA     ORAAR         KEY(1)
     C                   MOVE      '-'           KEY(5)
     C                   MOVEA     ORMND         KEY(6)
     C                   OTHER
     C                   MOVEL     ORCNO         XAN08             8
     C                   MOVEA     XAN08         KEY(1)
     C                   MOVE      '1'           KEY(256)
     C                   ENDSL
      *
     C                   MOVEL     ORCNO         XAN08             8
     C                   MOVEA     XAN08         KEY(31)
     C                   MOVEL     POCONO        XAN04             4
     C                   MOVEA     XAN04         KEY(39)
     C                   MOVEL     W2SAL         XAN10            10
     C                   MOVEA     XAN10         KEY(43)
     C                   MOVEA     ORBCD         KEY(53)
     C                   MOVEA     ORLC          KEY(58)
     C                   MOVEL     ORDTL         XAN08             8
     C                   MOVEA     XAN08         KEY(63)
     C                   MOVEA     XXAP          KEY(71)
     C                   MOVEA     POCONO        KEY(83)
     C                   MOVEL     W2ACC         XAN20            20
     C                   MOVEA     XAN20         KEY(103)
     C                   MOVEA     ORAAR         KEY(123)
     C                   MOVE      '-'           KEY(127)
     C                   MOVEA     ORMND         KEY(128)
      *
      * BEVARE DATA
     C                   MOVEA     POCONO        DATA(1)
     C                   MOVEA     ORSNM         DATA(21)
     C                   MOVEA     W2ACC         DATA(51)
     C                   MOVE      ORCLI         XAN05             5
     C                   MOVEA     XAN05         DATA(86)
     C                   MOVE      ORWT          XAN07             7
     C                   MOVEA     XAN07         DATA(91)
     C                   MOVE      ORVOL         XAN05             5
     C                   MOVEA     XAN05         DATA(98)
     C                   MOVEA     POCONO        DATA(103)
     C*                  EVAL      W2CNA = ORBCD
     C*                  CAT       '/':0         W2CNA
     C*                  CAT       VADRN3:0      W2CNA
     C                   EVAL      W2CNA = VADRNA
     C                   CAT       ',':0         W2CNA
     C                   CAT       VADRN3:1      W2CNA
     C                   MOVEA     W2CNA         DATA(107)
     C                   EVAL      RFREF = *BLANKS
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 34
     C                   DOW       *IN34 = *OFF
     C                   IF        RFKD = 'SAL'
     C                   LEAVE
     C                   END
     C     ORONO         READE     GSSRFF                                 34
     C  N34              ENDDO
     C                   MOVEA     RFREF         DATA(168)
     C                   MOVEA     ORFT4         DATA(203)
     C                   MOVEL     ORDTWL        XAN08             8
     C                   MOVEA     XAN08         DATA(205)
     C                   MOVE      ORDTL         XAN08             8
     C                   MOVEA     XAN08         DATA(213)
      * POS. 196 - 227 ER LEDIGE
     C                   IF        ORLMA = *BLANKS
     C                   MOVE      *ZEROS        ORLMA
     C                   END
     C                   MOVEA     ORLMA         DATA(253)
      *
     C                   MOVEA     *BLANKS       DATA(257)
     C     ORONO         SETGT     GSSLGL01
     C     ORONO         READPE    GSSLGL01                               34
     C                   DOW       *IN34 = *OFF
     C                   SELECT
     C                   WHEN      LGANY01 = 'Driver:'
     C                   MOVEA     LGANY02       DATA(257)
     C                   LEAVE
     C                   WHEN      LGANY11 = 'Sign:'
     C                   MOVEA     LGANY12       DATA(284)
      * TIL OG MED POS. 278
     C                   ENDSL
     C     ORONO         READPE    GSSLGL01                               34
     C  N34              ENDDO
      *
     C                   MOVE      ORONO         XAN07             7
     C                   MOVEA     XAN07         DATA(1015)
      *
     C                   WRITE     WRKCGISR
     C                   EVAL      AntSn = AntSn + 1
     C                   MOVE      'J'           XFOUND            1
      *
     C     LESNESTE      TAG
     C                   IF        ((AntRc >= MaxRc2) OR (AntSn >= MaxSn2))
     C                   GOTO      SLGETDTA
     C                   END
      *
     C                   IF        XPOD = 'J'
     C                   SELECT
     C                   WHEN      XXKN = 0
     C     GSSOR20K      READE     GSSORL20                               33
     C                   WHEN      ((XXKN >= 99999999) AND (XXLC<>'99999'))
     C     XXLC          READE     GSSORL19                               33
     C                   WHEN      ((XXKN >= 99999999) AND (XXLC='99999'))
     C                   READ      GSSORL19                               33
     C                   WHEN      XXLC = '99999'
     C     XXKN          READE     GSSORL20                               33
     C                   OTHER
     C     GSSOR19K      READE     GSSORL19                               33
     C                   ENDSL
     C                   ELSE
     C                   SELECT
     C                   WHEN      XXKN = 0
     C     GSSOR20K      READE     GSSORL18                               33
     C                   WHEN      ((XXKN >= 99999999) AND (XXLC<>'99999'))
     C     XXLC          READE     GSSORL17                               33
     C                   WHEN      ((XXKN >= 99999999) AND (XXLC='99999'))
     C                   READ      GSSORL17                               33
     C                   WHEN      XXLC = '99999'
     C     XXKN          READE     GSSORL18                               33
     C                   OTHER
     C     GSSOR19K      READE     GSSORL17                               33
     C                   ENDSL
     C                   END
      *
     C  N33              ENDDO
      *
     C                   GOTO      STGETDTA
      *
     C     SLGETDTA      TAG
     C                   callp     updhtmlvar('MaxRc':
     C                             %trim(%editc(Maxrc2:'Z')))
     C                   callp     updhtmlvar('MaxSn':
     C                             %trim(%editc(MaxSn2:'Z')))
     C                   callp     updhtmlvar('AntRc':
     C                             %trim(%editc(Antrc:'3')))
     C                   callp     updhtmlvar('AntSn':
     C                             %trim(%editc(AntSn:'3')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section
      *  (search criteria)
      *=====================================================================******
     C     SetData       begsr
      *
     C                   callp     wrtsection('tablestr')
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   MOVE      *ZEROS        TOTCLI            9 0
     C                   MOVE      *ZEROS        TOTWT             9 1
     C                   MOVE      *ZEROS        TOTVOL            9 2
     C                   MOVE      *ZEROS        TOTLM             9 2
      *
     C     WINTEG        SETLL     WRKCGISS
     C     WINTEG        READE(N)  WRKCGISS                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVEA     DATA(1015)    XAN07
     C                   MOVE      XAN07         ORONO
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
     C                   MOVEA     DATA(1)       POCONO
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   MOVEA     DATA(21)      ORSNM
     C                   callp     updhtmlvar('ORSNM':ORSNM)
     C                   MOVEA     DATA(51)      W2ACC
     C                   callp     updhtmlvar('WSACC':W2ACC)
     C                   MOVEA     DATA(86)      XAN05
     C                   MOVE      XAN05         ORCLI
     C                   callp     updhtmlvar('ORCLI':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   ADD       ORCLI         TOTCLI
     C                   MOVEA     DATA(91)      XAN07
     C                   MOVE      XAN07         ORWT
     C                   callp     updhtmlvar('ORWT':
     C                             %trim(%editc(ORWT:'3')))
     C                   ADD       ORWT          TOTWT
     C                   MOVEA     DATA(98)      XAN07
     C                   MOVE      XAN07         ORVOL
     C                   callp     updhtmlvar('ORVOL':
     C                             %trim(%editc(ORVOL:'3')))
     C                   ADD       ORVOL         TOTVOL
     C                   MOVEA     DATA(103)     XAN04             4
     C                   callp     updhtmlvar('WSPRO':XAN04)
     C                   MOVEA     DATA(107)     W2CNA
     C                   callp     updhtmlvar('WSCNA':W2CNA)
     C                   MOVEA     DATA(168)     RFREF
     C                   callp     updhtmlvar('WSSAL':RFREF)
     C                   MOVEA     DATA(203)     XAN02             2
     C                   callp     updhtmlvar('WSAREA':XAN02)
     C                   MOVEA     DATA(205)     XAN08
     C                   MOVE      XAN08         ORDTWL
     C                   callp     updhtmlvar('ORDTWL':
     C                             %trim(%editw(ORDTWL:'    .  .  ')))
     C                   MOVEA     DATA(213)     XAN08
     C                   MOVE      XAN08         ORDTL
     C                   callp     updhtmlvar('ORDTL':
     C                             %trim(%editw(ORDTL:'    .  .  ')))
      * POS. 196 - 227 ER LEDIGE
     C                   MOVEA     DATA(253)     ORLMA
     C                   MOVE      ORLMA         ORLM              4 2
     C                   callp     updhtmlvar('ORLM':
     C                             %trim(%editc(ORLM:'3')))
     C                   ADD       ORLM          TOTLM
     C                   MOVEA     DATA(257)     LGANY02
     C                   callp     updhtmlvar('WSDRIVER':LGANY02)
     C                   MOVEA     DATA(284)     LGANY12
     C                   callp     updhtmlvar('WSRECEIVER':LGANY12)
     C                   callp     updhtmlvar('WSTT':'T&T')
     C                   callp     updhtmlvar('WKEY':WKEY1)
      *
     C                   callp     wrtsection('tablerow')
      *
     C*                  DELETE    WRKCGISR
     C     WINTEG        READE(N)  WRKCGISS                               33
     C  N33              ENDDO
      *
      * SKRIV TOTAL
     C                   IF        TOTCLI > 0
     C                   MOVE      *BLANKS       XAN01             1
     C                   callp     updhtmlvar('ORONO':XAN01)
     C                   callp     updhtmlvar('POCONO':XAN01)
     C                   EVAL      ORSNM = 'TOTAL'
     C                   callp     updhtmlvar('ORSNM':ORSNM)
     C                   MOVE      *BLANKS       W2ACC
     C                   callp     updhtmlvar('WSACC':XAN01)
     C                   callp     updhtmlvar('ORCLI':
     C                             %trim(%editc(TOTCLI:'Z')))
     C                   callp     updhtmlvar('ORWT':
     C                             %trim(%editc(TOTWT:'3')))
     C                   callp     updhtmlvar('ORVOL':
     C                             %trim(%editc(TOTVOL:'3')))
     C                   callp     updhtmlvar('WSPRO':XAN01)
     C                   callp     updhtmlvar('WSCNA':XAN01)
     C                   callp     updhtmlvar('WSSAL':XAN01)
     C                   callp     updhtmlvar('WSAREA':XAN01)
     C                   callp     updhtmlvar('ORDTWL':XAN01)
     C                   callp     updhtmlvar('ORDTL':XAN01)
     C                   callp     updhtmlvar('ORLM':
     C                             %trim(%editc(TOTLM:'3')))
     C                   callp     updhtmlvar('WSDRIVER':XAN01)
     C                   callp     updhtmlvar('WSRECEIVER':XAN01)
     C                   callp     updhtmlvar('WSTT':XAN01)
     C                   callp     updhtmlvar('WKEY':XAN01)
      *
     C                   callp     wrtsection('tablerow')
     C                   END
      *
     C                   callp     wrtsection('tableend')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      BRPARF
     C                   OPEN      WRKCGISS
     C                   OPEN      CDBOAT
     C                   OPEN      GSSORL17
     C                   OPEN      GSSORL18
     C                   OPEN      GSSORL19
     C                   OPEN      GSSORL20
     C                   OPEN      GSSPOF
     C                   OPEN      GSSRFF
     C                   OPEN      GSSLGL01
     C                   OPEN      VADR
      *
     C     VADRK         KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    ORCNO
     C                   KFLD                    VADRNR
     C     GSSOR19K      KLIST
     C                   KFLD                    XXLC
     C                   KFLD                    XXKN
     C     GSSOR20K      KLIST
     C                   KFLD                    CBKN
     C                   KFLD                    XXLC
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     WRKCGISS
     C                   close     CDBOAT
     C                   close     GSSORL17
     C                   close     GSSORL18
     C                   close     GSSORL19
     C                   close     GSSORL20
     C                   close     GSSPOF
     C                   close     GSSRFF
     C                   close     GSSLGL01
     C                   close     VADR
     C                   eval      *inlr = *on
      *
     C                   endsr
