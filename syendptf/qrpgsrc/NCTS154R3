      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE57                    ***
      *** Rejection for office of departure                         ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSIH05  UF   E           K DISK
     FTVINF     O  A E             DISK
     FTRKODL01  IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM2     IF   E           K DISK    RENAME(EDIMR:EDIMR2)
     FEDIM5     IF   E           K DISK    RENAME(EDIMR:EDIMR5)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts154r3
     d ncts154r3       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts154r3       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d x_titrnr        s                   like(titrnr)
     d x_m1225         s                   like(m1225)
     d x_IE57          s              1a
     d x_ref           s              8a
     d x_TIR           s             17a
     d x_rtyp          s              3a
     d x_rcod          s              2a
     d x_str           s              1a
     d x_ecod          s              2a
     d x_erea          s              7a
     d x_opdft         s              1a
     d xn              s              3s 0
     d wdt             s              8
     d wtm             s              6
     d                 ds
     d x_rdt                   1      8  0
     d x_rdta                  1      8
     d                 ds
     d x_rtm                   1      6  0
     d x_rtma                  1      6
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
      *
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR2
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
     IEDIMR5
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC057C';
             x_ie57 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'CustomsOfficeOfDestinationActual';
           //exsr srxml2;
           when xrnv = 'TraderAtDestination';
           //exsr srxml3;
           when xrnv = 'FunctionalError';
             exsr srxml4;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie57 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'MRN';
             x_titrnr = xrverd;
           when xrnv = 'businessRejectionType';
             x_rtyp = xrverd;
           when xrnv = 'rejectionDateAndTime';
             x_rdta = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
             x_rtma = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                   + %subst(xrverd:18:2);
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = '570';
             f4815  = 'CL';
             if x_titrnr <> *blanks;
               xn     = xn + 1;
               ft(xn) = 'MRN: ' + %trim(x_titrnr);
             endif;
             xn     = xn + 1;
             tkunik = '570';
             tkkode = x_rtyp;
             chain (tkunik: tkkode) trkodl01;
             if not %found;
               tktxtn = *blanks;
             endif;
             ft(xn) = 'Rejection: ' + %trim(xrverd) + ' ' + %trim(x_rtyp)
                    + ' ' + %trim(tktxtn);
             write tvinff;
           when xrnv = 'rejectionCode';
             x_rcod = xrverd;
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = '227';
             f4815  = 'CL';
             tkunik = '227';
             tkkode = x_rcod;
             chain (tkunik: tkkode) trkodl01;
             if not %found;
               tktxtn = *blanks;
             endif;
             ft(1) = 'Code: ' + %trim(x_rcod) + ' ' + %trim(tktxtn);
             write tvinff;
           when xrnv = 'rejectionReason';
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = 'ERR';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             ft(1)  = %subst(xrvrd:1:70);
             ft(2)  = %subst(xrvrd:71:70);
             ft(3)  = %subst(xrvrd:141:70);
             ft(4)  = %subst(xrvrd:211:70);
             ft(5)  = %subst(xrvrd:281:70);
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_ref = xrverd;
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = *blanks;
             f4815  = *blanks;
             ft(1)  = 'Customs office of destination ' + xrverd;
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'identificationNumber';
             x_ref = xrverd;
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = *blanks;
             f4815  = *blanks;
             ft(1)  = 'Trader at destination ' + xrverd;
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4;
       //**********************************************

         x_opdft = 'N';

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'errorPointer';
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = 'EP';
             f4815  = *blanks;
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             ft(1)  = %subst(xrvrd:1:70);
             ft(2)  = %subst(xrvrd:71:70);
             ft(3)  = %subst(xrvrd:141:70);
             ft(4)  = %subst(xrvrd:211:70);
             ft(5)  = %subst(xrvrd:281:70);
             write tvinff;
           when xrnv = 'errorCode';
             x_ecod = xrverd;
             tkunik = '180';
             tkkode = xrverd;
             chain (tkunik: tkkode) trkodl01;
             if not %found;
               tktxtn = xrverd;
             endif;
             x_opdft = 'J';
           when xrnv = 'errorReason';
             x_erea = xrverd;
             x_opdft = 'J';
           when xrnv = 'originalAttributeValue';
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             ft(1)  = 'Kode: ' + %trim(x_ecod) + ' ' + %trim(tktxtn);
             if %subst(x_erea:1:2) = 'CL';
             // kodetabell hentes til f0077
               f0077  = %subst(x_erea:3:3);
               f4815  = *blanks;
             // Hent feilmelding fra kodefil
               tkunik = %subst(x_erea:3:3);
               tkkode = xrvrd;
               chain (tkunik: tkkode) trkodl01;
               if not %found;
                 tktxtn = xrvrd;
               endif;
               ft(2)  = 'Årsak: ' + %trim(xrvrd) + ' ' + %trim(tktxtn);
             else;
             // F0077 og F4815 kobles til fil TRREGELF. Nøkkel ligger i FT(2)/F0078B.
             // Eks. R0020 -> Bruk R og 0020 til å hente tekst i SAD003
               f0077  = 'TET';
               f4815  = 'F5';
               ft(2)  = x_erea;
               ft(3)  = %subst(xrvrd:1:70);
               ft(4)  = %subst(xrvrd:71:70);
               ft(5)  = %subst(xrvrd:141:70);
               if %subst(xrvrd:211:70) <> *blanks;
                 write tvinff;
                 f0077  = *blanks;
                 f4815  = *blanks;
                 fln    = fln + 1;
                 ft(1)  = %subst(xrvrd:211:70);
                 ft(2)  = %subst(xrvrd:281:70);
                 ft(3)  = %subst(xrvrd:351:70);
                 ft(4)  = %subst(xrvrd:421:70);
                 ft(5)  = %subst(xrvrd:491:22);
               endif;
             endif;
             write tvinff;
             x_opdft = 'N';
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         if x_opdft = 'J';
           ft     = *blanks;
           fln    = fln + 1;
           ft(1)  = 'Kode: ' + %trim(x_ecod) + ' ' + %trim(tktxtn);
           if %subst(x_erea:1:2) = 'CL';
           // kodetabell hentes til f0077
             f0077  = %subst(x_erea:3:3);
             f4815  = 'CL';
           // Hent feilmelding fra kodefil
             tkunik = %subst(x_erea:3:3);
             tkkode = xrvrd;
             chain (tkunik: tkkode) trkodl01;
             if not %found;
               tktxtn = xrvrd;
             endif;
             ft(2)  = 'Årsak: ' + %trim(x_erea) + ' ' + %trim(tktxtn);
           else;
           // F0077 og F4815 kobles til fil TRREGELF. Nøkkel ligger i FT(2)/F0078B.
           // Eks. R0020 -> Bruk R og 0020 til å hente tekst i SAD003
             f0077  = 'TET';
             f4815  = 'F5';
             ft(2)  = x_erea;
           endif;
           write tvinff;
         endif;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         x_m1225 = *blanks;
         chain x_titrnr nctsih05;
         if %found;
           tist = 'M';
           update nctsihr;
           setll (tiavd: titdn: 99999999) edim2;
           readpe (tiavd: titdn) edim2;
           dow not %eof;
             if xsr = 'S';
               x_m1225 = x1225;
               leave;
             endif;
             readpe (tiavd: titdn) edim2;
           enddo;
         endif;

         m0065  = 'XMLRES';
         m0068  = x_titrnr;
         m1001  = 'TEI';
         if x_m1225 = *blanks;
           m1225  = '057';
         else;
           m1225  = x_m1225;
         endif;
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '057';
         mavd   = tiavd;
         mtdn   = titdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = tiavd;
         ttopd  = titdn;
         ttacti = 'T57';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'Reject by customs NCTS: ' + %trim(%editw(x_rdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_rtm:'  :  :  '));
         tttexl = 'Avist av tollvesenet: ' + %trim(%editw(x_rdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_rtm:'  :  :  '));
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
