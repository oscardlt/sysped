********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 test posisjon ved reeksport
10XXX * 02 SH  PTF10 signatur
10XXX * 03 SH  PTF10 hent siste meldingskode
10XXX * 04 SH  PTF10 blanke fritekster
10XXX * 05 SH  PTF10 ta med eksport J/N
********************************************************************
      **************************************************************
      *** PROGRAM for utskrift av tollagerregnskap               ***
      **************************************************************
     FFREETXT2  IF   E           K DISK
     FEDIM2     IF   E           K DISK
N03  FTVINF     IF   E           K DISK
     FSAEH6B    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FHEAD18    IF   E           K DISK
     FHEADF     IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD2R)
     FHEADDT    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADDTR)
     FSADH      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYIM50F   O  a E             DISK
     FSYIM50FH  O  a E             DISK
     FSYIM50T   UF a E           K DISK
     FSYIM50D   CF   E             WORKSTN
     D                 DS
     D  XULTID                 1     14  0
     D  tltime                 1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D  XULDDMM                7     10  0
     D  XULÅÅ2                13     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  tldate                 1      8  0
     D                 DS
     D  DFDAG                  1      2  0
     D  DFMND                  3      4  0
     D  DFAAR                  5      6  0
     D  DATFRA                 1      6  0
     D                 DS
     D  DFAAR1                 1      4  0
     D  DFMND1                 5      6  0
     D  DFDAG1                 7      8  0
     D  DATFR1                 1      8  0
     D                 DS
     D  DTDAG                  1      2  0
     D  DTMND                  3      4  0
     D  DTAAR                  5      6  0
     D  DATTIL                 1      6  0
     D                 DS
     D  DTAAR1                 1      4  0
     D  DTMND1                 5      6  0
     D  DTDAG1                 7      8  0
     D  DATTI1                 1      8  0
     D                 DS
     D  DLDAG                  1      2  0
     D  DLMND                  3      4  0
     D  DLAAR                  5      6  0
     D  LOGDAT                 1      6  0
     D                 DS
     D  DLAAR1                 1      4  0
     D  DLMND1                 5      6  0
     D  DLDAG1                 7      8  0
     D  LOGDA1                 1      8  0
     D                 DS
     D  WSGNFR                 1     15
     D  WSGNFA                 1      4
     D  WSGNFB                 5      9
     D  WSGNFC                10     12
     D  WSGNFD                13     15
     D                 DS
     D  WSGNTI                 1     15
     D  WSGNTA                 1      4
     D  WSGNTB                 5      9
     D  WSGNTC                10     12
     D  WSGNTD                13     15
     D                 DS
     D  DSTID                  1     14  0
     D  DSCENT                11     14  0
     D                 DS
     D  Z                      1     20
     D                                     DIM(10)
     D  ZOT01                  1      2
     D  ZOT02                  3      4
     D  ZOT03                  5      6
     D  ZOT04                  7      8
     D  ZOT05                  9     10
     D  ZOT06                 11     12
     D  ZOT07                 13     14
     D  ZOT08                 15     16
     D  ZOT09                 17     18
     D  ZOT10                 19     20
     D                 DS
     D  S                      1     30
     D                                     DIM(10)
     D  S01                    1      3
     D  S02                    4      6
     D  S03                    7      9
     D  S04                   10     12
     D  S05                   13     15
     D  S06                   16     18
     D  S07                   19     21
     D  S08                   22     24
     D  S09                   25     27
     D  S10                   28     30
     D                SDS
     D  UUSER                254    263
      *_____
      * Init
      *"""""
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UIN01             1
     C*
     C                   TIME                    DSTID            14 0
     C                   MOVE      *ZEROS        DATTIL
     C                   MOVE      *ZEROS        DATTI1
     C                   MOVE      *ZEROS        DATFRA
     C                   MOVE      *ZEROS        DATFR1
     C                   MOVE      *ZEROS        LOGDAT
     C                   MOVE      *ZEROS        LOGDA1
N05  C                   MOVE      'N'           WSEJN
     C*
     C                   READ      FIRM                                   34
     C     START         TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       303536
     C                   SETOFF                                       37
     C   04              DO
     C                   EXSR      SØKUNR
     C                   GOTO      START
     C                   END
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C     WSKUND        IFNE      *ZEROS
     C                   MOVE      WSKUND        KUNDNR
     C     KUNKEY        CHAIN     CUNDL01                            35
     C                   MOVE      KNAVN         KNAVNS
     C   35              MOVE      'SPK0001'     MSGNR
     C   35              SETON                                        30
     C   35              GOTO      START
     C                   END
      *
      * TESTER GODSNUMMER
      *
     C     WSGNFR        IFGT      WSGNTI
     C                   SETON                                        37
     C                   MOVE      'SHU0724'     MSGNR
     C                   SETON                                        30
     C                   GOTO      START
     C                   END
      * en verdi må utfylles
     c     datti1        ifeq      *zeros
     C     wsgnti        andeq     *blanks
     C     avdtil        andeq     *zeros
     C                   MOVE      'SHU1177'     MSGNR
     C                   seton                                        30
     C                   GOTO      START
     c                   end
     C  N10              GOTO      START
     C*
     C*
     C                   EXSR      TELLSR
     C                   EXSR      PRINT
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *********************************************************
     C     PRINT         BEGSR
      *********************************************************
      *
      * skjekk beste fil
      *
     C                   setoff                                       505152
      * fra til dato
     C     datfr1        ifne      *zeros
     c     datti1        andne     *zeros
     C                   seton                                        50
     C                   end
     C
     C  n50wsgnfr        ifne      *blanks
     C     wsgnti        andne     *blanks
     C                   seton                                        51
     c                   end
      *
     c  n50
     Cann51avdfra        ifne      *zeros
     C     avdtil        andne     *zeros
     C                   seton                                        52
     c                   end
      *
      * ved ingen valg velg datofil
      *
     c     *in50         ifeq      '0'
     c     *in51         andeq     '0'
     c     *in52         andeq     '0'
     c                   seton                                        50
     c                   move      99999999      datti1
     c                   end
      *
      *
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     FRAKEY        KLIST
     C                   KFLD                    WSGNFR
     C     FRAKEYA       KLIST
     C                   KFLD                    AVDFRA
     C     FRAKEYD       KLIST
     C                   KFLD                    DATFR1
     C     SADKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FRIKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    UFOR              1
     C                   MOVE      'U'           UFOR
      *
     C     tkeymax       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    maxdat            8 0
     C                   z-add     99999999      maxdat
N03  C     TVIKMAX       KLIST
N03  C                   KFLD                    MMNSI
N03  C                   KFLD                    FLNmax            5 0
N03  C                   z-add     99999         FLNmax
N03  C     TVIK          KLIST
N03  C                   KFLD                    MMNSI             9 0
      *
     C                   EXSR      FIXDAT
      *
     C   50FRAKEYD       SETLL     HEADDT
     C   51FRAKEY        SETLL     HEAD18
     C   52FRAKEYA       SETLL     HEADf
     C     *IN33         DOWEQ     '0'
     C     NESTE         TAG
     C   50              READ      HEADDT                                 33
     C   51              READ      HEAD18                                 33
     C   52              READ      HEADf                                  33
     C   50HEDTR         comp      DATTI1                             33
     C   51HEGN          COMP      WSGNTI                             33
     C   52HEAVD         COMP      AVDTIL                             33
      *
      *
     C     *IN33         IFEQ      '0'
      *
      * TESTER slettet
      *
     C     HEST          IFEQ      'S'
     C                   GOTO      NESTE
     C                   END
      *
      * TESTER AVDELING FRA
      *
     C     HEAVD         IFLT      AVDFRA
     C                   GOTO      NESTE
     C                   END
      *
      * TESTER AVDELING TIL
      *
     C     AVDTIL        IFNE      *ZEROS
     C     HEAVD         ANDGT     AVDTIL
     C                   GOTO      NESTE
     C                   END
      *
      * TESTER AGENTNUMMER
      *
     C     WSKUND        IFNE      *ZEROS
     C     HEKNA         ANDNE     WSKUND
     C                   GOTO      NESTE
     C                   END
      *
      * TESTER DATO FRA
      *
     C     HEDTR         IFLT      DATFR1
     C                   GOTO      NESTE
     C                   END
      *
      * TESTER DATO TIL
      *
     C     DATTI1        IFNE      *ZEROS
     C     HEDTR         ANDGT     DATTI1
     C                   GOTO      NESTE
     C                   END
      *
      * TESTER OPPDRAGSTYPER
      *
     C     ZOT01         IFNE      '  '
     C     HEOT          LOOKUP    Z                                      44
     C   44              GOTO      NESTE
     C                   END
      *
      * TESTER om fritekst
      *
     C                   MOVE      ' '           FRITXT
N04  C                   MOVE      *blanks       FRTTXT
     C     FRIKEY        chain     freetxt2                           44
     C  N44              MOVE      'U'           FRITXT
     C  N44NOTAT         IFEQ      'J'
     C                   GOTO      NESTE
     C                   END
      *
      *  henter data fra sad
      *
     c                   move      *zeros        tvidaf
     c                   move      *zeros        tvidas
      *
     C     SADKEY        CHAIN     SADH                               34
      *
     C     *IN34         IFEQ      '1'
     C                   MOVE      HEKNK         KUNDNR
     C                   MOVE      HETLL         SITLL
     C                   MOVE      HETLE         SITLE
     C                   MOVE      *BLANKS       SIDTG
     C                   MOVEL     'IKKE SAD'    SIDTG
     C                   Z-ADD     HENT          ANT               5 0
     C                   Z-ADD     HEVKT         VEKT              7 0
     C                   move      HESG          SISG
     C                   ELSE
n02  c     sisg          ifeq      *blanks
n02  C                   move      HESG          SISG
n02  c                   end
     C                   Z-ADD     SINTK         ANT
     C                   Z-ADD     SIVKB         VEKT
     C                   MOVE      SIKNK         KUNDNR
     C                   END
      *
      * TESTER SIGNATURER
      *
     C     S01           IFNE      '  '
     C     SISG          LOOKUP    S                                      44
     C  N44              GOTO      NESTE
     C                   END
      *
      * TESTER VED KUN LØPENUMMER
      *
     C     LOPENR        IFEQ      'J'
     C     SITLL         IFNE      *blanks
     C     HEGNN         ORNE      *BLANKS
     C                   GOTO      NESTE
     C                   END
     C                   END
      *
     C     KUNKEY        CHAIN     CUNDL01                            35
     C  N35              MOVE      KNAVN         SINAK
     C   35              MOVE      HENAK         SINAK
      *
     c     tkeymax       setgt     edim2
     c     sadkey        readpe    edim2                                  36
N03  c                   move      *zeros        MMNSI
N03  c  N36              move      MMN           MMNSI
     c  N36              move      MDT           tvidas
     c     sadkey        setll     edim2
     c     sadkey        reade     edim2                                  36
     c  N36              move      MDT           tvidaf
N03   *
N03   * hent feilmeldingskode
N03   *
N03  c                   move      *blanks       tvi077
N03  c     *IN36         ifeq      '0'
N03  c     tvikmax       setgt     tvinf
N03  c     tvik          readpe    tvinf                                  36
N03  c  N36              movel     f0078A        tvi077
N03  c                   end
      *
      * tester hvis selektert på max siste tvinndato
      *
     c     logda1        ifne      *zeros
     c     tvidas        andge     logda1
     c                   goto      neste
     c                   end
      *
     c                   write     syim50fr
      *
     C     SITLL         IFEQ      *ZEROS
     C     SITLL         OREQ      *BLANKS
N05  C     WSEJN         IFEQ      'J'
     C                   EXSR      EXPO
N05  C                   END
     C                   END
     C                   END
     C                   END
     C                   ENDSR
     C*******************************************************
     C     EXPO          BEGSR
     C*******************************************************
     C                   SETOFF                                       34
     C                   MOVEL     '*REKSPOR'    HEGNN
     C                   MOVE      'TERT*  '     HEGNN
     C     HEGN          SETLL     SAEH6B
     C     *IN34         DOWEQ     '0'
      *
      *
     C     HEGN          READE     SAEH6B                                 34
     C     *IN34         IFEQ      '0'
N01  C     sipos         andeq     sepos
     C                   MOVE      SEKNS         KUNDNR
     C                   MOVE      SENAS         SINAK
     C                   MOVE      SETLE         SITLE
     C                   MOVE      SETLL         SITLL
     C                   MOVE      SEDTG         SIDTG
     C                   Z-ADD     SENTK         ANT
     C                   Z-ADD     SEVKB         VEKT
     C                   Z-ADD     SEAVD         HEAVD
     C                   Z-ADD     SETDN         HEOPD
     C                   MOVE      SESG          HESG
     c                   write     syim50fr
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUND
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TELLSR        BEGSR
     C***********************************************
      *
     c                   move      wstext        tltext
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     c                   move      uuser         tluser
      *
      * henter neste teller
      *
     c                   move      'A'           tltelk
     c     keytel        klist
     c                   kfld                    tltelk
     c     keytel        chain     syim50t                            33
     c                   add       1             tlunik
     c  N33              update    syim50tr
     c   33              write     syim50tr
     c                   write     syim50fhr
     c                   endsr
      *
     C***********************************************
     C     FIXDAT        BEGSR
     C***********************************************
      *
      * fra dato
      *
     C     DFAAR         IFNE      *ZEROS
     C     2000          ADD       DFAAR         DFAAR1
     C                   Z-ADD     DFMND         DFMND1
     C                   Z-ADD     DFDAG         DFDAG1
     C                   ELSE
     C                   MOVE      *ZEROS        DATFR1
     C                   END
      *
      *
      * til dato
      *
     C     DTAAR         IFNE      *ZEROS
     C     2000          ADD       DTAAR         DTAAR1
     C                   Z-ADD     DTMND         DTMND1
     C                   Z-ADD     DTDAG         DTDAG1
     C                   ELSE
     C                   MOVE      *ZEROS        DATTI1
     C                   END
      *
      *
      * høyeste tvinnloggdato
      *
     C     DLAAR         IFNE      *ZEROS
     C     2000          ADD       DLAAR         DLAAR1
     C                   Z-ADD     DLMND         DLMND1
     C                   Z-ADD     DLDAG         DLDAG1
     C                   ELSE
     C                   MOVE      *ZEROS        LOGDA1
     C                   END
     C                   ENDSR
