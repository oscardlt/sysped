      *
      ***********************************************************
      *** PROGRAM FOR INTERVALLSTYRING AV POSTNUMMER          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 42-88 95-97    ***
      ***********************************************************
      *  41    =     VEDLIKEHOLD AV KUNDERS POSTNUMMERSTYRING
     FKODTA     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSTED2D    IF   E           K DISK
     FBASEK     UF A E           K DISK
     FSYPO03FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     START0        TAG
     C                   EXFMT     BILDE01
     C*
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C* TEST DEPARTMENT
     C     KODAVD        CHAIN     KODTA                              30
     C   30              MOVE      'SPA0001'     MSGNR
     C   30              GOTO      START0
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2KLKF        YAKLKF            2
     C                   MOVE      W2KPNF        YAKPNF            5
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     PRIKU         SETLL     BASEK
     C     START1        TAG
     C     PRIKU         READE(N)  BASEK                                  94
     C   94              MOVE      *BLANKS       BAKLKF
     C   94              MOVE      *BLANKS       BAKPNF
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     PRIKU         READE(N)  BASEK                                  94
     C  N94              MOVE      BAKLKF        YAKLKF
     C  N94              MOVE      BAKPNF        YAKPNF
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              GOTO      START5
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C   12              GOTO      START0
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94PRISK3        SETLL     BASEK
     C  N94PRIKU         READE(N)  BASEK                                  94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     PRISK2        CHAIN     BASEK                              33
     C  N33              DELETE    BASEKR
     C                   EXSR      MOVB1
     C                   MOVE      '*DEL*'       BAKPNF
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ENDRE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       9089
     C                   SETOFF                                       30
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C     NY            IFEQ      'J'
     C                   GOTO      START5
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C*
     C     BASEKE        CHAIN     STED2D                             89
     C   89              MOVE      'SHU0042'     MSGNR
     C   89              SETON                                        30
     C   30              GOTO      SLTB1
     C                   END
     C*
     C*
     C* TEST CUSTOMER
     C*
     C     KUNKEY        CHAIN     CUNDL01                            88
     C   88              MOVE      'SPK0001'     MSGNR
     C   88              SETON                                        30
     C   88              GOTO      SLTB1
     C*
     C     PRISK         CHAIN     BASEK                              33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWPR
     C  N33              UPDATE    BASEKR
     C   33              WRITE     BASEKR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C   40              MOVE      'N'           NY                1
     C  N40              MOVE      'J'           NY
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     BAKPNF        COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVPRW
     C                   MOVE      ' '           WSNR
     C                   SETOFF                                       9089
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     BAKPNF        COMP      '*DEL*'                                98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWPR        BEGSR
     C***********************************************
     C                   MOVE      WSKLKF        BAKLKF
     C                   MOVE      WSKPNF        BAKPNF
     C                   MOVE      WSKKNR        BAKKNR
     C                   MOVE      WSKALT        BAKALT
     C                   MOVE      WSKALA        BAKALA
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVPRW        BEGSR
     C***********************************************
     C*
     C                   MOVE      BAKLKF        WSKLKF
     C                   MOVE      BAKPNF        WSKPNF
     C                   MOVE      BAKKNR        WSKKNR
     C                   MOVE      BAKALT        WSKALT
     C                   MOVE      BAKALA        WSKALA
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       BAKLKF
     C                   MOVE      *BLANKS       BAKPNF
     C                   MOVE      *BLANKS       BAKKNR
     C                   MOVE      *BLANKS       BAKALT
     C                   MOVE      *BLANKS       BAKALA
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSKLKF
     C                   MOVE      *BLANKS       WSKPNF
     C                   MOVE      *BLANKS       WSKKNR
     C                   MOVE      *BLANKS       WSKALT
     C                   MOVE      *BLANKS       WSKALA
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C* B = BRUTTO
     C* C = CUSTOMER-RELATED
     C*
     C*
     C*
     C     KODLKK        KLIST
     C                   KFLD                    LKUNIK
     C                   KFLD                    WSKLKF
     C     BASEKE        KLIST
     C                   KFLD                    WSKLKF
     C                   KFLD                    WSKPNF
     C     KODAVD        KLIST
     C                   KFLD                    AVUNIK
     C                   KFLD                    BAKDEP
     C     PRIKU         KLIST
     C                   KFLD                    BAKDEP
     C     PRISK         KLIST
     C                   KFLD                    BAKDEP
     C                   KFLD                    WSKLKF
     C                   KFLD                    WSKPNF
     C     PRISK1        KLIST
     C                   KFLD                    BAKDEP
     C                   KFLD                    W2KLKF
     C                   KFLD                    W2KPNF
     C     PRISK2        KLIST
     C                   KFLD                    BAKDEP
     C                   KFLD                    BAKLKF
     C                   KFLD                    BAKPNF
     C     PRISK3        KLIST
     C                   KFLD                    BAKDEP
     C                   KFLD                    YAKLKF
     C                   KFLD                    YAKPNF
     C*
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKKNR
     C*
     C                   MOVE      'LK '         LKUNIK            3
     C                   MOVE      'A'           AVUNIK            1
     C*
     C*
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
     C*
