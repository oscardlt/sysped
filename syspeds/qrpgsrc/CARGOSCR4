********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10153 * 01 JOV PTF10 Total omskr - Nytt prog miks av gml CARGOSUM /CARGOSCR3
10172 * 02 SH  PTF10 norsk text til Track and trace
10    * 03 JOV PTF10 flytt kolli-IDs til Viderefraktsoppdrag om det fins..
      * 04 jov PTF10 Vekt settes til 1 kg pr kll dersom den mangler (ønske 10 men...)
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 05 JOV PTF11 Hopp ut om oppdrag ikke eksisterer
********************************************************************
s03  F*OKUFM    UF   E           K DISK
N03  FDOKUFM    UF A E           K DISK
     FCARGOF    UF   E           K DISK
     FCARGOO    UF A E           K DISK
     FHEADF     UF   E           K DISK
     Fcum3lm    IF   E           K DISK
     FDOKUF7    IF   E           K DISK
     FDISP      UF   E           K DISK
     FTRACKL02  IF A E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  HXSNDN                 1     20
     D  HXSNDN1_10             1     10
     D  HXSNDN11_20           11     20
     D  HXSNDN4_20             4     20
     D                 DS
     D  FMMRK1                 1     35
     D  FMCLID                 1     18
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
      *
     c                   EXSR      INIT
      *
     c                   EXSR      MAIN
      *
     c                   if        HEVKT<>GMVKT or HEM3<>GMM3 or HELM<>GMLM
     c                   EXSR      TTZVK
     c                   endif
      *
     c                   move      *on           *inlr
     c                   return
      *
     C***********************************************
     C     MAIN          BEGSR
     C***********************************************
      * Akkumuler NÅ-verdier til sum...
     C     DokKey        setll     DOKUFM
     C     DokKey        Reade     DOKUFM                                 33
     C     *IN33         Doweq     '0'
     C     FMST          ifne      'I'
     C     FMST          andne     'E'
     c                   goto      NxtClID
     c                   endif
     C                   add       1             sumNTA                         Ant kolliIDs ALLE
     c     FMITER        ifne      ' '
     C                   add       1             sumNTX                         Ant sjekke INN TERM
     c                   endif
     c                   eval      KOID = '00' + FMCLID
     C     CARGOFKey     Chain     CARGOF                             34
     C     *IN34         ifeq      '1'
     C                   MOVE      *blanks       KOSTNR
     C                   MOVE      *zeros        KOMVKT
     C                   MOVE      *zeros        KOMM3
     C                   MOVE      *zeros        KOMLM
     c                   else
     C                   add       1             sumNTCS                        Ant kolli CS/CARGOF
      * oppdater at kolliet er matchet til oppdraget (gjøre også i ..CSR3 - overgang )
     C                   move      UUAVD         KOAVD
     C                   move      UUOPD         KOOPD
     C                   update    CARGOFR
     c     FMITER        ifne      ' '
     C                   move      KOINDT        TEINDT                         tid TEI
     C                   move      KOINTI        TEINTI
     C                   move      KOINDT        FMITDT                         tid skutt
     c                   move      KOINTI        FMITTM
     C                   move      KOUSER        FMITUS
     c                   endif
     c                   endif
     C     KOSTNR        ifeq      '02'                                         OSL pall
     C     KOSTNR        oreq      '52'                                         DRA pall
     C     KOSTNR        oreq      '98'                                         PDA- på pall
     C                   add       1             sumNTP                         Ant kolli på PALL
     C                   end
     C     KOMVKT        IFEQ      *ZEROS
S04  C*                  z-add     10            KOMVKT
N04  C                   z-add     1             KOMVKT
     C                   end
     C                   add       KOMVKT        sumVKT
     C                   add       KOMM3         sumM3
     C                   add       KOMLM         sumLM
     C     NxtClID       tag
     c                   update    dokufmR
     C     DokKey        Reade     DOKUFM                                 33
     C                   enddo
      *
      * Dersom NÅ-verdier er større enn Opprinnelig - oppdater
     C     SndKey        Chain     HEADF                              34
N05  c   34              move      *on           *inlr
N05  c   34              return
     c                   z-add     HEVKT         GMVKT
     c                   z-add     HEM3          GMM3
     c                   z-add     HELM          GMLM
      *
     C     SndKey        Chain     Cargoo                             35
      * Dersom første gang - benytt verdier fra oppdraget som GM /Original (COG...-felt)
     c     *in35         ifeq      '1'
     C                   move      UUAVD         COAVD
     C                   move      UUOPD         COOPD
     C                   move      *zeros        COLNR
     C                   move      *zeros        COLNR2
     C                   z-add     HENT          COGNTB
     C                   z-add     HXPALL        COGNTP
     C                   z-add     HEVKT         COGVKT
     C                   z-add     HEM3          COGM3
     C                   z-add     HELM          COGLM
     C                   z-add     HEFBV         COGFBV
     C                   z-add     TRFRAB        COFRAB
      * Oppdater TEI/evt frigi E-sending mm kun første gang (= normalt eneste gang..)
     C                   exsr      FixDisp
     C                   END
      *
     c                   z-add     sumNTCS       COMNT                          Kolli I CS/CARGOF
     c                   z-add     sumNTP        COMNTP                         På pall
     c                   z-add     sumNTP        HXPALL
      * IKKE lavere enn COG-verdier (G=GM/Original(EDI?) - lagres i CARGOO ved første WRITE.)
     c                   z-add     sumVKT        COMVKT
     C     sumVKT        iflt      COGVKT
     c                   z-add     COGVKT        sumVKT
     C                   endif
     C                   ADD       0.99          sumVKT                         (sumvkt=7.2 )
     C                   MOVE      00            sumVKT
     c                   z-add     sumVKT        HEVKT                          (hevkt = 9.0)
      *
     c                   z-add     sumM3         COMM3
     C     sumM3         iflt      COGM3
     c                   z-add     COGM3         sumM3
     C                   endif
     c                   z-add(H)  sumM3         HEM3
      *
     c                   z-add     sumLM         COMLM
     C     sumLM         iflt      COGLM
     c                   z-add     COGLM         sumLM
     C                   endif
     c                   z-add     sumLM         HELM
      *
     C                   exsr      FinnFvkt
     c                   z-add     COMFBV        HEFBV
      *
     c                   if        HEVKT<>GMVKT or HEM3<>GMM3 or HELM<>GMLM
     c                   move      *blanks       HXBLGK
     c                   if        HEKNA<>*zeros                                 Agent
     c                   Move      'R'           TRTSTA                          Reklakuler
     c                   endif
     c                   endif
     c  N34              update    HEADFR
     c     *in35         ifeq      '1'
     C                   write     CARGOOR
     c                   else
     C                   update    CARGOOR
     c                   endif
      *
     C     TrackKey      Chain     TrackL02                           33
     C   33              exsr      TTSR
      *
     C                   ENDSR
      *
     C***********************************************
     C     FinnFvkt      BEGSR
     C***********************************************
      *
     C                   Z-add(H)  COMVKT        COMFBV
     C     COMM3         IFeq      *zeros
     C     COMLM         andeq     *zeros
     c                   goto      UtFbv
     c                   endif
      *
      * Burde være hentet fra avd..
     C                   z-add     286           XXM3IN            3 0
     C                   z-add     2000          XXLMIN            5 0
      * avvikende Kundefaktor??
     C     HEKNSF        Ifne      *zeros
     C                   move      HEKNSF        M3KUND
     C                   else
     C                   move      HEKNKF        M3KUND
     C                   end
     C     KUNKEY        CHAIN     CUM3LM                             33
     C     *in33         IFEQ      *OFF
     C     m3m3          Ifne      *zeros
     C                   Z-ADD     m3m3          XXM3IN
     C                   endif
     C     mllm          Ifne      *zeros
     C                   Z-ADD     mllm          XXLMIN
     C                   endif
     C                   ENDIF
      *
      * Finn m3-fvekt
     C     COMM3         MULT(H)   XXM3IN        COMM3F            5 0
     C     Comm3F        Ifgt      COMFBV
     C                   Z-add(H)  COMM3F        COMFBV
     C                   END
      * Finn LM-fvekt
     C     COMLM         MULT(H)   XXLMIN        COMLMF            5 0
     C     COMLMF        Ifgt      COMFBV
     C                   Z-add(H)  COMLMF        COMFBV
     C                   END
      *
     C     UtFbv         ENDSR
      *
     C***********************************************
     C     FixDisp       BEGSR
     C***********************************************
      *
     C     SndKey        CHAIN     DISP                               33
     C     *in33         IFEQ      *OFF
     C     DISTA1        IFEQ      'E'
     C     DISTA1        OREQ      'V'
     C                   move      ' '           DISTA1
     C                   END
     c                   update    DISPR
     C                   END
N03   * Dersom oppdraget har VF-oppdrag under seg - flytt kolliIDene dit og frigjør
N03  c                   If        TROPD2<>0
N03  c                   exsr      FixDisp2
N03  c                   endif
      *
     C                   ENDSR
      *
N03
N03  C***********************************************
N03  C     FixDisp2      BEGSR
N03  C***********************************************
N03  C     DokKey        Setll     DOKUFM
N03  C     DokKey        READE     DOKUFM                                 33
N03  C     *in33         doweq     '0'
N03  C     FMST          IFEQ      'E'                                          Ekstern
N03  C     FMST          OREQ      'I'                                          Intern
N03  C     FMST          OREQ      'M'                                          Mankomerket
N03  C     FMST          OREQ      'O'                                          Ompakket/Original
N03  C                   delete    dokufmr
N03  C                   move      TRAVD2        FMAVD
N03  C                   move      TROPD2        FMOPD
N03  C                   z-add     1             FMFBNR
N03  C                   write     dokufmr
N03  C                   end
N03  C     DokKey        READE     DOKUFM                                 33
N03  C                   end
N03   *
N03  C     VFKey         CHAIN     DISP                               33
N03  C     *in33         IFEQ      *OFF
N03  C     DISTA1        IFEQ      'E'
N03  C     DISTA1        OREQ      'V'
N03  C                   move      ' '           DISTA1
N03  C                   END
N03  c                   update    DISPR
N03  C                   END
N03  C                   ENDSR
      *
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
      *

     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      'TEI'         TTACTI
     C                   EVAL      TTTEXT = 'Arrived terminal ' + FIKRTN
     C                   CAT       KOTERM:1      TTTEXT
N02  C                   EVAL      TTTEXL = 'Ankommet terminal ' + FIKRTN
N02  C                   CAT       KOTERM:1      TTTEXL
      * ikke alle kolli-IDs lest
     c*    sumNTX        ifne      sumNTA
     C*                  EVAL      TTTEXT = %trim(TTTEXT)+ ' Checked: '
     c*                            + %trim(%editc(sumNTX:'3'))
     c*                            + '/' + %trim(%editc(sumNTA:'3'))
     c*                  endif
     C                   MOVEL     FIKRTN        TTDEPO
     C                   CAT       KOTERM:1      TTDEPO
     C     TEINDT        IFEQ      *ZEROS
     C                   MOVE      ULDT          TEINDT
     C                   MOVE      XULTM         TEINTI
     C                   ENDIF
     C                   MOVE      TEINDT        TTDATE
     C                   MOVEL     TEINTI        TTTIME
     C                   MOVE      ULDT          TTDATL
     C                   MOVE      XULTM         TTTIML
     C                   MOVEL     'CARGOSCAN'   TTUSER
     C                   MOVE      'S'           TTMANU
     C                   WRITE     TRACKFR
      *
      * OPPDATER EV. BESTILLENDE OPPDRAG (DERSOM DETTE ER DUP AV...)
     C     HXSNDN        IFNE      *BLANKS
     C                   clear                   DF1004
     C     HXSNDN11_20   ifne      *blanks
     C                   movel     HXSNDN4_20    DF1004
     C                   else
     C                   movel     HXSNDN1_10    DF1004
     C                   end
     C* frakktbrev på andre oppdrag enn distr.oppdraget... Oppdater T&T
     C     DF1004        setll     DOKUF7
     C     DF1004        reade     DOKUF7                                 33
     C     *in33         doweq     '0'
     C     DFAVD         IFNE      HEAVD
     C     DFOPD         andne     HEOPD
     C                   MOVE      DFAVD         TTAVD
     C                   MOVE      DFOPD         TTOPD
     C                   MOVE      DFFBNR        TTFBNR
     C                   WRITE     TRACKFR
     C                   end
     C     DF1004        reade     DOKUF7                                 33
     C                   end
     C*
     C                   END
     C*
     C                   ENDSR
      *
     C***********************************************
     C     TTZVK         BEGSR
     C***********************************************

     C                   clear                   TRACKFR
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      'ZVK'         TTACTI
     C                   eval      TTtext = 'Wt/Vol adj:'
     C                             +%trimR(%editc(HEVKT:'Z'))+' Kg'
     C                             +%trimR(%editw(HEM3:'  0 ,   '))+' M3 '
     C                             +%trimR(%editw(HELM:'0 ,  '))+' LM '
     c                             +'BY '+%trim(FIKRTN)+' '+%trim(KOTERM)
N02  C                   eval      TTtexl = 'Vkt/Vol jus:'
n02  C                             +%trimR(%editc(HEVKT:'Z'))+' Kg'
n02  C                             +%trimR(%editw(HEM3:'  0 ,   '))+' M3 '
n02  C                             +%trimR(%editw(HELM:'0 ,  '))+' LM '
n02  c                             +'AV '+%trim(FIKRTN)+' '+%trim(KOTERM)
     C                   MOVEL     KOTERM        TTDEPO
     C                   MOVE      KOINDT        TTDATE
     C                   MOVEL     KOINTI        TTTIME
     C                   MOVE      ULDT          TTDATL
     C                   MOVE      XULTM         TTTIML
     C                   MOVEL     'CARGOSCAN'   TTUSER
     C                   MOVE      'S'           TTMANU
     C                   WRITE     TRACKFR
      *
     C*
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      * Foreløpig behandling kun på oppdragsnivå (i det en setter på tur)
      * Parametermessig også klargjort for å justere hente-oppdragssendinger
     C     *ENTRY        PLIST
     C                   PARM                    UUAVD             4 0
     C                   PARM                    UUOPD             7 0
     C                   PARM                    UUTERM           20
      *
     C                   eval      KOTERM = UUTERM
      *
     C     DokKey        KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    UUAVD
     C                   KFLD                    UUOPD
     C                   MOVE      'F'           FMRI
     C     SndKey        KLIST
     C                   KFLD                    UUAVD
     C                   KFLD                    UUOPD
N03  C     VFKey         KLIST
N03  C                   KFLD                    TRAVD2
N03  C                   KFLD                    TROPD2
     C     KunKey        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    M3KUND
     C     CARGOFKey     KLIST
     C                   KFLD                    KOID
     C                   KFLD                    KOTERM
      *
     C     TrackKey      KLIST
     C                   KFLD                    UUAVD
     C                   KFLD                    UUOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   Move      'TEI'         TTACTI
      *
     C     *loval        setll     FIRM
     C                   read      firm
      *
      *
      * siden ikke alle kolli nødvendigvis er skutt må hjelpefelt for TEI dato/tid benyttes
     C     *like         define    KOINDT        TEINDT
     C     *like         define    KOINTI        TEINTI
     C     *like         define    HEVKT         GMVKT
     C     *like         define    HEM3          GMM3
     C     *like         define    HELM          GMLM
     C     *like         define    COGNTA        sumNTA
     C     *like         define    sumNTA        sumNTX
     C     *like         define    COMNT         sumNTCS
     C     *like         define    COMNTP        sumNTP
     C     *like         define    COMVKT        sumVKT
     C     *like         define    COMM3         sumM3
     C     *like         define    COMLM         sumLM
     C     *like         define    COMFBV        sumFBV
     C                   Move      *zeros        COGNTA
     C                   Move      *zeros        COMNT
     C                   Move      *zeros        COMNTP
     C                   Move      *zeros        COMVKT
     C                   Move      *zeros        COMM3
     C                   Move      *zeros        COMLM
     C                   Move      *zeros        COMFBV
      *
     C                   ENDSR
