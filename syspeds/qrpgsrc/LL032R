     FLL032D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     FLLHEAD    IF   E           K DISK
     FLLLINE    IF   E           K DISK
     FLLLINE02  IF   E           K DISK
     F                                     RENAME(LLLINER:LLLINER2)
     FLLVARE01  IF   E           K DISK
     FLLW032    IF A E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK
     FKUNKO2    IF   E           K DISK
     FLL032P    O    E             PRINTER OFLIND(*IN01)
     F                                     USROPN
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(LL032P) -
     D                                     PAGESIZE(72 80) -
     D                                     CPI(10) -
     D                                     OVRFLW(64) -
     D                                     OUTQ(')
     D XCMD21          C                   CONST('CLRPFM -
     D                                     FILE(QTEMP/LLW013)')
     D                 DS
     D  LLVB                   1     35
     D  XLVB                   1      6
     D                 DS
     D  LHPAK                  1     17
     D  XHPAK                  1     14
     D                 DS
     D  LHBNA                  1     30
     D  LHBPN                 32     35  0
     D  XHBNA                  1     35
     D                 DS
     D  WSDTF                  1      6  0
     D  WSDTFD                 1      2  0
     D  WSDTFM                 3      4  0
     D  WSDTFÅ                 5      6  0
     D                 DS
     D  XXDTF                  1      8  0
     D  XXDTFÅ                 1      4  0
     D  XXDTFM                 5      6  0
     D  XXDTFD                 7      8  0
     D                 DS
     D  WSDTT                  1      6  0
     D  WSDTTD                 1      2  0
     D  WSDTTM                 3      4  0
     D  WSDTTÅ                 5      6  0
     D                 DS
     D  XXDTT                  1      8  0
     D  XXDTTÅ                 1      4  0
     D  XXDTTM                 5      6  0
     D  XXDTTD                 7      8  0
     D                 DS
     D  XHTDT                  1      6  0
     D  XHTDTD                 1      2  0
     D  XHTDTM                 3      4  0
     D  XHTDTÅ                 5      6  0
     D                 DS
     D  LHTDT                  1      8  0
     D  LHTDTÅ                 1      4  0
     D  LHTDTM                 5      6  0
     D  LHTDTD                 7      8  0
     D                 DS
     D  XIDAG                  1      6  0
     D  XIDD                   1      2  0
     D  XIDM                   3      4  0
     D  XIDÅ                   5      6  0
     D                 DS
     D  XIDAG2                 1      8  0
     D  XID2Å                  1      4  0
     D  XID2M                  5      6  0
     D  XID2D                  7      8  0
     D                 DS
     D  U1PARM                 1    256
     D  U1GRP                  1      8  0
     D  U1PNR                  9     16  0
     D  U1BNR                 17     33
     D  U1PAK                 34     50
     D  U1ORDR                51     67
     D  U1FLN                 68     84
     D  U1TDT                 85     92  0
     D  U1TDTÅ                85     88  0
     D  U1TDTM                89     90  0
     D  U1TDTD                91     92  0
     D  U1ALN                 93     99  0
     D  U1AVD                100    103  0
     D  U1OPD                104    110  0
     D  U1UT                 111    115
     D  U1FIRM               116    117
     D  U1ANR                118    132
     D  U1FLAG               256    256
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C     U1PARM        PARM      U1PARM        U2PARM          256
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     LL00K         KLIST
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LL02K         KLIST
     C                   KFLD                    U1ANR
     C     LH00K         KLIST
     C                   KFLD                    LLMN
     C     LLVARK        KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WSANR
     C     CUND1K        KLIST
     C                   KFLD                    U1FIRM
     C                   KFLD                    U1PNR
     C     KUNK2K        KLIST
     C                   KFLD                    U1PNR
     C                   KFLD                    WSBNR
     C     KUNKOK        KLIST
     C                   KFLD                    LHPNR
     C                   KFLD                    KUINTN
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         YRECNO            5 0
      *
     C     LL02K         SETLL     LLLINE02
     C     START1        TAG
     C                   EXSR      LESREC
      *
     C     START2        TAG
     C                   Z-ADD     *ZERO         XAL               1 0
     C                   MOVE      YRECNO        ZRECNO
     C     *IN94         DOWEQ     '0'
     C     XAL           IFGE      7
     C                   LEAVE
     C                   END
     C                   ADD       1             XAL
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   EXSR      LESREC
     C  N94              END
     C                   MOVE      ZRECNO        YRECNO
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     BLANKREC
     C                   EXFMT     NOREC
     C                   ELSE
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN03         IFEQ      '1'
     C                   MOVE      '3'           U1FLAG
     C                   GOTO      UTHOV
     C                   END
     C     *IN12         IFEQ      '1'
     C                   MOVE      '1'           U1FLAG
     C                   GOTO      UTHOV
     C                   END
      *
     C     *IN05         CABEQ     '1'           START
     C     *IN25         CABEQ     '1'           START1
     C     *IN07         IFEQ      '1'
     C                   EXSR      PRINT
     C   12              GOTO      START3
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C     ZRECNO        IFGT      0
     C     *IN52         DOWEQ     '0'
     C                   READC     SUBFIL                                 52
     C  N52              EXSR      OPTION
     C  N52              END
     C                   END
      *
     C                   GOTO      START3
      *
     C     UTHOV         ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     LESREC        BEGSR
      *////////////////////////////////////////////////////////////
     C     STLES         TAG
     C                   READ      LLLINE02                               94
     C     *IN94         IFEQ      '0'
     C     LH00K         CHAIN     LLHEAD                             33
     C  N33LHGRP         COMP      U1GRP                              3333
     C  N33LHPNR         COMP      U1PNR                              3333
     C  N33LHTDT         COMP      U1TDT                                33
     C   33              GOTO      STLES
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C     WSNR          IFEQ      '5'
     C     LL00K         CHAIN     LLLINE                             33
     C                   EXFMT     VISLIN
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLOPT
     C                   END
      *
     C     WSNR          IFEQ      '7'
     C     LH00K         CHAIN     LLHEAD                             33
     C     LHBNR         IFEQ      *BLANKS
     C                   MOVE      *ZEROS        KUKUN2
     C                   MOVE      *ZEROS        KUVADR
     C                   ELSE
     C                   MOVEL     LHBNR         KUINTN
     C     KUNKOK        CHAIN     KUNKO2                             90
     C                   END
     C                   EXFMT     VISHOD
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLOPT
     C                   END
      *
     C     SLOPT         ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     PRINT         BEGSR
      *////////////////////////////////////////////////////////////
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVEL     '*JOB'        WSPRT
     C                   MOVE      U1ANR         WSANR
     C                   MOVE      U1TDTÅ        WSDTFÅ
     C                   MOVE      U1TDTM        WSDTFM
     C                   MOVE      U1TDTD        WSDTFD
     C                   MOVE      *ZEROS        WSDTT
     C                   MOVE      *ZEROS        XXWNT0
      *
     C     STPRT1        TAG
     C                   EXFMT     PRTLIS
     C                   MOVEA     '00000'       *IN(86)
     C                   MOVE      '0'           *IN30
      *
     C   12              GOTO      SLPRT
     C   04              DO
     C                   CALL      'SYFA81B'
     C                   PARM      U1PNR         U1PNR2            8 0
     C                   PARM                    WSBNR
     C                   PARM                    UKN               8 0
     C                   GOTO      STPRT1
     C                   END
     C                   EXSR      TESTB
     C   30              GOTO      STPRT1
      *
      * SORTERE POSTER VIA ARBEIDSFIL
     C                   MOVE      *BLANKS       UCMD
     C                   MOVEL     XCMD21        UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C                   OPEN      LLW032
     C     WSANR         SETLL     LLLINE02
     C     STPRT2        TAG
     C     WSANR         READE     LLLINE02                               33
     C  N33LH00K         CHAIN     LLHEAD                             33
     C  N33              IF        ((LHGRP <> U1GRP) OR (LHPNR <> U1PNR))
     C                   GOTO      STPRT2
     C                   END
     C  N33LHTDT         IFLT      XXDTF
     C     LHTDT         ORGT      XXDTT
     C                   GOTO      STPRT2
     C                   END
     C  N33WSBNR         IFNE      *BLANKS
     C     WSBNR         ANDNE     LHBNR
     C                   GOTO      STPRT2
     C                   END
      *
     C     *IN33         DOWEQ     '0'
     C                   WRITE     LLW032R
     C     STPRT3        TAG
     C     WSANR         READE     LLLINE02                               33
     C  N33LH00K         CHAIN     LLHEAD                             33
     C  N33              IF        ((LHGRP <> U1GRP) OR (LHPNR <> U1PNR))
     C                   GOTO      STPRT3
     C                   END
     C  N33LHTDT         IFLT      XXDTF
     C     LHTDT         ORGT      XXDTT
     C                   GOTO      STPRT3
     C                   END
     C  N33WSBNR         IFNE      *BLANKS
     C     WSBNR         ANDNE     LHBNR
     C                   GOTO      STPRT3
     C                   END
     C  N33              ENDDO
      *
      * OVERSTYRE OG ÅPNE PRINTEFIL
     C                   MOVE      *BLANKS       UCMD
     C     XCMD11        CAT       WSPRT:0       UCMD
     C                   CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C                   OPEN      LL032P
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XIDAG2
     C                   MOVE      XID2D         XIDD
     C                   MOVE      XID2M         XIDM
     C                   MOVE      XID2Å         XIDÅ
     C                   MOVEL     WTM           XTID
     C     CUND1K        CHAIN     CUNDL01                            33
     C                   WRITE     LL032P1
      *
     C     *LOVAL        SETLL     LLW032
     C                   READ      LLW032                                 33
      *
     C     *IN33         DOWEQ     '0'
     C   01              WRITE     LL032P1
     C   01              MOVE      '0'           *IN01
     C                   MOVE      LHTDTD        XHTDTD
     C                   MOVE      LHTDTM        XHTDTM
     C                   MOVE      LHTDTÅ        XHTDTÅ
     C                   WRITE     LL032P2
     C                   ADD       LLWNT0        XXWNT0
     C                   READ      LLW032                                 33
     C  N33              ENDDO
      *
     C                   WRITE     LL032P3
     C                   CLOSE     LL032P
     C                   CLOSE     LLW032
      *
     C     SLPRT         ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C     LLVARK        CHAIN     LLVARE01                           90
     C     *IN90         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0585'     ZMSGNO
     C                   GOTO      SLTST
     C                   END
      *
     C                   MOVE      WSDTFD        UDD               2 0
     C                   MOVE      WSDTFM        UMM               2 0
     C                   MOVE      WSDTFÅ        UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      89
     C     *IN89         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPD0001'     ZMSGNO
     C                   GOTO      SLTST
     C                   END
      *
     C                   MOVE      WSDTTD        UDD               2 0
     C                   MOVE      WSDTTM        UMM               2 0
     C                   MOVE      WSDTTÅ        UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      88
     C     *IN88         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPD0001'     ZMSGNO
     C                   GOTO      SLTST
     C                   END
      *
     C                   MOVE      WSDTFD        XXDTFD
     C                   MOVE      WSDTFM        XXDTFM
     C     WSDTFÅ        IFGT      50
     C     1900          ADD       WSDTFÅ        XXDTFÅ
     C                   ELSE
     C     2000          ADD       WSDTFÅ        XXDTFÅ
     C                   END
     C                   MOVE      WSDTTD        XXDTTD
     C                   MOVE      WSDTTM        XXDTTM
     C     WSDTTÅ        IFGT      50
     C     1900          ADD       WSDTTÅ        XXDTTÅ
     C                   ELSE
     C     2000          ADD       WSDTTÅ        XXDTTÅ
     C                   END
     C     XXDTF         IFGT      XXDTT
     C                   MOVEA     '11'          *IN(88)
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPD0007'     ZMSGNO
     C                   GOTO      SLTST
     C                   END
      *
     C     WSBNR         IFNE      *BLANKS
     C     KUNK2K        CHAIN     KUNKO2                             87
     C     *IN87         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0584'     ZMSGNO
     C                   GOTO      SLTST
     C                   END
     C                   END
      *
     C     WSPRT         IFEQ      *BLANKS
     C                   MOVE      '1'           *IN86
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTST
     C                   END
      *
     C     SLTST         ENDSR
      *
