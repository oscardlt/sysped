      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKI026R MODULE(*LIBL/TDKI026R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12253 * 01 YBC PTF12 LINJENR ER UTVIDET FRA 2.0 TIL 5.0
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKiv      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D DKiv_SYAVA      S              4
     D DKiv_SYAV       S              4  0
     D DKiv_SYOPA      S              7
     D DKiv_SYOP       S              7  0
S01  D*DKiv_SYLIA      S              2
S01  D*DKiv_SYLI       S              2  0
N01  D DKiv_SYLIA      S              5
N01  D DKiv_SYLI       S              5  0
     D DKIV_ERR        S              1
     D JSONstring      S          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬dkiv_syav¬ : ¬'+%trim(%editc(DKiv_SYAV:'Z'))+'¬, '
        +'¬dkiv_syop¬ : ¬'+%trim(%editc(DKiv_SYOP:'Z'))+'¬, '
        +'¬dkiv_syli¬ : ¬'+%trim(%editc(DKiv_SYLI:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"lineErrupdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKiv

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      DKiv_SYAVA = zhbgetvar('AVD')
     C                   EVAL      DKiv_SYAV  = c2n2(DKiv_SYAVA)
     C                   EVAL      DKiv_SYOPA = zhbgetvar('OPD')
     C                   EVAL      DKiv_SYOP  = c2n2(DKiv_SYOPA)
     C                   EVAL      DKiv_SYLIA = zhbgetvar('LIN')
     C                   EVAL      DKiv_SYLI  = c2n2(DKiv_SYLIA)
     C                   EVAL      DKiV_ERR   = zhbgetvar('DKIV_ERR')
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DKivKey       klist
     C                   kfld                    DKiv_SYAV
     C                   kfld                    DKiv_SYOP
     C                   kfld                    DKiv_SYLI
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKiv

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     DKivKey       CHAIN     DKiv                               55
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XDKIV_ERR=DKIV_ERR
     C                   UPDATE    DKivR
     C                   ENDSR
