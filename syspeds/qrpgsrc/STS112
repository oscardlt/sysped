      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STS112   MODULE(*LIBL/STS112 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FSTSCLI    if   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D Sendnr          s             20
     D ypr             s              8
     D avd             s              4
     D opd             s              7
     D fbn             s              3
     D AN              s              5
     D ANT             s              5  0
     D UserId          s             10
     D User            s             10
     D VisAlle         s              1
     D SetPO           s              1
      * andre variabler
     D AntCli          s              5  0
     D Antmank         s              5  0
     D                 DS
     D PONR                    1     27
     D PONRONR                 1      7
     D PONRPO                  8     27
     D                 DS
     D SCCLID                  1     20
     D SCCLID10               11     20
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      SENDNR = zhbgetvar('SENDNR')
     C                   eval      SCTYPE = zhbgetvar('YTY')
     C                   eval      YPR    = zhbgetvar('YPR')
     C                   eval      scpro  = c2n2(YPR)
     C                   eval      AVD    = zhbgetvar('AVD')
     C                   eval      scavd  = c2n2(AVD)
     C                   eval      OPD    = zhbgetvar('OPD')
     C                   eval      scopd  = c2n2(OPD)
     C                   eval      FBN    = zhbgetvar('FBN')
     C                   eval      scfbnr = c2n2(FBN)
     C                   eval      AN    = zhbgetvar('AN')
     C                   eval      ANT    = c2n2(AN)
     C                   eval      SETPO  = zhbgetvar('SETPO')
     C                   eval      PONR   = zhbgetvar('PONR')
     C                   eval      userid = zhbgetvar('user')
     C                   eval      VisAlle = zhbgetvar('Alle')
      * Set libl/open files
     C                   exsr      Init
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Hent/vis kolli-id
     C                   exsr      HCLI
      *
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     STSCLI
     C                   eval      *inlr = *on
     C                   RETURN
      *
     C***********************************************
     C     HCLI          BEGSR
     C***********************************************
      * Repeat UserId for the next invocation
     C                   callp     updhtmlvar('SENDNR':sendnr)
     C                   callp     updhtmlvar('YTY':sctype)
     C                   callp     updhtmlvar('YPR':ypr)
     C                   callp     updhtmlvar('AVD':AVD)
     C                   callp     updhtmlvar('OPD':opd)
     C                   callp     updhtmlvar('FBN':fbn)
     C                   callp     updhtmlvar('AN':an)
     C                   callp     updhtmlvar('SETPO':setpo)
     C                   callp     updhtmlvar('PONR':ponr)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':userid)
     C                   callp     updhtmlvar('ALLE':VisAlle)
      *
     C                   callp     wrtsection('top')
      *
     c                   move      *zeros        antcli
     c                   move      *zeros        antmank
     C     cliKey        SETLL     STSCLI
     C     cliKey        READE     STSCLI                                 94
     C     *IN94         DOWEQ     '0'
     C                   add       1             antcli
     C     VisAlle       Ifne      'J'
     C     SCSTAT        Andne     ' '
     C     SCSTAT        Andne     'E'
     C                   goto      NEXT
     C                   end
     C     SCSTAT        IFEQ      ' '
     C     SCSTAT        Oreq      'E'
     C                   add       1             antmank
     C                   end
     C                   callp     updhtmlvar('ANTN':
     C                             %trim(%editc(Antcli:'3')))
     C                   callp     updhtmlvar('CLID':SCCLID)
     C                   callp     updhtmlvar('CLID10':SCCLID10)
     C     scstat        ifeq      *blanks
     C                   callp     updhtmlvar('STAT':spaces)
     C                   else
     C                   callp     updhtmlvar('STAT':SCSTAT)
     C                   end
      *
     C                   callp     wrtsection('tablerow')
     C     Next          tag
     C     cliKey        READE     STSCLI                                 94
     C  N94              ENDDO
      *
     C                   callp     updhtmlvar('ANTM':
     C                             %trim(%editc(Antmank:'3')))
     C                   callp     wrtsection('tablebot')
      *
     C                   ENDSR
      *
      *
      *=====================================================================******
     C     init          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              30
      *
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      STSCLI
      *
      *  keys mm
     C     CLIKEY        KLIST
     C                   KFLD                    sctype
     C                   KFLD                    scpro
     C                   KFLD                    scavd
     C                   KFLD                    scopd
     C                   KFLD                    scfbnr
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STS112'
      *------------------
     C                   endsr
