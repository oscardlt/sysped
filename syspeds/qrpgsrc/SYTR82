      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE AVD-STYRING INNLAND       ***
      ***********************************************************
      *
     FKODTRI    UF A E           K DISK
     FKODTA     IF   E           K DISK
     FSYTR82FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2TERM        YTERM             4 0
     C                   MOVE      W2PNRF        YPNRF             4 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODRI1        SETLL     KODTRI
     C     START1        TAG
     C                   READ(N)   KODTRI                                 94
     C   94              MOVE      *BLANKS       SFDEL
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   MOVE      *BLANKS       SFDEL
     C                   WRITE     SUBFIL
     C                   READ(N)   KODTRI                                 94
     C  N94              MOVE      RITERM        YTERM
     C  N94              MOVE      RIPNRF        YPNRF
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   WRITE     BUNN
     C                   END
     C                   SETOFF                                       98
     C                   EXFMT     SUBCTL
     C*
     C*** AVSLUTT
     C     ST41          TAG
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY LANDKODE
     C   06              EXSR      MOVB1
     C   06              GOTO      START5
     C*** SØK FRA LANDKODE
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C     *IN25         IFEQ      *ON
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODRI3        SETLL     KODTRI
     C  N94              READ      KODTRI                                 94
     C                   GOTO      START2
     C                   ENDIF                                                  <*IN25
     C*
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** ENDR
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     KODRI2        CHAIN(N)  KODTRI                             33
     C                   EXSR      MOVRIW
     C                   GOTO      START5
     C                   ENDIF                                                  <WSNR
     C*** SLETT
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   MOVE      *ON           *IN30
     C                   MOVE      'TRO0154'     MSGNR
     C                   EXFMT     VAREL3
     C                   MOVE      *OFF          *IN30
     C     *IN12         IFEQ      *OFF
     C     KODRI2        CHAIN     KODTRI                             33
     C  N33              DELETE    KODTRIR
     C                   EXSR      MOVB2
     C                   MOVEL     '*DELETE*'    SFDEL
     C                   ENDIF                                                  <*IN12
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   ENDIF                                                  <WSNR
     C*
     C*** LAGE/ENDRE LANDKODE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       307069
     C   03              GOTO      SLUTT
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C     *IN40         IFEQ      *ON
     C                   SETOFF                                       40
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      MOVB1
     C                   GOTO      START5
     C                   ENDIF                                                  <*IN40
     C*
     C*** SØK FRA DESTINASJON
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     KODTA1        CHAIN     KODTA                              30
     C     *IN30         IFEQ      *ON
     C                   MOVE      'TRO0152'     MSGNR
     C                   GOTO      SLTB
     C                   ENDIF                                                  <*IN30
     C     KODTA2        CHAIN     KODTA                              30
     C     *IN30         IFEQ      *ON
     C                   MOVE      'TRO0152'     MSGNR
     C                   GOTO      SLTB
     C                   ENDIF                                                  <*IN30
     C     KODTA3        CHAIN     KODTA                              30
     C     *IN30         IFEQ      *ON
     C                   MOVE      'TRO0152'     MSGNR
     C                   GOTO      SLTB
     C                   ENDIF                                                  <*IN30
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODRIK        CHAIN     KODTRI                             33
     C     *IN33         IFEQ      *OFF
     C     *IN40         ANDEQ     *OFF
     C                   MOVE      'TRO0153'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      SLTB
     C                   ENDIF                                                  <*IN33
     C*
     C                   EXSR      MOVWRI
     C  N33              UPDATE    KODTRIR
     C   33              WRITE     KODTRIR
     C                   MOVE      *BLANKS       SFDEL
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     SFDEL         COMP      '*DELETE*'                             98
     C   98              GOTO      SLHENT
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     SFDEL         COMP      '*DELETE*'                             98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
      * VED LAGE NY   (BEHOLD TERM FRA SISTE)
     C                   MOVE      *ZEROS        WSPNRF
     C                   MOVE      *ZEROS        WSPNRT
     C                   MOVE      *ZEROS        WSAVDU
     C                   MOVE      *ZEROS        WSAVDI
     C                   MOVE      *ZEROS        WSAVDH
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
      * VED DELETE
     C                   MOVE      *ZEROS        RIPNRF
     C                   MOVE      *ZEROS        RIPNRT
     C                   MOVE      *ZEROS        RIAVDU
     C                   MOVE      *ZEROS        RIAVDI
     C                   MOVE      *ZEROS        RIAVDH
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVRIW        BEGSR
     C***********************************************
     C                   MOVE      RITERM        WSTERM
     C                   MOVE      RIPNRF        WSPNRF
     C                   MOVE      RIPNRT        WSPNRT
     C                   MOVE      RIAVDU        WSAVDU
     C                   MOVE      RIAVDI        WSAVDI
     C                   MOVE      RIAVDH        WSAVDH
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     MOVWRI        BEGSR
     C***********************************************
     C                   MOVE      WSTERM        RITERM
     C                   MOVE      WSPNRF        RIPNRF
     C                   MOVE      WSPNRT        RIPNRT
     C                   MOVE      WSAVDU        RIAVDU
     C                   MOVE      WSAVDI        RIAVDI
     C                   MOVE      WSAVDH        RIAVDH
     C*
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTA1        KLIST
     C                   KFLD                    WSAUNI
     C                   KFLD                    WSAVDU
     C     KODTA2        KLIST
     C                   KFLD                    WSAUNI
     C                   KFLD                    WSAVDI
     C     KODTA3        KLIST
     C                   KFLD                    WSAUNI
     C                   KFLD                    WSAVDH
      * KEY FOR CHAIN PÅ VARELI
     C     KODRIK        KLIST
     C                   KFLD                    WSTERM
     C                   KFLD                    WSPNRF
      * KEY FOR SETLL/READ
     C     KODRI1        KLIST
     C                   KFLD                    W2TERM
     C                   KFLD                    W2PNRF
      * KEY FOR DELETE
     C     KODRI2        KLIST
     C                   KFLD                    RITERM
     C                   KFLD                    RIPNRF
      * KEY FOR BLA VIDERE
     C     KODRI3        KLIST
     C                   KFLD                    YTERM
     C                   KFLD                    YPNRF
     C*
     C                   MOVE      'PI '         YPIUNI            3
     C                   MOVE      'A'           WSAUNI            1
     C*
     C                   ENDSR
     C*
