*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH PTF8 NY HELP - FUNKSJON
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR FAXBIGPNL)
     A                                      HLPTITLE('GODSLISTE PÅ FAX')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØK')
     A                                      CF09(09 'SØK BIL')
     A                                      CF10(10 'SEND FAX')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  4'SYSPED/SYTR93B'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 SHU0752 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A                                  9  3'Kunde:'
     A            KUNDE          8Y 0B  9 11EDTCDE(4)
     A  71
     AO 70                                  DSPATR(RI)
     A            KNAVN         30A  B  9 21
     A                                 11  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B 11 11COLOR(YLW)
     A  72                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 13  3'Dato:'
     A            AAR            4Y 0B 13 11EDTCDE(4)
     A            MND            2Y 0B 13 16EDTCDE(4)
     A            DAG            2Y 0B 13 19EDTCDE(4)
     A                                 23  2'F3=avbryt'
     A                                      COLOR(BLU)
     A                                 23 20'F4=SØK   '
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 23 40'F10=send email'
     A                                      COLOR(BLU)
     A                                 12 11MSGCON(070 JOV1249 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
