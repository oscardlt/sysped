     A*%%TS  SD  20140218  075858  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/TGUSER)
     A                                      PRINT
     A                                      ALTHELP
     A                                      CF03(03 'Avslutt')
     A                                      HELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20140210  082401  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  6  3VALUES(' ' '1' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            TGKUNR    R    7   O  7  8
     A            TGUSR     R   20   O  7 18
     A            TGPWD     R   20   O  7 39
     A            TGBUD     R    1   O  7 62
     A            TGLIM     R    5S 2O  7 65EDTCDE(J)
     A            TGFRI     R   30   O  6 50
     A            TGTRAN         8S 0O  6  8
     A            VMNAVN        30   O  6 18
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20140218  075858  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0008)
     A  *DS4                                SFLSIZ(0008)
     A  *DS3                                SFLPAG(0007)
     A  *DS4                                SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFB05B'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(040 SHU1437 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(077 SHU1438 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  8MSGCON(070 SHU1442 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2'INVALID OPTION'
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A*%%TS  SD  20140212  084517  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            MSGNR          7A  P
     A                                  2  2MSGCON(070 SHU1438 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1  8MSGCON(050 SHU1442 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTRAN         8S 0B  3  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A            WSFRI         30A  B  3 50DSPATR(UL)
     A                                      CHECK(LC)
     A  40                                  DSPATR(PC)
     A            WSKUNR         7A  B  4  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  70                                  DSPATR(RI)
     A            WSUSR         20A  B  4 18DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      CHECK(LC)
     A            WSPWD         20A  B  4 39DSPATR(UL)
     A  68                                  DSPATR(RI)
     A                                      CHECK(LC)
     A            WSBUD          1A  B  4 62DSPATR(UL)
     A  40                                  DSPATR(HI)
     A                                      COLOR(GRN)
     A            WSLIM          5Y 2B  4 65COLOR(GRN)
     A                                      EDTCDE(J)
     A N30                              5  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              5 22MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              5 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL2
     A*%%TS  SD  20140210  100628  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            W2TRAN         8S 0I  2 16DSPATR(UL)
     A            W2KUNR         7A  I  3 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3 50MSGCON(013 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A          R VAREL3
     A*%%TS  SD  20140210  100326  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            TGKUNR    R    7   O  2  8DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            TGUSR     R   20   O  2 17DSPATR(UL)
     A                                      DSPATR(HI)
     A            TGPWD     R   20   O  2 40DSPATR(UL)
     A                                      DSPATR(HI)
     A            TGFRI     R   30   O  2 50DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A*%%TS  SD  20140210  100326  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFB05B'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(040 SHU1437 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(070 SHU1438 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(060 SHU1442 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
