********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10244 * 01 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A
      *              MYE ENDRING. INGEN MERKING
10262 * 02 YBC PTF10 TOLLSATS ER UTVIDERT TIL 7.2,
10286 * 03 YBC PTF10 VEKT ER ENDRET FRA 9.0 TIL 12.3
********************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC(SADE010 HELPNU HLP400)
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           3Y 0O  7  2EDTCDE(4)
     A            SLKNR          8S 0H
     A            SLALFA        28A  O  7  6TEXT('ALFASØKING')
     A            SLTXT2        20A  O  7 35
     A            SLR31          1A  O  7 56
     A            SLOPPL         2A  O  7 59TEXT('OPPRINGELSE LAND')
     A            SLTANR         8S 0O  7 62TEXT('TARIFFNR.')
     A            SLTN           1A  O  7 71TEXT('TOLLNEDSETTELSE')
     A            SLPRE          1A  O  7 74
S03  A*           SLVEKT         5Y 0O  7 76TEXT('VEKR')
N03  A            SLVEKT        12Y 3O  7 76TEXT('VEKR')
     A                                      EDTCDE(4)
     A            SLPVA          1A  O  7 90
S01  A*           SLSATS         5Y 2O  7 85TEXT('PVA SATS')
N01  A            SLSATS         7Y 2O  7 93TEXT('PVA SATS')
     A                                      EDTCDE(4)
S01  A*           SLMFR          1A  O  7 94
N01  A            SLMFR          1A  O  7102
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  9'SAD-transitt'
     A                                      COLOR(WHT)
     A                                  1 30'Tdeknr.'
     A            UAVD           4Y 0O  1 38COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 43'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 45COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  2'Siste li Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merke & nr.      -
     A                                       Sjø'
     A                                      COLOR(WHT)
     A            SVLI           5Y 0O  4  2COLOR(BLU)
     A                                      EDTCDE(4)
     A            SVVNT          8Y 0O  4 11COLOR(BLU)
     A                                      EDTCDE(4)
     A            SVVKTN         9Y 0O  4 20COLOR(BLU)
     A                                      EDTCDE(4)
     A            SVNTM          7Y 0O  4 34COLOR(BLU)
     A                                      EDTCDE(4)
     A            SVVT          15A  O  4 42COLOR(BLU)
     A            SVFT          17A  O  4 58COLOR(BLU)
     A            SV01           1A  O  4 76COLOR(RED)
     A  37                              5  2'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  6  2'Lin Alfasøkingtekst............. T-
     A                                      ekst.............. R31 Op Tariffnr -
     A                                      TN P VEKT          PVA Tol.sats MF'
     A                                      COLOR(WHT)
     *
     A          R VARELI
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF04(04 'SØK KODER')
     A                                      CF08(08 'SØK')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF03(03 'Be om utskrift')
     A                                      CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CF12(12 'Gå til bilde02')
     A  35                                  CF22(22 'MERK B PÅ STATUS')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPDOC('SADE010' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 014 02 042)
     A          H                           HLPDOC('SADE011' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 044 02 072)
     A          H                           HLPDOC('SADE012' 'HELPNU' 'HLP400')
     A                                      HLPARA(03 008 03 052)
     A          H                           HLPDOC('SAD015' 'HELPNU' 'HLP400')
     A                                      HLPARA(04 008 05 078)
     A          H                           HLPDOC('SAD016' 'HELPNU' 'HLP400')
     A                                      HLPARA(06 001 06 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  4'Linjenr  Brutto vekt Netto vekt Me-
     A                                      ngde.'
     A                                      COLOR(WHT)
     A            W2NR           4Y 0B  2  5TEXT('LINJENR.')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2VKTB         9Y 0B  2 14TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2VKTN         9Y 0B  2 26TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2NTM          7Y 0B  2 36TEXT('Mengde')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  2'V.B'
     A            W2FT          17A  B  3  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2NT           6Y 0B  3 24TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH           4A  B  3 31TEXT('Enhet')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2VT          15A  B  3 36TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A                                  4  2'T.O'
     A            W2TOPL        45A  B  4  6TEXT('TILLEGGSOPPLYSNING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A                                  5  2'F3/F17=Avslutt'
     A                                      COLOR(BLU)
     A                                  5 19'F8=Søk'
     A                                      COLOR(BLU)
     A                                  5 28'F15=Auto kopi T.O.'
     A                                      COLOR(BLU)
     A N36                              5 49'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                  1  9'SAD-transitt'
     A                                      COLOR(WHT)
     A                                  1 30'Tdeknr.'
     A            UAVD           4Y 0O  1 38COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 43'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 45COLOR(BLU)
     A                                      EDTCDE(4)
     A  37                              5  2'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A                                  6  2'Lin Alfasøkingtekst............. T-
     A                                      ekst.............. R31 Op Tariffnr -
     A                                      TN P VEKT          PVA Tol.sats MF'
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  3  2'Søk fra kundenr.  --->'
     A            W2KNR          8Y 0B  3 25TEXT('KUNDENR.')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4  2'  fra vare (alfa) --->'
     A            W2ALFA        28A  I  4 25TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
