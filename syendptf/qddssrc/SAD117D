      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            MVLI           2Y 0O  6  5TEXT('Linjenr')
     A                                      EDTCDE(4)
     A            MVBL          11Y 2O  6  8TEXT('Beløp')
     A                                      EDTCDE(4)
     A            MVVNT          8Y 0O  6 21TEXT('Tariffnr')
     A                                      EDTCDE(4)
     A            MVVKTB         9Y 3H
     A            MVVKTN         9Y 3O  6 30TEXT('Nettovekt')
     A                                      EDTCDE(4)
     A            MVNT           5Y 0O  6 41TEXT('Kolli')
     A                                      EDTCDE(4)
     A            MVVOEC        17A  O  6 47TEXT('VOEC NR')
     A            MVVS          50A  O  6 65TEXT('Varebeskrivelse')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Plukk')
     A                                      CF08(08 'Søk')
     A N49                                  CF16(16 'Lag ny consignment')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD117R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold kurermanifest - Linje'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Turnr:'
     A            UPRO           8Y 0O  2  9TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 22'Cons.nr.:'
     A            UCN            2Y 0O  2 32TEXT('Linjenr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A N49                              3  2'Velg   2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op LN        Beløp Tariffnr  Netto-
     A                                      vekt Kolli VOEC nr           Varebe-
     A                                      skrivelse'
     A                                      COLOR(WHT)
     A                                  5  2' '
      *
     A          R BUNN
     A                                 26  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Plukk')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A N49                                  CF16(16 'Lag ny consignment')
     A                                  1  2'SYSPED/SAD117R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold kurermanifest - Linje'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Turnr:'
     A            UPRO           8Y 0O  2  9TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 22'Cons.nr.:'
     A            UCN            2Y 0O  2 32TEXT('Linjenr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A N49                              3  2'Velg   2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op LN        Beløp Tariffnr  Netto-
     A                                      vekt Kolli VOEC nr           Varebe-
     A                                      skrivelse'
     A                                      COLOR(WHT)
     A                                  5  2' '
     A                                 26  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MVLI           2S 0H
     A            MSGNR          7A  P
     A                                  1  2'.......Beløp Tariffnr Bruttovekt  -
     A                                      Nettovekt Kolli VOEC nr           V-
     A                                      arebeskrivelse'
     A                                      COLOR(WHT)
     A            WSBL          11Y 2B  2  2TEXT('Beløp Tollverdi')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,00)
     A            WSVNT          8Y 0B  2 15TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVKTB         9Y 3B  2 24TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,000)
     A            WSVKTN         9Y 3B  2 35TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,000)
     A            WSNT           5Y 0B  2 46TEXT('Kolli')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVOEC        17A  B  2 52TEXT('VOEC nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  86                                  DSPATR(RI)
     A            WSVS          50A  B  2 70TEXT('Varebeskrivelse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  85                                  DSPATR(RI)
     A                                  3  2'F4=Søk   F12=Avbryt'
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MVLI           2S 0H
     A                                  1  2'.......Beløp Tariffnr Bruttovekt  -
     A                                      Nettovekt Kolli VOEC nr           V-
     A                                      arebeskrivelse'
     A                                      COLOR(WHT)
     A            MVBL          11Y 2O  2  2TEXT('Beløp Tollverdi')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MVVNT          8Y 0O  2 15TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MVVKTB         9Y 3O  2 24TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MVVKTN         9Y 3O  2 35TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MVNT           5Y 0O  2 46TEXT('Kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MVVOEC        17A  O  2 52TEXT('VOEC nr')
     A                                      COLOR(WHT)
     A            MVVS          50A  O  2 70TEXT('Varebeskrivelse')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'F4=Søk   F12=Avbryt'
