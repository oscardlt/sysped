      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSCS MODULE(*LIBL/STSCS
CRTPGM*         *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 LM kan mottas
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FBRPARF    IF   E           k disk    usropn
     FWRKCGI    UF A E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          s             10
     D WSCLID          s             20
     D WSPAAPALL       s              1
     D USER            s             10
     D CLID            s             20
     D WSLEST          s              5  0
     D WSPALL          s              5  0
     D LBH             s             15
     D STDLBH          s             15
     D VKT             s              4
     D VKTN            s              4  0
N01  D LM              s              5
N01  D LMN             s              4  2
     D DFTUSER         s             10
     D MAAL            s              1
     D MAALTXT         s             50
     D KRAVVKT         s              1
     D KRAVMAL         s              1
     D FEILMELDING     s             70
     D FEILKODE        s              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
      *
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      6  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
     D                 DS
     D  WKey2                  1     30
     D  WDsUser                1     10
     D  WDsCLID               11     30
     D                 DS
     D  Wdata                  1    220
     D  WDsDTÅÅ                1      4
     D  WDsDTMM                5      6
     D  WDsDTDD                7      8
     D  WDsTID                 9     14
     D  WDsPALL               15     15
     D  WDsVKT                16     19
     D  WDsLBH                21     35
N01  D  WDsLM                 36     39
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA1650            16     50
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
     C                   EXSR      INIT
      *
     C                   exsr      UPDFIL
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     c     MAAL          ifeq      'N'
     C                   callp     wrtsection('Maaldummy')
     C                   else
     C                   exsr      MaalSR
     C                   end
     C                   callp     updhtmlvar('MAALTXT':MAALTXT)
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     WRKCGI
     C                   close     BRPARF
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * MaalSR
      *=====================================================================******
     C     MaalSR        begsr
     C                   EVAL      MAALTXT = 'Oppgi evt mål FØR kolli skytes:'
     C                   callp     wrtsection('Maaltop')
     C                   callp     updhtmlvar('STDMAL':' ')
     C                   callp     updhtmlvar('STDMAT':'- std.mål -')
     C                   callp     wrtsection('Maalrow')
      *
      *
     C                   MOVE      'CSSTM'       PAID
     c                   move      DFTUSER       PABRID
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 40
     C     *in40         doweq     '0'
     C                   movel     PAVRDA        STDMAL           15
     C                   callp     updhtmlvar('STDMAL':STDMAL)
     C                   movel     PAVRDA1650    STDMAT
     C     STDMAT        IFEQ      *blanks
     C                   movel     STDMAL        STDMAT           15
     C                   end
     C                   callp     updhtmlvar('STDMAT':STDMAT)
     C                   callp     wrtsection('Maalrow')
     C     BrParKey      reade     BRPARF                                 40
     C                   enddo
     c
     C                   callp     wrtsection('Maalbot')
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      wsCLID = zhbgetvar('CLID')
     C                   eval      wsuser = zhbgetvar('USER')
     C                   eval      wsPAAPALL  = zhbgetvar('PALLV')
     C                   eval      LBH        = zhbgetvar('LBH')
     C                   eval      STDLBH     = zhbgetvar('STDLBH')
     C                   eval      VKT        = zhbgetvar('VKT')
     C                   EVAL      VKTN   = c2n2(VKT)
     C                   move      VKTN          VKT
N01  C                   eval      LM         = zhbgetvar('LM')
N01  C                   EVAL      LMN    = c2n2(LM)
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSCS'
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('CLID':WSCLID)
     C                   callp     updhtmlvar('LBH':LBH)
     C                   callp     updhtmlvar('WSLEST':
     C                             %trim(%editc(WSLEST:'3')))
     C                   callp     updhtmlvar('WSPALL':
     C                             %trim(%editc(WSPALL:'3')))
     C                   MOVE      *BLANKS       WSPAAPALL
     C                   callp     updhtmlvar('PALLV':WSPAAPALL)
     C                   callp     updHTMLvar('FEILTXT':FEILMELDING)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
     C                   EVAL      MAALTXT = 'Kolli id:'
     C                   callp     updhtmlvar('KRAVVKT':KRAVVKT)
     C                   callp     updhtmlvar('KRAVMAL':KRAVMAL)
      *
     C                   endsr
     C***********************************************
     C     UPDFIL        BEGSR
     C***********************************************
      * 1. GANG - KOMMER INN MED BLANK
     C     WSCLID        CABEQ     *BLANKS       UTLES
      *
     C                   MOVEL     WSCLID        XWSCLID           2
     C     XWSCLID       IFNE      '00'
     C                   eval      FEILMELDING ='Strekkode '+WSCLID+' MÅ '+
     C                             'begynne med 00 (Kolli-ID)'
     C                   eval      FEILKODE = 'X'
     c                   goto      utles
     c                   end
      *
     C                   eval      Wkey2 = WSUSER + WSCLID
     C     KEY           CHAIN     WRKCGI                             33
     C     *IN33         IFEQ      *OFF
     c                   SELECT
     C     WSPAAPALL     WHENEQ    'X'
     C     WDsPALL       ANDNE     'X'
      * korreksjonsskyting,  pluss 1 pall
     C                   eval      FEILMELDING ='Kll '+WSCLID+' ER alt skutt, '+
     C                             'PALL PLUSS 1 (Mål justert)'
     C                   eval      FEILKODE = 'X'
     C     WSPAAPALL     WHENNE    'X'
     C     WDsPALL       ANDEQ     'X'
      * korreksjonsskyting,  minus 1 pall
     C                   eval      FEILMELDING ='Kll '+WSCLID+' ER alt skutt, '+
     C                             'PALL MINUS 1 (Mål justert)'
     C                   eval      FEILKODE = 'X'
     C                   OTHER
      * skutt før, ingen pallendrieng
     C                   eval      FEILMELDING ='Kll '+WSCLID+' ER alt skutt! '+
     C                             '(Mål justert)'
     C                   eval      FEILKODE = 'X'
     C                   ENDSL
     C                   MOVE      WSPAAPALL     WDsPALL
     C                   MOVE      LBH           WDsLBH
     C                   MOVE      VKT           WDsVKT
N01  C                   MOVE      LMN           WDsLM
     C                   UPDATE    WRKCGIR
     C                   ELSE
     C                   eval      Wkey2 = WSUSER + WSCLID
     C                   MOVE      WSPAAPALL     WDsPALL
     C                   TIME                    XTIME
     C                   MOVE      XDD           WDsDTDD
     C                   MOVE      XMM           WDsDTMM
     C                   MOVE      XÅÅ           WDsDTÅÅ
     C                   MOVE      XTID          WDsTID
     C                   MOVE      LBH           WDsLBH
     C                   MOVE      VKT           WDsVKT
     C                   WRITE     WRKCGIR
     C                   END
      *
     C                   MOVE      *BLANKS       LBH
      * Akkumuler lest/pall  på ny
      * Sjekk manko, overtallige og antall pall med gods
     C     UTLES         TAG
      *
     C                   MOVE      *ZEROS        WSLEST
     C                   MOVE      *ZEROS        WSPALL
     C                   MOVE      *BLANKS       WSCLID
     C                   eval      Wkey2 = WSUSER + WSCLID
     C     KEY           SETLL     WRKCGI
     C                   READ(N)   WRKCGI                                 50
     C     *in50         doweq     '0'
     C     WDsUser       CABNE     WSUSER        UtUpd
     C                   ADD       1             WSLEST
     C     WDsPALL       IFNE      ' '
     C                   ADD       1             WSPALL
     C                   ENDIF
     C                   READ(N)   WRKCGI                                 50
     C  N50              ENDDO                                                  *hival
     C     UtUpd         ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      * winteg alltid 0 / wkey2 = Userid+kolliID
     C     KEY           KLIST
     C                   KFLD                    WINTEG
     C                   KFLD                    WKEY2
      *  Også wrkcgi ligger i sycgi -
     C                   open      BRIDF
      * MEN chain BRIDF for å validere user..
     C     WSUSER        CHAIN     BRIDF                              33
     C     *in33         ifeq      *on
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'STS01B':'/CGITAG/')
     c                   move      *blanks       FeilUsr          50
     C                   eval      FeilUsr ='"'+%trim(WSUSER)+'" var ikke '+
     c                             ' rett pålogget!!!'
     C                   callp     updHTMLvar('FEILTXT':FeilUsr)
     C                   callp     updHTMLvar('FEILKODE':'1')
     C                   callp     wrtsection('all')
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C                   MOVE      *ON           *INLR
     C                   return
     c                   end
     C                   open      WRKCGI
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      BRPARF
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     c                   Eval      DFTUSER = '*KUNDFT*'+BIFIRM
     c                   move      DFTUSER       PABRID
     c                   move      'CSMAL'       PAID
      * Kan/Skal mål/vekt oppgis ?
     c                   move      'N'           KRAVVKT
     c                   move      'N'           KRAVMAL
     C     BrParKey      chain     BRPARF                             33
     c  n33              movel     PAVRDA        MAAL
     c     MAAL          ifne      'J'
     c     MAAL          andne     'N'
     c     MAAL          andne     'V'
     c     MAAL          andne     'M'
     c     MAAL          andne     'B'
     c                   move      'N'           MAAL
     c                   else
      * Hvis skal/kan - legg KRAV i egne variable..
     C                   select
     C     MAAL          wheneq    'B'
     c                   move      'Y'           KRAVVKT
     c                   move      'Y'           KRAVMAL
     C     MAAL          wheneq    'V'
     c                   move      'Y'           KRAVVKT
     C     MAAL          wheneq    'M'
     c                   move      'Y'           KRAVMAL
     C                   endsl
     c                   end
      *
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FEILKODE
      *
     C                   ENDSR
