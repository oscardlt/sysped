      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE030R MODULE(*LIBL/TSVE030R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          c                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D SYAVA           S              4
     D SYAV            S              4  0
     D SYOPA           S              7
     D SYOP            S              7  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d X               S             10  0
     d MyPoDS          ds                  Qualified  dim(100)
     d  SVEF_syav                     4S 0
     d  SVEF_syop                     7S 0
     d  SVEF_faty                     4
     d  SVEF_fatx                    35
     d  SVEF_vakd                     3
     d  SVEF_vaku                     7S 4
     d  SVEF_fabl                    11S 3
     d  SVEF_tuid                    10
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SYAV:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SYOP:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"invList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    SYAV > 0;
         Exsr  AddAnd;
         Eval  WhereC = ' SVEF_SYAV  = ' + SYAVA;
       EndIf;

       If    SYOP > 0;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SVEF_SYOP  = ' + SYOPA;
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select SVEF_SYAV, SVEF_SYOP, SVEF_FATY, ' +
                             'SVEF_FATX, SVEF_VAKD, SVEF_VAKU, ' +
                             'SVEF_FABL, SVEF_TUID ' +
                             'from SVEF ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'SVEF_FATX ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         syava   = zhbgetvar('AVD');
         syav    = c2n2(syava);
         syopa   = zhbgetvar('OPD');
         syop    = c2n2(syopa);
        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).SVEF_faty = *blanks;
         leave;
       endif;

           chain (mypods(x).SVEF_vakd) SVTVK;
           if not %found;
              svvk_omr = *zeros;
           endif;


        // Valider extern ref
           error=' ';

           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬svef_syav¬ : ¬'+%trim(%editc(mypods(x).SVEF_SYAV:'Z'))+'¬, '
          +'¬svef_syop¬ : ¬'+%trim(%editc(mypods(x).SVEF_SYOP:'Z'))+'¬, '
          +'¬svef_faty¬ : ¬'+%trim(mypods(x).SVEF_FATY)+'¬, '
          +'¬svef_fatx¬ : ¬'+%trim(mypods(x).SVEF_FATX)+'¬, '
          +'¬svef_vakd¬ : ¬'+%trim(mypods(x).SVEF_VAKD)+'¬, '
          +'¬svef_vaku¬ : ¬'+%trim(%editc(mypods(x).SVEF_VAKU:'4'))+'¬, '
          +'¬svef_omr¬ : ¬'+%trim(%editc(SVVK_omr:'4'))+'¬, '
          +'¬svef_fabl¬ : ¬'+%trim(%editc(mypods(x).SVEF_FABL:'4'))+'¬, '
          +'¬svef_tuid¬ : ¬'+%trim(mypods(x).SVEF_TUID)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         open svtvk;
         else;

           error = 'Invalid userID';
         endif;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close svtvk;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
