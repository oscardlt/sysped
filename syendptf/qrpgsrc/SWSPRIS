      *********************************************************************
      *  Webservice for spørring på kunderpris (FRAKT + øvrige)
      *             Bygger på SYFRA1 - online priskalkulator
      *********************************************************************
      *                                                                   *
CRTPG+*  CRTPGM SYSPED/SWSPRIS MODULE(*LIBL/SWSPRIS *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *                                                                   *
      *********************************************************************
      *  OBS!!!!
      *  Dette er blåkopi av SYFRA1 (farkt/priskalkulator interaktivt) med minimum av endringer
      *  Derfor ..masse av unødig logikk (f.es kunde BIKUNR  alltid overstytrt av SYSKNN)
      *  Webservicen burde med fordel trimmes totalt - skrelle vekk alt unødig
*****************4444***********************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF11 Nytt prog men basert på SYFRA1 - interaktiv frakt/priskalkulator
10XXX * 02 JOV PTF11 SYSKNN slå opp FØR (kan overstyres av)  evt avtalekundenr
10XXX * 03 JOV PTF11 for smått dimensjonerte ANT, VKT, M3
10XXX * 04 JOV PTF11 Varme/farlig-%-sats kan være overstyrt på kunde
********************************************************************
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     Fsted2d    IF   E           K DISK    USROPN
     FUSRDFT    IF   E           K DISK    USROPN
     FNOPNR     IF   E           K DISK    USROPN
     FFIRINL    IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FCUTRAN    IF   E           K DISK    USROPN
     FSIDUPA    UF A E           K DISK    USROPN
     FSIDUPB    UF A E           K DISK    USROPN
     FSIDUPC    UF A E           K DISK    USROPN
     FKODTV     IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
     FKODTGE    IF   E           K DISK    USROPN
     FKODTG2    IF   E           K DISK    USROPN
     FKODSAM    IF   E           K DISK    usropn
     FBRPARF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Sammenslåing av F-linjer baserer seg på SAMME varekode/tekst ELLER sammensl.kode/tekst
     D FLI             S             85    DIM(100)
     D string          s          32000
      * Client input variables
     D WBOPD           S              7  0
     D oTyp            S              2
     D WSTOTN          S             13  2
     D FRA             s              5
     D TIL             s              5
s03  D*ANT             s              5
s03  D*VKT             s              5
s03  D*M3              s              5
N03  D ANT             s              7
N03  D VKT             s              9
N03  D M3              s              8
     D LM              s              5
     D NumBig          s             19  9
     D VARME           s              1
     D FARLIG          s              1
     D LENGDE          s              1
     D PROD            s              1
     D AVDG            s             40
     D FRDG            s             20
     D TILTX           s             35
     D userEnv         s             10
     D SYSKNR          S              8
     D SYSKNN          S              8  0
     D SYSKNTXT        S            200
     D TJEN            s             50                                         Tekst/output eSped
     D Tjeneste        s             50                                         Input web-serw
     D OVERSKR         s            150
     D MinFr           s             50
     D SEMI            s              1
     D OljValTxt       s             50
     D OljValBel       s              9  2
     D SeIntChecked    s             10
     D SeIntern        s              1
     D SortSam         s              1
     D MKOD            s              1
     D VTXT            s             45
     D XN01            s              3  0
     D MaxXN           s              3  0
     D StrXN           s              3  0
     D Mbr             s             10
     D PriceType       s             20
     D FRABOOKING      s              1
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  ZSDS                   1    429
     D  ZJOB                 244    253
     D  ZUSER                254    263
      *
     D                 DS
     D  ST2UNI                 1      9
     D  AVGDG                  1      7
     D  AV                     1      7
     D                                     DIM(7)
     D  FRADG                  8      9
     D                 DS
     D  DGNAVN                 1     14    inz('MaTiOnToFrLøSø')
     D  DGN                    1     14
     D                                     DIM(7)
      *
     D                 DS
     D  FAVT                   1     20
     D    FAVT1                1      1
     D    FAVT2_20             2     20
     D                 DS
     D* GMDS                   1     85
     D  GMDS                   1     86
     D    GM_SORT              1      3
     D    GM_SAMM              5      5
     D    GM_VT                7     47
     D    GM_MK               49     49
     D      GMDS_A             1     49
     D    GM_VAL              51     53
     D    GM_BELV             55     67  2
     D    GM_BELN             69     81  2
     D    GM_VK               83     85
     D    GM_KDA              86     86
     D                 DS
     D* NYDS                   1     85
     D  NYDS                   1     86
     D    NY_SORT              1      3
     D    NY_SAMM              5      5
     D    NY_VT                7     47
     D    FAKDM               49     49
     D      NYDS_A             1     49
     D    FAVAL               51     53
     D    FABELV              55     67  2
     D    FABELN              69     81  2
     D    FAVK                83     85
     D    FAKDA               86     86
     D                 DS
     D  HXHEAD                 1    256
     D  XXUR                   1      1
     D  XXFR                   2      4
     D  XXKDPL                 5      5
     D  XXLKA                  6      7
     D  XXSDF                  8     12
     D  XXTRI                 13     14
     D  XXSDT                 15     19
     D  XXLKS                 20     21
     D  XXSDFF                22     26
     D  XXLKK                 27     28
     D  XXSDVT                29     33
     D  XXVKT                 34     42  0
     D  XXFBV                 43     51  0
     D  XXM3                  52     58  3
     D  XXLM                  59     62  2
     D  XXNT                  63     69  0
     D  XXVERV                70     72
     D  XXVERB                73     75  2
      *
     D                 DS
     D  HXHEADRet              1    256
     D  XXFBVRet              43     51  0
      *
     D                 DS
     D  WSAVD                  1      7
     D  WSMOD                  1      1
     D  WSMOD2                 2      2
     D  WSSK                   3      3
     D  WBAVD                  4      7  0
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      /free
         exsr Parse;
         exsr INIT;
       // hent html-mal
         gethtmlifs('/syspeddev/html/XML.html':'/CGITAG/');
       // for debug:
        AUTHTYPE=getenv('AUTH_TYPE':qusec);
        RMTUSER =getenv('REMOTE_USER':qusec);

       // skriv toppen
         wrtsection('top');

         string=
           '<?xml version="1.0" encoding="utf-8" standalone="yes"?>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         string=
           '<GetPriceResponse>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         exsr SetAll;

         if xKunFrakt='J';
           exsr visFrakt;
         else;
           exsr hentFakt;                                 // syfi10x/lag linjer
           exsr visFakt;                                  // skriv linjer
         endif;


         string=
           '</GetPriceResponse>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');


         exsr Exit;
         *inlr = *on;
         return;
      /end-free
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
     C                   clear                   hxhead
      * Parse variables from QUERY_STRING environment variable
     C                   eval      XXLKA   = zhbgetvar('FRALK')
     C                   eval      XXLKA   = uppify(XXLKA)
     C                   eval      XXSDF   = zhbgetvar('FRA')
     C                   eval      XXSDF   = uppify(XXSDF)
     C                   eval      XXTRI   = zhbgetvar('TILLK')
     C                   eval      XXTRI   = uppify(XXTRI)
     C                   eval      XXSDT   = zhbgetvar('TIL')
     C                   eval      XXSDT   = uppify(XXSDT)
     c     XXLKA         Ifeq      *blanks
     c                   move      'NO'          XXLKA
     c                   end
     c     XXTRI         Ifeq      *blanks
     c                   move      'NO'          XXTRI
     c                   end
     C                   eval      ANT     = zhbgetvar('ANT')
     C                   eval      XXNT    = c2n2(ANT)
     C                   eval      VKT     = zhbgetvar('VKT')
     C                   eval      XXVKT   = c2n2(VKT)
     C                   eval      M3      = zhbgetvar('M3')
     C                   eval      XXM3    = *zeros
     C                   eval      NumBig  = c2n2(M3)
     C     NumBig        ifle      9999                                         XXM3 = 7,3
     C                   eval      XXm3    = NumBig
     C                   end
     C                   eval      LM      = zhbgetvar('LM')
     C                   eval      XXLM    = *zeros
     C                   eval      NumBig  = c2n2(LM)
     C     NumBig        ifle      99                                           XXLM = 4,2
     C                   eval      XXLM    = NumBig
     C                   end
     C                   eval      XXKDPL  = zhbgetvar('PROD')
     C                   eval      XXFR    = zhbgetvar('FRAN')
      * OBS!!!! HARDKODET  Tjenestetype = kobling til avd /modul/imp/eksp mm
      *         ved interaktiv kalkulator er dette parameter HEUR i eSped pr user
      * Gjort slik for å kunne gjenbruke i webservice, samtidig forenklerparameter i webservice
     C                   eval      Tjeneste= zhbgetvar('Tjeneste')
     C                   select
     C     Tjeneste      wheneq    'RoadDomestic'
     C                   eval      WSAVD   = 'HDS0080'
     C     Tjeneste      wheneq    'RoadImport'
     C                   eval      WSAVD   = 'HIK0001'
     C     Tjeneste      wheneq    'RoadExport'
     C                   eval      WSAVD   = 'HES0002'
     C     Tjeneste      wheneq    'AirImport'
     C                   eval      WSAVD   = 'CIK0001'
     C     Tjeneste      wheneq    'AirExport'
     C                   eval      WSAVD   = 'DES0002'
     C     Tjeneste      wheneq    'SeaImport'
     C                   eval      WSAVD   = 'FIK0001'
     C     Tjeneste      wheneq    'SeaExport'
     C                   eval      WSAVD   = 'GES0002'
     c                   other
     C                   eval      WSAVD   = 'H S0001'
     c                   endsl
      * 4 siste av wsavd er avdeling -WBAVD -sikre at det er numerisk
     c                   move      wsavd         Tstnum4           4
     C                   eval      WBAVD   = c2n2(Tstnum4)
     C                   eval      oTyp    = zhbgetvar('oTyp')
     C                   eval      FARLIG  = zhbgetvar('FARLIG')
     C                   eval      VARME   = zhbgetvar('VARME')
     C                   eval      LENGDE  = zhbgetvar('LENGDE')
     C                   eval      SYSKNR  = zhbgetvar('SYSKNR')
     C                   eval      SYSKNN  = c2n2(SYSKNR)
      *
      * UserEnvironment (dataasemiljø)
     C                   eval      userEnv = zhbgetvar('userEnv')
Nxx  C                   eval      userEnv = uppify(userEnv)
     C                   move      'J'           SortSam
      * legg System Data Structure i DS
     C                   move      PSDSDATA      Zsds
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     Sted2d
     C                   CLOSE     USRDFT
     C                   CLOSE     NOPNR
     C                   CLOSE     FIRM
     C                   CLOSE     FIRINL
     C                   CLOSE     CUTRAN
     C                   CLOSE     SIDUPA
     C                   CLOSE     SIDUPB
     C                   CLOSE     SIDUPC
     C                   CLOSE     KODTV
     C                   CLOSE     KODTA
     C                   CLOSE     KODTGE
     C                   CLOSE     KODTG2
     C                   CLOSE     KODSAM
     C                   CLOSE     CUNDL01
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$all
      *=====================================================================
     C     SetAll        begsr
      *
     C                   MOVE      BIKUNR        XXKN              8 0
s02  c     SYSKNN        ifne      *zeros
s02  C                   MOVE      SYSKNN        XXKN
s02  c                   end
     C     CUNDL01K      CHAIN     CUNDL01                            33        avt.kund på BIKUNR?
     C  N33              IF        AKNRKU <> 0
     C                   MOVE      AKNRKU        XXKN
     C                   END
s02  c*    SYSKNN        ifne      *zeros
s02  C*                  MOVE      SYSKNN        XXKN
s02  c*                  end
      *
      * ved "Kun fraktkalk" eller ved Tr-modul beregnes frakten i forkant
     c     WSMOD         ifne      ' '
     c     WSMOD         andne     'H'
     c                   goto      fullberegn
     c                   end
      * Beregne frakt...
      * 1. beregne ren brutto
     C                   MOVE      *ZEROS        UUKN
     C                   EXSR      BRT
     C                   MOVE      UUVAL         XXVALBRT          3
     C                   Z-ADD(H)  UUBELØ        BRUTTO           13 0
      *
      * 2. beregne ren avtale
     C                   Z-ADD     XXKN          UUKN              8 0
     C                   EXSR      AVT
     C                   Z-ADD(H)  UUBELØ        NETTO            13 0
      *
      * 3 Hvis ren avtale ikke funnet prøv %% av brutto
     C     NETTO         IFEQ      *ZEROS
     C                   Z-ADD     XXKN          UUKN              8 0
     C                   EXSR      BRT
     C                   Z-ADD(H)  UUBELØ        NETTO            13 0
     C                   END
      *
     C     XKDA          IFEQ      'A'
     C                   EVAL      PRICETYPE = 'AgreementPrice'
     C                   ELSE
     C                   EVAL      PRICETYPE = 'ListPrice'
     C                   ENDIF
      *
      * 25 % påslag for farlig gods / varmegods?
     C     XXLKA         IFEQ      'NO'
     C     XXTRI         andeq     'NO'
     C                   Move      IFFARP        XXFARP            4 3
     C                   Move      IFVARP        XXVARP            4 3
     C                   Move      IFLENP        XXLENP            4 3
     C                   Move      IFFARP        XXFARPB           3 1
     C                   Move      IFVARP        XXVARPB           3 1
     C                   Move      IFLENP        XXLENPB           3 1
     C     XXKN          Chain     CUTRAN                             33
     C     *in33         Ifeq      *OFF
     C     CTIKFA        Ifeq      'X'
     C                   Move      *ZEROS        XXFARP
N04  C                   else
N04  C     CTAVFA        Ifne      *zeros
N04  C                   Move      CTAVFA        XXFARP
N04  C                   end
     C                   end
     C     CTIKVA        Ifeq      'X'
     C                   Move      *ZEROS        XXVARP
N04  C                   else
N04  C     CTAVVA        Ifne      *zeros
N04  C                   Move      CTAVVA        XXVARP
N04  C                   end
     C                   end
     C     CTIKLG        Ifeq      'X'
     C                   Move      *ZEROS        XXLENP
     C                   else
     C     CTAVLP        Ifne      *zeros
     C                   Move      CTAVLP        XXLENP
     C                   end
     C                   end
     C                   end
     C     NETTO         mult(H)   XXFARP        NETT25Fa         13 0
     C     BRUTTO        mult(H)   XXFARPB       BRUTT25Fa        13 0
     C     NETTO         mult(H)   XXVARP        NETT25Va         13 0
     C     BRUTTO        mult(H)   XXVARPB       BRUTT25Va        13 0
     C     NETTO         mult(H)   XXLENP        NETT25Le         13 0
     C     BRUTTO        mult(H)   XXLENPB       BRUTT25Le        13 0
      * Farlig gods
     C     FARLIG        IFEQ      'Y'
     C     XXFARP        ANDNE     *ZEROS
     C                   ADD       NETT25Fa      NETTO
     C                   ADD       BRUTT25Fa     BRUTTO
     C                   END
      * Varmegods
     C     VARME         IFEQ      'Y'
     C     XXVARP        ANDNE     *ZEROS
     C                   ADD       NETT25Va      NETTO
     C                   ADD       BRUTT25Va     BRUTTO
     C                   END
      * Lengdegods
     C     LENGDE        IFEQ      'Y'
     C     XXLENP        ANDNE     *ZEROS
     C                   ADD       NETT25Le      NETTO
     C                   ADD       BRUTT25Le     BRUTTO
     C                   END
      *
      * Minstefrakt kunde  (Ingen egen avtale-> lik brutto)
     C                   eval      MinFr=*blanks
     c     IFFJKO        ifne      ' '
     c                   movel     USONE         TstFjK            1
     c     TstFjK        ifeq      IFFJKO
     C                   move      CTAFMF        CTAVFM                         Kunde avv min fjern
     C                   move      CTIFMF        CTIKFM                         Kunde ikke min fjer
     C     CTAVFM        IFEQ      *ZEROS
     C                   z-add     IFMIFF        CTAVFM                         Firma Minste fjern.
     C                   END
     C                   END
     C                   END
     C     CTAVFM        IFEQ      *ZEROS
     C                   z-add     IFMIFR        CTAVFM
     C                   END
     C     CTAVFM        Ifne      *zeros
     C     CTIKFM        andne     'X'
     C     XXKDPL        andeq     ' '
     C     NETTO         andgt     *zeros
     C     NETTO         andlt     CTAVFM
     C                   z-add     CTAVFM        NETTO
     C                   eval      MinFr='Minimumsfrakt:  ' +
     C                             %trim(%editc(NETTO:'Z')) +
     C                             spaces+spaces
     C                   END
      * Minstefrakt brutto
     C     IFMIFR        Ifne      *zeros
     C     XXKDPL        andeq     ' '
     C     BRUTTO        andgt     *zeros
     C     BRUTTO        andlt     IFMIFR
     C                   z-add     IFMIFR        BRUTTO
     C                   END
      *
     C                   END
      *olje/valuta-tillegg utfra felt i oppdragsbildet  (parametersettes, også HEUR ??)
     C                   IF        ((XKUNFRAKT = 'J') or  (XKUNFRAKT = 'j'))
     c                   move      'J'           VisOljVal         1
     c                   endif
     c                   clear                   OljValTxt
     c                   clear                   OljValBel
     C     VisOljVal     IFEQ      'J'
     C                   MOVE      'H'           UUR
     C                   MOVE      *ZEROS        UVALP
     C                   CALL      'SYFA24B'
     C                   PARM                    UUR               1
     C                   PARM                    UAVDOP            4 0
     C                   PARM      XXKN          UKNOP             8 0
     C                   PARM                    WSVALT            3            Geb.kode
     C                   PARM                    UVALP             3 1          %-sats
     c     UVALP         ifne      *Zeros
     c                   move      WSVALT        FAVK
     C     KGEKey        CHAIN     KODTGE                             33
     c     *in33         ifeq      '0'
     c                   Eval      OljValBel = (Netto*Uvalp/100) + 0.5
     C                   move      00            OljValBel
     c                   eval      Netto = Netto + OljValbel
     c                   eval      OljValTxt = '(Inkl '
     C                             + %trim(%editc(UVALP:'2'))+' % '
     c                             + %trim(KGENOT)+'.  ( '
     C                             + %trim(%editc(OljValBel:'L'))+' ) )'
     c                   end
     c                   end
     c                   END
      * Tilsteds NAVN, Avgangsdg(1-7) + framføringstid(8-9) i ZIP-kode(9)
     C                   MOVE      *BLANKS       ST2NVN
     C                   MOVE      *BLANKS       AVDG
     C                   MOVE      *BLANKS       FRDG
      * ved ren innlandsfrakt KAN NOPNR være brkt
     C     XXLKA         IFEQ      'NO'
     C     XXTRI         andeq     'NO'
     C                   movel     XXSDT         TSTPNR            4
     C     TSTPNR        IFge      '0000'
     C     TSTPNR        andle     '9999'
     C     zuser         CHAIN     usrdft                             33
     C                   move      TSTPNR        NOPONR
     C     InnlKey       CHAIN     NOPNR                              33
     C     *IN33         IFEQ      *OFF
     C                   moveL     NONAVN        ST2NVN
     C                   move      NOAVGD        AVGDG
     C                   move      NOFRFT        FRADG
     c     NOFRFT        IFeq      *zeros
     C                   move      *blanks       FRADG
     C                   else
     c     NOFRFT        IFlt      10
     C                   movel     ' '           FRADG
     C                   END
     C                   END
     C                   goto      PnrOk
     C                   END
     C                   END
     c                   end
      *
      *
     c     FullBeregn    tag
      *
     C     TILKEY        CHAIN     STED2D                             33
     C     *IN33         IFEQ      *OFF
     C     PnrOk         tag                                                    navn/dgr fra NOPNR
     C     AVGDG         IFNE      *BLANKS
     C                   EXSR      KONVDG
     C                   END
     C     FRADG         IFNE      *BLANKS
     C                   Eval      FRDG = 'Ant. framf.dg = '+FRADG
     C                   END
     C                   END
     C                   Eval      TILTX = 'To: '+XXTRI+' '+XXSDT+' '+ST2NVN
     C* klargjøre HTMLvariable
     C                   callp     updHTMLvar('FRALK':XXLKA)
     C                   callp     updHTMLvar('FRA':XXSDF)
      * Noen svardata dormateres ulikt ved url-forespørsel...
     C                   callp     updHTMLvar('TILLK':XXTRI)
     C                   callp     updHTMLvar('TIL':XXSDT)
     C                   callp     updHTMLvar('ANT':
     C                             %trim(%editc(XXNT:'Z')))
     C                   callp     updHTMLvar('VKT':
     C                             %trim(%editc(XXVKT:'Z')))
     C                   callp     updHTMLvar('M3':
     C                             %trim(%editc(XXM3:'2')))
     C                   callp     updHTMLvar('LM':
     C                             %trim(%editc(XXLM:'2')))
     C                   IF        XXFBVRet = 0
     C                   z-add     XXVKT         HEFBV
     C                   ELSE
     C                   z-add     XXFBVRet      HEFBV
     C                   END
      *
     C                   callp     updHTMLvar('PROD':XXKDPL)
     C                   callp     updHTMLvar('VARME':VARME)
     C                   callp     updHTMLvar('FARLIG':FARLIG)
     C                   callp     updHTMLvar('LENGDE':LENGDE)
     C     VARME         IFEQ      'Y'
     C                   callp     updHTMLvar('VARMTXT':'Varmegods')
     C                   ELSE
     C                   callp     updHTMLvar('VARMTXT':spaces)
     C                   END
     C     FARLIG        IFEQ      'Y'
     C                   callp     updHTMLvar('FARLTXT':'Farlig gods')
     C                   ELSE
     C                   callp     updHTMLvar('FARLTXT':spaces)
     C                   END
     C     LENGDE        IFEQ      'Y'
     C                   callp     updHTMLvar('LENGTXT':'Lengdegods')
     C                   ELSE
     C                   callp     updHTMLvar('LENGTXT':spaces)
     C                   END
      *
     C                   callp     updHTMLvar('NETTO':
     C                             %trim(%editc(NETTO:'L')))
     C                   callp     updHTMLvar('BRUTTO':
     C                             %trim(%editc(BRUTTO:'L')))
     C                   callp     updHTMLvar('OljValTxt':OljValTxt)
     C                   callp     updHTMLvar('AVDG':AVGDG)
     C                   callp     updHTMLvar('FRDG':FRADG)
      *
     C                   callp     updHTMLvar('MINFR':minfr)
     C                   callp     updHTMLvar('USERENV':USERENV)
     C                   callp     updHTMLvar('WSAVD':WSAVD)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHtmlVar('SYSKNR':
     C                             %trim(%editc(SYSKNN:'Z')))
      *
      *
      * sett overskrift etter kun frakt/full (for å kunne fjerne mange sections...)
     C                   eval      Overskr='<div class="centeredtitle">e-SPED'
     C                             +'   -  FRAKTKALKULATOR. </div>'
      * Ved full beregning - lag oppdrag i SIDUPB (ovverride HEADF..)
      * (må lages FØR write øvre del av skjermbildet for å få med F.vekt mm)
     C                   IF        ((XKUNFRAKT <> 'J') AND (XKUNFRAKT <>'j'))
     C                   eval      Overskr='<div class="centeredtitle">e-SPED'
     C                             +'   -  FULL FAKTURABEREGNING </div>'
     c                   exsr      SiDupOpd
     C                   END
      *
     C                   callp     updHTMLvar('FBV':
     C                             %trim(%editc(HEFBV:'Z')))
     C                   callp     updHTMLvar('OVERSKR':Overskr)
      *
     C                   endsr
      *
      ***********************************************************************
     C     SiDupOpd      begsr
      ***********************************************************************
     c                   move      'J'           POSTAL            1
     C     KodtaKey      CHAIN     KODTA                              52
     C  N52              move      KOAPOS        POSTAL
     C     WBAVD         CHAIN     SIDUPC                             52
     C     *IN52         IFEQ      '0'
     C                   MOVE      TEOPDN        WBOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   ELSE
     C                   Z-ADD     WBAVD         TEAVD
     C                   Z-ADD     1             TEOPDN
     C                   MOVE      TEOPDN        WBOPD
     C                   ADD       1             TEOPDN
     C                   WRITE     TELLR
     C                   END
      * fjern evt gamle..
     C     SIDUPAK       CHAIN     SIDUPA                             33
     C  N33              DELETE    HEADFR
     C     SIDUPAK       SETLL     SIDUPB
     C     SIDUPAK       READE     SIDUPB                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    FAKTR
     C     SIDUPAK       READE     SIDUPB                                 33
     C                   ENDDO
      *
     C                   EVAL      HEAVD  = WBAVD
     C                   EVAL      HEOPD  = WBOPD
     C                   MOVE      XXTRI         HELKK
     C                   EVAL      HELKS  = XXLKA                               FRA
     C                   EVAL      HELKK  = XXTRI                               TIL
     C                   SELECT
     C     WSMOD         WHENEQ    'H'
     C                   EVAL      HELKA  = XXLKA
     C                   EVAL      HETRI  = XXTRI
     C     WSMOD         WHENEQ    'A'
     C     WSMOD         OREQ      'C'
     C     WSMOD         OREQ      'E'
     C     WSMOD         OREQ      'F'
     C                   EVAL      HELKA  = XXLKA
     C                   OTHER
     C                   EVAL      HELKA  = XXTRI
     C                   ENDSL
     C                   EVAL      HEUR   = WSMOD
     C                   EVAL      HESG   = 'WEB'
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           HEDTR
     C                   MOVE      WDT           HEDTOP
     C                   MOVE      XXSDF         HESDF
     C                   MOVE      XXSDT         HESDT
     c     HEUR          ifne      'C'
     c     HEUR          andne     'D'
     c     HEUR          andne     'E'
     c     HEUR          andne     'F'
     c     HEUR          andne     'G'
     c     HEUR          andne     'H'
     c     POSTAL        andeq     'J'
      * til ER alt chainet
     c                   move      ST2KO2        HESDT
     C     FRAKEY        CHAIN     STED2D                             33
     c                   move      ST2KO2        HESDF
     C                   MOVE      XXSDF         HESDFF
     C                   MOVE      XXSDT         HESDVT
     c                   end
      *
     C                   IF        WSSK = 'S'
     C                   MOVE      XXKN          HEKNS
     C                   MOVE      XXKN          HEKNSF
     C                   MOVE      ' '           HEKDFS
     C                   MOVE      UUVAL         HEVALS
     C                   MOVE      *BLANKS       HENAS
     C                   MOVE      'X'           HEKDFK
     C                   ELSE
     C                   MOVE      XXKN          HEKNK
     C                   MOVE      XXKN          HEKNKF
     C                   MOVE      ' '           HEKDFK
     C                   MOVE      UUVAL         HEVALK
     C                   MOVE      *BLANKS       HENAK
     C                   MOVE      'X'           HEKDFS
     C                   END
      *
     C                   EVAL      HEKDPL = XXKDPL
     C                   MOVE      oTyp          HEOT
     C                   MOVE      XXFR          HEFR
     C                   MOVE      XXNT          HENT
     C                   MOVE      XXVKT         HEVKT
     C                   MOVE      XXLM          HELM
     C                   MOVE      XXM3          HEM3
     C     KODTVK        CHAIN     KODTV                              33
     C     HELM          MULT      KOVLKG        XFBV1             9 0
     C     HEM3          MULT      KOVKKG        XFBV2             9 0
     C     HEVKT         IFGE      XFBV1
     C                   Z-ADD     HEVKT         HEFBV
     C                   ELSE
     C                   Z-ADD     XFBV1         HEFBV
     C                   END
     C     HEFBV         IFLT      XFBV2
     C                   Z-ADD     XFBV2         HEFBV
     C                   END
     C     XXFBVRet      ifne      *zeros
     C                   z-add     XXFBVRet      HEFBV
     C                   end
     C                   MOVE      XXVALBRT      TRFRAV
     C                   IF        NETTO = 0
     C                   Z-ADD     BRUTTO        TRFRAB
     C                   ELSE
     C                   Z-ADD     NETTO         TRFRAB
     C                   END
      *
     C                   CALL      'SYFA24B'
     C                   PARM                    HEUR
     C                   PARM      WBAVD         UAVD              4 0
     C                   PARM                    XXKN
     C                   PARM                    HEVALT
     C                   PARM                    HEVALP
      *
     C                   WRITE     HEADFR
      *
      *NETTO ulik 0 KUN ved TR-modul.. - i så fall legg ut evt fakt på Linje 0 og val/oil på 1
     C                   IF        NETTO <> 0
     C                   EVAL      FAAVD  = WBAVD
     C                   EVAL      FAOPD  = WBOPD
     C                   MOVE      *ZEROS        FALI
     C                   EVAL      FASK   = WSSK
     C                   EVAL      FAVK   = 'FRA'
     C                   EVAL      FAVAL  = UUVAL
     C                   MOVE      XXKN          FAKUNR
     C                   EVAL      FAKDM  = 'J'
     C**                 EVAL      FAVT   = 'NETTO FRAKT'
     C                   EVAL      FABELV = NETTO
     C                   EVAL      FABELN = NETTO
     C                   MOVE      XKDA          FAKDA
     C                   WRITE     FAKTR
      * olj/val-tillegg HER kun når frakt alt er gitt (TR-modul)
     C                   IF        HEVALT <> *BLANKS
     C                   ADD       1             FALI
     C                   EVAL      FAVK   = HEVALT
     C                   EVAL      FAVT   = *BLANKS
     C                   EVAL      FABELV = NETTO * HEVALP
     C                   EVAL      FABELN = FABELV
     C                   add       0.50          FABELN
     C                   move      00            FABELN
     C                   MOVE      XKDA          FAKDA
     C                   WRITE     FAKTR
     C                   END
     C                   END
      *
      * kunne med fordel være mottatt som parameter...
     c                   select
     c     HEUR          wheneq    'A'
     c                   eval      TJEN = 'Land import'
     c     HEUR          wheneq    'B'
     c                   eval      TJEN = 'Land eksport'
     c     HEUR          wheneq    'C'
     c                   eval      TJEN = 'Fly import'
     c     HEUR          wheneq    'D'
     c                   eval      TJEN = 'Fly eksport'
     c     HEUR          wheneq    'E'
     c                   eval      TJEN = 'Fly innland'
     c     HEUR          wheneq    'F'
     c                   eval      TJEN = 'Sjø import'
     c     HEUR          wheneq    'G'
     c                   eval      TJEN = 'Sjø eksport'
     c     HEUR          wheneq    'H'
     c                   eval      TJEN = 'Transp. innland'
     c     WSMOD2        ifeq      'I'
     c                   eval      TJEN = 'Transp. import'
     C                   else
     c     WSMOD2        ifeq      'E'
     c                   eval      TJEN = 'Transp. eksport'
     C                   end
     C                   end
     C                   endsl
     c                   eval      TJEN=%trim(TJEN)+', avd '
     c                             +%trim(%editc(WBAVD:'Z'))
     C                   callp     updHTMLvar('TJEN':TJEN)
     C                   callp     updHTMLvar('HEOT':HEOT)
     C                   callp     updHTMLvar('HEFR':HEFR)
      *
     C                   endsr
      *=====================================================================******
      *  HENT FAKTURA
      *=====================================================================******
     C     HENTFAKT      begsr
      *
     C                   MOVE      'NY'          UNYGML            2
     C                   MOVE      *BLANKS       UMELD             1
     C                   Z-ADD     9             UFLAGG            1 0
     C                   Z-ADD     0             UKFRA             7 0
     C                   MOVE      'FR'          WFRUNI            2
     C                   MOVE      'S'           UKODE             1
     C                   MOVE      '*SECUR'      USECUR
     C                   CALL      'SYFI10X'
     C                   PARM                    BIFIRM                         FIRMAKODE
     C                   PARM                    HEAVD                          AVDELING
     C                   PARM                    HEOPD                          OPDNR
     C                   PARM                    UNYGML                         NY=N GML=G
     C                   PARM                    KO2UTL            1            UTL PROV
     C                   PARM                    XKUTL             4 2          UTLP SATS
     C                   PARM                    XSUTL             4 2          UTLP SATS
     C                   PARM                    XKUMIN            2 0          UTLP MIN
     C                   PARM                    XSUMIN            2 0          UTLP MIN
     C                   PARM                    KO2MVK            1            MVA KJØPER
     C                   PARM                    KO2MVS            1            MVA SELGER
     C                   PARM                    UKFRA                          =0
     C                   PARM                    KOVAVR            1            OVERST.AVRUN
     C                   PARM                    UMSG             62            DS
     C                   PARM                    UMELD                          =*BLANK
     C                   PARM                    UFLAGG                         =0
     C                   PARM                    UKODE             1
     C     USECUR        PARM      USECUR        USECUR            6            =*SECUR
      *
     C                   endsr
      *
      *=====================================================================******
      *  VIS FAKTURA
      *=====================================================================******
     C     VISFAKT       begsr
      *
     C     SIDUPAK       CHAIN     SIDUPA                             33
     C  N33              DELETE    HEADFR
      * skriv ut
     C     SIDUPAK       SETLL     SIDUPB
     C     SIDUPAK       READE     SIDUPB                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    FAKTR
      * klargjøring av linje flyttet ut i egen SR Setrow
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     SortSam       ifeq      'J'
     C                   exsr      SortSamSR
     c                   else
     C                   exsr      Setrow
     C                   callp     wrtsection('tablerow')
     c                   end
     C     SIDUPAK       READE     SIDUPB                                 33
     C                   ENDDO
      *
      * Hent sortert/sammenslått faktura fra array...
     c     SortSam       ifeq      'J'
     c                   sorta     FLI
     c                   eval      strXn = 100 - MaxXn + 1                      5 lin: elem 96->
     C     strXN         DO        100           XN01
     C     FLI(XN01)     IFNE      *BLANKS
     C                   MOVE      FLI(XN01)     NYDS
     C                   exsr      Setrow
     C                   callp     wrtsection('tablerow')
     C                   END
     C                   ENDDO
     c                   end
      *
     C                   callp     updHTMLvar('WSTOTN':
     C                             %trim(%editc(WSTOTN:'L')))
     C                   callp     wrtsection('tableend')
      *
     C                   endsr
      *******************************************
     C     VisFrakt      begsr
      *******************************************
      /free
           string=
             '<PriceLine>';
           updHTMLvar2('string':%addr(string):%len(string));
           wrtsection('lin');

           string=
                    '<FeeCode>'   +  'FRA'                       +'</FeeCode>'
              +     '<FeeText>'   +  'FRAKT'                     +'</FeeText>'
              +     '<Currency>'  +  FICURR                      +'</Currency>'
              +     '<Amount>'    + %trim(%editc(NETTO:'L'))     +'</Amount>'
              +     '<VATcode>'   +  'J'                         +'</VATcode>'
              +     '<PriceType>' + %trim(PRICETYPE)            +'</PriceType>';
           updHTMLvar2('string':%addr(string):%len(string));
           wrtsection('lin');

           string=
             '</PriceLine>';
           updHTMLvar2('string':%addr(string):%len(string));
           wrtsection('lin');
      /end-free
     C                   endsr

      *
      ***********************************************************
     C     SortSamSR     begsr
      ***********************************************************
      *
      * sort/sammenslå
      * kriterium for sammenslåing  - IKKE FRITEKST
      *            A)  SAMME GEBYRKODE       / FRITEKST  / momskode,
      *  ELLER     B)  SAMME SAMMENSNL.KODE  / FRITEKST  / momskode,
      * SAMMENSLÅTTE LINJER KOMMER FØRST
      * DERETTER LINJER  MED SAM-KODE MEN SOM IKKES SLÅS SAMMEN PGA FRITEKST
      * DERETTER ANDRE LINJER BASERT PÅ SORT-KODE
      *
      * a) klargjør noen variable
      * varetekst
     C     KG2Key        CHAIN     KODTG2                             33
     C     KGEKey        CHAIN     KODTGE                             33
     C                   IF        ((FAVK <> 'FRA') AND (FAVT = 'NETTO FRAKT'))
     C                   MOVE      *BLANKS       FAVT
     C                   END
     C                   eval      NY_VT = KGENOT+' '+FAVT
     c     FAVT1         ifeq      'X'
     C                   eval      NY_VT =  FAVT2_20
     c                   end
      * Sort-nr/samkod(+evt tekst derfra.)
     C                   MOVE      KG2SRT        NY_SORT
     C                   MOVE      FACD11        NY_SAMM
     C     NY_SAMM       IFNE      *BLANKS
     C     FAVT          IFNE      *BLANKS
     C                   MOVE      '  1'         NY_SORT
     C                   MOVE      *BLANKS       NY_SAMM
     C                   else
     C                   MOVE      '  2'         NY_SORT
     C     NY_SAMM       CHAIN     KODSAM                             33
     C                   eval      NY_VT = SANORS
     C                   END
     C                   END
      *
      * b) slå sammen eller legg i første ledige
     C     1             DO        100           XN01
     C     FLI(XN01)     IFNE      *BLANKS
     C                   MOVE      FLI(XN01)     GMDS
     C     NYDS_A        IFEQ      GMDS_A
     C                   ADD       FABELV        GM_BELV
     C                   ADD       FABELN        GM_BELN
     C                   move      *blanks       GM_VK                          sammenslått
     C                   MOVE      GMDS          FLI(XN01)
     C                   LEAVE
     C                   END
     C                   ELSE
     C                   MOVE      NYDS          FLI(XN01)
     c                   z-add     XN01          MaxXN
     C                   LEAVE
     C                   END
     C                   ENDDO
      *
     C                   endsr
      ********************************************************************
     c     SetRow        begsr
      ********************************************************************
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
      * Ved kundeview-sortert/sammenslått så vises avvikende tekst.
     c     SortSam       ifeq      'J'
     C                   eval      VTXT = NY_VT
     c                   else
     C     KGEKey        CHAIN     KODTGE                             33
     C   33              eval      KGENOT = '*Ukjent'
     C                   eval      VTXT = KGENOT + FAVT
     c                   end
     c     SortSam       ifne      'J'
     c     FACD11        andne     ' '
     C                   eval      VTXT = %trim(VTXT)+'  ('+FACD11+')'
     C                   end
     C                   callp     updHTMLvar('WSGKD':FAVK)
     C                   callp     updhtmlvar('WSGTXT':VTXT)
     C                   callp     updHTMLvar('WSVAL':FAVAL)
     C                   callp     updHTMLvar('WSBEL':
     C                             %trim(%editc(FABELV:'L')))
     C                   callp     updHTMLvar('WSBELN':
     C                             %trim(%editc(FABELN:'L')))
     C                   callp     updHTMLvar('WSMVA':FAKDM)
     C     FAKDA         IFEQ      'A'
     C                   EVAL      PRICETYPE = 'AgreementPrice'
     C                   ELSE
     C                   EVAL      PRICETYPE = 'ListPrice'
     C                   ENDIF
      /free
        string=
        '<PriceLine>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');

        string=
                    '<FeeCode>'   +  %trim(FAVK)                 +'</FeeCode>'
              +     '<FeeText>'   +  %trim(VTXT)                 +'</FeeText>'
              +     '<Currency>'  +  FICURR                      +'</Currency>'
              +     '<Amount>'    + %trim(%editc(FABELN:'L'))     +'</Amount>'
              +     '<VATcode>'   +  FAKDM                       +'</VATcode>'
              +     '<PriceType>' + %trim(PRICETYPE)            +'</PriceType>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');

        string=
        '</PriceLine>';
        updHTMLvar2('string':%addr(string):%len(string));
        wrtsection('lin');
      /end-free
     C                   ADD       FABELN        WSTOTN
     c                   endsr
      *=====================================================================******
      *  Konverter dagnr 123.. til Ma Ti On ....
      *=====================================================================******
     C     KONVDG        begsr
      *
     C*                  Eval      AVDG = 'Avg.dg: '+AVGDG+'(1=Man,2=Tir,3=O..)'
     C                   Eval      AVDG = 'Avg.dg: '
     C     1             DO        7             NR                1 0
     C     AV(NR)        IFGE      '1'
     C     AV(NR)        ANDLE     '7'
     C                   MOVE      AV(NR)        NN                1 0
     C                   CAT       DGN(NN):1     AVDG
     C                   END
     C                   END
      *
     C                   endsr
      *
      *=====================================================================******
      *  BRUTTO rate
      *=====================================================================******
     C     BRT           begsr
     C                   MOVE      'B'           XKDA              1
     C                   MOVE      *ZEROS        XXFBV
     C                   MOVE      *ZEROS        NAVD
     C                   MOVE      *ZEROS        N5
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      *BLANKS       USONE
     C                   MOVE      *BLANKS       UUVAL
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      'FRA'         UGEB
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE13'
     C                   PARM                    UGEB
     C                   PARM                    NAVD
     C                   PARM                    NOPD
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UUKN
     C                   PARM                    UHEAD
     C                   PARM                    atdum             1
     C                   MOVE      UHEAD         HXHEADRet
     C     UUBELØ        IFNE      *ZEROS
     C     URABAT        ANDNE     *ZEROS
     C                   MOVE      'A'           XKDA
     C                   END
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVTALE
      *=====================================================================******
     C     AVT           begsr
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      'FRA'         UGEB              3
     C                   MOVE      'FRA'         UGEBKO            3
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE14'
     C                   PARM                    UGEBKO
     C                   PARM                    UGEB
     C                   PARM                    NAVD
     C                   PARM                    NOPD
     C                   PARM                    UUKN
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    UAVNR             9 0
     C                   PARM                    UAVMSG           50
     C                   PARM                    USKA
     C                   PARM                    UHEAD
     C                   MOVE      UHEAD         HXHEADRet
     C     UUBELØ        IFNE      *ZEROS
     C                   MOVE      'A'           XKDA
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   EVAL      RC = DOCMD('OVRDBF FILE(BRIDF) +
     C                             TOFILE(SYSPED/BRIDF) SECURE(*YES)')
     C                   OPEN      BRIDF
     C     USERENV       CHAIN     BRIDF                              50
      * Ugyldig userID  - (noen ganger blank...)  hidre CPF / sett standard
     c     *in50         ifeq      '1'
     C                   callp     gethtml('HTMLSRC':'*LIBL':'ERRUSR':
     c                             '/CGITAG/')
     C                   callp     updhtmlvar('User':USERENV)
     C                   callp     wrtsection('all')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   eval      *inlr = *on
     C                   return
     c                   endif
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      Sted2d
     C                   OPEN      USRDFT
     C                   OPEN      NOPNR
     C                   OPEN      FIRM
     C                   OPEN      FIRINL
     C                   OPEN      CUTRAN
     C                   OPEN      KODTV
     C                   OPEN      KODTA
     C                   OPEN      KODTGE
     C                   OPEN      KODTG2
     C                   OPEN      KODSAM
     C                   OPEN      BRPARF
     C                   OPEN      SIDUPA
     C                   OPEN      SIDUPB
     C                   OPEN      SIDUPC
     C                   OPEN      CUNDL01
      * Key for Sted2d
     C     FRAKEY        KLIST
     C                   KFLD                    XXLKA
     C                   KFLD                    XXSDF
     C     TILKEY        KLIST
     C                   KFLD                    XXTRI
     C                   KFLD                    XXSDT
     C     SIDUPAK       KLIST
     C                   KFLD                    WBAVD
     C                   KFLD                    WBOPD
     C     InnlKey       KLIST
     C                   KFLD                    ZZTERM
     C                   KFLD                    NOPONR
     C     KODTVK        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    HEAVD
     C     KodtaKey      KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WBAVD
     C     KGEKey        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FAVK
     C     KG2Key        KLIST
     C                   KFLD                    KG2UNI
     C                   KFLD                    FAVK
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C*                  kfld                    BIKUNR
     C                   kfld                    xxKN
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     *LOVAL        SETLL     FIRINL
     C                   READ      FIRINL                                 33
      *
     C* Konstanter postnrbasert KALKULASJON
     C                   MOVE      'H'           XXUR
     C                   MOVE      *BLANKS       XXLKS
     C                   MOVE      *BLANKS       XXSDFF
     C                   MOVE      *BLANKS       XXLKK
     C                   MOVE      *BLANKS       XXSDVT
     C                   MOVE      *BLANKS       XXVERV
     C                   MOVE      *ZEROS        XXVERB
     C                   MOVE      *ZEROS        N5                5 0
     C                   MOVE      *ZEROS        NAVD              4 0
     C                   MOVE      *ZEROS        NOPD              7 0
     C                   MOVE      *ZEROS        uavnr
     C                   MOVE      *ZEROS        XXFBVRet
     C                   MOVE      'S'           USKA              1
     C                   MOVE      'V'           KOVUNI
     C                   MOVE      'A'           KOAUNI
     C                   MOVE      'GE'          KGEUNI
     C                   MOVE      'G2'          KG2UNI
      * Ved semi(=webservice) kun frakt..
     C**   SEMI          ifeq      'Y'
     C**   BIKUNR        oreq      *zeros
     C**   BIPO          oreq      *blanks
     C**                 MOVE      'J'           XKUNFRAKT
     C**                 else
     C                   MOVE      'N'           XKUNFRAKT
     C                   EVAL      PABRID = USERENV
     C                   MOVEL     'KUNFR'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        *IN33
     c                   EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        *IN33
     c                   EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PAKUNR = BIKNG
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       chain     BRPARF                             33
     C                   END
     C                   END
     C                   END
     C                   END
     C                   IF        *IN33
     C                   MOVE      'J'           XKUNFRAKT         1
     C                   ELSE
     C                   MOVEL     PAVRDA        XKUNFRAKT
     C                   END
     C**                 end
     C                   IF        ((XKUNFRAKT = 'J') OR (XKUNFRAKT = 'j'))
     C                   MOVE      *ZEROS        WBAVD
     C                   end
      *
     C                   move      *blanks       RMTUSER          30
     C                   move      *blanks       RMTIDENT         30
     C                   move      *blanks       authtype         30
      *
     C                   endsr
