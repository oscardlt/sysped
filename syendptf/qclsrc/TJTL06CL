/* skriv direkte til IFS/PDF  */
             PGM        PARM(&GN &PKOD &BANE)
             DCL        VAR(&BANE0) TYPE(*CHAR) LEN(11) +
                          VALUE('/tollarkiv/')
             DCL        VAR(&BANE9) TYPE(*CHAR) LEN(4) VALUE('.pdf')
             DCL        VAR(&BANE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&GN) TYPE(*CHAR) LEN(15)
             DCL        VAR(&PKOD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FILEN) TYPE(*CHAR) LEN(10) VALUE('LISTE')

             DCLF       FILE(FIRMARC) RCDFMT(*ALL)
             RCVF       DEV(*FILE) RCDFMT(*FILE)
             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)
             CHGVAR     VAR(&BANE) VALUE(&BANE0 *TCAT &PKOD *TCAT &GN +
                          *TCAT &BANE9)
             RMVLNK     OBJLNK(&BANE)
             MONMSG     MSGID(CPF0000)
    /*                                    */
    /* SKRIVER UT LISTE                   */
    /*                                    */
             OVRPRTF    FILE(LISTE) DEVTYPE(*AFPDS) PAGESIZE(64 160) +
                          LPI(8) CPI(15) OVRFLW(62) PAGRTT(90) +
                          FRONTOVL(SYSPED/GODSJ) OUTQ(SYCGIOUTQ) +
                          FORMTYPE(*STD) SPLFOWN(*JOB)

             CALL       PGM(SYTL06) PARM(&GN &PKOD)

             ADDLIBLE   LIB(MMAIL)
             MONMSG     MSGID(CPF0000)
/* LAGER PDF OG LEGGER I MAPPE          */
/* UTEN INFOPRINT                        */
/*                                       */
             IF         COND(&ARCINF = 'N') THEN(DO)
             CVTSPLFPDF SPLF(LISTE) JOB(&QNBR/&QUSER/&QJOB) +
                          TOPDF(&BANE)
             ENDDO
/* LAGER PDF OG LEGGER I MAPPE          */
/* MED  INFOPRINT                        */
/*                                       */
             IF         COND(&ARCINF = 'J') THEN(DO)
             CALL       PGM(INF001CL) PARM(&FILEN &QNBR &QUSER &QJOB +
                          &BANE)
             ENDDO
             RMVLIBLE   LIB(MMAIL)
             DLTSPLF    FILE(LISTE) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)

             ENDPGM
