********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 02 YBC PTF11 OMBEREGNING
12248 * 03 YBC PTF12 VEKT, KOLLI ER UTVIDET.
12282 * 04 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Gå til forrige bilde')
     A                                      HELP(01)
     A                                      CF01(01)
      *
     A          R BILDE01
     A                                  1  3'SYSPED/SYFTAE'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  1 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 29'S A D  -  eksport'
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(12 DIV1451 SYMSGF)
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(034 SSI0030 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0B  6 39DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23 23'F3 = Avslutt'
     A                                      COLOR(BLU)
     A  99        MSG105        78A  O 24  2MSGID(SPA0001 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE02
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/SYFTAE'
     A                                      COLOR(BLU)
     A                                  1 76'Del-1'
     A                                      COLOR(YLW)
     A                                  2 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 29'S A D  -  eksport'
     A                                      COLOR(WHT)
     A                                  4  2'Avdeling:'
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 28TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  4MSGCON(030 DIV1707 *LIBL/SYMSGF)
     A            WSDL           1A  B  7 37TEXT('BRUKE DATALISTE ?')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      VALUES(' ' 'J' 'N' '1')
     A                                  7 45MSGCON(025 DIV1708 *LIBL/SYMSGF)
     A                                  8  6MSGCON(015 DIV1709 *LIBL/SYMSGF)
     A            WSA4           1A  B  8 37DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  8 45MSGCON(009 DIV1710 *LIBL/SYMSGF)
     A                                  9  4MSGCON(030 DIV1714 *LIBL/SYMSGF)
     A            WSTDN          7Y 0B  9 37TEXT('TELLER TIL SAD-IMPORT')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  94                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 10  4MSGCON(030 DIV1715 *LIBL/SYMSGF)
     A            WSTOLK         1A  B 10 37TEXT('BRUKE TOLLKOMMUNIKASJON ?')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A  93                                  DSPATR(PC)
     A                                 11  4'Importeres eksterne data ?'
     A            WSEKST         1   B 11 37
     A                                 11 45'J/N/R  Ved R kan egen ref'
     A                                 12 45'inntastes for søking'
     A                                 23 17'F3=Avslutt uten endring'
     A                                      COLOR(BLU)
     A                                 23 42'F12=Gå til forrige uten endring'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE03
     A                                  1  3'SYSPED/SYFTAE'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  1 76'Del-2'
     A                                      COLOR(YLW)
     A                                  2 29'S A D  -  eksport'
     A                                      COLOR(WHT)
     A                                  4  2'Avdeling:'
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 28TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  5  2'TVINN . :'
     A            WSTOLK         1A  O  5 15TEXT('BRUKE TOLLKOMMUNIKASJON ?')
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N45                              9  4MSGCON(033 DIV1719 *LIBL/SYMSGF)
     A            W3039D         8Y 0B  9 40TEXT('DEKLARANTENS REFRANSE')
     A  45                                  DSPATR(PR)
     A  45                                  DSPATR(ND)
     A                                 10  4MSGCON(033 DIV1720 *LIBL/SYMSGF)
     A            W3039E         6Y 0B 10 40TEXT('TIL EKSP. ENHET')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  96                                  ERRMSG('Eksp. enhet kan ikke være 0-
     A                                      .' 95)
N02  A                                 10 54'SO:'
N02  A            W3039EO1       6Y 0B 10 58TEXT('TIL EKSP. ENHET')
N02  A                                      EDTCDE(4)
N02  A                                 10 65'EB/RE:'
N02  A            W3039EO2       6Y 0B 10 72TEXT('TIL EKSP. ENHET')
N02  A                                      EDTCDE(4)
     A                                 11  4MSGCON(034 SHU0791 *LIBL/SYMSGF)
     A            WSFORE         9A  O 11 40COLOR(WHT)
     A  97                                  DSPATR(RI)
     A                                 11 51'('
     A                                      COLOR(WHT)
     A            KOAKNR         8  0O 11 53COLOR(WHT)
     A                                      EDTCDE(4)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                 11 62')'
     A                                      COLOR(WHT)
     A                                 12  4MSGCON(033 DIV1722 *LIBL/SYMSGF)
     A            W0004         35A  B 12 40TEXT('NODI ID AVDELINGEN')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  95                                  DSPATR(PC)
     A  95                                  COLOR(RED)
     A                                 13  6MSGCON(033 DIV1721 *LIBL/SYMSGF)
     A            W0010         35A  B 13 40TEXT('NODI ID TOLLVESENET')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  94                                  DSPATR(PC)
     A  94                                  COLOR(RED)
     A                                 14  8'Application Reference:'
     A            W0026         14A  B 14 40
     A                                 16  4MSGCON(033 DIV1723 *LIBL/SYMSGF)
     A            W0035          1A  B 16 40TEXT('Testindikator')
     A                                      VALUES(' ' '1' '2')
     A                                 16 47MSGCON(018 DIV1724 *LIBL/SYMSGF)
     A                                 19  4'Skriv tollkv. ved utlev.attest:'
     A            WSUATK         1A  B 19 40VALUES('J' 'N')
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE04
     A                                  1 10'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 41'1.Dekl.'
     A                                  1 76'Del-3'
     A                                      COLOR(YLW)
     A            WSDTY          2A  B  1 49TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDP           2Y 0B  1 52TEXT('Dekl.prosedyre')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
N04  A            WSDP2          2Y 0B  1 55TEXT('Dekl.prosedyre 2')
N04  A                                      DSPATR(UL)
N04  A                                      EDTCDE(9)
N04  A                                      COLOR(YLW)
     A                                  2  2'2.Avsender'
     A            WSKNK          8Y 0B  2 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 22'Regnr'
     A            WSRG          11A  B  2 28TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S03  A*                                 2 54'6.Kolli'
S03  A*           WSNTK          7Y 0B  2 62TEXT('Antall kolli')
N03  A                                  2 54'6.Kl'
N03  A            WSNTK         10Y 0B  2 59TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 70'S/K'
     A            WSSKI          1A  B  2 74TEXT('Selg,Kjøper,Ingen')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 76'D/K'
     A            WSKDDK         1A  B  2 80TEXT('Dagsoppgjør/Kontant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAK         30A  B  3  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S03  A*                                 3 41'Brutto vekt'
S03  A*           WSVKB          9Y 0B  3 53TEXT('Bruttovekt')
N03  A                                  3 41'Br.vkt'
N03  A            WSVKB         11Y 0B  3 49TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 61'Ref'
     A            WSAAVD         4Y 0O  3 65COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  3 70COLOR(WHT)
     A                                      EDTCDE(4)
     A            SESG           3A  O  3 78COLOR(WHT)
     A            WSADK1        30A  B  4  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 40'15.Avs/utf.land'
     A            WSLKA          2A  B  4 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKAT        17A  B  4 59TEXT('F.T. AVS/UTF.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK2        30A  B  5  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'17.Be.land'
     A            WSLKB          2A  B  5 56TEXT('LANDK TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFTB         17A  B  5 59TEXT('FRITEKST TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B  6  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'8.Mottaker'
     A            WSKNS          8Y 0B  7 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 40'19.Cont.'
     A            WSKDC          1A  B  7 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 52'20.Lev.'
     A            WSLV           3A  B  7 60TEXT('Leveringsvilkår kod')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLVT         17A  B  7 64TEXT('Leveringsvilkår txt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAS         30A  B  8  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 40'21.Ident.'
     A            WSTRID        17A  B  8 50TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKT          2A  B  8 68TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADS1        30A  B  9  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40'22.faktura'
     A                                  9 70'23'
     A            WSVAL1         3A  B  9 51TEXT('Val.kode Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSBEL1        13Y 2B  9 55TEXT('Beløp Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVKU          7Y 3B  9 73TEXT('Valutakurs')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSADS2        30A  B 10  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 40'A.kostnad'
     A            WSVAL2         3A  B 10 51TEXT('Val.kode A. kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSBEL2        11Y 2B 10 55TEXT('Beløp A. kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSFTG2         1A  B 10 68TEXT('Fortegn +/- a.kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S04  A*                                10 70'24.Tr.typ'
S04  A*           WSTST          1Y 0B 10 80TEXT('Transaksjonstype')
N04  A                                 10 70'24.Tr.ty'
N04  A            WSTST          2Y 0B 10 79TEXT('Transaksjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSADS3        30A  B 11  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 35'25.Tr.måte'
     A            WSTRM          2Y 0B 11 46TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
S01  A*                                11 49'29.Utpass.'
S01  A*           WSKDH          2Y 0B 11 60TEXT('Havnekode')
N01  A                                 11 49'29.Utpas'
N01  A            WSKDH          4Y 0B 11 58TEXT('Havnekode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKDFT        17A  B 11 63TEXT('FRITEKST TOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  1'14.Deklarant'
     A            WSTLF         12A  B 12 14TEXT('Telefon Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 40'28.Dato'
     A            WSFID          6N 0B 12 49DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 56'Finans'
     A            WSFIF         17A  B 12 63TEXT('Finans oppl.faknr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAD         30A  B 13  2TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 35'30.Lag.sted'
     A            WSLS          16A  B 13 47TEXT('Lagringssted tekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKDLS         2A  B 13 64TEXT('Lagringssted kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14  2'Fritekst ved dataliste'
     A                                      COLOR(WHT)
     A                                 14 32'48.KT.'
     A            WSKTC          1A  B 14 39TEXT('Tollkontonr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKTA          5Y 0B 14 41TEXT('Tollkontonr-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKTB          2Y 0B 14 47TEXT('Tollkontonr-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 50'49.Godsn'
     A            WSGN          15A  B 14 59TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPOS          8A  B 15 59TEXT('POSISJON')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  1'31.'
     A            WSFT1         45A  B 15  5TEXT('Fritekst-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFT2         45A  B 16  5TEXT('Fritekst-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  1'44.'
     A            WSFT3         45A  B 17  5TEXT('Fritekst-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A*                                18 39'52.Garanti'
     A*           WSGFT1        30A  B 18 50TEXT('GARATI FRITEKST-1')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A*           WSGKD          1Y 0B 19 40TEXT('GARATIKODE')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A*                                     EDTCDE(4)
     A*           WSGFT2        30A  B 19 50TEXT('GARATI FRITEKST-2')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A                                 21 39'54.Sted'
     A            WSDST         15A  B 21 47TEXT('Deklarantens sted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22  1'Dato'
     A            WSDTG         10A  B 22  6TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 18'Løpenr'
     A            WSTLL          5A  B 22 25TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 31'Eksp.'
     A            WSTLE          3A  B 22 37TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 42'Registreringsdato'
     A            SEDT           8Y 0H
     A            WSDT           6Y 0O 22 60TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                 23  1'F3 = Avslutt uten oppdatering'
     A                                      COLOR(BLU)
     A                                 23 32'F12 = Gå til forrige uten oppdater-
     A                                      ing'
     A                                      COLOR(BLU)
