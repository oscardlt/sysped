     FEDIS4     UF   E           K DISK
     FEDIM6     UF   E           K DISK
     FSADH      UF   E           K DISK    USROPN
     FSAEH      UF   E           K DISK    USROPN
     FTRUH      UF   E           K DISK    USROPN
     FTRIH      UF   E           K DISK    USROPN
     FTRUGH     UF   E           K DISK    USROPN
     FSVXH      UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
     FSVNH      UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
     FSVXGH     UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D A               S              1    DIM(64)                              Input string
     D B               S              1    DIM(10)                              Output alpha string
      *//////////////
      * MAIN LOGIC //
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Init                                                   SRA
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WMSGID            7
     C                   PARM                    WMSG             64
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    MAVD
     C                   KFLD                    MTDN
     C     KEYB          KLIST
     C                   KFLD                    MAVD
     C                   KFLD                    MTDN
      *__________________________________
      * Hente dagens dato og klokkeslett.
      *""""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *________________________________________
      * Open files from SYSPED400, if installed
      *""""""""""""""""""""""""""""""""""""""""
     C                   OPEN      SADH                                 54
     C                   OPEN      SAEH                                 55
     C                   OPEN      TRUH                                 57
     C                   OPEN      TRIH                                 58
     C                   OPEN      TRUGH                                59
     C                   OPEN      SVXH                                 60
     C                   OPEN      SVNH                                 60
     C                   OPEN      SVXGH                                60
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Oppdater Sendningslogg                               SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Hente komponentnavn fra meldingstekst
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     *BLANKS       B
     C                   MOVEA     WMSG          A
     C     'member'      SCAN      WMSG          AX                2 0    62
     C                   ADD       7             AX
     C     1             DO        8             BX                1 0
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ENDDO
     C                   MOVEA     B             WMBR             10
      * Hente sendningslogg
     C     WMBR          CHAIN     EDIS4                              52
     C  N52SST           IFNE      'C'
      * Oppdatere sendningslogg med status=C (Mottat av SNADS i andre enden)
     C     WMSGID        IFEQ      'CPI8070'
     C                   MOVE      'C'           SST
     C                   ELSE
     C     WMSGID        IFEQ      'CPI8076'
     C                   MOVE      'E'           SST
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
      *
     C                   UPDATE    EDISR
      * Oppdatere meldingslogg med samme status som sendningslogg
     C     SST           IFEQ      'C'
     C     SSN           CHAIN     EDIM6                              53
     C     *IN53         DOWEQ     '0'
      *
     C                   MOVE      'C'           MST
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   UPDATE    EDIMR
      *
     C     M0065         IFEQ      'CUSDEC'
     C     *IN54         ANDEQ     '0'
     C     *IN55         ANDEQ     '0'
     C     *IN57         ANDEQ     '0'
     C     *IN58         ANDEQ     '0'
     C     *IN59         ANDEQ     '0'
     C     *IN60         ANDEQ     *OFF
     C                   EXSR      SRC
     C                   ENDIF
      *
     C     SSN           READE     EDIM6                                  53
     C  N53              ENDDO
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Update SYSPED                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
     C                   SELECT
     C                   WHEN      M1001 = '929'
     C     KEYA          CHAIN     SADH                               56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'C'           SIST
     C                   UPDATE    SADHR
     C                   ENDIF
     C                   WHEN      M1001 = '830'
     C     KEYA          CHAIN     SAEH                               56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'C'           SEST
     C                   UPDATE    SAEHR
     C                   ENDIF
     C                   WHEN      M1001 = 'TET'
     C     KEYA          CHAIN     TRUH                               56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'C'           THST
     C                   UPDATE    TRUHR
     C                   ENDIF
     C                   MOVEL     M0068         TGGNR
     C     TGGNR         CHAIN     TRUGH                              56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'P'           TGST
     C                   UPDATE    TRUGHR
     C                   ENDIF
     C                   WHEN      M1001 = 'TEI'
     C     KEYA          CHAIN     TRIH                               56
     C     *IN56         IFEQ      '0'
     C     TIMF          IFEQ      '007'
     C                   MOVE      'C'           TIST
     C                   ELSE
     C                   MOVE      'F'           TIST
     C                   END
     C                   UPDATE    TRIHR
     C                   ENDIF
      *
     C                   WHEN      M1001 = 'STE'
     C     KEYB          CHAIN     SVXH                               56
     C     *IN56         IFEQ      '0'
     C     TIMF          IFEQ      '007'
     C                   MOVE      'C'           SV_THST
     C                   ELSE
     C                   MOVE      'L'           SV_THST
     C                   END
     C                   UPDATE    SVXHR
     C                   ENDIF
     C                   MOVEL     M0068         SV_TGGNR
     C     SV_TGGNR      CHAIN     SVXGH                              56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'P'           SV_TGST
     C                   UPDATE    SVXGHR
     C                   ENDIF
      *
     C                   WHEN      M1001 = 'STI'
     C     KEYB          CHAIN     SVNH                               56
     C     *IN56         IFEQ      '0'
     C     TIMF          IFEQ      '007'
     C                   MOVE      'C'           SV_TIST
     C                   ELSE
     C                   MOVE      'L'           SV_TIST
     C                   END
     C                   UPDATE    SVNHR
     C                   ENDIF
      *
     C                   ENDSL
     C                   ENDSR
