     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SVX064PNL)
     A                                      HLPTITLE('REGISTRERING AV TET IMPOR-
     A                                      T')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SVX064D1
     A N27                                  CF04(04 'Søking')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF10(10 'Se TVINN logg')
     A                                      CA12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SVX064000 SVX064PNL)
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(SVX064001 SVX064PNL)
     A                                      HLPARA(01 001 13 072)
     A          H                           HLPPNLGRP(SVX064002 SVX064PNL)
     A                                      HLPARA(14 001 14 020)
     A          H                           HLPPNLGRP(SVX064003 SVX064PNL)
     A                                      HLPARA(14 022 14 040)
     A          H                           HLPPNLGRP(SVX064004 SVX064PNL)
     A                                      HLPARA(14 042 14 058)
     A          H                           HLPPNLGRP(SVX064005 SVX064PNL)
     A                                      HLPARA(15 001 15 014)
     A          H                           HLPPNLGRP(SVX064006 SVX064PNL)
     A                                      HLPARA(15 024 15 036)
     A          H                           HLPPNLGRP(SVX064015 SVX064PNL)
     A                                      HLPARA(16 007 16 076)
     A          H                           HLPPNLGRP(SVX064007 SVX064PNL)
     A                                      HLPARA(17 007 17 076)
     A          H                           HLPPNLGRP(SVX064008 SVX064PNL)
     A                                      HLPARA(18 002 18 047)
     A          H                           HLPPNLGRP(SVX064009 SVX064PNL)
     A                                      HLPARA(18 052 18 077)
     A          H                           HLPPNLGRP(SVX064010 SVX064PNL)
     A                                      HLPARA(19 002 19 026)
     A          H                           HLPPNLGRP(SVX064011 SVX064PNL)
     A                                      HLPARA(19 027 19 027)
     A          H                           HLPPNLGRP(SVX064012 SVX064PNL)
     A                                      HLPARA(19 029 19 032)
     A          H                           HLPPNLGRP(SVX064013 SVX064PNL)
     A                                      HLPARA(19 034 19 053)
     A          H                           HLPPNLGRP(SVX064014 SVX064PNL)
     A                                      HLPARA(19 055 19 056)
     A          H                           HLPPNLGRP(SVX064006 SVX064PNL)
     A                                      HLPARA(19 022 19 034)
     A          H                           HLPPNLGRP(SVX064017 SVX064PNL)
     A                                      HLPARA(20 002 20 011)
     A          H                           HLPPNLGRP(SVX064017 SVX064PNL)
     A                                      HLPARA(21 007 21 076)
     A          H                           HLPPNLGRP(SVX064017 SVX064PNL)
     A                                      HLPARA(22 007 22 076)
     A          H                           HLPPNLGRP(SVX064016 SVX064PNL)
     A                                      HLPARA(23 001 23 064)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVX064R'
     A                                      COLOR(BLU)
     A N27                              1 21'Underhåll lossmærknader'
     A                                      COLOR(WHT)
     A  27                              1 21'Fråga lossmærknader'
     A                                      COLOR(WHT)
     A                                  1 47'Ref.'
     A            UAVD           4Y 0O  1 52COLOR(WHT)
     A                                      EDTCDE(4)
     A            UTDN           7Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A            TISG           3A  O  1 65COLOR(WHT)
     A                                  2  2'MRN-nr.'
     A            TITRNR        18A  O  2 13TEXT('MRN-nr')
     A                                      COLOR(WHT)
     A                                  2 32'Godsnr.'
     A            TIGN          35A  O  2 40COLOR(WHT)
     A            TIGNSK         2A  O  2 76COLOR(WHT)
     A                                  3  2'Avs.land'
     A            TIALK          2A  O  3 13TEXT('Landk avs/utf-land')
     A                                      COLOR(WHT)
     A                                  3 16'Avt.lag.pl'
     A            TIALSK        17A  O  3 27COLOR(WHT)
     A            TIALS         35A  O  3 45COLOR(WHT)
     A                                  4  2'God.l.pl.:'
     A            TIGLSK        17A  O  4 13COLOR(WHT)
     A                                  4 42'Contr.pl  '
     A            TIACTS        17A  O  4 53COLOR(WHT)
     A                                  5  2'Freml.t.sd'
     A            TITSB          8A  O  5 13COLOR(WHT)
     A                                  5 22'Språkkod '
     A            TISKB          2A  O  5 32COLOR(WHT)
     A                                  5 42'Frigivelsedatum'
     A            TIDTF          8S 0O  5 58COLOR(WHT)
     A                                  6  2'Næringsliv'
     A            TINA          30A  O  6 13TEXT('Navn')
     A                                      COLOR(WHT)
     A                                  7  2'Huvudans.:'
     A            TINAA         30A  O  7 13TEXT('Navn')
     A                                      COLOR(WHT)
     A                                  8  2'Avs......:'
     A            TINAS         30A  O  8 13TEXT('Navn')
     A                                      COLOR(WHT)
     A                                  9  2'Mott.....:'
     A            TINAK         30A  O  9 13TEXT('Navn')
     A                                      COLOR(WHT)
     A                                 11  2'Total varulinj(NCTS):'
     A            TIVPOS         5Y 0O 11 24DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11 42'Total varulinj (CTL):'
     A            NIVPOS         5Y 0O 11 64DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                 12  2'Total kolli   (NCTS):'
     A            TINTK          7Y 0O 12 24DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12 42'Total kolli    (CTL):'
     A            NINTK          7Y 0O 12 64DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13  2'Total br.vikt (NCTS):'
     A            TIVKB         11Y 3O 13 24DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 13 42'Total br.vikt  (CTL):'
     A            NIVKB         11Y 3O 13 64DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                 14  2'Konform:'
     A            WSKONF         1Y 0B 14 13DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1)
     A                                 14 15'(1=Ja)'
     A                                 14 22'Fullførd:'
     A            WSFULF         1Y 0B 14 33DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1)
     A                                 14 35'(1=Ja)'
     A                                 14 42'Loss  DT:'
     A            WSDTL          8Y 0B 14 53DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 15  2'CTL resultat (OT/DI):'
     A                                 15 24'Språkkod: '
     A            WSCTSK         2A  B 15 35DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  97                                  DSPATR(RI)
     A N31                                  COLOR(YLW)
     A  31                                  COLOR(RED)
     A                                 16  2'OT:'
     A            WSCTB         70A  B 16  7DSPATR(UL)
     A  27                                  DSPATR(PR)
     A N31                                  COLOR(YLW)
     A  31                                  COLOR(RED)
     A                                 17  2'DI:'
     A            WSCTB2        70A  B 17  7DSPATR(UL)
     A  27                                  DSPATR(PR)
     A N31                                  COLOR(YLW)
     A  31                                  COLOR(RED)
     A                                 18  2'Pekare   :'
     A            WSCTP          5A  B 18 13DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  95                                  DSPATR(RI)
     A N28                                  COLOR(YLW)
     A  28                                  COLOR(RED)
     A                                 18 52'Nyt værde:'
     A            WSCTNV        15A  B 18 63DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  94                                  DSPATR(RI)
     A N28                                  COLOR(YLW)
     A  28                                  COLOR(RED)
     A                                 19  2'Plombering St/Ant/ID/SK:'
     A            WSDFST         1A  B 19 27DSPATR(UL)
     A  27
     AO 37                                  DSPATR(PR)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSDANT         4Y 0B 19 29DSPATR(UL)
     A  27
     AO 37                                  DSPATR(PR)
     A  92                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            TIDFKD        20A  H
     A            WSDFKD        20A  B 19 34DSPATR(UL)
     A  27
     AO 37                                  DSPATR(PR)
     A  91                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A            TIDFSK         2A  H
     A            WSDFSK         2A  B 19 55DSPATR(UL)
     A  27
     AO 37                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 20  2'Mærknader:'
     A                                 20 22'Språkkod: '
     A            WSMNSK         2A  B 20 33DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSMN1         70A  B 21  7DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSMN2         70A  B 22  7DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A N27                             23  1'F4=Søk'
     A                                      COLOR(BLU)
     A N36                             23 10'F7=Se uppdrag'
     A                                      COLOR(BLU)
     A                                 23 25'F10=NCTS'
     A                                      COLOR(BLU)
     A                                 23 35'F12=Avbryt '
     A                                      COLOR(BLU)
     A                                 23 49'F15=Tilleggskod '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SVX064D2
     A                                      WINDOW(*DFT 6 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                      CA23(23 'BEKREFT')
     A                                  1  2'SVX064R'
     A                                      COLOR(BLU)
     A                                  2  2'Avdelning'
     A            UAVD           4Y 0O  2 12EDTCDE(4)
     A                                  2 22'Uppd.nr.:'
     A            UTDN           7Y 0O  2 32EDTCDE(4)
     A                                  3  2'MRN-nr..:'
     A            TITRNR        18A  O  3 12TEXT('Signatur')
     A                                  4  2'Ærendet ligger i "brevlådan" før s-
     A                                      ændning.     '
     A                                      COLOR(RED)
     A                                  5  2'F23=Radera från brevlådan. '
     A                                      COLOR(YLW)
     A                                  6  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVX064D3
     A                                      WINDOW(*DFT 4 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'SVX064R'
     A                                      COLOR(BLU)
     A                                  2  2'Avdelning'
     A            UAVD           4Y 0O  2 12EDTCDE(4)
     A                                  2 22'Uppd.nr.:'
     A            UTDN           7Y 0O  2 32EDTCDE(4)
     A                                  3  2'Total bruttovikt:'
     A            WSVKB         11Y 3O  3 20DSPATR(UL)
     A                                      EDTCDE(4)
