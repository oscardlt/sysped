********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 BIAKT=L ER ENDRET TIL Å TESTE AUTORISASJON
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS151 MODULE(*LIBL/SS151
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPRF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FBRPARL2   IF   E           K DISK    USROPN
     F                                     RENAME(BRPARFR:BRPARFR2)
     FCUNDL01   IF   E           K DISK    USROPN
     FKUFAST    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WINT            s              6
     D WINTEG          s              6  0
     D MaxSN           s              5
     D MaxRC           s              5
     D MaxSN2          s              5  0
     D MaxRC2          s              5  0
     D lstbrd          s              1
     D XKN             S              8  0 DIM(100)
     D XLC             S              5    DIM(100)
     D XSUP            S              8  0 DIM(100)
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
     C                   exsr      SetTop
     C                   exsr      SetData
     C                   exsr      SetEnd
      *
     C                   callp     wrtsection('*fini')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   MOVE      'N'           XFEIL             1
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   eval      MaxSn   = zhbgetvar('MaxSn')
     C                   eval      MaxSn2  = c2n2(MaxSn)
     C                   IF        MaxSn2  = 0
     C                   eval      MaxSn2  = 100
     C                   MOVE      MaxSn2        MaxSn
     C                   END
     C                   eval      MaxRc   = zhbgetvar('MaxRc')
     C                   eval      MaxRc2  = c2n2(MaxRc)
     C                   IF        MaxRc2  = 0
     C                   eval      MaxRc2  = 1000
     C                   MOVE      MaxRc2        MaxRc
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS151'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section
      *  (search criteria)
      *=====================================================================******
     C     SetData       begsr
     C                   callp     updHtmlVar('wint':WINT)
     C                   callp     updHtmlVar('MaxSn':MaxSn)
     C                   callp     updHtmlVar('MaxRc':MaxRc)
     C                   callp     updhtmlvar('WSPRO':' ')
     C                   callp     updhtmlvar('RFREF':' ')
     C                   callp     updhtmlvar('ORBCD':' ')
     C                   callp     updhtmlvar('ORDTL':' ')
     C                   callp     updhtmlvar('WSAP':' ')
     C                   callp     updhtmlvar('POCONO':' ')
     C                   callp     updhtmlvar('WSACC':' ')
     C                   callp     updhtmlvar('WSTYM':' ')
      *
     C                   callp     wrtsection('top')
      *
     C     BIBRID        SETLL     BRPARF
     C     BIBRID        READE     BRPARF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        PAID = 'SSCUS'
     C                   Z-ADD     PAVRDN        XAN08             8 0
     C                   callp     updhtmlvar('WSCNO':
     C                             %trim(%editc(XAN08:'Z')))
     C                   callp     updhtmlvar('WSCNA':PAVRDA)
     C                   callp     wrtsection('tablerowCN')
     C                   ENDIF
     C     BIBRID        READE     BRPARF                                 33
     C  N33              ENDDO
     C                   callp     wrtsection('tableendCN')
      *
     C     BIBRID        SETLL     BRPARF
     C     BIBRID        READE     BRPARF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        PAID = 'SSLOC'
     C                   MOVEL     PAVRDA        XAN05             5
     C                   IF        XAN05 <> '99999'
     C     1             DO        100           XN                3 0
     C                   IF        XLC(XN) = *BLANKS
     C                   MOVE      XAN05         XLC(XN)
     C                   LEAVE
     C                   ELSE
     C     XLC(XN)       CABEQ     XAN05         NESTELC
     C                   END
     C                   ENDDO
     C                   MOVE      PAVRDA        XAN45            45
     C                   callp     updhtmlvar('WSLC':XAN05)
     C                   callp     updhtmlvar('WSLCTXT':XAN45)
     C                   callp     wrtsection('tablerowLC')
     C                   ENDIF
     C                   ENDIF
     C     NESTELC       TAG
     C     BIBRID        READE     BRPARF                                 33
     C  N33              ENDDO
     C                   callp     wrtsection('tableendLC')
      *
S01  C*                  IF        BIAKT = 'L'
N01  C                   IF        XAUT = 'L'
     C                   MOVE      *ZEROS        XSUP
     C                   Z-ADD     BIKUNR        XXPAVRDN
     C     BRPARL2K      SETLL     BRPARL2
     C     BRPARL2K      READE     BRPARL2                                33
     C                   DOW       *IN33 = *OFF
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33 = *OFF
     C     1             DO        100           XN
     C                   SELECT
     C                   WHEN      XSUP(XN) = 0
     C                   Z-ADD     PAVRDN        KUNDNR
     C                   MOVE      KUNDNR        XSUP(XN)
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   IF        *IN33 = *OFF
     C                   callp     updhtmlvar('WSSUP':KNAVN)
     C                   callp     updhtmlvar('WSSUPTXT':KNAVN)
     C                   callp     wrtsection('tablerowSUP')
     C                   ENDIF
     C                   LEAVE
     C                   WHEN      XSUP(XN) = PAVRDN
     C                   LEAVE
     C                   ENDSL
     C                   ENDDO
     C                   ENDIF
     C     BRPARL2K      READE     BRPARL2                                33
     C  N33              ENDDO
     C                   ELSE
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK2      CHAIN     BRPARF                             33
     C  N33              Z-ADD     PAVRDN        KUNDNR
     C  N33CUNDL01K      CHAIN     CUNDL01                            33
     C                   IF        *IN33 = *OFF
     C                   callp     updhtmlvar('WSSUP':KNAVN)
     C                   callp     updhtmlvar('WSSUPTXT':KNAVN)
     C                   callp     wrtsection('tablerowSUP')
     C                   ENDIF
     C                   ENDIF
     C                   callp     wrtsection('tableendSUP')
      *
     C     KFTYP         SETLL     KUFAST
     C     KFTYP         READE     KUFAST                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        ((((KFKOD <> ' DEFN') AND
     C                                (KFKOD <> 'ATA  ')) AND
     C                                (KFKOD <> 'ATD  ')) AND
     C                                (KFKOD <> 'POD  '))
     C                   callp     updhtmlvar('KFKOD':KFKOD)
     C                   callp     updhtmlvar('KFTXT':KFTXT)
     C                   callp     wrtsection('tablerowST')
     C                   ENDIF
     C     KFTYP         READE     KUFAST                                 33
     C  N33              ENDDO
     C                   callp     wrtsection('tableendST')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      BRPRF
     C                   OPEN      BRPARF
     C                   OPEN      BRPARL2
     C                   OPEN      CUNDL01
     C                   OPEN      KUFAST
      *
     C     BRPARL2K      klist
     C                   kfld                    XXPAVRDN         15 5
     C                   kfld                    XXPAID1
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    XXPAID2
     C     BRPARFK2      klist
     C                   kfld                    WSUSER
     C                   kfld                    PAKUNR
     C                   kfld                    XXPAID2
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    KUNDNR
      *
     c                   eval      WINTEG   = random(1:999999)
     c                   move      WINTEG        WINT
N01   *
N01  C                   MOVE      'AUTOR'       XXPAID2
N01  C     BRPARFK2      CHAIN     BRPARF                             33
N01  C                   IF        *IN33
N01  C                   MOVE      'N'           XAUT              1
N01  C                   ELSE
N01  C                   MOVEL     PAVRDA        XAUT
N01  C                   END
N01   *
     c                   MOVEL     'SSSTATUS'    KFTYP
     c                   MOVE      'SSCUS'       XXPAID1           5
     c                   MOVE      'SSFAB'       XXPAID2           5
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     BRPRF
     C                   close     BRPARF
     C                   close     BRPARL2
     C                   close     CUNDL01
     C                   close     KUFAST
     C                   eval      *inlr = *on
      *
     C                   endsr
