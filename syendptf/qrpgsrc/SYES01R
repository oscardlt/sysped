      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV 'TIDUT' fjrenet / "INNE" I/blank er gjrotr nondisplay
10XXX * 02 JOV Feil ved delete(parametre ble ikke deletet)
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 03 YBC Sjekk tellerfil, evt. lag ny post
12XXX * 04 YBC Gi QTMHHTTP rettighet
12XXX * 05 JOV PTF12 DDS-endring/vis kundegruppe i subfil
12XXX * 06 JOV PTF12 Nytt felt for LANGT passord
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE WEB-BRUKERE               ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24
      *** LEDIGE INDIKATORER 27-29 31 34-39 41-83 95-97
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FBRIDF     UF A E           K DISK
     FBRIDL01   IF   E           K DISK
     F                                     RENAME(BRIDFR:BRID01R)
     FBRIDL02   IF   E           K DISK
     F                                     RENAME(BRIDFR:BRID02R)
     FBRIDL03   IF   E           K DISK
     F                                     RENAME(BRIDFR:BRID03R)
     FBRPARF    UF A E           K DISK
S03  F*TELLGE    UF   E           K DISK
N03  FTELLGE    UF A E           K DISK
N06  FSYPARL1   IF   E           K DISK
     FSYES01D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BIDTV                  1      8  0
     D  BIDTVÅ                 1      4  0
     D  BIDTVM                 5      6  0
     D  BIDTVD                 7      8  0
     D                 DS
     D  WSDTV                  1      6  0
     D  WSDTVD                 1      2  0
     D  WSDTVM                 3      4  0
     D  WSDTVÅ                 5      6  0
     D                 DS
     D  BIDTF                  1      8  0
     D  BIDTFÅ                 1      4  0
     D  BIDTFM                 5      6  0
     D  BIDTFD                 7      8  0
     D                 DS
     D  WSDTF                  1      6  0
     D  WSDTFD                 1      2  0
     D  WSDTFM                 3      4  0
     D  WSDTFÅ                 5      6  0
N04  D UCMD            DS          1024
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO

      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   select
     c     W2BESK        whenne    *BLANKS
     c     W2BESK        SETLL     BRIDL01
     c     W2KUNR        whenne    *zeros
     c     W2KUNR        SETLL     BRIDL02
     c     W2KNG         whenne    *zeros
     c     W2KNG         SETLL     BRIDL03
     c                   other
     C     W2BRID        SETLL     BRIDF
     C                   endsl
      *
     C     START1        TAG
      *
     C                   select
     c     W2BESK        whenne    *BLANKS
     c                   READ      BRIDL01                                94
     c     W2KUNR        whenne    *zeros
     c                   READ      BRIDL02                                94
     c     W2KNG         whenne    *zeros
     c                   READ      BRIDL03                                94
     c                   other
     C                   READ(N)   BRIDF                                  94
     C                   endsl
      *
     C     *IN94         DOWEQ     '0'
     C     BIFIRM        IFEQ      FIFIRM
s01  C*    BRPARFK       CHAIN(N)  BRPARF                             33
s01  C*                  IF        *IN33
s01  C*                  MOVE      *ZEROS        BITUT
s01  C*                  ELSE
s01  C*                  Z-ADD     PAVRDN        BITUT
s01  C*                  END
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *
     C                   select
     c     W2BESK        whenne    *BLANKS
     c                   READ      BRIDL01                                94
     c     W2KUNR        whenne    *zeros
     c                   READ      BRIDL02                                94
     c     W2KNG         whenne    *zeros
     c                   READ      BRIDL03                                94
     c                   other
     C                   READ(N)   BRIDF                                  94
     C                   endsl
      *
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
      * FRISK OPP
     C   05              GOTO      START
      * LAGE NY
     C   06              DO                                                     1<-B
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END                                                    1<-E
      * SØK FRA
     C   08              GOTO      START6
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C*** CREATE A RECORD
     C     START5        TAG
     C                   EXFMT     VARELI
S06  C*                  MOVEA     '0000000'     *IN(84)
N06  C                   MOVEA     '00000000'    *IN(83)
     C                   SETOFF                                       30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     *IN04         IFEQ      *ON
     C     *IN09         OREQ      *ON
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     08            POSNBR
     C     START6b       TAG
     C                   EXFMT     VAREL2
     C     *IN04         IFEQ      *ON
     C     *IN09         oreq      *ON
     C                   EXSR      SØKDTA2
     C     W2KUNR        ifeq      *zeros
     C     W2KNG         andeq     *zeros
     C                   GOTO      START6b
     C                   END
     C                   END
     C   12              GOTO      START3
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSBRID        COMP      *BLANKS                                90
     C     *IN90         IFEQ      '0'
     C     WSBRID        CHAIN(N)  BRIDF                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      '1'           *IN90
     C                   END
     C                   END
     C   90              DO
     C                   MOVE      'YBC1853'     MSGNR
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     WSBESK        COMP      *BLANKS                                89
     C   89
     CANN30              DO
     C                   MOVE      'YBC1854'     MSGNR
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     48            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     *IN23         IFNE      *ON
N06   * krav om langt
N06  C                   if        PASLKRAV = 'J'
N06  C     WSPOL         COMP      *BLANKS                                83
N06  C   83
N06  CANN30              DO
N06  C                   MOVE      'JOV1791'     MSGNR
N06  C                   Z-ADD     17            LINNBR
N06  C                   Z-ADD     2             POSNBR
N06  C                   SETON                                        30
N06  C                   END
N06  c                   else
N06   * valgfritt
     C     WSPO          COMP      *BLANKS                                88
N06  C   88WSPOL         COMP      *BLANKS                                88
     C   88
     CANN30              DO
     C*                  MOVE      'YBC1855'     MSGNR
S06  C*                  MOVE      'JOV1438'     MSGNR
N06  C                   MOVE      'JOV1792'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   END
N06  C                   endif
     C                   END
      *
     C     WSAKT         COMP      ' '                                    87
     C   87
     CANN30              DO
     C                   MOVE      'YBC1856'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSSTDL        COMP      *BLANKS                            86
     C   86WSLIBL        COMP      *BLANKS                            86
     C   86
     CANN30              DO
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      'YBC1857'     MSGNR
     C                   SETON                                        30
     C                   END
      *
     C     WSPO          IFNE      *BLANKS
     C     CUN1KK        CHAIN     CUNDL01                            85
     C   85
     CANN30              DO
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      'SPK0001'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     WSKNG         IFNE      0
     C     WSKUNR        OREQ      0
     C     CUN1KG        CHAIN     CUNDL01                            84
     c     *IN84         IFEQ      *ON
     C                   MOVE      WSKNG         XKFKOD
     C                   MOVEL     'ESPEDKUGRP'  XKFTYP
     C     KUFASK        CHAIN     KUFAST                             84
     c                   end
     C   84
     CANN30              DO
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     48            POSNBR
     C                   MOVE      'SPK0001'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      * biblioteksliste - 1 = dafault sysepedf mm
     C     WSSTDL        IFeq      *blanks
     C     WSLIBL        andeq     *blanks
     c                   move      '1'           WSSTDL
     C                   MOVE      'JOV1439'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   GOTO      SLTB1
     C                   end
      *
     C     WSBRID        CHAIN     BRIDF                              33
     C                   EXSR      MOVWBI
     C                   MOVE      ' '           WSNR
     C   40              UPDATE    SUBFIL
     C  N40              DO
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   WRITE     SUBCTL
      *
     C  N33              UPDATE    BRIDFR
     C   33              WRITE     BRIDFR
N04   *  GRTUSRPMN TOUSER(QTMHHTTP) FORUSER(BING)
N04  C                   Z-ADD     1024          UCMDL            15 5
N04  C                   EVAL      UCMD = 'GRTUSRPMN TOUSER(QTMHHTTP) '
N04  C                             + 'FORUSER(' + %TRIM(BIBRID) + ')'
N04  C                   CALL      'QCMDEXC'                            33
N04  C                   PARM                    UCMD
N04  C                   PARM                    UCMDL
N04  C                   EVAL      UCMD = 'GRTUSRPMN TOUSER(QTMHHTP1) '
N04  C                             + 'FORUSER(' + %TRIM(BIBRID) + ')'
N04  C                   CALL      'QCMDEXC'                            33
N04  C                   PARM                    UCMD
N04  C                   PARM                    UCMDL
S01   *
S01  C*    BRPARFK       CHAIN     BRPARF                             33
S01  C*                  Z-ADD     BITUT         PAVRDN
S01  C*                  IF        *IN33
S01  C*                  MOVE      BIBRID        PABRID
S01  C*                  MOVE      'TIDUT'       PAID
S01  C*    'ESPEDPAR'    CHAIN     TELLGE                             34
S01  C*                  MOVE      GENO          PARECN
S01  C*                  ADD       1             GENO
S01  C*                  UPDATE    TELLGER
S01  C*                  WRITE     BRPARFR
S01  C*                  ELSE
S01  C*                  UPDATE    BRPARFR
S01  C*                  END
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C     BIBRID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGBI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '3'
     C     BIBRID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CPYBI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C     BIBRID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTBI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '5'
     C     BIBRID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DSPBI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '8'
     C     BIBRID        COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      WRKPAR
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGBI         BEGSR
     C***********************************************
     C     BIBRID        CHAIN(N)  BRIDF                              33
     C                   SETON                                        99
     C                   SETON                                        40
     C                   EXSR      MOVBIW
     C                   MOVE      BIDTVD        WSDTVD
     C                   MOVE      BIDTVM        WSDTVM
     C                   MOVE      BIDTVÅ        WSDTVÅ
     C                   MOVE      BIDTFD        WSDTFD
     C                   MOVE      BIDTFM        WSDTFM
     C                   MOVE      BIDTFÅ        WSDTFÅ
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     14            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     *IN04         IFEQ      *ON
     C     *IN09         OREQ      *ON
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C   12              DO
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYBI         BEGSR
     C***********************************************
     C     BIBRID        CHAIN(N)  BRIDF                              33
     C                   MOVE      BIBRID        XXBRID           10
     C                   MOVE      *BLANKS       WSBRID
     C                   MOVE      *BLANKS       WSBESK
     C                   MOVE      *BLANKS       WSPO
N06  C                   MOVE      *BLANKS       WSPOL
     C                   MOVE      BIAKT         WSAKT
     C                   MOVE      *ZEROS        WSDTV
     C                   MOVE      *ZEROS        WSTIDV
     C                   MOVE      BIINN         WSINN
     C                   MOVE      FIFIRM        WSFIRM
     C                   MOVE      BISTDL        WSSTDL
     C                   MOVE      BILIBL        WSLIBL
     C                   MOVE      BIWELC        WSWELC
     C                   MOVE      BIKUNR        WSKUNR
     C                   MOVE      BIKNG         WSKNG
     C                   MOVE      BIEIER        WSEIER
     C                   MOVE      BIFARG        WSFARG
     C                   MOVE      *ZEROS        WSANTF
     C                   MOVE      *ZEROS        WSDTF
     C                   MOVE      *ZEROS        WSTIDF
     C                   MOVE      *ZEROS        BITUT
     C                   SETON                                        99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     14            POSNBR
      *
     C     STCPY         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     *IN04         IFEQ      *ON
     C     *IN09         OREQ      *ON
     C                   EXSR      SØKDTA
     C                   GOTO      STCPY
     C                   END
     C     *IN12         IFEQ      '1'
     C                   EXSR      MOVB2
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      STCPY
      *
     C     XXBRID        SETLL     BRPARF
     C     XXBRID        READE(N)  BRPARF                                 33
     C                   DOW       *IN33 = *OFF
s01  C*                  IF        PAID <> 'TIDUT'
     C                   MOVE      BIBRID        PABRID
     C     'ESPEDPAR'    CHAIN     TELLGE                             34
N03  C                   IF        *IN34
N03  C                   EVAL      PARECN = 1000
N03  C                   EVAL      GECO = 'ESPEDPAR'
N03  C                   EVAL      GENO = 1001
N03  C                   WRITE     TELLGER
N03  C                   ELSE
     C                   MOVE      GENO          PARECN
     C                   ADD       1             GENO
     C                   UPDATE    TELLGER
N03  C                   END
     C                   WRITE     BRPARFR
s01  C*                  END
     C     XXBRID        READE(N)  BRPARF                                 33
     C  N33              ENDDO
      *
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTBI         BEGSR
     C***********************************************
     C     BIBRID        CHAIN(N)  BRIDF                              33
     C                   MOVE      BIDTVD        WSDTVD
     C                   MOVE      BIDTVM        WSDTVM
     C                   MOVE      BIDTVÅ        WSDTVÅ
     C                   MOVE      BIDTFD        WSDTFD
     C                   MOVE      BIDTFM        WSDTFM
     C                   MOVE      BIDTFÅ        WSDTFÅ
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     BIBRID        CHAIN     BRIDF                              33
     C  N33              DELETE    BRIDFR
s02  C*                  IF        *IN33
N02  C                   IF        *IN33='0'
     C     BIBRID        SETLL     BRPARF
     C     BIBRID        READE     BRPARF                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    BRPARFR
s02  C*    BIBRID        READE(N)  BRPARF                                 33
N02  C     BIBRID        READE     BRPARF                                 33
     C  N33              ENDDO
     C                   END
     C                   EXSR      MOVB1
     C                   MOVE      '0'           *IN32
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPBI         BEGSR
     C***********************************************
     C     BIBRID        CHAIN(N)  BRIDF
     C                   MOVE      BIDTVD        WSDTVD
     C                   MOVE      BIDTVM        WSDTVM
     C                   MOVE      BIDTVÅ        WSDTVÅ
     C                   MOVE      BIDTFD        WSDTFD
     C                   MOVE      BIDTFM        WSDTFM
     C                   MOVE      BIDTFÅ        WSDTFÅ
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     WRKPAR        BEGSR
     C***********************************************
     C                   CALL      'SYES03R'
     C                   PARM                    BIBRID
     C                   PARM                    BIBESK
     C                   PARM                    UFLAGG            1 0
      *
     C     UFLAGG        COMP      1                                      12
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWBI        BEGSR
     C***********************************************
     C     *IN33         IFEQ      '1'
     C                   CLEAR                   BRIDFR
     C                   END
     C                   MOVE      WSBRID        BIBRID
     C                   MOVE      WSBESK        BIBESK
     C                   MOVE      WSAKT         BIAKT
     C                   MOVE      WSPO          BIPO
N06  C                   MOVE      WSPOL         BIPOL
     C                   MOVE      WSFIRM        BIFIRM
     C                   MOVE      WSSTDL        BISTDL
     C                   MOVE      WSLIBL        BILIBL
     C                   MOVE      WSWELC        BIWELC
     C                   MOVE      WSKUNR        BIKUNR
     C                   MOVE      WSINN         BIINN
     C                   MOVE      WSFARG        BIFARG
     C                   MOVE      WSKNG         BIKNG
     C                   MOVE      WSEIER        BIEIER
     C                   MOVE      WSANTF        BIANTF
S01  C*                  MOVE      WSTUT         BITUT
N01  C                   MOVE      *ZEROS        BITUT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVBIW        BEGSR
     C***********************************************
     C                   MOVE      BIBRID        WSBRID
     C                   MOVE      BIBESK        WSBESK
     C                   MOVE      BIAKT         WSAKT
     C                   MOVE      BIPO          WSPO
N06  C                   MOVE      BIPOL         WSPOL
     C                   MOVE      BIFIRM        WSFIRM
     C                   MOVE      BISTDL        WSSTDL
     C                   MOVE      BILIBL        WSLIBL
     C                   MOVE      BIWELC        WSWELC
     C                   MOVE      BIKUNR        WSKUNR
     C                   MOVE      BIINN         WSINN
     C                   MOVE      BIFARG        WSFARG
     C                   MOVE      BIKNG         WSKNG
     C                   MOVE      BIEIER        WSEIER
     C                   MOVE      BIANTF        WSANTF
     C                   MOVE      BITIDV        WSTIDV
     C                   MOVE      BITIDF        WSTIDF
S01  C*                  MOVE      BITUT         WSTUT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   CLEAR                   BRIDFR
     C                   MOVEL     '*DLT'        BIBRID
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSBRID
     C                   MOVE      *BLANKS       WSBESK
     C                   MOVE      *BLANKS       WSAKT
N06  C                   MOVE      *BLANKS       WSPOL
     C                   MOVE      FIFIRM        WSFIRM
     C                   MOVE      *BLANKS       WSSTDL
     C                   MOVE      *BLANKS       WSLIBL
     C                   MOVE      *BLANKS       WSWELC
     C                   MOVE      *ZEROS        WSKUNR
     C                   MOVE      *BLANKS       WSINN
     C                   MOVE      *BLANKS       WSFARG
     C                   MOVE      *ZEROS        WSKNG
     C                   MOVE      *BLANKS       WSEIER
     C                   MOVE      *ZEROS        WSDTV
     C                   MOVE      *ZEROS        WSTIDV
     C                   MOVE      *ZEROS        WSDTF
     C                   MOVE      *ZEROS        WSTIDF
S01  C*                  MOVE      *ZEROS        WSTUT
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     14
     C     POSNBR        ANDLE     21
     C                   MOVE      *BLANKS       USØKKN
     C                   CALL      'SOEKKUN'
     C     WSKUNR        PARM      WSKUNR        UKN               8 0
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     48            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     48
     C     POSNBR        ANDLE     55
     C     *IN04         andeq     '1'
     C                   MOVE      *BLANKS       USØKKN
     C                   CALL      'SOEKKUN'
     C     WSKNG         PARM      WSKNG         UKN               8 0
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     *IN09         IFEQ      '1'
     C                   MOVE      'J'           UUPDAT
     C                   MOVEL     'ESPEDKUGRP'  XKFTYP
     C                   MOVE      *BLANKS       XKFKOD
     C                   CALL      'SYKUKD'
     C                   PARM                    XKFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    XKFKOD
     c     XKFKOD        IFGE      '00000'
     c     XKFKOD        andle     '99999'
     C                   MOVEL     '999'         KNR8A             8
     C                   MOVE      XKFKOD        KNR8A
     C                   MOVE      KNR8A         WSKNG
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   end
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     14
     C     POSNBR        ANDLE     23
     C                   MOVE      'J'           UUPDAT
     C                   MOVEL     'ESPEDFARGE'  XKFTYP
     C                   MOVE      *BLANKS       XKFKOD
     C                   CALL      'SYKUKD'
     C                   PARM                    XKFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    XKFKOD
     c     XKFKOD        IFNE      '     '
     C     KUFASK        CHAIN     KUFAST                             33
     C  N33              MOVEL     KFTXT         WSFARG
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   end
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     SØKDTA2       BEGSR
     C***********************************************
     C     *IN04         IFEQ      '1'
     C                   MOVE      *BLANKS       USØKKN
     C                   CALL      'SOEKKUN'
     C                   PARM                    W2KUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     53            POSNBR
     C                   GOTO      SLSØK2
     C                   end
      *
     C     *IN09         IFEQ      '1'
     C                   MOVE      'J'           UUPDAT
     C                   MOVEL     'ESPEDKUGRP'  XKFTYP
     C                   MOVE      *BLANKS       XKFKOD
     C                   CALL      'SYKUKD'
     C                   PARM                    XKFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    XKFKOD
     c     XKFKOD        IFGE      '00000'
     c     XKFKOD        andle     '99999'
     C                   MOVEL     '999'         KNR8A             8
     C                   MOVE      XKFKOD        KNR8A
     C                   MOVE      KNR8A         W2KNG
     C                   end
     C                   Z-ADD     64            POSNBR
     C                   GOTO      SLSØK2
     C                   end
      *
     C     SLSØK2        ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUN1KK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     CUN1KG        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNG
     C     KUFASK        KLIST
     C                   KFLD                    XKFTYP
     C                   KFLD                    XKFKOD
     C     *LIKE         DEFINE    KFTYP         XKFTYP
     C     *LIKE         DEFINE    KFKOD         XKFKOD
S01  C*    BRPARFK       KLIST
S01  C*                  KFLD                    BIBRID
S01  C*                  KFLD                    XXKUNR
S01  C*                  KFLD                    XXID
S01  C*    *LIKE         DEFINE    PAKUNR        XXKUNR
S01  C*    *LIKE         DEFINE    PAID          XXID
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   99
N06   * Firmaverdi for krav om langt passord
N06  C     SyparKey      KLIST
N06  C                   KFLD                    XXPAKN            8 0
N06  C                   KFLD                    XXPAID            5
N06  C                   move      *zeros        XXPAKN
N06  C                   movel     'WPASL'       XXPAID
N06  c     SyparKey      chain     SYPARL1                            33
N06  c                   if        %found
N06  c                   movel     SYVRDA        PASLKRAV          1
N06  c                   endif
     c
      *initier numeriske datastrukturer
     C                   Move      *zeros        WSDTV
     C                   Move      *zeros        BIDTV
     C                   Move      *zeros        WSDTF
     C                   Move      *zeros        BIDTF
S01  C*                  MOVE      'TIDUT'       XXID
      *
     C                   ENDSR
      *
