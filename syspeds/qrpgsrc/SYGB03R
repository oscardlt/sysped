      *
      ***************************************************************
      *** LAGER WORKFILE (RAPPORTGRUNNLAG)                        ***
      ***                                                         ***
      *** PROGRAMMERT AV TORE BRÅSTAD, SYSTEMA A/S, APRIL 1994    ***
      ***************************************************************
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER        ***
      *** LEDIGE INDIKATORER 01-99                                ***
      ***************************************************************
      *
     FFAKGB     IF   E           K DISK
     FFAKGB2    IF   E           K DISK
     F                                     RENAME(FAKTR:FAKTR2)
     FGBREGLF   IF   E           K DISK
     FG1WREG    O    E           K DISK
     D                 DS
     D  LESPER                 1      6  0
     D  FACC                   1      2  0
     D  FAÅR                   3      4  0
     D  FAMND                  5      6  0
     D                 DS
     D  TILPER                 1      6  0
     D  WSTÅRH                 1      2  0
     D  WSTAAR                 3      4  0
     D  WSTPER                 5      6  0
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UFPER             2
     C                   PARM                    UFAAR             2
     C                   PARM                    UTPER             2
     C                   PARM                    UTAAR             2
     C*
     C                   MOVE      UAVD          WSAVD             4 0
     C                   MOVE      UFPER         WSFPER            2 0
     C                   MOVE      UFAAR         WSFAAR            2 0
     C     WSFAAR        IFGT      50
     C                   MOVE      '19'          WSFÅRH            2 0
     C                   ELSE
     C                   MOVE      '20'          WSFÅRH
     C                   END
     C                   MOVE      UTPER         WSTPER            2 0
     C                   MOVE      UTAAR         WSTAAR            2 0
     C     WSTAAR        IFGT      50
     C                   MOVE      '19'          WSTÅRH
     C                   ELSE
     C                   MOVE      '20'          WSTÅRH
     C                   END
     C*
     C     LESKEY        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSFÅRH
     C                   KFLD                    WSFAAR
     C                   KFLD                    WSFPER
     C                   KFLD                    WSOPD             7 0
     C                   KFLD                    WSAVK             3
     C*
     C     LESKY2        KLIST
     C                   KFLD                    WSFÅRH
     C                   KFLD                    WSFAAR
     C                   KFLD                    WSFPER
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C                   KFLD                    WSAVK
     C*
     C     GBKEY         KLIST
     C                   KFLD                    AVD               4 0
     C                   KFLD                    FAVK
     C*
     C     WSAVD         IFEQ      0
     C                   SETON                                        50
     C                   END
     C*
     C     START         TAG
     C   50LESKY2        SETLL     FAKTR2
     C  N50LESKEY        SETLL     FAKTR
     C   50              READ      FAKTR2                                 10
     C  N50              READ      FAKTR                                  10
     C     *IN10         DOWEQ     '0'
     C     FAKDA         IFNE      'D'
     C                   EXSR      SJEKK
     C     *IN30         IFEQ      '1'
     C                   Z-ADD     FAAVD         G1AVD
     C                   Z-ADD     FAOPD         G1OPD
     C                   MOVEL     FAVK          G1AVK
     C                   MOVEL     FAKDM         G1MVA
     C                   MOVEL     FAKDA         G1KDA
     C                   Z-ADD     FABELN        G1BELN
     C                   WRITE     G1WRR
     C                   END
     C                   END
     C   50              READ      FAKTR2                                 10
     C  N50              READ      FAKTR                                  10
     C     WSAVD         IFNE      0
     C     FAAVD         IFNE      WSAVD
     C                   SETON                                        10
     C                   END
     C                   END
     C     LESPER        IFGT      TILPER
     C                   SETON                                        10
     C                   END
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***************************************************************
     C*
     C     SJEKK         BEGSR
     C*
     C                   Z-ADD     FAAVD         AVD
     C     GBKEY         CHAIN     GBREGR                             20
     C     *IN20         IFEQ      '0'
     C                   SETON                                        30
     C                   ELSE
     C                   Z-ADD     0             AVD
     C     GBKEY         CHAIN     GBREGR                             21
     C     *IN21         IFEQ      '0'
     C                   SETON                                        30
     C                   ELSE
     C                   SETOFF                                       30
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
