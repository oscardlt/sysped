      *********************************************************************
      * Vis manko på sending
      *********************************************************************
      *  RPG ILE MODULE *LIBL/STS15
      *
CRTPG+*  CRTPGM SYSPED/STS15 MODULE(*LIBL/STS15 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FHENOTW    IF   E           K DISK    USROPN
     FHENOKW3   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D WSUSER          s             10
     D WSSNDN          s             20
     D TIDOT           s             10
     D lastWSID        s             20
     D lstbrd          s              1
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D  XXREF                  1     30
     D  XUSER                  1     10
     D  XSNDN                 11     30
     D                 DS
     D  WSNR                   1     14
     D  WSANTN                 1      5  0
     D  WSAV                   7      8
     D  HSANT                 10     14  0
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      *
      * Parse input
     C                   exsr      Parse
     C                   EXSR      INIT
     C     WSUSER        CHAIN     BRIDF                              60
     C                   SELECT
     C                   WHEN      *IN60
     C                   exsr      SetSkl01B
     C                   WHEN      BIINN = ' '
     C                   exsr      SetSkl01B
     C                   OTHER
     C                   exsr      SetSkl
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *------------------                                                   ******
      * Go to the last Recd already shown (1. time = from top)
     C                   exsr      GotoRecord
      *------------------                                                   ******
      * Find number of Recds matching selection criteria
     C                   exsr      Recdsnumb
      *------------------                                                   ******
      * Write HTML sections
      *
      * 2-Section /$howmany
     C                   exsr      SetHowMany
      *
     C                   callp     wrtsection('howmany')
      * 3-Section /$tablestr
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      * 4-Sections /$tablerow
      *           go to the last Recd already shown
     C                   exsr      GotoRecord
     C                   z-add     0             bseq              2 0
     C     HENOKWK       SETLL     HENOKW3
     C     HENOKWK       READE     HENOKW3                                50
     C     *in50         doweq     '0'
     C                   ADD       1             WSANTN
     C     HKSTIN        IFEQ      ' '
     C                   eval      bseq=bseq + 1
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C                   ENDIF
     C     HENOKWK       READE     HENOKW3                                50
     C  N50              ENDDO                                                  *hival
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      * 5a-Section /$foundsome
     C                   exsr      SetFndSome
     C                   callp     wrtsection('foundsome')
      * 6-Section /$endhtml
     C                   exsr      SetEnd
     C                   callp     wrtsection('endhtml')
      *
     C                   ENDSL
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      LastWSID = zhbgetvar('lastWSID')
     C                   eval      XXREF    = zhbgetvar('XXREF')
     C                   MOVE      XUSER         WSUSER
     C                   MOVE      XSNDN         WSSNDN
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      * Color for text, background, links, visited links, active links
     C                   exsr      setcolor
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('WSSNDN':WSSNDN)
     C     HENOKWK       SETLL     HENOKW3
     C     HENOKWK       READE     HENOKW3                                50
     C                   callp     updhtmlvar('WSNAS':HONAS)
     C                   callp     updhtmlvar('WSNAK':HSNAK)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$howmany
      *=====================================================================******
     C     SetHowMany    begsr
     C* Overall Recds count
     C                   callp     updhtmlvar('RecordCnt':
     C                             %trim(%editw(RecordCnt:'   0 ')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
     C     HKID          CHAIN     HENOTW                             34
     C     *in34         IFeq      *OFF
     C                   Movel     '(Tidl.ot.)'  TIDOT
     C                   else
     C                   Move      *blanks       TIDOT
     C                   end
     c* Sequence of this Recd (numeric, converted to char)
     C                   move      bseq          bseqchar          2
     C                   callp     updhtmlvar('SEQ':bseqchar)
     C                   callp     updhtmlvar('HKID':HKID)
     C                   callp     updhtmlvar('TIDOT':TIDOT)
     C                   callp     updhtmlvar('WSANTN':
     C                             %trim(%editc(WSANTN:'3')))
     C                   callp     updhtmlvar('HSANT':
     C                             %trim(%editc(HSANT:'3')))
      * Repeat background color
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome    begsr
     C                   callp     updhtmlvar('XXREF':XXREF)
      * Repeat background color
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      *
     C                   endsr
      *=====================================================================******
      * Color for font, background, links, visited links, active links
      *=====================================================================******
     C     SetColor      begsr
     C                   eval      BckGnd = uppify(BckGnd)
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('TXTCOLOR':'white')
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
     C                   endsr
      *=====================================================================******
     C*  Find number of Recds matching search criteria
      *=====================================================================******
     C     Recdsnumb     begsr
     C                   eval      RecordCnt = 0
     C     HENOKWK       SETLL     HENOKW3
     C     HENOKWK       READE     HENOKW3                                50
     C     *in50         DoWeq     '0'
     C     HKSTIN        IFEQ      ' '
     C                   eval      RecordCnt = RecordCnt + 1
     C                   ENDIF
     C     HENOKWK       READE     HENOKW3                                50
     C  N50              enddo                                                  *hival
      *  re-position to top of file
     C     HENOKWK       SETLL     HENOKW3
     C                   endsr
      *=====================================================================******
     C*  Go to the last Recd aleady shown
      *=====================================================================******
     C     GotoRecord    begsr
     C     HENOKWK       SETLL     HENOKW3
     C     LastWSID      cabeq     *BLANKS       GotoRecordX
      *
     C     Bseq          Ifne      0
     C     HENOKWK       READE     HENOKW3                                50
     C                   end
      *
     C     GotoRecordX   tag
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS15'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS01B'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C     HENOKWK       KLIST
     C                   KFLD                    WSUSER
     C                   KFLD                    WSSNDN
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN     BRIDF                              60
     C                   IF        *IN60 = *OFF
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      HENOKW3                              50
     C                   open      HENOTW                               50
     C                   END
      *
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      'AV'          WSAV
     C                   MOVE      *ZEROS        WSANTN
     C                   MOVE      *ZEROS        HSANT
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     HENOKW3
     C                   close     HENOTW                               50
     C                   eval      *inlr = *on
      *
     C                   endsr
