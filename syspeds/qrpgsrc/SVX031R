      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED GARANTI                    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 05-09 11 13-26
      *** LEDIGE INDIKATORER 29 31 32 34-85
      ***********************************************************
      *
     FSVXGH     UF A E           K DISK
     FSVXGHL01  IF   E           K DISK
     F                                     RENAME(SVXGHR:SVXGHR01)
     FSVXGD     IF A E           K DISK
     FSVXGLG    IF A E           K DISK
     FSVXKODL01 IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSVTKD     IF   E           K DISK
     FSVTVK     IF   E           K DISK
     FSVX031D   CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  TGDTÅ                  1      4  0
     D  TGDTM                  5      6  0
     D  TGDTD                  7      8  0
     D  TGDT                   1      8  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRÅ                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  TGDTRÅ                 1      4  0
     D  TGDTRM                 5      6  0
     D  TGDTRD                 7      8  0
     D  TGDTR                  1      8  0
      *
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
     D                SDS
     D  UUSR                 254    263
     D                UDS
     D  UFIRM                  1      2
      *
     C                   EXSR      INIT
      *
     C     START         TAG
      *
     C                   EXFMT     SVX031D1
     C                   MOVEA     '000000000000'*IN(88)
     C                   MOVEA     '00'          *IN(87)
     C                   MOVE      '0'           *IN30
      *
      *  04 = SØK HOVEDANSVARLIG
     C   04              DO
     C                   EXSR      SØKDT
     C                   GOTO      START
     C                   END
      *
      *  10 = SE OPPLYSNIGER OM GODKJENNELSE FRA TOLLVESENET (KUN KOMMUNIKASJON)
     C   10              DO
     C                   EXSR      VISTOL
     C                   GOTO      START
     C                   END
      *
     C   12              GOTO      SLUTT
      *
     C                   IF        UTYPE = 'DLT'
     C     WSGNR         CHAIN     SVXGH                              33
     C                   IF        TGST <> 'S'
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      TGGNR         TLGNR
     C                   MOVE      TGRN          TLRN
     C                   MOVE      UUSR          TLUSR
     C                   MOVE      WDT           TLDT
     C                   MOVE      WTM           TLTM
     C                   MOVE      TGST          TLFST
     C                   MOVE      TGGTY         TLFGTY
     C                   MOVE      TGGVK         TLFVK
     C                   MOVE      TGGBL         TLFBL
     C                   MOVE      TGGBLB        TLFBLB
     C                   MOVE      TGAKNY        TLFKNY
     C                   MOVE      TGAKGM        TLFKGM
     C                   MOVE      TGPRM         TLFPRM
     C                   MOVE      TGGFV         TLFFV
     C                   MOVE      'S'           TLEST
     C                   MOVE      TGGTY         TLEGTY
     C                   MOVE      TGGVK         TLEVK
     C                   MOVE      TGGBL         TLEBL
     C                   MOVE      TGGBLB        TLEBLB
     C                   MOVE      TGAKNY        TLEKNY
     C                   MOVE      TGAKGM        TLEKGM
     C                   MOVE      TGPRM         TLEPRM
     C                   MOVE      TGGFV         TLEFV
     C                   WRITE     SVXGLGR
     C                   END
     C                   MOVE      'S'           TGST
     C                   UPDATE    SVXGHR
     C                   GOTO      SLUTT
     C                   END
      *
     C                   IF        UTYPE <> 'DSP'
     C                   EXSR      TESTB
     C   30              GOTO      START
      *
      * ----- INGEN FEIL HITTIL     OPPDATER SVXGH -----
     C                   EXSR      OPDTRH
     C   03              GOTO      SLUTT
     C                   END
      *
      *
     C                   EXSR      TOTRV
     C   12              GOTO      START
      *
     C                   IF        UTYPE <> 'DSP'
     C                   END
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C***********************************************
     C     SØKDT         BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * HOVEDANSVARLIGNR
     C     LINNBR        IFEQ      3
     C     POSNBR        ANDGE     20
     C     POSNBR        ANDLE     27
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNA
     C                   PARM                    UFIRM
     C                   PARM                    USØKKN
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * LANDKODE TIL HOVEDANSVARLIG
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     20
     C     POSNBR        ANDLE     21
     C                   CALL      'SVT003R'
     C                   PARM                    SVKD_TYP
     C                   PARM                    SVKD_IE
     C                   PARM                    WDT
     C                   PARM                    UKD              10
     C                   IF        UKD <> *BLANKS
     C                   EVAL      WSLKA = UKD
     C                   END
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * GARANTITOLLSTED
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     20
     C     POSNBR        ANDLE     27
     C                   MOVE      '106'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSTSD         UKODE
     C                   MOVE      'AVG'         UTYPE2            3
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSTSD
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     42
     C                   CALL      'SVT007R'
     C                   PARM                    UKD              10
     C     UKD           IFNE      *BLANKS
     C                   EVAL      WSGVK = UKD
     C                   END
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
      *
     C     WSKNA         IFNE      0                                            1<-B
     C     CUNDL01K      CHAIN     CUNDL01                            99
     C     *IN99         IFEQ      '1'                                           2<-B
     C                   MOVE      'SPK0001'     MSGNR
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   ELSE                                                    2
     C                   MOVE      KNAVN         WSNAA
     C                   MOVE      ADR1          WSADA1
     C     SYPOGE        IFEQ      *BLANKS                                        3<-B
     C                   MOVE      *BLANKS       WSPNA
     C                   MOVEL     POSTNR        WSPNA
     C                   ELSE                                                     3
     C                   MOVEL     SYPOGE        WSPNA
     C                   END                                                      3<-E
     C                   MOVE      ADR3          WSPSA
     C     SYLAND        IFNE      *BLANKS                                        3<-B
     C                   MOVE      SYLAND        WSLKA
     C                   END                                                      3<-E
     C                   IF        WSTINA = *BLANKS
     C                   SELECT
     C                   WHEN      EORI = *BLANKS
     C                   IF        ((SYFOR9 <> *BLANKS) AND (SYFOR9 <> *ZEROS))
     C                   IF        SYLAND = *BLANKS
     C                   EVAL      WSTINA = 'SE' + SYFOR9
     C                   ELSE
     C                   EVAL      WSTINA = SYLAND + SYFOR9
     C                   END
     C                   END
     C                   OTHER
     C                   EVAL      WSTINA = EORI
     C                   ENDSL
     C                   END
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     WSTINA        COMP      *BLANKS                                98
     C  N30              IF        *IN98
     C                   MOVE      'SVX0812'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSNAA         COMP      *BLANKS                                97
     C  N30              IF        *IN97
     C                   MOVE      'SVX1889'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSPNA         COMP      *BLANKS                                96
     C  N30              IF        *IN96
     C                   MOVE      'SVX1890'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSPSA         COMP      *BLANKS                                95
     C  N30              IF        *IN95
     C                   MOVE      'SVX1891'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     30            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C                   IF        WSLKA = *BLANKS
     C                   MOVE      'SE'          WSLKA
     C                   ELSE
     C                   EVAL      SVKD_KD = WSLKA
     C     SVTKDK        CHAIN     SVTKD                              94
     C  N30              IF        *IN94
     C                   MOVE      'SPL0001'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C                   MOVEL     WSTSD         TKKODE
     C     SVK01K        CHAIN     SVXKODL01                          93
     C  N30              IF        *IN93
     C                   MOVE      'SVX1768'     MSGNR
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C                   IF        *IN27 = *OFF                                 1<-B
     C     WSGNR         COMP      *BLANKS                                92
     C                   IF        *IN92 = *OFF                                  2<-B
     C     WSGNR         CHAIN(N)  SVXGH                              33
     C                   IF        *IN33 = *OFF                                   3<-B
     C                   SETON                                        92
     C  N30              IF        *IN92                                           4<-B
     C                   MOVE      'SVX1892'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C  N30              IF        *IN92                                          3<-B
     C                   MOVE      'SVX1891'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     WSAKNY        COMP      *BLANKS                                91
     C  N30              IF        *IN91
     C                   MOVE      'SVX1893'     MSGNR
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSAKGM        COMP      *BLANKS                                90
     C  N30              IF        *IN90
     C                   MOVE      'SVX1895'     MSGNR
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     48            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSGBL         COMP      0                                      89
     C  N30              IF        *IN89
     C                   MOVE      'SVX1896'     MSGNR
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSGVK         CHAIN     SVTVK                              88
     C  N30              IF        *IN88
     C                   MOVE      'SPV0001'     MSGNR
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSGBL         COMP      WSGBLB                               87
     C  N30              IF        *IN88
     C                   MOVE      'SVX1897'     MSGNR
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSPRM         COMP      100                                86  86
     C  N30              IF        *IN86
     C                   MOVE      'SVX1898'     MSGNR
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVW2         BEGSR
     C***********************************************
     C                   IF        UTYPE <> 'CRT'
     C                   MOVE      TGGNR         WSGNR
     C                   MOVE      TGGTY         WSGTY
     C                   MOVE      TGGVK         WSGVK
     C                   MOVE      TGGBL         WSGBL
     C                   MOVE      TGGBLB        WSGBLB
     C                   MOVE      TGKNA         WSKNA
     C                   MOVE      TGTINA        WSTINA
     C                   MOVE      TGNAA         WSNAA
     C                   MOVE      TGADA1        WSADA1
     C                   MOVE      TGPNA         WSPNA
     C                   MOVE      TGPSA         WSPSA
     C                   MOVE      TGLKA         WSLKA
     C                   MOVE      TGTSD         WSTSD
     C                   MOVE      TGAKNY        WSAKNY
     C                   MOVE      TGAKGM        WSAKGM
     C                   MOVE      TGPRM         WSPRM
     C                   MOVE      TGUSR         WSUSR
     C                   MOVE      TGDTD         WSDTD
     C                   MOVE      TGDTM         WSDTM
     C                   MOVE      TGDTÅ         WSDTÅ
     C                   MOVE      TGDTRD        WSDTRD
     C                   MOVE      TGDTRM        WSDTRM
     C                   MOVE      TGDTRÅ        WSDTRÅ
     C                   MOVE      TGGFV         WSGFV
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVTRH        BEGSR
     C***********************************************
     C                   MOVE      WSGNR         TGGNR
     C                   MOVE      WSGTY         TGGTY
     C                   MOVE      WSGVK         TGGVK
     C                   MOVE      WSGBL         TGGBL
     C                   MOVE      WSGBLB        TGGBLB
     C                   MOVE      WSKNA         TGKNA
     C                   MOVE      WSTINA        TGTINA
     C                   MOVE      WSNAA         TGNAA
     C                   MOVE      WSADA1        TGADA1
     C                   MOVE      WSPNA         TGPNA
     C                   MOVE      WSPSA         TGPSA
     C                   MOVE      WSLKA         TGLKA
     C                   MOVE      WSTSD         TGTSD
     C                   MOVE      WSAKNY        TGAKNY
     C                   MOVE      WSAKGM        TGAKGM
     C                   MOVE      WSPRM         TGPRM
     C                   MOVE      WSUSR         TGUSR
     C                   MOVE      WSGFV         TGGFV
     C                   MOVE      WDT           TGDT
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPDTRH        BEGSR
     C***********************************************
     C                   IF        *IN27 = *OFF
     C     XMINRN        SETLL     SVXGHL01
     C                   READ      SVXGHL01                               33
     C                   IF        *IN33
     C                   Z-SUB     1             XGRN              9 0
     C                   ELSE
     C                   EVAL      XGRN = TGRN - 1
     C                   END
     C                   END
      *
     C     WSGNR         CHAIN     SVXGH                              33
      *
     C                   MOVE      'N'           XLOGG             1
     C                   SELECT
     C                   WHEN      *IN33
     C                   CLEAR                   SVXGLGR
     C                   MOVE      WSGNR         TLGNR
     C                   MOVE      XGRN          TLRN
     C                   MOVE      'J'           XLOGG
     C     WSGTY         WHENNE    TGGTY
     C     WSGVK         ORNE      TGGVK
     C     WSGBL         ORNE      TGGBL
     C     WSGBLB        ORNE      TGGBLB
     C     WSAKNY        ORNE      TGAKNY
     C     WSAKGM        ORNE      TGAKGM
     C     WSPRM         ORNE      TGPRM
     C     WSGFV         ORNE      TGGFV
     C                   MOVE      TGGNR         TLGNR
     C                   MOVE      TGRN          TLRN
     C                   MOVE      TGST          TLFST
     C                   MOVE      TGGTY         TLFGTY
     C                   MOVE      TGGVK         TLFVK
     C                   MOVE      TGGBL         TLFBL
     C                   MOVE      TGGBLB        TLFBLB
     C                   MOVE      TGAKNY        TLFKNY
     C                   MOVE      TGAKGM        TLFKGM
     C                   MOVE      TGPRM         TLFPRM
     C                   MOVE      TGGFV         TLFFV
     C                   MOVE      TGST          TLEST
     C                   MOVE      WSGTY         TLEGTY
     C                   MOVE      WSGVK         TLEVK
     C                   MOVE      WSGBL         TLEBL
     C                   MOVE      WSGBLB        TLEBLB
     C                   MOVE      WSAKNY        TLEKNY
     C                   MOVE      WSAKGM        TLEKGM
     C                   MOVE      WSPRM         TLEPRM
     C                   MOVE      WSGFV         TLEFV
     C                   MOVE      'J'           XLOGG
     C                   ENDSL
     C                   IF        XLOGG = 'J'
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      UUSR          TLUSR
     C                   MOVE      WDT           TLDT
     C                   MOVE      WTM           TLTM
     C                   WRITE     SVXGLGR
     C                   END
      *
     C                   EXSR      MOVTRH
      *
     C                   IF        *IN33
     C                   MOVE      ' '           TGST
     C                   MOVE      XGRN          TGRN
     C                   MOVE      WDT           TGDTR
     C                   MOVE      TGDTRD        WSDTRD
     C                   MOVE      TGDTRM        WSDTRM
     C                   MOVE      TGDTRÅ        WSDTRÅ
     C                   WRITE     SVXGHR
     C                   MOVE      TGGNR         UGNR
     C  N27              IF        UTYPE = 'CPY'
     C                   EXSR      CPYTRV
     C                   END
     C                   MOVE      *ON           *IN27
     C                   ELSE
     C                   UPDATE    SVXGHR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYTRV        BEGSR
     C***********************************************
     C     XGNRG         SETLL     SVXGD
     C     XGNRG         READE(N)  SVXGD                                  33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      TGGNR         TDGNR
     C                   WRITE     SVXGDR
     C     XGNRG         READE(N)  SVXGD                                  33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     TOTRV         BEGSR
     C***********************************************
      *
     C                   CALL      'SVX032R'
     C                   PARM                    TGGNR
     C                   PARM                    TGAKNY
     C                   PARM                    UTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C     UFLAGG        COMP      1                                      12
     C     UFLAGG        COMP      3                                      03
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISTOL        BEGSR
     C***********************************************
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   MOVEL     TGRN          UAVD              4 0
     C                   MOVE      TGRN          UTDN              7 0
     C                   MOVE      '2'           WIN03             1
     C                   CALL      'SAD003'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    WIN03
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUNDL01K      KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSKNA
     C     SVK01K        KLIST
     C                   KFLD                    TKUNIK
     C                   KFLD                    TKKODE
     C                   MOVE      '106'         TKUNIK
     C     SVTKDK        KLIST
     C                   KFLD                    SVKD_IE
     C                   KFLD                    SVKD_TYP
     C                   KFLD                    SVKD_KD
     C                   MOVE      'GCY'         SVKD_TYP
     C                   MOVE      'A'           SVKD_IE
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGNR             24
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           UDT               8 0
     C     UTYPE         COMP      'CHG'                                  27
     C     UTYPE         COMP      'DSP'                                  28
     C  N28UTYPE         COMP      'DLT'                                  28
     C                   IF        UTYPE = 'CRT'
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSDTR
     C                   ELSE
     C     UGNR          CHAIN(N)  SVXGH                              33
     C                   EXSR      MOVW2
     C                   IF        UTYPE = 'CPY'
     C                   MOVE      UGNR          XGNRG            24
     C                   MOVE      *BLANKS       UGNR
     C                   MOVE      *BLANKS       WSGNR
     C                   MOVE      *ZEROS        WSGBL
     C                   MOVE      *ZEROS        WSGBLB
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSDTR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   END
     C                   END
     C                   MOVE      UUSR          WSUSR
     C                   Z-SUB     99999999999   XMINRN           11 0
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
