********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10199 * 01 YBC PTF10 TO NYE DATO FOR GATE IN OG OUT
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE BILL OF LADING            ***
      ***        VARELINJER (TYPE-1)                          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07-10 14 16-24          ***
      *** LEDIGE INDIKATORER 28 31 32 34-39 41-86 95-97       ***
      ***********************************************************
      *
     FBLV1      UF A E           K DISK
N01  FBLV1GATE  UF A E           K DISK
     FBLV1LF    IF   E           K DISK
     F                                     RENAME(BLV1R:BLV1RLF)
     FBLV2      IF   E           K DISK
     FCDCON4    IF   E           K DISK
     FSYSJ50FE  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BVMN                   1     17
     D  XDLT                   1      7
N01  D                 DS
N01  D  BVGTIN                 1      8  0
N01  D  BVGTINA                1      4  0
N01  D  BVGTINM                5      6  0
N01  D  BVGTIND                7      8  0
N01  D                 DS
N01  D  BVGTUT                 1      8  0
N01  D  BVGTUTA                1      4  0
N01  D  BVGTUTM                5      6  0
N01  D  BVGTUTD                7      8  0
N01  D                 DS
N01  D  WSGTIN                 1      6  0
N01  D  WSGTIND                1      2  0
N01  D  WSGTINM                3      4  0
N01  D  WSGTINA                5      6  0
N01  D                 DS
N01  D  WSGTUT                 1      6  0
N01  D  WSGTUTD                1      2  0
N01  D  WSGTUTM                3      4  0
N01  D  WSGTUTA                5      6  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   Z-ADD     *ZEROS        MAXRCN            4 0
     C                   MOVE      *ZEROS        XNESTE            3 0
     C                   MOVE      XANTL2        XANTL             3 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     BLV1K1        SETLL     BLV1LF
     C     BLV1K1        READE     BLV1LF                                 94
      *
     C  N94              DO
     C                   DO        999
     C                   ADD       1             ZRECNO
     C     BVLI          IFGT      XNESTE
     C                   MOVE      BVLI          XNESTE
     C                   END
N01  C                   MOVE      BVMN          XBVMN            17
N01  C     BLV1GATEK     CHAIN(N)  BLV1GATE                           33
N01  C   33              MOVE      *ZEROS        BVGTIN
N01  C   33              MOVE      *ZEROS        BVGTUT
N01  C                   MOVE      XBVMN         BVMN
     C                   WRITE     SUBFIL
     C     BLV1K1        READE     BLV1LF                                 94
     C  N94              END
     C                   END
     C                   ADD       1             XNESTE
     C                   Z-ADD     ZRECNO        MAXRCN
     C     XANTL         ADD       ZRECNO        XANTL
     C     XANTL         COMP      10                                 29
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     XNESTE        YLI
     C     XNESTE        IFLE      900
     C     XNESTE        MULT      10            WSSORT
     C                   ELSE
     C                   MOVE      XNESTE        WSSORT
     C                   END
     C                   WRITE     NYVARE
N01  C  N12              MOVE      *ZEROS        WSGTIN
N01  C  N12              MOVE      *ZEROS        WSGTUT
     C  N12              Z-ADD     22            LINNBR
     C  N12              Z-ADD     2             POSNBR
     C  N12              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   EXFMT     BUNN
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY AVDELING
     C   06              DO
     C                   Z-ADD     XNESTE        YLI
     C     XNESTE        IFLE      900
     C     XNESTE        MULT      10            WSSORT
     C                   ELSE
     C                   MOVE      XNESTE        WSSORT
     C                   END
N01  C                   MOVE      *ZEROS        WSGTIN
N01  C                   MOVE      *ZEROS        WSGTUT
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START5
     C                   END
     C*** TIL TYPE 2
     C   11              Z-ADD     2             UFLAGG
     C   11              GOTO      SLUTT
     C*** AVSLUTT
     C   12              Z-ADD     0             UFLAGG
     C   12              GOTO      SLUTT
     C*** TIL SIDE 2
     C   13              DO
     C                   CALL      'SYSJ50G'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UDTR
     C                   PARM                    USG
     C                   PARM                    UUKE
     C                   PARM                    UAAR
     C                   PARM                    UFLAGG
     C                   GOTO      START3
     C                   END
     C*** AVSLUTT OG GÅ TIL OPPDRAG
     C   15              DO
     C     BLK           SETLL     BLV1
     C     BLK           READE(N)  BLV1                                   30
     C   30              MOVE      'DIV4241'     MSGNR
     C   30              GOTO      START3
     C                   Z-ADD     5             UFLAGG
     C                   GOTO      SLUTT
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   ADD       15            ZRECNO
     C     ZRECNO        IFGT      MAXRCN
     C                   Z-ADD     MAXRCN        ZRECNO
     C                   END
     C                   SETOFF                                       25
     C                   GOTO      START3
     C                   END
     C*** BLA TILBAKE
     C   26              DO
     C                   SUB       15            ZRECNO
     C     ZRECNO        IFLE      0
     C     MAXRCN        IFEQ      0
     C                   Z-ADD     0             ZRECNO
     C                   ELSE
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   END
     C                   SETOFF                                       26
     C                   GOTO      START3
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
S01  C*                  MOVEA     '00'          *IN(89)
N01  C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                           30
     C   04              DO
     C                   EXSR      SØKKD
     C                   GOTO      START5
     C                   END
     C   12              DO
     C                   EXSR      MOVB2
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     SUBCTL
     C                   Z-ADD     XNESTE        YLI
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START5
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     WSMN          COMP      *BLANKS                                90
     C   90WSNT          COMP      0                                      90
     C   90WSKD          COMP      *BLANKS                                90
     C   90WSVS          COMP      *BLANKS                                90
     C   90WSVKT         COMP      0                                      90
     C   90WSM3          COMP      0                                      90
     C   90              DO
     C                   MOVE      'DIV3174'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETON                                        30
     C                   END
     C*
     C     WSKD          IFEQ      *BLANKS
     C     WSKD          OREQ      'TA'
     C                   MOVE      *ZEROS        WSENH             1 0
     C                   ELSE
     C     WSKD          CHAIN     CDCON4                             89
     C  N89              MOVEL     CC4FT         WSVS
     C  N89              MOVE      CC4NT         WSENH
     C   89
     CANN30              DO
     C                   MOVE      'SPK0131'     MSGNR
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
N01   * TEST GATE OUT
N01  C                   IF        WSGTUT <> 0
N01  C                   MOVE      WSGTUTA       UAA               2 0
N01  C                   MOVE      WSGTUTM       UMM               2 0
N01  C                   MOVE      WSGTUTD       UDD               2 0
N01  C                   CALL      'TESTDATO'
N01  C                   PARM                    UDD
N01  C                   PARM                    UMM
N01  C                   PARM                    UAA
N01  C                   PARM                    UFLAGG            1 0
N01  C                   IF        UFLAGG = 1
N01  C                   EVAL      *IN88 = *ON
N01  C                   IF        *IN30 = *OFF
N01  C                   MOVE      'SPD0001'     MSGNR
N01  C                   EVAL      *IN30 = *ON
N01  C                   Z-ADD     23            LINNBR
N01  C                   Z-ADD     54            POSNBR
N01  C                   END
N01  C                   END
N01  C                   END
N01   * TEST GATE IN
N01  C                   IF        WSGTIN <> 0
N01  C                   MOVE      WSGTINA       UAA               2 0
N01  C                   MOVE      WSGTINM       UMM               2 0
N01  C                   MOVE      WSGTIND       UDD               2 0
N01  C                   CALL      'TESTDATO'
N01  C                   PARM                    UDD
N01  C                   PARM                    UMM
N01  C                   PARM                    UAA
N01  C                   PARM                    UFLAGG            1 0
N01  C                   IF        UFLAGG = 1
N01  C                   EVAL      *IN87 = *ON
N01  C                   IF        *IN30 = *OFF
N01  C                   MOVE      'SPD0001'     MSGNR
N01  C                   EVAL      *IN30 = *ON
N01  C                   Z-ADD     23            LINNBR
N01  C                   Z-ADD     61            POSNBR
N01  C                   END
N01  C                   END
N01  C                   END
     C*
     C   30              GOTO      SLTB1
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     BLV1K         CHAIN     BLV1                               33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWBV
     C  N33              UPDATE    BLV1R
     C   33              WRITE     BLV1R
N01   *
N01  C                   MOVE      BVMN          XBVMN            17
N01  C                   MOVE      BVGTIN        XBVGTIN           8 0
N01  C                   MOVE      BVGTUT        XBVGTUT           8 0
N01  C     BLV1GATEK     CHAIN     BLV1GATE                           34
N01  C                   MOVE      XBVMN         BVMN
N01  C                   MOVE      XBVGTIN       BVGTIN
N01  C                   MOVE      XBVGTUT       BVGTUT
N01  C                   IF        *IN34
N01  C                   WRITE     BLV1GTR
N01  C                   ELSE
N01  C                   UPDATE    BLV1GTR
N01  C                   END
N01   *
     C   33              ADD       1             XNESTE
     C   33              ADD       1             XANTL
     C   33XANTL         COMP      10                                 29
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             MAXRCN
     C  N40              Z-ADD     MAXRCN        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGBV
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTBV
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGBV         BEGSR
     C***********************************************
     C                   EXSR      MOVBVW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
S01  C*                  MOVEA     '00'          *IN(89)
N01  C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                         30
     C   04              DO
     C                   EXSR      SØKKD
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTBV         BEGSR
     C***********************************************
     C                   SETON                                        99
N01  C                   MOVE      BVGTINA       WSGTINA
N01  C                   MOVE      BVGTINM       WSGTINM
N01  C                   MOVE      BVGTIND       WSGTIND
N01  C                   MOVE      BVGTUTA       WSGTUTA
N01  C                   MOVE      BVGTUTM       WSGTUTM
N01  C                   MOVE      BVGTUTD       WSGTUTD
     C                   EXFMT     VAREL3
     C  N12              DO                                                     1<-B
     C                   MOVE      BVLI          YLI
     C     BLV1K         CHAIN     BLV1                               33
     C  N33              DO                                                      2<-B
     C                   DELETE    BLV1R
N01  C     BLV1GATEK     CHAIN     BLV1GATE                           33
N01  C  N33              DELETE    BLV1GTR
     C                   SUB       1             XANTL
     C     XANTL         COMP      10                                 29
     C                   ADD       1             YLI
     C     YLI           IFEQ      XNESTE                                         3<-B
     C                   SUB       1             XNESTE
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   EXSR      MOVB1
     C                   END                                                    1<-E
     C*
     C     XLIN          IFEQ      0                                            1<-B
     C                   Z-ADD     15            LINNBR
     C                   ELSE                                                   1
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    1<-E
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWBV        BEGSR
     C***********************************************
     C                   MOVE      UAVD          BVAVD
     C                   MOVE      UOPD          BVOPD
     C                   MOVE      YLI           BVLI
     C                   MOVE      WSSORT        BVSORT
     C                   MOVE      WSMN          BVMN
     C                   MOVE      WSNT          BVNT
     C                   MOVE      WSVS          BVVS
     C                   MOVE      WSVKT         BVVKT
     C                   MOVE      WSM3          BVM3
     C                   MOVE      WSKD          BVKDC
     C                   MOVE      WSENH         BVENH
N01  C                   IF        WSGTIN = 0
N01  C                   EVAL      BVGTIN = 0
N01  C                   ELSE
N01  C                   EVAL      BVGTIN = 20000000 +
N01  C                             WSGTINA * 10000 + WSGTINM * 100 + WSGTIND
N01  C                   END
N01  C                   IF        WSGTUT = 0
N01  C                   EVAL      BVGTUT = 0
N01  C                   ELSE
N01  C                   EVAL      BVGTUT = 20000000 +
N01  C                             WSGTUTA * 10000 + WSGTUTM * 100 + WSGTUTD
N01  C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBVW        BEGSR
     C***********************************************
     C                   MOVE      BVLI          YLI
     C                   MOVE      BVSORT        WSSORT
     C                   MOVE      BVMN          WSMN
     C                   MOVE      BVNT          WSNT
     C                   MOVE      BVVS          WSVS
     C                   MOVE      BVVKT         WSVKT
     C                   MOVE      BVM3          WSM3
     C                   MOVE      BVKDC         WSKD
     C                   MOVE      BVENH         WSENH
N01  C                   MOVE      BVGTINA       WSGTINA
N01  C                   MOVE      BVGTINM       WSGTINM
N01  C                   MOVE      BVGTIND       WSGTIND
N01  C                   MOVE      BVGTUTA       WSGTUTA
N01  C                   MOVE      BVGTUTM       WSGTUTM
N01  C                   MOVE      BVGTUTD       WSGTUTD
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        BVSORT
     C                   MOVE      *BLANKS       BVMN
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *ZEROS        BVNT
     C                   MOVE      *BLANKS       BVVS
     C                   MOVE      *ZEROS        BVVKT
     C                   MOVE      *ZEROS        BVM3
     C                   MOVE      *BLANKS       BVKDC
     C                   MOVE      *ZEROS        BVENH
N01  C                   MOVE      *ZEROS        BVGTIN
N01  C                   MOVE      *ZEROS        BVGTUT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C     XNESTE        IFLE      900
     C     XNESTE        MULT      10            WSSORT
     C                   ELSE
     C                   MOVE      XNESTE        WSSORT
     C                   END
     C                   MOVE      *BLANKS       WSMN
     C                   MOVE      *ZEROS        WSNT
     C                   MOVE      *BLANKS       WSKD
     C                   MOVE      *BLANKS       WSVS
     C                   MOVE      *ZEROS        WSVKT
     C                   MOVE      *ZEROS        WSM3
     C                   MOVE      *BLANKS       WSENH
N01  C                   MOVE      *ZEROS        WSGTIN
N01  C                   MOVE      *ZEROS        WSGTUT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKD         BEGSR
     C***********************************************
     C                   CALL      'SYSJ02X'
     C                   PARM                    WSKD
     C     WSKD          IFNE      *BLANKS
     C     WSKD          CHAIN     CDCON4                             89
     C  N89              MOVEL     CC4FT         WSVS
     C                   END
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     58            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     BLK           KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BLV1K         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    YLI
N01  C     BLV1GATEK     KLIST
N01  C                   KFLD                    UAVD
N01  C                   KFLD                    UOPD
N01  C                   KFLD                    BVLI
     C     BLV1K1        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BLV2K         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    YLI
     C                   KFLD                    YLI2
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UDTR              6 0
     C                   PARM                    USG               3
     C                   PARM                    UUKE              2 0
     C                   PARM                    UAAR              2 0
     C                   PARM                    UFLAGG            1 0
     C*** UFLAGG = 0  -->  FRA LOKAL B/L
     C*** UFLAGG = 1  -->  FRA B/L (ÅPNE MULIGHET Å GÅ TIL OPPDRAG)
     C     UFLAGG        COMP      1                                      27
     C*
     C                   MOVE      *ZEROS        YLI               3 0
     C                   MOVE      *ZEROS        YLI2              1 0
     C*
     C*** TELLER ANTALL LINJER AV TYPE 2
     C     BLV2K         SETLL     BLV2
     C     STINI         TAG
     C     UAVD          READE     BLV2                                   33
     C  N33UOPD          COMP      BVOPD                              3333
     C  N33YLI           IFNE      BVLI
     C                   ADD       4             XANTL2            3 0
     C                   MOVE      BVLI          YLI
     C                   END
     C  N33              GOTO      STINI
     C*
     C                   MOVE      *ZEROS        YLI
     C*
     C                   ENDSR
     C*
