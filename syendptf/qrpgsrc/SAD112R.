      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KURERMANIFEST - LISTE     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     FSADKMCL1  UF   E           K DISK
     FSADKMCL2  IF   E           K DISK    RENAME(SADKMCFR:SADKMFCR2)
     F                                     PREFIX(M2_)
     FSAD112D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  MCTRF                  1     70
     D  MCTRF_35               1     35
     D                SDS
     D  UUSR                 254    263
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UPRO          SETLL     SADKMCL1
     C     START1        TAG
     C     UPRO          READE(N)  SADKMCL1                               94
      *
     C     START2        TAG
      *
     C                   DOW       *IN94 = *OFF
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     UPRO          READE(N)  SADKMCL1                               94
     C                   ENDDO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
      *
     C                   IF        ZRECNO > 19
     C                   EVAL      ZRECNO = 1
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   EXFMT     SUBCTL
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C     START4        TAG
     C   06              DO
     C                   EXSR      LAGOPD
     C                   GOTO      START
     C                   ENDDO
     C   16              DO
     C                   EXSR      PLKOPD
     C                   GOTO      START
     C                   ENDDO
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '0'                                           2<-B
     C     MCNAK         ANDNE     '* SLETTET *'
     C                   SELECT                                                   3<-B
     C     WSNR          WHENEQ    2                                              3
     C     MCST          ANDNE     'S'
     C     *IN49         ANDEQ     *OFF
     C                   MOVE      '1'           *IN99
     C                   EXSR      CHGOPD
     C     WSNR          WHENEQ    3                                              3
     C     MCST          ANDNE     'S'
     C     MCAVD         ANDEQ     0
     C     *IN49         ANDEQ     *OFF
     C                   MOVE      '1'           *IN99
     C                   EXSR      CPYOPD
     C     WSNR          WHENEQ    4                                              3
     C     MCST          ANDNE     'S'
     C     *IN49         ANDEQ     *OFF
     C                   MOVE      '1'           *IN99
     C                   EXSR      SLTOPD
     C     WSNR          WHENEQ    5                                              3
     C                   MOVE      '1'           *IN99
     C                   EXSR      DSPOPD
     C     WSNR          WHENEQ    9                                              3
     C     MCST          ANDEQ     'S'
     C     *IN49         ANDEQ     *OFF
     C                   MOVE      '1'           *IN99
     C                   EXSR      ANGSLT
     C     WSNR          WHENEQ    15                                             3
     C                   EXSR      VISSAD
     C                   ENDSL                                                    3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPOPD        BEGSR
     C***********************************************
     C                   MOVE      'DSP'         XTYPE                          SPØRRING
     C                   CALL      'SAD114R'
     C                   PARM      MCAVD         U_AVD             4 0
     C                   PARM                    UPRO
     C                   PARM      MCTDN         U_TDN             7 0
     C                   PARM      MCCN          U_CN              2 0
     C                   PARM                    XTYPE             3
     C                   PARM                    UFLAGG            1 0
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISSAD        BEGSR
     C***********************************************
     C                   CALL      'SAD005A'
     C                   PARM      MCAVD         U_AVD
     C                   PARM      MCTDN         U_TDN
     C                   PARM                    UFLAGG
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGOPD        BEGSR
     C***********************************************
     C                   MOVE      'CHG'         XTYPE             3
     C                   CALL      'SAD114R'
     C                   PARM      MCAVD         U_AVD             4 0
     C                   PARM                    UPRO
     C                   PARM      MCTDN         U_TDN             7 0
     C                   PARM      MCCN          U_CN              2 0
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C     SADKMCL1K     CHAIN(N)  SADKMCL1                           33
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYOPD        BEGSR
     C***********************************************
     C                   MOVE      'CPY'         XTYPE             3
     C                   CALL      'SAD114R'
     C                   PARM      MCAVD         U_AVD             4 0
     C                   PARM                    UPRO
     C                   PARM      MCTDN         U_TDN             7 0
     C                   PARM      MCCN          U_CN              2 0
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      *ZEROS        WSNR
     C                   UPDATE    SUBFIL
     C                   IF        U_CN <> MCCN
      * Kopiert
     C                   EVAL      MCCN = U_CN
     C     SADKMCL1K     CHAIN(N)  SADKMCL1                           33
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SLTOPD        BEGSR
     C***********************************************
     C     SADKMCL1K     CHAIN     SADKMCL1                           33
     C                   IF        MCST = ' '
      * Ikke sendt, slettes.
     C                   EVAL      MCNAK = '* SLETTET *'
     C                   DELETE    SADKMCFR
     C                   ELSE
      * Er sendt. Merke kansellert.
     C                   MOVE      'S'           MCST
     C                   UPDATE    SADKMCFR
     C                   END
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     ANGSLT        BEGSR
     C***********************************************
     C     SADKMCL2K     CHAIN     SADKMCL2                           33
     C                   MOVE      'J'           XANGRE            1
     C                   DOW       *IN33 = *OFF
     C                   IF        M2_MCPRO <> UPRO AND
     C                             (M2_MCST = ' ' OR M2_MCST = 'C')
      * EN ANNEN TUR HAR PLUKKET DETTE OPPDRAGET.
     C                   MOVE      'N'           XANGRE
     C                   LEAVE
     C                   END
     C     SADKMCL2K     READE     SADKMCL2                               33
     C                   ENDDO
      *
     C                   IF        XANGRE = 'J'
     C     SADKMCL1K     CHAIN     SADKMCL1                           33
     C                   MOVE      'C'           MCST
     C                   UPDATE    SADKMCFR
     C                   END
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     PLKOPD        BEGSR
      ***********************************************
     C                   CALL      'SAD113R'
     C                   PARM                    UAVD
     C                   PARM                    UPRO
     C                   PARM                    UUUID
     C                   PARM                    UFLAGG            1 0
      *
     C                   ENDSR
      *
      ***********************************************
     C     LAGOPD        BEGSR
      ***********************************************
     C                   MOVE      'MAN'         XTYPE                          Manuell oppdrag
     C                   MOVE      *ZEROS        U_AVD
     C                   MOVE      *ZEROS        U_TDN
     C                   MOVE      *ZEROS        U_CN
     C                   CALL      'SAD114R'
     C                   PARM                    U_AVD             4 0
     C                   PARM                    UPRO
     C                   PARM                    U_TDN             7 0
     C                   PARM                    U_CN              2 0
     C                   PARM                    XTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADKMCL1K     KLIST
     C                   KFLD                    UPRO
     C                   KFLD                    MCAVD
     C                   KFLD                    MCTDN
     C                   KFLD                    MCCN
     C     SADKMCL2K     KLIST
     C                   KFLD                    MCAVD
     C                   KFLD                    MCTDN
     C                   KFLD                    MCCN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C     UTYPE         COMP      'DSP'                                  49
      *
     C                   ENDSR
      *
