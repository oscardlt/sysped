     A*****************************************************************
     A* UTSKRIFT PERIODEAVREGNING                                     *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=132, OVERFLOW=45      *
     A*****************************************************************
     A          R HEAD1                     SKIPB(2)
     A
     A* HEADING
     A                                     2MSGCON(45 TRO0261 SYMSGF)
     A                                     2MSGCON(16 TRO0262 SYMSGF)
     A            WFRA          15        19
     A                                    34'-'
     A            WTIL          15        35
     A                                      SPACEB(2)
     A                                    60MSGCON(08 TRO0263 SYMSGF)
     A            WPERM          2  0     72
     A                                    74'-'
     A            WPERÅ          2  0     75
     A                                      SPACEA(1)
     A*DETALJ HEADINGER
     A          R HEAD2                     SPACEB(1)
     A                                    47MSGCON(08 TRO0264 SYMSGF)
     A                                    62MSGCON(08 TRO0264 SYMSGF)
     A                                    92MSGCON(17 TRO0265 SYMSGF)
     A                                      SPACEA(1)
     A                                     2MSGCON(04 TRO0266 SYMSGF)
     A                                    17MSGCON(04 TRO0266 SYMSGF)
     A                                    32MSGCON(06 TRO0267 SYMSGF)
     A                                    47MSGCON(06 TRO0267 SYMSGF)
     A                                    62MSGCON(06 TRO0267 SYMSGF)
     A                                    77MSGCON(06 TRO0267 SYMSGF)
     A                                    92MSGCON(09 TRO0268 SYMSGF)
     A                                   107MSGCON(09 TRO0268 SYMSGF)
     A                                      SPACEA(1)
     A                                     2MSGCON(04 TRO0269 SYMSGF)
     A                                    17MSGCON(05 TRO0270 SYMSGF)
     A                                    32MSGCON(08 TRO0271 SYMSGF)
     A                                    47MSGCON(08 TRO0271 SYMSGF)
     A                                    62MSGCON(09 TRO0272 SYMSGF)
     A                                    77MSGCON(09 TRO0272 SYMSGF)
     A                                    92MSGCON(08 TRO0271 SYMSGF)
     A                                   107MSGCON(09 TRO0272 SYMSGF)
     A                                      SPACEA(1)
     A*DETALJER
     A          R DETALJ                    SPACEB(1)
     A            AHEREF        15A        2
     A            AHAREF        15A       17
     A            WBELA         13S 2     27EDTCDE(M)
     A            WBELB         13S 2     42EDTCDE(M)
     A            WBELC         13S 2     57EDTCDE(M)
     A            WBELD         13S 2     72EDTCDE(M)
     A            WBELE         13S 2     87EDTCDE(M)
     A            WBELF         13S 2    102EDTCDE(M)
     A* SUM TURER
     A          R SUMTUR                    SPACEB(1)
     A                                     2'SUM'
     A            WSUMA         13S 2     27EDTCDE(M)
     A            WSUMB         13S 2     42EDTCDE(M)
     A            WSUMC         13S 2     57EDTCDE(M)
     A            WSUMD         13S 2     72EDTCDE(M)
     A            WSUME         13S 2     87EDTCDE(M)
     A            WSUMF         13S 2    102EDTCDE(M)
     A* TOT TRANSPORTØRKOSTNADER
     A          R SUMAV1                    SPACEB(1)
     A                                     2MSGCON(20 TRO0273 SYMSGF)
     A            WTOTE         13S 2     87EDTCDE(M)
     A* TOT HOVEDFRAKT AVREGNETR
     A          R SUMAV2                    SPACEB(1)
     A                                     2MSGCON(24 TRO0274 SYMSGF)
     A            WTOTB         13S 2     42EDTCDE(M)
     A            WTOTC         13S 2     57EDTCDE(M)
     A* PROSENTFORDELING
     A          R SUMAV3                    SPACEB(1)
     A                                     2MSGCON(16 TRO0285 SYMSGF)
     A            WPROB          5S 2     50EDTCDE(M)
     A            WPROC          5S 2     65EDTCDE(M)
     A* KORREKSJON HOVEDFRAKT
     A          R SUMAV4                    SPACEB(1)
     A                                     2MSGCON(24 TRO0275 SYMSGF)
     A            WKORB         13S 2     42EDTCDE(M)
     A            WKORC         13S 2     57EDTCDE(M)
     A* SUMTOTAL
     A          R SUMAV5                    SPACEB(1)
     A                                     2'SUM'
     A            WSUMTA        13S 2     27EDTCDE(M)
     A            WSUMTB        13S 2     42EDTCDE(M)
     A            WSUMTC        13S 2     57EDTCDE(M)
     A            WSUMTD        13S 2     72EDTCDE(M)
     A            WSUMTE        13S 2     87EDTCDE(M)
     A            WSUMTF        13S 2    102EDTCDE(M)
     A* KOSTNADER AVSENDERLAND
     A          R AVREG1                    SPACEB(1)
     A                                   111MSGCON(06 TRO0278 SYMSGF)
     A                                      SPACEA(1)
     A                                     2MSGCON(10 TRO0276 SYMSGF)
     A                                    32MSGCON(24 TRO0277 SYMSGF)
     A            WSUMTA        13S 2    102EDTCDE(M)
     A* KOSTNADER MOTTAKERLAND
     A          R AVREG2                    SPACEB(1)
     A                                    32MSGCON(24 TRO0279 SYMSGF)
     A            WSUMTD        13S 2    102EDTCDE(M)
     A* AVREGNING HOVEDFRAKT BETALBART HOS AVSENDER
     A          R AVREG3                    SPACEB(1)
     A                                    32MSGCON(45 TRO0280 SYMSGF)
     A            WSUMTB        13S 2    102EDTCDE(M)
     A* BETALT TRANSPORTKOSTNADER AVSENDER
     A          R AVREG4                    SPACEB(1)
     A                                    32MSGCON(36 TRO0281 SYMSGF)
     A            WSUMTE        13S 2    102EDTCDE(M)
     A* DEBET/CREDIT TIL AVSENDERKONTOR
     A          R AVREG5                    SPACEB(2)
     A N30                                32MSGCON(02 TRO0282 SYMSGF)
     A  30                                32MSGCON(02 TRO0283 SYMSGF)
     A                                    35MSGCON(21 TRO0284 SYMSGF)
     A            WAVRGE        13S 2    102EDTCDE(M)
     A* STREKLINJE
     A          R STREK                     SPACEB(1)
     A                                     2MSGCON(123 DIV2512 SYMSGF)
