********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 skriv eksternref
10XXX * 02 SH  PTF10 sitrt byttet med sitst i printfil
10247 * 03 YBC PTF10 INFORMASJON OM MIDLERTIDIG DEKLARASJON
12281 * 04 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
********************************************************************
     A                                      REF(*LIBL/SADH)
     A          R TVINUAA                   SKIPB(2)
     A            KFMT1         75         1
     A            KFMT2         75         1SPACEA(1)
     A                                    24'UTLEVERINGSATTEST (Innførsel)'
     A                                      UNDERLINE
     A                                     2'Referanse:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SIAVD     R             13EDTCDE(Z)
     A                                    17'/'
     A            SITDN     R             18EDTCDE(Z)
     A                                    25'/'
     A            SISG      R             26
     A  97                                30'('
     A  97        SIOPD     R             31EDTCDE(Z)
     A  97                                38'/'
     A  97        SIFRBN    R             39EDTCDE(Z)
     A  97                                42')'
N01  A            FSSOK         35        45
     A                                     2'Godkjennelsedato:'
     A                                      UNDERLINE
     A                                      SPACEB(1)
     A            SIDTG     R             20
     A                                    34'Løpenr.'
     A                                      UNDERLINE
     A            SITLL     R             42
     A                                    53'Ekspedisjonsnr.'
     A                                      UNDERLINE
     A            SITLE     R             69
     A                                     2' 2.Avsender:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SINAS     R             15
     A            SIADS1    R             47
     A            SIADS2    R             15
     A                                      SPACEB(1)
     A            SIADS3    R             47
     A                                     2'12.Mottaker:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SIKNK     R             15EDTCDE(Z)
     A            SIRG      R             27
     A  89                                32 '******'
     A            SINAK     R             15
     A                                      SPACEB(1)
     A            SIADK1    R             47
     A            SIADK2    R             15
     A                                      SPACEB(1)
     A            SIADK3    R             47
     A                                     2'1.Tolldeklarasjon:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SIDTY     R             21
     A            SIDP      R             24EDTCDE(Z)
N04  A            SIDP2     R             27
     A                                    31' 6.Antall kolli:'
     A                                      UNDERLINE
     A            SINTK     R             48EDTCDE(K)
     A                                    31'   Totalvekt:'
     A                                      UNDERLINE
     A                                      SPACEB(1)
     A            SIVKB     R             46EDTCDE(K)
     A                                     2'12.Frakt:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A N04        SIBEL1    R             12EDTCDE(K)
     A                                    31'15.Avsenderland:'
     A                                      UNDERLINE
     A            SILKA     R             48
     A                                    58'19.Cont.'
     A                                      UNDERLINE
     A            SIKDC     R             67
     A                                     2'20.Lev.vilkår:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SILV      R             17
     A            SILVT     R             22
     A                                    43'21.Identitet:'
     A                                      UNDERLINE
     A            SITRID    R             57
     A                                     2'22.Faktura:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SIVAL3    R             14
     A N03        SIBEL3    R             18EDTCDE(K)
     A                                    43'23.Kurs:'
     A                                      UNDERLINE
     A N03        SIVKU     R             52EDTCDE(K)
S04  A*                                   63'24.Trans.type:'
N04  A                                    63'24.Tr.type:'
     A                                      UNDERLINE
S02  A*           SITRT     R             78EDTCDE(Z)
S04  A*           SITST     R             78EDTCDE(Z)
N04  A            SITST          2  0     77EDTCDE(Z)
     A                                     2'25.Trans.måte:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SITRM     R             17EDTCDE(Z)
     A                                    26'28.Finans:'
     A                                      UNDERLINE
     A            SIFIF     R             37
     A            SIFID     R             65EDTWRD('    .  .  ')
     A                                     2'30.Varenes lagringssted:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SILS      R             27
     A            SIKDLS    R             44
     A                                     2'48.Konto:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A N01N02     SIKTC     R             12
     A N01N02     SIKTA     R             13EDTCDE(Z)
     A N01N02                             20'-'
     A N01N02     SIKTB     R             21EDTCDE(Z)
     A  01                                13'DAGSOPPGJØR'
     A  02                                13'KONTANT'
     A                                    25'49.Godsnr.'
     A                                      UNDERLINE
     A            SIGN      R             36
     A                                    51'/'
     A            SIPOS     R             52
N03  A N38                                 2'48B.'
N03  A                                      SPACEB(1)
N03  A N38        XSIFRDT        8  0     12EDTWRD('  .  .    ')
N03  A N38        XSIFRT1       60        12SPACEB(1)
N03  A N38        XSIFRT2       60        12SPACEB(1)
N03  A N38        XSIFRT3       60        12SPACEB(1)
N03  A N38        XSIFRT4       60        12SPACEB(1)
N03  A N38        XSIFRT5       60        12SPACEB(1)
     A                                     2'54.Sted og dato:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            SIDST     R             19
     A            SIDT      R             45EDTWRD('    .  .  ')
     A                                    19'            '
     A                                      SPACEB(1)
     A            SITARF    R             45
     A                                     2'Deklarasjons dato:'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A            M0068A    R             21REFFLD(M0068A EDIM)
     A                                      EDTCDE(Z)
     A                                    41'Sekvensenr.'
     A                                      UNDERLINE
     A            M0068B    R             53REFFLD(M0068B EDIM)
     A                                      EDTCDE(Z)
     A                                    62'Versjonsnr.'
     A                                      UNDERLINE
     A            M0068C    R             73REFFLD(M0068C EDIM)
     A                                      EDTCDE(Z)
     A                                     2'Ref Funk Tekst'
     A                                      UNDERLINE
     A                                      SPACEB(2)
     A          R TVINUAB                   SPACEB(2)
     A            F0077     R              2REFFLD(F0077 TVINF)
     A            F4815     R              6REFFLD(F4815 TVINF)
     A            F0078A    R             11REFFLD(F0078A TVINF)
     A                                      SPACEA(1)
     A            F0078B    R             11REFFLD(F0078B TVINF)
     A                                      SPACEA(1)
     A            F0078C    R             11REFFLD(F0078C TVINF)
     A                                      SPACEA(1)
     A            F0078D    R             11REFFLD(F0078D TVINF)
     A                                      SPACEA(1)
     A            F0078E    R             11REFFLD(F0078E TVINF)
     A                                      SPACEA(1)
