      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE STEDSNAVN                 ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     FSTED2D    UF A E           K DISK
     FSTED2A    IF   E           K DISK
     F                                     RENAME(STED2R:STED2SR)
     FSTED2S    IF   E           K DISK
     F                                     RENAME(STED2R:STED22R)
     FSTED2     IF   E           K DISK
     F                                     RENAME(STED2R:STED2RX)
     FKODTS2    IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYFA34FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2NVN         YNVN             30
     C                   MOVE      W2LK          YLK               2
     C     W2SORT        IFEQ      0
     C                   MOVE      *BLANKS       YKOD              5
     C                   ELSE
     C                   MOVEL     W2NVN         YKOD
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2SORT        IFEQ      0
     C     W2NVNK        SETLL     STED2A
     C                   ELSE
     C     W2SORT        IFEQ      1
     C     YKODK         SETLL     STED2D
     C                   ELSE
     C     Ynvnks        SETLL     STED2S
     C                   END
     C                   END
     C     START1        TAG
     C     W2SORT        IFEQ      0
     C                   READ      STED2A                                 94
     C                   ELSE
     C     W2SORT        IFEQ      1
     C                   READ(N)   STED2D                                 94
     C                   else
     C                   READ      STED2S                                 94
     C                   END
     C                   END
     C  N94              MOVE      ST2NVN        YNVN
     C  N94              MOVE      ST2LK         YLK
     C  N94              MOVE      ST2KOD        YKOD
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     W2SORT        IFEQ      0
     C                   READ      STED2A                                 94
     C                   ELSE
     C     W2SORT        IFEQ      1
     C                   READ(N)   STED2D                                 94
     c                   else
     C                   READ      STED2S                                 94
     C                   END
     C                   END
     C  N94              MOVE      ST2NVN        YNVN
     C  N94              MOVE      ST2LK         YLK
     C  N94              MOVE      ST2KOD        YKOD
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       999830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   MOVE      *BLANKS       WSKOD
     C                   GOTO      START5
     C                   END
     C*** SØK
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C     W2SORT        IFEQ      0
     C  N94YNVNK         SETLL     STED2A
     C     START4        TAG
     C  N94              READ      STED2A                                 94
     C  N94YKOD          IFNE      ST2KOD
     C                   GOTO      START4
     C                   END
     C                   ELSE
     C     W2SORT        IFEQ      1
     C  N94              READP(N)  STED2D                                 94
     C                   else
     C  N94              READP     STED2S                                 94
     C                   END
     C                   END
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE AVDELING
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000'       *IN(86)
     C                   SETOFF                                           30
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA AVDELING
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSKOD         COMP      *BLANKS                                90
     C   90              MOVE      'SPS0026'     MSGNR
     C  N90WSKODK        CHAIN(N)  STED2D                             33
     C  N90
     CANN33              DO
     C                   MOVE      'SPS0027'     MSGNR
     C                   SETON                                        90
     C                   END
     C   90              Z-ADD     9             POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C     WSNVN         COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SPN0011'     MSGNR
     C   89
     CANN30              Z-ADD     18            POSNBR
     C   89              SETON                                        30
     C*
     C     WSLK          IFEQ      'NO'
     C     WSSON         COMP      1                                    87
     C  N87WSSON         COMP      5                                  87
     C   87
     CANN30              MOVE      'SPS0028'     MSGNR
     C   87
     CANN30              Z-ADD     65            POSNBR
     C   87              SETON                                        30
     C                   ELSE
     C     KODS2K        CHAIN     KODTS2                             88
     C   88KODLKK        CHAIN     KODTLK                             88
     C   88
     CANN30              MOVE      'SPL0006'     MSGNR
     C   88
     CANN30              Z-ADD     50            POSNBR
     C   88              SETON                                        30
     C                   END
     C*
     C*  POINT TO ANOTHER CODE ???
     C     WSKOD2        IFNE      *BLANKS
     C     WSKO2K        CHAIN     STED2D                             86
     C   86
     CANN30              MOVE      'SPL0028'     MSGNR
     C   86
     CANN30              Z-ADD     67            POSNBR
     C   86              SETON                                        30
     C                   END
     C* Ved NY - alfa gis warning drsom ikke unik på tvers av landkoder
     C     *in40         ifeq      '0'
     C     *in30         andeq     '0'
     C     *in23         andeq     '0'
     C     WSKOD         andlt     '00000'
     C     WSKOD         CHAIN     STED2                              33
     C     *in33         ifeq      '0'
     C                   MOVE      'JOV1414'     MSGNR
     C                   SETON                                        9030
     C                   Z-ADD     9             POSNBR
     C                   end
     C                   end
     C   30              GOTO      SLTB
     C*
     C     WSKODK        CHAIN     STED2D                             33
     C                   EXSR      MOVWST
     C  N33              UPDATE    STED2R
     C   33              WRITE     STED2R
     C*
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     ST2KOD        COMP      '*DEL '                                98
     C   98              MOVE      'SPS0029'     MSGNR
     C   98              SETON                                            30
     C   98              GOTO      SLHENT
     C                   EXSR      CHGST
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     ST2KOD        COMP      '*DEL '                                98
     C   98              MOVE      'SPS0029'     MSGNR
     C   98              SETON                                            30
     C   98              GOTO      SLHENT
     C                   EXSR      DLTST
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTST         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     ST2KOK        CHAIN     STED2D                             33
     C  N33              DELETE    STED2R
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGST         BEGSR
     C***********************************************
     C                   EXSR      MOVSTW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSTW        BEGSR
     C***********************************************
     C                   MOVE      ST2KOD        WSKOD
     C                   MOVE      ST2NVN        WSNVN
     C                   MOVE      ST2LK         WSLK
     C                   MOVE      ST2SON        WSSON
     C                   MOVE      ST2PNR        WSPNR
     C                   MOVE      ST2SOL        WSSOL2
     C                   MOVE      ST2KO2        WSKOD2
     C                   MOVE      ST2SOI        WSSONI
     C                   MOVE      ST2UNI        WSUNI
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     18            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWST        BEGSR
     C***********************************************
     C                   MOVE      WSKOD         ST2KOD
     C                   MOVE      WSNVN         ST2NVN
     C                   MOVE      WSLK          ST2LK
     C                   MOVE      WSSON         ST2SON
     C                   MOVE      WSPNR         ST2PNR
     C                   MOVE      WSSOL2        ST2SOL
     C                   MOVE      WSKOD2        ST2KO2
     C                   MOVE      WSSONI        ST2SOI
     C                   MOVE      WSUNI         ST2UNI
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      '*DEL '       ST2KOD
     C                   MOVE      *BLANKS       ST2NVN
     C                   MOVE      *BLANKS       ST2LK
     C                   MOVE      *ZEROS        ST2SON
     C                   MOVE      *ZEROS        ST2PNR
     C                   MOVE      *BLANKS       ST2SOL
     C                   MOVE      *BLANKS       ST2KO2
     C                   MOVE      *ZEROS        ST2SOI
     C                   MOVE      *BLANKS       ST2UNI
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSKOD
     C                   MOVE      *BLANKS       WSNVN
     C                   MOVE      *BLANKS       WSLK
     C                   MOVE      *ZEROS        WSSON
     C                   MOVE      *ZEROS        WSPNR
     C                   MOVE      *BLANKS       WSSOL2
     C                   MOVE      *BLANKS       WSKOD2
     C                   MOVE      *ZEROS        WSSONI
     C                   MOVE      *BLANKS       WSUNI
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     W2NVNK        KLIST
     C                   KFLD                    W2LK
     C                   KFLD                    W2NVN
     C     YNVNK         KLIST
     C                   KFLD                    YLK
     C                   KFLD                    YNVN
     C     YNVNKS        KLIST
     C                   KFLD                    YNVN
     C     YKODK         KLIST
     C                   KFLD                    YLK
     C                   KFLD                    YKOD
     C     WSKODK        KLIST
     C                   KFLD                    WSLK
     C                   KFLD                    WSKOD
     C     WSKO2K        KLIST
     C                   KFLD                    WSLK
     C                   KFLD                    WSKOD2
     C     ST2KOK        KLIST
     C                   KFLD                    ST2LK
     C                   KFLD                    ST2KOD
     C     KODS2K        KLIST
     C                   KFLD                    YS2UNI
     C                   KFLD                    WSLK
     C     KODLKK        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    WSLK
     C*
     C                   MOVE      'S2 '         YS2UNI            3
     C                   MOVE      'LK '         YLKUNI            3
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
