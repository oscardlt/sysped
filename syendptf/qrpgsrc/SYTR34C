********************************************************************
      * FLYTTER(KOPIERER) KOLLI-IDS FRA HENOK -> DOKUFM  (MM) VED HENTEOPPDRAGS AVSLUTTTNING
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 OPPDATER OGSÅ CARGOO/CARGOF - KALL PROG SOM OPPDATERER TEI I T&T MM
10XXX * 02 JOV PTF12 ikke flytt slettede kolliIDs
********************************************************************
     FHENOK     IF   E           K DISK
     FDOKUFM    O  A E             DISK
N01  FCARGOO1   UF   E           K DISK
N01  FCARGOF1   UF   E           K DISK
      *
     C     *ENTRY        PLIST
     C                   PARM                    LNR               7 0                       ID
     C                   PARM                    LNR2              3 0                       ID
     C                   PARM                    AVD               4 0                       ID
     C                   PARM                    OPD               7 0                       ID
     C                   PARM                    FBN               3 0                       ID
     C                   PARM                    KOTERM
      *
     C                   move      *blanks       KOTERM
      *
     C     HENOKK        KLIST
     C                   KFLD                    LNR
     C                   KFLD                    LNR2
      *
     C                   MOVE      'F'           FMRI
     C                   MOVE      AVD           FMAVD
     C                   MOVE      OPD           FMOPD
     C                   MOVE      FBN           FMFBNR
     C                   MOVE      *ZEROS        FMLINR
     C                   MOVE      *ZEROS        FMMKNR
     C                   MOVE      *ZEROS        FMLONR
     C                   MOVE      *BLANKS       FMMRK1
     C                   MOVE      'E'           FMST
      *
     C     HENOKK        SETLL     HENOK
     C     HENOKK        READE     HENOK                                  35
     C     *IN35         DOWEQ     '0'
      *
N02  C                   if        %subst(HKID:1:3) <> '*D*'
     C                   ADD       1             FMMKNR
     C                   MOVE      HKID          MRK18            18
     C                   MOVEL     MRK18         FMMRK1
     C                   MOVE      HKSTIN        FMITER
     C                   WRITE     DOKUFMR
N02  C                   endif
     C     HENOKK        READE     HENOK                                  35
     C                   END
      *
N01
N01   * knytte sum-record til oppdraget
N01   * (tidligere var dette en SR 'KobleHent' i CARGOSUM)
N01  C     HENOKK        Chain     Cargoo1                            33
N01  C                   move      AVD           COAVD
N01  C                   move      OPD           COOPD
N01  C  N33              update    CARGOOR
N01   * knytte CS-kolliID til oppdraget
N01  C     HENOKK        setll     CargoF1
N01  C     HENOKK        reade     CargoF1                                33
N01  C     *in33         doweq     '0'
N01  C                   move      AVD           KOAVD
N01  C                   move      OPD           KOOPD
N01  C                   update    CARGOFR
N01  C     HENOKK        reade     CargoF1                                33
N01  C                   end
      *
     C                   SETON                                        LR
     C                   RETURN
