      *
      *****************************************************************
      *** PROGRAM FOR Å SKRIVE UT TURKONVOLUTT                       **
      *** (GODSLISTE HVIS VALGT TURKONV="G" I FIRMARECORD)           **
      *****************************************************************
     FLEVEL01   IF   E           K DISK
     FHODE      IF   E           K DISK
     FSYTR22PF  O    E             PRINTER
     FFIRTR     IF   E           K DISK
     FTILL      IF   E           K DISK
     FTUFE      IF   E           K DISK
     FREIS      IF   E           K DISK
     FTURER     UF   E           K DISK
     FTURER7    IF   E           K DISK
     F                                     RENAME(TURERR:TURE7R)
     FTURER14   IF   E           K DISK
     F                                     RENAME(TURERR:TURE14R)
     FFREETUR2  IF   E           K DISK
     D                 DS
     D  TUSJN1                 1     30
     D  TUSJ20                 1     20
     D                 DS
     D  TINR01                 1     15
     D  TINR02                16     30
     D  TINR03                31     45
     D  TINR04                46     60
     D  TINR05                61     75
     D  TINR06                76     90
     D  TI                     1     90
     D                                     DIM(6)
     D                 DS
     D  FEDATP                 1      6  0
     D  XDATPD                 1      2  0
     D  XDATPM                 3      4  0
     D  XDATPÅ                 5      6  0
     D                 DS
     D  FEDAT2                 1      8  0
     D  FEYEA2                 1      4  0
     D  FEMON2                 5      6  0
     D  FEDAY2                 7      8  0
     D                 DS
     D  DATOFR                 1      6  0
     D  DATFRD                 1      2  0
     D  DATFRM                 3      4  0
     D  DATFRÅ                 5      6  0
     D                 DS
     D  TUDT                   1      8  0
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     C*
     C                   EXSR      INIT
     C     UTURNR        IFNE      *ZEROS
     C     UTURNR        CHAIN     TURER14                            33
     C  N33KEYTUR        CHAIN     TURER                              33
     C  N33              MOVE      'P'           TUTST1
     C  N33              UPDATE    TURERR
     C   33              GOTO      SLUTT
     C                   ELSE
     C     TUR7KE        SETLL     TURER7
     C     STLES         TAG
     C     STAT          READE     TURER7                                 33
     C   33              GOTO      SLUTT
     C*  TEST AVD BARE NÅR ULIK 0
     C     UAVD          IFNE      *ZEROS
     C     TUAVD         ANDNE     UAVD
     C                   GOTO      STLES
     C                   END
     C*  TEST SIGN BARE NÅR ULIK BLANK (=ALLE)
     C  N33USIGN         IFNE      *BLANKS
     C     USIGN         ANDNE     TUSG
     C                   GOTO      SLUTT
     C                   END
     C                   END
     C     FIKONV        IFNE      'G'
     C* NORMAL TURKONVOLUTT
     C                   EXSR      PRINT
     C                   ELSE
     C* GODSLISTE SOM TURKONVOLUTT.
     C                   MOVE      TUAVD         XXAVD             4
     C                   MOVE      TUPRO         XXPRO             8
     C                   MOVE      *ZEROS        XXAGEN            8
     C                   MOVE      'N'           XXFAX             1
     C                   MOVE      'N'           XXADR             1
     C                   CALL      'SYFA55'
     C                   PARM                    XXAVD
     C                   PARM                    XXPRO
     C                   PARM                    XXAGEN
     C                   PARM                    XXBELJ            1
     C                   PARM                    XXFAX
     C                   PARM                    XXADR
     C                   END
     C                   ADD       1             UANT
     C     UTURNR        IFEQ      *ZEROS
     C     KEYTUR        CHAIN     TURER                              33
     C  N33              MOVE      'P'           TUTST1
     C  N33              UPDATE    TURERR
     C  N33              GOTO      STLES
     C                   END
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*****************************************************************
     C     PRINT         BEGSR
     C*****************************************************************
     C*
     C* HENTER TILLATELSER
     C*
     C     KEYTUR        SETLL     TILL
     C                   Z-ADD     1             NR                2 0
     C                   MOVE      *BLANKS       TI
     C                   DO        6
     C  N33KEYTUR        READE     TILL                                   33
     C  N33              MOVE      TINR          TI(NR)
     C  N33              ADD       1             NR
     C  N33              END
     C*
     C* PRINTER HEADING HVIS ØNSKELIG
     C*
     C     UHEAD         IFEQ      'J'
     C     UHEAD         OREQ      'Y'
     C                   WRITE     HEADP
     C                   ELSE
     C                   WRITE     HEAD0
     C                   END
     C*
     C* SKRIVER HEADING
     C*
     C                   WRITE     HEAD1A
     C                   WRITE     HEAD1B
     C                   MOVE      TUDTD         DATFRD
     C                   MOVE      TUDTM         DATFRM
     C                   MOVE      TUDTÅ         DATFRÅ
     C                   WRITE     HEAD1C
     C*
     C* LESER OG SKRIVER FRITEKSTER
     C*
     C     FREETK        SETLL     FREETUR2
     C     STFREE        TAG
     C     FREETK        READE     FREETUR2                               33
     C  N33              WRITE     DETT
     C  N33              GOTO      STFREE
     C*
     C* SKRIVER HEADING FERJEINFORMASJONER
     C*
     C                   WRITE     HEADFE
     C                   WRITE     HEADF2
     C*
     C*
     C* SKRIVER UT FERGEOVERFARTER
     C*
     C                   Z-ADD     0             ANTBLA            2 0
     C                   SETOFF                                       33
     C     KEYTUR        SETLL     TUFE
     C                   DO        6
     C  N33KEYTUR        READE     TUFE                                   33
     C  N33LEVKEY        CHAIN     LEVEL01                            34
     C                   WRITE     DETFB
     C     *IN33         IFEQ      '0'
     C                   MOVE      FEDAY2        XDATPD
     C                   MOVE      FEMON2        XDATPM
     C                   MOVE      FEYEA2        XDATPÅ
     C                   WRITE     DETFE
     C                   WRITE     DETFE
     C                   END
     C   33              ADD       1             ANTBLA
     C     ANTBLA        IFEQ      2
     C                   GOTO      UTBLA
     C                   END
     C                   END
     C*
     C     UTBLA         TAG
     C*
     C* SKRIVER 1.STE OG 2.DRE SJÅFØR
     C*
     C                   WRITE     HEADSJ
     C*
     C* SKRIVER UT REISEKASSER
     C*
     C                   WRITE     HEADRE
     C     KEYTUR        SETLL     REIS
     C     STREIS        TAG
     C     KEYTUR        READE     REIS                                   33
     C  N33              WRITE     DETRE
     C  N33              GOTO      STREIS
     C*
     C                   ENDSR
     C*
     C*****************************************************************
     C     INIT          BEGSR
     C*****************************************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    USIGN             3
     C                   PARM                    UANT              3 0
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTURNR            8 0
     C                   PARM                    UHEAD             1
     C*
     C*
     C     LEVKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FEFIRM
     C*
     C     TUR7KE        KLIST
     C                   KFLD                    STAT
     C                   KFLD                    USIGN
     C     KEYTUR        KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    TUPRO
     C     FREETK        KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    TUPRO
     C                   KFLD                    KODEKO
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    TUAVD
     C*
     C                   MOVE      ' '           YNET              1
     C                   MOVE      'K'           KODEKO            1
     C                   MOVE      'Y'           STAT              1
     C     UHEAD         IFEQ      'J'
     C     HODEK         CHAIN     HODE                               33
     C                   END
     C                   READ      FIRTR                                  33
     C*
     C                   ENDSR
