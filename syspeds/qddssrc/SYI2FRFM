     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                  2 58'Copyright: SYSTEMA A/S'
     A                                      COLOR(TRQ)
     A                                  5 18'D O K U M E N T U T S K R I F T'
     A                                      COLOR(WHT)
     A                                  6 18'FLYFRAKTBREV    INNENLANDS'
     A                                      COLOR(WHT)
     A                                  7 18'MAWB for samlastforsendelser'
     A                                      COLOR(WHT)
     A                                 10 18'Tast inn kostnadssted:'
     A            WSAVD          4Y 0B 10 44DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('Tastet kostnadssted er ikke-
     A                                       registrert' 99)
     A                                 12 18'Tast inn signatur:'
     A            WSSG           3A  B 12 44DSPATR(UL)
     A                                      COLOR(YLW)
     A  98                                  ERRMSG('Tastet signatur er ikke reg-
     A                                      istrert.' 98)
     A                                 12 55'(ALL = alle)'
     A                                 14 18'Tast inn samlastnr.'
     A            WSSD           3A  B 14 44DSPATR(UL)
     A                                      COLOR(YLW)
     A  97                                  ERRMSG('Tastet samlastnr. finnes ik-
     A                                      ke.' 97)
     A                                      DSPATR(PC)
     A            WSLNR          3Y 0B 14 48
     A  97                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16 18'Skal jobbkø benyttes:'
     A            WSJBK          1A  B 16 44DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 18 18'Avvikende printer:'
     A            WSPRT         10A  B 18 44DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23 18'F3 = Avslutt'
