********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PTF10 Oppdater feltet M1N07 i EDIM
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 CB  PTF11 Endre status i NCTS Import fra F til ?
11XXX * 03 YBC PTF11 LESE POST FOR SENDING
11XXX * 04 YBC PTF11 IKKE LÅS POST PÅ SVXH, SVNH, SVIH, SVEH
11104 * 05 YBC PTF11 IKKE OPPDATER THST VED THST = 'K' OG THWS <> *BLANK (UTSKRIFT)
********************************************************************
     FEDIS2     IF   E           K DISK
     FEDIM6     IF   E           K DISK    RENAME(EDIMR:EDIMR6)
     FEDIM      O  A E           K DISK    PREFIX(M_)
     FEDIC      UF   E             DISK
     FSVIH      UF A E           K DISK
     FSVEH      UF A E           K DISK
     FSVXH      UF A E           K DISK
     FSVNH      UF A E           K DISK
     FSVIHH     O  A E             DISK
     FSVEHH     O  A E             DISK
     FSVXHH     O  A E             DISK
     FSVNHH     O  A E             DISK
     FTVINF     O  A E             DISK
     FSVXKODL01 IF   E           K DISK
     D KD              S              5    DIM(10)
     D                 DS
     D  W0062                  1     14
     D  W0062A                 1      4S 0
     D  W0062B                 5     11S 0
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      FILUPD
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    W_0026           14
     C                   PARM                    W_0020           14
     C                   PARM                    W_0062           14
     C                   PARM                    W_0065            6
     C                   PARM                    W_0068           35
     C                   PARM                    W_0057           10
     C                   PARM                    W_SN              9 0
     C                   PARM                    KD
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      * INIT AV nycklar
     C     SVIH_KEY      KLIST
     C                   KFLD                    W0062A
     C                   KFLD                    W0062B
     C     SVXKODL01K    KLIST
     C                   KFLD                    TKUNIK
     C                   KFLD                    TKKODE
     C                   MOVE      '023'         TKUNIK


     C     ENDINI        ENDSR
      *////////////////////////////////////////////////////////////
      * SR før att uppdatera filer                         FILUPD /
      *////////////////////////////////////////////////////////////
     C     FILUPD        BEGSR
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     W_0062        M_M0068
     C                   MOVE      W_0065        M_M0065
     C                   EVAL      M_M1225 = %SUBST(W_0057:3:3)
     C                   MOVE      W_SN          M_MSN
     C                   MOVE      'R'           M_MSR
     C                   MOVE      'C'           M_MST
     C                   MOVE      WDT           M_MDT
     C                   MOVE      WTM           M_MTM
     C                   EVAL      M_M1N07 = %SUBST(W_0057:3:3)
      * Hent ursprunglig sendning
     C     W_0020        CHAIN     EDIS2                              51
N03  C                   DOW       *IN51 = *OFF
N03  C                   IF        SSR = 'S'
N03  C                   LEAVE
N03  C                   END
N03  C     W_0020        READE     EDIS2                                  51
N03  C                   ENDDO
     C     *IN51         CABEQ     *ON           FILUPD9
      * Læs alla meddelanden
     C     SSN           SETLL     EDIM6
     C     SSN           READE     EDIM6                                  52
     C     *IN52         DOWEQ     *OFF

     C                   EVAL      W0062 = M0062
     C                   IF        M1001 = 'STE'
     C     SVIH_KEY      CHAIN     SVXH                               53
     C                   IF        W_0057 = '907'
     C                   EVAL      W0062 = THTUID
     C                   END
     C                   EXSR      FILUPTE
     C                   ELSE
     C                   IF        M1001 = 'STI'
     C     SVIH_KEY      CHAIN     SVNH                               53
     C                   IF        W_0057 = '907'
     C                   EVAL      W0062 = TITUID
     C                   END
     C                   EXSR      FILUPTI
     C                   ELSE
     C     SVIH_KEY      CHAIN     SVIH                               53
     C                   IF        *IN53 = *OFF
     C                   EXSR      FILUPDI
N01  C                   EVAL      M_M1N07 = 'SVI'
N01  C                   EVAL      M_M1225 = 'MTF'
     C                   ELSE
     C     SVIH_KEY      CHAIN     SVEH                               53
     C  N53              EXSR      FILUPDE
N01  C  N53              EVAL      M_M1N07 = 'SVE'
N01  C                   EVAL      M_M1225 = 'MTF'
     C                   ENDIF
     C                   END
     C                   END
      *
     C     1             CHAIN     EDIC                               33
     C  N33              ADD       1             CMN
     C  N33              UPDATE    EDICR
     C                   MOVE      M0010         M_M0004
     C                   MOVE      M0004         M_M0010
     C                   MOVE      M1001         M_M1001
     C                   MOVE      M1004         M_M1004
     C                   MOVE      CMN           M_MMN
     C                   MOVEL     M0062         M_M0062
     C                   MOVE      M0068A        M_M0068A
     C                   MOVE      M0068B        M_M0068B
     C                   MOVE      MAVD          M_MAVD
     C                   MOVE      MTDN          M_MTDN
     C                   WRITE     EDIMR
     C                   IF        ((M1001 = 'STE') OR (M1001 = 'STI'))
     C                   EXSR      OPDTVINF
     C                   END

     C     SSN           READE     EDIM6                                  52
     C  N52              ENDDO

     C     FILUPD9       ENDSR
      *////////////////////////////////////////////////////////////
      * SR før att uppdatera filer import                 FILUPDI /
      *////////////////////////////////////////////////////////////
     C     FILUPDI       BEGSR
     C     SVIH_SYST     IFEQ      'A'
     C     SVIH_SYST     OREQ      'C'
     C                   IF        W_0026 = 'CONTRL-IEMFKV'
     C                   EVAL      SVIH_SYST='K'
     C                   ELSE
     C                   EVAL      SVIH_SYST='F'
     C                   ENDIF
     C                   UPDATE    SVIHR
      * Skriv til historikfil
     C                   CLEAR                   SVIHHR
     C                   EVAL      SVIHH_SYA = SVIH_SYAV
     C                   EVAL      SVIHH_SYO = SVIH_SYOP
     C                   EVAL      SVIHH_DTH = WDT
     C                   EVAL      SVIHH_TMH = WTM
     C                   EVAL      SVIHH_SR  = 'R'
     C                   EVAL      SVIHH_TXH  = W_0026
     C                   WRITE     SVIHHR
N04  C                   ELSE
N04  C                   UPDATE    SVIHR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR før att uppdatera filer export                 FILUPDE /
      *////////////////////////////////////////////////////////////
     C     FILUPDE       BEGSR
     C     SVEH_SYST     IFEQ      'A'
     C     SVEH_SYST     OREQ      'C'
     C                   IF        W_0026 = 'CONTRL-IEMFKV'
     C                   EVAL      SVEH_SYST='K'
     C                   ELSE
     C                   EVAL      SVEH_SYST='F'
     C                   ENDIF
     C                   UPDATE    SVEHR
      * Skriv til historikfil
     C                   CLEAR                   SVEHHR
     C                   EVAL      SVEHH_SYA = SVEH_SYAV
     C                   EVAL      SVEHH_SYO = SVEH_SYOP
     C                   EVAL      SVEHH_DTH = WDT
     C                   EVAL      SVEHH_TMH = WTM
     C                   EVAL      SVEHH_SR  = 'R'
     C                   EVAL      SVEHH_TXH  = W_0026
     C                   WRITE     SVEHHR
N04  C                   ELSE
N04  C                   UPDATE    SVEHR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR før att uppdatera filer export                 FILUPTE /
      *////////////////////////////////////////////////////////////
     C     FILUPTE       BEGSR
     C     THST          IFEQ      'A'
     C     THST          OREQ      'C'
     C     THST          OREQ      'K'
N05  C     THWS          ANDEQ     *BLANKS
     C                   SELECT
     C                   WHEN      W_0026 = 'CONTRL-MFKV'
     C                   EVAL      THST='K'
     C                   WHEN      W_0026 = 'CONTRLTU-KV'
     C                   EVAL      THST='N'
     C                   WHEN      ((W_0026 = 'CONTRLTU-FEL') OR
     C                              (W_0026 = 'CONTRL-MFFEL'))
     C                   EVAL      THST='F'
     C                   ENDSL
     C                   UPDATE    SVXHR
      * Skriv til historikfil
     C                   CLEAR                   SVXHHR
     C                   EVAL      SVXHH_SYA = THAVD
     C                   EVAL      SVXHH_SYO = THTDN
     C                   EVAL      SVXHH_DTH = WDT
     C                   EVAL      SVXHH_TMH = WTM
     C                   EVAL      SVXHH_SR  = 'R'
     C                   EVAL      SVXHH_TXH  = W_0026
     C                   WRITE     SVXHHR
N04  C                   ELSE
N04  C                   UPDATE    SVXHR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR før att uppdatera filer import                 FILUPTI /
      *////////////////////////////////////////////////////////////
     C     FILUPTI       BEGSR
     C     TIST          IFEQ      'A'
     C     TIST          OREQ      'C'
     C     TIST          OREQ      'K'
     C                   SELECT
     C                   WHEN      W_0026 = 'CONTRL-MFKV'
     C                   EVAL      TIST='K'
     C                   WHEN      W_0026 = 'CONTRLTU-KV'
     C                   EVAL      TIST='N'
     C                   WHEN      ((W_0026 = 'CONTRLTU-FEL') OR
     C                              (W_0026 = 'CONTRL-MFFEL'))
S02  C*                  EVAL      TIST='F'
N02  C                   EVAL      TIST='%'
     C                   ENDSL
     C                   UPDATE    SVNHR
      * Skriv til historikfil
     C                   CLEAR                   SVNHHR
     C                   EVAL      SVNHH_SYA = TIAVD
     C                   EVAL      SVNHH_SYO = TITDN
     C                   EVAL      SVNHH_DTH = WDT
     C                   EVAL      SVNHH_TMH = WTM
     C                   EVAL      SVNHH_SR  = 'R'
     C                   EVAL      SVNHH_TXH  = W_0026
     C                   WRITE     SVNHHR
N04  C                   ELSE
N04  C                   UPDATE    SVNHR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR OPPDATER TVINF                                OPDTVINF /
      *////////////////////////////////////////////////////////////
     C     OPDTVINF      BEGSR
      *
     C                   Z-ADD     1             XN                3 0
     C                   DOW       XN < 10
     C                   IF        KD(XN) <> *BLANKS
     C                   EVAL      FMN    = M_MMN
     C                   EVAL      FLN    = FLN + 1
     C                   EVAL      F0077  = M1001
     C                   EVAL      TKKODE = KD(XN)
     C     SVXKODL01K    CHAIN     SVXKODL01                          33
     C                   EVAL      F0078A = %TRIM(KD(XN))
     C  N33              CAT       ':':0         F0078A
     C  N33              CAT       TKTXTN:2      F0078A
     C                   EVAL      XN = XN + 1
     C                   IF        KD(XN) <> *BLANKS
     C                   EVAL      TKKODE = KD(XN)
     C     SVXKODL01K    CHAIN     SVXKODL01                          33
     C                   EVAL      F0078B = %TRIM(KD(XN))
     C  N33              CAT       ':':0         F0078B
     C  N33              CAT       TKTXTN:2      F0078B
     C                   ENDIF
     C                   EVAL      XN = XN + 1
     C                   IF        KD(XN) <> *BLANKS
     C                   EVAL      TKKODE = KD(XN)
     C     SVXKODL01K    CHAIN     SVXKODL01                          33
     C                   EVAL      F0078C = %TRIM(KD(XN))
     C  N33              CAT       ':':0         F0078C
     C  N33              CAT       TKTXTN:2      F0078C
     C                   ENDIF
     C                   EVAL      XN = XN + 1
     C                   IF        KD(XN) <> *BLANKS
     C                   EVAL      TKKODE = KD(XN)
     C     SVXKODL01K    CHAIN     SVXKODL01                          33
     C                   EVAL      F0078D = %TRIM(KD(XN))
     C  N33              CAT       ':':0         F0078D
     C  N33              CAT       TKTXTN:2      F0078D
     C                   ENDIF
     C                   EVAL      XN = XN + 1
     C                   IF        KD(XN) <> *BLANKS
     C                   EVAL      TKKODE = KD(XN)
     C     SVXKODL01K    CHAIN     SVXKODL01                          33
     C                   EVAL      F0078E = %TRIM(KD(XN))
     C  N33              CAT       ':':0         F0078E
     C  N33              CAT       TKTXTN:2      F0078E
     C                   ENDIF
     C                   WRITE     TVINFF
     C                   CLEAR                   TVINFF
     C                   ENDIF
     C                   EVAL      XN = XN + 1
     C                   ENDDO
      *
     C                   ENDSR
