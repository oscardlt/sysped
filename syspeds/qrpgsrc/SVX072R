********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10311 * 01 YBC PTF10 DOKUMENTKODE ER ENDRET FRA 3A TIL 4A
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED NY DOKUMENT                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-86 95-97
      ***********************************************************
      *
     FSVNLDK    UF A E           K DISK
     FSVXKODL01 IF   E           K DISK
     FSVX072D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSSUBN               378    379B 0
     D  WSCLOC               382    383B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UTDTY         IFNE      *BLANKS
     C     UNDTY         ORNE      *BLANKS
     C   27UNDTY         IFEQ      UTDTY
     C     UNDREF        ANDEQ     UTDREF
     C     UNDO          ANDEQ     UTDO
     C                   GOTO      STORG
     C                   END
     C                   MOVE      UNDTY         NVDTY
     C                   MOVE      UNDREF        NVDREF
     C                   MOVE      UNDSK         NVDSK
     C                   MOVE      UNDO          NVDO
     C                   MOVE      UNDOSK        NVDOSK
     C     STORG         TAG
     C                   MOVE      UTDTY         TVDTY
     C                   MOVE      UTDREF        TVDREF
     C                   MOVE      UTDSK         TVDSK
     C                   MOVE      UTDO          TVDO
     C                   MOVE      UTDOSK        TVDOSK
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SVLDKK        SETLL     SVNLDK
     C     SVLDKK        READE(N)  SVNLDK                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SVLDKK        READE(N)  SVNLDK                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C   06              DO
     C                   EXSR      CRTDK
     C                   GOTO      START3
     C                   END
      *
     C  N27ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      15
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     7
     C                   MOVE      '013'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSDTY         UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         WSDTY
     C                   Z-ADD     9             POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      15
     C     POSNBR        ANDGE     30
     C     POSNBR        ANDLE     31
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSDSK         UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2            3
     C                   MOVEL     UKODE         WSDSK
     C                   Z-ADD     33            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     WSDTY         IFNE      *BLANKS
     C     TVDTY         OREQ      *BLANKS                                      NY LINJE
     C                   MOVE      '013'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSDTY         UKODE
     C     SVK01K        CHAIN     SVXKODL01                          90
     C     *IN90         IFEQ      '1'
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      'SVX0813'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C     TVDTY         IFEQ      *BLANKS
     C     WSDREF        COMP      *BLANKS                                89
     C     *IN89         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   MOVE      'SVX0814'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C     WSDSK         IFNE      *BLANKS
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSDSK         UKODE
     C     SVK01K        CHAIN     SVXKODL01                          88
     C     *IN88         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     30            POSNBR
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C     WSDOSK        IFNE      *BLANKS
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSDOSK        UKODE
     C     SVK01K        CHAIN     SVXKODL01                          87
     C     *IN87         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     60            POSNBR
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     SVLDKK        SETLL     SVNLDK
     C     SVLDKK        READE     SVNLDK                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SVNLDKR
     C     SVLDKK        READE     SVNLDK                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UTDTY
     C                   MOVE      *BLANKS       UTDREF
     C                   MOVE      *BLANKS       UTDSK
     C                   MOVE      *BLANKS       UTDO
     C                   MOVE      *BLANKS       UTDOSK
     C                   MOVE      *BLANKS       UNDTY
     C                   MOVE      *BLANKS       UNDREF
     C                   MOVE      *BLANKS       UNDSK
     C                   MOVE      *BLANKS       UNDO
     C                   MOVE      *BLANKS       UNDOSK
     C                   MOVE      *ZEROS        XANTL             3 0
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     NVDTY         IFNE      '*DL'
     C                   MOVE      NVDTY         UNDTY
     C                   MOVE      NVDREF        UNDREF
     C                   MOVE      NVDSK         UNDSK
     C                   MOVE      NVDO          UNDO
     C                   MOVE      NVDOSK        UNDOSK
     C                   MOVE      TVDTY         UTDTY
     C                   MOVE      TVDREF        UTDREF
     C                   MOVE      TVDSK         UTDSK
     C                   MOVE      TVDO          UTDO
     C                   MOVE      TVDOSK        UTDOSK
     C                   ADD       1             XANTL
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     NVDTY         IFNE      '*DL'
     C     XANTL         IFEQ      0
     C                   MOVE      NVDTY         UNDTY
     C                   MOVE      NVDREF        UNDREF
     C                   MOVE      NVDSK         UNDSK
     C                   MOVE      NVDO          UNDO
     C                   MOVE      NVDOSK        UNDOSK
     C                   MOVE      TVDTY         UTDTY
     C                   MOVE      TVDREF        UTDREF
     C                   MOVE      TVDSK         UTDSK
     C                   MOVE      TVDO          UTDO
     C                   MOVE      TVDOSK        UTDOSK
     C                   ADD       1             XANTL
     C                   ELSE
     C                   ADD       1             XANTL
     C                   WRITE     SVNLDKR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     NVDTY         COMP      '*DL'                                  98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGDK
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     NVDTY         COMP      '*DL'                                  98
     C  N98TVDTY         COMP      *BLANKS                            9898
     C   98              GOTO      SLHENT
     C                   EXSR      DLTDK
     C                   END
     C  N98WSNR          IFEQ      '9'
     C     NVDTY         COMP      '*DL'                                  98
     C  N98TVDTY         COMP      *BLANKS                                98
     C   98              GOTO      SLHENT
     C                   EXSR      ANGDK
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTDK         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       TVDTY
     C                   MOVE      *BLANKS       TVDREF
     C                   MOVE      *BLANKS       TVDSK
     C                   MOVE      *BLANKS       TVDO
     C                   MOVE      *BLANKS       TVDOSK
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      *BLANKS       WSDTY
     C                   MOVE      *BLANKS       WSDREF
     C                   MOVE      *BLANKS       WSDSK
     C                   MOVE      *BLANKS       WSDO
     C                   MOVE      *BLANKS       WSDOSK
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCRT         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCRT
     C                   END
     C   12              GOTO      SLCRT
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCRT
      *
     C                   EXSR      MOVWDK
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGDK         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      NVDTY         WSDTY
     C                   MOVE      NVDREF        WSDREF
     C                   MOVE      NVDSK         WSDSK
     C                   MOVE      NVDO          WSDO
     C                   MOVE      NVDOSK        WSDOSK
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCHG
     C                   EXSR      MOVWDK
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTDK         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       NVDTY
     C                   MOVEL     '*DL'         NVDTY
     C                   MOVE      *BLANKS       NVDREF
     C                   MOVE      *BLANKS       NVDSK
     C                   MOVE      *BLANKS       NVDO
     C                   MOVE      *BLANKS       NVDOSK
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     ANGDK         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      TVDTY         NVDTY
     C                   MOVE      TVDREF        NVDREF
     C                   MOVE      TVDSK         NVDSK
     C                   MOVE      TVDO          NVDO
     C                   MOVE      TVDOSK        NVDOSK
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWDK        BEGSR
     C***********************************************
     C                   MOVE      WSDTY         NVDTY
     C                   MOVE      WSDREF        NVDREF
     C                   MOVE      WSDSK         NVDSK
     C                   MOVE      WSDO          NVDO
     C                   MOVE      WSDOSK        NVDOSK
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SVLDKK        KLIST
     C                   KFLD                    TVAVD
     C                   KFLD                    TVTDN
     C                   KFLD                    TVLI
     C     SVK01K        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    UKODE
      *
     C     *ENTRY        PLIST
     C                   PARM                    TVAVD
     C                   PARM                    TVTDN
     C                   PARM                    TVLI
S01  C*                  PARM                    UNDTY             3
N01  C                   PARM                    UNDTY             4
     C                   PARM                    UNDREF           35
     C                   PARM                    UNDSK             2
     C                   PARM                    UNDO             26
     C                   PARM                    UNDOSK            2
S01  C*                  PARM                    UTDTY             3
N01  C                   PARM                    UTDTY             4
     C                   PARM                    UTDREF           35
     C                   PARM                    UTDSK             2
     C                   PARM                    UTDO             26
     C                   PARM                    UTDOSK            2
     C     *IN27         PARM                    UIN27             1
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      *BLANKS       UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
      *
     C                   ENDSR
      *
