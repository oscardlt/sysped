     H DATEDIT(*YMD)
     FEDIM      UF   E           K DISK
     FEDIMD     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMRD)
     FEDTM902   IF   E           K DISK
     FENAD902   IF   E           K DISK
     FEMEA902   IF   E           K DISK
     FEGID902   IF   E           K DISK
     FECNT902   IF   E           K DISK
     FE131D     CF   E             WORKSTN
     F                                     SFILE(E131DB:ZRECNO)
     D LDADTA         UDS            21
     D  USG                   19     21
     IMEAR
     I              MMN                         MEAMMN
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     E131DC
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C     KEYTVD        SETLL     EDIMD
     C     KEYTVD        READE     EDIMD                                  94
      *
     C     *IN94         DOWEQ     '0'
      *
     C                   MOVE      *BLANKS       ZAVS
     C                   MOVE      *BLANKS       ZMOTT
     C                   MOVE      *BLANKS       ZANT
     C                   MOVE      *BLANKS       ZVEKT
     C                   MOVE      *BLANKS       ZDATL
      *
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     MMN           ZMN
     C                   Z-ADD     7             ZGN
     C     KEYMSG        SETLL     ENAD902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMSG        READE     ENAD902                                51
     C     *IN51         DOWEQ     '0'
     C     N3035         IFEQ      'CZ '
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        ZAVS
     C                   ELSE
     C                   MOVEL     N3036A        ZAVS
     C                   ENDIF
     C                   ELSE
     C     N3035         IFEQ      'CN '
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        ZMOTT
     C                   ELSE
     C                   MOVEL     N3036A        ZMOTT
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C     KEYMSG        READE     ENAD902                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   Z-ADD     0             ZGN
     C     KEYMSG        CHAIN     ECNT902                            51
     C     *IN51         IFEQ      '0'
     C                   MOVEL     C6066A        ZANT
     C                   ENDIF
      *
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     11            ZGN
     C     KEYMSG        SETLL     EGID902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMSG        READE     EGID902                                51
     C     *IN51         DOWEQ     '0'
     C                   MOVEL     G7224A        ZANT
     C     KEYMSG        READE     EGID902                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     14            ZGN
     C     KEYMSG        SETLL     EMEA902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMSG        READE     EMEA902                                51
     C     *IN51         DOWEQ     '0'
     C     M6311         IFEQ      'GRO'
     C                   MOVEL     M6314         ZVEKT
     C                   ENDIF
     C     KEYMSG        READE     EMEA902                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN
     C     KEYMSG        SETLL     EDTM902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMSG        READE     EDTM902                                51
     C     *IN51         DOWEQ     '0'
     C     D2005         IFEQ      '175'
     C                   MOVEL     D2782         ZDATL
     C                   ENDIF
     C     KEYMSG        READE     EDTM902                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ADD       1             ZRECNO
     C                   WRITE     E131DB
     C     KEYTVD        READE     EDIMD                                  94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C     VISDE         TAG
     C                   EXFMT     E131DD
     C     *IN03         IFNE      '1'
     C     *IN12         ANDNE     '1'
     C                   GOTO      VISDE
     C                   END                                                    <*IN12
      *
     C                   ELSE                                                   <ZRECNO
     C                   WRITE     E131DD
     C                   EXFMT     E131DC
     C                   END                                                    <ZRECNO
      *
     C                   MOVEA     '0000000'     *IN(30)
      *
     C                   SETOFF                                       9998
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F5=Frisk opp
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN03
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   EXSR      GETALT
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   ENDIF                                                  <*IN03
      *** F12=Annuller
     C     *IN12         IFEQ      '1'
     C                   GOTO      START3
     C                   ENDIF                                                  <*IN12
     C                   ENDIF                                                  <ZRECNO
      *
     C                   GOTO      START3
      *
     C     ENDPGM        TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *                                                    GETALT /
      *////////////////////////////////////////////////////////////
     C     GETALT        BEGSR
      *
     C                   Z-ADD     0             OPTNBR            2 0
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     E131DB                                 98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   ENDIF                                                  <*IN99
     C                   GOTO      ENDGET
     C                   ENDIF                                                  <*IN98
     C                   MOVE      *OFF          *IN68
     C                   MOVE      *OFF          *IN70
      *** 1=Bekreft
     C     ZALT          IFEQ      '1'
     C                   MOVE      *BLANK        ZALT
     C                   UPDATE    E131DB
     C                   EXSR      CONFIR
     C                   GOTO      ENDGET
     C                   ENDIF                                                  <WSNR
      *** 5=Vis
     C     ZALT          IFEQ      '5'
     C                   MOVE      *BLANK        ZALT
     C                   UPDATE    E131DB
     C     MMN           CHAIN(N)  EDIM                               90
     C                   MOVEL     'E132R_'      ZPGM             10
     C                   MOVE      '902 '        ZPGM
     C                   Z-ADD     MMN           ZMN
     C                   CALL      ZPGM
     C                   PARM                    ZMN
      *
     C                   ENDIF
      *
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDGET        TAG
     C                   MOVE      *OFF          *IN54
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *
     C     KEYTVD        KLIST
     C                   KFLD                    ZST
     C                   KFLD                    ZSR
     C                   KFLD                    Z0065
     C     KEYMSG        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN
     C     *LIKE         DEFINE    MST           ZST
     C     *LIKE         DEFINE    MSR           ZSR
     C     *LIKE         DEFINE    M0065         Z0065
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN
     C                   Z-ADD     0             PPUNIK            9 0
     C                   MOVEL     *BLANKS       PPREFF           17
     C                   MOVE      ' '           ZST
     C                   MOVE      'R'           ZSR
     C                   MOVE      'IFTMIN'      Z0065
     C     *DTAARA       DEFINE    *LDA          LDADTA
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                    CONFIR /
      *////////////////////////////////////////////////////////////
     C     CONFIR        BEGSR
      *
     C     CON01         TAG
      *
     C                   EXFMT     E131DF
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   GOTO      CON99
     C                   ENDIF
     C                   MOVE      '0'           *IN30
      *_____________________
      * CONVERT TO SYSPED400
      *"""""""""""""""""""""
     C                   MOVEL     'E133R_'      WPGM              9
     C                   MOVE      '902'         WPGM
     C     ÆWRKOP        IFEQ      'Y'
     C     ÆWRKOP        OREQ      'J'
     C                   MOVEL     ÆSSG          ÆSSG
     C                   ELSE
     C                   MOVEL     'EDI'         ÆSSG
     C                   ENDIF
     C                   Z-ADD     ÆSAVD         HEAVD             4 0
     C                   Z-ADD     0             HEOPD             7 0
     C                   MOVEL     'E133R_'      ZPGM
     C                   MOVE      '902 '        ZPGM
     C                   Z-ADD     MMN           ZMN
     C                   CALL      ZPGM
     C                   PARM                    ZMN
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    ÆSSG
      *_______________________________
      * CALL HEADF Maintance program ?
      *""""""""""""""""""""""""""""""""
     C     ÆWRKOP        IFEQ      'Y'
     C     ÆWRKOP        OREQ      'J'
     C                   IN        LDADTA
     C                   MOVE      ÆSSG          USG
     C                   OUT       LDADTA
     C                   CALL      'SYFA10'
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    ÆSSG
     C                   PARM      'CHG'         UTYPE             3
     C                   PARM      ' '           UKODE             1
     C                   PARM      ' '           UDUMMY            1
     C                   PARM                    PPUNIK            9 0
     C                   PARM                    PPREFF           17
      *
     C                   ENDIF
      *____________
      * Update EDIM
      *""""""""""""
     C     MMN           CHAIN     EDIM                               90
     C     *IN90         IFEQ      '0'
     C                   MOVEL     'C'           MST
     C                   Z-ADD     HEAVD         MAVD
     C                   Z-ADD     HEOPD         MTDN
     C                   UPDATE    EDIMR
     C                   END                                                    <*IN90
      *______________________
      * Create IFTMCS records
      *""""""""""""""""""""""
     C                   MOVEL     'E134R_'      ZPGM
     C                   MOVE      '902 '        ZPGM
     C                   Z-ADD     MMN           ZMN
     C                   CALL      ZPGM
     C                   PARM                    ZMN
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    ÆSSG
     C                   PARM                    Æ2782
     C                   PARM                    Æ4440A
     C                   PARM                    Æ4440B
     C                   PARM                    Æ4440C
     C                   PARM                    Æ4440D
     C                   PARM                    Æ4440E
     C                   PARM                    Æ3413
     C                   PARM                    Æ4774
     C                   PARM      '   '         Æ4343             3
      *
     C     CON99         ENDSR
