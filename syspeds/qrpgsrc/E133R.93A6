BHR   * VISSE HARDKODEDE VERDIER
      *****************************************************************
      * MOTTAK AV IFTMIN FRA EURISCO/"SENDPAKKE"                      *
      * DETTE ER EN FORH≈NDSADVISERING OM AT SENDING ER REGISTRERT    *
      * I WEBLÿSNINGEN (NOE ER P≈ GANG...)                            *
      * LAGRES I HENOP/HENOS MED STATUS "Z"                           *
      * SENDERS KUNDENR MED NEGATIVT FORTEGN BRUKES SOM FAST LÿPENR   *
      * LÿPENR2 (LINJER I HENOS) SETTES FAST TIL 0                    *
      * VERDI I HENOP (HEADER) AKKUMULERES FORTLÿPENDE                *
      * SENINGSNUMMER SLETTES FRA FILEN N≈R IFCSUM MOTTARS (ELLER N≈R *
      * MELDING OM SLETTING MOTTAS...)                                *
      * VERDI I HENOP (HEADER) REDISSERES  FORTLÿPENDE                *
      * (SYNKRONISERING HENOP/HENOS BÿR SIKRES I NATTRUTINE)          *
      *****************************************************************
      *
     H            Y
     FCUNDL01 IF  E           K        DISK
     FEDIS    IF  E           K        DISK
     FEDIM    IF  E           K        DISK
     FEUNB    IF  E           K        DISK
     FEUNH    IF  E           K        DISK
     FFIRM    IF  E           K        DISK
     FECNT93A IF  E           K        DISK
     FEFTX93A IF  E           K        DISK
     FEGID93A IF  E           K        DISK
     FENAD93A IF  E           K        DISK
     FERFF93A IF  E           K        DISK
     FETSR93A IF  E           K        DISK
     FEEQD93A IF  E           K        DISK
     FEEQN93A IF  E           K        DISK
     FEMEA93A IF  E           K        DISK
     FKODTRI  IF  E           K        DISK
     FKODTAG  IF  E           K        DISK
     FHEAD35  IF  E           K        DISK
     FHENOP   UF  E           K        DISK                      A
     FHENOS   O   E                    DISK                      A
S01  F*ENOS01 IF  E           K        DISK
N01  FHENOS01 UF  E           K        DISK
     F            HENOSR                            KRENAMEHENOS01R
S01  F*ENOF   O   E                    DISK                      A
N01  FHENOF01 UF  E           K        DISK                      A
     E                    A          79  1
     E                    B          18  1
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     I            DS
     I                                        1   76ZDEC
     I                                        2   7 ZDEC6
     I                                       11  220ZHEL
     I            DS
     I                                        1   70WMN2
     I            DS
     I                                        1  35 M1004
     I                                        1  17 W1004A
     I            DS
     I                                        1  140XULTID
     I                                        1   60XULTM
     I                                        7   80XULDD
     I                                        9  100XULMM
     I                                       11  140XUL≈≈
     I            DS
     I                                        1   40UL≈≈
     I                                        5   60ULMM
     I                                        7   80ULDD
     I                                        1   80ULDT
     I            DS
     I                                        1  35 HERFK
     I                                        1  20 HXSNDN
     I                                       21  23 HXPALL
     I                                       33  33 HXEDIK
     I            DS
     I                                        1  30 XXDS
     I                                        1   8 XXDATE
     I                                        9  14 XXTIME
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                     EXSR INIT
      *________
      * CONVERT
      *""""""""
     C                     EXSR RTVD
      *
     C                     MOVE '1'       *INLR
      *
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C           RTVD      BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                     MOVEL'401'     SNDN20 20
     C                     MOVE W1004A    SNDN20
     C                     MOVELSNDN20    SNDN35 35
      * SLETTEMELDING ??
     C           U1225     IFEQ '3'
     C           U2005A    OREQ '3'                        FEIL EURISCO!!
     C                     EXSR DELPRE
     C                     GOTO NXTFBR
     C                     END
      * FINNES SENDINGEN ALLEREDE...
     C           SNDN20    SETLLHENOS01
S01  C*          SNDN20    READEHENOS01                  33
N01  C           SNDN20    READEHENOS01             N    33
     C           *IN33     CABEQ'0'       NXTFBR
      *
     C           SNDN35    SETLLHEAD35
     C                     READ HEAD35                   33
     C  N33      HXSNDN    CABEQSNDN20    NXTFBR
      *
      * LEGG INN SNDNR / TID FRA UNH/UNB - HENT RESTEN FRA EDISEGMENTER
     C                     CLEARHENOSR
     C                     MOVELSNDN20    HSSNDN
     C                     MOVELXXDS      HSDS
      *
     C                     MOVE '0'       *IN51
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                     Z-ADDCREP0     ZREPB0
     C                     EXSR NAD10
N01  C           HOKNS     CABEQ*ZEROS    NXTFBR
     C                     EXSR RFF03
     C                     EXSR FTX00
     C                     EXSR TSR00
     C                     EXSR CNT00
     C                     EXSR GID16
     C                     EXSR EQD34
      *________________
      * WRITE TO SYSPED
      *""""""""""""""""
     C                     MOVE HOLNR     HSLNR
     C                     MOVE HOST      HSST
     C                     WRITEHENOSR
      * ADDER VEKTER I HEADER
     C           HOLNR     CHAINHENOP                55
     C           *IN55     IFEQ *OFF
      * L≈NER FELTENE HOMINT / HOMINK FOR ANT SEND/ANT KLL.
     C                     ADD  1         HOMINT            *ANT SND
     C                     ADD  HSANT     HOMINK            *ANT KLL
     C                     ADD  HSVKT     HOVKT
     C                     ADD  HSM3      HOM3
     C                     ADD  HSLM      HOLM
     C                     UPDATHENOPR
     C                     END
      *
     C           NXTFBR    TAG
     C                     ENDSR
      *////////////////////////////////////////////////////////////
     C           DELPRE    BEGSR
      *////////////////////////////////////////////////////////////
      *__________________
      * DELETE PREADVICES
      *""""""""""""""""""
      * Fjern Sending / tell ned(/fjern)i header -  fra Preadv.
     C           SNDN20    SETLLHENOS01
     C           SNDN20    READEHENOS01                  33               33
     C           *IN33     DOWEQ'0'
     C           HSLNR     IFLT 0
     C                     DELETHENOS01R
     C           HSLNR     CHAINHENOP                33
     C           *IN33     IFEQ '0'
     C                     SUB  1         HOMINT                                 *ANT SND
     C                     SUB  HSANT     HOMINK                                 *ANT KLL
     C                     SUB  HSVKT     HOVKT
     C                     SUB  HSM3      HOM3
     C                     SUB  HSLM      HOLM
     C           HOMINT    IFGT 0
     C                     UPDATHENOPR
     C                     ELSE
     C                     DELETHENOPR
     C                     END
     C                     END
      *
      * Fjern Fritekster fra Preadv.
     C           HENOFK    KLIST
     C                     KFLD           HSLNR
     C                     KFLD           SNDN20
     C           HENOFK    SETLLHENOF01
     C           HENOFK    READEHENOF01                  33               33
     C           *IN33     DOWEQ'0'
     C                     DELETHENOFR
     C           HENOFK    READEHENOF01                  33               33
     C                     END
      *
     C                     END
     C           SNDN20    READEHENOS01                  33               33
     C                     END
      *
      *
     C           UTDEL     ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TSR00                                               TSR00 /
      *////////////////////////////////////////////////////////////
     C           TSR00     BEGSR
     C                     MOVE '0'       *IN51
     C                     Z-ADD0         ZGN0
     C           KEYMS0    SETLLETSR93A                  20
     C           *IN20     IFEQ '1'
     C           KEYMS0    READEETSR93A                  51
     C           *IN51     DOWEQ'0'
      * RMD=FARLIG (-> ADR-MERKING)  ZHO=VARME (-> IMD-MERKING)
     C                     SELEC
     C           T7085     WHEQ 'RMD'
     C                     MOVEL'X'       HSFARL           *FARLIG
     C           T7085     WHEQ 'ZHO'
     C                     MOVEL'X'       HSVARM           *VARME
     C           T7085     WHEQ 'ZPL'
     C                     MOVEL'P'       HSPROD           *PALL PRISET
     C           T7085     WHEQ 'ZKL'
     C                     MOVEL'K'       HSPROD           *KOLLIPRISET
     C                     ENDSL
     C  N51      KEYMS0    READEETSR93A                  51
     C  N51                ENDDO
     C                     ENDIF
      *
     C                     ENDSR
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C           CNT00     BEGSR
     C                     MOVE '0'       *IN51
     C                     Z-ADD0         ZGN0
     C           KEYMS0    SETLLECNT93A                  20
     C           *IN20     IFEQ '1'
     C           KEYMS0    READEECNT93A                  51
     C           *IN51     DOWEQ'0'
      *
     C                     MOVEAC6066     A
     C                     EXSR GETNUM
     C                     SELEC
     C           C6069     WHEQ '7  '
     C                     Z-ADDZNUM      HSVKT
     C           C6069     WHEQ '11 '
     C                     Z-ADDZNUM      HSANT
     C           C6069     WHEQ '15 '                       DMQ
     C           ZNUM      DIV  1000      HSM3
     C           C6069     WHEQ 'ZLM'
     C                     Z-ADDZNUM      HSLM
     C                     ENDSL
      *
     C  N51      KEYMS0    READEECNT93A                  51
     C  N51                ENDDO
     C                     ENDIF
      *
     C                     ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX00                                               FTX00 /
      *////////////////////////////////////////////////////////////
     C           FTX00     BEGSR
     C                     CLEARHENOFR
     C                     MOVE HOLNR     HFLNR
     C                     MOVE HSSNDN    HFSNDN
     C                     MOVE 'T'       HFKODE
     C                     MOVE '0'       *IN51
     C                     Z-ADD0         ZGN0
     C           KEYMS0    SETLLEFTX93A                  20
     C           KEYMS0    READEEFTX93A                  51
     C           *IN51     DOWEQ'0'
      * KUN MELDING TIL TRANSP. ER INTTERESSANT I FORH≈NDSADVISERINGEN
     C           F4451     IFEQ 'SIC'
     C                     MOVELF4440A    HFTXT
     C                     WRITEHENOFR
     C           F4440B    IFNE *BLANKS
     C                     MOVELF4440B    HFTXT
     C                     WRITEHENOFR
     C                     ENDIF
     C           F4440C    IFNE *BLANKS
     C                     MOVELF4440C    HFTXT
     C                     WRITEHENOFR
     C                     ENDIF
     C           F4440D    IFNE *BLANKS
     C                     MOVELF4440D    HFTXT
     C                     WRITEHENOFR
     C                     ENDIF
     C           F4440E    IFNE *BLANKS
     C                     MOVELF4440E    HFTXT
     C                     WRITEHENOFR
     C                     ENDIF
     C                     ENDIF
      *
      *
     C           KEYMS0    READEEFTX93A                  51
     C  N51                ENDDO
      *
     C                     ENDSR
      *////////////////////////////////////////////////////////////
      * FTX16                                               FTX16 /
      *////////////////////////////////////////////////////////////
     C           FTX16     BEGSR
     C                     MOVE '0'       *IN52
     C                     Z-ADD16        ZGN1
     C           KEYMS1    SETLLEFTX93A                  20
     C           *IN20     IFEQ '1'
     C           KEYMS1    READEEFTX93A                  52
     C           *IN52     DOWEQ'0'
      *
     C           F4451     IFEQ 'AAA'
     C           HSVB      CAT  F4440A:1  HSVB
     C                     ENDIF
      *
     C  N52      KEYMS1    READEEFTX93A                  52
     C  N52                ENDDO
     C                     ENDIF
      *
     C                     ENDSR
      *////////////////////////////////////////////////////////////
      * MEA19                                               MEA19 /
      *////////////////////////////////////////////////////////////
     C           MEA19     BEGSR
     C                     MOVE '0'       *IN52
     C                     Z-ADD19        ZGN1
     C           KEYMS1    SETLLEMEA93A                  20
     C           *IN20     IFEQ '1'
     C           KEYMS1    READEEMEA93A                  52
     C           *IN52     DOWEQ'0'
      *
     C                     MOVEAM6314     A
     C                     EXSR GETNUM
     C                     SELEC
     C           M6311     WHEQ 'WT '
     C                     ADD  ZNUM      XXVKT
     C           M6311     WHEQ 'LMT'
     C                     ADD  ZNUM      XXLM
     C           M6311     WHEQ 'VOL'
     C           M6411     IFEQ 'DMQ'
     C           ZNUM      DIV  1000      ZNUM      H
     C                     END
     C                     ADD  ZNUM      XXM3
     C                     ENDSL
      *
     C  N52      KEYMS1    READEEMEA93A                  52
     C  N52                ENDDO
     C                     ENDIF
      *
     C                     ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C           GETNUM    BEGSR
     C                     MOVE *ZEROS    ZHEL
     C                     MOVE *ZEROS    ZDEC
     C                     MOVE *ZEROS    B
     C                     Z-ADD1         AX      20
     C                     Z-ADD1         BX      20
      * Get Integer
     C           A,AX      DOWGE'0'
     C           ZHEL      MULT 10        ZHEL
     C                     MOVE A,AX      ZHEL
     C                     ADD  1         AX
     C                     ENDDO
      * Decimal point ?
     C           A,AX      IFEQ '.'
     C           A,AX      OREQ ','
     C                     ADD  1         AX
     C           A,AX      DOWGE'0'
     C                     MOVE A,AX      B,BX
     C                     ADD  1         AX
     C                     ADD  1         BX
     C                     ENDDO
     C                     MOVEAB         ZDEC6
     C                     ENDIF
      * Concatenate
     C                     Z-ADDZHEL      ZNUM   186
     C                     ADD  ZDEC      ZNUM
     C                     ENDSR
      *////////////////////////////////////////////////////////////
      * GID16                                               GID16 /
      *////////////////////////////////////////////////////////////
     C           GID16     BEGSR
     C                     MOVE *ZEROS    XXANT   70
     C                     MOVE *ZEROS    XXVKT   70
     C                     MOVE *ZEROS    XXM3    73
     C                     MOVE *ZEROS    XXLM    72
     C                     MOVE '0'       *IN51
     C                     Z-ADD16        ZGN0
     C           KEYMS0    SETLLEGID93A                  20
     C           *IN20     IFEQ '1'
     C           KEYMS0    READEEGID93A                  51
     C           *IN51     DOWEQ'0'
     C                     MOVEAG7224A    A
     C                     EXSR GETNUM
     C                     ADD  ZNUM      XXANT
     C                     MOVELG7064A    HEVS1
      *
     C                     Z-ADDGREP0     ZREPB0
     C                     EXSR FTX16
     C                     EXSR MEA19
      *
     C           KEYMS0    READEEGID93A                  51
     C  N51                ENDDO
     C                     ENDIF
     C           HSANT     IFEQ *ZEROS
     C                     Z-ADDXXANT     HSANT
     C                     END
     C           HSVKT     IFEQ *ZEROS
     C                     Z-ADDXXVKT     HSVKT
     C                     END
     C           HSM3      IFEQ *ZEROS
     C                     Z-ADDXXM3      HSM3
     C                     END
     C           HSLM      IFEQ *ZEROS
     C                     Z-ADDXXLM      HSLM
     C                     END
      *
     C                     ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C           INIT      BEGSR
     C           KEYMS0    KLIST
     C                     KFLD           ZMN
     C                     KFLD           ZGN0
     C           KEYMS1    KLIST
     C                     KFLD           ZMN
     C                     KFLD           ZGN1
     C                     KFLD           ZREPB0
     C           KODRIK    KLIST
     C                     KFLD           XXTERM  40
     C                     KFLD           NUMPNR  40
     C           KUNKEY    KLIST
     C                     KFLD           FIFIRM
     C                     KFLD           KUNDNR
     C           STEDKE    KLIST
     C                     KFLD           YYLAND  2
     C                     KFLD           YYSTED  5
     C                     MOVE 'NO'      YYLAND
      *
     C           *LIKE     DEFN NMN       ZMN
     C           *LIKE     DEFN NGN       ZGN0
     C           *LIKE     DEFN NGN       ZGN1
     C           *LIKE     DEFN NREP0     ZREPB0
      *
     C           *ENTRY    PLIST
     C                     PARM           WMN1    7
     C                     MOVELWMN1      WMN2
     C                     Z-ADDWMN2      ZMN
      * Get msg and interchange values
     C           ZMN       CHAINEUNH                 51
     C           ZMN       CHAINEDIM                 51
     C           MSN       CHAINEDIS                 51
     C           MSN       CHAINEUNB                 33
     C                     MOVEL'20'      XXDATE
     C                     MOVE U0017     XXDATE
     C                     MOVE '00'      XXTIME
     C                     MOVELU0019     XXTIME
     C           U0017     IFEQ *ZEROS
     C           U0017     OREQ *BLANKS
     C                     TIME           XULTID
     C                     MOVE XULDD     ULDD
     C                     MOVE XULMM     ULMM
     C                     MOVE XUL≈≈     UL≈≈
     C                     MOVE ULDT      XXDATE
     C                     MOVE XULTM     XXTIME
     C                     END
      *
     C           *LOVAL    SETLLFIRM
     C                     READ FIRM                     33
      *
     C                     ENDSR
      *////////////////////////////////////////////////////////////
      * NAD10                                               NAD10 /
      *////////////////////////////////////////////////////////////
     C           NAD10     BEGSR
     C                     Z-ADD10        ZGN0
     C           KEYMS0    SETLLENAD93A                  20
     C           *IN20     IFEQ '1'
     C           KEYMS0    READEENAD93A                  51
     C           *IN51     DOWEQ'0'
      *
     C                     SELEC
      *
     C           N3035     WHEQ 'CZ'                       AVSENDER
N01  C                     MOVE *ZEROS    HOKNS
     C                     MOVEAN3039     A
     C                     EXSR GETNUM
     C           ZNUM      IFNE *ZEROS
     C                     CLEARHENOPR
     C                     Z-SUBZNUM      HOLNR   70
     C           HOLNR     CHAINHENOP                55
     C                     Z-ADDZNUM      KUNDNR
     C           KUNKEY    CHAINCUNDL01              33
     C           *IN33     IFEQ '0'
     C                     MOVE KUNDNR    HOKNS
     C                     MOVELKNAVN     HONAS
     C                     MOVELADR1      HOADS1
     C                     MOVELPOSTNR    HOPNRS
     C                     MOVELADR3      HOSTDS
     C                     ELSE
     C                     MOVE *ZEROS    HOKNS
     C                     MOVE *BLANKS   HONAS
     C                     MOVE *BLANKS   HOADS1
     C                     MOVE *ZEROS    HOPNRS
     C                     MOVE *BLANKS   HOSTDS
     C                     END
BHR   * VIA MOTTAGER EDI FINN OM DET ER OSLO ELLER DRAMMEN..
BHR  C                     MOVELM0010     MOTEDI  8
BHR  C           MOTEDI    IFEQ 'RIKSGODS'
BHR  C                     MOVE 'Y'       HOST               OSLO
BHR  C                     MOVE 1000      XXTERM
BHR  C                     ELSE
     C                     MOVE 'Z'       HOST               DRAMMEN
     C                     MOVE 3000      XXTERM
BHR  C                     END
     C                     MOVE XXTERM    HOPNRT
     C   55                WRITEHENOPR
     C  N55                UPDATHENOPR
     C                     END
      *
      *
     C           N3035     WHEQ 'CN'                       MOTTAKER
     C                     MOVELN3036A    HSNAK
     C                     MOVELN3042A    HSADK1
     C                     MOVELN3251     HSPNRK
     C                     MOVELN3164     HSSTDK
      *
     C           N3035     WHEQ 'DP'                       LEV.ADR
     C                     MOVELN3036A    HSNAK
     C                     MOVELN3042A    HSADK1
     C                     MOVELN3251     HSPNRK
     C                     MOVELN3164     HSSTDK
      *
     C                     ENDSL
      *
     C           KEYMS0    READEENAD93A                  51
     C  N51                ENDDO
     C                     ENDIF
      *
      *
     C                     MOVE HSPNRK    NUMPNR
      * FINN AVD
     C           KODRIK    SETGTKODTRI
     C           XXTERM    REDPEKODTRI                   33
     C  N33      NUMPNR    IFGE RIPNRF
     C           NUMPNR    ANDLERIPNRT
     C                     MOVE RIAVDU    HSAVDH
     C                     END
      * VIA...
     C           HSAVDH    IFNE *ZEROS
     C           HSAVDH    CHAINKODTAG               33
     C  N33                MOVE 'J'       TRTSTA
     C                     END
      *
     C                     ENDSR
      *////////////////////////////////////////////////////////////
      * RFF03                                               RFF03 /
      *////////////////////////////////////////////////////////////
     C           RFF03     BEGSR
     C                     MOVE '0'       *IN51
     C                     Z-ADD3         ZGN0
     C           KEYMS0    SETLLERFF93A                  20
     C           *IN20     IFEQ '1'
     C           KEYMS0    READEERFF93A                  51
     C           *IN51     DOWEQ'0'
      *
     C           R1153     IFEQ 'CU '                         TG
     C           R1153     OREQ 'CO '                         LG
     C                     MOVELR1154     HSRFA
     C                     ENDIF
      *
     C  N51      KEYMS0    READEERFF93A                  51
     C  N51                ENDDO
     C                     ENDIF
      *
     C                     ENDSR
      *
      *////////////////////////////////////////////////////////////
      * EQD34                                               EQD13 /
      *////////////////////////////////////////////////////////////
     C           EQD34     BEGSR
     C                     MOVE *ZEROS    ANTPLL  30
     C                     Z-ADD34        ZGN0
     C                     Z-ADD34        ZGN1
      *
     C           KEYMS0    SETLLEEQD93A                  20
     C           *IN20     IFEQ '1'
     C           KEYMS0    READEEEQD93A                  51
     C           *IN51     DOWEQ'0'
      *
      * ETT ELLER 2 SEGMENT...
     C           E8053     IFEQ 'EFP'
     C                     Z-ADDEREP0     ZREPB0
     C           KEYMS1    CHAINEEQN93A              33
     C           *IN33     IFEQ '0'
     C           E6350     ANDNE*BLANKS
     C                     CLEARA
     C                     MOVEAE6350     A
     C                     EXSR GETNUM
     C                     Z-ADDZNUM      ANTPLL
     C                     END
     C                     END
      *
     C  N51      KEYMS0    READEEEQD93A                  51
     C  N51                END
     C                     END
     C                     MOVE ANTPLL    HSANTP
      *
     C                     ENDSR
