      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS216IN MODULE(*LIBL/SS216IN *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FGSSTRF    IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D UsrSpcName      s             10
     D WSTNO           S             11
     D BSTNO           S             11  0
     D IFSFIL          S             50
     D Dialog          S              1
     D EMNE            S             44
     D SENDER          S             17
     D CMD             S           5000
     D MELD            S           5000
     D XEPMOTT         S             50    DIM(5)
      * Number of records matching search criteria
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      ** Ved behov for dialog FØR spool kjøres legges den her...
     C     Dialog        IFeq      'J'
     c                   else
      *
      * ALT OK - aktuell printjobb skal kjøres...
     C     BSTNO         CHAIN     GSSTRF                             33
     C                   IF        ((((TRST = ' ') OR
     C                                (TRST = '1')) OR
     C                                (TRST = '2')) OR
     C                                (TRST = '3'))
      *  Kall prog for å generere html-fil eller PDF-fil i IFS.
     C                   EVAL      IFSFil = '/pdf/tmp/INV'
     C                             + WSTNO + '.PDF'
      *
     C                   call      'SS216CIN'
     c                   parm                    WSTNO
     C                   PARM                    IFSFIL
      * Set output skeleton html member name
      *-dummy html for å få kjørt prog                                      ******
      * "bytter ut seg selv" med Html-fil i IFS umiddelbart                 ******
     C                   exsr      SetSklSpool
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   callp     updhtmlvar('IFSFIL':IFSFIL)
     C                   callp     wrtsection('top')
      *
     C                   IF        XEPMOTT(1) <> *BLANKS                          3<-B
     C                   EXSR      SNDEMAIL
     C                   END                                                      3<-E
     C                   END
     c                   end
      *------------------                                                   ******
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C*
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     GSSTRF
     C                   CLOSE     HEADF
     C                   eval      *inlr = *on
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSTNO   = zhbgetvar('WSTNO')
     C                   eval      BSTNO   = c2n2(WSTNO)
     C                   eval      Dialog = zhbgetvar('Dialog')
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklSpool   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SPLHTMALL'
     C                   endsr
      *=====================================================================******
      * Send MEDLING TIL BRUKER
      *=====================================================================******
     C     SNDEMAIL      begsr
      *
     C                   Z-ADD     1024          CMDL             15 5
     C                   EVAL      SENDER = '*CURRENT'
     C                   MOVE      TRTNO         XTRTNO           11
     C                   MOVE      TRTNO         HEOPD
     C                   MOVEL     TRTNO         HEAVD
     C     HEADFK        CHAIN     HEADF                              33
      *
     C     1             DO        5             XN                3 0
     C                   IF        XEPMOTT(XN) <> *BLANKS
      * Emne
     C                   Eval      EMNE = 'Print proforma invoice of trip '
     C                                    + %trim(XTRTNO)
      *
     C                   EVAL      MELD = 'Proforma invoice is printed by '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ':/NAWB No. ' + %trim(HEGN)
     C                             + ':/P:/PThis message is created '
     C                             + 'automaticly by the booking system.'
     C                             + ':/NPlease do not reply!'
      *
      * param for tointnet og longmsg må være i apostrofer ( hex 7D )
      * (strenger i single-parametere som inneholder blank el. spesialtegn)
     C                   eval      cmd = 'SNDDST TYPE(*LMSG) TOINTNET('
     C                             +'('+ X'7D' +%trim(XEPMOTT(XN))+ X'7D' +')) +
     C                             DSTD('+ X'7D' + %trim(EMNE) + X'7D' + ') +
     C                             LONGMSG('+ X'7D' + %trim(MELD) + X'7D' +') +
     C                             USRID('+  %trim(SENDER) +') SNDFMT(*NOCHG)'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    CMD
     C                   PARM                    CMDL
      *
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      BRPARF
     C                   OPEN      GSSTRF
     C                   OPEN      HEADF
      *
     C     HEADFK        klist
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C     BrParKey      klist
     C                   kfld                    WSUSER
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      'EPOST'       PAID
     C                   Z-ADD     0             XN
     C     BrParKey      chain     BRPARF                             33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVEL     PAVRDA        XEPMOTT(XN)
     C     XN            COMP      5                                      33
     C  N33BrParKey      READE     BRPARF                                 33
     C                   ENDDO
      *
     C                   endsr
