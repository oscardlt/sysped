********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12237 * 01 YBC PTF11 KURS UTVIDES TIL 7,3
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOE032R MODULE(*LIBL/TNOE032R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSAEF      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D SFAVD           S              4S 0
     D SFOPDN          S              7S 0
     D SFTXT           S             17
     D SFDT            S              8S 0
     D SFBL28          S             12S 2
     D SFVK28          S              3
S01  D*SFKR28          S              6S 3
N01  D SFKR28          S              7S 3
     D SFOM28          S              3S 0
     D SFXXX           S              8

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SFAVD:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SFOPDN:'Z'))+'¬, '
        +'¬fak¬ : ¬'+%trim(SFTXT)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"invoiceUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SAEF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      SFAVD      = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      SFOPDN     = c2n2(WA)
     C                   EVAL      SFTXT      = zhbgetvar('FAK')
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      WA      = zhbgetvar('SFDT')
     C                   EVAL      SFDT    = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SFBL28')
N01  C                   EVAL      SFBL28  = c2n2(WA)
     C                   EVAL      SFVK28  = zhbgetvar('SFVK28')
     C                   EVAL      WA      = zhbgetvar('SFKR28')
N03  C                   EVAL      SFKR28  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SFOM28')
N04  C                   EVAL      SFOM28  = c2n2(WA)
     C                   EVAL      SFXXX   = zhbgetvar('SFXXX')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SAEFKEY       KLIST
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPDN
     C                   KFLD                    SFTXT
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SAEF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C*    WMODE         IFNE      'A'
     C     SAEFKEY       CHAIN     SAEF                               55
     C*                  ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   IF        *IN55=*OFF
     C                   eval      Error = 'Feil ! Inntastet finansiell +
     C                                      opplysning finnes allerede +
     C                                      på deklarasjonen !'
     C                   ELSE
     C                   EXSR      SRA
     C                   ENDIF
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   CLEAR                   SADFR
     C                   EVAL      XSFAVD=SFAVD
     C                   EVAL      XSFOPDN=SFOPDN
     C                   EVAL      XSFTXT=SFTXT
     C                   WRITE     SADFR
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    SADFR
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XSFAVD=SFAVD
     C                   EVAL      XSFOPDN=SFOPDN
     C                   EVAL      XSFTXT=SFTXT
     C                   EVAL      XSFDT=SFDT
     C                   EVAL      XSFBL28=SFBL28
N01  C                   EVAL      XSFVK28=SFVK28
     C                   EVAL      XSFKR28=SFKR28
     C                   EVAL      XSFOM28=SFOM28
     C                   EVAL      XSFXXX=SFXXX
     C                   UPDATE    SADFR
     C                   ENDSR
