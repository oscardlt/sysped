      * LEGGES in i WRKREGINF under:
      * Utgangspunkt:   QIBM_QTMF_SVR_LOGON      Format:   TCPL0200
      * ADDEXITPGM PGMNBR(1) PGM(SYFTPLO/SYSPED) MLTTHDACN(*MSG) PGMDTA(277 0 ' ')
      *
     HOPTION(*NODEBUGIO:*SRCSTMT) DFTACTGRP(*NO) ACTGRP(*CALLER)
     FSYFTPUF   IF   E           K DISK
     DSYFTPLO          PR
     D APPLICATIONID                 10I 0
     D USERID                       999A    OPTIONS(*VARSIZE)
     D USERIDLEN                     10I 0
     D AUTH                         999A    OPTIONS(*VARSIZE)
     D AUTHLEN                       10I 0
     D IPADDRESS                    999A    OPTIONS(*VARSIZE)
     D IPADDRESSLEN                  10I 0
     D ALLOWLOGIN                    10I 0
     D USERPROFILE                   10A
     D PASSWORD                      10A
     D CURLIB                        10A
     D HOMEDIR                      999A    OPTIONS(*VARSIZE)
     D HOMEDIRLEN                    10I 0
     D APPINFO                      999A    OPTIONS(*VARSIZE)
     D APPINFOLEN                    10I 0

     DSYFTPLO          pi
     D APPLICATIONID                 10i 0
     D UserID                       999a    Options(*VarSize)
     D UserIDLen                     10i 0
     D Auth                         999a    Options(*VarSize)
     D AuthLen                       10i 0
     D IPAddress                    999a    Options(*VarSize)
     D IPAddressLen                  10i 0
     D AllowLogin                    10i 0
     D UserProfile                   10a
     D Password                      10a
     D CurLib                        10a
     D HomeDir                      999a    Options(*VarSize)
     D HomeDirLen                    10i 0
     D AppInfo                      999a    Options(*VarSize)
     D AppInfoLen                    10i 0

     dAppInfoDS        ds                   Qualified
     d NameFmt                       10i 0
     d CWD                           10i 0
     d FileListFmt                   10i 0
     d ConnSecurity                  10i 0
     d DataEncrypt                   10i 0
     d ConnCipher                     5i 0
     d DataCipher                     5i 0


     dNeverAllow       c                   Const(-1)
     dDontAllow        c                   Const(0)
     dAllow            c                   Const(1)
     dAlwaysAllow      c                   Const(2)

     dWuserid          S             10
      /free
           AppInfoLen = %Size(AppInfoDS);
           If %Subst(UserID:1:UserIDLen) = 'ANONYMOUS';
               AllowLogin = DontAllow;
           Else;
               AllowLogin = Allow;
               Wuserid = %Subst(UserID:1:UserIDLen);
               Chain Wuserid SYFTPUF;
               If %found(SYFTPUF);
                 If SYFTPIFS = 'J';
                   AppInfoDS.NameFmt = 1;
                   AppInfoDS.CWD = 1;
                 else;
                   AppInfoDS.NameFmt = 0;
                   AppInfoDS.CWD = 0;
                 EndIF;
                   AppInfoDS.FileListFmt = 0;
                   AppInfoDS.ConnSecurity = 0;
                   AppInfoDS.DataEncrypt = 0;
                   AppInfoDS.ConnCipher = 0;
                   AppInfoDS.DataCipher = 0;
                 If SYFTPIFS = 'J';
                   HomeDir = SYFTPUDI;
                   HomeDirLen = %Len(%Trim(HomeDir));
                 else;
                   Curlib = SYFTPUDI;
                 EndIF;
                   %Subst(AppInfo:1:AppInfoLen) = AppInfoDS;
               EndIF;
             EndIf;
             *InLR = *On;
             Return;
