     A*%%TS  SD  20151228  141514  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A                                      PRINT
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 60 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            XVATA4         4A  O  5  5
     A            SVTX10_01     10A  H
     A            SVTX10_315    50A  O  5 10
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20151228  141514  CB          REL-V7R1M0  5770-WDS
     A  *DS4                                SFLSIZ(0011)
     A  *DS3                                SFLSIZ(0011)
     A  *DS4                                SFLPAG(0010)
     A  *DS3                                SFLPAG(0010)
     A  *DS4                                WINDOW(WINDOWSZ)
     A  *DS3                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVT004D'
     A                                      COLOR(BLU)
     A                                  1 23'Sökning av varukod'
     A                                      COLOR(WHT)
     A                                  2  2'Välj option:  1=Plocka'
     A                                      COLOR(BLU)
     A                                  3  2'Op Kod  Beskrivning'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A            W2SØK2         4A  I  4  5DSPATR(UL)
     A            W2SØK1        50A  I  4 10DSPATR(UL)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N30                              1  2'F3/F12=Avbryt   F5=Uppdatera'
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVT004R'
     A                                      COLOR(BLU)
     A                                  1 22'Sökning av varukod'
     A                                      COLOR(WHT)
     A                                  2  2'Välj option:  1=Plocka'
     A                                      COLOR(BLU)
     A                                  3  2'Op Kod  Beskrivning'
     A                                      COLOR(WHT)
     A            W2SØK2         4A  I  4  5DSPATR(UL)
     A            W2SØK1        50A  I  4 10DSPATR(UL)
     A                                 15  2'F3/F12=Avbryt   F5=Uppdatera'
     A                                      COLOR(BLU)
