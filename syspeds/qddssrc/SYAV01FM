     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            WSNRO          1A  B  8  2VALUES(' ' '2' '5')
     A            SFBILN        10   O  8  4
     A  44                                  COLOR(WHT)
     A            SFPRO          8  0O  8 15EDTCDE(4)
     A  44                                  COLOR(WHT)
     A            SFDATO         7  0O  8 24EDTCDE(4)
     A  47                                  COLOR(WHT)
     A            SFNTO          7Y 0O  8 33EDTCDE(4)
     A            SFNT           7Y 0O  8 42EDTCDE(4)
     A            SFVKT          7Y 0O  8 51EDTCDE(4)
     A            SFCBM          5Y 2O  8 60EDTCDE(4)
     A            SFBELN        11Y 2O  8 68EDTCDE(M)
     A                                      DSPATR(HI)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYAV01'
     A                                      COLOR(BLU)
     A                                  1 24'Distribusjonsturer til avregning'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  1 65SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 15'Modus...'
     A N45                              2 24'UFERDIGE/IKKE KONTROLLERTE'
     A  45N46                           2 24'FERDIGE/KONTROLLERTE '
     A                                      COLOR(RED)
     A  45 46                           2 24'**** FERDIG AVREGNET ****'
     A                                      COLOR(RED)
     A                                  2 72TIME
     A                                  3  2'Transportør:'
     A            TUKNT          8Y 0O  3 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  3 24COLOR(WHT)
     A                                  3 57'T.o.m. dato.:'
     A            W1DATO         6Y 0O  3 72COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 57'Kun avdeling:'
     A            W1AVD          4Y 0O  4 76COLOR(WHT)
     A                                      EDTCDE(4)
     A N46                              5  2'2=Endre / kontrollere   '
     A                                      COLOR(BLU)
     A                                  5 27'5=Se detaljer '
     A                                      COLOR(BLU)
     A N47                              7  2'O Bilnr.     Turnr.     Dato   Ant-
     A                                      .opp  Ant.kll     Vekt  Kubikk     -
     A                                       Beløp  '
     A                                      DSPATR(HI)
     A  47                              7  2'O Bilnr.     Turnr.    Avr.nr  Ant-
     A                                      .opp  Ant.kll     Vekt  Kubikk     -
     A                                       Beløp  '
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF07(07 'Endre MODUS/VIS AVR.NR')
     A                                      CF05(05 'Forny')
     A  45                                  CF08(08 'PRØVEUTSKR')
     A  45N46                               CF16(16 'SKRIV AVR.')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'Antall turer:'
     A            MRECNO         4Y 0O  1 17DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  1 23MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  45N46                           1 42'F8 =Prøveutskr.'
     A                                      COLOR(BLU)
     A                                  1 61'Sum:'
     A            SMBELN        10  2O  1 69DSPATR(HI)
     A                                      EDTCDE(M)
     A                                  2  3MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N46                              2 23'F7=Forande MODUS'
     A                                      COLOR(BLU)
     A  46                              2 23'F7=Vis avregn.nr'
     A                                      COLOR(BLU)
     A  45N46                           2 42'F16=Skriv avregning'
     A                                      COLOR(BLU)
     A  45 46                           2 42'F8 =Kopi-utskr.'
     A                                      COLOR(BLU)
     A                                  2 69MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              3  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Forny')
     A                                      CF07(07 'MODUS/VIS AVR.NR')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAV01'
     A                                      COLOR(BLU)
     A                                  1 24'Distribusjonsturer til avregning'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  1 65DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 15'Modus...'
     A N45                              2 24'UFERDIGE/IKKE KONTROLLERTE'
     A  45N46                           2 24'FERDIGE/KONROLLERTE  '
     A                                      COLOR(RED)
     A  45 46                           2 24'**** FERDIG AVREGNET ****'
     A                                      COLOR(RED)
     A                                  2 72TIME
     A                                  3  2'Transportør:'
     A            TUKNT          8Y 0O  3 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  3 24COLOR(WHT)
     A                                  3 57'T.o.m. dato.:'
     A            W1DATO         6Y 0O  3 72COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 57'Kun avdeling:'
     A            W1AVD          4Y 0O  4 76COLOR(WHT)
     A                                      EDTCDE(4)
     A N46                              5  2'2=Endre / kontrollere   '
     A                                      COLOR(BLU)
     A                                  5 27'5=Se detaljer '
     A                                      COLOR(BLU)
     A N47                              7  2'O Bilnr.     Turnr.     Dato   Ant-
     A                                      .opp  Ant.kll     Vekt  Kubikk     -
     A                                       Beløp  '
     A                                      DSPATR(HI)
     A  47                              7  2'O Bilnr.     Turnr.    Avr.nr  Ant-
     A                                      .opp  Ant.kll     Vekt  Kubikk     -
     A                                       Beløp  '
     A                                      DSPATR(HI)
     A                                 22 23MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 61'Sum:'
     A            SMBELN        10Y 2O 22 69DSPATR(HI)
     A                                      EDTCDE(M)
     A                                 23  3MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N46                             23 23'F7=Forande MODUS'
     A                                      COLOR(BLU)
     A  46                             23 23'F7=Vis avregn.nr'
     A                                      COLOR(BLU)
     A                                 23 69MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'SØK')
     A                                      CF12(03)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYAV01'
     A                                      COLOR(BLU)
     A                                  1 22'Transportøravregning Distribusjon '
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4 15'Transportør:'
     A            W1KNT          8Y 0B  4 29EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                  5 15'F.o.m. dato:'
     A            W1DATF         6Y 0B  5 29EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                  5 39'(DD/MM/ÅÅ)'
     A                                  6 15'T.o.m. dato:'
     A            W1DATO         6Y 0B  6 29EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                  6 39'(DD/MM/ÅÅ)'
     A                                  9 15'Kun avd....:'
     A            W1AVD          4Y 0B  9 29EDTCDE(4)
     A                                  9 39'(0=Alle)'
     A                                 10 15'Kun turnr..:'
     A            W1TUNR         8Y 0B 10 29EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                 10 39'(0=Alle)'
     A                                 12 15'Vis ferdige/'
     A                                 13 15'avregnede..:'
     A            W1VISA         1A  B 13 29
     A                                 13 39'(J/N)'
     A                                 14 15'Kun avr.nr.:'
     A            W1AVNR         7Y 0B 14 29EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                 16 15'Transp.utvalg'
     A                                 17 15'ved F4 søk.:'
     A            W1F4KD         1A  B 17 29
     A                                 17 39'(  A   = Alle transp. som avregnes-
     A                                        )'
     A                                 18 39'(Blank = Har uferdige/ukontrollert-
     A                                      e )'
     A                                 19 39'(  K   = Har ferdige/kontrollerte -
     A                                        )'
     A                                 20 39'(  B   = Begge, enten blank eller -
     A                                      K )'
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 27'F4=Søk transportør'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
