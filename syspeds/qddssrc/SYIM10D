********************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P504XA* 02 CB  PTF5 VEDLIKEHOLD KONTAKTPERSONER
PXXXXA* 03 YBC PTF5 VEDLIKEHOLD AV MILJØAVGIFT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLKUN' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF04(04 'Søk kunder')
     A                                      CF03(03 'Avslutt')
     A                                      CF10(10 'SEND MAIL')
     A            MSGNR          7A  P
     A                                  1  4'SYSPED/SYIM10'
     A                                      COLOR(BLU)
     A                                  1 19'Sende tollagerregnskap på mail'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 62TIME
     A                                      COLOR(BLU)
     A                                  8  4'Fra godsnummer.....:'
     A            WSGNFA         4A  B  8 26
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A            WSGNFB         5A  B  8 31
     A N30                                  DSPATR(PC)
     A            WSGNFC         3A  B  8 37
     A            WSGNFD         3A  B  8 41
     A                                 10  4'Til godsnummer.....:'
     A            WSGNTA         4A  B 10 26
     A            WSGNTB         5A  B 10 31
     A            WSGNTC         3A  B 10 37
     A            WSGNTD         3A  B 10 41
     A                                 12  4'Epost:'
     A            EPOST         70A  B 12 11CHECK(LC)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                 14  4'Utelat oppdragstyper:'
     A            ZOT01          2A  B 14 26
     A            ZOT02          2A  B 14 29
     A            ZOT03          2A  B 14 32
     A            ZOT04          2A  B 14 35
     A            ZOT05          2A  B 14 38
     A            ZOT06          2A  B 14 41
     A            ZOT07          2A  B 14 44
     A            ZOT08          2A  B 14 47
     A            ZOT09          2A  B 14 50
     A            ZOT10          2A  B 14 53
     A                                 16  4'Kun kundenummer.....:'
     A            WSKUND         8Y 0B 16 26EDTCDE(4)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            KNAVNS        30A  O 16 35
     A                                 18  4'Printernavn utskrift:'
     A            WSPRIN        10A  B 18 26
     A                                 20  4MSGCON(021 SHU1023 *LIBL/SYMSGF)
     A            WSSDV          1A  B 20 26VALUES('J' 'N' ' ')
     A                                 20 31MSGCON(005 TBR0254 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40'F10=send mail / Start utskrift'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
