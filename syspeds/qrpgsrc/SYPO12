      *
      ***********************************************************
      *** PROGRAM FOR BEREGNING AV MIN/MAX PRIS FOR KUNDERS   ***
      ***             EGNE FRAKTTABELLER                      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-31 34-99                      ***
      ***********************************************************
     FAVFRD     UF   E           K DISK
     FAVFRHL01  IF   E           K DISK
     C*
     C                   EXSR      INIT
     C*
     C     AVFRHK        CHAIN     AVFRHL01                           33
     C  N33AFMIMA        IFEQ      '1'
     C                   EXSR      RMINP
     C                   ELSE
     C     AFMIMA        IFEQ      '2'
     C                   EXSR      RMAXP
     C                   ELSE
     C     AFMIMA        IFEQ      '0'
     C                   EXSR      RNULL
     C                   END
     C                   END
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     RMINP         BEGSR
     C***********************************************
     C* Kode 1=Maxpris i et intervall skal legges inn i neste intervall
     C* som minimumspris (MAXPRIS legges i YPRIS)
     C                   Z-ADD     AFMINI        YPRIS            13 2
     C                   Z-ADD     1             XVEKT             9 0
     C                   Z-ADD     AFMINI        XMINI            13 2
      *
      * Beregn minpris for alle intervallene
     C     AVFRDK        SETLL     AVFRD
     C     STRMIN        TAG
     C     AVFRDK        READE     AVFRD                                  33
     C   33              GOTO      SLRMIN
      *
     C     AFCOBE        IFNE      'T'                                          1<-B
      * Ikke beregn minpris pga. pris skal beregnes fra sonetabell
     C                   MOVE      *ZEROS        AMIMAP
     C                   ELSE                                                   1
      * Finn minpris i denne intervallen (XVEKT=Minvekt i denne interv)
     C     AFPRPR        IFEQ      0                                             2<-B
     C                   Z-ADD     AFPRIA        AMIMAP
      *
     C                   ELSE                                                    2
     C                   Z-ADD     AFPRIA        XWORK            17 6
     C     AFPRPR        IFGT      1                                              3<-B
     C     AFPRIA        DIV(H)    AFPRPR        XWORK
     C                   END                                                      3<-E
     C                   MULT      XVEKT         XWORK
      * Hvis minpris (denne intervall) < maxpris (forrige interv.),
      * bruker maxpris (forrige interv.) som minpris i denne interv.
     C     XWORK         IFLT      YPRIS                                          3<-B
     C                   Z-ADD     YPRIS         AMIMAP
     C                   ELSE                                                     3
     C                   Z-ADD(H)  XWORK         AMIMAP
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C                   MOVE      AFMIMA        AMIMAC
     C     AMIMAP        IFLT      AFMINI
     C                   Z-ADD     AFMINI        AMIMAP
     C                   END
     C     AMIMAP        IFGT      AFMAXI
     C     AFMAXI        ANDNE     0
     C                   Z-ADD     AFMAXI        AMIMAP
     C                   END
     C     AFVEKT        IFNE      AFGREN                                       1<-B
     C     AMIMAP        ANDLT     XMINI
     C                   Z-ADD     XMINI         AMIMAP
     C                   END                                                    1<-E
     C                   Z-ADD     AMIMAP        XMINI
      * Oppdater minpris på filen.
     C                   EXCEPT    NYPRIS
      *
     C     AFVEKT        IFEQ      AFGREN                                       1<-B
      * Vekt er lik grensevekt. Dvs. at minpris i neste intervallen
      * ikke skal ta hensyn til maxpris i denne intervallen.
     C                   Z-ADD     AFMINI        YPRIS
     C                   Z-ADD     AFVEKT        XVEKT
     C                   Z-ADD     0             XMINI
     C                   ELSE                                                   1
      * Beregn maxpris for denne intervallen.
     C     AFPRPR        IFEQ      0                                             2<-B
     C                   Z-ADD     AFPRIA        XWORK
     C                   ELSE                                                    2
     C                   Z-ADD     AFVEKT        XVEKT
     C                   Z-ADD     AFPRIA        XWORK
     C     AFPRPR        IFGT      1                                              3<-B
     C     AFPRIA        DIV(H)    AFPRPR        XWORK
     C                   END                                                      3<-E
     C                   MULT      XVEKT         XWORK
     C                   END                                                     2<-E
     C                   Z-ADD     XWORK         YPRIS
      *
     C                   END                                                    1<-E
      * Beregn MAXvekt for DETTE INTERVALL
     C     AFVEKT        IFLT      999999999
     C                   Z-ADD     AFVEKT        XVEKT
     C                   ELSE
     C                   Z-ADD     999999999     XVEKT
     C                   END
      *
     C                   GOTO      STRMIN
     C*
     C     SLRMIN        ENDSR
     C*
     C***********************************************
     C     RMAXP         BEGSR
     C***********************************************
     C* Kode 2=Minpris i et intervall skal legges inn i forrige interv.
     C* som maximumspris (MINPRIS legges i XPRIS)
     C                   Z-ADD     1             XVEKT             9 0
     C     AFMAXI        IFEQ      0
     C                   MOVE      '9999999'     XMAXI            13 2
     C                   MOVEL     '999999'      XMAXI
     C                   ELSE
     C                   Z-ADD     AFMAXI        XMAXI            13 2
     C                   END
      *
      * Beregn maxpris for alle intervallene
     C     AVFRDK        SETGT     AVFRD
     C     STRMAX        TAG
     C     AVFRDK        READPE    AVFRD                                  33
     C   33              GOTO      SLRMAX
      *
     C     AFCOBE        IFNE      'T'                                          1<-B
      * Ikke beregn maxpris pga. pris skal beregnes fra sonetabell
     C                   MOVE      *ZEROS        AMIMAP
     C                   ELSE                                                   1
      *
     C     AFVEKT        IFEQ      AFGREN                                        2<-B
      * Vekt er lik grensevekt. Dvs. at maxpris i denne intervallen
      * ikke skal ta hensyn til minpris i neste intervallen.
     C                   Z-ADD     AFMAXI        YPRIS
     C                   ELSE                                                    2
      *
      * Beregn minpris for neste intervall.
     C     XFCOBE        IFNE      'T'                                            3<-B
     C                   MOVE      *ZEROS        YPRIS
     C                   ELSE                                                     3
     C     XVEKT         IFEQ      1                                               4<-B
     C                   Z-ADD     AFMAXI        YPRIS            13 2
     C                   ELSE                                                      4
     C     XFPRPR        IFEQ      0                                                5<-B
     C                   Z-ADD     XFPRIA        XWORK
     C                   ELSE                                                       5
     C                   Z-ADD     AFVEKT        XVEKT
     C     XFADJU        IFGT      0                                                 6<-B
     C     AFVEKT        ANDLT     999999999
     C                   Z-ADD     AFVEKT        XVEKT
     C                   END                                                         6<-E
     C                   Z-ADD     XFPRIA        XWORK
     C     XFPRPR        IFGT      1                                                 6<-B
     C     XFPRIA        DIV(H)    XFPRPR        XWORK
     C                   END                                                         6<-E
     C                   MULT      XVEKT         XWORK
     C                   END                                                        5<-B
     C                   Z-ADD     XWORK         YPRIS
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
      *
      * Finn maxpris i denne intervallen
     C     AFPRPR        IFEQ      0                                             2<-B
     C                   Z-ADD     AFPRIA        AMIMAP
      *
     C                   ELSE                                                    2
     C                   Z-ADD     AFVEKT        XVEKT
     C                   Z-ADD     AFPRIA        XWORK
     C     AFPRPR        IFGT      1                                              3<-B
     C     AFPRIA        DIV(H)    AFPRPR        XWORK
     C                   END                                                      3<-E
     C                   MULT      XVEKT         XWORK
      * Hvis maxpris (denne intervall) > minpris (neste interv.),
      * bruker minpris (neste interv.) som maxpris i denne interv.
     C     XWORK         IFGT      YPRIS                                          3<-B
     C     YPRIS         ANDNE     0
     C                   Z-ADD     YPRIS         AMIMAP
     C                   ELSE                                                     3
     C                   Z-ADD(H)  XWORK         AMIMAP
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
     C                   MOVE      AFMIMA        AMIMAC
     C     AMIMAP        IFGT      AFMAXI                                       1<-B
     C     AFMAXI        ANDNE     0
     C                   Z-ADD     AFMAXI        AMIMAP
     C                   END                                                    1<-E
     C     AMIMAP        IFLT      AFMINI
     C                   Z-ADD     AFMINI        AMIMAP
     C                   END
     C     AFVEKT        IFNE      AFGREN                                       1<-B
     C     AMIMAP        ANDGT     XMAXI
     C                   Z-ADD     XMAXI         AMIMAP
     C                   END                                                    1<-E
     C     AMIMAP        IFEQ      0                                            1<-B
     C     AFVEKT        IFEQ      AFGREN                                        2<-B
     C                   Z-ADD     AFMAXI        AMIMAP
     C                   ELSE                                                    2
     C                   Z-ADD     XMAXI         AMIMAP
     C                   END                                                     2<-E
<    C                   END                                                    1<-E
     C     AMIMAP        IFNE      0                                            1<-B
     C                   Z-ADD     AMIMAP        XMAXI
     C                   END                                                    1<-E
      * Oppdater maxpris på filen.
     C                   EXCEPT    NYPRIS
      * Bevar data av denne intervallen.
     C                   MOVE      AFPRIA        XFPRIA           13 2
     C                   MOVE      AFPRPR        XFPRPR            5 0
     C                   MOVE      AFADJU        XFADJU            5 0
     C                   MOVE      AFCOBE        XFCOBE            1
     C                   MOVE      *ZEROS        XVEKT
      *
     C                   GOTO      STRMAX
     C*
     C     SLRMAX        ENDSR
     C*
     C***********************************************
     C     RNULL         BEGSR
     C***********************************************
     C* Nullstil min/max pris
     C     AVFRDK        SETLL     AVFRD
     C     STRNUL        TAG
     C     AVFRDK        READE     AVFRD                                  33
     C   33              GOTO      SLRNUL
     C                   MOVE      *ZEROS        AMIMAP
     C                   MOVE      AFMIMA        AMIMAC
     C                   EXCEPT    NYPRIS
     C                   GOTO      STRNUL
     C*
     C     SLRNUL        ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     AVFRDK        KLIST
     C                   KFLD                    UFAVNR
     C                   KFLD                    UFCDE1
     C     AVFRHK        KLIST
     C                   KFLD                    UFAVNR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UFAVNR            9 0
     C                   PARM                    UFCDE1            1
     C*
     C                   ENDSR
     C*
     OAVFRDR    E            NYPRIS
     O                       AMIMAP
     O                       AMIMAC
