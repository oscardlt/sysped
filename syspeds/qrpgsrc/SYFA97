      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE OPPDRAGSTYPE FRA-TIL      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     FOAVT      UF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTOT2   IF   E           K DISK
     FSYFA97FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSTST                  1      4
     D  WSOTF                  1      2
     D  WSOT                   3      4
     D                 DS
     D  SFTST                  1      4
     D  SFOTF                  1      2
     D  SFOT                   3      4
     C*
     C                   EXSR      INIT
     C*
     C     CHOISE        TAG
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     17            POSNBR
     C                   EXFMT     BILDE1
     C                   SETOFF                                       9930
     C*
     C**  AVSLUTT
     C*
     C     *IN03         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END                                                    <*IN03
     C*
     C**  SØK
     C*
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKUNR
     C     KUNKEY        CHAIN     CUNDL01                            99
     C     *IN99         IFEQ      '1'
     C                   MOVE      *ZEROS        W1KNR
     C                   SETOFF                                       99
     C                   GOTO      CHOISE
     C                   END                                                    <*IN99
     C                   END                                                    <*IN04
     C*
     C     KUNKEY        CHAIN     CUNDL01                            35
     C     *IN35         IFEQ      '0'
     C                   MOVE      KNAVN         W1NAVN
     C                   ELSE
     C                   MOVE      '1'           *IN30
     C                   MOVE      'DIV2480'     MSGNR
     C                   GOTO      CHOISE
     C                   END                                                    <*IN35
     C*
     C     START         TAG
     C                   CLEAR                   SFOTF
     C                   CLEAR                   SFOT
     C     STARTF        TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SFKEY         SETLL     OAVT
     C     START1        TAG
     C                   READ      OAVT                                   94
     C     OAKNR         IFNE      W1KNR
     C                   MOVE      '1'           *IN94
     C                   END                                                    <OAKNR
     C  N94              MOVE      OAOTF         SFOTF
     C  N94              MOVE      OAOT          SFOT
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      OAVT                                   94
     C     OAKNR         IFNE      W1KNR
     C                   MOVE      '1'           *IN94
     C                   END                                                    <OAKNR
     C  N94              MOVE      OAOTF         SFOTF
     C  N94              MOVE      OAOT          SFOT
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYKUNDE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   Z-ADD     WSSUBN        XRECNO
     C                   EXFMT     BUNN
     C                   END
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       999830
     C*** AVSLUTT
     C     *IN03         IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END                                                    <*IN03
     C*** FRISK OPP
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN05
     C*** LAGE
     C     *IN06         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      START5
     C                   END                                                    <*IN06
     C*** SØK
     C     *IN08         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      START6
     C                   END                                                    <*IN08
     C*** FORRIGE
     C     *IN12         IFEQ      '1'
     C                   GOTO      CHOISE
     C                   END                                                    <*IN12
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94SFKEY         SETLL     OAVT
     C     START4        TAG
     C  N94              READ      OAVT                                   94
     C* N94      YSK       IFNE SFOTF
     C*                    GOTO START4
     C*                    END
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*                    GOTO START
     C*
     C*** NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000'       *IN(86)
     C                   SETOFF                                           30
     C   12              EXSR      MOVBWS
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*** SØK FRA AVDELING
     C     START6        TAG
     C                   EXFMT     VAREL2
     C     *IN12         IFNE      '1'
     C                   MOVE      WSOTF         SFOTF
     C                   GOTO      STARTF
     C                   END                                                    <*IN12
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*          N40 = NY
     C*           40 = ENDRING
     C*
     C*  TEST FOR GYLDIG FRA OPPDRAGSTYPE
     C*
     C                   MOVE      WSOTF         ZOT
     C     KODO2K        CHAIN     KODTOT2                            37
     C     *IN37         IFNE      '0'
     C                   MOVE      'DIV2483'     MSGNR
     C                   Z-ADD     10            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN37
     C                   GOTO      SLTB
     C                   END                                                    <*IN37
     C*
     C*  TEST FOR GYLDIG TIL OPPDRAGSTYPE
     C*
     C                   MOVE      WSOT          ZOT
     C     KODO2K        CHAIN     KODTOT2                            37
     C     *IN37         IFNE      '0'
     C                   MOVE      'DIV2483'     MSGNR
     C                   Z-ADD     21            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN37
     C                   GOTO      SLTB
     C                   END                                                    <*IN37
     C*
     C  N40WSKEY         CHAIN     OAVT                               33
     C*
     C*  TEST OM FRA OPPDRAGSTYPE FINNES FRA FØR VED ENDRING
     C*
     C     *IN40         IFEQ      '1'
     C     WSTST         IFNE      SFTST
     C     WSKEY         CHAIN     OAVT                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'DIV2486'     MSGNR
     C                   Z-ADD     10            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END                                                    <*IN33
     C                   END                                                    <WSTST
     C                   END                                                    <*IN40
     C*
     C   40SFKEY         CHAIN     OAVT                               33
     C*
     C*  TEST OM FRA OPPDRAGSTYPE FINNES FRA FØR VED NYREGISTRERING
     C*
     C     *IN33         IFEQ      '0'
     C     *IN40         ANDEQ     '0'
     C                   MOVE      'DIV2486'     MSGNR
     C                   Z-ADD     10            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END                                                    <*IN33
     C*
     C                   EXSR      MOVWKA
     C  N33              UPDATE    OAVTR                                38
     C   33              WRITE     OAVTR
     C*
     C                   EXSR      MOVWSF
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   EXSR      MOVBWS
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              MOVE      '1'           *IN30
     C******************** Z-ADD1         LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              4 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     SFOTF         COMP      '*D'                                   98
     C   98              MOVE      'SPS0029'     MSGNR
     C   98              MOVE      '1'           *IN30
     C   98              GOTO      SLHENT
     C                   EXSR      CHGST
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     SFOTF         COMP      '*D'                                   98
     C   98              MOVE      'SPS0029'     MSGNR
     C   98              MOVE      '1'           *IN30
     C   98              GOTO      SLHENT
     C                   EXSR      DLTST
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTST         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     SFKEY         CHAIN     OAVT                               33
     C  N33              DELETE    OAVTR
     C                   EXSR      MOVBSF
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGST         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   MOVE      '1'           *IN99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVBWS
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      SFOTF         WSOTF
     C                   MOVE      SFOT          WSOT
     C                   MOVE      '1'           *IN40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     10            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWKA        BEGSR
     C***********************************************
     C                   MOVE      W1KNR         OAKNR
     C                   MOVE      WSOTF         OAOTF
     C                   MOVE      WSOT          OAOT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C                   MOVE      WSOTF         SFOTF
     C                   MOVE      WSOT          SFOT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBSF        BEGSR
     C***********************************************
     C                   MOVE      '*D'          SFOTF
     C                   MOVE      'EL'          SFOT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBWS        BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSOTF
     C                   MOVE      *BLANKS       WSOT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     OAVTKY        KLIST
     C                   KFLD                    W1KNR
     C                   KFLD                    OAOTF
     C*
     C     SFKEY         KLIST
     C                   KFLD                    W1KNR
     C                   KFLD                    SFOTF
     C*
     C     WSKEY         KLIST
     C                   KFLD                    W1KNR
     C                   KFLD                    WSOTF
     C*
     C     *LIKE         DEFINE    FIRMA         ZFIRMA
     C     KUNKEY        KLIST
     C                   KFLD                    ZFIRMA
     C                   KFLD                    W1KNR
     C*
     C     KODO2K        KLIST
     C                   KFLD                    YO2UNI
     C                   KFLD                    ZOT               2
     C*
     C                   MOVE      'O2Y'         YO2UNI            3
     C*
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        ZFIRMA
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W1KNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   ENDSR
     C*
     C*
