      *********************************************************************
      * Print alle lapper for gitt tur (knyttet til Losselista)           *
      *            KUN på fraktbrev der det er satt "S" i label           *
      *********************************************************************
     FHEAD11    IF   E           K DISK
     FDOKUF     UF   E           K DISK
     FKODTP     IF   E           K DISK
     FTRAN      IF   E           K DISK

     D CmdLine         S            512
     D CmdLen          S             15  5

      *
     C                   EXSR      INIT
     C                   EXSR      MAIN
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *********************************************************************
     C     MAIN          BEGSR
      *********************************************************************
      *  For alle oppdrag på turen...
     c                   select
      * Via TUR
     c     UPRO          whenne    *zeros
     C     UPRO          SETLL     HEAD11
     C     UPRO          READE     HEAD11                                 55
     C     *IN55         DOWEQ     *OFF
     C                   EXSR      FBREV
     C     UPRO          READE     HEAD11                                 55
     C                   END
      * Via OPPDRAG
     c     UOPD          whenne    *zeros
     c                   movel     UAVD          HEAVD
     c                   movel     UOPD          HEOPD
      *   alle FB på ett
     c     Ufbnr         ifeq      *zeros
     C                   EXSR      FBREV
     c                   else
      *   kun ETT FB
     c     DOKKEYB       chain     DOKUF                              33
     c     *in33         ifeq      *off
     C     DFKDME        ifeq      'S'
     C                   EXSR      PRINT
     C                   endif
     c                   update    DOKUFR
     c                   end
     c                   end
     c
     c                   endsl
     C                   ENDSR
      *
      *********************************************************************
     C     FBREV         BEGSR
      *********************************************************************
      *  For alle fraktbrev på ett oppdrag - dersom label IKKE er printet..
     C     DOKKEY        SETLL     DOKUF
     C     DOKKEY        READE     DOKUF                                  55
     C     *IN55         DOWEQ     *OFF
     C     DFKDME        ifeq      'S'
     C                   EXSR      PRINT
     C                   END
     c                   update    DOKUFR
     C     DOKKEY        READE     DOKUF                                  55
     C                   END
     C                   ENDSR
      *********************************************************************
     C     PRINT         BEGSR
      *********************************************************************
      * Print labels  for ETT brev
     c     ELTRON        IFEQ      'J'
     C                   MOVE      'SYFA12SE  '  LABPGM           10
     c                   else
     C                   MOVE      'SYFA12SN  '  LABPGM           10
     c                   ENDIF
      * Posten..?
     c     DFtran        ifne      *zeros
     c     TraKey        chain     Tran                               33
S01  c**   *in33         ifeq      '1'
N01  c     *in33         ifeq      '0'
     c     VMLAYO        Ifeq      'PO'
     c     VMLAYO        oreq      'P4'
N01  C     DFKNM         OREQ      '0413898'                                    TG mypack via BRING
     c     ELTRON        IFEQ      'J'
     C                   MOVE      'SYFA12SF  '  LABPGM           10
     c                   else
     C                   MOVE      'SYFA12SP  '  LABPGM           10
     c                   endif
     c                   end
     c                   end
     c                   end
     c                   move      'N'           UUKOPI
     C                   Call      LABPGM
     C                   PARM                    UFIRM
     C                   PARM      '  '          VMLAYO            2
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    DFFBNR
     C                   PARM                    UUKOPI            1
     C* P=PRINTET
     C                   EVAL      DFKDME='P'
     C                   ENDSR
      *
      *********************************************************************
     C     INIT          BEGSR
      *********************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFBNR             3 0
     C                   PARM                    USNDNR           17
     C                   PARM                    UPRTKO            1
     C                   PARM                    UPRTOVER         10
      * ekstra parameter definert men ikke programmert ennå..:
      *      gitt SENDnr (via scan ??)
      *      UPRTKO- Tanke A = ALLE (også de med blank i prt-kod) O=Oppdrag(der FB mangler)
     C     TraKey        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    DFTRAN
     C     DOKKEY        KLIST
     C                   KFLD                    DFRI              1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   EVAL      DFRI='F'
     C     DOKKEYB       KLIST
     C                   KFLD                    DFRI              1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    UFBNR
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    KOPLNR
     C                   MOVE      'P'           WSPUNI            1
     C                   Z-ADD     67            KOPLNR
     C     KODTPK        CHAIN     KODTP                              33
     C                   MOVE      KOPNVN        UPRT67           10
     C     UPRTOVER      IFNE      *BLANKS
     C                   MOVE      UPRTOVER      UPRT67           10
     C                   END
     C                   MOVE      'N'           ELTRON            1
     C     KOPTY         IFEQ      'ELTRON'
     C                   MOVE      'J'           ELTRON
     C                   END
      * printerstyring / label-prog avhengig av type
     c     ELTRON        IFEQ      'J'
     C                   EVAL      CMDLINE = 'OVRPRTF FILE(SYLA03P) OUTQ('+
     C                             %trim(UPRT67)+') SPLFOWN(*JOB) ' +
     C                             'OVRSCOPE(*JOB)'
     c                   else
     C                   EVAL      CMDLINE = 'OVRPRTF FILE(ZBARC) OUTQ('+
     C                             %trim(UPRT67)+') SPLFOWN(*JOB) ' +
     C                             'OVRSCOPE(*JOB)'
     c                   ENDIF
      *
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen
      *
     C                   ENDSR
