      *******************************************************************************
      * Finn avgangsdager/framføringstid for en strekning basert på register LEVTID *
      *******************************************************************************
      * OBS!! Setter IKKE *LR av ytelseshensyn
      *
     FLEVTID01  IF   E           K DISK
      *
      * EKS -  tabell utsnitt fra LEVTID
      *   KUNDE P  FLK FrPn1 FrPn2  TLK TiPn1 TiPn2  Avg.dg Frdg Frti St-Ko SiBoo SiInn SiAvg SiLev
      *  118823    NO  2800  2899   NO  0001  9999   12345    3       OPPD  1000        1400  1200
      *  144681    NO  2800  2899   NO  4000  5999   25       2       OPPD  1000        1400  1200
      *  144681    NO  2800  2899   NO  0001  3999   12345    3       OPPD  1000        1400  1200
      *  144681    NO  2800  2899   NO  6000  9999   12345    3       OPPD  1000        1400  1200
      *
     c     *entry        plist
     c                   parm                    KundeNr           8 0
     c                   parm                    ProdKod           1
     c                   parm                    FraLK             2
     c                   parm                    FraPn             5
     c                   parm                    TilLK             2
     c                   parm                    TilPn             5
     c                   parm                    UavgDgr           7
     c                   parm                    UframfTid         2 0
     c                   parm                    UframfTimer       3 0
     c                   parm                    UStartKod         5            LTSTMK
     c                   parm                    USisteBook        4 0          LTBOTI
     c                   parm                    USisteInn         4 0          LTINTI
     c                   parm                    USisteAvg         4 0          LTAVTI
     c                   parm                    USisteLev         4 0          LTLETI
      *
     c                   move      *blanks       UavgDgr
     c                   move      *zeros        UframfTid
     c                   move      *zeros        UframfTimer
     c                   move      *blanks       UStartKod
     c                   move      *zeros        USisteBook
     c                   move      *zeros        USisteInn
     c                   move      *zeros        USisteAvg
     c                   move      *zeros        USisteLev
      *
     c                   move      KundeNr       LTKNR
     c                   move      ProdKod       LTPROD
     c     FraKey        klist
     c                   kfld                    LTKNR
     c                   kfld                    LTPROD
     c                   kfld                    TilLK
     c                   kfld                    FraLK
     c                   kfld                    FraPnX
      * Hjelpekey for å finne nærmeste lavere FRA-nummer (mot akt. tilLK)
     c     FraKeyE       klist
     c                   kfld                    LTKNR
     c                   kfld                    LTPROD
     c                   kfld                    TilLK
     c                   kfld                    FraLK
      * Hjelpekey for å finne nærmeste lavere TIL-nummer (fra akt. fraLK/fraPn)
     c     TilKey        klist
     c                   kfld                    LTKNR
     c                   kfld                    LTPROD
     c                   kfld                    TilLK
     c                   kfld                    FraLK
     c                   kfld                    FraPnX            5
     c                   kfld                    TilPnX            5
      *
      * 1 . finn føste post som på FRAsiden har lavere postnr (fra det 'indre i ring' gå utover)
     c                   move      FraPn         FraPnx
     c                   move      TilPn         TilPnx
     C     FraKey        setgt     LEVTID01
     C     FraKeyE       readpe    LEVTID01                               35
     c     *in35         cabeq     '1'           ikkeFunnet
     c                   If        FraPn<LTFPN1 or FraPn>LTFPN2                 Foregåed post er et
     c                   goto      ikkeFunnet                                   omr. som ikke dekker
     c                   endif                                                  akt postnr..
      * Kommet hit=Det finnes minst en post(mot akt tilland) der FRA stemmer (fra land/postnr)
      *
      * 2.  basert på fra-post, finn første post på tilside med lavere postnr..
     c                   move      LTFPN1        FraPnX
     C     TilKey        setgt     LEVTID01
     C     FraKey        readpe    LEVTID01                               35
     c     *in35         cabeq     '1'           ikkeFunnet
     c                   If        TilPn<LTTPN1 or TilPn>LTTPN2                 Foregåed post er et
     c                   goto      ikkeFunnet                                   omr. som ikke dekker
     c                   endif                                                  akt postnr..
      *
      * kommet hit uten skipp = OK - funnet post der både fra og til stemmer
     c                   move      LTAVGD        UavgDgr
     c                   move      LTFRFT        UframfTid
     c                   move      LTFRTT        UframfTimer
     c                   move      LTSTMK        UStartKod
     c                   move      LTBOTI        USisteBook
     c                   move      LTINTI        USisteInn
     c                   move      LTAVTI        USisteAvg
     c                   move      LTLETI        USisteLev
      *
     C     ikkeFunnet    tag
     C                   return
