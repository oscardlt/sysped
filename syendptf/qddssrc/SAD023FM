********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
10XXX * 02 TH HJELPETEKST HTML
10XXX * 05 sh  PTF10 søke på versjonsnummer
      * 10 SH  UNDELINE VED EPOST
10XXX * 03 YBC PTF10 UTVIDER TIL 27X132, INGEN MERKING
10XXX * 04 YBC PTF10 OPSJON 18 = NOTISBLOKK
10330 * 06 YBC PTF10 OPSJON 31 = ARBEID MED NCTS
XXXXXX*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
11007 * 07 YBC PTF11 VIS J/N PÅ OM EPOST ER SENDT TIL KUNDE
11016 * 08 YBC PTF11 OPSJON 12 = OMBEREGNING, 15 = VIS OMBEREGNING
11XXX * 09 YBC PTF11 VIS LØPENR FOR VIS OMBEREGNING
11XXX * 10 YBC PTF11 OPSJON 16 = PRINT OMBEREGNING
11109 * 11 YBC PTF11 OMBEREGNING PÅ OMBEREGNING
11183 * 12 YBC PTF11 EKSP.ENHET MED SØKING AV LØPENR
XXXXXA*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
12050 * 13 YBC PTF12 SØK OPPDRAG VIA FAKTURANR, MERKING/CONTAINER/UNDERST.
12107 * 14 YBC PTF12 SØK OMBEREGNING VIA STATUS.
12201 * 15 YBC PTF12 CONTAINERNR I EGET FELT
12201 * 16 SH  PTF12 SØK FORETAKSNUMMER
12248 * 17 YBC PTF12 VEKT, KOLLI ER UTVIDET.
12255 * 18 YBC PTF12 F13 = oppgjør av midlertidig import til ordinær utførsel
********************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD061PNL)
     A                                      HLPTITLE('SAD EKSPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde/leverandør')
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD023'
     A                                      COLOR(BLU)
     A N28                              1 20'Arbeide med SAD-eksport'
     A                                      COLOR(WHT)
     A  28                              1 30'Spørring på SAD-eksport'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  4 13'Avdeling:'
     A            WSAAVD         4Y 0B  4 23TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('Tastet avdeling er ikke reg-
     A                                      istrert.' 99)
     A            KOANVN        33A  O  4 30TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  8 20'EKSTERN REF'
     A            WFSSOK        35A  B  8 35TEXT('CL-REF')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  7 20'Velg søkevei med startpunkt'
     A                                      COLOR(WHT)
     A                                  9 20'Dato:'
     A            WSDT           6Y 0B  9 35TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  98                                  ERRMSG('Du må velge et av de startp-
     A                                      unktene.' 98)
     A                                 10 20'Tolldek.nr.'
     A            WSTDN          7Y 0B 10 35TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20'Avsendersnr.'
     A            WSKNK          8Y 0B 11 35TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  97                                  ERRMSG('Tastet kundenr. finnes ikke-
     A                                      . Frøv F5 for kundesøk.' 97)
     A                                 11 48'Dato:'
     A            WSDTK          6Y 0B 11 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20'Mottakersnr.'
     A            WSKNS          8Y 0B 12 35TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  96                                  ERRMSG('Tastet lev.nr. finnes ikke.-
     A                                       Prøv F5 for lev.søking' 96)
     A                                 12 48'Dato:'
     A            WSDTS          6Y 0B 12 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13 20'Tariffør:'
     A            WSSG           3A  B 13 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 48'Dato:'
     A            WSDTT          6Y 0B 13 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 20'Løpenummer:'
     A            WSTLL         10A  B 14 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
N12  A                                 15 23'Eksp.enhet:'
N12  A            WSTLE          6Y 0B 15 38DSPATR(UL)
N12  A                                      EDTCDE(Z)
N12  A                                      COLOR(YLW)
N12   * START  LINJE PÅ ALLE FELTER ER + 1.
     A                                 16 20'Godsnummer:'
     A            WSAAR          4A  B 16 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGN          11A  B 16 40TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 20MSGCON(014 YBC0053 *LIBL/SYMSGF)
     A            WSST           1A  B 17 35TEXT('STATUS')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
N14  A                                 17 48'OMB status:'
N14  A            WSSTOMB        1A  B 17 60TEXT('STATUS')
N14  A                                      DSPATR(UL)
N14  A                                      COLOR(YLW)
     A                                 18 20MSGCON(014 DIV1909 *LIBL/SYMSGF)
     A            WSMI           1A  B 18 35TEXT('Midlertidig  /I')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'I')
N05  A                                 19 20MSGCON(013 SHU1234 *LIBL/SYMSGF)
N05  A            WSSEKV         6Y 0B 19 35DSPATR(UL)
N05  A                                      COLOR(YLW)
N05  A                                      EDTCDE(Z)
N05  A                                 19 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
N05  A            WSSEKD         6Y 0B 19 55TEXT('Registreringsdato')
N05  A                                      DSPATR(UL)
N05  A                                      COLOR(YLW)
N05  A                                      EDTCDE(4)
N16  A                                 20 20MSGCON(013 SHU1279 *LIBL/SYMSGF)
N16  A            WSRG          11A  B 20 35DSPATR(UL)
N16  A                                      COLOR(YLW)
N16  A                                 20 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
N16  A            WSDTRG         6Y 0B 20 55TEXT('Registreringsdato')
N16  A                                      DSPATR(UL)
N16  A                                      COLOR(YLW)
N16  A                                      EDTCDE(4)
N13  A                                 21 20MSGCON(013 YBC2022 *LIBL/SYMSGF)
N13  A            WSFN          17A  B 21 35DSPATR(UL)
N13  A                                      COLOR(YLW)
N13  A                                 22 20MSGCON(013 YBC2023 *LIBL/SYMSGF)
S15  A*           WSMRK         28A  B 22 35DSPATR(UL)
N15  A            WSMRK         17A  B 22 35DSPATR(UL)
N13  A                                      COLOR(YLW)
N12   * SLUTT  LINJE PÅ ALLE FELTER ER + 1.
     A                                 26 20'F3 = Avslutt'
     A                                      COLOR(BLU)
     A                                 26 36'F4 = Søk kunde/leverandør'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            SEKNK          8Y 0H
N10  A            XIN50          1   H
N04  A            XIN36          1   H
N05  A            SETLE          6A  H
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            SEAVD          4Y 0O  6  5TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A            SETDN          7Y 0O  6 10TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  42                                  COLOR(TRQ)
N04  A  36                                  DSPATR(RI)
     A            SESG           3A  O  6 18TEXT('Signatur')
     A  45                                  COLOR(TRQ)
     A            WSDT2          6S 0O  6 22TEXT('Registreringsdato')
     A  41                                  COLOR(TRQ)
N06  A            SEDTG         10A  H
     A            SETLL         10A  H
     A            XSETLL        10A  O  6 29TEXT('Tollnr Løpenr')
N05  A  38
E05  AO 46                                  COLOR(TRQ)
S05  A*           SETLE          6A  O  6 38TEXT('Tollnr Ekspsted')
N05  A            XSETLE         6A  O  6 40TEXT('Tollnr Ekspsted')
N05  A  38
N05  AO 46                                  COLOR(TRQ)
     A            SEST           1A  O  6 47TEXT('Status')
N10  A  50                                  DSPATR(UL)
     A N49                                  COLOR(RED)
     A  49                                  COLOR(TRQ)
S09  A*           WSNAK         30A  O  6 49
N09  A            WSNAK         26A  O  6 49
     A  40
N13  AO 34
N13  AO 35
     AO 43                                  COLOR(TRQ)
S17  A*           SEVKB          9Y 0O  6 76TEXT('Bruttovekt')
N17  A            SEVKB         11Y 0H
N17  A            SEVKB09        9Y 0O  6 76TEXT('Bruttovekt')
     A                                      EDTCDE(4)
E09  A            SEMI           1A  O  6 86
     A  48                                  COLOR(TRQ)
E09  A            SEGN          15A  O  6 88TEXT('Godsnummer')
     A  47                                  COLOR(TRQ)
E09  A            WSEPJN         1A  O  6105COLOR(RED)
E09  A            O2_SEMF        3A  O  6107
E09  A            O2_SEST        1A  O  6111COLOR(PNK)
E09  A            WSDT3          6Y 0O  6114TEXT('Registreringsdato')
N08  A                                      EDTCDE(Z)
N09  A            O2_SETLL      10A  O  6121TEXT('Tollnr Løpenr')
N09  A  46                                  COLOR(TRQ)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N28                                  CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A                                      CF23(23 'Søk')
N18  A N28                                  CF13(13 'Oppgjør midlertidig imp')
     A N28                                  CF15(15 'Skriv ut')
     A N28                                  CF16(16 'Lag flere nye')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
N02  A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
N02  A                                      HLPARA(01 001 01 130)
N02  A          H                           HLPPNLGRP(SAD005020 SAD005PNL)
N02  A                                      HLPARA(02 001 02 130)
N02  A          H                           HLPPNLGRP(SAD005030 SAD005PNL)
N02  A                                      HLPARA(03 001 03 130)
N02  A          H                           HLPPNLGRP(SAD005040 SAD005PNL)
N02  A                                      HLPARA(04 001 04 130)
N02  A          H                           HLPPNLGRP(SAD005050 SAD005PNL)
N02  A                                      HLPARA(05 001 05 130)
N02  A          H                           HLPPNLGRP(SAD005030 SAD005PNL)
N02  A                                      HLPARA(06 001 06 004)
N02  A          H                           HLPPNLGRP(SAD005060 SAD005PNL)
N02  A                                      HLPARA(06 005 06 092)
N02  A          H                           HLPPNLGRP(SAD005065 SAD005PNL)
N02  A                                      HLPARA(06 069 06 130)
N02  A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
N02  A                                      HLPARA(07 001 21 130)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD023'
     A                                      COLOR(BLU)
     A N28                              1 20'Arbeide med SAD-eksport'
     A                                      COLOR(WHT)
     A  28                              1 20'Spørring på SAD-eksport'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            WSAAVD         4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 17TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
N08  A N28N77                           2 62'51=Arkiv 52=Pr.Sk.ark'
N08  A                                      COLOR(BLU)
N11  A N28N77                           2 84'17=Angre OMB på OMB'
N11  A                                      COLOR(BLU)
     A  77                              2 84'19=Se på epost 25=T&T LOGG'
     A                                      COLOR(BLU)
     A N28N77                           3  2'Velg  1=Lage 2=Endre 3=Kopi 5=Vis -
N08  A                                      6=Print 9=TVINN-logg 12=Omberegn.  -
E10  A                                      15=Vis omb. 16=Print OMB 18=Notisbl-
E10  A                                      okk 22=PDF 31=NCTS'
S08  A*                                     6=Print 9=TVINN-logg 18=Notisblokk -
S08  A*                                     22=PDF 31=NCTS 52=Pr.Sk.ark'
S05  A*                                     22=PDF 52=Pr.Sk.ark'
S04  A*                                     6=Print 9=TVINN-logg 22=PDF -
S04  A*                                     52=Pr.Sk.ark'
     A                                      COLOR(BLU)
     A N28 77                           3  2'51=Arkiverte dokumenter 88=Omsend-
     A                                      e til Emma 89=Omsende omb. til Emma'
     A                                      COLOR(BLU)
     A  28N77                           3  2'Velg  5=Vis SAD  6=Print -
N08  A                                       9=Vis TVINN-logg  12=Vis omberegn-
E10  A                                      .  16=Print OMB  22=Vis PDF  51=Ar-
E10  A                                      kiv  52=Pr.Sk.=ark'
S08  A*                                      9=Vis TVINN-logg  22=Vis PDF 52=-
S08  A*                                     Pr.Sk.ark  7=Frisøk'
     A  28 77                           3  2'51=Arkiverte dokumenter'
     A                                      COLOR(BLU)
S05  A*                                 4  2MSGCON(078 YBC0049 *LIBL/SYMSGF)
N05  A N38                              4  2MSGCON(130 YBC0049 *LIBL/SYMSGF)
N05  A                                      COLOR(WHT)
N05  A  38                              4  2MSGCON(130 SHU1236 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TLL         10A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 47DSPATR(UL)
E09  A            W2MI           1A  B  5 86DSPATR(UL)
E09  A            W2AAR          4A  B  5 88DSPATR(UL)
E09  A            W2GN          11A  B  5 93DSPATR(UL)
N14  A            W2STOMB        1A  B  5111DSPATR(UL)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
N02  A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
N02  A                                      HLPARA(01 001 01 130)
     A  28                             26  2'F3=Avslutt   F5=Frisk   F8=Søk'
     A                                      COLOR(BLU)
     A N28                             26  2'F3=Avslutt   F5=Frisk   F6=Lage ny-
     A                                         F8=Søk   F15=Skriv ut   F16=Lage-
     A                                       flere   F23=flere opsjoner'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N28                                  CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A N28                                  CF15(15 'Skriv ut')
     A N28                                  CF16(16 'Lag flere nye')
N02  A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
N02  A                                      HLPARA(01 001 01 130)
N02  A          H                           HLPPNLGRP(SAD005020 SAD005PNL)
N02  A                                      HLPARA(02 001 02 130)
N02  A          H                           HLPPNLGRP(SAD005030 SAD005PNL)
N02  A                                      HLPARA(03 001 03 130)
N02  A          H                           HLPPNLGRP(SAD005040 SAD005PNL)
N02  A                                      HLPARA(04 001 04 130)
N02  A          H                           HLPPNLGRP(SAD005050 SAD005PNL)
N02  A                                      HLPARA(05 001 05 130)
N02  A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
N02  A                                      HLPARA(06 001 22 130)
N02  A          H                           HLPPNLGRP(SAD005999 SAD005PNL)
N02  A                                      HLPARA(23 001 24 130)
     A                                  1  2'SYSPED/SAD023'
     A                                      COLOR(BLU)
     A N28                              1 20'Arbeide med SAD-eksport'
     A                                      COLOR(WHT)
     A  28                              1 20'Spørring på SAD-eksport'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            WSAAVD         4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 17TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A N28                              3  2'Velg,  1=Lage 2=Endre 3=Kopiere 5=-
N08  A                                       9=Vis TVINN-logg  12=Vis omberegn-
E10  A                                      15=Vis omb. 16=Print OMB 18=Notisbl-
E10  A                                      okk 22=PDF 31=NCTS'
S08  A*                                     Vise 6=Print 9=TVINN-logg 22=Vis PD-
S08  A*                                     F'
     A                                      COLOR(BLU)
     A  28                              3  2'Velg opsjon,   5=Vis SAD  6=Print -
N08  A                                       9=Vis TVINN-logg  12=Vis omberegn-
E10  A                                      .  16=Print OMB  22=Vis PDF  51=Ar-
E10  A                                      kiv  52=Pr.Sk.=ark'
S08  A*                                      9=Vis TVINN-logg  22=Vis PDF'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(130 YBC0049 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TLL         10A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 47DSPATR(UL)
E09  A            W2MI           1A  B  5 86DSPATR(UL)
E09  A            W2AAR          4A  B  5 88DSPATR(UL)
E09  A            W2GN          11A  B  5 93DSPATR(UL)
N14  A            W2STOMB        1A  B  5111DSPATR(UL)
     A N28                             26  2'F3=Avslutt   F5=Frisk   F6=Lage ny-
     A                                         F8=Søk   F15=Skriv ut   F16=Lage-
     A                                       flere'
     A                                      COLOR(BLU)
     A  28                             26  2'F3=Avslutt   F5=Frisk   F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 6 35 *NOMSGLIN)
     A                                      CA12(12 'GÅ TILBAKE')
     A                                      OVERLAY
     A                                  2  2'Printer til hovedark:'
     A            W3PRT1        10A  B  2 24DSPATR(UL)
     A                                  3  2'Printer til till.ark:'
     A            W3PRT2        10A  B  3 24DSPATR(UL)
     A                                  4  2'Printer til datalist:'
     A            W3PRT3        10A  B  4 24DSPATR(UL)
     A                                  5  2'F12=Avbryt'
     A                                      COLOR(BLU)
N11   *
N11  A          R BNYOMB
N11  A                                      WINDOW(*DFT 7 35 *NOMSGLIN)
N11  A                                      CA12(12 'GÅ TILBAKE')
N11  A                                      OVERLAY
N11  A                                  2  2'Denne fortollingen er allerede'
N11  A                                  3  2'omberegnet.'
N11  A                                  4  2'Ny omberegning ?'
N11  A            WSNYOMB        1A  B  4 24DSPATR(UL)
N11  A                                      VALUES('J' 'N')
N11  A                                  4 26'(J/N)'
N11  A                                  5  2'Basert på siste omb.?'
N11  A            WSSOMB         1A  B  5 24DSPATR(UL)
N11  A                                      VALUES('J' 'N')
N11  A                                  5 26'(J/N)'
N11  A                                  6  2'N=Original fortolling'
N18   *
N18  A          R BOPPGJ
N18  A                                      WINDOW(*DFT 7 42 *NOMSGLIN)
N18  A                                      CA12(12 'GÅ TILBAKE')
N18  A                                      OVERLAY
N18  A                                  2  2'Oppgjør av midlertidig import'
N18  A                                      COLOR(WHT)
N18  A                                  3  2'Full eksport'
N18  A                                      COLOR(WHT)
N18  A                                  4  2'Import avdeling:'
N18  A            WSAVOPG        4Y 0B  4 19DSPATR(UL)
N18  A  99
N18  AON30                                  DSPATR(PC)
N18  A  99                                  DSPATR(HI)
N18  A                                      EDTCDE(Z)
N18  A                                  5  2'Import oppdrag:'
N18  A            WSOPOPG        7Y 0B  5 19DSPATR(UL)
N18  A  98                                  DSPATR(PC)
N18  A  98                                  DSPATR(HI)
N18  A                                      EDTCDE(Z)
N18  A                                  6  2'F12=Avbryt'
N18  A                                      COLOR(BLU)
N18  A  30        XMSG          40A  O  7  2COLOR(WHT)
