********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6XXX * 01 YBC PTF6 SALDO ER ØKT FRA 5.0 TIL 7.0
P8058 * 02 YBC PTF8 NYE FELTER
P8XXX * 03 YBC PTF8 VIS STATUS OG F12
P8068A* 05 TH  PTF8 NY HELPFUNKSJON  KUN DDS
P9XXX * 06 YBC PTF9 M=MOTTATT, N=IKKE MOTTATT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
N05  A                                      HLPPNLGRP(HLPSCR LL018PNL)
N05  A                                      HLPTITLE('ARBEIDE MED SERIENR')
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
E03  A                                      WINDOW(*DFT 17 45 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
S06  A*           WSNR           1A  I  6  2VALUES(' ' '4')
N06  A            WSNR           1A  I  6  2VALUES(' ' '4' 'N' 'M')
S03  A*N28                                  DSPATR(PR)
N03  A  29                                  DSPATR(PR)
N02  A            SNMNU          7  0H
N02  A            SNVNI          3  0H
S03  A*           WSSN          35A  B  6  5
N03  A            SNST           1A  O  6  5
N03  A            WSSN          35A  B  6  8
     A  28                                  DSPATR(PR)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
N03  A                                      CA12(12 'AVSLUTT')
     A                                      OVERLAY
S05  A*         H                           HLPDOC('LL01801' 'LL018' 'SY400LO/-
S05  A*                                     LOGI')
N05  A          H                           HLPPNLGRP(HLPSCR LL018PNL)
     A                                      HLPARA(01 001 16 040)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL018R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(20 YBC1144 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(05 YBC1145 SYMSGF)
     A            UANR          15A  O  2  8COLOR(BLU)
     A                                  2 24MSGCON(04 YBC1146 SYMSGF)
E01  A            UANT           7  0O  2 29COLOR(YLW)
     A                                      EDTCDE(4)
E01  A            ZRECNO         4S 0O  2 37SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(09 YBC1147 SYMSGF)
     A            ULOK          25A  O  3 12COLOR(RED)
E06  A                                  4  2MSGCON(43 YBC1148 SYMSGF)
     A                                      COLOR(BLU)
S03  A*                                 5  2MSGCON(30 YBC1149 SYMSGF)
N03  A                                  5  2'Op St Serienummer'
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                 16  2MSGCON(30 YBC1150 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O 17  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             17  2'(C) COPYRIGHT  SYSTEMA A/S  2001'
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
