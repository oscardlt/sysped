*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0504A* 01 JOV VER5 BEGRENSE PÅ POSTNR / LANDKODE
P0504A* 02 JOV VER9 S0SKILLEARK / P= PDF-DOK
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/CUNDF)
     A                                      CF03(03 'Gå ut')
     A                                      CF12(12 'Forrige')
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R VALG
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYFA15'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 DIV1519 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(050 DIV1520 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(020 DIV1521 *LIBL/SYMSGF)
     A            ZSØNAV    R        B  5 24REFFLD(SONAVN)
N01  A                                  7  2MSGCON(020 JOV0854 *LIBL/SYMSGF)
N01  A                                  8  2MSGCON(020 JOV0855 *LIBL/SYMSGF)
N01  A            WSPOST         4D  B  8 24
N01  A                                 10  2MSGCON(020 JOV0856 *LIBL/SYMSGF)
N01  A            WSLK           2   B 10 24
     A*
     A          R SUBFIL                    SFL
     A            KNAVN     R        O  6  6
     A            KPERS     R        O  6 37
E01  A            KUNDNR    R        O  6 71EDTCDE(4)
N01  A            SFARKIV        1   O  6 80COLOR(RED)
     A            WSNR           1A  I  7  2
     A            ADR1      R        O  7  6
     A            TLF       R        O  7 37COLOR(WHT)
     A            TFAXNR    R        O  7 53COLOR(WHT)
     A            SFFORE         9A  O  7 71
     A            ADR2      R        O  8  6DSPATR(UL)
     A            WSADR3        30A  O  8 37DSPATR(UL)
     A            SYLAND    R        O  8 68DSPATR(UL)
     A            SYKONX         9A  O  8 72
     A                                      DSPATR(UL)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYFA15'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 DIV1519 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(050 DIV1520 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S02  A*                                 4  2MSGCON(070 SHU0681 *LIBL/SYMSGF)
N02  A                                  4  2MSGCON(070 JOV1428 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  4 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  5  2'WS'
     A                                      COLOR(WHT)
     A                                  5  6MSGCON(025 DIV1523 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 37MSGCON(030 DIV3561 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 69MSGCON(012 DIV1524 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                 22  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 25MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDEKU
     A                                  1  2'SYSPED/SYFA15'
     A                                      COLOR(BLU)
     A                                  2 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0682 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(025 DIV1592 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0O  3 32TEXT('Kundenummer')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  5  3MSGCON(025 DIV1593 *LIBL/SYMSGF)
     A            KNAVN         30A  O  5 32TEXT('Kundenavn')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  3MSGCON(025 DIV1594 *LIBL/SYMSGF)
     A            KPERS         30A  O  7 32TEXT('Kontaktperson')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  3MSGCON(025 DIV1595 *LIBL/SYMSGF)
     A            ADR1          30A  O  8 32TEXT('Adresse 1')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  3MSGCON(025 DIV1596 *LIBL/SYMSGF)
     A            ADR2          30A  O  9 32TEXT('Adresse 2')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  3MSGCON(025 DIV1597 *LIBL/SYMSGF)
     A            POSTNR         4Y 0O 10 32TEXT('Postnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10 37'/'
     A            SYPOGE         9A  O 10 39DSPATR(UL)
     A                                      COLOR(WHT)
     A            ADR3          24A  O 10 51TEXT('Sted')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  3MSGCON(025 DIV2005 *LIBL/SYMSGF)
     A            SYLAND         2A  O 11 32
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 51MSGCON(016 SHU0116 *LIBL/SYMSGF)
     A                                 12  3MSGCON(025 DIV1599 *LIBL/SYMSGF)
     A            SONAVN        10A  O 12 32TEXT('Søkenavn')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRNA    R        O 12 51REFFLD(VADRR/VADRNA *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  3MSGCON(025 DIV1600 *LIBL/SYMSGF)
     A            VALKOD         3A  O 13 32TEXT('Valutakode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN1    R        O 13 51REFFLD(VADRR/VADRN1 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  3MSGCON(025 DIV1601 *LIBL/SYMSGF)
     A            FMOT           8Y 0O 14 32TEXT('Fakturamottager v/kjede')
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN2    R        O 14 51REFFLD(VADRR/VADRN2 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15  3MSGCON(025 DIV1598 *LIBL/SYMSGF)
     A            TLF           15A  O 15 32TEXT('Telefon')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN3    R        O 15 51REFFLD(VADRR/VADRN3 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16  3MSGCON(025 DIV3560 *LIBL/SYMSGF)
     A            TFAXNR        15A  O 16 32
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17  3MSGCON(025 DIV1602 *LIBL/SYMSGF)
     A            SYMINU         4Y 2O 17 32TEXT('MIN.UTLPRO')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18  3MSGCON(025 DIV1603 *LIBL/SYMSGF)
     A            SYUTLP         4Y 2O 18 32TEXT('UTL.PROV')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18 42MSGCON(040 DIV1612 *LIBL/SYMSGF)
     A                                 19  3MSGCON(025 DIV1604 *LIBL/SYMSGF)
     A            SYREGN         8Y 0O 19 32TEXT('REGNR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 19 42MSGCON(025 DIV1605 *LIBL/SYMSGF)
     A            SYFR02         1A  O 19 69DSPATR(UL)
     A                                      COLOR(WHT)
     A            SYKONT    R        O 19 71REFFLD(KUNDR/SYKONT *LIBL/CUNDF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('     -  ')
     A                                 20  3MSGCON(025 DIV1606 *LIBL/SYMSGF)
     A            SYOPDT         2A  O 20 32TEXT('OPD.TYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20 42MSGCON(025 DIV1607 *LIBL/SYMSGF)
     A            SYLIKV         1A  O 20 70TEXT('LIKV.KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 21  3MSGCON(025 SHU0024 *LIBL/SYMSGF)
     A            SFAKT          1A  O 21 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 21 42MSGCON(025 SHU0172 *LIBL/SYMSGF)
     A            SYFORE         9A  O 21 70DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22  3MSGCON(025 SHU0139 *LIBL/SYMSGF)
     A            DKUND          1A  O 22 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22 42MSGCON(025 JOV0265 *LIBL/SYMSGF)
     A            SYFR03         2A  O 22 70
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
