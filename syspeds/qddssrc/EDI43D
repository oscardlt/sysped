N01   * LENGRE FELT
     A*%%TS  SD  20131112  185236  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/EDISSS)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      HLPDOC('EDI43R10' 'EDI43R' 'SY400LO-
     A                                      /EDI')
     A                                      HELP
      *
     A          R EDI43DB                   SFL
S01  A*           WDATA         78A  O  7  2
N01  A            WDATA        128A  O  7  2
     A*
     A          R EDI43DC                   SFLCTL(EDI43DB)
     A*%%TS  SD  20131112  185236  CB          REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPDOC('EDI43R20' 'EDI43R' 'SY400LO-
     A                                      /EDI')
     A                                      HLPARA(01 001 27 132)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 55'Detailed Interchange log'
     A                                      COLOR(WHT)
     A                                  1125'EDI43D B'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 67SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Interchange ID:'
     A                                      COLOR(WHT)
     A            SSSN      R        O  3 18REFFLD(SSSSN)
     A                                  3 28'Dato:'
     A                                      COLOR(WHT)
     A            WDT            8Y 0O  3 34EDTWRD('    .  .  ')
     A                                  3 47'Tid:'
     A                                      COLOR(WHT)
     A            WTM            6Y 0O  3 52EDTWRD('  :  :  ')
     A                                  6  2'Log_Text'
     A                                      COLOR(WHT)
     A*
     A          R EDI43DD
     A*%%TS  SD  20131112  185032  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                 26  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 26MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI43DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
