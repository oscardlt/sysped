********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde/leverandør')
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD005'
     A                                      COLOR(BLU)
     A                                  1 20'SPØRRING PÅ SAD-IMPORT (HISTORIKK)'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  4 13MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0B  4 26TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 32TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  7 20MSGCON(035 DIV1901 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9 20MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  9 35TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  41
     AO 98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 20MSGCON(014 DIV1903 *LIBL/SYMSGF)
     A            WSTDN          7Y 0B 10 35TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A  42                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20MSGCON(014 DIV1904 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B 11 35TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  43
     AO 97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDTS          6Y 0B 11 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20MSGCON(014 DIV1905 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 12 35TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  44
     AO 96                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDTK          6Y 0B 12 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13 20MSGCON(014 DIV1906 *LIBL/SYMSGF)
     A            WSSG           3A  B 13 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  45                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDTT          6Y 0B 13 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 20MSGCON(014 DIV1907 *LIBL/SYMSGF)
     A            WSTLL         10A  B 14 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 15 20MSGCON(014 DIV1908 *LIBL/SYMSGF)
     A            WSGN          11A  B 15 40TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A  47                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A            WSAAR          4A  B 15 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 23 20MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV1911 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '5')
     A            SIAVD          4Y 0O  5  4TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A            SITDN          7Y 0O  5  9TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  42                                  COLOR(TRQ)
     A            SIKNK          8Y 0O  5 17TEXT('Kundenr Kjøper')
     A                                      EDTCDE(4)
     A  44                                  COLOR(TRQ)
     A            WSDT2          6S 0O  5 26TEXT('Registreringsdato')
     A  41                                  COLOR(TRQ)
     A            SITLL         10A  O  5 33TEXT('Tollnr Løpenr')
     A  46                                  COLOR(TRQ)
     A            SITLE          6A  O  5 42TEXT('Tollnr Ekspsted')
     A            SIVKB          9Y 0O  5 52TEXT('Bruttovekt')
     A                                      EDTCDE(4)
     A            SISG           3A  O  5 62TEXT('Signatur')
     A  45                                  COLOR(TRQ)
     A            SIGN          15A  O  5 66TEXT('Godsnummer')
     A  47                                  COLOR(TRQ)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD005'
     A                                      COLOR(BLU)
     A                                  1 20'SPØRRING PÅ SAD-IMPORT (HISTORIKK)'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(045 DIV1913 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1'Op Avd. Tdeknr. Mottaker Dato.. Lø-
     A                                      penr.  eksp.enh. Vekt..... Tar Gods-
     A                                      nr..'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2MSGCON(18 DIV1914 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF01(01 'BYTT AVDELNGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SAD005'
     A                                      COLOR(BLU)
     A                                  1 20'SPØRRING PÅ SAD-IMPORT +
     A                                      (HISTORIKK)'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2MSGCON(12 DIV1705 SYMSGF)
     A            WSAAVD         4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(45 DIV1913 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1'Op Avd. Tdeknr. Mottaker Dato.. Lø-
     A                                      penr.  eksp.enh. Vekt..... Tar Gods-
     A                                      nr..'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV1914 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
