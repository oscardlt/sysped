      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR07A MODULE(*LIBL/TJETUR07A *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WSDOKN          S            512A
     D JSONSTRING      S          32000
     D ERROR           S            150
     D TMPDIR          S            128a
     D VALIDS          S              1a

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * Teste input og utfør
     C                   IF        ERROR=*BLANKS
     C                   EXSR      TEST
     C                   ENDIF

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONSTRING='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬wsdokn¬ : ¬'+%trim(WSDOKN)+'¬, '
        +'¬valids¬ : ¬'+%trim(VALIDS)+'¬, '
        +'¬tmpdir¬ : ¬'+%trim(TMPDIR)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONSTRING
     C                   CALLP     updHTMLvar('JSONSTRING':JSONSTRING)
     C                   CALLP     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   EVAL      JSONSTRING ='"chksuffix" : ['
     C                   CALLP     updHTMLvar('JSONSTRING':JSONSTRING)
     C                   CALLP     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     C                   EVAL      JSONSTRING =']'
     C                   CALLP     updHTMLvar('JSONSTRING':JSONSTRING)
     C                   CALLP     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     C                   EVAL      JSONSTRING ='}'
     C                   CALLP     updHTMLvar('JSONSTRING':JSONSTRING)
     C                   CALLP     wrtsection('JSONlin')

      * Quit
     C                   EXSR      Exit
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     BRPARF

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSDOKN = zhbgetvar('WSDOKN')
     C                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   CALL      BILIBL
     C                   end

     C     BRPARKEY      KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
     C                   EVAL      PABRID = '*KUNDFT*' + %TRIM(BIFIRM)
     C                   MOVE      'OLTMP'       PAID

     C   50              EVAL      ERROR = 'Invalid userID'
     C  N50              OPEN      BRPARF
     C                   IF        *IN50=*OFF
     C     BrParKey      CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      TMPDIR = '/pdf/tmp/'
     C                   ELSE
     C                   EVAL      TMPDIR = PAVRDA
     C                   ENDIF
     C                   ENDIF

     C                   ENDSR
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Sjekk file extention for gyldighet
     C                   CALL      'FUPLOEXT'
     C                   PARM                    WSDOKN
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    ValidExt        512
     C                   PARM                    UFlagg            1
     C                   EVAL      VALIDS='Y'
     C     Uflagg        IFEQ      '1'
     C                   EVAL      ERROR='Upload of file ' +
     C                             %trim(WSDOKN) +
     C                             ' failed: ' +
     C                             'file extention not valid.'
     C                   EVAL      VALIDS='N'
     C                   ENDIF
     C                   ENDSR
