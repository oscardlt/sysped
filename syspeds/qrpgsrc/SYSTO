      *********************************************************************
      * Spørring på (generering av) STATISTIKK
      *********************************************************************
      * PROGRAM BEHANDLER INPUT FRA SYMN1
      *
CRTPG+*  CRTPGM SYSPED/SYSTo MODULE(*LIBL/SYSTo *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 avvikende kundenr dersom User er intern
********************************************************************
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FBRSTP     IF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D i               s              1  0
     D                 DS
     D  DSNAVN                 1     20    inz('Postnr ')
     D  DSPNRF                 8     11    inz('0000')
     D  DSSTREK               12     12    inz('-')
     D  DSPNRT                13     16    inz('0999')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * Blank passord=anonym, OK, Ellers må være "Inne"
     C     BIPO          IFNE      *BLANKS
     C*                  IF        ((BIBRID = *BLANKS) OR
     C                   IF        ((*IN50 = *ON) OR
     C                              (BIINN = *BLANKS))
     C                   exsr      SetSkl01B
     C                   GOTO      SLUTT
     C                   END
     C                   END
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
N01  C                   IF        (XINTERN = 'J')
N01  C                   callp     wrtsection('AvvKnr')
N01  C                   END
N01  C                   callp     wrtsection('Del2')
     c                   move      *blanks       gmmaid           10
     c     rikunr        setll     brstp
     c     rikunr        reade     brstp                                  33
     c     *in33         doweq     *OFF
     c     Rimaid        ifne      gmMaid
     c     Rimaid        andne     'STANDARD'
     C                   callp     updHTMLvar('MAID':RIMAID)
     C                   callp     wrtsection('row')
     c                   end
     c                   move      Rimaid        gmmaid           10
     c     rikunr        reade     brstp                                  33
     c                   end
     C                   callp     wrtsection('bot')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYMNB'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     updHTMLvar('FEILTXT':'.')
     C                   callp     wrtsection('all')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
N01  C                   callp     updHtmlVar('AVVKNR':' ')
     C                   callp     updHTMLvar('FEILTXT':Spaces)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYSTO'
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      BRSTP
      * Key for POSTNROMRÅDE
     C     OmrKey        KLIST
     C                   KFLD                    RIKUNR
     C                   KFLD                    RIMAID
     C                   MOVE      BIKUNR        RIKUNR
     C                   MOVEL     'STANDARD'    RIMAID
      * Sjekk om Område MAL-ID "STANDARD" finnes - hvis ikke lag.
     C     OmrKey        CHAIN     BRSTP                              33
     C     *IN33         IFEQ      *ON
     C                   eval      RIPNRF    = 0000
     C                   eval      RIPNRT    = 0999
     C                   eval      RISONR    = 0
     C                   eval      RISONA    = DSNAVN
     C                   write     BRSTPR
     C                   eval      i = 1
     C     1             DO        9             i
     C                   movel     i             RIPNRF
     C                   movel     i             RIPNRT
     C                   eval      RISONR    = i
     C                   movel     i             DSPNRF
     C                   movel     i             DSPNRT
     C                   eval      RISONA    = DSNAVN
     C                   write     BRSTPR
     c     i             cabeq     9             utstd
     C                   ENDDO
     C                   END
     c     utstd         tag
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     BRSTP
     C                   eval      *inlr = *on
      *
     C                   endsr
