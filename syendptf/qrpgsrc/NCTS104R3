********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12326 * 01 YBC PTF12 Diverse feilrettinger
********************************************************************
      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE29                    ***
      *** Release for transit                                       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH05  UF   E           K DISK
     FNCTSERR   IF A E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM2     IF   E           K DISK    RENAME(EDIMR:EDIMR2)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104r3
     d ncts104r3       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104r3       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d x4_xrlev1       s                   like(xrlev1)
     d x4_xrlev2       s                   like(xrlev2)
     d x4_xrlev3       s                   like(xrlev3)
     d x4_xrlev4       s                   like(xrlev4)
     d x4_xrlev5       s                   like(xrlev5)
     d x_thtrdt        s              8
     d x_thtrnr        s                   like(thtrnr)
     d x_thddt         s              8
     d x_thddtk        s              8
     d x_thdk          s                   like(thdk)
     d x_thdkav        s                   like(thdkav)
     d x_thdkr         s                   like(thdkr)
     d x_thdktx        s                   like(thdktx)
     d x_thcats        s                   like(thcats)
     d x_thomd         s                   like(thomd)
     d x_thcnr         s                   like(thcnr)
     d x_thdant        s                   like(thdant)
     d x_thdfkd        s                   like(thdfkd)
     d x_thginr        s                   like(thginr)
     d x_IE29          s              1a
     d wdt             s              8
     d wtm             s              6
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR2
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC029C';
             x_ie29 = 'J';
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'Authorisation';
           when xrnv = 'CustomsOfficeOfDeparture';
             exsr srxml2;
           when xrnv = 'CustomsOfficeOfDestination';
           when xrnv = 'CustomsOfficeOfTransit';
           when xrnv = 'CustomsOfficeOfExitForTransit';
           when xrnv = 'HolderOfTheTransitProcedure';
           when xrnv = 'Represebtative';
           when xrnv = 'ControlResult';
             exsr srxml3;
           when xrnv = 'Guarantee';
           when xrnv = 'Consignment';
             exsr srxml4;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie29 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'LRN';
             x0068 = xrverd;
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
           when xrnv = 'declarationType';
             x_thdk   = xrverd;
           when xrnv = 'additionalDeclarationType';
           when xrnv = 'TIRCarnetNumber';
           when xrnv = 'declarationAcceptanceDate';
             x_thtrdt = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
           when xrnv = 'TIRCarnetNumber';
           when xrnv = 'releaseDate';
             //x_thddt  = %subst(xrverd:1:4) + %subst(xrverd:6:2)
             //      + %subst(xrverd:9:2);
           when xrnv = 'security';
           when xrnv = 'reducedDatasetIndicator';
           when xrnv = 'specificCircumstanceIndicator';
           when xrnv = 'communicationLanguageAtDeparture';
           when xrnv = 'bindingItinerary';
             x_thomd = %dec(xrverd:1:0);
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_thcats = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'code';
             x_thdkr = xrverd;
           when xrnv = 'date';
             x_thddtk = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
S01    //  when xrnv = 'controlBy';
N01        when xrnv = 'controlledBy';
             x_thdkav = xrverd;
           when xrnv = 'text';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             x_thdktx = xrvrd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'countryOfDispatch';
           when xrnv = 'countryOfDestination';
           when xrnv = 'containerIndicator';
           when xrnv = 'inlandModeOfTransport';
           when xrnv = 'modeOfTransportAtTheBorder';
           when xrnv = 'grossMass';
           when xrnv = 'referenceNumberUCR';
           when xrnv = 'Carrier';
           when xrnv = 'Consignor';
           when xrnv = 'Consignee';
           when xrnv = 'AdditionalSupplyChainActor';
           when xrnv = 'TransportEquipment';
             exsr srxml401;
           when xrnv = 'LocationOfGoods';
           when xrnv = 'DepartureTransportMeans';
           when xrnv = 'CountryOfRoutingOfConsignment';
           when xrnv = 'ActiveBorderTransportMeans';
           when xrnv = 'PlaceOfLoading';
           when xrnv = 'PlaceOfUnloading';
           when xrnv = 'PreviousDocument';
           when xrnv = 'SupportingDocument';
           when xrnv = 'TransportDocument';
           when xrnv = 'AdditionalReference';
           when xrnv = 'AdditionalInformation';
           when xrnv = 'TransportCharges';
           when xrnv = 'HouseConsignment';
             exsr srxml410;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml401;
       //**********************************************

         x3_xrlev1 = xrlev1;
         x3_xrlev2 = xrlev2;
         x3_xrlev3 = 1;
         x3_xrlev4 = 0;

         chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'sequenceNumber';
           when xrnv = 'countainerIdentificationNumber';
             x_thcnr = xrverd;
           when xrnv = 'numberOfSeals';
             x_thdant = %dec(xrverd:4:0);
           when xrnv = 'Seal';
             exsr srxml4011;
           when xrnv = 'goodsReference';
             exsr srxml4012;
           endsl;

           x3_xrlev3 = x3_xrlev3 + 1;
           chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4011;
       //**********************************************

         x4_xrlev1 = xrlev1;
         x4_xrlev2 = xrlev2;
         x4_xrlev3 = xrlev3;
         x4_xrlev4 = 1;
         x4_xrlev5 = 0;

         chain (xrsn:x4_xrlev1:x4_xrlev2:x4_xrlev3:x4_xrlev4
               :x4_xrlev5) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'sequenceNumber';
           when xrnv = 'Identifier';
             x_thdfkd = xrverd;
           endsl;

           x4_xrlev4 = x4_xrlev4 + 1;
           chain (xrsn:x4_xrlev1:x4_xrlev2:x4_xrlev3:x4_xrlev4
                 :x4_xrlev5) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4012;
       //**********************************************

         x4_xrlev1 = xrlev1;
         x4_xrlev2 = xrlev2;
         x4_xrlev3 = xrlev3;
         x4_xrlev4 = 1;
         x4_xrlev5 = 0;

         chain (xrsn:x4_xrlev1:x4_xrlev2:x4_xrlev3:x4_xrlev4
               :x4_xrlev5) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'sequenceNumber';
           when xrnv = 'declarationGoodsItemNumber';
             x_thginr = %dec(xrverd:5:0);
           endsl;

           x4_xrlev4 = x4_xrlev4 + 1;
           chain (xrsn:x4_xrlev1:x4_xrlev2:x4_xrlev3:x4_xrlev4
                 :x4_xrlev5) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml410;
       //**********************************************

         x3_xrlev1 = xrlev1;
         x3_xrlev2 = xrlev2;
         x3_xrlev3 = 1;
         x3_xrlev4 = 0;

         chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'sequenceNumber';
           when xrnv = 'countryOfDispatch';
           when xrnv = 'grossMass';
           when xrnv = 'referenceNumberUCR';
           when xrnv = 'securityIndicatorFromExportDeclaration';
           when xrnv = 'Consignor';
           when xrnv = 'Consignee';
           when xrnv = 'AdditionalSupplyChainActor';
           when xrnv = 'DepartureTransportMeans';
           when xrnv = 'PreviousDocument';
           when xrnv = 'SupportingDocument';
           when xrnv = 'TransportDocument';
           when xrnv = 'AdditionalReference';
           when xrnv = 'AdditionalInformation';
           when xrnv = 'TransportCharges';
           when xrnv = 'ConsignmentItem';
           endsl;

           x3_xrlev3 = x3_xrlev3 + 1;
           chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         chain x_thtrnr nctseh05;
         if %found;
           thst   = 'K';
           thst029= 'J';
           thpkl  = 'J';
           thws   = 'TVINR';
           //thddt  = %dec(x_thddt:8:0);
           thddtk = %dec(x_thddtk:8:0);
           if thddtk <> 0;
             thddt  = thddtk;
           endif;
           thdkav = x_thdkav;
           thdktx = x_thdktx;
           thcats = x_thcats;
           thomd  = x_thomd;
           thcnr  = x_thcnr;
           thdant = x_thdant;
           thdfkd = x_thdfkd;
           thginr = x_thginr;
N01        thdkr  = x_thdkr;
           update nctsehr;
         else;
           temf = *blanks;
           setll (' ': x_thtrnr) nctserr;
           reade (' ': x_thtrnr) nctserr;
           dow not %eof;
             if temf = '029';
               leave;
             endif;
             reade (' ': x_thtrnr) nctserr;
           enddo;
           if temf <> '029';
             testat = *blanks;
             temf   = '029';
             tetrnr = x_thtrnr;
             tesn   = xrsn;
             write nctserrr;
           endif;
           *inlr = *on;
           return;
         endif;

         setll (thavd: thtdn: 99999999) edim2;
         readpe (thavd: thtdn) edim2;

         chain 1 edic;
         cmn = cmn + 1;
         update edicr;
         mmn = cmn;
         m0065  = 'XMLDEC';
         m0068  = x0068;
         m1001  = 'TET';
         m1225  = '029';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '029';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = 'T29';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'Release customs NCTS '
                  + %trim(%editc(thddtk:'Z'))+ ' '  + %trim(thtrnr);
         tttexl = 'Frigi toll.no NCTS'
                  + %trim(%editc(thddtk:'Z'))+ ' '  + %trim(thtrnr);
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
