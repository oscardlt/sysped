      *********************************************************************
      * Endre total og valutakode
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS159 MODULE(*LIBL/SS159
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSORF    UF   E           k DISK    USROPN
     FGSSLGF    O    E             DISK    USROPN
     FVALUL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSAMT           S             12
     D XXAMT           S             11  2
     D WSVAL           S              3
     D WSONO           S              7
     D XXONO           S              7  0
     D LAGREAMT        S              1
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL01          C                   CONST('Amount can not be 0.')
     D FEIL02          C                   CONST('Currency code is not valid.')
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D XXTXT01         C                   CONST('Currency/amount is changed.')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * hvis en har tastet submit fra vedlikehodsbildet, test/lagre
     c     LAGREAMT      IFEQ      'Y'
     c                   exsr      tstfeil
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
      * Test TOTAL
     C     XXAMT         IFEQ      0
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      * Test VALUTA
     C     VALUL01K      CHAIN     VALUL01                            33
     C     *IN33         IFEQ      '1'
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * ALT OK - Oppdater  - Deretter avslutt
     C                   MOVE      'Y'           AVSLUTT
      * Hvis endring..
     c     XXONO         chain     GSSORF                             55
     c     *in55         IFeq      *OFF
     C                   EXSR      OPDLOGG
     c                   MOVE      WSVAL         ORCURC
     c                   MOVE      XXAMT         ORAMT
     c                   update    gssorfr
     c                   end
      *
     C     UtTest        endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG       begsr
      *
     C                   IF        ((ORCURC <> WSVAL) OR (ORAMT <> XXAMT))
     C                   MOVE      WSUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'SS159'       LGPGM
     C                   Z-ADD     5             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      XXONO         LGONO
     C                   MOVE      *ZEROS        LGLNO
     C                   EVAL      LGTXT = XXTXT01
     C                   EVAL      LGANY = 'New currency code:'
     C                   CAT       WSVAL:1       LGANY
     C                   EVAL      LGAGM = 'Old currency code:'
     C                   CAT       ORCURC:1      LGAGM
     C                   EVAL      LGNNY = XXAMT
     C                   EVAL      LGNGM = ORAMT
     C                   WRITE     GSSLGFR
     C                   END
      *
     C                   endsr
      *************************************
     C     GETORD        BEGSR
      *************************************
     c     XXONO         chain(N)  GSSORF                             55
      *
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      LAGREAMT= zhbgetvar('LAGREAMT')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSAMT   = zhbgetvar('WSAMT')
     C                   eval      XXAMT   = c2n2(WSAMT)
     C                   eval      WSVAL   = zhbgetvar('WSVAL')
     C                   eval      WSONO   = zhbgetvar('ORONO')
     C                   eval      XXONO   = c2n2(WSONO)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(XXONO:'Z')))
     C                   IF        LAGREAMT= 'Y'
     C                   callp     updhtmlvar('WSVAL':WSVAL)
     C                   callp     updhtmlvar('WSAMT':
     C                             %trim(%editc(XXAMT:'3')))
     C                   ELSE
     C                   callp     updhtmlvar('WSVAL':ORCURC)
     C                   callp     updhtmlvar('WSAMT':
     C                             %trim(%editc(ORAMT:'3')))
     C                   ENDIF
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS159'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSORF
     C                   OPEN      GSSLGF
     C                   OPEN      VALUL01
      *
     C     VALUL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    WSVAL
      *
      *
      * Ved første kall - hent verdier fra fil.
     c     XXONO         IFNE      *zeros
     c     LAGREAMT      ANDNE     'Y'
     C                   EXSR      GETORD
     c                   END
     c                   move      *blanks       feilmelding
     c                   move      *blanks       fmeldnr
     C                   MOVE      'N'           AVSLUTT           1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSLGF
     C                   close     VALUL01
     C                   eval      *inlr = *on
      *
     C                   endsr
