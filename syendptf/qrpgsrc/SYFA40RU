      *********************************************************************
      * MERKE RUCKSATTEST
      *********************************************************************
CRTPG+*  CRTPGM SYSPED/SYFA40RU MODULE(*LIBL/SYFA40RU
CRTPGM*        *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF12  oppdater også ruckp
10XXX * 02 JOV PTF12  parameter &unik må stemme med opptraget (Trackf/RAS - TTDEPO )
10XXX * 03 JOV PTF12  dersom post finnes i SADH (mot formodning) - oppdater også der
      * 04 JOV PTF12  Sjekk mot unik kode flyttet UNDER sjekk av oppdraget/Chain headf
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     UF   E           K DISK    USROPN
N03  FSADH      UF   E           K DISK    USROPN
N01  FRUCKP     UF   E           K DISK    USROPN
     FTRACKl02  UF A E           K DISK    USROPN
E03  FFRISOK    UF A E           K DISK    USROPN
     FKODFRI    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10

     D WSUSER          S             10
     D MailAdr         S             70
     D MerkTxt         S            170
nxx  D feilTxt         S             70
     D AVD             S              4
     D OPD             S              7
N02  D UNIK            S             10
     D Funk            S              1
nxx  D WSGNN           S             15
nxx  D WSTLE           S              6
nxx  D WSTLL           S             10
nxx  D EXTREF          S             35
     D EKSPENH         S            100
     D LOPENR          S            100
     D NYTTGNR         S            100
     D OKBUTTON        S            100
     D TTTEX2          S             60
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  WDsÅMD                 1      8  0
     D  WDsÅMDÅ                1      4  0
     D  WDsÅMDM                5      6  0
     D  WDsÅMDD                7      8  0
     D                 DS
     D  WDsDMÅ                 1      8  0
     D  WDsDMÅD                1      2  0
     D  WDsDMÅM                3      4  0
     D  WDsDMÅÅ                5      8  0
nxx  D                 DS
nxx  D  WSETD                  1      6
nxx  D   WSETDD                1      2
nxx  D   WSETDM                3      4
nxx  D   WSETDÅ                5      6
nxx  D   WSETD1_1              1      1
nxx  D   WSETD2_5              2      5
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
      * Oppdater/vis bekrefting
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SYFA40RU'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   EXSR      HOVED
     C                   callp     wrtsection('all')
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *********************************************************************
     C     Hoved         begsr
      *********************************************************************
nxx  C                   eval      feilTxt = *blanks
     C                   eval      merkTxt = *blanks
      *
     C     HEADFK        chain     HEADF                              33
     c                   if        not %found
     C                   EVAL      feilTxt = 'Angitt oppdrag finnes ikke!!'
     c                   goto      utMerk
     c                   endif
N04   *
N04   * &unik må samsvare med oppdraget (Trackf/RAS(første) / TTDEPO )
N04   *       (sjekk av unik kode flyttet ETTER CHAIN HEADF )
N04  c                   MOVE      'RAS'         TTACTI
N04  c     TT02KEY       chain     TRACKL02
N04  c                   if        UNIK = *blanks or
N04  c                             UNIK <> TTDEPO
N04  c                   eval      MerkTxt = 'Benyttet URL er ikke korrekt.'
N04  c                   eval      feilTxt = 'Unik kode samsvarer ikke med '
N04  c                                     + 'oppdraget. '
N04  c                   eval      HETLL = '.'                                  Ungå inp-felt
N04  c                   goto      utMerk
N04  c                   endif
N04   *
     c                   eval      FSKODE = '*CR'
E03  c     FriKey        chain(n)  FRISOK
     c                   if        %found (FRISOK)
     c                   eval      EXTREF = FSSOK
     c                   endif
      *  ER alt løpenr/el godsnr inne
     c                   if        HETLL <> *blanks or HEGNN <> *blanks
     c                   eval      MerkTxt = 'Ruckattest er allerede '
     c                             +' lagt inn.'
      *  ER den bekreftet via WEB ??
     C                   MOVE      'RUW'         TTACTI
     C     TT02max       setgt     TRACKL02
     C     TT02KEY       readpe(N) TRACKL02                               34
     c     *in34         ifeq      '0'
     c                   movel     TTTIME        TTTIM4            4 0
     c                   eval      MerkTxt = 'Ruckattest er allerede '
     c                             +' lagt inn via web - '
     c                             +%trim(%editw(TTDATE:'    .  .  '))
     c                             + ' / ' + %trim(%editw(TTTIM4:'  :  '))
     c                   endif
     c                   else
     c                   eval      MerkTxt = 'Fyll ut verdier og trykk '
     c                             +'på OK-knappen for å avgi Ruckattest.'
     c                   endif
      *
      * Bekreft tildeling
     C     FUNK          IFEQ      'B'
nxx  c                   exsr      TstBekr
nxx  c                   if        feilTxt = *blanks
     c                   exsr      Bekreft
nxx  c                   endif
     c                   endif
      *
     c     UtMerk        tag
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('HERFA':HERFA)
N02  C                   callp     updHtmlVar('HEGN':HEGN)
N02  C                   move      *blanks       HEPOS2A           7
N02  C                   if        HEPOS2 <> *zeros
N02  C                   eval      HEPOS2A = '/ '+ %trim(%editc(HEPOS2:'Z'))
N02  C                   endif
N02  C                   callp     updHtmlVar('HEPOS1':HEPOS1)
N02  C                   callp     updHtmlVar('HEPOS2A':HEPOS2A)
     C                   callp     updhtmlvar('HEAVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updhtmlvar('HEOPD':
     C                             %trim(%editc(HEOPD:'Z')))
N02  C                   callp     updHtmlVar('UNIK':UNIK)
     C                   callp     updHtmlVar('EXTREF':EXTREF)
     C                   callp     updHtmlVar('HENAK':HENAK)
     C                   callp     updhtmlvar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHtmlVar('HEVS1':HEVS1)
     C                   callp     updhtmlvar('HEVKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHtmlVar('MerkTxt':MerkTxt)
     C                   callp     updHtmlVar('feilTxt':feilTxt)
      *
     c                   move      *blanks       EKSPENH
     c                   move      *blanks       LOPENR
     c                   move      *blanks       NYTTGNR
     c                   move      *blanks       OKBUTTON
     c                   if        HETLL = *blanks and HEGNN = *blanks
     c                   eval      OKBUTTON ='<INPUT TYPE="button" '
     c                             +'VALUE="OK" NAME="Bekr" '
     c                             +'onClick="Bekreft()">'
     c                   eval      EKSPENH ='<input type="text" name="WSTLE"'
     c                             +' size="6" maxlength="6">'
     c                   eval      LOPENR  ='<input type="text" name="WSTLL"'
     c                             +' size="10" maxlength="10">'
     c                   eval      NYTTGNR ='<input type="text" name="WSGNN"'
     c                             +' size="15" maxlength="15">'
     c                   else
     c                   eval      OKBUTTON ='<INPUT TYPE="button" '
     c                             +'VALUE="Print" NAME="PRT" '
     c                             +'onClick="self.print()">'
     c                   eval      EKSPENH  = HETLE
     c                   eval      LOPENR   = HETLL
     c                   eval      NYTTGNR  = HEGNN
     c                   end
      *
     C                   callp     updHtmlVar('EKSPENH':EKSPENH)
     C                   callp     updHtmlVar('LOPENR':LOPENR)
     C                   callp     updHtmlVar('NYTTGNR':NYTTGNR)
     C                   callp     updHtmlVar('OKBUTTON':OKBUTTON)
      *
     C                   endsr
      *
      *********************************************************************
     C     TstBekr       begsr
      *********************************************************************
     c                   IF        WSTLE =  *BLANKS AND
     c                             WSGNN =  *BLANKS
     C                   EVAL      feilTxt = 'Eksp.enhet/Løpenr ELLER nytt '
     C                                     + 'godsnr må angis.'
     c                   goto      utTst
     c                   ENDIF
      *
     c                   IF        WSTLE <> *BLANKS AND
     c                             WSTLL =  *BLANKS
     C                   EVAL      feilTxt = 'BÅDE Eksp.enhet og Løpenr '
     C                                     + 'godsnr må angis.'
     c                   goto      utTst
     c                   ENDIF
      *
     c                   IF        WSTLE =  *BLANKS AND
     c                             WSTLL <> *BLANKS
     C                   EVAL      feilTxt = 'BÅDE Eksp.enhet og Løpenr '
     C                                     + 'godsnr må angis.'
     c                   goto      utTst
     c                   ENDIF
      *
     C     UtTst         endsr
      *
      *********************************************************************
     C     Bekreft       begsr
      *********************************************************************
     c     HEADFK        chain     HEADF
     c                   if        %found
     c                   eval      HETLE = WSTLE
     c                   eval      HETLL = WSTLL
     c                   eval      HEGNN = WSGNN
     c                   update    HEADFR
      *
      *
     c                   eval      MerkTxt = 'Ruckattest er nå lagt inn - '
     c                             +' steng vinduet (e/evt print av kvittering)'
     C                   eval      TTTEX2 = 'E:'+HETLE+' L:'+HETLL
     c                             +' N.Gn:'+HEGNN
     c*
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'RUW'         TTACTI
     C                   MOVE      WSUSER        TTUSER
     C                   EVAL      TTTEXT ='Ruckattest received via web '
     c                             + TTTEX2
     C                   EVAL      TTTEXL ='Ruckattest mottatt via web '
     c                             + TTTEX2
     C                   MOVE      'S'           TTMANU
     C                   write     TRACKFR
N01   * oppdater loggen, O=Oppgjort (ville fikses  av syru02 neste natt, men greit å ta fortløpende)
N01  c     HEADFK        chain     RUCKP
N01  c                   if        %found
N01  C                   eval      RUST = 'O'
N01  C                   update    RUCKPR
N01  c                   endif
N03   * Oppdater sadh
N03  c     HEADFK        chain     SADH
N03  c                   if        %found
N03  c                   eval      SITLE = WSTLE
N03  c                   eval      SITLL = WSTLL
N03  c                   update    SADHR
N03  c                   endif
N03   * Oppdater Frisok TLE /TLØ
N03  C                   if        WSTLL <> *blanks
N03   * eksp-enh i TLE
N03  c                   eval      FSKODE = 'TLE'
n03  c                   move      *blanks       KFSODK
N03  c     FSKODE        CHAIN     KODFRI
N03  c     FriKey        chain     FRISOK
N03  c                   eval      FSAVD = HEAVD
N03  c                   eval      FSOPD = HEOPD
N03  c                   eval      FSDTOP= HEDTOP
N03  c                   eval      FSDOKK= KFSODK
N03  c                   eval      FSSOK = WSTLE
N03  c                   if        %found
N03  c                   update    FRISOKR
N03  c                   else
N03  c                   write     FRISOKR
N03  c                   endif
N03   * løpenumm i TLØ
N03  c                   eval      FSKODE = 'TLØ'
n03  c                   move      *blanks       KFSODK
N03  c     FSKODE        CHAIN     KODFRI
N03  c     FriKey        chain     FRISOK
N03  c                   eval      FSAVD = HEAVD
N03  c                   eval      FSOPD = HEOPD
N03  c                   eval      FSDTOP= HEDTOP
N03  c                   eval      FSDOKK= KFSODK
N03  c                   eval      FSSOK = WSTLL
N03  c                   if        %found
N03  c                   update    FRISOKR
N03  c                   else
N03  c                   write     FRISOKR
N03  c                   endif
N03  c                   endif
      *
     c                   endif
      *
     C                   endsr
      *
      *
      *********************************************************************
     C     Exit          begsr
      *********************************************************************
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C                   CLOSE     HEADF
N03  C                   CLOSE     SADH
N01  C                   CLOSE     RUCKP
     C                   CLOSE     TRACKL02
     C                   CLOSE     FRISOK
N03  C                   CLOSE     KODFRI
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      AVD     = zhbgetvar('AVD')
     C                   eval      HEAVD   = c2n2(AVD)
     C                   eval      OPD     = zhbgetvar('OPD')
     C                   eval      HEOPD   = c2n2(OPD)
N02  C                   eval      UNIK    = zhbgetvar('UNIK')
     C                   eval      FUNK    = zhbgetvar('FUNK')
     C                   eval      WSTLE   = zhbgetvar('WSTLE')
     C                   eval      WSTLL   = zhbgetvar('WSTLL')
     C                   eval      WSGNN   = zhbgetvar('WSGNN')

     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      HEADF
N03  C                   open      SADH
N01  C                   open      RUCKP
     C                   open      TRACKL02
     C                   open      FRISOK
N03  C                   open      KODFRI
      *
     c     HEADFK        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c     FRIKEY        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c                   kfld                    FSKODE
     C     TT02MAX       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   KFLD                    Maxdate
     C                   move      99999999      Maxdate           8 0
     C     TT02KEY       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
      *
     C                   endsr
      *
