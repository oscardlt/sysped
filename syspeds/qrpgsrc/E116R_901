      * Dette programmet startes fra CLP: E116C_901 som i sin tur startes fra
      * SYEDI Standard gjenom tabellen Kunders standardmeldinger.
      *
     FEUNB      IF   E           K DISK
     FEUNH      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FEDTM901   IF   E           K DISK
     FELIN901   IF   E           K DISK
     FENAD901   IF   E           K DISK
     FHORDL09   IF   E           K DISK    PREFIX(H9_)
     FVADRL01   IF A E           K DISK    PREFIX(V1_)
     FOTRAF     O  A E             DISK    PREFIX(O0_)
     FOTRAL01   IF   E           K DISK    PREFIX(O1_)
     F                                     RENAME(OTRAR:OTRAR01)
     FKUNKO2    IF A E           K DISK    PREFIX(K2_)
     D TAB1            S              1    DIM(29) CTDATA PERRCD(1)
     D TAB2            S              1    DIM(29) ALT(TAB1)
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D C               S              1    DIM(8)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  O0_LEVDATO             1      8  0
     D  O0_LEVAAR              1      4  0
     D  O0_LEVMND              5      6  0
     D  O0_LEVDAG              7      8  0
     D                 DS
     D  VNR                    1     14    DIM(14)
     D  VNR14A                 1     14
     D                 DS
     D  SONV                   1     10    DIM(10)
     D  SONV10                 1     10
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     OTRAL01K      KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    O0_KUNDNR
     C     HORDL09K      KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    H9_OTBKOD
     C                   KFLD                    H9_ORDAAR
     C                   KFLD                    O0_KUNREF
     C     VADR1K        KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    O0_KUNDNR
     C                   KFLD                    XX_VADRNR
     C     VADR1K2       KLIST
     C                   KFLD                    XX_FIRMA
     C                   KFLD                    O0_KUNDNR
     C     KUNKO2K       KLIST
     C                   KFLD                    O0_KUNDNR
     C                   KFLD                    K2_KUINTN
     C     KEYMS0        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    ZGN0

     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
     C                   MOVE      'SY'          XX_FIRMA          2
     C                   MOVE      'O'           H9_OTBKOD
     C                   Z-ADD     999           XX_VADRNR         3 0

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""

     C     ZMN           CHAIN     EDIM                               50
     C                   IF        *IN50 = *OFF                                 1<-B
     C                   CLEAR                   OTRAR

      * HENT KUNDES ORDRENR
     C     ZMN           CHAIN     EUNH                               51
     C                   MOVEL     U1004         O0_KUNREF
      * Get interchange values
     C     UIN           CHAIN     EUNB                               51

      * HENT KUNDENR
     C                   Z-ADD     1             ZGN0
     C     KEYMS0        SETLL     ENAD901
     C     KEYMS0        READE     ENAD901                                51
     C                   DOW       *IN51 = *OFF                                  2<-B
     C                   IF        N3035 = 'BY'                                   3<-B
     C                   MOVEA     N3039         C
     C     1             DO        8             XN                3 0             4<-B
     C                   IF        C(XN) <> ' '                                     5<-B
     C                   MULT      10            O0_KUNDNR
     C                   MOVE      C(XN)         O0_KUNDNR
     C                   ELSE
     C                   GOTO      KUNDEOK
     C                   ENDIF                                                      5<-E
     C                   ENDDO                                                     4<-E
     C                   ENDIF                                                    3<-E
     C     KEYMS0        READE     ENAD901                                51
     C  N51              ENDDO                                                   2<-E
     C     *IN51         CABEQ     '1'           SLUTT

     C     KUNDEOK       TAG
      * TEST OM BESTILLING FINNES I TRANSIT-FIL FRA FØR.
     C     OTRAL01K      SETLL     OTRAL01
     C     OTRAL01K      READE     OTRAL01                                51
     C                   DOW       *IN51 = *OFF                                  2<-B
     C     O1_KUNREF     CABEQ     O0_KUNREF     SLUTT
     C     OTRAL01K      READE     OTRAL01                                51
     C  N51              ENDDO                                                   2<-E

      * TEST OM BESTILLING FINNES I ORDRE-FIL FRA FØR.
     C*                  MOVEL     U2001         XNR2              2 0
     C                   MOVEL     U2782A        XNR2              2 0
     C                   EVAL      H9_ORDAAR = 2000 + XNR2
     C     HORDL09K      SETLL     HORDL09
     C     HORDL09K      READE     HORDL09                                51
     C     *IN51         CABEQ     '0'           SLUTT

      * HENT LEVERINGSADRESSE
     C     KEYMS0        SETLL     ENAD901
     C     KEYMS0        READE     ENAD901                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
     C                   IF        N3035 = 'DP'                                   3<-B
     C                   MOVEL     N3039         K2_KUINTN
     C     KUNKO2K       CHAIN     KUNKO2                             33
     C                   IF        *IN33                                           4<-B
      * LEVERINGSADRESSE FINNES IKKE FRA FØR
      * LAG NY LEVERINGSADRESSE
     C     VADR1K        SETLL     VADRL01
     C     VADR1K2       READPE    VADRL01                                34
     C                   IF        *IN34                                            5<-B
     C                   MOVE      'A'           V1_AKTKOD
     C                   MOVE      XX_FIRMA      V1_FIRMA
     C                   MOVE      O0_KUNDNR     V1_KUNDNR
     C                   Z-ADD     1             V1_VADRNR
     C                   ELSE                                                       5
     C                   ADD       1             V1_VADRNR
     C                   ENDIF                                                      5<-E
     C                   MOVEL     N3124B        V1_VADRNA
     C                   MOVEL     N3124A        V1_VADRN1
     C                   MOVEL     N3124C        V1_VADRN2
     C                   MOVEL     N3124D        V1_VADRN3
     C                   MOVEL     N3124E        V1_POSTVA
     C                   MOVEL     N3124B        SONV10
     C     1             DO        10            XN
     C     SONV(XN)      LOOKUP    TAB1          TAB2                     33
     C   33              MOVE      TAB2          SONV(XN)
     C                   ENDDO
     C                   MOVEL     SONV10        V1_SONAVN
     C                   WRITE     VADRR
      * LAG KRYSSEREF.
     C                   MOVE      O0_KUNDNR     K2_KUKUN1
     C                   MOVE      O0_KUNDNR     K2_KUKUN2
     C                   MOVE      V1_SONAVN     K2_KUALFA
     C                   Z-ADD     V1_VADRNR     K2_KUVADR
     C                   WRITE     KUNKOR
     C                   ENDIF                                                     4<-E
      *
     C                   LEAVE
     C                   ENDIF                                                    3<-E

     C     KEYMS0        READE     ENAD901                                51
     C  N51              ENDDO                                                   2<-E
     C                   IF        *IN51                                         2<-B
     C                   MOVE      *ZEROS        O0_VADRNR
     C                   ELSE
     C                   MOVE      K2_KUVADR     O0_VADRNR
     C                   ENDIF                                                   2<-E

      * HENT LEVERINGSDATO
     C                   MOVE      *ZEROS        O0_LEVDATO
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM901
     C     KEYMS0        READE     EDTM901                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
     C                   IF        D2005 = '002'                                  3<-B
     C                   MOVEL     D2001         XNR6              6 0
     C                   EVAL      O0_LEVDATO = 20000000 + XNR6
     C                   LEAVE
     C                   ENDIF                                                    3<-E
     C     KEYMS0        READE     EDTM901                                51
     C  N51              ENDDO                                                   2<-E

     C                   MOVE      '1'           O0_TRANS
     C                   MOVE      XX_FIRMA      O0_FIRMA
     C                   MOVE      'O'           O0_ORDTYP
     C                   MOVE      'TT'          O0_STYPE

      * HENT VARENR OG ANTALL KOLLI
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ELIN901
     C     KEYMS0        READE     ELIN901                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
     C                   MOVEL     L7020A        VNR14A
     C                   MOVE      *BLANKS       XVNR             15 0
     C     1             DO        14            XN                3 0            3<-B
     C                   SELECT                                                    4<-B
     C     VNR(XN)       WHENGE    '0'
     C     VNR(XN)       ANDLE     '9'
     C                   EVAL      XVNR = XVNR * 10
     C                   MOVE      VNR(XN)       XVNR
     C     VNR(XN)       WHENNE    ' '
      * ALFANUMMERISK
     C                   MOVEL     L7020A        O0_VARENR
     C                   GOTO      SLVARENR
     C                   OTHER
     C                   LEAVE
     C                   ENDSL                                                     4<-E
     C                   ENDDO                                                    3<-E
     C                   MOVE      XVNR          VNR14A
     C     1             DO        14            XN                               3<-B
     C     VNR(XN)       IFEQ      '0'                                             4<-B
     C                   MOVE      ' '           VNR(XN)
     C                   ELSE                                                       5
     C                   LEAVE
     C                   ENDIF                                                      5<-E
     C                   ENDDO                                                     4<-E
      * NUMMERISK
     C                   MOVE      VNR14A        O0_VARENR
     C     SLVARENR      TAG
     C                   MOVEA     L6060         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          O0_BESTIL
     C                   MOVEA     L5110A        A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          O0_PRIS
     C                   MOVEA     L5116         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          O0_BELOP
     C                   WRITE     OTRAR
     C     KEYMS0        READE     ELIN901                                51
     C  N51              ENDDO                                                   2<-E

     C                   ENDIF                                                  1<-E

     C     SLUTT         ENDSR
** KONVERTERINGSTABELL
aA
bB
cC
dD
eE
fF
gG
hH
iI
jJ
kK
lL
mM
nN
oO
pP
qQ
rR
sS
tT
uU
vV
wW
xX
yY
zZ
æÆ
øØ
åÅ
