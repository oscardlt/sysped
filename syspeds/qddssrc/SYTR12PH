*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
      *_____________________________________________
      * Format for hovedopplysninger
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R KJØP
     A                                  2  2MSGCON(70 JOV0003 SYMSGF)
     A                                      HIGHLIGHT
     A                                  3  2MSGCON(70 JOV0711 SYMSGF)
     A                                      HIGHLIGHT
     A                                  3 98'SIDE'
     A                                      HIGHLIGHT
     A            XSIDE          2  0   3106EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                  4  2MSGCON(70 JOV0003 SYMSGF)
     A                                      HIGHLIGHT
     A                                  4 78MSGCON(15 JOV0174 SYMSGF)
     A            UUBNRN         7S 0   4102EDTCDE(Z)
     A            YNAVN     R           5 11REFFLD(KNAVN CUNDF)
     A                                      HIGHLIGHT
     A            YADR1     R           6 11REFFLD(ADR1 CUNDF)
     A                                      HIGHLIGHT
     A            YADR2     R           7 11REFFLD(ADR2 CUNDF)
     A                                      HIGHLIGHT
     A            YADR3         35      8 11
     A                                      HIGHLIGHT
     A            KLKNVN    R           9 11REFFLD(KLKNVN KODTLK)
     A                                      HIGHLIGHT
     A                                  5 78MSGCON(15 DIV1668 SYMSGF)
     A            YKUNDN    R           5100REFFLD(KUNDNR CUNDF)
     A                                      EDTCDE(Z)
     A                                  6 78MSGCON(15 DIV2254 SYMSGF)
     A            THTNR          8S 0   6100EDTCDE(Z)
     A                                  7 78MSGCON(15 JOV0005 SYMSGF)
     A            IDAGX          6S 0   7100EDTCDE(Y)
     A                                  8 78MSGCON(15 JOV0007 SYMSGF)
     A            W1PERM         2  0   8103
     A                                  8105'/'
     A            W1PERÅ         2  0   8106
S01  A*                                10  3MSGCON(119 JOV0004 SYMSGF)
N01  A                                 10  3MSGCON(119 YBC2000 SYMSGF)
     A                                      HIGHLIGHT
     A                                 10  2'________________________'
     A                                 10 26'________________________'
     A                                 10 50'________________________'
     A                                 10 74'________________________'
     A                                 10 98'______________________'
      *_______________________________
      * Format for faktura-varelinjer.
      *"""""""""""""""""""""""""""""""
     A          R DET                       SPACEB(2)
     A            TURUND         8  0      2EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUAVD          4  0     11EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUPRO          8  0     16EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUDATO         6  0     25EDTWRD('  .  .  ')
     A                                      HIGHLIGHT
     A            TUSDF         20        34
     A                                      HIGHLIGHT
     A            TUSDT         20        55
     A                                      HIGHLIGHT
     A            TUBILN        10        77
     A                                      HIGHLIGHT
     A            TUHENG        10        87
     A                                      HIGHLIGHT
S01  A*           TUTVKT         5  0     97EDTCDE(3)
N01  A            TUTVKT         9  0     97EDTCDE(3)
     A                                      HIGHLIGHT
S01  A*N96        TUTLM          4  2    103EDTWRD(' 0,  ')
N01  A N96        TUTLM          4  2    106EDTWRD(' 0,  ')
     A                                      HIGHLIGHT
S01  A*N97        TUTM3          5  2    109EDTWRD('  0,  ')
N01  A N97        TUTM3          7  3    111EDTWRD('   0,   ')
     A                                      HIGHLIGHT
     A          R DETST                     SPACEB(1)
     A                                     3MSGCON(15 JOV0006 SYMSGF)
     A  99                                20MSGCON(35 JOV0013 SYMSGF)
     A                                      HIGHLIGHT
     A N99                                20MSGCON(35 JOV0014 SYMSGF)
     A                                      HIGHLIGHT
     A          R DET2H                     SPACEB(1)
     A                                     3MSGCON(118 JOV0008 SYMSGF)
     A          R DET2                      SPACEB(1)
     A  51N61                              3'Avtalt/est(A)'
     A  52N62                              3MSGCON(13 JOV0154 SYMSGF)
     A  53N63                              3MSGCON(13 JOV0155 SYMSGF)
     A  54N64                              3MSGCON(13 JOV0156 SYMSGF)
     A  55N65                              3MSGCON(13 JOV0157 SYMSGF)
     A  56N66                              3MSGCON(13 JOV0158 SYMSGF)
     A  57N67                              3MSGCON(13 JOV0159 SYMSGF)
     A            TVVK           3        16
     A            TVTXT         35        20
     A            TVOAVD         4  0     53EDTCDE(Z)
     A                                    57'/'
     A            TVOPD          7  0     58EDTCDE(Z)
     A  55
     AO 56        TVBNRB         7  0     66EDTCDE(Z)
     A  52 50                             66MSGCON(7 JOV0172 SYMSGF)
     A  52        TVBLLB        11  2     74EDTCDE(J)
     A  52        TVPRO          5  2     89EDTWRD('   .  ')
     A            TVBLL         11  2     95EDTCDE(J)
     A          R DETTOT                    SPACEB(1)
     A            THTOT         11  2    103EDTCDE(J)
     A                                      HIGHLIGHT
     A  34                               120'*'
      *_______________________________
      * POSTER  FOR GODSKRIVING VED REKV.
      *"""""""""""""""""""""""""""""""
     A          R GHEAD                     SPACEB(1)
     A                                     3MSGCON(79 DIV2409 SYMSGF)
     A                                      HIGHLIGHT
     A          R GDET                       SPACEB(1)
     A            BUBILN        10         3
     A            BUDATO         6  0     12EDTWRD('  .  .  ')
     A            TVTXT         35        21
     A                                    56MSGCON(4 JOV0161 SYMSGF)
     A            BUOAVD         4  0     60EDTCDE(Z)
     A                                    64'/'
     A            BUOPD          7  0     65EDTCDE(Z)
     A                                    73MSGCON(4 JOV0162 SYMSGF)
     A            BUTAVD         4  0     77EDTCDE(Z)
     A                                    81'/'
     A            BUTUNR         8  0     82EDTCDE(Z)
     A            TVBLL         11  2     92EDTCDE(J)
     A  34                               120'*'
     A          R GDETT                      SPACEB(1)
     A                                    11MSGCON(80 JOV0153 SYMSGF)
     A            THTOT         11  2    103EDTCDE(J)
     A                                      HIGHLIGHT
      *_______________________________
      * POSTER  FOR TREKK (UTLEGG)  REKV.
      *"""""""""""""""""""""""""""""""
     A          R THEAD                     SPACEB(1)
     A                                     3MSGCON(79 JOV0150 SYMSGF)
     A                                      HIGHLIGHT
     A          R TDET                       SPACEB(1)
     A            BUBILN        10         3
     A            BUDATO         6  0     12EDTWRD('  .  .  ')
     A            TVTXT         35        21
     A                                    56MSGCON(8 JOV0151 SYMSGF)
     A            BUBNR          7  0     65EDTCDE(Z)
     A                                    73MSGCON(8 JOV0152 SYMSGF)
     A            BUBNR2         7  0     81EDTCDE(Z)
     A            TVBLL         11  2     90EDTCDE(J)
     A  34                               120'*'
     A          R TOTA                      SPACEB(2)
     A                                     3MSGCON(16 DIV1682 SYMSGF)
     A            XMVAGR        13  2     22EDTCDE(J)
     A                                    41MSGCON(20 DIV1683 SYMSGF)
     A            PRITAX         4  2     54EDTCDE(J)
     A            XVAL           3        97
     A            XMOMS         13  2    100EDTCDE(J)
     A                                      HIGHLIGHT
      *_________________________________________________
      * Format for faktura-total.
      *""""""""""""""""""""""""""""""""""""""""""""""""""
     A          R TOTC                      SPACEB(2)
     A                                    88MSGCON(12 DIV1685 SYMSGF)
     A                                      HIGHLIGHT
     A            XVAL           3        97
     A                                      HIGHLIGHT
     A            XBRUTT        13  2    100EDTCDE(J)
     A                                      HIGHLIGHT
     A                                     2' '
     A                                      SPACEB(1)
     A                                    88'================================'
     A                                      HIGHLIGHT
