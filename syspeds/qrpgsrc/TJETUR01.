      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR01 MODULE(*LIBL/TJETUR01 *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FTURER1    IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
N01  FHEAD11    IF   E           K DISK    USROPN
N01  FTRACKL02  IF   E           K DISK    USROPN
N01  FFREETUR2  IF   E           K DISK    USROPN
N01  FFREETXT3  IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D user            s             10
     D WTUBILN         s             10    varying
     D WSSAVD          S              4
     D WBSAVD          S              4  0
     D WSSST           S              1
     D WTUSTEF         S              5    varying
     D WTUSTET         S              5    varying
     D WTUDT           S              8
     D WTUDTN          S              8  0
     D WTUDTT          S              8
     D WTUDTTN         S              8  0
     D WTUSG           S              3
     D Error           S            150
     D JSONstring      s          64000
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D AntLest         S              9  0
     D AntVist         S              9  0
N01  D MaxLes          S              9  0
N01  D MaxVis          S              9  0
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT

      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬wssst¬ : ¬'+%trim(WSSST)+'¬, '
        +'¬wssavd¬ : ¬'+%trim(WSSAVD)+'¬, '
        +'¬wtubiln¬ : ¬'+%trim(WTUBILN)+'¬, '
        +'¬wtustef¬ : ¬'+%trim(WTUSTEF)+'¬, '
        +'¬wtustet¬ : ¬'+%trim(WTUSTET)+'¬, '
        +'¬wtudt¬ : ¬'+%trim(WTUDT)+'¬, '
        +'¬wtudtt¬ : ¬'+%trim(WTUDTT)+'¬, '
        +'¬wtusg¬ : ¬'+%trim(WTUSG)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"wrktriplist" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     c     Error         ifeq      *blanks
      * HOVEDRUTINE
      * Vis ordre på trip.
     C                   IF        XAVD=9999
     C     TURER1K       SETGT     TURER1
     C                   READP     TURER1                                 35
     C                   ELSE
     C     TURER1K       SETGT     TURER1
     C     TURER1K       READPE    TURER1                                 35
     C                   ENDIF
     C                   DOW       *IN35 = *OFF
     C                   Add       1             AntLest
     C                   EXSR      SRFILTER
     C                   IF        XVIS = 'J'
     C                   EXSR      SETROW
     C                   ENDIF
N01  c                   IF        AntLest >= MaxLes or
N01  c                             AntVist >= MaxVis
N01  c                   leave
N01  c                   endif
     C                   IF        XAVD=9999
     C                   READP     TURER1                                 35
     C                   ELSE
     C     TURER1K       READPE    TURER1                                 35
     C                   ENDIF
     C                   ENDDO
     c                   endif

      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * hoppet ut pga for mange - =gi warning
N01  c                   IF        AntLest >= MaxLes or
N01  c                             AntVist >= MaxVis
N01  c                   IF        AntVist >= MaxVis
N01  c                   EVAL      JSONstring =',"maxWarning" : "Max '
N01  c                             +%trim(%editc(MaxVis:'Z'))+ ' is shown. '
N01  c                             +'Please limit your search."'
N01  c                   else
N01  c                   EVAL      JSONstring =',"maxWarning" : "Max '
N01  c                             +%trim(%editc(MaxLes:'Z'))+ ' is searched.'
N01  c                             +'Please limit your search."'
N01  c                   endif
N01  C                   CALLP     updHTMLvar2('JSONstring'
N01  C                                         :%addr(JSONstring)
N01  C                                         :%len(JSONstring))
N01  C                   CALLP     wrtsection('JSONlin')
N01  c                   endif
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   exsr      Exit
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user     = zhbgetvar('user')
     C                   eval      WSSST    = zhbgetvar('WSSST')
     C     LC:UC         XLATE     WSSST         WSSST
     C                   eval      WSSAVD   = zhbgetvar('WSSAVD')
     C     LC:UC         XLATE     WSSAVD        WSSAVD                         I fall all,in,out..
     C                   EVAL      WBSAVD   = c2n2(WSSAVD)
     C                   eval      WTUBILN  = zhbgetvar('WTUBILN')
     C                   eval      WTUSTEF  = zhbgetvar('WTUSTEF')
     C                   eval      WTUSTET  = zhbgetvar('WTUSTET')
     C                   eval      WTUDT    = zhbgetvar('WTUDT')
     C                   EVAL      WTUDTN   = c2n2(WTUDT)
     C                   eval      WTUDTT   = zhbgetvar('WTUDTT')
     C                   EVAL      WTUDTTN  = c2n2(WTUDTT)
     C                   eval      WTUSG    = zhbgetvar('WTUSG')
     C     LC:UC         XLATE     WTUSG         WTUSG

     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     TURER1
     C                   CLOSE     KODTA
N01  C                   CLOSE     HEAD11
N01  C                   CLOSE     TRACKL02
N01  C                   CLOSE     FREETUR2
N01  C                   CLOSE     FREETXT3
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return

     C                   endsr
      *=====================================================================
      *  TEST OM TUR SKAL VISES
      *=====================================================================
     C     SRFILTER      begsr

     C                   MOVE      'N'           XVIS              1
     C                   IF        XTURTYPE = 'O'
     C                   IF        ((TUSG = 'WEB') AND (TUATA = 0))
     C                   MOVE      'J'           XVIS
     C                   ENDIF
     C                   ELSE
     C                   IF        TURUND <> 0
     C                   MOVE      'J'           XVIS
     C                   ENDIF
     C                   ENDIF
     C     XVIS          IFEQ      'J'
     C     WTUBILN       ANDNE     *BLANKS
     C     WTUBILN       SCAN      TUBILN                                 34
     C  N34              MOVE      'N'           XVIS
     C   34              MOVE      'J'           XVIS
     C                   ENDIF
     C     XVIS          IFEQ      'J'
     C     WTUSTEF       ANDNE     *BLANKS
     C     WTUSTEF       SCAN      TUSTEF                                 34
     C  N34              MOVE      'N'           XVIS
     C   34              MOVE      'J'           XVIS
     C                   ENDIF
     C     XVIS          IFEQ      'J'
     C     WTUSTET       ANDNE     *BLANKS
     C     WTUSTET       SCAN      TUSTET                                 34
     C  N34              MOVE      'N'           XVIS
     C   34              MOVE      'J'           XVIS
     C                   ENDIF
     C     XVIS          IFEQ      'J'
     C     WTUDTN        ANDNE     *ZEROS
     C     TUDT          IFGE      WTUDTN
     C                   MOVE      'J'           XVIS
     C                   ELSE
     C                   MOVE      'N'           XVIS
     C                   ENDIF
     C                   ENDIF
     C     XVIS          IFEQ      'J'
     C     WTUDTTN       ANDNE     *ZEROS
     C     TUDTT         IFLE      WTUDTTN
     C                   MOVE      'J'           XVIS
     C                   ELSE
     C                   MOVE      'N'           XVIS
     C                   ENDIF
     C                   ENDIF
     C     XVIS          IFEQ      'J'
     C     WTUSG         ANDNE     *BLANKS
     C     WTUSG         IFNE      TUSG
     C                   MOVE      'N'           XVIS
     C                   ENDIF
     C                   ENDIF
     C     XVIS          IFEQ      'J'
      *  IN - kun inngående avdelinger innland
     C                   IF        WSSAVD = 'IN '
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE <> '2'
     C                   EVAL      XVIS='N'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  OUT- kun utgående  avdelinger innland
     C                   IF        WSSAVD = 'OUT'
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE <> '1'
     C                   EVAL      XVIS='N'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  DOM- kun domestic (D/1/2)
     C                   IF        WSSAVD = 'DOM'
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE<>'D' and KOAIE<>'1' and KOAIE<>'2'
     C                   EVAL      XVIS='N'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  IMP- kun import
     C                   IF        WSSAVD = 'IMP'
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE <> 'I'
     C                   EVAL      XVIS='N'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
      *  EXP- kun eksport
     C                   IF        WSSAVD = 'EXP'
     C                   MOVE      *blanks       KOAIE
     C     KOAkey        CHAIN     KODTA
     C                   IF        KOAIE <> 'E'
     C                   GOTO      SlFilter2
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C     SLFILTER2     TAG
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     C                   move      *blanks       TurClose        100
     c                   if        XTURTYPE = 'L'
     c                   if        TUST = ' '
     c                   eval      TurClose='close'
     c                   else
     c                   eval      TurClose='open'
     c                   endif
     c                   endif
     C                   MOVE      TUPRO         TUPRO2            8
     c                   if        TUBILN = '.' and TUHENG<>' '
     c                   eval      TUBILN = TUHENG
     c                   endif
     C                   MOVEL     TUSONF        TULKF             2
     C                   MOVEL     TUSONT        TULKT             2
      * FINN TURENS RESULTAT I PROSENT AV OPPNÅDD RESULTATMÅL/ESTIMAT
     c                   z-ADD     0             UBELØP           13 2
     c                   z-ADD     0             BUDSJ            11 2
     C                   Z-ADD     0             TURES             5 2
      * Sum frakt/fraktrelaterte inntekter
     c                   move      'T'           kodet
     c                   call      'SYGE83'
     c                   parm                    tupro
     c                   parm                    kodet             1
     c                   parm                    ubelop           13 2
      *
      *  hente budsjett  kalkulert kost
     c                   move      tupro         uproa             8
     c                   call      'SYGE79TCL'
     c                   parm                    uproa
     c                   parm                    wstvalb           3
     c                   parm                    budsj            11 2
     c                   parm                    wstakob           1
      *
     c                   move      *zeros        work             15 5
     c     budsj         ifne      0
     c     ubelop        div       budsj         work             15 5
     c     work          mult      100           work
     c                   end
     c     Work          iflt      999.99
     c                   z-add     work          TURES
     c                   else
     c                   z-add     999.99        TURES
     c                   endif
     C
N01   * PDA-tester
N01  c                   MOVE      *blanks       PDASTAT          10
N01  c                   MOVE      'false'       PDAOPDMSG         5
N01  c                   MOVE      'false'       PDATURMSG         5
N01  c                   MOVE      'false'       PDACHGVKT         5
N01  C     'PDA'         SCAN      TUSJN1                                 33
N01  c     *in33         ifeq      '1'
N01  c                   eval      PDASTAT = 'assigned'
N01  c                   select
N01  C     TUTST4        wheneq    '1'
N01  c                   eval      PDASTAT = 'inWork  '
N01  C     TUTST4        wheneq    '2'
N01  c                   eval      PDASTAT = 'finished'
N01  c                   endsl
N01   * melding på turnivå?
N01  c     PDFTTKey      Chain     FreeTur2                           33
N01  c  N33              eval      PDATURMSG ='true'
N01   * oppdrag med ukvittert melding eller oppdragsendring ?
N01  C     Tupro         setll     Head11
N01  C     Tupro         Reade     Head11                                 33
N01  C     *in33         doweq     *OFF
N01  c     TTCHGKey      Chain     TRACKl02                           33
N01  C  N33              eval      PDACHGVKT ='true'
N01  c     PDFTSKey      Chain     FreeTxt3                           33
N01  C  N33              eval      PDAOPDMSG ='true'
N01  C     Tupro         Reade     Head11                                 33
N01  C                   enddo
N01   *
N01  c                   endif
      **
     C                   Add       1             AntVist
      /free
       if AntVist =1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬xturtype¬ : ¬'+%trim(XTURTYPE)+'¬, '
          +'¬tuavd¬ : ¬'+%trim(%editc(TUAVD:'Z'))+'¬, '
          +'¬tupro¬ : ¬'+%trim(%editc(TUPRO:'Z'))+'¬, '
          +'¬tupro2¬ : ¬'+%trim(TUPRO2)+'¬, '
          +'¬tustef¬ : ¬'+%trim(TUSTEF)+'¬, '
          +'¬tustet¬ : ¬'+%trim(TUSTET)+'¬, '
          +'¬tuetd¬ : ¬'+%trim(%editc(TUETD:'Z'))+'¬, '
          +'¬tueta¬ : ¬'+%trim(%editc(TUETA:'Z'))+'¬, '
          +'¬tuatd¬ : ¬'+%trim(%editc(TUATD:'Z'))+'¬, '
          +'¬tucon1¬ : ¬'+%trim(TUCON1)+'¬, '
          +'¬tubiln¬ : ¬'+%trim(TUBILN)+'¬, '
          +'¬tudt¬ : ¬'+%trim(%editc(TUDT:'Z'))+'¬, '
          +'¬tudtt¬ : ¬'+%trim(%editc(TUDTT:'Z'))+'¬, '
          +'¬tutm¬ : ¬'+%trim(%editc(TUTM:'Z'))+'¬, '
          +'¬tutmt¬ : ¬'+%trim(%editc(TUTMT:'Z'))+'¬, '
          +'¬tulkf¬ : ¬'+%trim(TULKF)+'¬, '
          +'¬tulkt¬ : ¬'+%trim(TULKT)+'¬, '
          +'¬tusg¬ : ¬'+%trim(TUSG)+'¬, '
          +'¬turund¬ : ¬'+%trim(%editc(TURUND:'Z'))+'¬, '
          +'¬tutst1¬ : ¬'+%trim(TUTST1)+'¬, '
          +'¬tust¬ : ¬'+%trim(TUST)+'¬, '
          +'¬tuao¬ : ¬'+%trim(%editc(TUAO:'3'))+'¬, '
          +'¬tuts¬ : ¬'+%trim(%editc(TUTS:'3'))+'¬, '
          +'¬tutvkt¬ : ¬'+%trim(%editc(TUTVKT:'3'))+'¬, '
          +'¬tutm3¬ : ¬'+%trim(%editc(TUTM3:'3'))+'¬, '
          +'¬tutlm2¬ : ¬'+%trim(%editc(TUTLM2:'3'))+'¬, '
          +'¬tupoen¬ : ¬'+%trim(%editc(TUPOEN:'Z'))+'¬, '
          +'¬tures¬ : ¬'+%trim(%editc(TURES:'3'))+'¬, '
          +'¬pdaStat¬ : ¬'+%trim(PDASTAT)+'¬, '
          +'¬pdaChgVkt¬ : ¬'+%trim(pdaChgVkt)+'¬, '
          +'¬pdaTurMsg¬ : ¬'+%trim(pdaTurMsg)+'¬, '
          +'¬pdaOpdMsg¬ : ¬'+%trim(pdaOpdMsg)+'¬, '
          +'¬turclose¬ : ¬'+%trim(TURCLOSE)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C   50              eval      Error = 'Invalid userID'
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      TURER1
     C                   OPEN      KODTA
N01  C                   OPEN      HEAD11
N01  C                   OPEN      TRACKL02
N01  C                   OPEN      FREETUR2
N01  C                   OPEN      FREETXT3
      *
     C     TURER1K       klist
     C                   kfld                    WSSST
     C                   kfld                    XAVD
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     KOAkey        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    TUAVD
     C                   MOVEL     'A'           KOAUNI
N01  C     TTCHGKey      KLIST
N01  C                   KFLD                    HEAVD
N01  C                   KFLD                    HEOPD
N01  C                   KFLD                    XXFBNR            3 0
N01  C                   KFLD                    XXACTI            3
N01  C                   MOVE      'CHG'         XXACTI
N01   * Kode P / BLANK i LEST = ulest PDA-melding PÅ TUR
N01  C     PDFTTKey      KLIST
N01  C                   KFLD                    TUAVD
N01  C                   KFLD                    TUPRO
N01  C                   KFLD                    XFTKOD            1
N01  C                   KFLD                    XFTLES
N01  c     *like         Define    FRTLES        XFTLES
N01   * Kode P /  ulest PDA-melding PÅ OPPDRAG
N01  C     PDFTSKey      KLIST
N01  C                   KFLD                    HEAVD
N01  C                   KFLD                    HEOPD
N01  C                   KFLD                    XFTKOD            1
N01  C                   KFLD                    XFTLES            1            blank = Ulest
N01  C                   MOVE      'P'           XFTKOD                         P=melding fra PDA
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Bruke oversjø eller landtransport
     C                   MOVE      'TURPL'       PAID
     C     BrParKey      chain     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XTURTYPE          1
     C   33              MOVE      'O'           XTURTYPE
      * Brukeren kan kun
     C                   IF        XTURTYPE = 'O'
     C                   MOVE      'OSAVD'       PAID
     C                   ELSE
     C                   MOVE      'LTAVD'       PAID
     C                   END
     C     BrParKey      chain     BRPARF                             33
     C  N33              Z-ADD     PAVRDN        XAVD              4 0
     C                   IF        WBSAVD <> 0
     C                   EVAL      XAVD = WBSAVD
     C                   END
     C                   IF        WSSAVD='ALL' OR
     C                             WSSAVD='IN ' OR WSSAVD='OUT' OR
     C                             WSSAVD='IMP' OR WSSAVD='EXP' OR
     C                             WSSAVD='DOM'
     C                   EVAL      XAVD=9999
     C                   ENDIF
      * hardkodede maxverdier
     C                   z-add     5000          MaxLes
     C                   z-add     200           MaxVis
     C                   endsr
