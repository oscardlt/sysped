     A*%%TS  SD  20150322  224001  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 SH  PTF10 ny dangerous goods logikk
10XXXA* 02 YBC PTF10 LEVERINGSMÅTE I SUBFIL
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      ALTHELP
     A                                      HELP
     A*
     A          R GSS020DA                  SFL
     A*%%TS  SD  20150322  224001  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  B  6  2VALUES(' ' '1' '2' '3' '5' '6' '9' -
N07  A                                      'P' 'A' 'L' 'I' 'C' 'D' 'M')
S07  A*                                     'P' 'A' 'L' 'I' 'C' 'D')
     A            TRTNO         11Y 0O  6  5EDTCDE(4)
     A  41                                  COLOR(WHT)
     A            TRST           1A  O  6 17
     A            TRAD1         30A  H
     A            XTRAD1        28A  O  6 19
     A            TRBCD          5A  O  6 48
     A  43                                  COLOR(WHT)
     A            TRCNO          8Y 0O  6 54EDTCDE(4)
     A  42                                  COLOR(WHT)
     A            TRDTL          8Y 0H
     A            WXDTL          6Y 0O  6 63EDTCDE(4)
     A            TRDTS          8Y 0H
     A            WXDTS          6Y 0O  6 70EDTCDE(4)
     A            TRFC           3A  O  6 77
     A            TRDW          15A  H
     A*
     A          R GSS020DB                  SFLCTL(GSS020DA)
     A*%%TS  SD  20150322  224001  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0016)
     A  *DS4                                SFLSIZ(0016)
     A  *DS3                                SFLPAG(0015)
     A  *DS4                                SFLPAG(0015)
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE EN NY')
     A                                      CF23(23 'BEKREFTE')
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS020D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with trips'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
N07  A                                  2  2'M=Messurement'
N07  A                                      COLOR(BLU)
     A                                  2 18MSGCON(010 SHU1010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 30'C=Calc inv'
     A                                      COLOR(BLU)
     A                                  2 45'D=DG'
     A                                      COLOR(BLU)
     A                                  2 58'I=Box'
     A                                      COLOR(BLU)
     A                                  2 69'L=Labels'
     A                                      COLOR(BLU)
     A                                  3  2'Type options: 1=Create  2=Change 5-
     A                                      =Display 6=Print 9=Confirm  P=Picki-
     A                                      ng list'
     A                                      COLOR(BLU)
     A                                  4  2'Op Trip No... St Reciever         -
     A                                                  BoatC ClientsN Del.DT D-
     A                                      is.DT Fir'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2DSPATR(UL)
     A            W2TNO         11Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2BCD          5A  B  5 48DSPATR(UL)
     A            W2CNO          8Y 0B  5 54DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  2  2'E=Send EDI'
     A                                      COLOR(BLU)
      *
     A          R GSS020DC
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=End   F4=Query   F5=Refresh   F-
     A                                      6=Create   F23=Confirm'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS020DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF06(06 'LAGE EN NY')
     A                                      CF23(23 'BEKREFTE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS020D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with trips'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2 58'I=Box'
     A                                      COLOR(BLU)
     A                                  2 69'L=Labels'
     A                                      COLOR(BLU)
     A                                  3  2'Type options:   1=Create  2=Change-
     A                                        5=Display  6=Print  9=Confirm  P=-
     A                                      Plukkliste'
     A                                      COLOR(BLU)
     A                                  4  2'Op Trip No... St Reciever         -
     A                                                  BoatC ClientsN Del.DT D-
     A                                      is.DT Fir'
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2DSPATR(UL)
     A            W2TNO         11Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2BCD          5A  B  5 48DSPATR(UL)
     A            W2CNO          8Y 0B  5 54DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  8  2'No trip found'
     A                                      COLOR(RED)
     A                                 23  2'F3=End   F4=Query   F6=Create   F2-
     A                                      3=Confirm'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 7 30 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R GSS020DX
     A                                      WINDOW(WINDOWSZ)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print packinglist'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRT         10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'No. of copies:'
     A            WSCPY          1S 0B  4 17DSPATR(UL)
     A                                  5  2'Send Fax/Email'
     A            WSFAX          1A  B  5 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  5 20'Y/N'
     A                                  6  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99        WSMSGN        25A  O  7  2MSGID(GSS 0042 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R GSS020DY
     A                                      WINDOW(*DFT 12 60 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print picking/packing list'
     A                                      COLOR(WHT)
     A                                  3  2'Type/sort:'
     A            WSSORT         1A  B  3 13VALUES('L' 'O' 'B' 'P' 'F' 'T')
     A                                      DSPATR(PC)
     A                                  3 15'(L=Lo,O=Or,B=Bo sorted Pick.list)'
     A                                  4 15'(P=Pack.l. pr Box,F=Final Pack.lis-
     A                                      t,T=Cont)'
     A                                  6  2'Cl labels:'
     A            WSCLAB         1A  B  6 13DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  6 15'(Y=Print new colli labels (L,O,B))'
     A                                  8  2'Send Email'
     A            WSFAX          1A  B  8 13DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  8 15'(Y=Email/Fax Final Packing list)'
     A                                 10  2'Printer  :'
     A            WSPRT         10A  B 10 13DSPATR(UL)
     A                                      DSPATR(PC)
     A                                 11  2'Label prt:'
     A            WSPRT2        10A  B 11 13DSPATR(UL)
     A                                      DSPATR(PC)
     A                                 11 24'(On Type=P or Cl lab=Y)'
     A  99        WSMSGN        25A  O 12  2MSGID(GSS 0042 SYMSGF)
     A                                      COLOR(WHT)
     A                                 12 31'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R GSS020DH
     A                                      WINDOW(*DFT 6 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print labels'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRTM        10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  5  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  89        WSMSGN        25A     6  2MSGID(GSS0042 SYMSGF)
