********************************************************************
      * LESER FILER I IFS
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
     H DATEDIT(*DMY) debug(*yes)
     H DFTACTGRP(*NO)
N38  FWRKFLR    O  A E           K DISK
      *_______________________________________________________
      * Les lenker i en katalog  og kopier dem til DB2 i SYEDI
      * Dette program fungerer kun for80 bytes recordlengde.
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""
      *_____________________________
      * Prototype for API prosedyrer
      *"""""""""""""""""""""""""""""
     Dlstat            PR            10I 0 EXTPROC('lstat')
     D                                 *   VALUE
     D                                 *   VALUE

     Dopendir          PR              *   EXTPROC('opendir')
     D                                 *   VALUE

     Dreaddir          PR              *   EXTPROC('readdir')
     D                                 *   VALUE

     Dclosedir         PR            10I 0 EXTPROC('closedir')
     D                                 *   VALUE
     D SndPgmMsg       PR              N
     D Qmsgid                         7    CONST
     D Qmsgf                         20    CONST
     D Qmsg                         128    CONST
     D Qmsgtp                        10    CONST OPTIONS(*NOPASS)

     D GetTimeZone     PR             5A

     D                 DS
     D  datoxx                 1     60
     D  DSAAR                  1      4  0
     D  DSMND                  6      7  0
     D  DSDAG                  9     10  0
     D  DSTIM                 12     13  0
     D  DSMIN                 15     16  0
     D  DSSEK                 18     19  0
     D                 DS
     D  datoe                  1      8
     D  DSAAR2                 1      4  0
     D  DSMND2                 5      6  0
     D  DSDAG2                 7      8  0
     D                 DS
     D  tide                   1      6
     D  DSTIM2                 1      2  0
     D  DSMIN2                 3      4  0
     D  DSSEK2                 5      6  0

     D timezone        DS
     D   tzDir                        1A
     D   tzHour                       2S 0
     D   tzFrac                       2S 0

      *   stat data structure fra prosedyre: lstat()
     D StatDS          DS           128
     D  st_mode                      10U 0
     D  st_ino                       10U 0
     D  st_nlink                      5U 0
     D  reserved1                     2A
     D  st_uid                       10U 0
     D  st_gid                       10U 0
     D  st_size                      10U 0
     D  st_atime                     10U 0
     D  st_mtime                     10U 0
     D  st_ctime                     10U 0
     D  st_dev                       10U 0
     D  st_blksize                   10I 0
     D  st_allocsize                 10I 0
     D  st_objtype                   10A
     D  reserved2                     2A
     D  st_codepage                   5U 0
     D  st_reserved1                 62A
     D  st_ino_gen_id                10U 0

      *   direntry data structure fra prosedyre: readdir()
     D DirEntry        DS
     D d_reserved1                   16A
     D d_fileno_genid                10U 0
     D d_fileno                      10U 0
     D d_reclen                      10U 0
     D d_reserved3                   10I 0
     D d_reservedØ4                   6A
     D d_reserved5                    2A
     D d_ccsid                       10I 0
     D d_country_id                   2A
     D d_language_id                  3A
     D d_nls_reserved                 3A
     D d_namelen                     10U 0
     D d_name                       640A

     D AccessTime      S               Z
     D ModifyTime      S               Z
     D ChgStsTime      S               Z
     D charTS          S             26A

     D Epoch           S               Z   INZ(z'1970-01-01-00.00.00')
     D Null            S              1A   Inz(X'00')
     D RETURNInt       S             10I 0
     D RETURNDir       S               *
     D PtrToEntry      S               *
     D RtnEntry        S                   BASED(PtrToEntry) Like(DirEntry)
     D EntryName       S            120A
     D EntryPath       S            256A
     D EntryPath2      S            256A
     D CmdLine         S            512
     D CmdLen          S             15  5
     D HHMMSS          S              6  0
     D DirError        C                   'Feil ved åpning av katalog'

      *   Input Parametere
     D DirName         S            100A
     D FullName        S            256A

      *   Arbeidsvariabler
     D Outfile         DS
     D  OutFilNam                    10    INZ('Q2        ')
     D  OutFilLib                    10    INZ('QTEMP     ')
      *
      *
     D ObjVar          S             90
     D ObjVarLen       S             10I 0 Inz(%size(ObjVar))
     D ObjVarFmt       S              8
     D ObjTyp          S             10
N01  D Tstampz         S               Z
N01  D Tstampa         S             26
N01  D fpath           S            256
N01  D fname           S            120

     D APIERR          DS
     D  ERRSIZ                 1      4B 0 INZ(256)
     D  ERRLEN                 5      8B 0 INZ(0)
     D  ERRMIC                 9     15
     D  ERRNBR                16     16
     D  ERRDTA                17    272


     D PSDS           SDS           512

     C                   EVAL      FullName = %trimr(DirName) + Null
      *
      * Åpne katalog
      *
     C                   EVAL      RETURNDir = opendir(%addr(FullName))
      *
      * Avslutt hvis feil ved åpning av katalog
      *
     C                   IF        RETURNDir = *Null
     C                   CALLP     SndPgmMsg('CPF9898':'QCPFMSG'
     C                                       :DirError)
     C                   EVAL      *INLR = *ON
     C                   RETURN
     C                   ENDIF

      *
      * GÅR I LØKKE OG LESER
      *
     C                   DOU       PtrToEntry = *Null
      *
     C                   EXSR      lesfolder
     C                   exsr      fixdato
     C                   exsr      skriv
     C                   ENDDO
      *
      *
      *
      * Lukk katalog
     C                   EVAL      RETURNInt = closedir(RETURNDir)
      * Les alle poster sortert
     C***                EXSR      CPYSTMF

     C                   EVAL      *INLR = *ON
      *********************************************************************
     C     LESFOLDER     BEGSR
      *********************************************************************
      *
      * Les neste katalog lenke
      *
     C                   EVAL      PtrToEntry = readdir(RETURNDir)
      *
      * Lenknavn er i feltet d_name
      *
     C                   IF        PtrToEntry <> *Null
     C                   EVAL      DirEntry = RtnEntry
      *
      * Hent katalog lenknavn
      *
     C                   EVAL      EntryName = %str(%addr(d_name))
      *
      * Vilken typ av lenke ?
      *
     C                   EVAL      EntryPath = %trim(DirName) + '/'
     C                             + %trimr(EntryName) + Null
     C                   EVAL      RETURNInt = lstat(%addr(EntryPath)
     C                                         : %addr(StatDS))
     C                   IF        st_objtype='*STMF'
     C                   EVAL      FPATH = %trim(DirName) + '/'
     C                             + %trimr(EntryName)
     C                   EVAL      FNAME = %str(%addr(d_name))
     C                   MOVEL     st_ctime      FTIME            10
     C                   ENDIF
      * Kopier streamfile til PF
      * Skriv lenk til fil
     C                   ENDIF
     C                   ENDSR
      **************************************************************
     C     *INZSR        BEGSR
      **************************************************************
      *  Init av PGM

     C     Qcmdexc       PLIST
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen

     C     *ENTRY        PLIST
     C                   PARM                    UDIR            100
      *
     C                   TIME                    HHMMSS

      *
      * Lag streng for katalognavn
      *
N02  C*                  EVAL      DirName = '/film'
     C                   EVAL      DirName = %TrimR( UDIR )
     C                   ENDSR
      **************************************************************
     C     FIXDATO       BEGSR
      **************************************************************
     c     Epoch         adddur    st_atime:*S   AccessTime
     c     Epoch         adddur    st_mtime:*S   ModifyTime
     c     Epoch         adddur    st_ctime:*S   ChgStsTime

      ** adjust timestamps for timezone:
     c                   eval      timezone = GetTimeZone
     c                   if        tzDir = '-'
     c                   subdur    tzHour:*H     AccessTime
     c                   subdur    tzHour:*H     ModifyTime
     c                   subdur    tzHour:*H     ChgStsTime
     c                   else
     c                   adddur    tzHour:*H     AccessTime
     c                   adddur    tzHour:*H     ModifyTime
     c                   adddur    tzHour:*H     ChgStsTime
     c                   endif
     c                   endsr
      **************************************************************
     C     skriv         BEGSR
      **************************************************************
     c                   movel     ModifyTime    datoxx
     c                   move      DSAAR         DSAAR2
     c                   move      DSMND         DSMND2
     c                   move      DSDAG         DSDAG2
     c                   move      DSTIM         DSTIM2
     c                   move      DSMIN         DSMIN2
     c                   move      DSSEK         DSSEK2
     C                   EVAL      NAME = %str(%addr(d_name))
     C                   EVAL      DIR = %str(%addr(Dirname))
     C                   write     WRKFLRR
     c                   endsr
      *-------------------------------------------------------------------------
      * Send pgm message
      *-------------------------------------------------------------------------
     P SndPgmMsg       B
     D                 PI              N
     D Msgid                          7    CONST
     D MsgF                          20    CONST
     D Msgdta                       128    CONST
     D Msgtp                         10    CONST OPTIONS(*NOPASS)
      * Work variables
     D Qmsgid          S              7
     D Qmsgf           S             20
     D Qmsgdta         S            128
     D Qmsgln          S             10I 0
     D Qmsgtp          S             10
     D Qmsgq           S             10
     D Qmsgqn          S             10I 0 INZ(3)
     D Qmsgky          S              4
     D Qmsger          S             15
      * Hvis bibliotek er lik blank set in *LIBL
     C                   EVAL      Qmsgid = Msgid
     C                   EVAL      Qmsgf = Msgf
     C                   EVAL      Qmsgdta = Msgdta
     C                   IF        %subst(Qmsgf:11:10) = *blank
     C                   EVAL      %subst(Qmsgf:11:10) = '*LIBL'
     C                   ENDIF
     C                   EVAL      Qmsgln = %len(%trim(Qmsgdta))
     C                   EVAL      Qmsgq = '*'
     C                   EVAL      Qmsgtp = '*DIAG'
     C                   IF        %PARMs > 3
     C                   EVAL      Qmsgtp = Msgtp
     C                   ENDIF
     C                   IF        Qmsgtp = '*STATUS'
     C                   EVAL      Qmsgq = '*EXT'
     C                   ENDIF
      *
     C                   CALL      'QMHSNDPM'                           99
     C                   PARM                    Qmsgid
     C                   PARM                    Qmsgf
     C                   PARM                    Qmsgdta
     C                   PARM                    Qmsgln
     C                   PARM                    Qmsgtp
     C                   PARM                    Qmsgq
     C                   PARM                    Qmsgqn
     C                   PARM                    Qmsgky
     C                   PARM      *LOVAL        Qmsger
      *
     C                   RETURN    *ON
     P                 E
     P*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     P*  This gets the offset from Universal Coordinated Time (UTC)
     P*    from the system value QUTCOFFSET
     P*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     P GetTimeZone     B
     D GetTimeZone     PI             5A
     D peRcvVar        S              1A   DIM(100)
     D peRVarLen       S             10I 0
     D peNumVals       S             10I 0
     D peSysValNm      S             10A
     D p_Offset        S               *
     D wkOffset        S             10I 0 BASED(p_Offset)
     D p_SV            S               *
     D dsSV            ds                  BASED(p_SV)
     D   dsSVSysVal                  10A
     D   dsSVDtaTyp                   1A
     D   dsSVDtaSts                   1A
     D   dsSVDtaLen                  10I 0
     D   dsSVData                     5A
     D dsErrCode       DS
     D  dsBytesPrv             1      4B 0 INZ(256)
     D  dsBytesAvl             5      8B 0 INZ(0)
     D  dsExcpID               9     15
     D  dsReserved            16     16
     D  dsExcpData            17    256
     C                   CALL      'QWCRSVAL'                           99
     C                   PARM                    peRcvVar
     C                   PARM      100           peRVarLen
     c                   PARM      1             peNumVals
     c                   PARM      'QUTCOFFSET'  peSysValNm
     c                   PARM                    dsErrCode
     c                   if        dsBytesAvl > 0  or  *IN99 = *On
     c                   return    *blanks
     c                   endif
     c                   eval      p_Offset = %addr(peRcvVar(5))
     c                   eval      p_SV = %addr(peRcvVar(wkOffset+1))
     c                   return    dsSVData
     P                 E
