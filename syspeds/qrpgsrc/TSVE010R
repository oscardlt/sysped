      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE010R MODULE(*LIBL/TSVE010R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVEV2     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SVEV_SYAVA      S              4
     D SVEV_SYAV       S              4  0
     D SVEV_SYOPA      S              7
     D SVEV_SYOP       S              7  0
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬svev_syav¬ : ¬'+%trim(%editc(SVEV_SYAV:'Z'))+'¬, '
        +'¬svev_syop¬ : ¬'+%trim(%editc(SVEV_SYOP:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"orderList" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
     C     SVEVkey       setll     SVEV2
     C     SVEVkey       READE     SVEV2                                  55
     C     *IN55         DOWEQ     '0'
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬svev_syav¬ : ¬'+%trim(%editc(SVEV_SYAV:'Z'))+'¬, '
          +'¬svev_syop¬ : ¬'+%trim(%editc(SVEV_SYOP:'Z'))+'¬, '
          +'¬svev_syli¬ : ¬'+%trim(%editc(SVEV_SYLI:'Z'))+'¬, '
          +'¬svev_kota¬ : ¬'+%trim(%editc(SVeV_KOTA:'Z'))+'¬, '
          +'¬svev_kot2¬ : ¬'+%trim(%editc(SVeV_KOT2:'Z'))+'¬, '
          +'¬svev_kot3¬ : ¬'+%trim(%editc(SVeV_KOT3:'Z'))+'¬, '
          +'¬svev_kot4¬ : ¬'+%trim(%editc(SVeV_KOT4:'Z'))+'¬, '
          +'¬svev_kot5¬ : ¬'+%trim(%editc(SVeV_KOT5:'Z'))+'¬, '
          +'¬svev_vano¬ : ¬'+%trim(%editc(SVEV_VANO:'4'))+'¬, '
          +'¬svev_ulkd¬ : ¬'+%trim(SVEV_ULKD)+'¬, '
          +'¬svev_vata¬ : ¬'+%trim(SVEV_VATA)+'¬, '
          +'¬svev_vati¬ : ¬'+%trim(SVEV_VATI)+'¬, '
          +'¬svev_eup1¬ : ¬'+%trim(SVEV_EUP1)+'¬, '
          +'¬svev_brut¬ : ¬'+%trim(%editc(SVEV_BRUT:'4'))+'¬, '
          +'¬svev_neto¬ : ¬'+%trim(%editc(SVEV_NETO:'4'))+'¬, '
          +'¬svev_ankv¬ : ¬'+%trim(%editc(SVEV_ANKV:'4'))+'¬, '
          +'¬svev_vasl¬ : ¬'+%trim(SVEV_VASL)+'¬, '
          +'¬svev_godm¬ : ¬'+%trim(SVEV_GODM)+'¬, '
          +'¬svev_stva¬ : ¬'+%trim(%editc(SVEV_STVA:'4'))+'¬, '
          +'¬svev_stva2¬ : ¬'+%trim(%editc(SVEV_STVA2:'4'))+'¬, '
          +'¬svev_call¬ : ¬'+%trim(SVEV_CALL)+'¬, '
          +'¬svev_kosl¬ : ¬'+%trim(SVEV_KOSL)+'¬, '
          +'¬svev_fabl¬ : ¬'+%trim(%editc(SVEV_FABL:'4'))+'¬, '
          +'¬svev_bit1¬ : ¬'+%trim(SVEV_BIT1)+'¬, '
          +'¬svev_bit2¬ : ¬'+%trim(SVEV_BIT2)+'¬, '
          +'¬svev_bit3¬ : ¬'+%trim(SVEV_BIT3)+'¬, '
          +'¬svev_bit4¬ : ¬'+%trim(SVEV_BIT4)+'¬, '
          +'¬svev_bit5¬ : ¬'+%trim(SVEV_BIT5)+'¬, '
          +'¬svev_bii1¬ : ¬'+%trim(SVEV_BII1)+'¬, '
          +'¬svev_bii2¬ : ¬'+%trim(SVEV_BII2)+'¬, '
          +'¬svev_bii3¬ : ¬'+%trim(SVEV_BII3)+'¬, '
          +'¬svev_bii4¬ : ¬'+%trim(SVEV_BII4)+'¬, '
          +'¬svev_bii5¬ : ¬'+%trim(SVEV_BII5)+'¬, '
          +'¬svev_err¬ : ¬'+%trim(SVEV_ERR)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C     SVEVkey       READE     SVEV2                                  55
     C                   ENDDO
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVEV2

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      SVEV_SYAVA = zhbgetvar('AVD')
     C                   EVAL      SVEV_SYAV  = c2n2(SVEV_SYAVA)
     C                   EVAL      SVEV_SYOPA = zhbgetvar('OPD')
     C                   EVAL      SVEV_SYOP  = c2n2(SVEV_SYOPA)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SVEVkey       klist
     C                   kfld                    SVEV_SYAV
     C                   kfld                    SVEV_SYOP
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVEV2

     c                   endsr
      *
