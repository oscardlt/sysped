********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 UNIK NUMMERTELLER
********************************************************************
     H DATEDIT(*YMD)
     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FECNI96A   IF   E           K DISK
     FECOM96A   IF   E           K DISK
     FEDTM96A   IF   E           K DISK
     FEEQD96A   IF   E           K DISK
     FELOC96A   IF   E           K DISK
     FETDT96A   IF   E           K DISK
     FEFTX96A   IF   E           K DISK
     FENAD96A   IF   E           K DISK
     FERFF96A   IF   E           K DISK
     FETOD96A   IF   E           K DISK
     FEGID96A   IF   E           K DISK
     FEMEA96A   IF   E           K DISK
     FEMOA96A   IF   E           K DISK
     FEPCI96A   IF   E           K DISK
     FEQTY96A   IF   E           K DISK
N01  FFIRM      IF   E           K DISK
N01  FZTELL     UF A E           K DISK
     FZEADF     O  A E             DISK
     FZREETXT   O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D TABA            S             35    DIM(8) CTDATA PERRCD(1)
     D TABB            S              8    DIM(8) ALT(TABA)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C*          *LIKE     DEFN HEHAWB    WEHAWB
     C     *LIKE         DEFINE    HEREFF        WEHAWB
     C     *LIKE         DEFINE    HETRI         WETRI
     C     *LIKE         DEFINE    HETRC         WETRC
     C     *LIKE         DEFINE    HETRCN        WETRCN
     C     *LIKE         DEFINE    HESDF         WESDF
     C     *LIKE         DEFINE    HEKNA         WEKNA
     C     *LIKE         DEFINE    HESDL         WESDL
      * INIT WORKFIELDS
     C                   MOVE      *BLANKS       WEHAWB
     C                   MOVE      *BLANKS       WETRI
     C                   MOVE      *BLANKS       WETRC
     C                   MOVE      *BLANKS       WETRCN
     C                   MOVE      *BLANKS       WESDF
     C                   MOVE      *ZEROS        WEKNA
     C                   MOVE      *BLANKS       WESDL
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
N01  C     *LOVAL        SETLL     FIRM
N01  C                   READ      FIRM                                   51
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *_______________________
      * READ ALL MAIN SEGMENTS
      *"""""""""""""""""""""""
      * NAD09
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     09            ZGN0
     C     KEYMS0        SETLL     ENAD96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD96A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     N3035         IFEQ      'CS'                                         SENDING AG
     C                   Z-ADD     0             WEKNA
     C     N3039         LOOKUP    TABA          TABB                     20
     C     *IN51         IFEQ      *ON
     C                   MOVEL     TABB          WEKNA
     C                   MOVE      '1'           *IN51
     C                   ENDIF
     C                   ENDIF
      *
     C  N51KEYMS0        READE     ENAD96A                                51
     C  N51              END
     C                   END
      * DTM05
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        CHAIN     EDTM96A                            51
     C     *IN51         IFEQ      '0'
     C                   MOVEL     D2380         W2380            35
     C                   ENDIF
      * LOC13
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     13            ZGN0
     C     KEYMS0        SETLL     ELOC96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELOC96A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     L3227         IFEQ      '5'
     C                   MOVEL     E3055A        WESDF
     C                   ELSE
     C     L3227         IFEQ      'ZZZ'
     C                   MOVEL     E3055A        WETRI
     C                   END
     C                   END
      *
     C  N51KEYMS0        READE     ELOC96A                                51
     C  N51              ENDDO
     C                   END
      * EQD13
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     13            ZGN0
     C     KEYMS0        SETLL     EEQD96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EEQD96A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     E8053         IFEQ      'CN'
     C                   MOVE      '1'           WETRC
     C                   MOVEL     E8260         WETRCN
     C                   MOVE      '1'           *IN51
     C                   ELSE
     C     E8053         IFEQ      'TE'
     C*                    MOVELE8260     WEHAWB
     C                   MOVEL     E8260         WETRCN
     C                   MOVE      '1'           *IN51
     C                   END
     C                   END
      *
     C  N51KEYMS0        READE     EEQD96A                                51
     C  N51              ENDDO
     C                   END
      * TDT05
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ETDT96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT96A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     T8212         WEHAWB
      *
     C  N51KEYMS0        READE     ETDT96A                                51
     C  N51              ENDDO
     C                   END
      *___________________
      * READ CONSIGNEMENTS
      *"""""""""""""""""""
     C                   Z-ADD     0             WPOS              3 0
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     15            ZGN0
     C     KEYMS0        SETLL     ECNI96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNI96A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ECNI96A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   MOVE      'A'           HEUR
     C                   Z-ADD     WEKNA         HEKNA
     C                   MOVEL     'HELLMAN'     HENAA
     C                   MOVEL     'S'           HESDLK
     C                   MOVEL     'S'           HESDL
     C*                    MOVELWEKNA     HEKNA
     C                   MOVEL     M1004         HERFA
     C                   ADD       1             WPOS
     C*                    MOVELWEHAWB    HEHAWB
     C                   MOVEL     WEHAWB        HETRCN
     C                   MOVEL     WPOS          HEPOS1
     C                   MOVEL     SLIB          HELIB
     C                   MOVEL     SFILE         HEFILE
     C                   MOVEL     SMBR          HEMBR
     C                   MOVE      'IFCSUM'      HETYPE
     C                   MOVE      '96A'         HELVL
     C                   Z-ADD     ZMN           HESNN
     C                   MOVEL     WEHAWB        HEREFF
     C     HEREFF        CAT       M1004:1       HEREFF
S01  C*    ZMN           MULT      1000          HEUNIK
S01  C*    HEUNIK        ADD       WPOS          HEUNIK
N01  C     FIFIRM        CHAIN     ZTELL                              33
N01  C                   IF        *IN33
N01  C                   EVAL      FIRMA = FIFIRM
N01  C                   Z-ADD     700000001     HEUNIK
N01  C                   Z-ADD     700000002     ZUNIK
N01  C                   WRITE     ZTELLR
N01  C                   ELSE
N01  C                   Z-ADD     ZUNIK         HEUNIK
N01  C                   ADD       1             ZUNIK
N01  c                   UPDATE    ZTELLR
N01  c                   END
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      FTX15
     C                   EXSR      LOC20
     C                   EXSR      TOD21
     C                   EXSR      QTY26
     C                   EXSR      NAD30
     C                   EXSR      RFF34
     C                   EXSR      GID37
     C                   EXSR      MEA39
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     HEADFR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LOC20                                               LOC20 /
      *////////////////////////////////////////////////////////////
     C     LOC20         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     20            ZGN1
     C     KEYMS1        SETLL     ELOC96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     L3227         IFEQ      '1'
     C                   MOVEL     L3224         HESDT
     C                   MOVE      '1'           *IN52
     C                   END
      *
     C     KEYMS1        READE     ELOC96A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TOD21                                               TOD21 /
      *////////////////////////////////////////////////////////////
     C     TOD21         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     21            ZGN1
     C     KEYMS1        SETLL     ETOD96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETOD96A                                52
     C     *IN52         IFEQ      '0'
      *
     C                   MOVEL     T4053         HEFR
     C                   MOVEL     T4053         HEFR2             1
     C                   SELECT
     C     HEFR2         WHENEQ    '1'
     C                   MOVEL     'EXW'         HEFR
     C     HEFR2         WHENEQ    '2'
     C                   MOVEL     'FCA'         HEFR
     C     HEFR2         WHENGE    '3'
     C     HEFR2         ANDLE     '7'
     C                   MOVEL     'DAF'         HEFR
     C     HEFR2         WHENEQ    '8'
     C                   MOVEL     'CIF'         HEFR
     C     HEFR          WHENGE    '900'
     C     HEFR          ANDLE     '903'
     C                   MOVEL     'DDU'         HEFR
     C     HEFR          WHENGE    '960'
     C                   MOVEL     'DDP'         HEFR
     C                   ENDSL
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * NAD30                                               NAD30 /
      *////////////////////////////////////////////////////////////
     C     NAD30         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     30            ZGN1
     C     KEYMS1        SETLL     ENAD96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CN'                                         LEV.ADR
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAK
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADK1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADK2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADK3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C     4             SUBST     WEADS1:3      HEADK3
     C     HEADK3        CAT       WEADS2:1      HEADK3
     C     4             SUBST     WEADS1:3      HESDVT
     C                   MOVEL     'NO'          HELKK
     C                   ENDIF
      * HENT COM
     C                   Z-ADD     31            ZGN2
     C                   Z-ADD     NREP0         ZREPC0
     C                   Z-ADD     NREP1         ZREPC1
     C     KEYMS2        CHAIN     ECOM96A                            53
     C     *IN53         IFEQ      *OFF
     C                   MOVEL     C3155         HEKDL
     C     HEKDL         CAT       C3148:1       HEKDL
     C                   ENDIF
      * HENT MOA
     C                   Z-ADD     30            ZGN2
     C                   Z-ADD     NREP0         ZREPC0
     C                   Z-ADD     NREP1         ZREPC1
     C     KEYMS2        CHAIN     EMOA96A                            53
     C     *IN53         IFEQ      *OFF
     C     M5025         ANDEQ     '50 '
     C                   MOVEL     'CL'          HEKF
     C                   MOVEL     M6345         TRVERV
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          TRVERB
     C                   ENDIF
      *
     C     N3035         WHENEQ    'CZ'                                         AVS.
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAS
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADS1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADS2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADS3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAS
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADS1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADS3
     C                   MOVE      WEADS2        HEADS3
     C                   MOVEL     N3251         HESDFF
     C                   MOVE      ' '           HESDFF
     C                   ENDIF
     C     N3207         IFNE      *BLANKS
     C                   MOVEL     N3207         HELKS
     C                   ENDIF
      *
     C                   ENDSL
      *
     C     KEYMS1        READE     ENAD96A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RFF34                                               RFF34 /
      *////////////////////////////////////////////////////////////
     C     RFF34         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     34            ZGN1
     C     KEYMS1        SETLL     ERFF96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     R1153         IFEQ      'AAJ'
     C                   MOVEL     R1154         HEGM2
     C                   MOVE      '1'           *IN52
     C                   ENDIF
      *
     C  N52KEYMS1        READE     ERFF96A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX15                                               FTX15 /
      *////////////////////////////////////////////////////////////
     C     FTX15         BEGSR
     C                   CLEAR                   FREETXTR
     C                   MOVE      HEUNIK        FRUNIK
     C                   MOVE      HEREFF        FRREFF
     C                   Z-ADD     MDT           FRTDT
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     15            ZGN1
     C     KEYMS1        SETLL     EFTX96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'DIN'
     C     F4451         OREQ      'HAN'
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440A        FRTTXT
     C                   WRITE     FREETXTR
     C     F4440B        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440B        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440C        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440C        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440D        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440D        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440E        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440E        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C                   ENDIF
      *
     C  N52KEYMS1        READE     EFTX96A                                52
     C  N52              ENDDO
     C                   ENDIF
      * LEGG IN AVGANGSDATO I FITEKST
     C     W2380         IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     'AVG.DATO'    FRTTXT
     C     FRTTXT        CAT       W2380:1       FRTTXT
     C                   WRITE     FREETXTR
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * GID37                                               GID37 /
      *////////////////////////////////////////////////////////////
     C     GID37         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     37            ZGN1
     C     KEYMS1        SETLL     EGID96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EGID96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C     HENT          IFEQ      0
     C                   ADD       ZNUM          HENT
     C                   MOVEL     G7065A        HEVS1
      * HENT FTX
     C                   Z-ADD     37            ZGN2
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C     KEYMS2        CHAIN     EFTX96A                            53
     C     *IN53         IFEQ      *OFF
     C*          HEVS1     CAT  F4440A:1  HEVS1
     C     'PLL'         CAT       F4440A:1      HEVS1
     C                   ENDIF
     C                   ELSE
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  G7224A        FRTTXT
     C     FRTTXT        CAT       G7064A:1      FRTTXT
      * HENT FTX
     C                   Z-ADD     37            ZGN2
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C     KEYMS2        CHAIN     EFTX96A                            53
     C     *IN53         IFEQ      *OFF
     C     FRTTXT        CAT       F4440A:1      FRTTXT
     C                   ENDIF
     C                   WRITE     FREETXTR
     C                   ENDIF
      * HENT PCI
     C                   Z-ADD     42            ZGN2
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C     KEYMS2        CHAIN     EPCI96A                            53
     C     *IN53         IFEQ      *OFF
     C                   MOVEL     P7102A        HEGM1
     C                   ENDIF
      *
     C     KEYMS1        READE     EGID96A                                52
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA39                                               MEA39 /
      *////////////////////////////////////////////////////////////
     C     MEA39         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     39            ZGN1
     C     KEYMS1        SETLL     EMEA96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C                   ADD       ZNUM          HEVKT
     C     M6311         WHENEQ    'LMT'
     C                   Z-ADD     ZNUM          HELM
     C     M6311         WHENEQ    'VOL'
     C     M6313         IFEQ      'ABJ'
     C                   ADD       ZNUM          HEM3
     C                   ELSE
     C                   ADD       ZNUM          HELM
     C                   ENDIF
     C                   ENDSL
      *
     C     KEYMS1        READE     EMEA96A                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * QTY26                                               QTY26 /
      *////////////////////////////////////////////////////////////
     C     QTY26         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     26            ZGN1
     C     KEYMS1        SETLL     EQTY96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EQTY96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     Q6060         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     Q6063         WHENEQ    '100'
     C                   ADD       ZNUM          HEVKT
     C     Q6063         WHENEQ    '109'
     C                   ADD       ZNUM          HEM3
     C     Q6063         WHENEQ    '112'
     C                   ADD       ZNUM          HELM
     C                   ENDSL
      *
     C     KEYMS1        READE     EQTY96A                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
**
FODE047373                         00000000
FODE070450                         00000000
FODE072110                         00091910
FODE072819                         00091920
FODE073371                         00000000
FODE074909                         00091900
FODE133088                         00000000
FDY                                00091970
