      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE23RF MODULE(*LIBL/TJGE23RF *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDOKUFVF   UF A E           K DISK    USROPN
     F                                     PREFIX(X)
N07  FZOKUFVF2  UF A E           K DISK    USROPN
N07  F                                     RENAME(DOKUFVFR:ZOKUFVFR)
N07  F                                     PREFIX(X)
N01  FADRUNNRF  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D NyErr           S            150                                         en errormelding
     D Error           S           5000
     D WA              S             15
     D AVD             S              4S 0
     D OPD             S              7S 0
     D FBN             S              3S 0
N07  D UNIK            S              9S 0
N07  D REFF            S             17
     D LIN             S              2S 0
     D LIN2            S              2S 0
     D FMMRK1          S             35
     D FVANT           S              7P 0
     D FVPAKN          S              7
     D FVVT            S             35
     D FVVKT           S              9P 0
     D FVVOL           S              7P 3
     D FVLM            S              4P 2
     D FVLM2           S              4P 2
     D FVLEN           S              4P 0
     D FVBRD           S              4P 0
     D FVHOY           S              4P 0
     D FFUNNR          S              4
     D FFEMBG          S              3
N01  D FFINDX          S              1
     D FFKLAS          S              4
     D FFSEDD          S             10
     D FFTRES          S             10
     D FFFAKT          S              4S 0
     D FFANTK          S              5S 0
S06  D*FFANTE          S              7S 0
N06  D FFANTE          S             10S 3
     D FFENH           S              3
     D FFPOEN          S              5S 0

N05  D BigNum          S             19S 5

     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
          +'¬avd¬ : ¬'+%trim(%editc(avd:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(opd:'Z'))+'¬, '
          +'¬fbn¬ : ¬'+%trim(%editc(fbn:'Z'))+'¬, '
N07       +'¬unik¬ : ¬'+%trim(%editc(UNIK:'Z'))+'¬, '
N07       +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
          +'¬lin¬ : ¬'+%trim(%editc(lin:'Z'))+'¬, '
          +'¬lin2¬ : ¬'+%trim(%editc(lin2:'Z'))+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"awblineUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DOKUFVF
N07  C  N50              CLOSE     ZOKUFVF2
N01  C  N50              CLOSE     ADRUNNRF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      AVD        = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      OPD        = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('FBN')
     C                   EVAL      FBN        = c2n2(WA)
N07  C                   if        FBN  = *zeros
N07  C                   EVAL      FBN  = 1
N07  C                   endif
N07  C                   eval      REFF    = zhbgetvar('REFF')
N07  C                   eval      WA      = zhbgetvar('UNIK')
N07  C                   eval      UNIK    = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('LIN')
     C                   EVAL      LIN        = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('LIN2')
     C                   EVAL      LIN2       = c2n2(WA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      FFUNNR  = zhbgetvar('FFUNNR')
     C                   EVAL      FFEMBG  = zhbgetvar('FFEMBG')
N01  C                   EVAL      FFINDX  = zhbgetvar('FFINDX')
     C                   EVAL      FFKLAS  = zhbgetvar('FFKLAS')
     C                   EVAL      FFSEDD  = zhbgetvar('FFSEDD')
     C                   EVAL      FFTRES  = zhbgetvar('FFTRES')
     C                   EVAL      WA      = zhbgetvar('FFFAKT')
     C                   EVAL      FFFAKT  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FFANTK')
     C                   EVAL      FFANTK  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('FFANTE')
     C                   EVAL      FFANTE  = c2n2(WA)
N01  C                   EVAL      FFENH   = zhbgetvar('FFENH')
     C                   EVAL      WA      = zhbgetvar('FFPOEN')
     C                   EVAL      FFPOEN  = c2n2(WA)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DOKUFVFK      KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FBN
     C                   KFLD                    LIN
     C                   KFLD                    LIN2
     C     DOKUFVK       KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FBN
     C                   KFLD                    LIN
N07  C     ZOKUFVFK      KLIST
N07  C                   KFLD                    UNIK
N07  C                   KFLD                    FBN
N07  C                   KFLD                    LIN
     C                   KFLD                    LIN2
N07  C     ZOKUFVK       KLIST
N07  C                   KFLD                    UNIK
N07  C                   KFLD                    FBN
N07  C                   KFLD                    LIN
      *
N01  C     UnNrKey       KLIST
N01  C                   KFLD                    FFUNNR
N01  C                   KFLD                    FFEMBG
N03  C                   KFLD                    FFINDX
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DOKUFVF
N07  C  N50              OPEN      ZOKUFVF2
N01  C  N50              OPEN      ADRUNNRF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     c                   if        lin2 = *zeros
     c                   exsr      NyLin
     c                   endif
N07  c     OPD           ifne      *zeros
N04  C     DOKUFVFK      CHAIN(N)  DOKUFVF                            55
N07  c                   else
N07  C     ZOKUFVFK      CHAIN(N)  ZOKUFVF2                           55
N07  c                   endif
N04  C                   if        *in55=*off or LIN = 0
N04  C                   eval      Error = 'Ugyldig linjenummer '
N04  C                             + '(tips gå ut /inn igjen)'
N04  C                   else
     C                   eval      Error = *blanks
N08  c                   exsr      TstError
N08  C                   if        Error = *blanks
     C                   EXSR      SRA
N08  C                   endif
N04  C                   endif
      *
     C     WMODE         WHENEQ    'U'
     C     WMODE         OREQ      'D'
N07  c     OPD           ifne      *zeros
N02  C     DOKUFVFK      CHAIN     DOKUFVF                            55
N07  c                   else
N07  C     ZOKUFVFK      CHAIN     ZOKUFVF2                           55
N07  c                   endif
     C                   IF        *IN55=*ON
N02  C                   eval      Error = 'ADR-linje finnes ikke.'
     C                   ELSE
     C                   eval      Error = *blanks
     c                   if        WMODE = 'U'
N08  c                   exsr      TstError
N08  C                   if        Error = *blanks
     C                   EXSR      SRU
N08  C                   endif
N08  C                   else
     C                   EXSR      SRD
N08  C                   endif
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
N01  C     UnNrKey       chain     ADRUNNRF                           33
     C                   CLEAR                   DOKUFVFR
N07  C                   CLEAR                   ZOKUFVFR
     C                   EVAL      XFFAVD=AVD
     C                   EVAL      XFFOPD=OPD
     C                   EVAL      XFFFBNR=FBN
     C                   EVAL      XFFLINR=LIN
N01  C                   EVAL      XFFLIN2=LIN2
     C                   EVAL      XFFUNNR=FFUNNR
     C                   EVAL      XFFEMBG=FFEMBG
N03  C                   EVAL      XFFINDX=FFINDX
s01  C                   EVAL      XFFKLAS=ADKLAS
s01  C                   EVAL      XFFSEDD=ADSEDD
s01  C                   EVAL      XFFTRES=ADTRES
s01  C                   EVAL      XFFFAKT=ADFAKT
     C                   EVAL      XFFANTK=FFANTK
     C                   EVAL      XFFANTE=FFANTE
     C                   EVAL      XFFENH=FFENH
N05  C                   EVAL      BigNum =ADFAKT*FFANTE
N05  C                   if        BigNum < 99999
N05  C                   EVAL      XFFPOEN=BigNum
N05  C                   else
N05  C                   EVAL      XFFPOEN=99999
N05  C                   endif
N07  c     OPD           IFNE      *zeros
     C                   WRITE     DOKUFVFR
N07  c                   else
N07  C                   EVAL      XFFREFF=REFF
N07  C                   EVAL      XFFUNIK=UNIK
N07  C                   WRITE     ZOKUFVFR
N07  c                   ENDIF

     C                   ENDSR
S02   *
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
      * DOKUFVF
      *  - farlig gods - skapes/endres dersom FFUNR ulik blank
N01  C     UnNrKey       chain     ADRUNNRF                           33
     C                   EVAL      XFFUNNR=FFUNNR
     C                   EVAL      XFFEMBG=FFEMBG
N03  C                   EVAL      XFFINDX=FFINDX
s01  C                   EVAL      XFFKLAS=ADKLAS
s01  C                   EVAL      XFFSEDD=ADSEDD
s01  C                   EVAL      XFFTRES=ADTRES
s01  C                   EVAL      XFFFAKT=ADFAKT
     C                   EVAL      XFFANTK=FFANTK
     C                   EVAL      XFFANTE=FFANTE
     C                   EVAL      XFFENH=FFENH
N05  C                   EVAL      BigNum =ADFAKT*FFANTE
N05  C                   if        BigNum < 99999
N05  C                   EVAL      XFFPOEN=BigNum
N05  C                   else
N05  C                   EVAL      XFFPOEN=99999
N05  C                   endif
N07  c     OPD           ifne      *zeros
     C                   UPDATE    DOKUFVFR
N07  c                   else
N07  C                   UPDATE    ZOKUFVFR
N07  c                   endif
      *
     C                   ENDSR
      *
      *______________________________________________________
      * Delete                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
N01   *
N07  c     OPD           ifne      *zeros
N01  C                   DELETE    DOKUFVFR
N07  c                   else
N07  C                   DELETE    ZOKUFVFR
N07  c                   endif
      *
     C                   ENDSR
      *
N08   *************************************************
N08  C     NyLin         BEGSR
N08   *************************************************
      * kan trygt satse på at det er mindre enn 99 linjer
     C                   eval      Lin2 = 99
N07  c     OPD           ifne      *zeros
N04  C     DOKUFVFK      SETGT     DOKUFVF
N04  C     DOKUFVK       READPE(N) DOKUFVF                                33
N07  c                   else
N07  C     ZOKUFVFK      SETGT     ZOKUFVF2
N07  C     ZOKUFVFK      READPE(N) ZOKUFVF2                               33
N07  c                   endif
     c                   if         *in33=*on
     c                   eval       Lin2 = 1
     c                   else
     c                   eval       Lin2 = XFFLIN2 + 1
     c                   endif

     C                   ENDSR
N08   *************************************************
N08  C     TstError      BEGSR
N08   *************************************************
     c                   move      *blanks       NyErr
      * Sjekk kombinasjon UnNr/Emb.grp (om det på gitt UNnr fins kun en emb-grp, autovelg )
     C     UnNrKey       chain     ADRUNNRF                           33
     C     *in33         ifeq      '1'
     C     FFUNNR        chain     ADRUNNRF                           33
     C     *in33         ifeq      '1'
     C                   eval      NyErr = 'Ugyldig UnNo'
     c                   exsr      AddErr
     c                   goto      UtUnnr
     c                   endif
      * kommer hit kun når gitt UNNR+EMBG IKKE finnes men det finnes minst en på gitt UNNR
     C     FFUNNR        reade     ADRUNNRF                               33
     C     *in33         ifeq      *on
     C                   eval      FFEMBG = ADEMBG
     C                   else
     C                   eval      NyErr = 'Ugyldig Emb.Grp for dette UnNo'
     c                   exsr      AddErr
     c                   goto      UtUnnr
     c                   endif
     c                   endif
      * OK UNnr/emb  - returner div verdier
     C                   EVAL      FFKLAS=ADKLAS
     C                   EVAL      FFSEDD=ADSEDD
     C                   EVAL      FFTRES=ADTRES
     C                   EVAL      FFFAKT=ADFAKT
      *
     c     UtUnnr        tag
     C                   if        FFANTK = 0
     C                   eval      NyErr = 'Antall kolli (som inneholder ADR) '
     c                             +'kan ikke være 0'
     c                   exsr      AddErr
     c                   endif
     C                   if        FFANTE = 0
     C                   eval      NyErr = 'Antall enheter (LTR/KG/GR) '
     c                             +'kan ikke være 0'
     c                   exsr      AddErr
     c                   endif
     C                   if        FFENH<>'KG' and FFENH<>'LTR' and FFENH<>'GR'
     C                   eval      NyErr = 'Enhetskode må være KG, LTR eller GR'
     c                   exsr      AddErr
     c                   endif
      * kun hvis alt ok - beregn poeng
     c                   if        FFPOEN=*zeros
     c                   EVAL      FFPOEN=FFFAKT * FFANTE
     c                   endif
      *
N08  C                   ENDSR
      *******************************************************
     C     AddErr        BEGSR
      *******************************************************
     c                   if        Error = *blanks
     c                   eval      Error = NyErr
     C                   else
     c                   eval      Error = %trim(Error) +'§'+NyErr
     C                   endif
     C                   ENDSR
