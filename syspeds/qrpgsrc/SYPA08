     FPALFIR    IF   E             DISK
     FPALLDS04  UF   E           K DISK
     FPAPA      UF   E           K DISK
     FPAPAL01   IF   E           K DISK
     F                                     RENAME(PAPAR:PAPAR2)
     FFIRM      IF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FHEDUMMY   IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD2R)
     FTELL      UF   E           K DISK
     FPRIS      IF   E           K DISK
     FHEADF     O  A E           K DISK
     FFAKT      O  A E           K DISK
     FFREETXT   O  A E           K DISK
     D                 DS
     D  XXFR1                  1     70
     D  XXNT                  11     17
     D  NT                    11     17
     D                                     DIM(7)
     D  XXFR1B                19     70
     D                 DS
     D  XXFR2                  1     70
     D  XXFR2B                19     70
     D                 DS
     D  QDATA                  1    109
     D  QDA01                  1      2
     D  QDA02                  3      6
     D  QDA03                  7     13
     D  QDA04                 14     14
     D  QDA05                 16     16
     D  QDA06                 17     20
     D  QDA07                 21     24
     D  QDA08                 25     26
     D  QDA09                 27     28
     D  QDA10                 29     29
     D  QDA11                 30     30
     D  QDA12                 31     37
     D  QDA13                 38     38
     D  QDA14                 39    100
     D  QDA15                101    101
     D  QDA16                102    102
     D  QDA17                103    103
     D  QDA18                104    109
     D                 DS
     D  IDAG                   1      8  0
     D  IDAGÅ                  1      4  0
     D  IDAGM                  5      6  0
     D  IDAGD                  7      8  0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUPPNR            8 0
     C                   PARM                    UUDATO            6 0
     C                   PARM                    W2KUNR            8 0
     C                   PARM                    W2KATE            1
     C                   PARM                    W2SPPM            1
     C                   PARM                    W2STPM            1
     C                   PARM                    W2ALFA            1
     C                   PARM                    MINST1            1
      *
     C                   MOVE      ' '           MINST1
     C                   MOVE      UUDATO        TOMDAT            6 0
     C     UUDATO        IFGT      501231
     C     UUDATO        ADD       19000000      UUDAT8            8 0
     C                   ELSE
     C     UUDATO        ADD       20000000      UUDAT8
     C                   END
      *
     C     PALKEY        KLIST
     C                   KFLD                    XXSTAT            1
     C                   KFLD                    XXPPNR
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    PPKUNR
     C     PRISK1        KLIST
     C                   KFLD                    PFBTAB
     C                   KFLD                    PFVK
     C                   KFLD                    PRINT
     C     PRISK2        KLIST
     C                   KFLD                    PFBTAB
     C                   KFLD                    PFVK
      * Dato for avregning i ÅÅMMDD format
     C                   MOVE      UDAY          IDAGD
     C                   MOVE      UMONTH        IDAGM
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          IDAGÅ
     C                   ELSE
     C     UYEAR         ADD       2000          IDAGÅ
     C                   END
      * Hent datoformat/firmakode
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *Konverter til og med dato til inhouseformat
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C                   PARM                    TOMDAT
      *
      * Hent evt verdier for std. pristab/gebyrkode
     C     1             CHAIN     PALFIR                             33
      * Hent std verdier for oppdragsgenerering (finnes ikke->UT)
     C     PFOAVD        CHAIN     HEDUMMY                            33
     C     *IN33         IFEQ      '0'
      * Skriv - Kun for en / flere utfra utvalg???
     C     UUPPNR        IFNE      *ZEROS
     C                   MOVE      UUPPNR        XXPPNR            8 0
     C     XXPPNR        CHAIN(N)  PAPA                               33
     C                   EXSR      ENPART
     C                   ELSE
     C                   EXSR      FLPART
     C                   END
     C                   END
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *
      *
      ********************************************************
     C     ENPART        BEGSR
      ********************************************************
      * Finn purret saldo fram t.o.m dato basert på detaljposter
      *    STATUS P=Purret (men ennå ikke avregnet)
     C                   MOVE      'P'           XXSTAT
     C                   MOVE      *ZEROS        TOTANT            5 0
     C                   EXSR      DETA
      * Positiv saldo - lag avregn.oppdrag
     C     TOTANT        IFGT      *ZEROS
     C                   MOVE      *ZEROS        TOTANT            5 0
      * Lag avregningsoppdrag/merk detaljlinjer med status/oppd.nr
     C                   EXSR      DETB
      * Oppdater "Saldo purret"/"Saldo total"/"ant avregnet" i partsreg
     C     XXPPNR        CHAIN     PAPA                               33
     C                   SUB       TOTANT        PPSALP
     C                   SUB       TOTANT        PPSALT
     C                   ADD       TOTANT        PPAVRE
     C                   MOVE      IDAG          PPSADT
     C                   UPDATE    PAPAR
      * Minst en er avregnet. (RETURPARAMETER)
     C                   MOVE      'X'           MINST1
     C                   END
      *
     C                   ENDSR
      *
      *
      ********************************************************
     C     DETA          BEGSR
      ********************************************************
     C     PALKEY        SETLL     PALLDS04
     C     PALKEY        READE(N)  PALLDS04                               33
     C     *IN33         DOWEQ     '0'
     C     PAKVDT        IFGT      UUDAT8
     C                   GOTO      UTDETA
     C                   END
     C                   ADD       PAANT         TOTANT            5 0
     C     PALKEY        READE(N)  PALLDS04                               33
     C                   END
     C     UTDETA        ENDSR
      *
      ********************************************************
     C     DETB          BEGSR
      ********************************************************
      *
     C     PFOAVD        CHAIN     TELL                               33
     C                   MOVE      TEOPDN        HEOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
      *
      * Merk detaljlinjer med status/oppdragsnr
     C     PALKEY        SETLL     PALLDS04
     C     PALKEY        READE     PALLDS04                               33
     C     *IN33         DOWEQ     '0'
     C     PAKVDT        IFGT      UUDAT8
     C                   GOTO      UTDETB
     C                   END
     C                   ADD       PAANT         TOTANT            5 0
     C                   MOVE      'O'           PASTAT
     C                   MOVE      HEAVD         PAAVD
     C                   MOVE      HEOPD         PAOPD
     C                   UPDATE    PALLDR
     C     PALKEY        READE     PALLDS04                               33
     C                   END
      *
     C     UTDETB        TAG
      *
      * Oppdater Kunde/antall/pris i avr.oppd
     C     KUNKEY        CHAIN(N)  CUNDL01                            33
     C                   Z-ADD     KUNDNR        HEKNK
     C                   Z-ADD     KUNDNR        HEKNKF
     C                   MOVE      KNAVN         HENAK
     C                   MOVE      ADR1          HEADK1
     C                   MOVE      ADR2          HEADK2
     C                   MOVEL     POSTNR        HEADK3
     C                   MOVE      ADR3          HEADK3
     C*
     C                   MOVE      IDAG          HEDTOP
     C                   MOVE      IDAG          HEDTR
     C*
     C                   MOVE      TOMDAT        HEVS2
     C                   Z-ADD     TOTANT        HENT
     C                   WRITE     HEADFR
     C*
     C* Skriv fritekst (+ 2 blanklinjer før/etter)
     C                   EXSR      FREETX
     C*
      *Hvis tabell/gebyrkode er oppgitt kalkulere pris umiddelbart
     C     PFVK          IFNE      *BLANKS
     C     PFBTAB        ANDNE     *BLANKS
     C                   EXSR      FAKLIN
      * ellers lager SYFI10X fakt.forslag basert på brutto/avtale-syst.
     C                   ELSE
     C                   EXSR      SFI10X
     C                   END
     C                   ENDSR
      *
     C***********************************************
     C     FAKLIN        BEGSR
     C***********************************************
      * Momskode settes fast til "J"
      * Er en uenig i dette må en beytte standardoppdrag/"Hvem bel.hva"
      * evt kindeavtale for å ha full styring
     C                   Z-ADD     HENT          PRINT
     C     PRISK1        SETLL     PRIS
     C     PRISK2        READE     PRIS                                   33
     C     *IN33         IFEQ      '0'
     C                   CLEAR                   FAKTR
     C                   MOVE      'B'           FAKDA
     C                   MOVE      HEAVD         FAAVD
     C                   MOVE      HEOPD         FAOPD
     C                   MOVE      'K'           FASK
     C                   MOVE      PFVK          FAVK
     C                   MOVE      FICURR        FAVAL
     C                   MOVE      'J'           FAKDM
     C     PRBELØ        MULT      HENT          FABELN
     C                   Z-ADD     FABELN        FABELV
     C                   EXSR      DECI
     C                   Z-ADD     KUNDNR        FAKUNR
     C                   CALL      'SYGE06'
     C                   PARM                    FAAVD
     C                   PARM                    FAOPD
     C                   PARM                    FALI
     C                   WRITE     FAKTR
      * Juster saldo ufakturert
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              SUB       FABELN        SYSALU
     C  N33              UPDATE    KUNDR
     C                   END
      *
     C                   ENDSR
      ***********************************************
     C     FREETX        BEGSR
      ***********************************************
     C                   MOVE      HEAVD         FRTAVD
     C                   MOVE      HEOPD         FRTOPD
     C                   MOVE      HEDTR         FRTDT
     C                   MOVE      *ZEROS        FRTFAK
     C                   MOVE      'K'           FRTKOD
     C                   Z-ADD     1             FRTLI
     C                   MOVE      *BLANKS       FRTTXT
     C                   WRITE     FREETXTR
     C*
     C                   ADD       1             FRTLI
     C                   WRITE     FREETXTR
     C* Antall samt HEVS1
     C                   MOVE      HENT          XXNT
     C     1             DO        7             NN                1 0
     C     NT(NN)        IFEQ      '0'
     C                   MOVE      ' '           NT(NN)
     C                   ELSE
     C                   GOTO      UTNT
     C                   END
     C                   END
     C     UTNT          TAG
     C                   MOVEL     HEVS1         XXFR1B
     C                   MOVEL     XXFR1         FRTTXT
     C                   ADD       1             FRTLI
     C                   WRITE     FREETXTR
     C* HEVS2
     C                   MOVE      *BLANKS       FRTTXT
     C                   MOVEL     HEVS2         XXFR2B
     C                   MOVEL     XXFR2         FRTTXT
     C                   ADD       1             FRTLI
     C                   WRITE     FREETXTR
     C* 2 linjer blank
     C                   MOVE      *BLANKS       FRTTXT
     C                   ADD       1             FRTLI
     C                   WRITE     FREETXTR
     C*
     C                   MOVE      *BLANKS       FRTTXT
     C                   ADD       1             FRTLI
     C                   WRITE     FREETXTR
      *
     C                   ENDSR
      *
      ***********************************************
     C     DECI          BEGSR
      ***********************************************
     C     FIDECI        IFEQ      0
     C                   Z-ADD(H)  FABELN        FABEL0           11 0
     C                   Z-ADD     FABEL0        FABELN
     C                   ELSE
     C     FIDECI        IFEQ      1
     C                   Z-ADD(H)  FABELN        FABEL1           12 1
     C                   Z-ADD     FABEL1        FABELN
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SFI10X        BEGSR
     C***********************************************
      *
     C                   MOVEL     FIFIRM        QDA01
     C                   MOVEL     HEAVD         QDA02
     C                   MOVEL     HEOPD         QDA03
     C                   MOVEL     'NY'          QDA04
     C                   MOVEL     ' '           QDA05
     C                   MOVE      *ZEROS        QDA06
     C                   MOVE      *ZEROS        QDA07
     C                   MOVE      *ZEROS        QDA08
     C                   MOVE      *ZEROS        QDA09
     C                   MOVEL     ' '           QDA10
     C                   MOVEL     ' '           QDA11
     C                   MOVE      *ZEROS        QDA12
     C                   MOVEL     ' '           QDA13
     C                   MOVE      *BLANKS       QDA14
     C                   MOVE      *BLANKS       QDA15
     C                   MOVEL     '3'           QDA16
     C                   MOVEL     'A'           QDA17
     C                   MOVE      *BLANKS       QDA18
     C                   MOVEL     'SYFI10XQ'    QDTAQ            10
     C                   MOVEL     '*LIBL'       QLIB             10
     C                   Z-ADD     109           QRLEN             5 0
     C                   CALL      'QSNDDTAQ'
     C                   PARM                    QDTAQ
     C                   PARM                    QLIB
     C                   PARM                    QRLEN
     C                   PARM                    QDATA
      *
     C                   ENDSR
      *
     C***********************************************
     C     FLPART        BEGSR
     C***********************************************
     C* FLERE PARTER - GÅ GJENNOM ALLE EXKLUDER ETTER UVALG I BILDET
     C* 51 ON ALFA SORTERING/OFF PARTSNR SORTERING
     C* (LAR PARAMETRESTYRT SORTERING LIGGE I FALL DET SENERE BLIR
     C*  AKTUELT Å SKRIVE NOE KVITTERINGSLISTE. ELLERS ER DET UTEN BET)
     C     W2ALFA        COMP      'X'                                    51
     C  N51*LOVAL        SETLL     PAPA
     C   51*LOVAL        SETLL     PAPAL01
     C*
     C  N51              READ      PAPA                                   94
     C   51              READ      PAPAL01                                94
      *
     C     *IN94         DOWEQ     '0'
     C     NESTE         TAG
     C                   EXSR      TSTEXC
     C     EXCLUD        IFEQ      'J'
     C  N51              READ      PAPA                                   94
     C   51              READ      PAPAL01                                94
     C  N94              GOTO      NESTE
     C   94              GOTO      UTFLER
     C                   END
     C* SKAL VÆRE MED...
     C                   MOVE      PPNR          XXPPNR
     C                   EXSR      ENPART
     C  N51              READ      PAPA                                   94
     C   51              READ      PAPAL01                                94
     C  N94              END
     C*
     C     UTFLER        ENDSR
     C***********************************************
     C     TSTEXC        BEGSR
     C***********************************************
     C                   MOVE      'N'           EXCLUD            1
     C*
     C     W2KUNR        IFNE      *ZEROS
     C     W2KUNR        ANDNE     PPKUNR
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2KATE        IFNE      ' '
     C     W2KATE        ANDNE     PPKATE
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2SPPM        IFEQ      '+'
     C     PPSALP        ANDLE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2SPPM        IFEQ      '-'
     C     PPSALP        ANDGE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2SPPM        IFEQ      '0'
     C     PPSALP        ANDNE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2STPM        IFEQ      '+'
     C     PPSALT        ANDLE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2STPM        IFEQ      '-'
     C     PPSALT        ANDGE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C     W2STPM        IFEQ      '0'
     C     PPSALT        ANDNE     0
     C                   MOVE      'J'           EXCLUD
     C                   GOTO      UTEXCL
     C                   END
     C*
     C     UTEXCL        ENDSR
