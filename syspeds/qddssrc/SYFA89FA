     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  4VALUES(' ' '5')
     A            PKAVTN         1A  O  6 11
     A            PKGEBY         3A  O  6 16
     A            KGENOT        20A  O  6 22
     A            PKTNR          1A  O  6 47
     A            PKMVA          1A  O  6 55
     A            PKLNR          4  0H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N43N44                            ROLLUP(25)
     A  41N43N44                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A            MSG101        45A  O  1  2MSGID(DIV1312 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG102        15A  O  2  2MSGID(DIV1164 SYMSGF)
     A            UFR01          3A  O  2 18DSPATR(UL)
     A            MSG103        15A  O  2 32MSGID(DIV1165 SYMSGF)
     A            UOT01          2A  O  2 48DSPATR(UL)
     A            MSG104        30A  O  3  2MSGID(DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        20A  O  4  2MSGID(DIV1311 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG106        78A  O  5  2MSGID(DIV1166 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN                      CSRLOC(LINNBR POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF08(08 'SEARCH')
     A                                      CF12(12 'CANCEL')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A            MSG201        18A  O  3  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 62MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSG205        78A  O  4  2MSGID(DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        12A  O  1 18MSGID(DIV1167 SYMSGF)
     A            MSG402        15A  O  2  2MSGID(DIV1168 SYMSGF)
     A            W2AVTN         1A  I  2 19DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A            W2GEBY         3A  I  2 24DSPATR(UL)
     A                                      COLOR(YLW)
     A            MSG403        18A  O  3  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF08(08 'SEARCH')
     A                                      CF12(12 'CANCEL')
     A            MSG601        45A  O  1  2MSGID(DIV1312 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG602        15A  O  2  2MSGID(DIV1164 SYMSGF)
     A            UFR01          3A  O  2 18DSPATR(UL)
     A            MSG603        15A  O  2 32MSGID(DIV1165 SYMSGF)
     A            UOT01          2A  O  2 48DSPATR(UL)
     A            MSG604        30A  O  3  2MSGID(DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG605        20A  O  4  2MSGID(DIV1311 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG606        78A  O  5  2MSGID(DIV1166 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG607        18A  O 23  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG608        18A  O 23 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG609        18A  O 23 42MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG610        18A  O 23 62MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSG611        78A  O 24  2MSGID(DIV0012 SYMSGF)
     A                                      COLOR(WHT)
