      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/INQBOAT MODULE(*LIBL/INQBOAT *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8XXX * 01 SH  PTF8 Ikke ta med båter med landkode = blank
ODDEV *ODDEV   PTF9 ODDEVEN-logikk
P9XXX * 02 YBC PTF9 WSVARNV: Variable navn som mottar verdi
      *             WSFORMS: Formnavn som mottar verdi
      *             WSREFR : J/Y betyr program for mottak av verdi skal refreshes
P8XXX * 03 SH  PTF9 kun båtgruppe hvis med i parameter
P9XXX * 04 YBC PTF9 AGENT
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
N04  FBRPARF    IF   E           K DISK    USROPN
     FcdboatL2  IF   E           K DISK    USROPN
     Fbatgr     IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
OddEvD OddEven         s             10
      * Client input variables
N03  D wsboagr         s              7
     D SOKLK           s              2
     D SOKNVN          s             30    varying
     D SOKNVN2         s             30
     D USER            s             10
     D VARLK           s             10
     D VARKOD          s             10
N02  D WSVARNV         s             20
N02  D WSFORMS         s             20
N02  D WSREFR          s              1
N02  D REFRESH         s             50
     D MaxV            s              5
     D MaxVis          s              5  0
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
N03  C                   eval      wsboagr = zhbgetvar('wsboagr')
     C                   eval      SOKLK   = zhbgetvar('SOKLK')
     C                   eval      SOKLK   = uppify(SOKLK)
     C                   eval      SOKNVN  = zhbgetvar('SOKNVN')
     C                   eval      SOKNVN  = uppify(SOKNVN)
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     C                   eval      VARLK   = zhbgetvar('VARLK')
     C                   eval      VARKOD  = zhbgetvar('VARKOD')
N02  C                   eval      WSVARNV = zhbgetvar('WSVARNV')
N02  C                   IF        WSVARNV = *BLANKS
N02  C                   EVAL      WSVARNV = 'wskdb'
N02  C                   END
N02  C                   eval      WSFORMS = zhbgetvar('WSFORMS')
N02  C                   IF        WSFORMS = *BLANKS
N02  C                   EVAL      WSFORMS = 'forms[0]'
N02  C                   END
N02  C                   eval      WSREFR  = zhbgetvar('WSREFR')
N02  C                   IF        ((((WSREFR = 'J') OR
N02  C                                (WSREFR = 'j')) OR
N02  C                                (WSREFR = 'Y')) OR
N02  C                                (WSREFR = 'y'))
N02  C                   eval      REFRESH = 'self.opener.document.'
N02  C                             + %trim(WSFORMS)
N02  C                             + '.submit();'
N02  C                   END
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      USER = zhbgetvar('USER')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     cdboatL2
n03  C                   CLOSE     batgr
N04  C                   CLOSE     BRPARF
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C*                  callp     gethtml('HTMLSRC':
     C*                            '*LIBL':
     C*                            'INQBOAT':'/CGITAG/')
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'INQBOAT'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('SOKLK':SOKLK)
     C                   callp     updHTMLvar('SOKNVN':SOKNVN)
     C                   callp     updHTMLvar('VARLK':VARLK)
     C                   callp     updHTMLvar('VARKOD':VARKOD)
N02  C                   callp     updHTMLvar('wsvarnv':WSVARNV)
N02  C                   callp     updHTMLvar('wsforms':WSFORMS)
N02  C                   callp     updHTMLvar('wsrefr':WSREFR)
N02  C                   callp     updHTMLvar('refresh':REFRESH)
N03  C                   callp     updHTMLvar('wsboagr':wsboagr)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     c                   Move      *zeros        AntVist           5 0
     c                   eval      SOKNVN2   = SOKNVN
     C     SOKNVN        IFEQ      *BLANKS
     C                   GOTO      UTLES
     C                   END
      *
N04  C                   IF        XAGENT = 'J'
N04  C     *LOVAL        SETLL     CDBOATL2
N04  C                   READ      CDBOATL2                               33
N04  C                   ELSE
     C     BOATKEY       setll     cdboatl2
     C     BOATKEY       reade     cdboatl2                               33
N04  C                   END
     C     *in33         DOWEQ     *OFF
N01  C     SOKNVN        SCAN      CBNAB                                  34
     c     *IN34         CaBEQ     *OFF          NESTE
     C                   exsr      exclude
     c     EXCL          IFEQ      'N'
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('CBKDB':CBKDB)
     C                   callp     updHTMLvar('CBNAB':CBNAB)
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C                   END
     C     NESTE         TAG
N04  C                   IF        XAGENT = 'J'
N04  C                   READ      CDBOATL2                               33
N04  C                   ELSE
     C     BOATKEY       reade     cdboatl2                               33
N04  C                   END
     C                   end
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
N04  C                   IF        XAGENT = 'J'
N04  C                   IF        ((CBLK = *BLANKS) OR (CBKN = 0))
N04  C                   MOVE      'J'           EXCL
N04  C                   ELSE
     C                   MOVE      'N'           EXCL              1
N04  C                   END
N04  C                   ELSE
     c     grukey        klist
     c                   kfld                    wsboagr
     c                   kfld                    CBKDB
     C                   Move      'N'           EXCL              1
      * diverse tester - om ikke bestått hopp ut (med J i EXCL)
      * Posten skal med på liste
n03  c     wsboagr       ifne      *blanks
n03  c     grukey        chain     batgr                              34
n03  C   34              Move      'J'           EXCL              1
n03  c                   end
n01  c     CBLK          ifeq      '  '
n01  c                   move      'J'           EXCL
n01  c                   end
N04  C                   END
      *
     C     UTEXCL        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      cdboatl2
n03  C                   OPEN      batgr
N04  C                   OPEN      BRPARF
      * Key for CDBOAT  KUNDENR
     C     BOATKEY       KLIST
     C                   KFLD                    BIKUNR
N04  C     BRPARFK       klist
N04  C                   kfld                    USER
N04  C                   kfld                    PAKUNR
N04  C                   kfld                    PAID
n03  c     wsboagr       ifne      *blanks
n03  c                   exsr      chgusr
n03  c                   end
N04  C                   MOVE      'SSAGT'       PAID
N04  C     BRPARFK       CHAIN     BRPARF                             33
N04  C  N33              MOVE      'J'           XAGENT            1
      *
     C                   endsr
n03   ****************************************************************************
n03  C     chgusr        begsr
n03   ****************************************************************************
n03   *bytter evenbtuelt bikunr **************************************************
n03   *
N03  C     BATGRKE2      klist
N03  C                   kfld                    wsboagr
N03  C     BATGRKE2      CHAIN     BATGR                              34
n03  c     *in(34)       ifne      '1'
n03  c                   z-add     BAKUNR        bikunr
n03  c                   end
n03  c                   endsr
