     FHEADF     IF   E           K DISK
     FDOKEF     IF   E           K DISK
     FDOKEM     IF   E           K DISK
     FSYAV02F   O  A E             DISK
      *__________________
      * DS for AWB-nummer
      *""""""""""""""""""
     D                 DS
     D  HEGN                   1     15
     D  WEFLY                  5      7  0
     D  HEFLY1                 5      5
     D  HEFLY2                 6      6
     D  HEFLY3                 7      7
      *__________________
      * DS for date
      *""""""""""""""""""
      *_____________
      * DS for DOKEM
      *"""""""""""""
     D                 DS
NRFA D* HERFA                  1     15
NRFA D  HERFA                  1     35
NRFA D  HERFA15                1     15
     D  WFSD                   1      3
     D  WFLNR                  4      6  0
     D  WFLNRA                 4      6
      *_____________
      * DS for HEADF
      *"""""""""""""
     D                 DS
     D  WEAVD                  1      4  0
      *//////////////
      *  main logic /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initialize                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Define Workfields
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    HEOPD         WEOPD
     C     *LIKE         DEFINE    DFRI          WFRI
     C     *LIKE         DEFINE    DFRI          WFRIC
      *__________________
      * Definiere keys
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WEAVD
     C                   KFLD                    WEOPD
     C     KEYB          KLIST
     C                   KFLD                    WFRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KEYC          KLIST
     C                   KFLD                    WFRIC
     C                   KFLD                    WFSD
     C                   KFLD                    WFLNR
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    ZAVDF             4 0
     C                   PARM                    ZAVDT             4 0
     C                   PARM                    ZFLYF             3 0
     C                   PARM                    ZFLYT             3 0
     C                   PARM                    ZDTF              6 0
     C                   PARM                    ZDTT              6 0
     C                   PARM      *IN03         WIN03
      *_____________
      * Init of fields
      *"""""""""""""
     C                   MOVE      ZDTF          ZDTF8             8 0
     C     ZDTF          IFGT      501231
     C                   MOVEL     '19'          ZDTF8
     C                   ELSE
     C                   MOVEL     '20'          ZDTF8
     C                   END
     C                   MOVE      ZDTT          ZDTT8             8 0
     C     ZDTT          IFGT      501231
     C                   MOVEL     '19'          ZDTT8
     C                   ELSE
     C                   MOVEL     '20'          ZDTT8
     C                   END
     C                   Z-ADD     ZAVDF         WEAVD
     C                   MOVE      *ZEROS        WEOPD
     C                   MOVE      'F'           WFRI
     C                   MOVE      'M'           WFRIC
     C                   MOVE      *ZEROS        WFLNR
     C                   MOVE      *ZEROS        WFSD
     C                   MOVE      '1'           *IN03
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Read HEADF - write valid AWBs to workfile            SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Setll
     C     KEYA          SETLL     HEADF
     C                   READ      HEADF                                  51
     C  N51HEAVD         COMP      ZAVDT                              51
     C     *IN51         DOWEQ     '0'
     C                   MOVEL     HESAML        HERFA
      * Date
     C     HEDTOP        IFGE      ZDTF8
     C     HEDTOP        ANDLE     ZDTT8
      * first second third letter in Airline number
     C     HEFLY1        ANDGE     '0'
     C     HEFLY2        ANDGE     '0'
     C     HEFLY3        ANDGE     '0'
      * Airline
     C     WEFLY         ANDGE     ZFLYF
     C     WEFLY         ANDLE     ZFLYT
      * Must have "System code" "D" (= Air export)
     C     HEUR          ANDEQ     'D'
      * Status  on the order
     C     HEST          IFEQ      'S'
     C                   GOTO      SRB1
     C                   END
      * There must be an AWB, single or master (in DOKEF eller DOKEM)
     C     KEYB          CHAIN     DOKEF                              52
      * Must have the  status of "P" (printed)
     C  N52DFST          IFNE      'P'
     C                   GOTO      SRB1
     C                   END
     C   52WFLNRA        IFEQ      *BLANKS
     C                   Z-ADD     0             WFLNR
     C                   END
     C   52KEYC          CHAIN     DOKEM                              52
      * Must have the  status of "P" (printed)
     C  N52DFST          IFNE      'P'
     C                   GOTO      SRB1
     C                   END
     C     *IN52         IFEQ      '0'
      * write to workfile
     C                   MOVE      '0'           *IN03
     C                   Z-ADD     WEFLY         HEFLY
     C                   WRITE     SYAV02FF
      *
     C                   END
     C                   END
     C     SRB1          TAG
      * read next record
     C                   READ      HEADF                                  51
     C  N51HEAVD         COMP      ZAVDT                              51
     C  N51              END
      *
     C                   ENDSR
