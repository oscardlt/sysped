      *
      ***********************************************************
      *** PROGRAM SKAL RETTE FORHOLDSTALL PGA. DISSE ER ENDRET***
      ***********************************************************
      *
     FLLAGJL01  IF   E           K DISK
     FLLAGJD01  UF   E           K DISK
     FLLHEAD    UF   E           K DISK
     FLLLINE    UF   E           K DISK
      *
     C                   EXSR      INIT
      *
      * TEST RUND
     C     LAGD1K        SETLL     LLAGJD01
     C     LAGD1K        READE(N)  LLAGJD01                               33
     C     *IN33         DOWEQ     '0'                                          1<-B
     C     VDLDAT        IFEQ      99999999                                      2<-B
      * BEREGN NYE ENHETER
     C                   EXSR      BEREGN
      *
      * TEST LAVESTE SALGSENHET
     C     UMIN          IFGT      0                                              3<-B
     C     XWNT0         ANDNE     0
     C                   Z-ADD     1             UFLAGG
     C                   GOTO      SLUTT
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
     C     LAGD1K        READE(N)  LLAGJD01                               33
     C  N33              ENDDO                                                  1<-E
      *
     C     UFLAGG        CABEQ     1             SLUTT
      *
      * RETTE RUND
     C     LAGD1K        SETLL     LLAGJD01
     C     LAGD1K        READE     LLAGJD01                               33
     C     *IN33         DOWEQ     '0'                                          1<-B
     C     VDLDAT        IFEQ      99999999                                      2<-B
      * BEREGN NYE ENHETER
     C                   EXSR      BEREGN
      *
      * RETTE LINJE/HEAD I PAKKSEDDEL OG LINJE I LAGERBEHOLDNING
     C     LLINEK        CHAIN     LLLINE                             34
     C     VDMNL         CHAIN     LLHEAD                             35
      * TREK GAMLE ANTALL FRA HEAD OG LINJER I PAKKSEDDEL
     C                   SUB       LLWNTL        LHANTL
     C                   SUB       LLWNT1        LHANT1
     C                   SUB       LLWNT2        LHANT2
     C                   SUB       LLWNT3        LHANT3
     C                   SUB       VDSAE3        LLWNT3
     C                   SUB       VDSAE2        LLWNT2
     C                   SUB       VDSAE1        LLWNT1
     C                   SUB       VDSAE0        LLWNTL
      * BEREGN GAMMEL VEKT OG KUBIKK TIL LINJE
     C     VDSAE0        MULT      VAVKT0        XXWVEK
     C     VDSAE1        MULT      VAVKT1        XWVEK
     C                   ADD       XWVEK         XXWVEK
     C     VDSAE2        MULT      VAVKT2        XWVEK
     C                   ADD       XWVEK         XXWVEK
     C     VDSAE3        MULT      VAVKT3        XWVEK
     C                   ADD       XWVEK         XXWVEK
     C     VDSAE0        MULT      VAKUB0        XXWKUB
     C     VDSAE1        MULT      VAKUB1        XWKUB
     C                   ADD       XWKUB         XXWKUB
     C     VDSAE2        MULT      VAKUB2        XWKUB
     C                   ADD       XWKUB         XXWKUB
     C     VDSAE3        MULT      VAKUB3        XWKUB
     C                   ADD       XWKUB         XXWKUB
      * TREK VEKT OG KUBIKK FRA HEAD OG LINJE I PAKKSEDDEL
     C                   SUB       XXWVEK        LHWVEK
     C                   SUB       XXWKUB        LHWKUB
     C                   SUB       XXWVEK        LLWVEK
     C                   SUB       XXWKUB        LLWKUB
      *
      * OPDATER NYE ENHETER PÅ LINJER I LAGERBEHOLDNING
     C                   MOVE      XWNT3         VDSAE3
     C                   MOVE      XWNT2         VDSAE2
     C                   MOVE      XWNT1         VDSAE1
     C                   MOVE      XWNT0         VDSAE0
      *
      * LEGG NYE ENHETER TIL HEAD OG LINJE I PAKKSEDDEL
     C                   ADD       XWNT3         LLWNT3
     C                   ADD       XWNT2         LLWNT2
     C                   ADD       XWNT1         LLWNT1
     C                   ADD       XWNT0         LLWNTL
     C                   ADD       LLWNTL        LHANTL
     C                   ADD       LLWNT1        LHANT1
     C                   ADD       LLWNT2        LHANT2
     C                   ADD       LLWNT3        LHANT3
      * BEREGN NY VEKT OG KUBIKK TIL LINJE
     C     XWNT0         MULT      UVKT0         XXWVEK
     C     XWNT1         MULT      UVKT1         XWVEK
     C                   ADD       XWVEK         XXWVEK
     C     XWNT2         MULT      UVKT2         XWVEK
     C                   ADD       XWVEK         XXWVEK
     C     XWNT3         MULT      UVKT3         XWVEK
     C                   ADD       XWVEK         XXWVEK
     C     XWNT0         MULT      UKUB0         XXWKUB
     C     XWNT1         MULT      UKUB1         XWKUB
     C                   ADD       XWKUB         XXWKUB
     C     XWNT2         MULT      UKUB2         XWKUB
     C                   ADD       XWKUB         XXWKUB
     C     XWNT3         MULT      UKUB3         XWKUB
     C                   ADD       XWKUB         XXWKUB
      * LEGG NYE VEKT OG KUBIKK TIL HEAD OG LINJE I PAKKSEDDEL
     C                   ADD       XXWVEK        LHWVEK
     C                   ADD       XXWKUB        LHWKUB
     C                   ADD       XXWVEK        LLWVEK
     C                   ADD       XXWKUB        LLWKUB
     C  N35              UPDATE    LLHEADR
     C  N34              UPDATE    LLLINER
      *
     C                   END                                                     2<-E
     C                   UPDATE    LLAGJDR
     C     LAGD1K        READE     LLAGJD01                               33
     C  N33              ENDDO
      *                                                    1<-E
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     BEREGN        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        XWNT3
     C                   MOVE      *ZEROS        XWNT2
     C                   MOVE      *ZEROS        XWNT1
     C                   MOVE      VDLANT        XWNT0
     C     UAN03         IFNE      0
     C     XWNT0         DIV       UAN03         XWNT3
     C                   MVR                     XWNT0
     C                   END
     C     UAN02         IFNE      0
     C     UMIN          ANDLE     2
     C     XWNT0         DIV       UAN02         XWNT2
     C                   MVR                     XWNT0
     C                   END
     C     UAN01         IFNE      0
     C     UMIN          ANDLE     1
     C     XWNT0         DIV       UAN01         XWNT1
     C                   MVR                     XWNT0
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    VDGRUP        UGRP
     C     *LIKE         DEFINE    VDKJED        UPNR
     C     *LIKE         DEFINE    VDMN          UMN
     C     *LIKE         DEFINE    LLWNT0        XWNT0
     C     *LIKE         DEFINE    LLWNT1        XWNT1
     C     *LIKE         DEFINE    LLWNT2        XWNT2
     C     *LIKE         DEFINE    LLWNT3        XWNT3
     C     *LIKE         DEFINE    LLWVEK        XWVEK
     C     *LIKE         DEFINE    LLWKUB        XWKUB
     C     *LIKE         DEFINE    LLWVEK        XXWVEK
     C     *LIKE         DEFINE    LLWKUB        XXWKUB
     C     LAGD1K        KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    UMN
     C     LLINEK        KLIST
     C                   KFLD                    VDMNL
     C                   KFLD                    VDVNL
     C     LAGJ1K        KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    UMN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGRP
     C                   PARM                    UPNR
     C                   PARM                    UMN
     C                   PARM                    UVKT0             7 2
     C                   PARM                    UKUB0             7 4
     C                   PARM                    UVKT1             7 2
     C                   PARM                    UKUB1             7 4
     C                   PARM                    UVKT2             7 2
     C                   PARM                    UKUB2             7 4
     C                   PARM                    UVKT3             7 2
     C                   PARM                    UKUB3             7 4
     C                   PARM                    UAN01             5 0
     C                   PARM                    UAN02             5 0
     C                   PARM                    UAN12             5 0
     C                   PARM                    UAN03             5 0
     C                   PARM                    UAN23             5 0
     C                   PARM                    UMIN              1 0
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      *ZEROS        UFLAGG
     C     LAGD1K        CHAIN     LLAGJL01                           33
      *
     C                   ENDSR
      *
