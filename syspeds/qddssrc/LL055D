     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            GKPNR          8  0O  5  5EDTCDE(4)
     A            KNAVN         30A  O  5 14
     A            WSFN           6  0O  5 45EDTCDE(4)
     A            WSFDT          6  0O  5 52EDTCDE(Y)
     A            WS≈≈MU         4  0O  5 61EDTWRD('0  .  ')
     A            WSMMUU         1A  O  5 67
     A            WSSTAT         3A  O  5 69
     A            WSAVD          4  0H
     A            WSOPD          7  0O  5 73EDTCDE(4)
     A            WSSG           3   H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL055R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1621 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(12 YBC1622 SYMSGF)
     A            UAVD           4  0O  2 15EDTCDE(4)
     A                                      COLOR(BLU)
     A            KOANVN        33   O  2 21COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(40 YBC1623 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1624 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL055R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1621 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(12 YBC1622 SYMSGF)
     A            UAVD           4  0O  2 15EDTCDE(4)
     A                                      COLOR(BLU)
     A            KOANVN        33   O  2 21COLOR(BLU)
     A                                  3  2MSGCON(40 YBC1623 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1624 SYMSGF)
     A                                  6  2MSGCON(78 YBC1625 SYMSGF)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
