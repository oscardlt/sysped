********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF12 endringer
********************************************************************
     FSYTR04FK  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(STATDS)
     FFIRM      IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FUNITAKM   UF A E           K DISK
     D                 DS
     D  KOANVN                 1     33
     D  AVDNVN                 1     20
     D  SLETT                  1      8
     D STATDS          DS
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      ***************************************************************
     C     INIT          BEGSR
      ***************************************************************
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *ENTRY        PLIST
     C                   PARM                    UNTRAN            8 0
     C                   PARM                    UNBILN           10
     C                   PARM                    UNAVN            30
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEY           KLIST
     C                   KFLD                    UNTRAN
     C                   KFLD                    UNBILN
     C     KEY2          KLIST
     C                   KFLD                    UNTRAN
     C                   KFLD                    UNBILN
     C                   KFLD                    UNSAVD
     C     KEY2W         KLIST
     C                   KFLD                    UNTRAN
     C                   KFLD                    UNBILN
     C                   KFLD                    WSSAVD
     C     AVDKEY        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WSSAVD
     C                   MOVE      'A'           KOAUNI
      *
     C                   READ      FIRM                                   55
      *
     C                   ENDSR
      *
      *
      ***************************************************************
     C     HOVED         BEGSR
      ***************************************************************
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     KEY           SETLL     UNITAKM
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C     KEY           READE(N)  UNITAKM                                94
      *
     C     *IN94         DOWEQ     '0'
      *
     C     AVDKEY        CHAIN     KODTA                              33
     C   33              MOVE      *BLANKS       KOANVN
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
      *
     C     KEY           READE(N)  UNITAKM                                94
     C  N94              ENDDO
      *
     C     UT            TAG
      *
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
s01  C*                  MOVE      'SHU1895'     ZMSGNO
s01  C*                  MOVE      '1'           *IN30
     C                   WRITE     BLANKREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
s01  C*  51              Z-ADD     17            POSNBR
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_____________
      * F6=Lag ny
      *"""""""""""""
     C     *IN06         IFEQ      '1'
     C                   exsr      movw
     C                   EXSR      CRTNEW
     C                   GOTO      START
     C                   END
     c                   EXSR      OPTION
     C                   GOTO      START
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         ENDSR
      *
      *
      ***************************************************************
     C     OPTION        BEGSR
      ***************************************************************
     C*
     C                   SETOFF                                       919299
     C                   SETOFF                                       3098
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
      * sletting
      *
     C     WSNR          IFEQ      '4'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY2          CHAIN     UNITAKM                            55
     C  N55              DELETE    UNITAKMR
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
      * ENDRING
      *
     C     WSNR          IFEQ      '2'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY2          CHAIN(N)  UNITAKM                            55
     C     *IN55         IFEQ      '0'
     C                   EXSR      MOVFW
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     12            POSNBR
     c                   EXSR      CHGSR
     C                   END
     C                   END
     C                   END
      *
      * kopiering
      *
     C     WSNR          IFEQ      '3'
     C                   MOVE      ' '           WSNR
     c                   update    subfil
     C     SLETT         IFNE      '*SLETTET'
     C     KEY2          CHAIN(N)  UNITAKM                            55
     C     *IN55         IFEQ      '0'
     C                   EXSR      MOVFW
     C                   move      *zeros        wssavd
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     12            POSNBR
     c                   EXSR      CRTNEW
     C                   END
     C                   END
     C                   END
     C                   END
     C     SLHENT        ENDSR
      *
      *
      ******************************************************************
     C     CRTNEW        BEGSR
      ******************************************************************
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     5             POSNBR
     c     feilny        tag
     C                   EXFMT     NEWREC
     c                   setoff                                       3070
     C     *IN12         IFNE      '1'
     c     avdkey        chain     kodta                              70
     c  n70wssavd        comp      0                                      70
     c     *in70         ifeq      '1'
     c                   move      'SPA0001'     msgnr
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     5             POSNBR
     c                   seton                                        30
     c                   goto      feilny
     c                   end
     C                   EXSR      TESTVAL
     C   30              GOTO      FEILNY
     C     KEY2W         CHAIN     UNITAKM                            55
     C     *IN55         IFEQ      '1'
     C                   EXSR      MOVWF
     C                   WRITE     UNITAKMR
     C     XRECNO        ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   ELSE
     c                   move      'SHU1893'     msgnr
     c                   seton                                        3070
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      FEILNY
     C                   END
     C                   END
     C                   ENDSR
      *
      ******************************************************************
     C     CHGSR         BEGSR
      ******************************************************************
     C     FEILCHG       TAG
     C                   EXFMT     CHGREC
     c                   setoff                                       30
     C     *IN12         IFNE      '1'
     C                   EXSR      TESTVAL
     C   30              GOTO      FEILCHG
     C     KEY2          CHAIN     UNITAKM                            55
     C                   EXSR      MOVWF
     C                   UPDATE    UNITAKMR
     C                   END
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   ENDSR
      ******************************************************************
     C     MOVWF         BEGSR
      ******************************************************************
     C                   MOVE      WSSAVD        UNSAVD
     C                   MOVE      WSVAL         UNVAL
     C                   MOVE      WSKMP         UNKMP
     C                   MOVE      WSVALE        UNVALE
     C                   MOVE      WSKMPE        UNKMPE
     C                   MOVE      WSVALI        UNVALI
     C                   MOVE      WSKMPI        UNKMPI
     C                   ENDSR
      *
      ******************************************************************
     C     MOVFW         BEGSR
      ******************************************************************
     C                   MOVE      UNSAVD        WSSAVD
     C                   MOVE      UNVAL         WSVAL
     C                   MOVE      UNKMP         WSKMP
     C                   MOVE      UNVALE        WSVALE
     C                   MOVE      UNKMPE        WSKMPE
     C                   MOVE      UNVALI        WSVALI
     C                   MOVE      UNKMPI        WSKMPI
     C                   ENDSR
      *
      ******************************************************************
     C     MOVW          BEGSR
      ******************************************************************
     C                   MOVE      *zeros        WSSAVD
     C                   MOVE      *blanks       WSVAL
     C                   MOVE      *zeros        WSKMP
     C                   MOVE      *blanks       WSVALE
     C                   MOVE      *zeros        WSKMPE
     C                   MOVE      *blanks       WSVALI
     C                   MOVE      *zeros        WSKMPI
     C                   ENDSR
      ***************************************************************
      *
      ******************************************************************
     C     TESTVAL       BEGSR
      ******************************************************************
     C                   Z-ADD     23            LINNBR
     c                   movea     '000'         *in(71)
     c     keyval1       klist
     c                   kfld                    fifirm
     c                   kfld                    wsval
     c     keyval2       klist
     c                   kfld                    fifirm
     c                   kfld                    wsvale
     c     keyval3       klist
     c                   kfld                    fifirm
     c                   kfld                    wsvali
     c     wsval         ifne      *blanks
     c     keyval1       chain     valul01                            71
     c     *in71         ifeq      '1'
     c                   seton                                        30
     c                   z-add     12            posnbr
     c                   end
     c                   end
     c     wsvale        ifne      *blanks
     c     keyval2       chain     valul01                            72
     c     *in72         ifeq      '1'
     c  N30              z-add     25            posnbr
     c                   seton                                        30
     c                   end
     c                   end
     c     wsvali        ifne      *blanks
     c     keyval3       chain     valul01                            73
     c     *in73         ifeq      '1'
     c  N30              z-add     38            posnbr
     c                   seton                                        30
     c                   end
     c                   end
     C   30              MOVE      'SPV0001'     MSGNR
     c                   endsr
