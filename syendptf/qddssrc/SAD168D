      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            EMAVD          4Y 0O  6  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A            EMPRO          8Y 0O  6 10TEXT('Turnr')
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A            EMTSD          8A  O  6 19TEXT('PasseringsTollsted')
     A            EMRGD         17A  H
     A            EMRGD_10      10A  O  6 28TEXT('Org.nr deklarant')
     A            EMNAD         30A  O  6 39TEXT('Navn deklarant')
     A            EMDKMT         4A  O  6 70TEXT('Master document type')
     A            EMDKM         50A  H
     A            EMDKM_20      20A  O  6 75TEXT('Master document')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD168R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold ekspressfort. (avd.)'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2'Velg:  2=Endre   3=Kopier   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Turnr.   Tollsted Deklaran-
     A                                      t orgnr og navn                   M-
     A                                      aster dokumentnr'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                 26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD138R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold ekspressfort. (avd.)'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  3  2'Velg:  2=Endre   3=Kopier   5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Turnr.   Tollsted Deklaran-
     A                                      t orgnr og navn                   M-
     A                                      aster dokumentnr'
     A                                      COLOR(WHT)
     A                                 26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(25)
     A                                      CLRL(03)
     A                                  1  2'Avdeling:'
     A            W3AVD          4Y 0I  1 12DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'F12=Forrige'
     A                                      COLOR(BLU)
