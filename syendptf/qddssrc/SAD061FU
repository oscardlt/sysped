     A*%%TS  SD  20180926  154631  SVEIN       REL-V7R1M0  5770-WDS
     A*
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 FIELD-EXIT = FIELD-EXIT + ENTER
10244A* 02 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A.
10262A* 03 YBC PTF10 TOLLSATS ER UTVIDERT TIL 7.2
10XXXA* 04 CB  PTF10 ENDRET TIL *DS4, INGEN MERKING
10286A* 05 YBC PTF10 VEKT ER ENDRET FRA 9.0 TIL 12.3
     A*              VAREBESKRIVELSE ER ØKT FRA 15A TIL 30A
10329A* 06 YBC PTF10 MENGDE I ANNEN ENHET ER ENDRET FRA 7.0 TIL 9.0
     A*              INGEN MERKING
xxxxxA*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11009A* 07 YBC PTF11 VIS FEILMELDING PÅ EN ANNEN MÅTE
11016A* 08 YBC PTF11 OMBEREGNING
11174A* 09 YBC PTF11 TILLEGGSLINJER MED SAMME POST
11185A* 10 YBC PTF11 VIS TOTAL VEKT OG SUM AV LINJEVEKT
xxxxxA*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXXA* 11 YBC PTF12 DOBBEL BRUK AV IND. 62
12053A* 12 YBC PTF12 FLERE BELØP-FELTER ER ENDRET FOR Å TA STØRRE VERDI
12098A* 13 YBC PTF12 VIS SUM AV LINJE NETTOVEKT
12XXXA* 14 YBC PTF12 TEST OM NETTOVEKT ER MINDRE ENN 0
12XXXA* 15 YBC PTF12 VIS EDI-ARTIKKELNR
12108A* 16 YBC PTF12 MULIGHET FOR AVVIKENDE PROSEDYRE
12110A* 17 YBC PTF12 GI MELDING NÅR AVG.GR.LAG/AVGIFT ER FOR STOR.
12200 * 18 YBC PTF12 CONTAINERNR I EGET FELT
12225 * 19 YBC PTF12 Ikke endre felt på omberegning hvis det kan endre samslåing
12281 * 20 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD061APNL)
     A                                      HLPTITLE('SAD IMPORT')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R BILDE
     A*%%TS  SD  20180926  154631  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'BEKREFTELSE')
     A                                      CF09(09 'Kalkulator av/på')
     A                                      CF10(10 'SØK VARER')
     A                                      CF11(11 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A                                      CF12(12 'Forrige')
     A                                      CF13(13 'Oppdater søketekst på tari-
     A                                      ff-register')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
N18  A                                      CF18(18 'Flere container')
     A  60                                  CF21(21 'Automatisk beregning av br-
     A                                      utto/nettovekt')
     A  60                                  CF22(22 'MERK B PÅ STATUS')
     A                                      CF23(23 'LEGG UT I VAREREG')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A          H                           HLPPNLGRP(SAD010 SAD061APNL)
     A                                      HLPARA(03 004 03 008)
     A          H                           HLPPNLGRP(SAD011 SAD061APNL)
     A                                      HLPARA(03 011 03 020)
     A          H                           HLPPNLGRP(SADPRE SAD061APNL)
     A                                      HLPARA(03 023 03 023)
     A          H                           HLPPNLGRP(SAD012 SAD061APNL)
     A                                      HLPARA(03 025 03 064)
     A          H                           HLPPNLGRP(SAD013 SAD061APNL)
     A                                      HLPARA(03 067 03 078)
     A          H                           HLPPNLGRP(SAD014 SAD061APNL)
     A                                      HLPARA(04 001 11 007)
     A          H                           HLPPNLGRP(SAD014A SAD061APNL)
     A                                      HLPARA(04 009 11 011)
     A          H                           HLPPNLGRP(SAD014B SAD061APNL)
     A                                      HLPARA(04 013 11 020)
     A          H                           HLPPNLGRP(SAD014C SAD061APNL)
     A                                      HLPARA(04 022 11 033)
     A          H                           HLPPNLGRP(SAD014D SAD061APNL)
     A                                      HLPARA(04 035 11 046)
     A          H                           HLPPNLGRP(SAD014E SAD061APNL)
     A                                      HLPARA(12 002 18 050)
     A          H                           HLPPNLGRP(SAD015 SAD061APNL)
     A                                      HLPARA(19 002 23 032)
     A          H                           HLPPNLGRP(SAD016 SAD061APNL)
     A                                      HLPARA(22 035 23 072)
     A          H                           HLPPNLGRP(SAD016 SAD061APNL)
     A                                      HLPARA(24 001 24 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2VNT          8S 0H
S07  A*           MSGNR          7A  P
     A                                  1  2'SYSPED/SAD061U'
     A                                      COLOR(BLU)
     A                                  1 21'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr'
     A            UAVD           4Y 0O  1 47COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 52'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 54COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  1 63'-'
     A                                      COLOR(WHT)
     A            SVLI           5Y 0O  1 65COLOR(WHT)
     A                                      EDTCDE(4)
     A            SVLN           5Y 0O  1 71COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                  1125TIME
S12  A*                                 2  4'VF Opp Varenr.  TN P Tollverdi... -
S12  A*                                     Bruttovekt... Nettovekt.... Mengde.-
S12  A*                                     .. PVA T.sats Mf'
     A                                  2  4'VF Opp Varenr.  TN P Tollverdi....-
     A                                      . Bruttovekt... Nettovekt.... Mengd-
     A                                      e... PVA T.sats Mf Pros'
     A                                      COLOR(WHT)
     A            W2VF           1A  B  3  4TEXT('Verdi fasts.kode')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
S19  A* 39                                  DSPATR(PR)
N19  A  28
N19  AO 39                                  DSPATR(PR)
     A  99                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
     A            W2LK           2A  B  3  7TEXT('Landk. oppr.land')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
S19  A* 39                                  DSPATR(PR)
N19  A  28
N19  AO 39                                  DSPATR(PR)
     A  98                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
     A            W2VNTI         8A  B  3 11TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
     A  28
     AO 39                                  DSPATR(PR)
     A  97                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
     A            W2TN           1A  B  3 20TEXT('Tollnedsettelse')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
S19  A* 39                                  DSPATR(PR)
N19  A  28
N19  AO 39                                  DSPATR(PR)
     A  96                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
     A            W2PRE          1A  B  3 23TEXT('Preferanse')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
S19  A* 39                                  DSPATR(PR)
N19  A  28
N19  AO 39                                  DSPATR(PR)
     A  95                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
     A            W2BELT        13Y 2B  3 25TEXT('Beløp Tollverdi')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,00)
S05  A*           W2VKTB         9Y 0B  3 38TEXT('Vekt Brutto')
     A            W2VKTB        12Y 3B  3 40TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,000)
S05  A*           W2VKTN         9Y 0B  3 48TEXT('Vekt Netto')
     A            W2VKTN        12Y 3B  3 54TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,000)
     A            W2NTM          9Y 0B  3 68TEXT('Mengde')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A            W2PVA          1A  B  3 79TEXT('Enhet PVAF')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
S19  A* 39                                  DSPATR(PR)
N19  A  28
N19  AO 39                                  DSPATR(PR)
     A  92                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
S03  A*           W2AS           5Y 2B  3 72TEXT('Avgiftssats')
     A            W2AS           7Y 2B  3 81TEXT('Avgiftssats')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
S19  A* 39                                  DSPATR(PR)
N19  A  28
N19  AO 39                                  DSPATR(PR)
     A  91                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2MFR          1A  B  3 90TEXT('MVA fri kod')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
S19  A* 39                                  DSPATR(PR)
N19  A  28
N19  AO 39                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
     A            W2DP           2Y 0B  3 93TEXT('Prosedyre')
     A                                      DSPATR(UL)
     A  39                                  DSPATR(HI)
S19  A* 39                                  DSPATR(PR)
N19  A  28
N19  AO 39                                  DSPATR(PR)
     A  51                                  DSPATR(RI)
     A N39                                  COLOR(YLW)
     A                                      EDTCDE(4)
N20  A            W2DP2          2Y 0B  3 96TEXT('Prosedyre')
N20  A                                      DSPATR(UL)
N20  A  39                                  DSPATR(HI)
N20  A  28
N20  AO 39                                  DSPATR(PR)
N20  A  51                                  DSPATR(RI)
N20  A N39                                  COLOR(YLW)
N20  A                                      EDTCDE(4)
     A                                  4  2'Avg'
     A            W2AKD1         2A  B  4  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASV1         3A  B  4  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA1         7Y 2B  4 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AGR1        13Y 2B  4 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2ABL1        13Y 2B  4 37DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4 54'Justering.....:'
     A            W2BEL         11Y 2B  4 70DSPATR(UL)
     A  63                                  DSPATR(HI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A            W2AKD2         2A  B  5  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASV2         3A  B  5  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA2         7Y 2B  5 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AGR2        13Y 2B  5 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2ABL2        13Y 2B  5 37DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5 54'% av vareverdi:'
     A            W2PROS         5Y 2B  5 70DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A            W2AKD3         2A  B  6  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASV3         3A  B  6  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA3         7Y 2B  6 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AGR3        13Y 2B  6 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2ABL3        13Y 2B  6 37DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  6 54'Valutakode....:'
     A            W2VAL          3A  B  6 70DSPATR(UL)
     A  52                                  DSPATR(HI)
     A                                      COLOR(YLW)
     A            W2AKD4         2A  B  7  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASV4         3A  B  7  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA4         7Y 2B  7 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AGR4        13Y 2B  7 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2ABL4        13Y 2B  7 37DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 54'Beløp i NOK...:'
     A            W2BELN        13Y 2O  7 70DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(L)
     A            W2AKD5         2A  B  8  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASV5         3A  B  8  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA5         7Y 2B  8 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AGR5        13Y 2B  8 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2ABL5        13Y 2B  8 37DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AKD6         2A  B  9  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASV6         3A  B  9  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA6         7Y 2B  9 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AGR6        13Y 2B  9 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2ABL6        13Y 2B  9 37DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
N18  A                                  9 54'Cont.nr:'
N18  A            W2CNR         17A  B  9 63TEXT('Fritekst')
N18  A  50                                  DSPATR(RI)
N18  A                                      DSPATR(UL)
N18  A                                      COLOR(YLW)
N18  A  36                              9 81'Flere'
N18  A                                      COLOR(RED)
     A            W2AKD7         2A  B 10  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASV7         3A  B 10  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA7         7Y 2B 10 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AGR7        13Y 2B 10 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2ABL7        13Y 2B 10 37DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 86'Fakt.bel:'
S12  A*           UFAKNO         9Y 0O 10 98COLOR(WHT)
     A            UFAKNO        11Y 0O 10 98COLOR(WHT)
     A                                      EDTCDE(4)
     A            W2AKD8         2A  B 11  6TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASV8         3A  B 11  9TEXT('Avgiftskode')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ASA8         7Y 2B 11 13TEXT('Toll og Avg.sats')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AGR8        13Y 2B 11 22TEXT('Avgiftsgrunnlag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2ABL8        13Y 2B 11 37DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 86'Frakt:'
     A            SIBEL8        11Y 2O 11 98TEXT('Frakt omberegnet')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 12  2'V.B'
     A            W2FT01        28A  B 12  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT01         6Y 0B 12 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH01         4A  B 12 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*           W2VT01        15A  B 12 47TEXT('Varetekst')
     A            W2VT01        30A  B 12 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*                                12 71'Rabatt:'
S05  A*           SIBELC        11Y 2O 12 83COLOR(WHT)
     A                                 12 86'Rabatt:'
     A            SIBELC        11Y 2O 12 98COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 13  1'(31)'
     A            W2FT02        28A  B 13  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT02         6Y 0B 13 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH02         4A  B 13 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*           W2VT02        15A  B 13 47TEXT('Varetekst')
     A            W2VT02        30A  B 13 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*                                13 71'Andre kost:'
S05  A*           SIBEL9        11Y 2O 13 83TEXT('Andre kost.omber')
     A                                 13 86'Andre kost:'
     A            SIBEL9        11Y 2O 13 98TEXT('Andre kost.omber')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            W2FT03        28A  B 14  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT03         6Y 0B 14 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH03         4A  B 14 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*           W2VT03        15A  B 14 47TEXT('Varetekst')
     A            W2VT03        30A  B 14 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*                                14 71'Forsik:'
S05  A*           SIBELA         7Y 0O 14 85TEXT('Beløp Forsikring')
     A                                 14 86'Forsik:'
S12  A*           SIBELA         7Y 0O 14100TEXT('Beløp Forsikring')
     A            SIBELA         9Y 0O 14100TEXT('Beløp Forsikring')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            W2FT04        28A  B 15  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT04         6Y 0B 15 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH04         4A  B 15 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*           W2VT04        15A  B 15 47TEXT('Varetekst')
     A            W2VT04        30A  B 15 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*                                15 71'CIFSUM:'
S05  A*           WSBEL4         9Y 0O 15 83TEXT('Beløp CIFsum')
     A                                 15 86'CIFSUM:'
S12  A*           WSBEL4         9Y 0O 15 98TEXT('Beløp CIFsum')
     A            WSBEL4        11Y 0O 15 98TEXT('Beløp CIFsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            W2FT05        28A  B 16  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT05         6Y 0B 16 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH05         4A  B 16 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*           W2VT05        15A  B 16 47TEXT('Varetekst')
     A            W2VT05        30A  B 16 47TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2FT06        28A  B 17  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT06         6Y 0B 17 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH06         4A  B 17 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*           W2VT06        15A  B 17 47TEXT('Varetekst')
N05  A*           W2VT06        30A  B 17 47TEXT('Varetekst')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A  39                             17 86'Kalkulator på'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A            W2FT07        28A  B 18  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NT07         6Y 0B 18 35TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH07         4A  B 18 42TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S05  A*           W2VT07        15A  B 18 47TEXT('Varetekst')
N05  A*           W2VT07        30A  B 18 47TEXT('Varetekst')
     A*                                     DSPATR(UL)
     A*                                     COLOR(YLW)
     A                                 19  2'T.O'
     A            W2TOP1        17A  B 19  6DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19 24'Ref.'
     A            W2CRE1         3A  B 19 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19 42'T.O'
     A            W2TOP2        17A  B 19 46DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19 64'Ref.'
     A            W2CRE2         3A  B 19 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19 86'Fsum->'
     A            SIBEL3        13Y 2O 19 98TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 20  1'(44)'
     A            W2TOP3        17A  B 20  6DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE3         3A  B 20 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  68                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP4        17A  B 20 46DSPATR(UL)
     A  67                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE4         3A  B 20 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 20 86'Vsum->'
     A            W2VSUM        13Y 2O 20 98TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            W2TOP5        17A  B 21  6DSPATR(UL)
     A  65                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE5         3A  B 21 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  64                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP6        17A  B 21 46DSPATR(UL)
     A  63                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE6         3A  B 21 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 21 86'Diff->'
     A            W2DIFF        13Y 2O 21 98TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A            W2TOP7        17A  B 22  6DSPATR(UL)
     A  61                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE7         3A  B 22 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP8        17A  B 22 46DSPATR(UL)
     A  58                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE8         3A  B 22 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  57                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 22 86'Tvkt->'
     A            SIVKB          9Y 0O 22100TEXT('Tot. brutto vekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            W2TOP9        17A  B 23  6DSPATR(UL)
     A  56                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE9         3A  B 23 30TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  55                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TOP10       17A  B 23 46DSPATR(UL)
     A  54                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2CRE10        3A  B 23 70TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  53                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 23 86'Lvkt->'
     A            W2VKB         13Y 3O 23 99TEXT('Brutto linjevekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 24  2'Edi artikkelnr:'
     A            W2EXR03       35A  O 24 19COLOR(YLW)
     A                                 24 86'Diff->'
     A            W2VDIF        13Y 3O 24 99TEXT('Diff. brutto vekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                 25 86'LNVt->'
     A            W2VKN         13Y 3O 25 99TEXT('Netto linjevekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                             26  2'F5=Lagre'
     A                                      COLOR(BLU)
     A                                 26 13'F10=Søk'
     A                                      COLOR(BLU)
     A                                 26 23MSGCON(012 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 37'F23=Legg til i varekatalog'
     A                                      COLOR(BLU)
N18  A                                 26 66'F18=Flere containenr'
N18  A                                      COLOR(BLU)
     A N30        MSGTX2        78A  O 27  2COLOR(RED)
S07  A* 30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
S07  A*                                     COLOR(WHT)
     A  30        MSGTX3        78A  O 27  2COLOR(WHT)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      OVERLAY
S02  A*                                     WINDOW(*DFT 3 75 *NOMSGLIN)
N02  A                                      WINDOW(*DFT 3 90 *NOMSGLIN)
      *
     A          R VINDU
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A                                      WINDOW(WINDOWSZ)
S02  A*                                 1  1MSGCON(050 SHU0814 *LIBL/SYMSGF)
N02  A                                  1  1MSGCON(050 YBC1993 *LIBL/SYMSGF)
     A            WSLEVE         8Y 0B  2  1TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
S02  A*           VARENR        15A  B  2 10TEXT('ALFASØKING')
N02  A            VARENR        28A  B  2 10TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI)
S02  A*           VAREBE        50A  B  2 26TEXT('BESKRIVELSE')
N02  A            VAREBE        50A  B  2 39TEXT('BESKRIVELSE')
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R ANTENH
     A                                      WINDOW(*DFT 3 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                  2  2'Antall enheter:'
     A            WSANT          5Y 0B  2 18TEXT('ANTALL ENHETER')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
N01  A                                      CHECK(ER)
N17   *
N17  A          R STORBEL
N17  A                                      WINDOW(5 60 3 55 *NOMSGLIN)
N17  A                                      OVERLAY
N17  A                                  2  2'Minst en avgift med stort gr.lag e-
N17  A                                      ller stort beløp.'
N17  A                                      COLOR(YLW)
N17  A                                  3  2'Er det riktig?'
N17  A            WSJN           1A  B  3 18DSPATR(UL)
N17  A                                      VALUES('J' 'N')
N17  A                                  3 20'(N=Gå til feilretting)'
