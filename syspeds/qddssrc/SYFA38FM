*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
P8068A* 03 SH  PTF9 SONER
*****A**************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A                                      HLPPNLGRP(HLPSCR SYFA38PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FLYSELSKAP-
     A                                      ER')
S03  A*           WSNR           1A  I  4  2VALUES(' ' '2' '4')
     A          R SUBFIL                    SFL
     A            KFSKO2         2S 0H
S03  A*           WSNR           1A  I  4  2VALUES(' ' '2' '4')
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' 'S')
     A            KFSFNR         3Y 0O  4  5TEXT('FLYSELSKAP')
     A                                      EDTCDE(4)
     A            KFSNAV        30A  O  4  9TEXT('FLYSELSKAPSNAVN')
     A            KFSKD2         2A  O  4 40TEXT('KODE-2')
     A            KFSAC         10A  O  4 43TEXT('VÅRT ACCN')
     A            KFSKNR         8Y 0O  4 54TEXT('KUNDENR.')
     A                                      EDTCDE(4)
     A            KFSMC          3Y 0O  4 63TEXT('MC')
     A                                      EDTCDE(4)
     A            KFSMC2         3S 0O  4 71
     A            KFSJAT         1A  O  4 75TEXT('JATA-LAND')
     A            KFSKOM         2Y 2O  4 77TEXT('KOMMISJON %')
     A                                      EDTCDE(4)
     A            KFSMCO         3  0O  4 67
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA38'
     A                                      COLOR(BLU)
     A            MSG101        40A  O  1 19MSGID(DIV 1553 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A            MSG102        30A  O  2  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
S03  A*           MSG103        30A  O  2 34MSGID(DIV 0017 SYMSGF)
     A            MSG103        30A  O  2 34MSGID(SHU 1187 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG104        78   O  3  2MSGID(DIV 1554 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG201        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 41MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF04(04 'Søk kunde')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            WSFNR          3Y 0B  2  5TEXT('FLYSELSKAP')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(GRN)
     A            WSNAV         30A  B  2  9TEXT('FLYSELSKAPSNAVN')
     A                                      DSPATR(UL)
     A  40N30
     AO 89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A            WSKD2          2A  B  2 40TEXT('KODE-2')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A            WSAC          10A  B  2 43TEXT('VÅRT ACCN')
     A                                      DSPATR(UL)
     A            WSKNR          8Y 0B  2 54TEXT('KUNDENR.')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSMC           3Y 0B  2 63TEXT('MC')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSJAT          1A  B  2 75TEXT('JATA-LAND')
     A                                      DSPATR(UL)
     A            WSKOM          2Y 0B  2 77TEXT('KOMMISJON %')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            MSG302        18A  O  3  2MSGID(DIV 0032 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG303        18A  O  3 22MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1  5MSGCON(075 DIV1962 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSMCO          3Y 0B  2 67EDTCDE(Z)
     A            WSMC2          3Y 0B  2 71EDTCDE(Z)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG601        12A  O  2  2MSGID(DIV 0026 SYMSGF)
     A            W2NAV         30A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG602        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG401        74   O  1  5MSGID(DIV 1962 SYMSGF)
     A                                      COLOR(WHT)
     A            KFSFNR         3Y 0O  2  5TEXT('FLYSELSKAP')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KFSNAV        30A  O  2  9TEXT('FLYSELSKAPSNAVN')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFSKD2         2A  O  2 40TEXT('KODE-2')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFSAC         10A  O  2 44TEXT('VÅRT ACCN')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFSKNR         8Y 0O  2 56TEXT('KUNDENR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KFSMC          3Y 0O  2 65TEXT('MC')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KFSJAT         1A  O  2 71TEXT('JATA-LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            KFSKO2         2Y 0O  2 75TEXT('KOMMISJON %')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA38'
     A                                      COLOR(BLU)
     A            MSG501        40A  O  1 19MSGID(DIV 1553 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A            MSG502        30A  O  2  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG503        30A  O  2 34MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG504        78A  O  3  2MSGID(DIV 1554 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG509        18A  O 23 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG510        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
