********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 Vise også omberegning (negativ oppdragsnr)
      * 02 jov PTF12 tA HENSYN TIL MAPPE HISTORIKK
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE001 MODULE(*LIBL/TJGE001 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FARKIVL08  IF   E           K DISK    USROPN
     FARKIVL11  IF   E           K DISK    USROPN
     F                                     RENAME(arkivr:arkivr11)
     FARKTXT    IF   E           K DISK    USROPN
     FFIRMARC   IF   E           K DISK    USROPN
     FARKEXT    IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FKOSTA     IF   E           K DISK    USROPN
     FLEVEL01   IF   E           K DISK    USROPN
     FSADH      IF   E           K DISK    USROPN
     FSAEH      IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FTURER14   IF   E           K DISK    USROPN
     FDOKUF     IF   E           K DISK    USROPN
     FFAK3      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D URUNDE          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
N01  D OPDN_OMB        S              7  0
N01  D X_OMB           S              1
     D updf            S            200
     D wstex1          S            200
     D wstex2          S            200
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
     D                 DS
     D  ARDATE                 1      8  0
     D  ARAA                   3      4
     D  ARMM                   5      6
     D  ARDG                   7      8
     D                 DS
     D  ARTIME                 1      6  0
     D  ARTIT                  1      2
     D  ARTIM                  3      4
     D  ARTIS                  5      6
     D                 DS
     D  FADATO                 1      8  0
     D  FAAA                   3      4
     D  FAMM                   5      6
     D  FADG                   7      8
     D                 DS
     D  ARUNDE                 1     10
     D  ARUN08                 1      8
     D  ARU1T3                 1      3
     D  AR4T10                 4     10
     ILEVER
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"archiveElements" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
N01  C                   EVAL      X_OMB = 'N'
     C  N80keyA08        SETLL     ARKIVL08
     C   80keyA11        SETLL     ARKIVL11
      *
     C  N80keyA08        READE     ARKIVL08                               55
     C   80keyA11        READE     ARKIVL11                               55
     C     *IN55         DOWEQ     '0'
     C                   Add       1             AntLest
     c     artype        ifeq      'go'
     c     artype        oreq      'gf'
     c     artype        oreq      'am'
     c     artype        oreq      'fx'
     c     artype        oreq      'fs'
     c                   move      *zeros        aravd
     c                   move      *zeros        aropd
     c                   end
     c                   exsr      genbane
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬url¬ : ¬'+%trim(updf)+'¬, '
          +'¬docType¬ : ¬'+%trim(ARTYPE)+'¬, '
          +'¬subject¬ : ¬'+%trim(WSTEX1)+'¬, '
          +'¬additionalInfo¬ : ¬'+%trim(WSTEX2)+'¬, '
          +'¬createUser¬ : ¬'+%trim(ARUSER)+'¬, '
          +'¬createDate¬ : ¬'+%trim(%editc(ARDATE:'Z'))+'¬, '
          +'¬createTime¬ : ¬'+%trim(%editc(ARTIME:'Z'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
N01  C  N80              IF        X_OMB <> 'J'
     C  N80keyA08        READE     ARKIVL08                               55
N01  C                   ELSE
N01  C     keyA08_OMB    READE     ARKIVL08                               55
N01  C                   END
N01  C  N80
N01  CAN 55              IF        X_OMB <> 'J'
N01  C                   EVAL      X_OMB = 'J'
N01  C     keyA08_OMB    SETLL     ARKIVL08
N01  C     keyA08_OMB    READE     ARKIVL08                               55
N01  C                   END
     C   80keyA11        READE     ARKIVL11                               55
     C                   ENDDO
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     ARKIVL08
     C  N50              CLOSE     ARKIVL11
     C  N50              CLOSE     ARKTXT
     C  N50              CLOSE     FIRMARC
     C  N50              CLOSE     ARKEXT
     C  N50              CLOSE     FIRM
     C  N50              CLOSE     KOSTA
     C  N50              CLOSE     LEVEL01
     C  N50              CLOSE     SADH
     C  N50              CLOSE     SAEH
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     CUNDL01
     C  N50              CLOSE     TURER14
     C  N50              CLOSE     DOKUF
     C  N50              CLOSE     FAK3

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD   = zhbgetvar('AVD')
     C                   EVAL      AVDN  = c2n2(AVD)
     C                   EVAL      OPD   = zhbgetvar('OPD')
     C                   EVAL      OPDN  = c2n2(OPD)
     C                   EVAL      URUNDE  = zhbgetvar('Key2')
N01  C                   Z-SUB     OPDN          OPDN_OMB
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      ARKIVL08
     C  N50              OPEN      ARKIVL11
     C  N50              OPEN      ARKTXT
     C  N50              OPEN      FIRMARC
     C  N50              OPEN      ARKEXT
     C  N50              OPEN      FIRM
     C  N50              OPEN      KOSTA
     C  N50              OPEN      LEVEL01
     C  N50              OPEN      SADH
     C  N50              OPEN      SAEH
     C  N50              OPEN      HEADF
     C  N50              OPEN      CUNDL01
     C  N50              OPEN      TURER14
     C  N50              OPEN      DOKUF
     C  N50              OPEN      FAK3
      *
      * 80 *on = Basert på underkegrep/Key2(ARUNDE)
     c     urunde        comp      *blanks                            8080
     C     keyA08        KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
N01  C     keyA08_OMB    KLIST
N01  C                   KFLD                    ARSTAT
N01  C                   KFLD                    AVDN
N01  C                   KFLD                    OPDN_OMB
     C     keyA11        KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    URUNDE
     C                   MOVE      'A'           ARSTAT

     C     kostke3       KLIST
     C                   KFLD                    KABNR
     C     leveke        KLIST
     C                   kfld                    fifirm
     C                   KFLD                    KALNR
     C     dokufk        KLIST
     C                   KFLD                    dfuni
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    WSFNR             3 0
     c                   move      'F'           dfuni             1
     C*
     C     faktke        KLIST
     C                   KFLD                    fauni             1
     C                   KFLD                    WSFANR            7 0
     C*
     C*
     C     faktkk        KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    fakunr
     C*
     C*
     C     arklagk       KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    arklag
     C*
     C     KEYhea        KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
N01  C     KEYhea_OMB    KLIST
N01  C                   KFLD                    AVDN
N01  C                   KFLD                    OPDN_OMB
     c  N50keyhea        chain     headf                              33
     c  N50              read      firm                                   33
      *
     c                   endsr
     C***********************************************
     C     genbane       BEGSR
     C***********************************************
     c     keyext        klist
     c                   kfld                    fifirm
     c                   kfld                    arcext
     c                   move      *blanks       wbane           140
     c     artype        chain     ARKTXT                             33
     C     ARKLAG        IFNE      *blanks
     c     arklagk       chain     arkext
     c                   else
     c     fifirm        chain     firmarc                            33
     c                   end
N02  c     arbhis        ifne      *blanks
N02  c                   eval      arcane=arbhis
N02  c                   end
     c
     c                   eval      updf='/'+%trim(arcane)+'/'+%trim(arlink)
      * gamler på at dersom . ikke finnes så er den intern - add .pdf
     C     '.'           SCAN      arlink                                 33
     c  N33              eval      updf=%trim(updf)+'.pdf'
      * Hovedtekst / tillegstekst
S01  c*                  movel     artxt         wstex1
N01  C                   EVAL      WSTEX1 = artxt
N01  C                   IF        X_OMB = 'J'
N01  C                   CAT       'OMB':1       WSTEX1
N01  C                   END
     c                   move      *blanks       wstex2
      * stykkgodsfraktbrev
     c     artype        ifeq      'sb'
     c                   movel     arunde        var03             3
     c                   move      var03         wsfnr
     c     dokufk        chain     dokuf                              33
     c     *in(33)       ifeq      '0'
     C                   movel     'Fb.nr:'      wstex2
     C     wstex2        CAT       df1004:1      wstex2
     C     wstex2        CAT       'Til:':1      wstex2
     C     wstex2        CAT       dfnavm:1      wstex2
     c                   end
     c                   end
      *samlefaktura
     c     artype        ifeq      'fs'
     c     artype        oreq      'fx'
     c     artype        oreq      'fa'
     C                   movel     'Fanr:'       wstex2
     c                   movel     arunde        var07             7
     C     wstex2        CAT       var07:1       wstex2
     c                   move      var07         wsfanr            7 0
     c                   move      'O'           fauni
     c     faktke        chain     fak3                               33
     C     *IN33         DOWEQ     '0'
     C     FAKDA         ANDEQ     'K'
     c     faktke        READE     fak3                                   33
     C                   END
     c   33              move      'F'           fauni
     c   33faktke        chain     fak3                               33
     C     *IN33         DOWEQ     '0'
     C     FAKDA         ANDEQ     'K'
     c     faktke        READE     fak3                                   33
     C                   END
     c   33              move      'G'           fauni
     c   33faktke        chain     fak3                               33
     C     *IN33         DOWEQ     '0'
     C     FAKDA         ANDEQ     'K'
     c     faktke        READE     fak3                                   33
     C                   END
     c     *in(33)       ifeq      '0'
     C     wstex2        CAT       'dato:':1     wstex2
     C     wstex2        CAT       fadg:1        wstex2
     C     wstex2        CAT       '.':0         wstex2
     C     wstex2        CAT       famm:0        wstex2
     C     wstex2        CAT       '.':0         wstex2
     C     wstex2        CAT       faaa:0        wstex2
     c     faktkk        CHAIN     CUNDL01                            33
     C  N33wstex2        cat       'Til:':1      wstex2
     C  N33wstex2        cat       knavn:1       wstex2
     c                   end
     c                   end
      *Godsliste
     c     artype        ifeq      'go'
     c     artype        oreq      'gf'
     C                   movel     'Turnr:'      wstex2
     c                   movel     arunde        var08             8
     c                   move      var08         tupro             8 0
     C     wstex2        CAT       arunde:1      wstex2
     c     tupro         chain     turer14                            33
     c     *in(33)       ifeq      '0'
     C     wstex2        CAT       'Fra:':1      wstex2
     C     wstex2        CAT       tusdf:1       wstex2
     C     wstex2        CAT       'Til:':1      wstex2
     C     wstex2        CAT       tusdt:1       wstex2
     c                   end
     c                   end
      * flyfraktbrev   awb/hawb
     c     artype        ifeq      'aw'
     c     artype        oreq      'am'
     C                   movel     'awb.nr:'     wstex2
     C     wstex2        CAT       hegn:1        wstex2
     C                   IF        artype = 'aw'
     C     wstex2        CAT       'Hawnr:':1    wstex2
     C     wstex2        CAT       hehawb:1      wstex2
     c                   end
     C     wstex2        CAT       'Fra:':1      wstex2
     C     wstex2        CAT       hesdf:1       wstex2
     C     wstex2        CAT       'Til:':1      wstex2
     C     wstex2        CAT       hesdt:1       wstex2
     c                   end
      * SAD import
     c     artype        ifeq      'si'
N01  C                   IF        X_OMB <> 'J'
     c     keyhea        chain     sadh                               33
N01  C                   ELSE
N01  c     keyhea_OMB    chain     sadh                               33
N01  C                   END
     c     *in(33)       ifeq      '0'
     C                   movel     'Løpenr:'     wstex2
     C     wstex2        CAT       sitle:1       wstex2
     C     wstex2        CAT       sitll:1       wstex2
     C     wstex2        CAT       sidtg:1       wstex2
     C     wstex2        CAT       'kunde:':1    wstex2
     C     wstex2        CAT       sinak:1       wstex2
     c                   end
     c                   end
      * commersial invoice
     c     artype        ifeq      'pf'
     c                   move      arunde        comm              1
     C     comm          ifeq      'Y'
     C                   movel     'Commersial'  wstex2
     C                   end
     C                   end
      * SAD eksport
     c     artype        ifeq      'se'
N01  C                   IF        X_OMB <> 'J'
     c     keyhea        chain     saeh                               33
N01  C                   ELSE
N01  c     keyhea_OMB    chain     saeh                               33
N01  C                   END
     c     *in(33)       ifeq      '0'
     C                   movel     'Løpenr:'     wstex2
     C     wstex2        CAT       setle:1       wstex2
     C     wstex2        CAT       setll:1       wstex2
     C     wstex2        CAT       sedtg:1       wstex2
     C     wstex2        CAT       'kunde:':1    wstex2
     C     wstex2        CAT       senas:1       wstex2
     c                   end
     c                   end
      * Z-DOKUMENTER SCANNET
     c                   movel     artype        artyp1            1
     c     artyp1        ifeq      'Z'
     C     wstex2        CAT       'Date:':1     wstex2
     C     wstex2        CAT       ardg:1        wstex2
     C     wstex2        CAT       '.':0         wstex2
     C     wstex2        CAT       armm:0        wstex2
     C     wstex2        CAT       '.':0         wstex2
     C     wstex2        CAT       araa:0        wstex2
     C     wstex2        CAT       'Time:':1     wstex2
     C     wstex2        CAT       artit:1       wstex2
     C     wstex2        CAT       ':':0         wstex2
     C     wstex2        CAT       artim:0       wstex2
     C     wstex2        CAT       ':':0         wstex2
     C     wstex2        CAT       artis:0       wstex2
     c                   end
      * Kostnadsbilag
     c     aru1t3        ifeq      'KL:'
     c                   move      arunde        kabnr
     c     kostke3       chain     kosta                              33
     c     *in(33)       ifeq      '0'
     c                   move      kabnr2        kabnr2a           7
     C                   movel     'Bilagsnr:'   wstex2
     C     wstex2        CAT       kabnr2a:1     wstex2
     C     wstex2        CAT       'Navn:':1     wstex2
     c     leveke        chain     level01                            33
     C  N33wstex2        CAT       lnavn:1       wstex2
     C   33wstex2        CAT       katxt:1       wstex2
     c                   end
     c                   end
      * tekst i  fritt felt kunderef skjøtes på..
     C     ARRFK         ifne      *blanks
     C                   eval      wstex2=%trim(wstex2)+', '+ARRFK
     c                   end
     c                   endsr
     C*
