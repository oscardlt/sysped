     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLGRP1' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØKING')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPO11'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0051 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  9  2MSGCON(025 SHU0030 *LIBL/SYMSGF)
     A            PTPROD    R        B  9 28REFFLD(PRODTR/PTPROD *LIBL/PRODT)
     A                                      DSPATR(UL)
     A  99                                  DSPATR(HI)
     A                                  9 36MSGCON(025 SHU0052 *LIBL/SYMSGF)
     A                                 11  2MSGCON(025 SHU0031 *LIBL/SYMSGF)
     A            PTAVDE    R        B 11 28REFFLD(PRODTR/PTAVDE *LIBL/PRODT)
     A                                      DSPATR(UL)
     A  98                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 11 36MSGCON(025 SHU0053 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            PTVEKT    R        B  5  8REFFLD(PRODTR/PTVEKT *LIBL/PRODT)
     A                                      EDTCDE(4)
     A            PTPRPR    R        B  5 27REFFLD(PRODTR/PTPRPR *LIBL/PRODT)
     A                                      EDTCDE(4)
     A            PTADJU    R        B  5 42REFFLD(PRODTR/PTADJU *LIBL/PRODT)
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYPO11'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0051 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(012 SHU0035 *LIBL/SYMSGF)
     A            PTPROD    R        O  2 36REFFLD(PRODTR/PTPROD *LIBL/PRODT)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A            PTAVDE    R        O  2 58REFFLD(PRODTR/PTAVDE *LIBL/PRODT)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 SHU0054 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2MSGCON(078 SHU0054 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSVEKT    R        B  2  8REFFLD(PRODTR/PTVEKT *LIBL/PRODT)
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(GRN)
     A            WSPRPR    R        B  2 27REFFLD(PRODTR/PTPRPR *LIBL/PRODT)
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PC)
     A            WSADJU    R        B  2 42REFFLD(PRODTR/PTADJU *LIBL/PRODT)
     A                                      DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2VEKT    R        I  2 15REFFLD(PRODTR/PTVEKT *LIBL/PRODT)
     A                                      COLOR(WHT)
     A                                      DSPATR(PC UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2MSGCON(078 SHU0054 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            PTVEKT    R        O  2  8REFFLD(PRODTR/PTVEKT *LIBL/PRODT)
     A            PTPRPR    R        O  2 27REFFLD(PRODTR/PTPRPR *LIBL/PRODT)
     A            PTADJU    R        O  2 43REFFLD(PRODTR/PTADJU *LIBL/PRODT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SYPO07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0028 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(012 SHU0035 *LIBL/SYMSGF)
     A            PTPROD    R        O  2 36REFFLD(PRODTR/PTPROD *LIBL/PRODT)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A            PTAVDE    R        O  2 58REFFLD(PRODTR/PTAVDE *LIBL/PRODT)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 SHU0054 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG511        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
