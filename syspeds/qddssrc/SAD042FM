     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SAD04201' 'SAD042' 'SY400-
     A                                      LO/SADTVINN')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '2')
     A            SEAVD          4Y 0O  6  5EDTCDE(4)
     A            SETDN          7Y 0O  6 10TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A            SENAK         30A  H
     A            XSENAK        28A  O  6 18TEXT('Navn Kjøper')
     A            SETLL         10A  O  6 47TEXT('Tollnr Løpenr')
     A            SETLE          6A  O  6 58TEXT('Tollnr Ekspsted')
     A            SEDTG         10A  O  6 65TEXT('Dekl. godkj.dato')
     A            SECTN          5Y 0O  6 76EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2MSGCON(050 DIV1760 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  2  2'SYSPED/SAD042'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(015 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Avd. Tdeknr. Mottagersnavn.....-
     A                                      .......... Løpenr     Eksp.nr Dato.-
     A                                      .... Tr.nr'
     A                                      DSPATR(HI)
      *
     A          R BUNN                      CSRLOC(LINNBR POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Op Avd. Tdeknr. Mottagersnavn.....-
     A                                      .......... Løpenr     Eksp.nr Dato.-
     A                                      .... TR.nr'
     A                                      COLOR(WHT)
     A            SEAVD          4Y 0O  2  5TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SETDN          7Y 0O  2 10TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SENAK         30A  H
     A            XSENAK        28A  O  2 18TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSTLL         10A  B  2 47TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A N90                                  COLOR(YLW)
     A            WSTLE          6A  B  2 58TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A N90                                  COLOR(YLW)
     A            WSDTG         10A  B  2 65TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A N90                                  COLOR(YLW)
     A            WSCTN          5Y 0B  2 76TEXT('Transitt.nr.')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A N90                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  90                              4  2MSGCON(078 SPT0025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk fra avd./tolldek.nr.:'
     A            W2AVD          4Y 0I  2 28DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2TDN          7Y 0I  2 33DSPATR(UL)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                  1  2MSGCON(50 DIV1760 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2'SYSPED/SAD042'
     A                                      COLOR(BLU)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(15 DIV0656 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Avd. Tdeknr. Mottagersnavn.....-
     A                                      .......... Løpenr Eksp.nr Dato.....-
     A                                      . Tr.nr'
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
