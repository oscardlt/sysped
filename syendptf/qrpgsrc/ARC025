********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 Ny parameter sammenslå PDF
10XXX * 02 SH  PTF11 kode for upload info dokumenter
10XXX * 03 SH  PTF11 kjøres nas
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 04 JOV PTF12 Vedlikehold også av mapper for skann til arkiv (TKpdfHandler)
      * 05 JOV PTF12 J/N/P for om TKpdfHandler kjøres  / test på  Esped User
10XXX * 06 SH  PTF12 bane historikk
10XXX * 07 JOV PTF12 J i HTTPS
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE arkivlager                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FFIRMARC   UF A E           K DISK
N04  FFIRMARCSI UF A E           K DISK
     FBRIDF     IF   E           K DISK
     FARC025FM  CF   E             WORKSTN
     C*
N04
N04  C     Start         tag
N04
     C                   READ      FIRM                                   30
     c                   move      fifirm        arcfir
     c     arcfir        chain(n)  firmarc                            33
     C                   MOVE      ARCPAD        WRCPAD
     C                   MOVE      ARCANE        WRCANE
n06  C                   MOVE      ARBHISF       WRBHIS
     C                   MOVE      ARCPAE        WRCPAE
N07  C                   MOVE      ARCPSI        WRCPSI
N07  C                   MOVE      ARCPSE        WRCPSE
     C                   MOVE      ARCINF        WRCINF
     C                   MOVE      ARCL1         WRCL1
N01  C                   MOVE      ARCMRG        WRCMRG
N02  C                   MOVE      ARINF         WRINF
N02  C                   MOVE      ARNAS         WRNAS
N05  c                   move      *blanks       WRTKPD
N05  c     'KJ'          chain(n)  firmarcsi                          33        kjører/benyttes
N05  c  N33              movel     ASIANE        WRTKPD
N05  c                   move      *blanks       WREUSR
N05  c     'EU'          chain(n)  firmarcsi                          33        eSped userID
N05  c  N33              movel     ASIANE        WREUSR
N05  c                   move      *blanks       WRLOGL
N05  c     'LL'          chain(n)  firmarcsi                          33        Log Level
N05  c  N33              movel     ASIANE        WRLOGL
N04  c                   move      *blanks       ASIANE
N04  c     'RO'          chain(n)  firmarcsi                          33
N04  c                   move      ASIANE        WRSROM
N04  c                   move      *blanks       ASIANE
N04  c                   setoff                                       50
N04  c     'IN'          setll     firmarcsi
N04  c     'IN'          reade(n)  firmarcsi                              33
N04  c     *in33         ifeq      '0'
N04  c                   move      ASIANE        WRSINM
N04  c     'IN'          reade(n)  firmarcsi                              33
N04  c     *in33         ifeq      '0'
N04  c                   eval      WRSINM = 'Flere,Se F9 - Multiple,See F9'
N04  c                   seton                                        50
N04  c                   endif
N04  c                   endif
      *
      *
N05  c     B1Tag         tag
     C                   EXFMT     BILDE01
     c   12              goto      slutt
      *
N05  c                   exsr      testB1
N05  c   30              goto      B1Tag
      *
N04   * F9 = flere innmapper
N04  c     *in09         ifeq      *on
N04  c                   call      'ARC025B'
N04  c                   goto      Start
N04  c                   endif
N04   *
N05   *  Oppdater (OBS ingen tester ...)
     c     arcfir        chain     firmarc                            33
     C                   MOVE      WRCPAD        ARCPAD
     C                   MOVE      WRCANE        ARCANE
n06  C                   MOVE      WRBHIS        ARBHISF
     C                   MOVE      WRCPAE        ARCPAE
N07  C                   MOVE      WRCPSI        ARCPSI
N07  C                   MOVE      WRCPSE        ARCPSE
     C                   MOVE      WRCINF        ARCINF
     C                   MOVE      WRCL1         ARCL1
N01  C                   MOVE      WRCMRG        ARCMRG
N02  C                   MOVE      WRINF         ARINF
N03  C                   MOVE      WRNAS         ARNAS
     C   33              WRITE     FIRMARCR
     C  N33              UPDATE    FIRMARCR
N04   *
N04   * Kjører TKpdfHandler ?? (J = Ja / Java-servervesrj, P=PC-versjon
N05   * div defaulter flyttet uten merking til test-rutine
N04  c                   move      'KJ'          ASITY
N04  c     ASITY         chain     firmarcsi                          33
N04  c                   eval      ASIANE = WRTKPD
E05  c                   eval      ASITXT = 'TKpdfHandler installed?(J/P/N)'
N04  C   33              WRITE     FIRARCSIR
N04  C  N33              UPDATE    FIRARCSIR
N05   * User for esped/webservice
N05  c                   move      'EU'          ASITY
N05  c     ASITY         chain     firmarcsi                          33
N05  c                   eval      ASIANE = WREUSR
N05  c                   eval      ASITXT = 'eSped userid/webservice'
N05  C                   if        ASIANE <> *blanks
N05  C   33              WRITE     FIRARCSIR
N05  C  N33              UPDATE    FIRARCSIR
N05  c                   else
N05  C  N33              DELETE    FIRARCSIR
N05  c                   endif
N05   * Loglevel i javabasert - (DEBUG / INFO / ERROR, Dft: INFO)
N05  c                   move      'LL'          ASITY
N05  c     ASITY         chain     firmarcsi                          33
N05  c                   eval      ASIANE = WRLOGL
N05  c                   eval      ASITXT = 'Log Level java pgm'
N05  C                   if        ASIANE <> *blanks
N05  C   33              WRITE     FIRARCSIR
N05  C  N33              UPDATE    FIRARCSIR
N05  c                   else
N05  C  N33              DELETE    FIRARCSIR
N05  c                   endif
N04   * scan-mapper
N04   * Root
N04  c                   move      'RO'          ASITY
N04  c     ASITY         chain     firmarcsi                          33
N04  c                   move      WRSROM        ASIANE
N04  c                   eval      ASITXT = 'Root folder scann'
N04  C                   if        ASIANE <> *blanks
N04  C   33              WRITE     FIRARCSIR
N04  C  N33              UPDATE    FIRARCSIR
N04  c                   else
N04  C  N33              DELETE    FIRARCSIR
N04  c                   endif
N04   * Innmappe (kun hvis 0 eller 1  - ikke tillatt hvis flere )
N04  c     *in50         ifeq      *OFF
N04  c                   move      'IN'          ASITY
N04  c     ASITY         chain     firmarcsi                          33
N04  c                   move      WRSINM        ASIANE
N04  c                   eval      ASITXT = 'Std in folder (under root)'
N04  C                   if        ASIANE <> *blanks
N04  C   33              WRITE     FIRARCSIR
N04  C  N33              UPDATE    FIRARCSIR
N04  c                   else
N04  C  N33              DELETE    FIRARCSIR
N04  c                   endif
N04  c                   endif
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
N05   ********************************************************
N05  C     testB1        BEGSR
N05   ********************************************************
N05  c                   move      '0'           *in30
N05  c                   movea     '000000000'   *in(59)
N05  c                   move      'N'           Dftsatt           1
N05   * div defaultet flyttet uten merking
N05   * Inst-kode
N04  C                   if        WRTKPD <> 'J'  and WRTKPD <> 'P'
N04  c                   eval      WRTKPD = 'N'
N04  c                   endif
N05   * user
N05  C                   if        WRTKPD <> 'J'
N05  c                   eval      WREUSR = *blanks
N05  c                   else
N05  c                   if        WREUSR = *blanks  and
N05  c                             FILIBF = 'SYSPEDF'
N05  c                   eval      WREUSR = 'NN'
N05  c                   move      'J'           Dftsatt
N05  c                   endif
N05  c                   endif
N05   * logLvl
N05  C                   if        WRTKPD <> 'J'
N05  c                   eval      WRLOGL = *blanks
N05  c                   else
N05  c                   if        WRLOGL<>'DEBUG' and
N05  c                             WRLOGL<>'ERROR' and
N05  c                             WRLOGL<>'INFO'
N05  c                   eval      WRLOGL = 'INFO'
N05  c                   move      'J'           Dftsatt
N05  c                   endif
N05  c                   endif
N05   * rootmappe
N05  C                   if        WRTKPD <> 'J' and WRTKPD <> 'P'
N05  c                   eval      WRSROM = *blanks
N05  c                   else
N05  c                   if        WRSROM = *blanks  and
N05  c                             FILIBF = 'SYSPEDF'
N05  c                   eval      WRSROM = '/sysca'
N05  c                   move      'J'           Dftsatt
N05  c                   endif
N05  c                   endif
N05   * innmappe
N05  C                   if        WRTKPD <> 'J' and WRTKPD <> 'P'
N05  c                   eval      WRSINM = *blanks
N05  c                   else
N05  c                   if        WRSINM = *blanks  and
N05  c                             FILIBF = 'SYSPEDF'
N05  c                   eval      WRSINM = 'innTKpdf'
N05  c                   move      'J'           Dftsatt
N05  c                   endif
N05  c                   endif
N05   *
N05   *
N05   * TESTER ...
N05   *
N05   * UserId
N05  c                   if        WREUSR <> *blanks or
N05  c                             WRTKPD = 'J'
N05  c     WREUSR        chain     BRIDF                              51
N05  c  N51BIFIRM        comp      FIFIRM                             5151
N05  c                   if        *in51='1'
N05  C                   MOVE      'JOV1748'     MSGNR
N05  C                   Z-ADD     22            POSNBR
N05  C                   Z-ADD     16            LINNBR
N05  C                   SETON                                        3089
N05  c                   goto      UtTestB1
N05  c                   endif
N05  c                   endif
N05   *
N05   *
N05   *
N05   *
N05   *
N05   * ingen feil....
N05   * Hvis defailter er auto-satt, gi varsel om det
N05  c                   if        *in30 = *off and
N05  c                             Dftsatt = 'J'
N05  C                   MOVE      'JOV1749'     MSGNR
N05  C                   Z-ADD     22            POSNBR
N05  C                   Z-ADD     15            LINNBR
N05  C                   SETON                                        3089
N05  c                   endif
N05   *
N05  C     UtTestB1      ENDSR
