     FDKEV      IF   E           K DISK
     FDKEH      IF   E           K DISK
     FDKTHA     IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FDKE007P   O    E             PRINTER OFLIND(*IN27) USROPN
     FFIRM      IF   E             DISK
     FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FARKIVP    O  A E           K DISK
     D                SDS
     D  ZUSER                254    263
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UKEHASYAV         4
     C                   PARM                    UKEHASYOP         7
     C                   PARM                    CALLTYP           1
     C                   MOVE      UKEHASYAV     UKEH_SYAV
     C                   MOVE      UKEHASYOP     UKEH_SYOP
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    DKEH_SYAV     UKEH_SYAV
     C     *LIKE         DEFINE    DKEH_SYOP     UKEH_SYOP
     C     *LIKE         DEFINE    KOPUNI        UOPUNI
     C     *LIKE         DEFINE    KOPAVD        UOPAVD
     C     *LIKE         DEFINE    KOPLNR        UOPLNR
     C     *LIKE         DEFINE    KFMSD         UFMSD
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UKEH_SYAV
     C                   KFLD                    UKEH_SYOP
     C     KEYB          KLIST
     C                   KFLD                    UKEH_SYAV
     C                   KFLD                    UKEH_SYOP
     C                   KFLD                    DKEV_SYLI
     C     KEYP          KLIST
     C                   KFLD                    UOPUNI
     C                   KFLD                    UOPAVD
     C                   KFLD                    UOPLNR
     C     KEYF          KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C                   EVAL      UOPUNI='P'
     C                   EVAL      UOPAVD=UKEH_SYAV
     C                   EVAL      UOPLNR=252
     C                   EVAL      UFMSD=1
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD           1024
      *______________________________________
      * Hente firmaopplysninger fra spedisjon
      *""""""""""""""""""""""""""""""""""""""
     C                   READ      FIRM
      *_____________________
      * Hente printerstyring
      *"""""""""""""""""""""
     C     KEYP          CHAIN     KODTP
     C     KEYF          CHAIN     KODTFM
      *_______________
      * Printerstyring
      *"""""""""""""""
     C     CALLTYP       IFEQ      'A'
     C                   CALL      'DKE007CA'
     C                   ELSE
     C                   EVAL      UCMD='OVRPRTF FILE(DKE007P) OUTQ('
     C     UCMD          CAT       KOPNVN:0      UCMD
     C     UCMD          CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   ENDIF
     C                   OPEN      DKE007P
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Læs varuposter                                       SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     DKEH
     C                   WRITE     DKE007PA

     C     KEYA          SETLL     DKEV
     C     KEYA          READE     DKEV                                   51
     C     *IN51         DOWEQ     *OFF
     C                   EVAL      WRUBR32=DKEV_32
     C                   MOVEL     DKEV_331      WRUBR33A
     C                   MOVE      DKEV_331      WRUBR33B
     C                   EVAL      WRUBR33C=DKEV_332
     C                   EVAL      WRUBR34=DKEV_34A
     C                   MOVEL     DKEV_37       WRUBR37A
     C                   MOVE      DKEV_37       WRUBR37B
     C*                  EVAL      WRUBR36=DKEV_36
     C*                  EVAL      WRUBR39=DKEV_39
     C                   EVAL      WRUBR35=DKEV_35
     C                   EVAL      WRUBR38=DKEV_38
     C                   EVAL      WRUBR31=DKEV_311
     C                   EVAL      WRUBR31B=DKEV_315
     C                   EVAL      WRUBR41=DKEV_412
     C                   IF        WRUBR41=999999999
     C                   EVAL      WRUBR41=*ZEROS
     C                   ENDIF
     C                   EVAL      WRUBR46=DKEV_46
     C                   MOVEL     DKEV_446B     WRUBR44
     C     WRUBR44       CAT       DKEV_312A:1   WRUBR44
     C     WRUBR44       CAT       DKEV_312B:1   WRUBR44

     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKE007PA
     C                   ENDIF
     C                   WRITE     DKE007PB

     C                   EXSR      SRBB
     C
     C     KEYA          READE     DKEV                                   51
     C                   ENDDO

     C                   WRITE     DKE007PZ
     C     CALLTYP       IFEQ      'C'
     C                   EXSR      ARKSR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv R44                                           SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR

      * Særskilda upplysningar-1
     C     DKEV_446A     IFNE      *BLANKS
     C     DKEV_446B     ORNE      *BLANKS
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKEV_446A     WRUBR44
     C     WRUBR44       CAT       DKEV_446B:1   WRUBR44
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKE007PA
     C                   ENDIF
     C                   WRITE     DKE007PD
     C                   ENDIF
      * Særskilda upplysningar-2
     C     DKEV_446C     IFNE      *BLANKS
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKEV_446C     WRUBR44
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKE007PA
     C                   ENDIF
     C                   WRITE     DKE007PD
     C                   ENDIF
      * Container 1 - 4
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKEV_312A     WRUBR44
     C     WRUBR44       CAT       DKEV_312B:1   WRUBR44
     C     WRUBR44       CAT       DKEV_312C:1   WRUBR44
     C     WRUBR44       CAT       DKEV_312D:1   WRUBR44
     C     WRUBR44       IFNE      *BLANKS
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKE007PA
     C                   ENDIF
     C                   WRITE     DKE007PD
     C                   ENDIF
      * Container 5
     C                   EVAL      WRUBR44=*BLANKS
     C                   MOVEL     DKEV_312E     WRUBR44
     C     WRUBR44       IFNE      *BLANKS
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     DKE007PA
     C                   ENDIF
     C                   WRITE     DKE007PD
     C                   ENDIF
     C
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *    Arkiver                                          ARKSR /
      /////////////////////////////////////////////////////////////
     C     ARKSR         BEGSR
      * SKAL DOKUMENT ARKIVERES ?
     C                   MOVE      'D4'          ARTYPE
     C     FIFIRM        CHAIN     FIRMARC                            34
     C  N34ARTYPE        CHAIN     ARKTXT                             34
     C     *IN(34)       IFEQ      '0'
     C     ARKJN         ANDEQ     'J'

     C                   z-add     DKEH_AVKN     ARKUND
     C                   MOVE      FIFIRM        ARFIRM
     C                   MOVE      UKEH_SYAV     ARAVD
     C                   MOVE      UKEH_SYOP     AROPD
     C                   MOVE      WDT           ARDATE
     C                   MOVE      WTM           ARTIME
     C                   MOVE      ZUSER         ARUSER
     C                   MOVE      UKEH_SYOP     ARUNDE
     C                   MOVEL     'D4:'         ARUNDE
     C                   WRITE     ARKIVR
      *_________________________
      * Aktivera arkivfunktionen  FJERNES SENERE
      *"""""""""""""""""""""""""
     C     DKEH_SYSG     CHAIN     DKTHA
     C                   CALL      'ARC002CLB'
     C                   PARM      'D4'          WATYPE            2
     C                   PARM                    ARUSER
     C                   ENDIF
     C                   ENDSR
