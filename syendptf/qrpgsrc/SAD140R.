      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE EKSPRESSFORTOLL. MANIFEST ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADEFCL3  IF   E           K DISK    RENAME(SADEFCFR:SADEFCFR3)
     FSADEFCL4  IF   E           K DISK    RENAME(SADEFCFR:SADEFCFR4)
     FSADEFCL5  IF   E           K DISK    RENAME(SADEFCFR:SADEFCFR5)
     FSADEFCFOPDUF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSAD140D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'SAD141R'
     d SAD141R         pr                  extpgm('SAD141R')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d xtype_                              like(xtype)
     d uflagg_                             like(uflagg)
      *  Prototype for SAD140R
     d sad140r         pr
      *  *ENTRY Interface for Main Procedure
     d sad140r         pi
      *
     d i               s              3s 0
     d xrecno          s              5s 0
     d xrecn2          s              5s 0
     d xtype           s              3a
     d x_efst          s                   like(efst)
     d x_fortsett      s              1a
     d x_sokok         s              1a
     d x_nysok         s              1a
     d wdt             s              8a
     d wtm             s              6a
     d win03           s              1a
     d wsjn            s              1a
     d u_avd           s              4  0
     d u_tdn           s              7  0
     d uflagg          s              1  0
     d utype           s              3a
      *
      /free

         exsr init;

         dow *in03 = *off;
           exsr srvisopd;
         enddo;

         if wsavd <> 0;
           uavd = wsavd;
         endif;

         *inlr = *on;
         return;

      // ********************************************
       begsr srvisopd;

       *in93 = *on;
       write subctl;
       *in93 = *off;
       *in94 = *off;
       zrecno = 0;
       x_fortsett = 'J';
       x_nysok = 'N';

       dow x_fortsett = 'J' and x_nysok = 'N';    // START                     // 1<b

         exsr setkey;

         dow *in03 = *off and x_nysok = 'N';                                   // 2<b
           if *in94 = *off;          // START1                                 // 3<b
           // les post til max 19 poster pr gang.
             for i = 1 to 19;                                                  // 4<b
               exsr lesrec;
               if *in94 = *off;                                                // 5<b
                 zrecno = zrecno + 1;
                 write subfil;
                 xrecn2 = zrecno;
               endif;                                                          // 5<e
             endfor;                                                           // 4<e
             xrecno = zrecno;
           endif;                                                              // 3<e

           dow *in03 = *off;             // START3                             // 3<b
           // vis subfil og få input
             *in91 = *on;
             *in92 = *on;
             write bunn;
             if zrecno = 0;                                                    // 4<b
               if *in27 = *off;                                                // 5<b
                 *in27 = *on;
                 x_fortsett = 'N';
                 leave;
               endif;                                                          // 5<e
               exfmt nyvare;
             else;                                                             // 4
               exfmt subctl;
             endif;                                                            // 4<e

             *in30 = *off;
             *in98 = *off;
             *in99 = *off;

             select;                                                           // 4<b
             when *in03;                                                       // 4
               *inlr = *on;
               return;
             when *in05;                                                       // 4
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               exsr setkey;
               leave;
             when *in06;                                                       // 4
               exsr crttur;
             when *in08 or *in12;                                              // 4
               exsr srsok;      // handle om START0
               leave;           // til start
             other;                                                            // 4
               if w2nr <> 0 or w2tdn <> 0 or wstrid <> *blanks                 // 5<b
                  or w23039e <> 0;
                 if w2nr = 0;                                                  // 6<b
                   exsr nysok;
                   x_nysok = 'J';
                   leave;          // til START
                 else;                                                         // 6
                   if w2nr = 1;                                                // 7<b
                     *in06 = *on;
                   endif;                                                      // 7<e
                   if *in06;                                                   // 7<b
                     if w2avd <> 0;                                            // 8<b
                       wsavd = w2avd;
                     endif;                                                    // 8<e
                     wsnr = 0;
                     wsavd = 0;
                     exsr crtopd;
                     leave;
                   endif;                                                      // 7<e
                   if w2tdn = 0;                                               // 7<b
                     *in98 = *on;
                     *in30 = *on;
                     msgnr = 'YBC2104';
                   else;                                                       // 7
                     exsr nytur;
                     w2tdn = 0;
                     w2trid = *blanks;
                     w23039e = 0;
                     leave;
                   endif;                                                      // 7<e

                 endif;                                                        // 6<e
               endif;                                                          // 5<e

               if zrecno <> 0;                                                 // 5<b
                 exsr hent;
               endif;                                                          // 5<e

             endsl;                                                            // 4<e

           enddo;                                                              // 3<e

         enddo;                                                                // 2<e
       enddo;                                                                  // 1<e

       endsr;

      // ********************************************
       begsr srsok;

       x_sokok = 'N';
       dow x_sokok = 'N';
         exfmt bilde01;
         *in98 = *off;
         *in99 = *off;
         *in30 = *off;
         select;
           when *in03 = *on;
             *inlr = *on;
             return;
           //when *in04 = *on;
           //  exsr sokkun;
           other;
           exsr testb1;
         endsl;
       enddo;

       *in93 = *on;
       *in94 = *off;
       write subctl;
       *in93 = *off;
       *in94 = *off;
       zrecno = 0;
       exsr setkey;

       endsr;

      // ********************************************
       begsr nysok;

       clear bilde01;
       syge15c (wdt: wtm);
       if w2avd = 0;
         wsavd = uavd;
       else;
         wsavd = w2avd;
       endif;
       select;
       when w2pro <> 0;
         wspro = w2pro;
       when w2eta <> 0;
         wseta = w2eta;
       when w2st <> *blanks;
         wsst = w2st;
       when w2dtr <> 0;
         wsdtr = w2dtr;
       endsl;

       w2nr = 0;
       w2avd = 0;
       w2pro = 0;
       w2eta = 0;
       w2st = *blanks;
       w2dtr = 0;

       *in98 = *off;
       *in99 = *off;
       exsr testb1;

       endsr;

      // ********************************************
       begsr nytur;

       if w2avd = 0;
         w2avd = uavd;
       endif;
       chain (wsavd: w2pro) sadefl01;
       if not %found;
         msgnr = 'YBC2035';
         *in98 = *on;
       else;
         *in90 = *on;
         select;
         when w2nr = 2;
          // kan ikke gå inn på normale oppdrag fra dummy
           syge15c (wdt: wtm);
           xtid_a = wtm;
           if efst <> ' ' and efst <> 'C' and efst <> 'M'
              or efeta < xidag_n or efeta = xidag_n and efetm < xtid_n;
             *in30 = *on;
             msgnr = 'YBC2036';
           else;
             exsr chgtur;
           endif;
         when w2nr = 3;
           exsr cpytur;
         when w2nr = 5;
           exsr dsptur;
         when w2nr = 9;
           exsr dsplog;
         endsl;
       endif;

       if *in98;
         *in30 = *on;
       else;
         w2avd = 0;
         w2pro = 0;
       endif;
       *in90 = *off;
       w2nr = 0;

       endsr;

      // ********************************************
       begsr hent;

       msgnr = *blanks;
       *in90 = *off;
       *in91 = *off;
       *in92 = *off;
       *in99 = *off;
       readc subfil;
       dow not %eof;
         select;
         when wsnr = 2;
           *in99 = *on;
           syge15c (wdt: wtm);
           xtid_a = wtm;
           if efst <> ' ' and efst <> 'C' and efst <> 'M'
              or efeta < xidag_n or efeta = xidag_n and efetm < xtid_n;
             *in30 = *on;
             msgnr = 'YBC2103';
             wsnr = 0;
             update subfil;
             leave;
           endif;
           exsr chgtur;
         when wsnr = 3;
           *in99 = *on;
           exsr cpytur;
         when wsnr = 5;
           *in99 = *on;
           exsr dsptur;
         when wsnr = 9;
           *in99 = *on;
           exsr dsplog;
         when wsnr = 90;
           if efst <> ' ' and efst <> 'C' and efst <> 'M';
             *in30 = *on;
             msgnr = 'YBC2103';
             wsnr = 0;
             update subfil;
             leave;
           endif;
           *in99 = *on;
           exsr sndtur;
         when wsnr = 99;
           select;
           when efeta >= xidag_n;
             *in33 = *on;
           when efst = ' ' or efst = 'C' or efst = 'M';
           // Sjekk om manifest er godkjent før
             *in33 = *on;
             u_pro_n = efpro;
             s0020 = 'EF' + u_pro_a;
             setll s0020 edis2;
             reade s0020 edis2;
             dow not %eof;
               if ssr = 'S' and s0026 = 'SADEFJSON' and sst = 'C';
                 *in33 = *off;
                 leave;
               endif;
               reade s0020 edis2;
             enddo;
           other;
             *in33 = *on;
           endsl;
           if *in33;
             *in30 = *on;
             msgnr = 'YBC2102';
             wsnr = 0;
             update subfil;
             leave;
           endif;
           *in99 = *on;
           exsr avsltur;
         endsl;
         readc subfil;
       enddo;

       if *in99 = *off;
         msgnr = 'DIV0012';
         *in30 = *on;
       endif;
       *in99 = *off;

       endsr;

      // ********************************************
       begsr setcur;

       posnbr = 35;
       select;
       when wspro <> 0;
         linnbr = 8;
       when wsuuid <> *blanks;
         linnbr = 9;
       when wseta <> 0;
         linnbr = 10;
       when wsdtr <> 0;
         linnbr = 11;
       when wsst <> *blanks;
         linnbr = 12;
       other;
         linnbr = 8;
       endsl;

       endsr;

      // ********************************************
       begsr dsptur;

       xtype = 'DSP';            // SPØRRING
       u_avd = efavd;
       u_pro = efpro;
       sad131r (u_avd: u_pro: usg: xtype: uflagg);
       select;
       when uflagg = 3;
         *inlr = *on;
         return;
       when *in90 = *off;
         wsnr = 0;
         update subfil;
       endsl;

       endsr;

      // ********************************************
       begsr chgtur;

       xtype = 'CHG';                // ENDRING
       u_avd = efavd;
       u_pro = efpro;
       sad131r (u_avd: u_pro: usg: xtype: uflagg);

       x_efst = efst;
       chain (u_avd: u_pro) sadeffopd;
       efst = x_efst;
       update sadeffr;
       X_EFETAD = EFETAD;
       X_EFETAM = EFETAM;
       X_EFETAA = EFETAA;
       X_EFATAD = EFATAD;
       X_EFATAM = EFATAM;
       X_EFATAA = EFATAA;
       X_EFDTRD = EFDTRD;
       X_EFDTRM = EFDTRM;
       X_EFDTRA = EFDTRA;
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr cpytur;

       xtype = 'CPY';                // KOPIERING
       u_avd = efavd;
       u_pro = efpro;
       sad131r (u_avd: u_pro: usg: xtype: uflagg);

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       if u_pro <> 0 and u_pro <> efpro;
         chain(n) (u_avd: u_pro) sadeffopd;
         X_EFETAD = EFETAD;
         X_EFETAM = EFETAM;
         X_EFETAA = EFETAA;
         X_EFATAD = EFATAD;
         X_EFETAM = EFATAM;
         X_EFETAA = EFATAA;
         X_EFDTRD = EFDTRD;
         X_EFDTRM = EFDTRM;
         X_EFDTRA = EFDTRA;
         xrecn2 = xrecn2 + 1;
         zrecno = xrecn2;
         write subfil;
       endif;

       endsr;

      // ********************************************
       begsr crttur;

       xtype = 'CRT';                // LAGE NY TUR
       if wsavd = 0;
         u_avd = uavd;
       else;
         u_avd = efavd;
       endif;
       u_pro = 0;
       sad131r (u_avd: u_pro: usg: xtype: uflagg);

       if u_pro > 0;
         chain(n) (u_avd: u_pro) sadeffopd;
         if %found;
           if u_avd = 0;
             wsavd = u_avd;
           else;
             uavd = u_avd;
           endif;
           X_EFETAD = EFETAD;
           X_EFETAM = EFETAM;
           X_EFETAA = EFETAA;
           X_EFETAD = EFATAD;
           X_EFETAM = EFATAM;
           X_EFETAA = EFATAA;
           X_EFDTRD = EFDTRD;
           X_EFDTRM = EFDTRM;
           X_EFDTRA = EFDTRA;
           xrecn2 = xrecn2 + 1;
           zrecno = xrecn2;
           write subfil;
         endif;
       endif;

       endsr;

      // ********************************************
       begsr dsplog;

       win03 = '8';                // Vis edi logg
       xnr11 = 0;
       xnr11_pro = efpro;
       u_avd = xnr11_avd;
       u_tdn = xnr11_tdn;
       sad003 (u_avd: u_tdn: win03);
       if win03 = '1';
         *inlr = *on;
         return;
       endif;
       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr sndtur;

       if efst = ' ' or efst = 'C' or efst = 'M';
         chain ('S': 'EXPREM') ecmsg1;
         if %found;
           // Sjekk om manifest er godkjent før
           *in51 = *on;
           u_pro_n = efpro;
           s0020 = 'EF' + u_pro_a;
           setll s0020 edis2;
           reade s0020 edis2;
           dow not %eof;
             if ssr = 'S' and s0026 = 'SADEFJSON' and sst = 'C';
               *in51 = *off;
               leave;
             endif;
             reade s0020 edis2;
           enddo;
           if *in51;
             wsst2 = 'S';
           else;
             wsst2 = 'R';
           endif;
           dow *in03 = *off and *in12 = *off;
             exfmt sendtur;
             if wsst2 <> ' ';
               *in03 = *on;
             endif;
           enddo;
           if *in12 = *off;
             u_pro_n = efpro;
             chain (efavd: efpro) sadeffopd;
             efst2 = wsst2;
             update sadeffr;
             sadefjson (u_pro_a);
             chain (efavd: efpro) sadeffopd;
             // efst = 'B';
             eferr = *blanks;
             update sadeffr;
           endif;
           *in03 = *off;
         else;
           exfmt errmsg;
         endif;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr avsltur;

       wsjn = 'J';
       *in33 = *off;
       dow *in33 = *off;
         exfmt comptur;
         if wsjn <> ' ';
           leave;
         endif;
       enddo;
       if wsjn = 'J';
         chain (efavd: efpro) sadeffopd;
         efst = 'Z';
         update sadeffr;
       endif;

       if *in90 = *off;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      // ********************************************
       begsr testb1;

       koanvn = *blanks;
       if wsavd > 0;
         chain ('A': wsavd) kodta;
         if not %found;
           linnbr = 4;
           posnbr = 26;
           msgnr = 'SPA0001';
           *in30 = *on;
           *in99 = *on;
         else;
           uavd = wsavd;
           chain (fifirm: koaknr) cundl01;
         endif;
       endif;

       *in41 = *off;
       *in42 = *off;
       *in43 = *off;
       *in44 = *off;
       *in45 = *off;
       x_sokok = 'J';
       select;
       when wspro > 0;
         *in41 = *on;
       when wsuuid <> *blanks;
         *in42 = *on;
       when wseta > 0;
         *in43 = *on;
       when wsdtr > 0;
         *in44 = *on;
       when wsst <> *blanks;
         *in45 = *on;
       other;
         x_sokok = 'N';
         msgnr = 'DIV0064';
         *in30 = *on;
         *in98 = *on;
         linnbr = 8;
         posnbr = 35;
       endsl;

       endsr;

      // ********************************************
       begsr setkey;

       select;
       when *in41;
       when *in42;
       when *in43;
         if wseta = 999999;
           xseta = 99999999;
         else;
           xsetad = wsetad;
           xsetam = wsetam;
           xsetaa = wsetaa;
           xsetac = 20;
         endif;
       when *in44;
         if wsdtr = 999999;
           xsdtr = 99999999;
         else;
           xsdtrd = wsdtrd;
           xsdtrm = wsdtrm;
           xsdtra = wsdtra;
           xsdtrc = 20;
         endif;
       when *in45;
       endsl;

       exsr mintur;

       endsr;

      // ********************************************
       begsr mintur;

       select;
       when *in41;
         if wsavd = 0;
           setll wspro sadefl02;
         else;
           setll (wsavd: wspro) sadefl01;
         endif;
       when *in42;
         setll wsuuid sadefl03;
       when *in43;
         setll xseta sadefl04;
       when *in44;
         setll xsdtr sadefl05;
       when *in45;
         setll (wsavd: wsst) sadefl06;
       endsl;

       endsr;

      // ********************************************
       begsr lesrec;

       select;
       when *in41;
         if wsavd = 0;
           read sadefl02;
         else;
           reade wsavd sadefl01;
         endif;
       when *in42;
         read sadefl03;
       when *in43;
         if xseta = 99999999;
           readp sadefl04;
         else;
           read sadefl04;
         endif;
       when *in44;
         if xsdtr = 99999999;
           readp sadefl05;
         else;
           read sadefl05;
         endif;
       when *in45;
         if wsavd = 0;
           //read wsst sadefl06;
         else;
           reade (wsavd: wsst) sadefl06;
         endif;
       endsl;

       if %eof;
         *in94 = *on;
         i = 50;
       endif;

       endsr;

      // ********************************************
       begsr init;

       wspro = 0;
       wsavd = uavd;
       wseta = 0;
       wsdtr = 999999;
       w2eta = 0;
       w2dtr = 0;
       *in41 = *off;
       *in42 = *off;
       *in43 = *off;
       *in44 = *on;
       *in45 = *off;

       setll *loval firm;
       read firm;
       chain ('A': wsavd) kodta;
       chain (fifirm: koaknr) cundl01;

       syge15c (wdt: wtm);
       xidag_a = wdt;

       endsr;

       /end-free
