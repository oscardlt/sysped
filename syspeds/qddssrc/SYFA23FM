     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDKLKNR' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde')
     A                                      CF12(12 'Gå til forrige bilde')
     A                                  1  2'SYSPED/SYFA23'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                 11  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0B 11 17DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 19MSGCON(040 SHU0532 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A            KUODEL         1   O  5 10
     A            KUOAVD         4  0O  5 20
     A            KUOOTY         2   O  5 30
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYFA23'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0532 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 25TEXT('Kundenavn')
     A                                      COLOR(WHT)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 74SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(060 SHU0533 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              5  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  4MSGCON(040 SHU0533 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSODEL         1   B  2 11
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WSOAVD         4  0B  2 24
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A            WSOOTY         2   B  2 32
     A  89                                  DSPATR(RI)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2ODEL         1   I  2 16
     A            W2OAVD         4  0I  2 21
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1  4MSGCON(040 SHU0533 *LIBL/SYMSGF)
     A            KUODEL         1   O  2 13COLOR(WHT)
     A            KUOAVD         4  0O  2 23COLOR(WHT)
     A            KUOOTY         2   O  2 33COLOR(WHT)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SYFA23'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0532 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(BLU)
     A            KNAVN         30A  O  2 25TEXT('Kundenavn')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(040 SHU0533 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
