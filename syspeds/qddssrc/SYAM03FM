********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P9XXX * 01 YBC PTF9 VISNING PÅ FLERE FELTER
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLSTED' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4' '5' '6')
     A            SFKNA          8Y 0O  5  6TEXT('AGENTNUMMER')
     A                                      EDTCDE(4)
     A            SFNVN         25A  O  5 15TEXT('NAVN')
     A            SFTYPE         1A  O  5 42
     A            SFDEL          3Y 0O  5 46EDTCDE(4)
S01  A*           SFVAL          3A  O  5 53TEXT('VALUTA')
S01  A*           SFUTV          1A  O  5 65
N01  A            SFVAL          3A  O  5 51TEXT('VALUTA')
N01  A            SFKURA         7Y 4O  5 55EDTCDE(4)
N01  A            SFAVSL         1A  O  5 64
N01  A            SFLAND         2A  O  5 68
N01  A            SFGREN         7Y 0O  5 71EDTCDE(4)
     A            SFNETT         1A  O  5 79
     A            SFLIMT         5Y 0H
     A            SFLOSS         1A  H
     A            SFLKG          5Y 0H
     A            SFKKG          5Y 0H
S01  A*           SFAVSL         1A  H
     A            SFVALA         3A  H
S01  A*           SFKURA         7Y 4H
     A            SFVALE         3A  H
     A            SFKURE         7Y 4H
     A            SFSPES         1A  H
N01  A            SFUTV          1A  H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYAM03'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2 63TIME
     A                                  1 22MSGCON(030 TGI0022 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
S01  A*                                 4  2MSGCON(079 TRO0462 *LIBL/SYMSGF)
N01  A                                  4  2MSGCON(079 TGI0001 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 29MSGCON(020 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 50MSGCON(014 TGI0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 67MSGCON(014 TGI0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A  44                                  CF04(04 'Søk kunde')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  1MSGCON(020 TRO0457 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 39MSGCON(023 TRO0458 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 63MSGCON(009 TRO0464 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  70                                  DSPATR(ND)
     A                                  1 74MSGCON(005 TRO0465 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  70                                  DSPATR(ND)
     A            WSKNA          8Y 0B  2  6TEXT('AGENTNUMMER')
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNVN         25A  B  2 15TEXT('NAVN')
     A                                      DSPATR(PR)
     A            WSTYPE         1A  B  2 41VALUES('F' 'P' 'H')
     A            WSDEL          3Y 0B  2 45CHECK(FE)
     A                                      DSPATR(UL)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A            WSVAL          3A  B  2 52TEXT('VALUTA')
     A                                      DSPATR(UL)
     A            WSUTV          1A  B  2 59VALUES(' ' 'J' 'N')
     A            WSLOSS         1A  B  2 65VALUES('J' 'N')
     A  70                                  DSPATR(ND)
     A  70                                  DSPATR(PR)
     A            WSNETT         1A  B  2 70VALUES(' ' 'A' 'B' 'N' 'P')
     A  70                                  DSPATR(ND)
     A  70                                  DSPATR(PR)
     A            WSSPES         1A  B  2 76
     A  70                                  DSPATR(ND)
     A  70                                  DSPATR(PR)
     A                                      VALUES(' ' 'J' 'N' 'S')
     A                                  3  1MSGCON(010 TRO0460 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            WSVALA         3A  B  3 12TEXT('VALUTA')
     A                                      DSPATR(UL)
     A            WSKURA         7Y 4B  3 17EDTCDE(1)
     A                                      DSPATR(UL)
     A                                  3 26MSGCON(010 TRO0461 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            WSVALE         3A  B  3 37TEXT('VALUTA')
     A                                      DSPATR(UL)
     A            WSKURE         7Y 4B  3 42EDTCDE(1)
     A                                      DSPATR(UL)
     A                                  3 52MSGCON(008 TRO0205 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  70                                  DSPATR(ND)
     A            WSLKG          5Y 0B  3 61EDTCDE(4)
     A  70                                  DSPATR(ND)
     A  70                                  DSPATR(PR)
     A                                  3 67MSGCON(008 TRO0206 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  70                                  DSPATR(ND)
     A            WSKKG          5Y 0B  3 76EDTCDE(4)
     A  70                                  DSPATR(ND)
     A  70                                  DSPATR(PR)
     A  44                              4  4MSGCON(015 TGI0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(079 TGI0001 *LIBL/SYMSGF)
     A            SFKNA          8Y 0O  2  6TEXT('AGENTNUMMER')
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A            SFNVN         25A  O  2 15TEXT('NAVN')
     A                                      DSPATR(HI)
     A            SFDEL          3Y 0O  2 46TEXT('ANDEL')
     A                                      DSPATR(HI)
     A            SFVAL          3A  O  2 51TEXT('VALUTA')
     A                                      DSPATR(HI)
     A            SFTYPE         1A  O  2 42DSPATR(HI)
     A            SFAVSL         1A  O  2 65DSPATR(HI)
     A            SFLIMT         5Y 0O  2 72DSPATR(HI)
     A                                      EDTCDE(4)
     A            SFKURS         7Y 4O  2 55DSPATR(HI)
     A                                      EDTCDE(M)
     A            SFLOSS         1A  O  2 69DSPATR(HI)
     A            SFNETT         1A  O  2 79DSPATR(HI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYKUNDE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORIGE BILDE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM03'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(030 TGI0022 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                  2 63TIME
     A                                  1 22MSGCON(030 TGI0022 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 29MSGCON(020 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(020 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 55MSGCON(020 TGI0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 50MSGCON(014 TGI0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 67MSGCON(014 TGI0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(079 TGI0001 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
