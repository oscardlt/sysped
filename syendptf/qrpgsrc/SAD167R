      *
      ***********************************************************
      *** PROGRAM SKAL VISE LOGG PÅ SENDINGSSVAR              ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FSADEXLOG1 IF   E           K DISK
     FSAD167D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  ELLTXT                 1   1024
     D  ELLTXT80               1     80
     D  ELLTXT01               1    100
     D  ELLTXT02             101    200
     D  ELLTXT03             201    300
     D  ELLTXT04             301    400
     D  ELLTXT05             401    500
     D  ELLTXT06             501    600
     D  ELLTXT07             601    700
     D  ELLTXT08             701    800
     D  ELLTXT09             801    900
     D  ELLTXT10             901   1000
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   41SADEXLOG1K1   SETGT     SADEXLOG1
     C   42SADEXLOG1K2   SETGT     SADEXLOG1
     C   41SADEXLOG1K1   READPE    SADEXLOG1                              94
     C   41              DO
     C  N94              DOW       ELTDN <> 0
     C     SADEXLOG1K1   READPE    SADEXLOG1                              94
     C  N94              ENDDO
     C                   ENDDO
     C   42SADEXLOG1K2   READPE    SADEXLOG1                              94
      *
     C                   DOW       *IN94 = *OFF
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   SELECT
     C                   WHEN      *IN41
     C     SADEXLOG1K1   READPE    SADEXLOG1                              94
     C  N94              DOW       ELTDN <> 0
     C     SADEXLOG1K1   READPE    SADEXLOG1                              94
     C  N94              ENDDO
     C                   WHEN      *IN42
     C     SADEXLOG1K2   READPE    SADEXLOG1                              94
     C                   ENDSL
     C                   ENDDO
      *
     C                   IF        ZRECNO > 0
     C                   EVAL      ZRECNO = 1
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      ***
      *
     C                   SETOFF                                       9830
      *
      *** F12 ---> AVBRYT
     C   12              GOTO      SLUTT
     C*** ROLL UP ---> BLA TIL NESTE SIDE
     C   25              DO
     C                   GOTO      START3
     C                   END
      *
      *** SJEKKE OM BRUKER HAR VALGT OPSJONER. HIVS SÅ,T UTFØRE DISSE.
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
      *** SJEKKE OM BRUKER HAR VALGT NOEN OPSJONER. HVIS IKKE, SETTER
      *** IND. 98 PÅ OG GIR FEILMELDING TIL BRUKER.
      *** OPSJON 5 ---> VISE DETALJER AV POST.
      *** LINNBR, POSNBR ---> RETUR POSISJON AV CURSOR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   ENDDO
     C     ZRECNO        DIV       12            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     12            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      VISTXT
     C   12              GOTO      SLHENT
     C                   END
     C                   ENDDO
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     VISTXT        BEGSR
      ***********************************************
      *
     C                   EXFMT     BILDE
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SADEXLOG1K1   KLIST
     C                   KFLD                    UPRO
     C     SADEXLOG1K2   KLIST
     C                   KFLD                    UPRO
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8 0
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
      *
     C                   SELECT
     C                   WHEN      UTDN = 0
     C                   MOVEA     '10'          *IN(41)
     C                   OTHER
     C                   MOVEA     '01'          *IN(41)
     C                   ENDSL
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
