********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXX * 01 YBC PTF7 BEHANDLE JUSTERINGSJOURNAL
P9XXX * 02 YBC PTF9 OPSJON 9 --> PLUKK TIL TOLLAGER
P9XXX * 03 YBC PTF9 SORTERING
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            JOSTAT         1A  H
     A            JOBDAT         8  0H
     A            JOBTID         4  0H
     A            WSNR           1A  B  6  2
N03  A  51                                  DSPATR(PR)
N02  A                                      VALUES(' ' '1' '4' '9')
S02  A*                                     VALUES(' ' '1' '4')
     A            JOFIRM         2A  O  6  5
     A            JOAAR          4  0O  6  8EDTCDE(4)
     A            JONR           5  0O  6 13EDTCDE(4)
N03  A  41
N03  AAN51                                  COLOR(WHT)
N03  A  41
N03  AA 51                                  COLOR(YLW)
     A            JEKSNR         7  0O  6 19EDTCDE(4)
N03  A  42
N03  AAN51                                  COLOR(WHT)
N03  A  42
N03  AA 51                                  COLOR(YLW)
     A            JTYPE          1A  O  6 27
     A            JBESTN         7  0O  6 30EDTCDE(4)
N03  A  43
N03  AAN51                                  COLOR(WHT)
N03  A  43
N03  AA 51                                  COLOR(YLW)
     A            JOTXT         30A  H
S03  A*           XXTXT         25A  O  6 38
N03  A            XXTXT         13A  O  6 38
N03  A            JCONR         20A  H
N03  A            XXCONR        11A  O  6 52
N03  A  44
N03  AAN51                                  COLOR(WHT)
N03  A  44
N03  AA 51                                  COLOR(YLW)
     A            JOUDAT         8  0O  6 64EDTWRD('    .  .  ')
     A            JOUTID         4  0O  6 75EDTWRD('  :  ')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'FORNY')
     A                                      CA12(12 'Cancel')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/LL072R'
     A                                      COLOR(BLU)
     A                                  1 20'Plukk lagerjournal'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Lagerkunde:'
     A            XXKJED         8  0O  2 17COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 27COLOR(WHT)
     A            ZRECNO         4S 0O  2 60SFLRCDNBR
     A                                  2 72TIME
N03  A                                  3  2'Sort: 1=Journalnr  2=Tallimann  3=-
N03  A                                      Bestilling  4=Container'
N03  A            WSVALG         1  0B  3 60DSPATR(UL)
N03  A                                      EDTCDE(4)
N03  A                                      VALUES(1 2 3 4)
     A                                  4  2'Velg opsjon,   1=Plukk   4=Slett'
     A                                      COLOR(BLU)
N02  A                                  4 38'9=Plukk til tolllager'
N02  A                                      COLOR(BLU)
     A                                  5  2'Op FK   År Journ Eksp.nr Ty Best.n-
E03  A                                      r Beskrivelse   Containernr Utlagt -
     A                                      dato/tid'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      SLNO(23)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3/F12=Avbryt   F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NOREC
     A                                      CA03(03 'Avslutt')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL072R'
     A                                      COLOR(BLU)
     A                                  1 20'Plukk lagerjournal'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Lagerkunde:'
     A            XXKJED         8  0O  2 17COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 27COLOR(WHT)
N03  A                                  3  2'Sort: 1=Journalnr  2=Tallimann  3=-
N03  A                                      Bestilling  4=Container'
N03  A            WSVALG         1  0B  3 60DSPATR(UL)
N03  A                                      EDTCDE(4)
N03  A                                      VALUES(1 2 3 4)
     A                                  5  2'Op FK   År Journ Eksp.nr Ty Best.n-
E03  A                                      r Beskrivelse   Containernr Utlagt -
     A                                      dato/tid'
     A                                      COLOR(WHT)
     A                                 23  2'F3/F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
N01   *
N01  A          R BJUST
N01  A                                      WINDOW(*DFT 13 40 *NOMSGLIN)
N01  A                                      OVERLAY
N01  A                                      CA12(12 'AVBRYT')
N01  A                                  1  2'LL072R'
N01  A                                      COLOR(BLU)
N01  A                                  1 10'Plukk lagerjournal'
N01  A                                      DSPATR(HI)
N01  A                                  2  2'Firma:'
N01  A            JOFIRM         2A  O  2  8COLOR(WHT)
N01  A                                  2 12'År:'
N01  A            JOAAR          4  0O  2 16EDTCDE(4)
N01  A                                      COLOR(WHT)
N01  A                                  2 22'Journalnr:'
N01  A            JONR           5  0O  2 33EDTCDE(4)
N01  A                                      COLOR(WHT)
N01  A                                  3  2'Justeringsjournalnr:'
N01  A            WSJN1          5  0B  3 23DSPATR(UL)
N01  A  90                                  DSPATR(HI)
N01  A  90                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
N01  A            WSJN2          5  0B  4 23DSPATR(UL)
N01  A  89                                  DSPATR(HI)
N01  A  89                                  DSPATR(PC)
N01  A            WSJN3          5  0B  5 23DSPATR(UL)
N01  A  88                                  DSPATR(HI)
N01  A  88                                  DSPATR(PC)
N01  A            WSJN4          5  0B  6 23DSPATR(UL)
N01  A  87                                  DSPATR(HI)
N01  A  87                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
N01  A            WSJN5          5  0B  7 23DSPATR(UL)
N01  A  86                                  DSPATR(HI)
N01  A  86                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
N01  A            WSJN6          5  0B  8 23DSPATR(UL)
N01  A  85                                  DSPATR(HI)
N01  A  85                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
N01  A            WSJN7          5  0B  9 23DSPATR(UL)
N01  A  84                                  DSPATR(HI)
N01  A  84                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
N01  A            WSJN8          5  0B 10 23DSPATR(UL)
N01  A  83                                  DSPATR(HI)
N01  A  83                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
N01  A            WSJN9          5  0B 11 23DSPATR(UL)
N01  A  82                                  DSPATR(HI)
N01  A  82                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
N01  A            WSJNA          5  0B 12 23DSPATR(UL)
N01  A  81                                  DSPATR(HI)
N01  A  81                                  DSPATR(PC)
N01  A                                      EDTCDE(4)
N01  A N30                             13  2'F12=Avbryt'
N01  A                                      COLOR(BLU)
N01  A  30                             13  2'Oppgitt just.journalnr er ugyldig.'
N01  A                                      COLOR(WHT)
