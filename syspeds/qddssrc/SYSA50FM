********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA670000' 'SYFA67' 'SY400-
     A                                      LO/MAINT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('Parameters for price raise')
     A                                      CF03(03 'End program')
     A                                      CF04(04 'CUSTOMER BY ALPHA')
     A                                      CF05(05 'start flytting')
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYSA50'
     A                                      COLOR(BLU)
     A N88N87                           1 19MSGCON(040 SHU0479 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  88N87                           1 19MSGCON(040 SHU0493 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  87                              1 19MSGCON(040 SHU0522 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  8  8MSGCON(023 DIV2107 *LIBL/SYMSGF)
     A            WAFRKU         8Y 0B  8 35EDTCDE(4)
     A                                      CHECK(RB)
     A  92                                  DSPATR(PC)
     A  92                                  DSPATR(RI)
     A                                  8 44MSGCON(040 SHU0482 *LIBL/SYMSGF)
     A                                  9  8MSGCON(023 DIV2101 *LIBL/SYMSGF)
     A            WAFRFR         5A  B  9 35
     A  94                                  DSPATR(PC)
     A  94                                  DSPATR(RI)
     A            WAFRFL         2A  B  9 41
     A                                  9 44MSGCON(025 SHU0483 *LIBL/SYMSGF)
     A                                 10  8MSGCON(023 DIV2103 *LIBL/SYMSGF)
     A            WAFRTI         5A  B 10 35
     A  93                                  DSPATR(PC)
     A  93                                  DSPATR(RI)
     A            WAFRTL         2A  B 10 41
     A                                 10 44MSGCON(025 SHU0483 *LIBL/SYMSGF)
     A                                 11  8MSGCON(023 SHU0480 *LIBL/SYMSGF)
     A            WAFROT         2A  B 11 35
     A                                 11 44MSGCON(010 SHU0484 *LIBL/SYMSGF)
     A                                 12  8MSGCON(023 SHU0481 *LIBL/SYMSGF)
     A            WAFRLE         3A  B 12 35
     A                                 12 44MSGCON(010 SHU0485 *LIBL/SYMSGF)
     A                                 13  8MSGCON(023 SHU0487 *LIBL/SYMSGF)
     A            WAGEDA         6Y 0B 13 35EDTCDE(4)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A                                 13 44MSGCON(022 SHU0488 *LIBL/SYMSGF)
     A N87                             15  8MSGCON(023 SHU0489 *LIBL/SYMSGF)
     A N87        WSBYTT         1A  B 15 35
     A N87                             15 44MSGCON(010 DIV0259 *LIBL/SYMSGF)
     A N87                             19  8MSGCON(030 SHU0491 *LIBL/SYMSGF)
     A N87        ANTFLY         6  0O 19 40EDTCDE(4)
     A N87                                  COLOR(WHT)
     A N87                             20  8MSGCON(030 SHU0492 *LIBL/SYMSGF)
     A N87        ANTFOR         6  0O 20 40EDTCDE(4)
     A N87                                  COLOR(WHT)
     A                                 23  2MSGCON(018 SHU0490 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
