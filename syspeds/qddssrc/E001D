     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EQUA902)
     A                                      PRINT
     A                                      CF03(03 'Gå ut')
     A                                      CF12(12 'Forrige')
      *
     A          R E001DA
     A                                      CF04(04)
     A                                      OVERLAY
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 28MSGCON(027 EDI0310 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 76'E001D'
     A                                  3  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(034 EDI0312 *LIBL/SYMSGF)
     A            WUALTA         4A  B  5 39
     A  31                                  DSPATR(PC)
     A  31                                  COLOR(RED)
     A                                      RANGE('   a' '9999')
     A                                  5 52MSGCON(018 EDI0313 *LIBL/SYMSGF)
     A                                  6  4MSGCON(032 EDI0314 *LIBL/SYMSGF)
     A            XUALCO         6A  I  6 39DFT('*ALL')
     A                                  7  4MSGCON(032 EDI0316 *LIBL/SYMSGF)
     A            XUALTX        10A  I  7 39DFT('*ALL')
     A                                      CHECK(LC)
     A                                 22  3MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 16MSGCON(014 EDI0804 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E001DB                    SFL
     A            ZOP            1A  I  9  3
     A  41                                  DSPATR(ND)
     A                                      DSPATR(PC)
     A                                      VALUES(' ' '1' '2' '3' '4' '5' '6')
     A            QUALCO    R        O  9  6
     A            QUALTX    R        O  9 13
      *
     A          R E001DC                    SFLCTL(E001DB)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Create')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'EDI    '
     A                                      COLOR(BLU)
     A                                  1 27MSGCON(029 EDI0310 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 76'E001D'
     A                                  3  2MSGCON(014 EDI0320 *LIBL/SYMSGF)
     A            WUALTA         4A  O  3 17
     A            WTAGTX        25A  O  3 22
     A                                  3 50MSGCON(018 EDI0321 *LIBL/SYMSGF)
     A            XUALCO         6A  I  3 71DFT('*ALL')
     A                                  4 50MSGCON(018 EDI0322 *LIBL/SYMSGF)
     A            XUALTX        10A  I  4 71DFT('*ALL')
     A                                      CHECK(LC)
     A                                  5  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  4'1=Velg   2=Endre   3=Kopier   4=Sl-
     A                                      ette   5=Vise  6=X-ref externe kode-
     A                                      r'
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(003 EDI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  6MSGCON(004 EDI0326 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 13MSGCON(005 EDI0327 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E001DD
     A                                      OVERLAY
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 28MSGCON(010 EDI0806 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 41MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E001DE
     A                                      OVERLAY
     A                                  1  2'EDI    '
     A                                      COLOR(BLU)
     A                                  1 29MSGCON(029 EDI0310 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 76'E001D'
     A                                  3  2MSGCON(014 EDI0320 *LIBL/SYMSGF)
     A            WUALTA         4A  O  3 17
     A            WTAGTX        25A  O  3 22
     A                                  5  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(034 EDI0332 *LIBL/SYMSGF)
     A            ÅUALCO         6A  B  7 39
     A  31                                  COLOR(RED)
     A  61                                  DSPATR(PR)
     A                                  8  4MSGCON(032 EDI0333 *LIBL/SYMSGF)
     A            ÅUALTX        60A  B  9  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A                                 11  4MSGCON(032 EDI0334 *LIBL/SYMSGF)
     A            ÅUALD1        58A  B 12  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅUALD2        58A  B 13  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅUALD3        58A  B 14  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅUALD4        58A  B 15  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅUALD5        58A  B 16  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E001DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
