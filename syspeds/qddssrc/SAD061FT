      *
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10192A* 01 YBC PTF10 SJEKK TOTAL ANTALL AVGIFTER, STOP VED > 8 AVGIFTER
10286 * 02 YBC PTF10 VEKT ER ENDRET FRA 9.0 TIL 12.3
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 03 YBC PTF11 OMBEREGNING
*****A**************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
     A          R BILDE01
     A                                      CF17(17 'Avslutt med diff')
     A                                      CF03(03 'Avslutt med diff')
     A                                      CF12(12 'Ikke godta diff og gå tilb-
     A                                      ake')
     A                                 10  5'DU HAR DIFFERANSE MELLOM FAKTURASU-
     A                                      M OG VARELINJER ----->'
     A                                      COLOR(RED)
     A            UDIFF         13  2O 10 63TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A  29                             11  5'Ved TVINN-sending er dette ikke ti-
     A                                      llatt'
     A                                      DSPATR(BL)
     A                                      COLOR(YLW)
     A                                 23  5'F3/F17 = Avslutt med diff'
     A                                      COLOR(BLU)
     A                                 23 31'F12 = Ikke godta differanse'
     A                                      COLOR(BLU)
      *
     A          R BILDE02
     A                                      CF12(12 'Avbryt uten utskrift')
     A                                      CF03(03 'Avbryt med utskrift')
     A                                      CF20(20 'Avbryt med EPOST')
     A                                      CF10(10 'Endre ekspedisjonsenhet')
     A                                      OVERLAY
     A                                  1  3'Avd.    Tdeknr.  Selgersnavn      -
     A                                                    Kjøpersnavn'
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  2  5COLOR(WHT)
     A                                      EDTCDE(4)
     A            UTDN           7Y 0O  2 11COLOR(WHT)
S03  A*                                     EDTCDE(4)
N03  A                                      EDTCDE(L)
     A            SINAS         30A  O  2 20TEXT('Navn Selger')
     A                                      COLOR(BLU)
     A            SINAK         30A  O  2 51TEXT('Navn Kjøper')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(005 DIV1938 *LIBL/SYMSGF)
     A            SIBEL5         9Y 0O  3  8TEXT('Beløp Tot Toll')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 22MSGCON(005 DIV1939 *LIBL/SYMSGF)
     A            SIBEL6         9Y 0O  3 28TEXT('Beløp Tot MVA')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 42MSGCON(005 DIV1940 *LIBL/SYMSGF)
     A            W2AVGA         9Y 0O  3 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 62MSGCON(005 DIV1941 *LIBL/SYMSGF)
     A            SIBEL7        11Y 0O  3 68TEXT('Beløp Tot avg')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6  3'Meldings funksjon  . . . :'
     A            W1N07     R        B  6 32REFFLD(M1N07 *LIBL/EDIM)
     A                                      TEXT('Meldingsfunksjon')
     A                                      DSPATR(UL)
S03  A*                                     VALUES('   ' 'DFU' 'DKO' 'DMA' 'DOM-
S03  A*                                     ' 'DFO' 'DEN')
N03  A                                      VALUES('   ' 'DFU' 'DKO' 'DMA' 'DFO-
N03  A                                      ' 'DEN' 'DEB' 'DRE' 'DSO')
E03  A N39                              6 48'DFU=Fullstendig deklarasjon'
N03  A  39                              6 48'DEB=Etterberegning'
E03  A N39                              7 48'DMA=Manuell deklarasjon'
N03  A  39                              7 48'DRE=Refusjon'
     A                                  7  3'OBS! Denne er avvist tidligere '
     A  99                                  DSPATR(RI)
     A  99                                  COLOR(RED)
     A N99                                  DSPATR(ND)
N03  A N39                              8 48'DFO=Foreløpig deklarasjon'
N03  A  39                              8 48'DSO=Statistisk oppretting'
     A                                  8  3'(Siste tilbakemelding er "IU") '
     A  99                                  DSPATR(RI)
     A N99                                  DSPATR(ND)
     A  99                                  COLOR(RED)
N03  A N39                              9 48'DEN=Endelig deklarasjon'
     A                                  9  3'F1/F3=Avbryt/IKKE send på TVINN'
     A  99                                  DSPATR(RI)
     A N99                                  DSPATR(ND)
     A  99                                  COLOR(RED)
N03  A N39                             10 48'DKO=Korrigert deklarasjon'
     A                                 10  3'Enter=Send som komplett ny sak '
     A  99                                  DSPATR(RI)
     A N99                                  DSPATR(ND)
     A  99                                  COLOR(RED)
S03  A* 27                             11  3'Avvikende eksp.en.->ofte forhåndss-
N03  A  27N39                          11  3'Avvikende eksp.en.->ofte forhåndss-
     A                                      end dekl.'
     A                                      COLOR(WHT)
S03  A*                                11 48'DOM=Krav omberegn. (IKKE I BRUK)'
S03  A* 27                             12  3'Er dette forhåndssending.:'
S03  A* 27        WSFORH         1A  B 12 32COLOR(RED)
S03  A* 27                                  DSPATR(PC)
S03  A* 27                             12 34'(J=Ja)'
S03  A* 27                             12 41'SMS til mobilnr.:'
S03  A* 27        WSMOB         15A  B 12 59COLOR(RED)
N03  A  27N39                          12  3'Er dette forhåndssending.:'
N03  A  27N39     WSFORH         1A  B 12 32COLOR(RED)
N03  A                                      DSPATR(PC)
N03  A  27N39                          12 34'(J=Ja)'
N03  A  27N39                          12 41'SMS til mobilnr.:'
N03  A  27N39     WSMOB         15A  B 12 59COLOR(RED)
     A                                 13  3'Til ekspedisjonsenhet  . :'
     A            W3039E    R        B 13 32REFFLD(EDIMR/M3039E *LIBL/EDIM)
     A N27                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A  27                                  COLOR(RED)
N03  A  39                             13 48'SO:'
N03  A  39        S3039EO1  R        O 13 52REFFLD(EDIMR/M3039E *LIBL/EDIM)
N03  A                                      COLOR(WHT)
N03  A                                      EDTCDE(4)
N03  A  39                             13 60'EB/RE:'
N03  A  39        S3039EO2  R        O 13 67REFFLD(EDIMR/M3039E *LIBL/EDIM)
N03  A                                      COLOR(WHT)
N03  A                                      EDTCDE(4)
     A                                 14  3'Ønsket behandlingsdato . :'
     A            W2005B         6Y 0B 14 32TEXT('Ønsket Beh.dato')
     A                                      DSPATR(UL)
     A                                      EDTCDE(Y)
     A                                 14 48'DD/MM/ÅÅ eller 00/00/00'
     A                                 15  3'Depositum beløp  . . . . :'
     A            W5004D    R        B 15 32REFFLD(M5004D *LIBL/EDIM)
     A                                      TEXT('Beløp Depositum')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 16  3'Ventegrupe . . . . . . . :'
     A            WVEN      R        B 16 32REFFLD(MVEN *LIBL/EDIM)
     A                                      TEXT('Ventekode')
     A                                      DSPATR(UL)
     A                                 16 48'Blank=Sende direkte '
     A                                 17  3'Testekod . . . . . . . . :'
     A            W0035     R        B 17 32REFFLD(M0035 *LIBL/EDIM)
     A                                      RANGE(' ' '2')
     A                                 17 48'Blank=Produksjon'
     A                                 18  3'Ekspederingsprioritet. . :'
     A            W9N01     R        B 18 32REFFLD(M9N01 *LIBL/EDIM)
     A                                      TEXT('Meldingsprioritet')
     A                                      RANGE(0 4)
     A                                      EDTCDE(4)
     A                                      DSPATR(PR)
     A                                 18 48'1-4 (1=Express)'
     A                                 23  3'F3=Avbryt med utskrift'
     A                                      COLOR(BLU)
     A                                 23 27'F10=Endre ekspdisjonsenhet'
     A                                      COLOR(BLU)
     A                                 23 55'F12=Avbryt uten utskrift'
     A                                      COLOR(BLU)
     A  30                             24  2'Du må benytte en av de 6 melding I-
     A                                      D på skjermen'
     A                                      COLOR(WHT)
     A                                 22  3MSGCON(027 SHU1359 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BILDE03
     A                                      CF12(12 'TILBAKE')
     A                                  8  5'BRUTTO VEKT PÅ HOVED-SIDE.......:'
     A            SIVKB          9Y 0O  8 40TEXT('BRUTTO VEKT')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10  5'SUM AV BRUTTO VEKT PÅ VARELINJER:'
S02  A*           WSVKB          9Y 0O 10 40TEXT('BRUTTO VEKT')
N02  A            WSVKB         12Y 3O 10 40TEXT('BRUTTO VEKT')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12  5'DIFFERANSE BRUTTO VEKT..........:'
S02  A*           WSVKTD         9Y 0O 12 40TEXT('DIFF AV BRUTTO VEKT')
N02  A            WSVKTD        12Y 3O 12 40TEXT('DIFF AV BRUTTO VEKT')
     A                                      COLOR(RED)
     A                                      EDTCDE(L)
     A                                 14  5'AKSEPTERER DU DIFFERANSE ?       '
     A            WSJN           1A  B 14 40DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 14 50'(J/N)'
     A                                 23  5'F12 = FORRRIGE'
     A                                      COLOR(BLU)
N01   *
N01  A          R BILDE04
N01  A                                 10  5'TOTAL ANTALL AVGIFTER ER FLERE ENN-
N01  A                                       8.'
N01  A                                      COLOR(RED)
N01  A            AV01           2   O 12  5
N01  A            AV02           2   O 12  9
N01  A            AV03           2   O 12 13
N01  A            AV04           2   O 12 17
N01  A            AV05           2   O 12 21
N01  A            AV06           2   O 12 25
N01  A            AV07           2   O 12 29
N01  A            AV08           2   O 12 33
N01  A            AV09           2   O 12 37
N01  A            AV10           2   O 12 41
N01  A            AV11           2   O 12 45
N01  A            AV12           2   O 12 49
N01  A            AV13           2   O 12 53
N01  A            AV14           2   O 12 57
N01  A            AV15           2   O 12 61
N01  A            AV16           2   O 12 65
N01  A            AV17           2   O 12 69
N01  A            AV18           2   O 12 73
N01  A                                 14  5'TVINN godtar max 8 avgifter.'
N01  A                                      COLOR(YLW)
N01  A                                 16  5'Du må dele denne fortollingen til -
N01  A                                      flere fortollinger '
N01  A                                 17  5'slik at ingen har flere enn 8 avgi-
N01  A                                      fter.'
