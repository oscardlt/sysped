     A*%%TS  SD  20221109  172649  JOVO        REL-V7R4M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 20 125 *NOMSGLIN)
     A                                      OVERLAY
     A*
     A          R BILDE01
     A*%%TS  SD  20221109  172649  JOVO        REL-V7R4M0  5770-WDS
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søking')
     A                                      CF05(05 'Deklar/repre.')
     A                                      CF10(10 'Lagres u/test')
     A                                      CA12(12 'Avbryt')
     A                                      CF18(18 'Flere')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD164R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med oppdrag (HouseConsignme-
     A                                      nt)'
     A                                      COLOR(WHT)
     A                                  2  2'Avd.:'
     A            EMAVD          4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Turnr:'
     A            EMPRO          8Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 29'M-ID:'
     A            EMMID         18A  O  2 35TEXT('MasterID')
     A                                      COLOR(BLU)
     A                                  2 55'House MRN:'
     A            EHMID     R        O  2 66REFFLD(SADEXHFR/EHMID *LIBL/SADEXHF)
     A                                      COLOR(WHT)
     A*
     A  51                              3  2'OBS - Splittet oppdrag - se valg 1-
     A                                      1 for detaljer'
     A                                      COLOR(YLW)
     A                                  3 55'House LRN:'
     A            EHUUID    R        O  3 66REFFLD(SADEXHFR/EHUUID *LIBL/SADEXH-
     A                                      F)
     A                                      COLOR(WHT)
     A*
     A                                  4  2'Avdeling:'
     A            WSAVD          4Y 0O  4 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 20'Oppdrag:'
     A            WSTDN          7Y 0O  4 29TEXT('Oppdragsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 37'Status:'
     A            EHST           1A  O  4 45TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A* consigmentHouseLevel
     A                                  5  2'Container:'
     A            WSCNIN         2Y 0B  5 15TEXT('Container indikator')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N99                                  COLOR(YLW)
     A  99                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSCNR         11A  B  5 18TEXT('Containernr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A                                  5 30'Vekt:'
     A            WSVKB         13Y 3B  5 36TEXT('Total vekt')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5 51'Koll:'
     A            WSNTK          7Y 0B  5 57TEXT('Total kolli')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5 65'Varebeskr:'
     A            WSVT          30A  B  5 76TEXT('Varebeskrivelse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
     A* importProcedure
     A                                  6  2'Imp.pros:'
     A            WSPR           2A  B  6 15TEXT('Prosedyre')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A            WSPRT         30A  H
     A            WSPRT20       20A  O  6 18TEXT('Prosedyre')
     A                                      COLOR(WHT)
     A* outgoingProcedure
     A                                  6 39'Utg.pros:'
     A            WSUPR          5A  B  6 49TEXT('UtgåendeProsedyre')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N95                                  COLOR(YLW)
     A  95                                  COLOR(WHT)
     A            WSUPRT        30A  H
     A* exportFromEU
     A                                  6 55'Eksp. EU:'
     A            WSETYP         2A  B  6 65TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N94                                  COLOR(YLW)
     A  94                                  COLOR(WHT)
     A            WSETYPT       30A  H
     A            WSETYPT15     15A  O  6 68TEXT('Type tekst')
     A                                      COLOR(WHT)
     A                                  6 84'Eksp.Id:'
     A            WSEID         18A  B  6 93TEXT('Eksport Id')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A* Norsk import
     A                                  7  2'NO Imp Dekl:'
     A            WSRG          11A  B  7 15TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A                                  7 27'Dato:'
     A            WS0068A        8Y 0B  7 33TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N59                                  COLOR(YLW)
     A  59                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7 42'Sekvens:'
     A            WS0068B        6Y 0B  7 51TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N58                                  COLOR(YLW)
     A  58                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A* NCTS
     A                                  7 62'NCTS MRN.nr:'
     A            WSTRNR        18A  B  7 75TEXT('MRN-nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N91                                  COLOR(YLW)
     A  91                                  COLOR(WHT)
     A                                  7 94'Type:'
     A            WSTRTY         4A  B  7100TEXT('Ref.type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  90                                  COLOR(WHT)
     A                                  8  2'Fra sted:'
     A            WSLKF          2A  B  8 15TEXT('Fra land')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A            WSSDF          5A  B  8 18TEXT('Fra sted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
     A            WSSDFT        30A  B  8 24TEXT('Fra sted navn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 55'Til sted:'
     A            WSLKT          2A  B  8 65TEXT('Til land')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N87                                  COLOR(YLW)
     A  87                                  COLOR(WHT)
     A            WSSDT          5A  B  8 68TEXT('Til sted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N86                                  COLOR(YLW)
     A  86                                  COLOR(WHT)
     A            WSSDTT        30A  B  8 74TEXT('Til sted navn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A* additionalFiscalReference
     A                                  9  2'MVA-nr:'
     A            WSRGA         17A  B  9 15TEXT('MVA.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N85                                  COLOR(YLW)
     A  85                                  COLOR(WHT)
     A                                  9 33'Aktørens rolle:'
     A            WSRGRO         3A  B  9 49TEXT('MVA-aktørens rolle')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N84                                  COLOR(YLW)
     A  84                                  COLOR(WHT)
     A* additionalSupplyChainActor
     A                                  9 55'Øko-operator:'
     A            WSRGE         17A  B  9 69TEXT('Autorisert økonomi')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9 87'Operatør rolle:'
     A            WSROE          2A  B  9103TEXT('Rollen operatør')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N83                                  COLOR(YLW)
     A  83                                  COLOR(WHT)
     A* Avsender
     A                                 10  2'Avsender:'
     A            WSKNS          8Y 0B 10 15TEXT('Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N82                                  COLOR(YLW)
     A  82                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10 24'Org.nr.:'
     A            WSRGS         17A  B 10 33TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N81                                  COLOR(YLW)
     A  81                                  COLOR(WHT)
     A                                 10 53'Navn:'
     A            WSNAS         30A  B 10 59TEXT('Navn Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N80                                  COLOR(YLW)
     A  80                                  COLOR(WHT)
     A            WSNA2S        30A  B 10 90TEXT('Navn Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11  2'     Adr.:'
     A            WSAD1S        30A  B 11 15TEXT('Adr-1 Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N79                                  COLOR(YLW)
     A  79                                  COLOR(WHT)
     A            WSNRS         15A  B 11 46TEXT('Husnr Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNS          9A  B 11 62TEXT('Postnr Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N78                                  COLOR(YLW)
     A  78                                  COLOR(WHT)
     A            WSPSS         24A  B 11 72TEXT('Adr-3 Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N77                                  COLOR(YLW)
     A  77                                  COLOR(WHT)
     A                                 11 97'LK:'
     A            WSLKS          2A  B 11101TEXT('Landkode Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N76                                  COLOR(YLW)
     A  76                                  COLOR(WHT)
     A                                 11104'PB:'
     A            WSPBS         15A  B 11108TEXT('Postboks Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12  2'Tlf/epost:'
     A            WSEMS         50A  B 12 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N75                                  COLOR(YLW)
     A  75                                  COLOR(WHT)
     A                                      CHECK(LC)
     A                                 12 66'Type:'
     A            WSEMST         2A  B 12 72TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'TE' 'EM')
     A                                 12 77'(TE eller EM)'
     A* Mottaker
     A                                 13  2'Mottaker:'
     A            WSKNM          8Y 0B 13 15TEXT('Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N74                                  COLOR(YLW)
     A  74                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 13 24'Org.nr.:'
     A            WSRGM         17A  B 13 33TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N73                                  COLOR(YLW)
     A  73                                  COLOR(WHT)
     A                                 13 53'Navn:'
     A            WSNAM         30A  B 13 59TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N72                                  COLOR(YLW)
     A  72                                  COLOR(WHT)
     A            WSNA2M        30A  B 13 90TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14  2'     Adr.:'
     A            WSAD1M        30A  B 14 15TEXT('Adr-1 Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N71                                  COLOR(YLW)
     A  71                                  COLOR(WHT)
     A            WSNRM         15A  B 14 46TEXT('Husnr Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNM          9A  B 14 62TEXT('Postnr Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N70                                  COLOR(YLW)
     A  70                                  COLOR(WHT)
     A            WSPSM         24A  B 14 72TEXT('Adr-3 Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N69                                  COLOR(YLW)
     A  69                                  COLOR(WHT)
     A                                 14 97'LK:'
     A            WSLKM          2A  B 14101TEXT('Landkode Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N68                                  COLOR(YLW)
     A  68                                  COLOR(WHT)
     A                                 14104'PB:'
     A            WSPBM         15A  B 14108TEXT('Postboks Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 15  2'Tlf/epost:'
     A            WSEMM         50A  B 15 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N67                                  COLOR(YLW)
     A  67                                  COLOR(WHT)
     A                                      CHECK(LC)
     A                                 15 66'Type:'
     A            WSEMMT         2A  B 15 72TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'TE' 'EM')
     A* transportDocumentHouseLevel
     A                                 16  2'Trans.dok.:'
     A*           WSDKH         50A  B 16 15TEXT('House Dokumentnr')
     A            WSDKH         50A  O 16 15TEXT('House Dokumentnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 66'Type:'
     A*           WSDKHT         4A  B 16 72TEXT('House Dokumenttype')
     A            WSDKHT         4A  O 16 72TEXT('House Dokumenttype')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 77'Bet.måte:'
     A            WSTCMP         1A  B 16 87TEXT('Betalingsmåte')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 16 89'Val:'
     A            WSTCVA         3A  B 16 94TEXT('Valutakode')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N66                                  COLOR(YLW)
     A  66                                  COLOR(WHT)
     A                                 16 98'Frakt:'
     A            WSTCBL        11Y 2B 16105TEXT('Beløp')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N65                                  COLOR(YLW)
     A  65                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A* countryOfRoutingOfConsinments
     A                                 17  2'Rute:'
     A            WSLKR1         2A  B 17 15TEXT('Landkode1 reiserute')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N64                                  COLOR(YLW)
     A  64                                  COLOR(WHT)
     A            WSLKR2         2A  B 17 18TEXT('Landkode2 reiserute')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N64                                  COLOR(YLW)
     A  64                                  COLOR(WHT)
     A            WSLKR3         2A  B 17 21TEXT('Landkode3 reiserute')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N64                                  COLOR(YLW)
     A  64                                  COLOR(WHT)
     A            WSLKR4         2A  B 17 24TEXT('Landkode4 reiserute')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N64                                  COLOR(YLW)
     A  64                                  COLOR(WHT)
     A            WSLKR5         2A  B 17 27TEXT('Landkode5 reiserute')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N64                                  COLOR(YLW)
     A  64                                  COLOR(WHT)
     A            WSLKR6         2A  B 17 30TEXT('Landkode6 reiserute')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N64                                  COLOR(YLW)
     A  64                                  COLOR(WHT)
     A            WSLKR7         2A  B 17 33TEXT('Landkode7 reiserute')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N64                                  COLOR(YLW)
     A  64                                  COLOR(WHT)
     A            WSLKR8         2A  B 17 36TEXT('Landkode8 reiserute')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N64                                  COLOR(YLW)
     A  64                                  COLOR(WHT)
     A* Passiv kjøretøy
     A                                 17 42'Passiv tr.m:'
     A            WSPMRK        30A  B 17 55TEXT('Kjennetegn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 17 86'Type:'
     A            WSPTYP         2A  B 17 92TEXT('Kjøretøytype')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N63                                  COLOR(YLW)
     A  63                                  COLOR(WHT)
     A                                 17 95'Tr.M.ty:'
     A            WSPTM          4A  B 17104TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N62                                  COLOR(YLW)
     A  62                                  COLOR(WHT)
     A            WSPTMT        50A  H      TEXT('Transportmåte tekst')
     A                                 17109'LK:'
     A            WSPLK          2A  B 17113TEXT('nasjonalitet')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N61                                  COLOR(YLW)
     A  61                                  COLOR(WHT)
     A* Bilnr
     A*                                18  2'Bilnr:'
     A*           WSTRID        17A  B 18 15TEXT('Bilnr')
     A*                                     DSPATR(UL)
     A* 49                                  DSPATR(PR)
     A*                                     COLOR(YLW)
     A* UCR reference
     A                                 18  2'UCR:'
     A            WSUCR         50A  B 18 15TEXT('Ref.nr UCR')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N60                                  COLOR(YLW)
     A  60                                  COLOR(WHT)
     A*
     A  49                             19  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             19  2'F4=Søk   F5=Deklarant/representati-
     A                                      v   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             19 53'F10=Lagres u/test'
     A                                      COLOR(RED)
     A                                 19 73'F18=Flere export-id'
     A                                      COLOR(BLU)
     A  30        MSGTXT       100A  O 20  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BILDE02
     A*%%TS  SD  20221025  084952  JOVO        REL-V7R4M0  5770-WDS
     A                                      WINDOW(*DFT 9 80 *NOMSGLIN)
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Avbryt')
     A                                      CF18(18 'Flere')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD164R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med oppdrag (HouseConsignme-
     A                                      nt)'
     A                                      COLOR(WHT)
     A                                  2  2'Avd.:'
     A            EMAVD          4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Turnr:'
     A            EMPRO          8Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 29'M-ID:'
     A            EMMID         18A  O  2 35TEXT('MasterID')
     A                                      COLOR(BLU)
     A*
     A                                  4  2'Avdeling:'
     A            WSAVD          4Y 0O  4 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 20'Oppdrag:'
     A            WSTDN          7Y 0O  4 29TEXT('Oppdragsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 37'Status:'
     A            EHST           1A  O  4 45TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  2'Vekt:'
     A            WSVKB         13Y 3B  5 15TEXT('Total vekt')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N99                                  COLOR(YLW)
     A  99                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A* Avsender
     A                                  6  2'Mottaker:'
     A            WSKNM          8Y 0B  6 15TEXT('Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6 25'Navn:'
     A            WSNAM         30A  B  6 32TEXT('Navn Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
     A* transportDocumentHouseLevel
     A                                  7  2'Trans.dok.:'
     A            WSDKH         50A  B  7 15TEXT('House Dokumentnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7 66'Type:'
     A            WSDKHT         4A  B  7 72TEXT('House Dokumenttype')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A*
     A  49                              8  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                              8  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  9  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BILDE03
     A*%%TS  SD  20221025  084952  JOVO        REL-V7R4M0  5770-WDS
     A                                      WINDOW(*DFT 10 125 *NOMSGLIN)
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD161R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med deklarant/representativ'
     A                                      COLOR(WHT)
     A* Deklarant
     A                                  3  2'Deklarant:'
     A            WSKND          8Y 0B  3 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N99                                  COLOR(YLW)
     A  99                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 24'Org.nr.:'
     A            WSRGD         17A  B  3 33TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A                                  3 53'Navn:'
     A            WSNAD         30A  B  3 59TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
     A            WSNA2D        30A  B  3 90TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  4  2'     Adr.:'
     A            WSAD1D        30A  B  4 15TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A            WSNRD         15A  B  4 46TEXT('Husnr deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPND          9A  B  4 62TEXT('Postnr Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N95                                  COLOR(YLW)
     A  95                                  COLOR(WHT)
     A            WSPSD         24A  B  4 72TEXT('Adr-3 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N94                                  COLOR(YLW)
     A  94                                  COLOR(WHT)
     A                                  4 97'LK:'
     A            WSLKD          2A  B  4101TEXT('Landkode Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A                                  4104'PB:'
     A            WSPBD         15A  B  4108TEXT('Postboks deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  5  2'Tlf/epost:'
     A            WSEMD         50A  B  5 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A                                      CHECK(LC)
     A                                  5 66'Type:'
     A            WSEMDT         2A  B  5 72TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'TE' 'EM')
     A                                  5 77'(TE eller EM)'
     A* Representative
     A                                  6  2'Represent:'
     A            WSKNR          8Y 0B  6 15TEXT('Representativ')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N91                                  COLOR(YLW)
     A  91                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  6 24'Org.nr.:'
     A            WSRGR         17A  B  6 33TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  90                                  COLOR(WHT)
     A                                  6 51'St:'
     A            WSSTR          1A  B  6 55TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A                                      VALUES(' ' '2' '3')
     A                                  6 57'Navn:'
     A            WSNAR         30A  B  6 63TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
     A            WSNA2R        30A  B  6 93TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7  2'     Adr.:'
     A            WSAD1R        30A  B  7 15TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N87                                  COLOR(YLW)
     A  87                                  COLOR(WHT)
     A            WSNRR         15A  B  7 46TEXT('Husnr deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNR          9A  B  7 62TEXT('Postnr Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N86                                  COLOR(YLW)
     A  86                                  COLOR(WHT)
     A            WSPSR         24A  B  7 72TEXT('Adr-3 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N85                                  COLOR(YLW)
     A  85                                  COLOR(WHT)
     A                                  7 97'LK:'
     A            WSLKR          2A  B  7101TEXT('Landkode Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N84                                  COLOR(YLW)
     A  84                                  COLOR(WHT)
     A                                  7104'PB:'
     A            WSPBR         15A  B  7108TEXT('Postboks deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8  2'Tlf/epost:'
     A            WSEMR         50A  B  8 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N83                                  COLOR(YLW)
     A  83                                  COLOR(WHT)
     A                                      CHECK(LC)
     A                                  8 66'Type:'
     A            WSEMRT         2A  B  8 72TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'TE' 'EM')
     A                                  8 77'(TE eller EM)'
     A N49                              9  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  49                              9  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT       100A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
