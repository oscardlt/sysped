      *********************************************************************
      * Spørring på brukte lagerlokasjoner: hent vareeier/leverandør
      *
CRTPG+*  CRTPGM SYSPED/SYL011 MODULE(*LIBL/SYL011
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FCUNDL01   IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FKUFAST    IF   E           K DISK    USROPN
     FLLGrpK05  if   e           k disk    USROPN
     FLLVADR    IF   E           K DISK    USROPN
     FLLVADR3   IF   E           K DISK    USROPN
     F                                     RENAME(LLVADRR:LLVADRR3)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
     D UsrSpcName      s             10
     D lstbrd          s              1
      * Variables received from the input string
      * and converted to numeric
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D  PAVRDA                 1     50
     D  VE                     1     50    DIM(10)
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * Hent VAREEIER
     C                   exsr      HENTVE
      * Hent Transportørsnr
     C                   exsr      HENTLEV
     C     slutt         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   MOVE      'N'           XFEIL             1
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updhtmlvar('WSNA':KNAVN)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WSGRP':
     C                             %trim(%editC(GKGRP:'Z')))
     C                   callp     updhtmlvar('WSPNR':
     C                             %trim(%editC(GKPNR:'Z')))
     C                   callp     updhtmlvar('WSDT':
     C                             %trim(%editW(BIDTF:'    .  .  ')))
     C                   callp     updHtmlVar('WSANR':'')
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYL011'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     HentVE        begsr
      * VIS *ALL
     C                   EVAL      KFKOD  = '*ALL'
     C                   EVAL      KFTXT  = '*ALL'
     C                   callp     updhtmlvar('WSVE1':KFKOD)
     C                   callp     updhtmlvar('WSVE2':KFTXT)
     C                   callp     wrtsection('typVEtrow')
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   EVAL      PAID = 'LLVE'
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   END
     C                   END
      *
     C                   IF        *IN33
     C     KUFASTK       SETLL     KUFAST
     C     KUFASTK       READE     KUFAST                                 33
     C  N33              IF        KFKOD = ' DEFN'
     C     KUFASTK       READE     KUFAST                                 33
     C                   END
     C                   DOW       *IN33 = *OFF
     C                   callp     updhtmlvar('WSVE1':KFKOD)
     C                   callp     updhtmlvar('WSVE2':KFTXT)
     C                   callp     wrtsection('typVEtrow')
     C     KUFASTK       READE     KUFAST                                 33
     C                   ENDDO
      *
     C                   ELSE
     C     1             DO        10            XN                3 0
     C                   IF        VE(XN) <> *BLANKS
     C                   EVAL      KFKOD = VE(XN)
     C     KUFASTK1      CHAIN     KUFAST                             33
     C   33              EVAL      KfTXT = ' ER *UKJENT'
     C                   callp     updhtmlvar('WSVE1':KfKod)
     C                   callp     updhtmlvar('WSVE2':KFTXT)
     C                   callp     wrtsection('typVEtrow')
     C                   end
     C                   enddo
      *
     C                   END
      *
     C                   callp     wrtsection('typVEtend')
      *
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     HentLEV       begsr
      * VIS *ALL
     C                   EVAL      VAKURE = '*ALL'
     C                   EVAL      VADRNA = '*ALL'
     C                   callp     updhtmlvar('WSLEV1':VAKURE)
     C                   callp     updhtmlvar('WSLEV2':VADRNA)
     C                   callp     wrtsection('typLEVrow')
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   EVAL      PAID = 'LLLEV'
     C     BRPARFK       CHAIN     BRPARF                             40
      *
     C                   IF        *IN40
      *
     C     LLVADRK       SETLL     LLVADR
     C     LLVADRK       READE     LLVADR                                 40
     C                   DOW       *IN40 = *OFF
     C                   callp     updhtmlvar('WSLEV1':VAKURE)
     C                   callp     updhtmlvar('WSLEV2':VADRNA)
     C                   callp     wrtsection('typLEVrow')
     C     LLVADRK       READE     LLVADR                                 40
     C                   ENDDO
      *
     C                   ELSE
      *
     C                   DOW       *IN40 = *OFF
     C                   EVAL      VAKURE = PAVRDA
     C     LLVADR3K      CHAIN     LLVADR3                            33
     C   33              EVAL      VADRNA = '*UKJENT'
     C                   callp     updhtmlvar('WSLEV1':VAKURE)
     C                   callp     updhtmlvar('WSLEV2':VADRNA)
     C                   callp     wrtsection('typLEVrow')
     C     BRPARFK       READE     BRPARF                                 40
     C  N40              ENDDO
      *
     C                   END
      *
     C                   callp     wrtsection('typLEVend')
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      CUNDL01                              50
     C                   open      BRPARF                               50
     C                   open      KUFAST                               50
     C                   open      LLGrpK05                             50
     C                   open      LLVADR                               50
     C                   open      LLVADR3                              50
      *
     C     CUND1K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     BRPARFK       klist
     C                   kfld                    BIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     KUFASTK       klist
     C                   kfld                    KFTYP
     C                   MOVEL     'VE'          KFTYP
     C                   MOVE      BIKUNR        KFTYP
     C     KUFASTK1      klist
     C                   kfld                    KFTYP
     C                   kfld                    KFKOD
     C     LLVADRK       klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     LLVADR3K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   kfld                    VAKURE
      *
     C     CUND1K        CHAIN     CUNDL01                            50
     C     BIKUNR        chain     LLGrpK05                           50
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     CUNDL01
     C                   close     BRPARF
     C                   close     KUFAST
     C                   close     LLGrpK05
     C                   close     LLVADR
     C                   close     LLVADR3
     C                   eval      *inlr = *on
      *
     C                   endsr
