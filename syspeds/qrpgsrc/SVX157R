********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VAREBESKRIVELSE ER ENDRET
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT LOSSETILLATELSE              ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 1-26                             ***
      *** LEDIGE INDIKATORER 27-29 31 32 37-40 42-97 99
      ***********************************************************
     H DECEDIT('0,') DATEDIT(*DMY.)
     FKODTFM    IF   E           K DISK
     FSVNH      UF   E           K DISK
     FSVNH06    IF   E           K DISK
     F                                     RENAME(SVNHR:SVNH06R)
     FSVNHFS    IF   E           K DISK
     FSVNL      IF   E           K DISK
     FSVNLCN    IF   E           K DISK
     FSVNLDK    IF   E           K DISK
     FSVNLMN    IF   E           K DISK
     FSVNLKL    IF   E           K DISK
     FSVXKODL01 IF   E           K DISK
     FKODTP     IF   E           K DISK
     FSVX157P   O    E             PRINTER USROPN
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(SVX157P) -
     D                                     TOFILE(SVX157P) -
     D                                     OUTQ(')
     D XCMD21          C                   CONST('PAGESIZE(72 120) -
     D                                     CPI(15) -
     D                                     OVRFLW(65) -
     D                                     USRDTA(')
     D XCMD31          C                   CONST(') FORMTYPE(')
     D                 DS
     D  TVMN                   1     42
     D  TVMNXX                 1     17
     D                 DS
S01  D* TVVT                   1    140
N01  D  TVVT                   1     70
     D  TVVT48                 1     48
     D                 DS
     D  TVDREF                 1     35
     D  XVDREF                 1     27
     D                 DS
     D  XDFKD1                 1     20
     D  XDFKD2                21     40
     D  XDFKD3                41     60
     D  XDFKD4                61     80
     D  FS                     1   1800    DIM(90)
      *
     C     *ENTRY        PLIST
     C                   PARM                    DTUSER           10
     C                   PARM                    UWS              10
     C                   MOVE      'K'           XST               1
     C                   Z-ADD     1024          UCMDL            15 5
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      *
     C     START0        TAG
     C                   EXSR      INIT
     C     UWS           IFNE      'TVINR'
     C                   OPEN      SVX157P
     C                   EXSR      PRINT
     C                   CLOSE     SVX157P
     C                   GOTO      SLUTT
     C                   END
      *
     C     SVH06K        SETLL     SVNH06                             30
     C     START         TAG
     C     SVH06K        READE     SVNH06                                 30
     C  N30              DO
     C     TIAVD         CABNE     UAVD          START0
     C                   OPEN      SVX157P
     C                   EXSR      PRINT
     C                   CLOSE     SVX157P
     C                   GOTO      START
     C                   END
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     PRINT         BEGSR
      ***********************************************
     C     SVHK          CHAIN     SVNH                               33
     C   33              GOTO      SLPRI
      *
     C     TIDFKD        IFEQ      *BLANKS
     C                   MOVE      *ZEROS        XN                3 0
     C                   ELSE
     C                   Z-ADD     1             XN
     C                   MOVEL     TIDFKD        FS(XN)
     C                   END
     C     SVHK          SETLL     SVNHFS
     C     SVHK          READE     SVNHFS                                 33
     C     *IN33         DOWEQ     '0'
     C                   ADD       1             XN
     C                   MOVEL     TIDFKD        FS(XN)
     C     SVHK          READE     SVNHFS                                 33
     C  N33              ENDDO
      *
     C                   EXSR      PRINT2
      *
     C     UWS           IFEQ      'TVINR'
     C     TILSTL        IFGT      0
     C                   MOVE      'V'           TIST
     C                   ELSE
     C                   MOVE      'U'           TIST
     C                   END
     C                   MOVE      *BLANKS       TIWS
     C                   END
     C                   EXCEPT    UPDTRH
      *
     C     SLPRI         ENDSR
      *
      ***********************************************
     C     PRINT2        BEGSR
      ***********************************************
     C                   WRITE     SVX157P1
     C     FS(1)         IFNE      *BLANKS
     C                   MOVE      '0'           *IN41
     C                   WRITE     SVX157P21
     C                   Z-ADD     5             XN
     C     XN            DOWLT     88
     C     FS(XN)        IFNE      *BLANKS
     C                   MOVE      '1'           *IN41
     C                   MOVE      FS(XN)        XDFK01
     C     XN            ADD       1             XN2               3 0
     C                   MOVE      FS(XN2)       XDFK02
     C                   ADD       1             XN2
     C                   MOVE      FS(XN2)       XDFK03
     C                   ADD       1             XN2
     C                   MOVE      FS(XN2)       XDFK04
     C                   WRITE     SVX157P22
     C                   END
     C                   ADD       4             XN
     C                   ENDDO
     C                   END
     C                   WRITE     SVX157P3
      *
      *
     C     SVHK          SETLL     SVNL
     C     SVHK          READE     SVNL                                   33
     C     *IN33         DOWEQ     '0'
     C                   EXSR      HENTOP
     C                   MOVE      '0'           *IN98
     C                   WRITE     SVX157P4
     C                   EXSR      NYSIDE
     C                   MOVE      '1'           *IN98
      *
      * LES ANTALL EKSTRA MERKE & NR, CONTAINERNR
     C     SVVK          SETLL     SVNLCN
     C     SVVK          SETLL     SVNLKL
     C                   MOVE      *BLANKS       TVVT
     C                   MOVEA     '000'         *IN(34)
     C     STPR21        TAG
     C  N35SVVK          READE     SVNLCN                                 35
     C  N36SVVK          READE     SVNLKL                                 36
     C     *IN36         IFEQ      '0'
     C     SVVKMN        CHAIN     SVNLMN                             34
     C                   ELSE
     C                   MOVE      '1'           *IN34
     C                   END
     C  N34
     CORN35              DO
     C   34              MOVE      *BLANKS       TVMN
     C   35              MOVE      *BLANKS       TVCNR
     C   36              MOVE      *BLANKS       TVEH
     C   36              MOVE      *ZEROS        TVNT
     C   36              MOVE      *ZEROS        TVNTEH
     C                   WRITE     SVX157P4
     C                   EXSR      NYSIDE
     C                   GOTO      STPR21
     C                   ENDDO
      *
     C                   EXSR      SKILLE
      *
     C                   MOVE      '0'           *IN98
     C     TVDK          IFEQ      *BLANKS
     C                   MOVE      '---'         TVDK
     C                   END
     C                   WRITE     SVX157P5
     C     TVDK          IFEQ      '---'
     C                   MOVE      *BLANKS       TVDK
     C                   END
     C                   EXSR      NYSIDE
      *
     C                   EXSR      SKILLE
      *
     C                   MOVE      '0'           *IN98
     C     TVDTY         IFNE      *BLANKS
     C     TVDO          OREQ      *BLANKS
     C     TVTLO         ANDEQ     *BLANKS
     C                   MOVE      '013'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     TVDTY         YKODE
     C     SVK01K        CHAIN     SVXKODL01                          34
     C                   MOVEL     TKTXTN        XVDTY
     C                   WRITE     SVX157P6
     C                   EXSR      NYSIDE
      *
     C                   MOVE      '1'           *IN98
     C     SVVK          SETLL     SVNLDK
     C     SVVK          READE     SVNLDK                                 34
     C     *IN34         DOWEQ     '0'
     C     TVDTY         IFNE      *BLANKS
     C                   MOVE      '013'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     TVDTY         YKODE
     C     SVK01K        CHAIN     SVXKODL01                          34
     C                   MOVEL     TKTXTN        XVDTY
     C                   WRITE     SVX157P6
     C                   EXSR      NYSIDE
     C                   END
     C     SVVK          READE     SVNLDK                                 34
     C  N34              ENDDO
     C                   END
      *
     C     TVDO          IFNE      *BLANKS
     C                   WRITE     SVX157P7
     C                   EXSR      NYSIDE
     C                   END
      *
     C     TVTLO         IFNE      *BLANKS
     C                   WRITE     SVX157P8
     C                   EXSR      NYSIDE
     C                   END
      *
     C                   EXSR      SKILLE
      *
     C                   WRITE     SVX157PA
     C                   ADD       1             XLINNR
     C     TVADS1        IFNE      *BLANKS
     C     TVADK1        ORNE      *BLANKS
     C                   EXSR      SKILLE
     C                   WRITE     SVX157PB
     C                   ADD       1             XLINNR
     C                   END
     C     TVPNS         IFNE      *BLANKS
     C     TVPNS         ORNE      *BLANKS
     C                   EXSR      SKILLE
     C                   WRITE     SVX157PC
     C                   ADD       1             XLINNR
     C                   END
      *
     C                   EXSR      SKILLE
      *
     C     SVHK          READE     SVNL                                   33
     C     *IN33         IFEQ      '1'
     C                   WRITE     SVX157PD
     C                   ELSE
     C                   MOVE      'J'           XSISTL            1
     C                   ADD       1             XLINNR
     C                   EXSR      SKILLE
     C                   MOVE      'N'           XSISTL
     C                   END
     C  N33              ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     NYSIDE        BEGSR
      ***********************************************
     C                   ADD       1             XLINNR            3 0
     C     XLINNR        IFGE      42
     C     XSISTL        IFNE      'J'
     C                   WRITE     SVX157PE
     C                   END
     C                   WRITE     SVX157PD
     C                   MOVE      *ZEROS        XLINNR
     C                   WRITE     SVX157P1
     C                   WRITE     SVX157P3
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SKILLE        BEGSR
      ***********************************************
     C     XLINNR        IFGT      0
     C     XLINNR        ANDLE     42
     C                   WRITE     SVX157PE
     C     XLINNR        IFEQ      42
     C                   WRITE     SVX157PD
     C                   MOVE      *ZEROS        XLINNR
     C                   WRITE     SVX157P1
     C                   WRITE     SVX157P3
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     HENTOP        BEGSR
      ***********************************************
     C     TVDK          IFEQ      *BLANKS
     C                   MOVE      TIDK          TVDK
     C                   END
     C     TVALK         IFEQ      *BLANKS
     C                   MOVE      TIALK         TVALK
     C                   END
     C     TVBLK         IFEQ      *BLANKS
     C                   MOVE      TIBLK         TVBLK
     C                   END
     C     TVNAS         IFEQ      *BLANKS
     C                   MOVEL     '---'         TVNAS
     C                   END
     C     TVNAK         IFEQ      *BLANKS
     C                   MOVEL     '---'         TVNAK
     C                   END
     C     TVNT          IFEQ      0
     C                   Z-ADD     TVNTEH        TVNT
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SVHK          KLIST
     C                   KFLD                    TIAVD
     C                   KFLD                    TITDN
     C     SVH06K        KLIST
     C                   KFLD                    UWS
     C                   KFLD                    XST
     C     SVHLK         KLIST
     C                   KFLD                    TVAVD2
     C                   KFLD                    TVTDN2
     C     SVVK          KLIST
     C                   KFLD                    TVAVD
     C                   KFLD                    TVTDN
     C                   KFLD                    TVLI
     C     SVVKMN        KLIST
     C                   KFLD                    TVAVD
     C                   KFLD                    TVTDN
     C                   KFLD                    TVLI
     C                   KFLD                    TVLI2
     C     SVSTDK        KLIST
     C                   KFLD                    UAVD
     C     KODKEY        KLIST
     C                   KFLD                    WOPUNI
     C                   KFLD                    WOPAVD
     C                   KFLD                    WOPLNR
     C     SVK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
      *
     C                   MOVE      *ZEROS        TITDN
     C     UWS           IFEQ      'TVINR'
     C     SVH06K        SETLL     SVNH06
     C     SVH06K        READE     SVNH06                                 33
     C     *IN33         IFEQ      '1'
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C                   ELSE
     C                   MOVEL     UWS           TIAVD
     C                   MOVE      UWS           TITDN
     C                   END
     C                   MOVE      TIAVD         UAVD              4 0
     C                   MOVE      'P'           WOPUNI
      *
     C                   EXSR      OVRF
      *
     C     UWS           IFEQ      'TVINR'
     C                   MOVE      *ZEROS        TIAVD
     C                   MOVE      *ZEROS        TITDN
     C                   END
     C     *LIKE         DEFINE    KOPAVD        WOPAVD
     C     *LIKE         DEFINE    KOPUNI        WOPUNI
     C     *LIKE         DEFINE    KOPLNR        WOPLNR
     C                   Z-ADD     0             WOPAVD
     C                   Z-ADD     0             WOPLNR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OVRF          BEGSR
      ***********************************************
      *
     C                   Z-ADD     TIAVD         WOPAVD
     C                   Z-ADD     96            WOPLNR
     C     KODKEY        CHAIN     KODTP                              33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *BLANKS       KOPNVN
     C                   MOVEL     '*JOB'        KOPNVN
     C                   MOVE      *BLANKS       KOPTY
     C                   MOVE      *ZEROS        KFMPOS
     C                   ELSE
     C     KOPFM         SETLL     KODTFM                             33
     C     KOPFM         READE     KODTFM                                 33
     C                   ENDIF
     C                   MOVE      *BLANKS       UCMD
     C     XCMD11        CAT       KOPNVN:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   CAT       XCMD21:1      UCMD
     C                   CAT       DTUSER:0      UCMD
     C                   CAT       XCMD31:0      UCMD
     C                   CAT       GRAAPP:0      UCMD
     C                   CAT       KOPTY:0       UCMD
     C                   CAT       GRAAPP:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            34
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C                   ENDSR
      *
     OSVNHR     E            UPDTRH
     O                       TIST
     O                       TIWS
