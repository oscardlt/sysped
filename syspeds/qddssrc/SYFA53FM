     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA53PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FRAKTTILLE-
     A                                      GG')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            VEIDEL         1A  O  6  7
     A            VEIKNR         8Y 0O  6 11EDTCDE(4)
     A            WSNA          20A  O  6 20
     A            VEILKO         2A  O  6 42
     A            VEISON         5A  O  6 47
     A            VEITAB         5A  O  6 54
     A            PKSAMM         1   O  6 64
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYFA53'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0805 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A            WSGEB          3A  O  2 19COLOR(WHT)
     A                                  2 23'='
     A                                      COLOR(WHT)
     A            KGENOT        20A  O  2 25COLOR(WHT)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(075 SHU0806 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF08(08 'SEARCH')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A N40                                  CF04(04 'PROMT')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  6MSGCON(060 SHU0807 *LIBL/SYMSGF)
     A            WSDEL          1A  B  2  6RANGE('A' 'H')
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A            WSKNR          8Y 0B  2 11
     A  70                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSLKO          2A  B  2 22
     A  69                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A            WSSON          5A  B  2 28
     A  68                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A            WSTAB          5A  B  2 35
     A  67                                  DSPATR(HI)
     A            WSSAM          1   B  2 45
     A                                  3  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 18MSGCON(040 SHU0807 *LIBL/SYMSGF)
     A                                  2  2MSGCON(014 DIV1147 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2KNR          8Y 0B  2 23DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A            W2DEL          1A  B  2 19
     A            W2LKO          2A  B  2 34DSPATR(UL)
     A            W2SON          5A  B  2 40DSPATR(UL)
     A            W2TAB          5A  B  2 47DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG501        60A  O  1  6MSGID(DIV 1146 SYMSGF)
     A                                      COLOR(WHT)
     A            VEIKNR         8Y 0O  2 12DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            VEIDEL         1A  O  2  7COLOR(WHT)
     A            VEILKO         2A  O  2 22COLOR(WHT)
     A            VEISON         5A  O  2 31COLOR(WHT)
     A            VEITAB         5A  O  2 39COLOR(WHT)
     A            MSG502        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF08(08 'SEARCH')
     A                                  1  2'SYSPED/SYFA53'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0805 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  5  2MSGCON(075 SHU0806 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA53'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0805 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            WSGEB          3A  B  5 31DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  5  2MSGCON(025 SHU0808 *LIBL/SYMSGF)
