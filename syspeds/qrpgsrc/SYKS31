      *
      ************************************************************
      *** UTSKRIFT AV TUR.RAPPORT                              ***
      ************************************************************
     FHEAD11    IF   E           K DISK
     FHEADF     IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFRX)
     FFAK3      IF   E           K DISK
     FFAK11     IF   E           K DISK
     F                                     RENAME(FAKTR:FAKTR11)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FSYKS31PF  O    E             PRINTER OFLIND(*IN88)
     D KB              S              8    DIM(200)
     D                 DS
     D  WSDTUD                 1      2  0
     D  WSDTUM                 3      4  0
     D  WSDTUÅ                 5      6  0
     D  WSDATO                 1      6  0
     D                 DS
     D  DFDTUÅ                 1      4  0
     D  DFDTUM                 5      6  0
     D  DFDTUD                 7      8  0
     D  FADATO                 1      8  0
     D                 DS
     D  FAVT                   1     20
     D  XXNØKK                20     20
     D                 DS
     D  PARMDS                 1    256
     D  WSOPTU                 1      1
     D  WSFALI                 2      2
     D  WSUFLI                 3      3
     D  WSFKOS                 4      4
     D  WSBKOS                 5      5
     D  WSILIN                 6      6
     D  UUREGT                 7     13
     D  WSREGT                 7     13  0
     D  UUDBGT                14     18
     D  WSDBGT                14     18  2
     D  UURELT                19     25
     D  WSRELT                19     25  0
     D  UUDBLT                26     30
     D  WSDBLT                26     30  2
     D  WSAVRS                31     31
     D  UUPERF                32     37
     D  WSPERF                32     37  0
     D  UUPERT                38     43
     D  WSPERT                38     43  0
     D  UUAVDF                44     47
     D  WSAVDF                44     47  0
     D  UUAVDT                48     51
     D  WSAVDT                48     51  0
     D  WSAVDG                52     58
     D  WSORAN                59     59
     D  WSPERI                60     60
     D  WSGEIO                61     61
     D  WSGE01                62     64
     D  WSGE02                65     67
     D  WSGE03                68     70
     D  WSGE04                71     73
     D  WSGE05                74     76
     D  WSGE06                77     79
     D  WSGE07                80     82
     D  WSGE08                83     85
     D  WSGE09                86     88
     D  WSGE10                89     91
     D  WSGE11                92     94
     D  WSGE12                95     97
     D  WSGE13                98    100
     D  GE                    62    100
     D                                     DIM(13)
     D  WSSTIO               101    101
     D  WSSTGR               102    111
     D  SG                   102    111
     D                                     DIM(10)
     D  WSAVSL               112    112
     D  WSBAVD               113    113
     D  WSTURF               114    121  0
     D  UUTURF               114    121
     D  WSTURT               122    129  0
     D  UUTURT               122    129
     D  DUMMY                256    256
     IHEADFRX
     I              HEAVD                       XXAVD
     I              HEOPD                       XXOPD
     I              HEPRO                       XXPRO
     C*
     C                   EXSR      INIT
     C     WSTURF        IFNE      *ZEROS
     C                   EXSR      TURFT
     C                   ELSE
     C*                    EXSR TURPER
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
      *
     C*****************************************************************
     C     TURFT         BEGSR
     C*****************************************************************
     C*
     C     WSTURF        SETLL     HEAD11
     C                   READ      HEAD11                                 94
     C     *IN94         DOWEQ     '0'
     C     HEPRO         CABGT     WSTURT        TURFT9
     C                   EXSR      ENTUR
     C                   ADD       1             HEPRO
     C     HEPRO         SETLL     HEAD11
     C                   READ      HEAD11                                 94
     C                   END
     C     TURFT9        ENDSR
      *
     C*****************************************************************
     C     ENTUR         BEGSR
     C*****************************************************************
     C*
     C                   SETOFF                                       88
     C                   Z-ADD     1             SIDE
     C                   MOVE      *ZEROS        TOTI
     C                   MOVE      *ZEROS        TOTK
     C                   CLEAR                   KB
      *
     C                   WRITE     HEAD
     C                   WRITE     HEADI
     C* FINN SKRIV INNTEKSTS-DETALJER FOR HVERT OPPDRAG
     C     HEPRO         SETLL     HEAD11
     C     HEPRO         READE     HEAD11                                 95
     C     *IN95         DOWEQ     '0'
     C                   MOVE      'O'           FXOPKO
     C                   EXSR      INNTEK
     C                   MOVE      'T'           FXOPKO
     C                   EXSR      INNTEK
     C                   MOVE      'Z'           FXOPKO
     C                   EXSR      INNTEK
     C     HEPRO         READE     HEAD11                                 95
     C                   END
      * SKRIV SUM INNTEKTER
     C                   WRITE     TOTINT
      * FINN/SKRIV KOSTNADSDETALJER PR BILAGSNR
     C                   MOVE      'O'           FXOPKO
     C                   EXSR      KOSTNA
      * SKRIV SUM KOSTNADER
     C                   WRITE     TOTKOS
      * SKRIV TOTALER FOR TUREN
     C     TOTI          ADD       TOTK          TOTR
      * DB1 I %
     C     TOTI          IFEQ      *ZEROS
     C     TOTR          IFLT      *ZEROS
     C                   Z-SUB     999.99        DBPROS
     C                   ELSE
     C                   MOVE      *ZEROS        DBPROS
     C                   END
     C                   ELSE
     C     TOTR          DIV(H)    TOTI          DBFAK             9 5
     C     DBFAK         MULT      100           DBPROS
     C                   END
     C                   WRITE     TOTTOT
      *
      *
     C                   ENDSR
      *
      *
     C*****************************************************************
     C     INNTEK        BEGSR
     C*****************************************************************
     C* LIST DETALJERT INNTEKTENE PÅ ETT OPPDRAG
     C* OG LEGG UT BILAGSNR/NØKKEL I ARRAY HVIS KOSTNAD
     C     FAK11K        SETLL     FAK11
     C     FAK11K        READE     FAK11                                  96
     C     *IN96         DOWEQ     '0'
     C                   EXSR      INCOM
     C     *IN30         IFEQ      *OFF
     C     FAKDA         IFEQ      'K'
     C                   EXSR      ADDKBI
     C                   ELSE
     C     FAFAKT        IFNE      GMFAKT
     C     KUNKEY        CHAIN     CUNDL01                            33
     C                   END
     C                   MOVEL     KNAVN         KUNLEV
     C     GEBKEY        CHAIN     KODTGE                             33
     C                   MOVE      *BLANKS       GEBTXT
     C  N33              MOVEL     KGENOT        GEBTXT
     C     FAVT          IFNE      *BLANKS
     C                   MOVEL     FAVT          GEBTXT
     C                   END
     C     FAVAL         COMP      FICURR                             5050
     C                   EXSR      KDTFM1
     C     *IN88         IFEQ      '1'
     C                   ADD       1             SIDE
     C                   WRITE     HEAD
     C                   SETOFF                                       88
     C                   END
     C                   WRITE     DET
     C                   ADD       FABELN        TOTI
     C                   END
     C                   END
     C                   MOVE      FAFAKT        GMFAKT
     C     FAK11K        READE     FAK11                                  96
     C                   END
     C                   ENDSR
      *
     C*****************************************************************
     C     ADDKBI        BEGSR
     C*****************************************************************
      *
      * SIKRE at det max er 2 typer på ett bilag (0=IKKE TUR(oppdr) / 2=TUR )
     C     XXNØKK        ifne      '2'
     C                   MOVE      '0'           XXNØKK
     C                   END
     C                   MOVEL     FAFAKT        FAKNØK            8
     C                   MOVE      XXNØKK        FAKNØK
      *
      * LEGG KOSTNADSBILAG INN I ARRAY OM DET IKKE FINNES FRA FØR
     C     1             DO        200           NR                3 0
     C     KB(NR)        IFEQ      *BLANKS
     C                   MOVE      FAKNØK        KB(NR)
     C                   END
     C     KB(NR)        CABEQ     FAKNØK        ADDKB9
     C                   END
      *
     C     ADDKB9        ENDSR
      *
     C*****************************************************************
     C     KOSTNA        BEGSR
     C*****************************************************************
      * SORTER OG FINN FØRSTE NONBLANK
     C                   SORTA     KB
     C     1             DO        200           NR                3 0
     C     KB(NR)        CABNE     *BLANKS       KOSTN1
     C                   END
     C     KOSTN1        TAG
     C                   MOVE      NR            N1                3 0
      *
      *
      * FØRST PRINT BILAG SOM IKKE!! KONTERT PÅ TURNR (NORMALT PÅ OPD)
     C                   WRITE     HEADK
     C     N1            DO        200           NR                3 0
     C     KB(NR)        IFNE      *BLANKS
     C                   MOVEL     KB(NR)        FXFAK             7 0
     C                   MOVE      KB(NR)        FXNØK             1
     C     FXNØK         IFNE      '2'
     C                   MOVE      'O'           FXOPKO
     C                   EXSR      KOST1
     C                   MOVE      'T'           FXOPKO
     C                   EXSR      KOST1
     C                   MOVE      'Z'           FXOPKO
     C                   EXSR      KOST1
     C                   END
     C                   END
     C                   END
      *
      * DERETTER PRINT BILAG SOM ER KONTERT PÅ TURNR
     C     N1            DO        200           NR                3 0
     C     KB(NR)        IFNE      *BLANKS
     C                   MOVEL     KB(NR)        FXFAK
     C                   MOVE      KB(NR)        FXNØK
     C     FXNØK         IFEQ      '2'
     C                   EXSR      KOST2
     C                   END
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C*****************************************************************
     C     KOST1         BEGSR
     C*****************************************************************
     C* KOSTNADER SOM IKKE ER KONTERT PÅ TURNR (NORMALT PÅ OPPD.NR)
     C* PRINTES DETALJERT
      * OBS! FAK3K ER PÅ BILAGSNR!! FILTRER UT KUN AKTUELLE FOR TUREN!!
     C     FAK3K         SETLL     FAK3
     C     FAK3K         READE     FAK3                                   96
     C     *IN96         DOWEQ     '0'
     C     FAKDA         IFEQ      'K'
     C     XXNØKK        ANDNE     '2'
     C                   EXSR      INCOM
     C     *IN30         IFEQ      *OFF
     C     HEADFK        CHAIN     HEADF                              30
     C  N30XXPRO         COMP      HEPRO                              3030
     C                   END
     C     *IN30         IFEQ      *OFF
     C     GEBKEY        CHAIN     KODTGE                             33
     C                   MOVE      *BLANKS       GEBTXT
     C  N33              MOVEL     KGENOT        GEBTXT
     C                   MOVE      FAVT          KUNLEV
     C     FAVAL         COMP      FICURR                             5050
     C                   EXSR      KDTFM1
     C     *IN88         IFEQ      '1'
     C                   ADD       1             SIDE
     C                   WRITE     HEAD
     C                   SETOFF                                       88
     C                   END
     C                   WRITE     DET
     C                   ADD       FABELN        TOTK
     C                   END
     C                   END
     C     FAK3K         READE     FAK3                                   96
     C                   END
      *
     C                   ENDSR
     C*****************************************************************
     C     KOST2         BEGSR
     C*****************************************************************
     C* KOSTNADER SOM ER KONTERT PÅ TURNR, AKKUMULERES KUN PR BILAGSNR
      * OBS! FAK3K ER PÅ BILAGSNR!! FILTRER UT KUN AKTUELLE FOR TUREN!!
     C                   MOVE      *BLANKS       BIVK              3
     C                   MOVE      *BLANKS       BIVAL             3
     C                   MOVE      *BLANKS       BIVT             20
     C                   MOVE      *ZEROS        BIDATO            8 0
     C                   MOVE      *ZEROS        TOTBIN           13 2
     C                   MOVE      *ZEROS        TOTBIV           13 2
      *
     C                   MOVE      'O'           FXOPKO
     c     Kost2st       Tag
     C     FAK3K         SETLL     FAK3
     C     FAK3K         READE     FAK3                                   96
     C     *IN96         DOWEQ     '0'
     C     FAKDA         IFEQ      'K'
     C     XXNØKK        ANDEQ     '2'
     C                   EXSR      INCOM
     C     *IN30         IFEQ      *OFF
     C     HEADFK        CHAIN     HEADF                              30
     C  N30XXPRO         COMP      HEPRO                              3030
     C                   END
     C     *IN30         IFEQ      *OFF
     C                   ADD       FABELN        TOTBIN
     C                   ADD       FABELV        TOTBIV
     C                   ADD       FABELN        TOTK
     C     BIVK          IFEQ      *BLANKS
     C                   MOVE      FAVK          BIVK
     C                   MOVE      FAVAL         BIVAL
     C                   MOVE      FAVT          BIVT
     C                   MOVE      FADATO        BIDATO
     C                   END
     C                   END
     C                   END
     C     FAK3K         READE     FAK3                                   96
     C                   END
      * også kode T / Z (evt regen av stat.)
     c     FXOPKO        IFEQ      'O'
     C                   MOVE      'T'           FXOPKO
     c                   goto      Kost2st
     c                   end
     c     FXOPKO        IFEQ      'T'
     C                   MOVE      'Z'           FXOPKO
     c                   goto      Kost2st
     c                   end
      *
      * PRINT SUM PR BILAG
     C                   MOVE      BIVK          FAVK
     C     GEBKEY        CHAIN     KODTGE                             33
     C                   MOVE      *BLANKS       GEBTXT
     C  N33              MOVEL     KGENOT        GEBTXT
     C                   Z-ADD     TOTBIN        FABELN
     C                   Z-ADD     TOTBIV        FABELV
     C                   MOVE      BIVAL         FAVAL
     C                   MOVE      BIVT          KUNLEV
     C                   MOVE      BIDATO        FADATO
     C     FAVAL         COMP      FICURR                             5050
     C                   MOVE      *ZEROS        FAAVD
     C                   MOVE      *ZEROS        FAOPD
     C                   EXSR      KDTFM1
     C     *IN88         IFEQ      '1'
     C                   ADD       1             SIDE
     C                   WRITE     HEAD
     C                   SETOFF                                       88
     C                   END
     C                   WRITE     DET
      *
     C                   ENDSR
      *
     C*****************************************************************
     C     INIT          BEGSR
     C*****************************************************************
     C*
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UUPARM          256
     C*
     C                   MOVE      UUPARM        PARMDS
     C     WSGEIO        COMP      *BLANKS                            51
     C     WSSTIO        COMP      *BLANKS                            52
     C*
     C                   READ      FIRM                                   33
     C     *LIKE         DEFINE    FAFAKT        GMFAKT
      *
     C     FAK11K        KLIST
     C                   KFLD                    FXOPKO            1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
     C     FAK3K         KLIST
     C                   KFLD                    FXOPKO
     C                   KFLD                    FXFAK
     C                   MOVE      'O'           FXOPKO
      *
     C     HEADFK        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
      *
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FAKUNR
      *
     C     GEBKEY        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FAVK
     C                   MOVE      'GE'          KGEUNI
      *
     C*
     C                   ENDSR
     C***********************************************
     C     KDTFM1        BEGSR
     C***********************************************
     C                   MOVE      DFDTUD        UDT1              2 0
     C                   MOVE      DFDTUM        UDT2              2 0
     C                   MOVE      DFDTUÅ        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTUD
     C                   MOVE      UDT2          WSDTUM
     C                   MOVE      UDT3          WSDTUÅ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INCOM         BEGSR
     C***********************************************
      * Include / Omit basert på GEB-kode
     C                   SETOFF                                       3033
     C     WSGEIO        IFNE      ' '
     C     WSGEIO        IFEQ      'I'
      * Ved include må begrepet finnes i -arrayet (->33 on)
     C     FAVK          LOOKUP    GE                                     33
     C  N33              SETON                                        30
     C                   ELSE
      * Ved omit må begrepet IKKE finnes i -arrayet (->33 off)
     C     FAVK          LOOKUP    GE                                     33
     C   33              SETON                                        30
     C                   END
     C                   END
      *
      *
      * Include / Omit basert på STAT-GRUPPE-KODE
     C     WSSTIO        IFNE      ' '
     C     *IN30         ANDEQ     *OFF
      * Dersom linjen IKKE har gruppe (ufakt...) hent grp.via geb-kode
     C     FAGGRP        IFEQ      *BLANKS
     C                   MOVE      FAVK          KGEKOD
     C     GEBKEY        CHAIN     KODTGE                             33
     C     FAKDA         IFNE      'K'
     C     FASK          IFEQ      'I'
     C                   MOVEL     KGEII         FAGGRP
     C                   ELSE
     C                   MOVEL     KGEIE         FAGGRP
     C                   END
     C                   ELSE
     C     FASK          IFEQ      'I'
     C                   MOVEL     KGEKI         FAGGRP
     C                   ELSE
     C                   MOVEL     KGEKE         FAGGRP
     C                   END
     C                   END
     C                   END
      *
     C     FAGGRP        IFNE      *BLANKS
      *
     C                   SETOFF                                       33
     C                   MOVEL     FAGGRP        TSTGRP            1
     C     WSSTIO        IFEQ      'I'
      * Ved include må begrepet finnes i -arrayet (->33 on)
     C     TSTGRP        LOOKUP    SG                                     33
     C  N33              SETON                                        30
     C                   ELSE
      * Ved omit må begrepet IKKE finnes i -arrayet (->33 off)
     C     TSTGRP        LOOKUP    SG                                     33
     C   33              SETON                                        30
     C                   END
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
