/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 10XXX * 01 YBC PTF10 ENDRE AUTORISASJON TIL *PUBLIC                     */
/* ********************************************************************    */
             PGM        PARM(&LIB &FILE &MBR &IID &OK)
             /*_____________________*/
             /* Deklarere Variabler */
             /*"""""""""""""""""""""*/
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&LIB2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IID) TYPE(*CHAR) LEN(35)
             DCL        VAR(&OK) TYPE(*CHAR) LEN(2) VALUE('  ')
             DCL        VAR(&TIL) TYPE(*CHAR) LEN(128) VALUE('  ')
             DCL        VAR(&TIL2) TYPE(*CHAR) LEN(128) VALUE('  ')
             DCL        VAR(&TIL256) TYPE(*CHAR) LEN(256) VALUE('  ')
             /*________________________*/
             /* Hent prod.filbibliotek */
             /*""""""""""""""""""""""""*/
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB2)
             /*____________________________________*/
             /* Hvilket bibliotek ligger filen i ? */
             /*""""""""""""""""""""""""""""""""""""*/
             IF         COND(&LIB *EQ '*LIBL     ' *AND &FILE *EQ +
                          'EDISE     ') THEN(DO)
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
             ENDDO
             IF         COND(&LIB *EQ '*LIBL     ' *AND &FILE *EQ +
                          'EDISE1K   ') THEN(DO)
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
             ENDDO
             /*______________*/
             /* Lag temp fil */
             /*""""""""""""""*/
             DLTF       FILE(QTEMP/Q)
             MONMSG     MSGID(CPF0000)
             IF         COND(&FILE *EQ 'EDISE1K   ') THEN(DO)
             CRTPF      FILE(QTEMP/Q) RCDLEN(1024)
             MONMSG     MSGID(CPF0000)
             ENDDO
             IF         COND(&FILE *NE 'EDISE1K   ') THEN(DO)
             CRTPF      FILE(QTEMP/Q) RCDLEN(80)
             MONMSG     MSGID(CPF0000)
             ENDDO
             /*______________________________*/
             /* Kopiere orginal til temp-fil */
             /*""""""""""""""""""""""""""""""*/
             CPYF       FROMFILE(&LIB/&FILE) TOFILE(QTEMP/Q) +
                          MBROPT(*REPLACE) FROMKEY(1 (&MBR)) +
                          TOKEY(1 (&MBR)) FMTOPT(*NOCHK)
             /*_______________________________*/
             /* Lage streamfile i temp-område */
             /*"""""""""""""""""""""""""""""""*/
             CHGVAR     VAR(&TIL) VALUE(&LIB2 *TCAT 'X/' *TCAT &FILE +
                          *TCAT '/' *TCAT &IID *TCAT '/TEMP/' *TCAT &MBR)
             CPYTOSTMF  FROMMBR('QSYS.LIB/QTEMP.LIB/Q.FILE/Q.MBR') +
                          TOSTMF(&TIL) STMFOPT(*REPLACE) +
                          ENDLINFMT(*FIXED)
             MONMSG     MSGID(CPF0000)
/*N01*/      CHGAUT     OBJ(&TIL) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)
/*N01*/      MONMSG     MSGID(CPF0000)
             /*____________________________________________*/
             /* Flytte streamfile fra temp til henteområde */
             /*""""""""""""""""""""""""""""""""""""""""""""*/
       CHGVAR     VAR(&TIL2) VALUE('/' *TCAT &LIB2 *TCAT 'X/' *TCAT &FILE +
                          *TCAT '/' *TCAT &IID)
             MOV        OBJ(&TIL) TODIR(&TIL2)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(&OK) VALUE('OK')
             /*____________________________________________*/
             /* Logge x-ref for katalog / fil              */
             /*""""""""""""""""""""""""""""""""""""""""""""*/
             CHGVAR     VAR(&TIL256) VALUE(&TIL2)
             CALL       PGM(EDISXR3) PARM(&MBR &TIL256)
             ENDPGM
