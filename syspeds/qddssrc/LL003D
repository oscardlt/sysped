********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXX * 01 YBC PTF6 SETT POS. VED FEIL
P7103 * 02 YBC PTF7 OPSJON 8 FOR ARBEID MED KUNDE
P8XXX * 03 YBC PTF8 DEAKTIVERE MOTTAKERE SOM IKKE ER BRUKT I MINST 1 ÅR
P8XXX * 04 YBC PTF8 A=ARBEID MED AUTORISASJON
P8068A* 06 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(LLGRPK LLGRPKR)
     A                                      PRINT
N06  A                                      HLPPNLGRP(HLPSCR LL003PNL)
N06  A                                      HLPTITLE('VEDLIKEHOLD LAGERKUNDER')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
S03  A*           WSNR           1A  I  6  2VALUES(' ' '2' '3' '4' '8' '9')
N03  A            WSNR           1A  I  6  2VALUES(' ' '2' '3' '4' '8' '9' 'I' -
N04  A                                      'D' 'A')
S04  A*                                     'D')
     A            GKGRP     R        O  6  5EDTCDE(4)
     A            GKGSOK        10A  H
     A            XGNV          25A  O  6 14
     A            GKPNR     R        O  6 40EDTCDE(4)
     A            GKPSOK        10A  H
     A            XPNV          25A  O  6 49
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'Søk')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      OVERLAY
S06  A*         H                           HLPDOC('LL00301' 'LL003' 'SY400LO/L-
S06  A*                                     OGI')
N06  A          H                           HLPPNLGRP(HLPSCR LL003PNL)
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/LL003R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC0944 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
E03  A                                  2  2MSGCON(78 YBC0945 SYMSGF)
     A                                      COLOR(BLU)
N03  A                                  3  9'I/D=Deaktiver mottaker/artikkel'
N03  A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0946 SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  B  5  2VALUES(' ' '1')
     A            W2GRP          8  0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2GSOK        10A  B  5 14DSPATR(UL)
     A            W2PSOK        10A  B  5 49DSPATR(UL)
      *
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
S01  A*                                     CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
S01  A*           LINNBR         3S 0H
S01  A*           POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  5MSGCON(75 YBC0947 SYMSGF)
     A                                      DSPATR(HI)
     A            WSGRP          8Y 0B  2  5DSPATR(UL)
     A N30N40
     AO 90N40                               DSPATR(PC)
     A  90                                  DSPATR(RI)
     A     40                               DSPATR(PR)
     A     40                               DSPATR(HI)
     A                                      EDTCDE(4)
     A            WSGSOK        10A  B  2 14DSPATR(UL)
     A     40                               DSPATR(PC)
     A            WSPNR          8Y 0B  2 40DSPATR(UL)
N01  A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A     40                               DSPATR(PR)
     A     40                               DSPATR(HI)
     A                                      EDTCDE(4)
     A            WSPSOK        10A  B  2 49DSPATR(UL)
     A                                  3  2MSGCON(18 DIV0032 SYMSGF)
     A                                  3 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(75 YBC0947 SYMSGF)
     A                                      DSPATR(HI)
     A            GKGRP          8Y 0O  2  5DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A            GKGSOK        10A  O  2 14DSPATR(UL)
     A                                      DSPATR(HI)
     A            GKPNR          8Y 0O  2 40DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            GKPSOK        10A  O  2 49DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'Søk')
     A                                      CA06(06 'LAGE NY')
S06  A*         H                           HLPDOC('LL00301' 'LL003' 'SY400LO/L-
S06  A*                                     OGI')
N06  A          H                           HLPPNLGRP(LL00301 LL003PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL003R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC0944 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
E03  A                                  2  2MSGCON(30 YBC0948 SYMSGF)
     A                                      COLOR(BLU)
N03  A                                  3  9'I/D=Deaktiver mottaker/artikkel'
N03  A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0946 SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  B  5  2VALUES(' ' '1')
     A            W2GRP          8  0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2GSOK        10A  B  5 14DSPATR(UL)
     A            W2PSOK        10A  B  5 49DSPATR(UL)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
