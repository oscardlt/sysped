     A                                      DSPSIZ(27 132 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 13 80 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søking')
     A                                      CF05(05 'Splitte')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD134R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med oppdrag'
     A                                      COLOR(WHT)
     A                                  2  2'Avd.:'
     A            EFAVD          4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Turnr:'
     A            EFPRO          8Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 29'UUID:'
     A            EFUUID        36A  O  2 35TEXT('Turnr')
     A                                      COLOR(BLU)
      *
     A  51                              3  2'OBS - Splittet oppdrag - se valg 1-
     A                                      1 for detaljer'
     A                                      COLOR(YLW)
      *
     A                                  4  2'Avdeling:'
     A            WSAVD          4Y 0O  4 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 20'Oppdrag:'
     A            WSTDN          7Y 0O  4 29TEXT('Oppdragsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 37'Status:'
     A            CLST           1A  O  4 45TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4 47'Kolli:'
     A            WSNTK          7Y 0B  4 54TEXT('Kolli')
     A                                      DSPATR(UL)
     A N48
     AO 49                                  DSPATR(PR)
     A  48
     AAN99                                  COLOR(YLW)
     A N48
     AO 99                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 62'Vekt:'
     A            WSVKB          9Y 0B  4 68TEXT('Vekt')
     A                                      DSPATR(UL)
     A N48
     AO 49                                  DSPATR(PR)
     A  48
     AAN98                                  COLOR(YLW)
     A N48
     AO 98                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2'Vareslag:'
     A            WSVT          30A  B  5 15TEXT('Vareslag')
     A                                      DSPATR(UL)
     A N48
     AO 49                                  DSPATR(PR)
     A  48
     AAN97                                  COLOR(YLW)
     A N48
     AO 97                                  COLOR(WHT)
      *
     A N51                              6  2'Deklarant:'
     A N51        WSRG          11A  B  6 15TEXT('Senders org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A N51                              6 27'Dato:'
     A N51        WS0068A        6Y 0B  6 33TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N95                                  COLOR(YLW)
     A  95                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A*                                     EDTWRD('  .  .  ')
     A N51                              6 42'Sekvens:'
     A N51        WS0068B        6Y 0B  6 51TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N94                                  COLOR(YLW)
     A  94                                  COLOR(WHT)
     A                                      EDTCDE(4)
      *
     A                                  7  2'Fra sted:'
     A            WSLKF          2A  B  7 15TEXT('Fra land')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A            WSSDF          5A  B  7 18TEXT('Fra sted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A            WSSDFT        30A  B  7 24TEXT('Fra sted navn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A                                  8  2'Til sted:'
     A            WSLKT          2A  B  8 15TEXT('Til land')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A            WSSDT          5A  B  8 18TEXT('Til sted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A            WSSDTT        30A  B  8 24TEXT('Til sted navn')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
      *
     A N51                              9  2'Prosedyre:'
     A N51        WSPR           2A  B  9 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N91                                  COLOR(YLW)
     A  91                                  COLOR(WHT)
     A N51        WSPRT         45A  O  9 21COLOR(WHT)
     A N51                             10  2'Eksp.type:'
     A N51        WSETYP         2A  B 10 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  90                                  COLOR(WHT)
     A N51        WSETYPT       45A  O 10 21COLOR(WHT)
     A N51                             11  2'Eksport Id:'
     A N51        WSEID         18A  B 11 15TEXT('Eksport Id')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A N51                             11 35'Sertifisert:'
     A N51        WSESER         1A  B 11 48TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A N51                             11 50'(J/N)'
     A  49                             12  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A N48
     AAN49                             12  2'F4=Søk   F5=Splitte   F12=Avbryt'
     A                                      COLOR(BLU)
     A  48                             12  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 13  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
