     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            TLUNIK    R        H      REFFLD(ARKLOGHR/EPUNIK *LIBL/ARKLOG-
     A                                      H)
     A            WSNR           1A  B  6  2VALUES(' ' '4' '5' '8')
     A            TLUSER        10A  O  6  4
     A            TLTEXT        30A  O  6 15
     A            TLDATE    R        O  6 64REFFLD(ARKLOGHR/EPDATE *LIBL/ARKLOG-
     A                                      H)
     A            TLTIME    R        O  6 73REFFLD(ARKLOGHR/EPTIME *LIBL/ARKLOG-
     A                                      H)
     A            TLAUTO    R        O  6 80REFFLD(ARKLOGHR/EPAUTO *LIBL/ARKLOG-
     A                                      H)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYIM51'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            WSUSER    R        O  2  2REFFLD(ARKLOGHR/EPUSER *LIBL/ARKLOG-
     A                                      H)
     A                                      COLOR(YLW)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'O Bruker     tekst på bestilling  -
     A                                                                  dato   -
     A                                        tid    s'
     A                                      COLOR(WHT)
     A                                  3  2'5 = Vis listen     4 = slett liste-
     A                                      n     8 = send via epost'
     A                                      COLOR(BLU)
     A                                  1 18'Arbeide med ufortollet gods'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'TILBAKE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A  30        MSGTXT        70A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(RED)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(008 SHU0908 *LIBL/SYMSGF)
     A            WSUSER        10A  B  2 11
     A                                  2 36MSGCON(004 BBA0070 *LIBL/SYMSGF)
     A            WSDATE         8Y 0B  2 41EDTCDE(4)
     A                                  2 50MSGCON(008 SHU0909 *LIBL/SYMSGF)
     A            WSSTAT         1A  B  2 59
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2005  '
     A                                      COLOR(WHT)
     A                                  2 22MSGCON(010 SHU1159 *LIBL/SYMSGF)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYIM51'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            WSUSER    R        O  2  2REFFLD(ARKLOGHR/EPUSER *LIBL/ARKLOG-
     A                                      H)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'O Bruker     tekst på bestilling  -
     A                                                                  dato   -
     A                                        tid    s'
     A                                      COLOR(WHT)
     A  98        MSG205        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 17'Arbeide med ufortollet gods'
     A                                      COLOR(WHT)
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 4 70 *NOMSGLIN)
     A                                      CF12(12 'GÅ TILBAKE')
     A                                      OVERLAY
     A                                  2  2'EPOSTADR:'
     A            EPOST         50A  B  2 12DSPATR(UL)
     A                                      CHECK(LC)
     A                                  3  2'F12=Avbryt'
     A                                      COLOR(BLU)
