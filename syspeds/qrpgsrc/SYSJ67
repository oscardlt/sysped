      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE LASTELISTE (LOKAL-BÅT SJØ)      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FBOOKLF15  IF   E           K DISK
     FBLV1LF    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTP     IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSTED2     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYSJ67FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     FSYSJ67P   O    E             PRINTER OFLIND(*IN27)
     F                                     USROPN
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(SYSJ67P) -
     D                                     OUTQ(')
     D XCMD21          C                   CONST('PAGESIZE(72 120) -
     D                                     CPI(15) -
     D                                     OVRFLW(64)')
     D XCMD22          C                   CONST('PAGESIZE(48 120) -
     D                                     CPI(12) -
     D                                     OVRFLW(42)')
     D                 DS
     D  WSDT1D                 1      2  0
     D  WSDT1M                 3      4  0
     D  WSDT1Å                 5      6  0
     D  WSDT1                  1      6  0
     D                 DS
     D  YDT1Å                  1      4  0
     D  YDT1M                  5      6  0
     D  YDT1D                  7      8  0
     D  YDT1                   1      8  0
     D                 DS
     D  XDT3D                  1      2  0
     D  XDT3M                  3      4  0
     D  XDT3Å                  5      6  0
     D  XDT3                   1      6  0
     D                 DS
     D  BODT3Å                 1      4  0
     D  BODT3M                 5      6  0
     D  BODT3D                 7      8  0
     D  BODT3                  1      8  0
     D                 DS
     D  MN                     1   1700
     D                                     DIM(100)
     D  XMN1                   1     17
     D  XMN2                  18     34
     D  XMN3                  35     51
     D  XMN4                  52     68
     D                 DS
     D  NT                     1    500  0
     D                                     DIM(100)
     D  XNT1                   1      5  0
     D  XNT2                   6     10  0
     D  XNT3                  11     15  0
     D  XNT4                  16     20  0
     D                 DS
     D  VS                     1   2500
     D                                     DIM(100)
     D  XVS1                   1     25
     D  XVS2                  26     50
     D  XVS3                  51     75
     D  XVS4                  76    100
     D                 DS
     D  VK                     1    900  0
     D                                     DIM(100)
     D  XVKT1                  1      9  0
     D  XVKT2                 10     18  0
     D  XVKT3                 19     27  0
     D  XVKT4                 28     36  0
     D                 DS
     D  M3                     1    900  3
     D                                     DIM(100)
     D  XM31                   1      9  3
     D  XM32                  10     18  3
     D  XM33                  19     27  3
     D  XM34                  28     36  3
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C  N30
     CANN04              Z-ADD     5             LINNBR
     C  N30
     CANN04              Z-ADD     20            POSNBR
     C                   EXFMT     BILDE01
     C                   MOVEA     '0000'        *IN(96)
     C                   MOVE      '0'           *IN30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   03              GOTO      SLUTT
      *
     C   04              DO
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSLB
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      START
     C                   END
      *
     C                   EXSR      TESTB
     C   30              GOTO      START
      *
     C                   EXSR      PRINT
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C******************************************
     C     TESTB         BEGSR
     C******************************************
      *** TEST LOKAL BÅT
     C     WSLB          CHAIN     CDBOAT                             99
     C     *IN99         IFEQ      '1'                                          1<-B
     C                   MOVE      'SPB0102'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   ELSE                                                   1
     C                   MOVE      CBNAB         XNAB
     C                   END                                                    1<-E
      *
      *** TEST AVGANGSDATO
     C                   MOVE      WSDT1Å        UÅÅ               2 0
     C                   MOVE      WSDT1M        UMM               2 0
     C                   MOVE      WSDT1D        UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1                                            1<-B
     C                   MOVE      'SPD0001'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   END                                                    1<-E
      *
      * TEST TUREN
     C                   MOVE      WSDT1D        YDT1D
     C                   MOVE      WSDT1M        YDT1M
     C     WSDT1Å        IFGT      50                                           1<-B
     C     WSDT1Å        ADD       1900          YDT1Å
     C                   ELSE                                                   1
     C     WSDT1Å        ADD       2000          YDT1Å
     C                   END                                                    1<-E
     C     BOK15K        SETLL     BOOKLF15
     C     BOK15K        READE     BOOKLF15                               99
     C     *IN99         IFEQ      '1'                                          1<-B
     C                   MOVE      'YBC0715'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   END                                                    1<-E
      *
      * TEST PRINTERANVN
     C     WSPRT         IFEQ      *BLANKS                                      1<-B
     C                   MOVE      'YBC0716'     MSGNR
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   END                                                    1<-E
      * TEST PRINTERANVN
     C     WSLIGG        IFNE      'S'
     C     WSLIGG        ANDNE     'L'
     C                   MOVE      'JOV1033'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLTB
     C                   END                                                    1<-E
      *
     C     SLTB          ENDSR
      *
     C******************************************
     C     PRINT         BEGSR
     C******************************************
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD
     C     XCMD11        CAT       WSPRT:0       UCMD
     C                   CAT       ')':0         UCMD
     C     WSLIGG        IFNE      'L'
     C                   CAT       XCMD21:1      UCMD                             STÅ. A4
     C                   ELSE
     C                   CAT       XCMD22:1      UCMD                             LIGG.A4
     C                   END
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C     BOK15K        SETLL     BOOKLF15
     C     BOK15K        READE     BOOKLF15                               33
      *
     C     *IN33         DOWEQ     '0'
     C     BOLSH         IFEQ      *BLANKS
     C                   MOVE      '.'           BOLSH
     C                   END
     C     BOVHL         IFEQ      *BLANKS
     C                   MOVE      '.'           BOVHL
     C                   END
     C     BOLSH         IFNE      XLSH
     C     BOVHL         ORNE      XVHL
     C     XLSH          IFNE      *BLANKS
     C                   EXSR      TOTAL
     C                   CLOSE     SYSJ67P
     C                   END
     C                   EXSR      HHEAD
     C                   END
      *
     C                   EXSR      HDETJ
      *
     C     *IN27         IFEQ      '1'
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ67P1
     C                   END
     C                   WRITE     SYSJ67P2
      *
     C                   MOVEL     BOBKNL        XBKNL
     C     5             DO        99            XN
     C     MN(XN)        IFNE      *BLANKS
     C     VS(XN)        ORNE      *BLANKS
     C     VK(XN)        ORNE      0
     C     M3(XN)        ORNE      0
     C     NT(XN)        ORNE      0
     C                   MOVE      MN(XN)        XMN5
     C                   MOVE      NT(XN)        XNT5
     C                   MOVE      VS(XN)        XVS5
     C                   MOVE      VK(XN)        XVKT5
     C                   MOVE      M3(XN)        XM35
     C     *IN27         IFEQ      '1'
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ67P1
     C                   END
     C                   WRITE     SYSJ67P3
     C                   MOVE      *BLANKS       XBKNL
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   ENDDO
      *
     C     XBKNL         IFNE      *BLANKS
     C                   WRITE     SYSJ67P4
     C                   END
      * SKILLELINJE -----
     C                   WRITE     SYSJ67P5
      *
     C     BOK15K        READE     BOOKLF15                               33
     C  N33              ENDDO
      *
     C                   EXSR      TOTAL
     C                   CLOSE     SYSJ67P
      *
     C                   ENDSR
      *
     C******************************************
     C     TOTAL         BEGSR
     C******************************************
      *
     C     TOTVKT        IFNE      *ZEROS
     C     TOTKUB        ORNE      *ZEROS
     C                   WRITE     SYSJ67P6
     C                   END
     C                   MOVE      *ZEROS        TOTNT
     C                   MOVE      *ZEROS        TOTVKT
     C                   MOVE      *ZEROS        TOTKUB
     C                   ENDSR
      *
     C******************************************
     C     HHEAD         BEGSR
     C******************************************
     C                   OPEN      SYSJ67P
      *
     C     BOLSH         CHAIN     STED2                              31
     C     *IN31         IFEQ      '0'
     C                   MOVE      ST2NVN        XNALSH
     C                   ELSE
     C                   MOVE      *BLANKS       XNALSH
     C                   MOVEL     BOLSH         XNALSH
     C                   END
     C                   MOVE      BOLSH         XLSH              5
      *
     C     BOVHL         CHAIN     STED2                              31
     C     *IN31         IFEQ      '0'
     C                   MOVE      ST2NVN        XNAVHL
     C                   ELSE
     C                   MOVE      *BLANKS       XNAVHL
     C                   MOVEL     BOVHL         XNAVHL
     C                   END
     C                   MOVE      BOVHL         XVHL              5
      *......
      *
     C                   MOVE      '0'           *IN27
     C                   WRITE     SYSJ67P1
      *
     C                   ENDSR
      *
     C******************************************
     C     HDETJ         BEGSR
     C******************************************
     C                   MOVE      BODT3D        XDT3D
     C                   MOVE      BODT3M        XDT3M
     C                   MOVE      BODT3Å        XDT3Å
      * AVSENDER
     C     CUN1KS        CHAIN     CUNDL01                            31
     C     *IN31         IFEQ      '1'
     C                   MOVE      *BLANKS       KNAVN
     C     BONAA         IFEQ      *BLANKS
     C                   MOVEL     BOKNA         KNAVN
     C                   ELSE
     C                   MOVEL     BONAA         KNAVN
     C                   END
     C                   MOVE      *BLANKS       ADR1
     C                   MOVE      *BLANKS       ADR2
     C                   MOVE      *ZEROS        POSTNR
     C                   MOVE      *BLANKS       ADR3
     C                   END
      *
      * LOSSEHAVN
     C     BOLOH         CHAIN     STED2                              31
     C     *IN31         IFEQ      '0'
     C                   MOVEL     ST2NVN        XNALOH
     C                   ELSE
     C                   MOVE      *BLANKS       XNALOH
     C                   MOVEL     BOLOH         XNALOH
     C                   END
      * OVERSJØ-BÅT.
     C     BOOB          CHAIN     CDBOAT                             31
     C     *IN31         IFEQ      '0'
     C                   MOVEL     CBNAB         XNAOB
     C                   ELSE
     C                   MOVE      *BLANKS       XNAOB
     C                   MOVEL     BOOB          XNAOB
     C                   END
      *
      * B/L-F10-VARELINJER
     C                   MOVE      *BLANKS       MN
     C                   MOVE      *BLANKS       VS
     C                   MOVE      *ZEROS        NT
     C                   MOVE      *ZEROS        VK
     C                   MOVE      *ZEROS        M3
     C                   MOVE      *ZEROS        XN                3 0
     C     BLV1K         SETLL     BLV1LF
     C     BLV1K         READE     BLV1LF                                 34
     C     *IN34         DOWEQ     '0'
     C                   ADD       1             XN
     C                   MOVE      BVMN          MN(XN)
     C                   MOVE      BVNT          NT(XN)
     C                   MOVE      BVVS          VS(XN)
     C                   MOVE      BVVKT         VK(XN)
     C                   MOVE      BVM3          M3(XN)
     C                   ADD       BVNT          TOTNT
     C                   ADD       BVVKT         TOTVKT
     C                   ADD       BVM3          TOTKUB
     C     BLV1K         READE     BLV1LF                                 34
     C  N34              ENDDO
      *
     C                   ENDSR
      *
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     CUN1KA        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KOAKNR
     C     CUN1KS        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    BOKNA
     C     CUN1KR        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    BOKNR
     C     BOK15K        KLIST
     C                   KFLD                    WSLB
     C                   KFLD                    YDT1
     C     BLV1K         KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    UAVD
     C     KODTPK        KLIST
     C                   KFLD                    KOPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    KOPLNR
      *
     C                   MOVE      'A'           KOAUNI
     C                   MOVE      'P'           KOPUNI
     C                   Z-ADD     33            KOPLNR
     C     KODTAK        CHAIN     KODTA                              33
     C     CUN1KA        CHAIN     CUNDL01                            33
     C                   MOVE      KNAVN         XNAA
     C                   MOVE      ADR1          XAD1A
     C                   MOVE      ADR2          XAD2A
     C                   MOVEL     POSTNR        XAD3A
     C                   MOVE      ADR3          XAD3A
     C     KODTPK        CHAIN     KODTP                              33
     C                   MOVE      KOPNVN        WSPRT
     C                   MOVE      *ZEROS        WSDT1
     C                   MOVE      'S'           WSLIGG
      *
     C                   ENDSR
      *
