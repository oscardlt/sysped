     H DFTACTGRP(*NO)
     FEDIM      UF   E           K DISK
     FEDIS      UF   E           K DISK
     FEDISE     UF   E           K DISK
     FEDIRE     UF   E           K DISK
     FEDISE1K   UF   E           K DISK    RENAME(EDISER:EDISER1K)
     F                                     PREFIX(K_)
     FEDIRE1K   UF   E           K DISK    RENAME(EDIRER:EDIRER1K)
     F                                     PREFIX(K_)
     FEDISX     UF   E           K DISK
     FXMLRCVF   UF   E           K DISK
     FXMLRCAF   UF   E           K DISK
     FXMLRCLF   UF   E           K DISK
     FSVIF      UF   E           K DISK
     FSVIFZ     UF   E           K DISK    RENAME(SVIFR:SVIFZR)
     FSVIH      UF   E           K DISK
     FSVIHE     UF   E           K DISK
     FSVIHF     UF   E           K DISK
     FSVIHG     UF   E           K DISK
     FSVIHH     UF   E           K DISK
     FSVIHI     UF   E           K DISK
     FSVIV      UF   E           K DISK
     FSVIVA     UF   E           K DISK
     FSVIVX     UF   E           K DISK
     FSVIVZ     UF   E           K DISK    RENAME(SVIVR:SVIVZR)
     FSVIX      UF   E           K DISK
     FSVIZ      UF   E           K DISK
     FFPSTD     UF   E           K DISK
     FFPXREF    UF   E           K DISK
     F*GODSJF    UF   E           K DISK
     FTVINF     UF   E           K DISK
     FFRISOK    UF   E           K DISK
     FTRACKF    UF   E           K DISK
      *
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FARKIVL17  UF   E           K DISK

     d                 ds
     d  usn_n                  1      9  0
     d  usn                    1      9
     d                 ds
     d  smbr                   1     10A
     d  smbr_1                 1      1A
     d  smbr_2                 2      8A
     d  smbr_3                 9     10A
     d  smbr_4                 4     10A

     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows0         s             10i 0
     d myrows          s             10i 0
     d myrows2         s             10i 0
     D XDTF            S              8  0
     D XDTP            S              8  0
     D XPOS            S              3  0
     d XAPS            C                   const(X'7D')
     D XMBR            S             10A
     D Error           S            150
     d X               S             10  0
     d X2              S             10  0
     D ufil            S            256A
     D uok             S              1A
     D XSLUTT          S              1
     D XSLUTT2         S              1
     D XSLT            S              1
      *
      *  Prototype for 'SYREO01C3'
     d SYREO01C3       pr                  extpgm('SYREO01C3')
     d ufil_                               like(ufil)
      *  Prototype for 'SYREO32R2'
     d syreo32r2       pr                  extpgm('SYREO32R2')
     d usn_                                like(usn)
     d udtp_                               like(udtp)
     d uok_                                like(uok)
      *  Prototype for syreo32r
     d syreo32r        pr
     d udtf_                               like(udtf)
     d udtp_                               like(udtp)
      *  *ENTRY Interface for Main Procedure
     d syreo32r        pi
     d udtf                           8
     d udtp                           8
      *
     d MyPoDS0         ds                  Qualified  dim(1)
     d  FIFIRM                        2A
     d MyPoDS          ds                  Qualified  dim(10000)
     d  SVIH_SYAV                     4S 0
     d  SVIH_SYOP                     7S 0
     d  SVIH_SYSG                     3A
     d  SVIH_SYDT                     8S 0
     d MyPoDS2         ds                  Qualified  dim(1000)
     d  MAVD                          4S 0
     d  MTDN                          7S 0
     d  MSR                           1A
     d  MMN                           9S 0
     d  MSN                           9S 0
      /free

         exsr init;

         dow xslutt <> 'J';
           exsr srhoved;
         enddo;

       *inlr = *on;
       return;

       //=====================================================================
       // Hoved logikk
       //=====================================================================
       begsr srhoved;

       clear MyPoDS;
       SqlStm = 'Select SVIH_SYAV, SVIH_SYOP, SVIH_SYSG, SVIH_SYDT ' +
                'from SVIH ' +
                'Where SVIH_SYOP > 0 AND SVIH_SYDT <= ' + %trim(udtf);
       Exec SQL PREPARE DynSel from :SqlStm;
       EXEC SQL DECLARE  A33  cursor for DynSel;
       Exec SQL OPEN A33;
       Myrows = %elem(MyPoDS);
       exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       Exec SQL CLOSE A33;

       if myrows > 0;
         for x = 1 to myrows;
           if mypods(x).svih_sysg = *blanks;
             xslutt = 'J';
             leave;
           endif;

       //  if %subst(mypods(x).sirg:10:2) = *blanks or                          //Firma 5 år
       //     %subst(mypods(x).sirg:10:2) <> *blanks and mypods(x).sidt < xdtp; //Personkunde 10 år
             svih_syav = mypods(x).svih_syav;
             svih_syop = mypods(x).svih_syop;

             exsr sra;
             exsr srb;
             xslutt2 = 'N';
             dow xslutt2 <> 'J';
               exsr src;
             enddo;
             exsr sre;
             exsr srf;
             exsr srg;

             exec sql
             delete from SVIHE
             where svihe_sya = :svih_syav and svihe_syo= :svih_syop
             with NC;
             exec sql
             delete from SVIHF
             where svihf_sya = :svih_syav and svihf_syo= :svih_syop
             with NC;
             exec sql
             delete from SVIHG
             where svihg_sya = :svih_syav and svihg_syo= :svih_syop
             with NC;
             exec sql
             delete from SVIHH
             where svihh_sya = :svih_syav and svihh_syo= :svih_syop
             with NC;
             exec sql
             delete from SVIHI
             where svihi_sav = :svih_syav and svihi_sop= :svih_syop
             with NC;
             exec sql
             delete from SVIX
             where svix_syav = :svih_syav and svix_syop= :svih_syop
             with NC;
             exec sql
             delete from SVIZ
             where sviz_syav = :svih_syav and sviz_syop= :svih_syop
             with NC;

           // slett hovedpost
             exec sql
             delete from SVIH
             where svih_syav = :svih_syav and svih_syop = :svih_syop
             with NC;
        // endif;  // End of "if %subst(mypods(x).svih_sysg:10:2) = *blanks or"

         endfor;
       endif;

       endsr;
      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM SVIV
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sra;

       exec sql
       delete from sviv
       where sviv_syav=:svih_syav and sviv_syop=:svih_syop
       with NC;

       exec sql
       delete from sviva
       where sviva_sav=:svih_syav and sviva_sop=:svih_syop
       with NC;

       exec sql
       delete from svivx
       where svivx_syav=:svih_syav and svivx_syop=:svih_syop
       with NC;

       exec sql
       delete from svivz
       where sviv_syav=:svih_syav and sviv_syop=:svih_syop
       with NC;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM SVIF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srb;

       exec sql
       delete from svif
       where svif_syav=:svih_syav and svif_syop=:svih_syop
       with NC;

       exec sql
       delete from svifz
       where svif_syav=:svih_syav and svif_syop=:svih_syop
       with NC;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIM/EDIS/TVINF
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src;

       clear MyPoDS2;
       SqlStm = 'Select MAVD, MTDN, MSR, MMN, MSN ' +
                'from EDIM ' +
                'Where MAVD = ' + %trim(%editc(svih_syav:'Z')) +
                ' and MTDN = ' + %trim(%editc(svih_syop:'Z')) +
                ' and M1001 <> ' + xaps + '929' + xaps +
                ' and M1001 <> ' + xaps + '830' + xaps +
                ' and M1001 <> ' + xaps + 'TEI' + xaps +
                ' and M1001 <> ' + xaps + 'TET' + xaps +
                ' and M1001 <> ' + xaps + 'SVE' + xaps +
                ' and M1N07 <> ' + xaps + 'SVE' + xaps +
                ' and M1001 <> ' + xaps + 'STI' + xaps +
                ' and M1N07 <> ' + xaps + 'STI' + xaps +
                ' and M1001 <> ' + xaps + 'STE' + xaps +
                ' and M1N07 <> ' + xaps + 'STE' + xaps +
                ' and M1001 <> ' + xaps + 'DKI' + xaps +
                ' and M1N07 <> ' + xaps + 'DKI' + xaps +
                ' and M1001 <> ' + xaps + 'DKE' + xaps +
                ' and M1N07 <> ' + xaps + 'DKE' + xaps;
       Exec SQL PREPARE DynSel2 from :SqlStm;
       EXEC SQL DECLARE  A34  cursor for DynSel2;
       Exec SQL OPEN A34;
       Myrows2 = %elem(MyPoDS2);
       exec sql FETCH A34 for :MyRows2 rows
                  INTO :MyPoDs2;
       Exec SQL CLOSE A34;

       if myrows2 > 0;
         for x2 = 1 to myrows2;
           if mypods2(x2).msr = *blanks;
             xslutt2 = 'J';
             leave;
           endif;

           mmn = mypods2(x2).mmn;
           msn = mypods2(x2).msn;

           exec sql
           delete from TVINF
           where fmn = :mmn
           with NC;
           exec sql
           delete from XMLRCVF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCAF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCLF
           where xrsn = :msn
           with NC;

           chain (msn) edis;
           if %found;
            // slett komponent
             exsr src2;
            // slett fil i IFS
             if sifs <> *blanks;
               if %subst(sifs:1:1) <> '/';
                 sifs = '/' + %trim(sifs);
               endif;
               ufil = sifs;
               syreo01c3 (ufil);
             endif;
             delete edisr;
           endif;

         // slett hovedpost
           exec sql
           delete from EDIM
           where mmn = :mmn
           with NC;

         endfor;
       endif;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIRE/EDISE/EDIRE1K/EDISE1K/XMLRCVF/AF/LF/EDISX
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src2;

       exec sql
       delete from edire
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise
       where sembr = :smbr
       with NC;

       exec sql
       delete from edire1k
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise1k
       where sembr = :smbr
       with NC;

       chain smbr edisx;
       if %found;
         if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
           sxifsobj = '/' + %trim(sxifsobj);
         endif;
         syreo01c3 (sxifsobj);
         delete edisxr;
       else;
         if smbr_3 = ' ';
         // MBR = R1234567 -> PRØV R001234567
           xmbr = smbr_1 + '00' + smbr_2;
         else;
         // MBR = R001234567 -> PRØV R1234567
           xmbr = smbr_1 + smbr_4 + '  ';
         endif;
         chain xmbr edisx;
         if %found;
           if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
             sxifsobj = '/' + %trim(sxifsobj);
           endif;
           syreo01c3 (sxifsobj);
           delete edisxr;
         endif;
       endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM GODSJF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sre;

       //if %subst(sign:14:2) <> *blanks;
       //exec sql
       //delete from godsjf
       //where gogn = :sign
       //with NC;
       //endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM FRISOK/TRACKF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srf;

       exec sql
       delete from frisok
       where fsavd = :svih_syav and fsopd = :svih_syop
       with NC;

       exec sql
       delete from trackf
       where ttavd = :svih_syav and ttopd = :svih_syop
       with NC;

       setll (svih_syav:svih_syop) arkivl17;
       reade (svih_syav:svih_syop) arkivl17;
       dow not %eof;
         chain (fifirm) firmarc;
         chain (artype) arktxt;
         if %found and arklag <> *blanks;
           chain (fifirm:arklag) arkext;
         endif;
         if arbhis <> *blanks;
         // Bane for historikk
           arcane = arbhis;
         endif;
         xpos = %scan ('.' : arlink : 1);
         if xpos = 0;
           ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK) + '.pdf';
         else;
           ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK);
         endif;
         syreo01c3 (ufil);

         delete arkivr;
         reade (svih_syav:svih_syop) arkivl17;
       enddo;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM FPSTD/FPXREF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srg;

       exec sql
       delete from fpstd
       where fpstda = :svih_syav and fpstdo = :svih_syop
       with NC;

       exec sql
       delete from fpxref
       where fpavd = :svih_syav and fpopd = :svih_syop
       with NC;

       endsr;

      //***********************************************
       begsr init;
      //***********************************************
       xdtf = %float(udtf);
       xdtp = %float(udtp);

       SqlStm = 'Select * ' +
                'from FIRM ' +
                'FOR READ ONLY ';
       Exec SQL PREPARE DynSel0 from :SqlStm;
       EXEC SQL DECLARE  A30  cursor for DynSel0;
       Exec SQL OPEN A30;
       Myrows0 = %elem(MyPoDS0);
       exec sql FETCH A30   for :MyRows0 rows
                  INTO :MyPoDs0;
       Exec SQL CLOSE A30;
       FIFIRM = mypods0(1).fifirm;

       endsr;

      /end-free
