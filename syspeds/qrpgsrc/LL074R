     FLLLISF    UF A E           K DISK
     FKODTSB    IF   E           K DISK
     FKODTSC    IF   E           K DISK
     FLL074D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  LITXT                  1     35
     D  XDLT                   1      7
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *ZEROS        XXLN              3 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LLISK1        SETLL     LLLISF
     C     LLISK1        READE(N)  LLLISF                                 94
     C     *IN94         DOWEQ     '0'
     C                   MOVE      LILN          XXLN
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     LLISK1        READE(N)  LLLISF                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      *
     C                   SETOFF                                       9830
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *
      *** LAG NY
     C     *IN06         IFEQ      '1'
     C                   EXSR      CRTPOS
     C                   GOTO      START3
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C                   GOTO      START3
     C                   END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       9998
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       10            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C     XDLT          ANDNE     '*DELETE'
     C                   EXSR      CHGPOS
     C                   END
     C     WSNR          IFEQ      '4'
     C     XDLT          ANDNE     '*DELETE'
     C                   EXSR      DLTPOS
     C                   END
     C     WSNR          IFEQ      '9'
     C     XDLT          ANDNE     '*DELETE'
     C                   EXSR      CPYPOS
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
     C                   ENDSR
      *
     C***********************************************
     C     CRTPOS        BEGSR
     C***********************************************
     C                   EXSR      MOVB2
      *
     C                   MOVE      '0'           *IN40
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     10            POSNBR
      *
     C     STCRT         TAG
     C                   EXFMT     BILDE
     C                   MOVEA     '00000'       *IN(86)
     C                   MOVE      '0'           *IN30
      *
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKDTA
     C                   GOTO      STCRT
     C                   END
      *
     C     *IN12         IFEQ      '1'
     C                   GOTO      SLCRT
     C                   END
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCRT
      *
     C     SLCRT         TAG
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGPOS        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN40
      *
     C                   MOVE      LIKD          WSKD
     C                   MOVE      LITXT         WSTXT
     C                   MOVE      LIREFI        WSREFI
     C                   MOVE      LIREFE        WSREFE
     C                   MOVE      LIANT         WSANT
      *
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     10            POSNBR
      *
     C     STCHG         TAG
     C                   EXFMT     BILDE
     C                   MOVEA     '00000'       *IN(86)
     C                   MOVE      '0'           *IN30
      *
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
      *
     C     *IN12         IFEQ      '1'
     C                   GOTO      SLCHG
     C                   END
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCHG
      *
     C     SLCHG         TAG
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTPOS        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
      *
     C                   MOVE      *BLANKS       LIKD
     C                   MOVE      *BLANKS       LITXT
     C                   MOVEL     '*DELETE'     LITXT
     C                   MOVE      *BLANKS       LIREFI
     C                   MOVE      *BLANKS       LIREFE
     C                   MOVE      *ZEROS        LIANT
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C     LLISK2        CHAIN     LLLISF                             33
     C                   DELETE    LLLISFR
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYPOS        BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN40
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   CALL      'LL077R'
     C                   PARM                    UGRP
     C                   PARM                    UPNR
     C                   PARM                    UMN
     C                   PARM                    UMRNR
     C                   PARM                    LIKD
     C                   PARM                    LITXT
     C                   PARM                    LIREFI
     C                   PARM                    LIREFE
     C                   PARM      LIANT         UANT              5 0
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     10            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      *BLANKS       WSKD
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      *BLANKS       WSREFI
     C                   MOVE      *BLANKS       WSREFE
     C                   MOVE      *ZEROS        WSANT
     C                   MOVE      *ZEROS        LILN
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     WSTXT         COMP      *BLANKS                                89
     C     *IN89         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC1841'     MSGNR
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      SLTST
     C                   END
      *
     C     WSREFI        IFNE      *BLANKS
     C     WSREFI        CHAIN     KODTSB                             88
     C     *IN88         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPC0006'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      SLTST
     C                   END
     C                   END
      *
     C     WSREFE        IFNE      *BLANKS
     C     WSREFE        CHAIN     KODTSC                             87
     C     *IN87         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPC0006'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      SLTST
     C                   END
     C                   END
      *
     C     LLISK3        CHAIN     LLLISF                             33
     C                   MOVE      WSKD          LIKD
     C                   MOVE      WSTXT         LITXT
     C                   MOVE      WSREFI        LIREFI
     C                   MOVE      WSREFE        LIREFE
     C                   MOVE      WSANT         LIANT
     C     *IN33         IFEQ      '0'
     C                   UPDATE    LLLISFR
     C                   ELSE
     C                   MOVE      UMN           LIMN
     C                   ADD       1             XXLN
     C                   MOVE      XXLN          LILN
     C                   WRITE     LLLISFR
     C                   END
      *
     C     *IN40         IFEQ      '1'
     C                   UPDATE    SUBFIL
     C                   ELSE
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTST         ENDSR
      *
      ***********************************************
     C     SØKDTA        BEGSR
      ***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      7
     C                   MOVE      'J'           UIMP              1
     C                   CALL      'SAD061K'
     C                   PARM                    WSREFI
     C                   PARM                    UIMP
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      8
     C                   MOVE      'N'           UIMP              1
     C                   CALL      'SAD061K'
     C                   PARM                    WSREFE
     C                   PARM                    UIMP
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UGRP              8 0
     C                   PARM                    UPNR              8 0
     C                   PARM                    UMN               7 0
     C                   PARM                    UMRNR             5 0
     C                   PARM                    UANR             15
     C                   PARM                    UVB              35
     C                   PARM                    USALD             7 0
      *
     C     LLISK1        KLIST
     C                   KFLD                    UMN
     C     LLISK2        KLIST
     C                   KFLD                    UMN
     C                   KFLD                    LILN
     C     LLISK3        KLIST
     C                   KFLD                    UMN
     C                   KFLD                    LILN
      *
     C                   WRITE     WINDOWSZ
     C                   MOVEL     UVB           XVB
      *
     C                   ENDSR
      *
