      *
      ***********************************************************
      *** PROGRAM SKAL SPØRRE PÅ UTLEVERINGSFORBEHOLD
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-88 95-97
      ***********************************************************
      *
     FKODTU     IF   E           K DISK
     FKODTULF   IF   E           K DISK
     F                                     RENAME(KODTUR:KODTURL)
     FSYFA71FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N27W2KD1         SETLL     KODTU
     C   27W2SØK         SETLL     KODTULF
     C     START1        TAG
     C  N27              READ      KODTU                                  94
     C   27              READ      KODTULF                                94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        5
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C  N27              READ      KODTU                                  94
     C   27              READ      KODTULF                                94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     *IN29         IFEQ      '0'
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     9             POSNBR
     C                   MOVE      '1'           *IN29
     C                   ELSE
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C*
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   MOVE      *BLANKS       USØK
     C                   MOVE      *BLANKS       UKD1X
     C                   CALL      'SYFA71'
     C                   PARM                    UKD1X             2
     C                   PARM                    USØK              1
     C                   GOTO      START
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C     W2KD1         ANDNE     *BLANKS
     C                   MOVE      W2KD1         UKD1
     C                   GOTO      SLUTT
     C                   END
     C     W2KD1         IFNE      *BLANKS
     C                   MOVE      '0'           *IN27
     C                   GOTO      START
     C                   END
     C     W2SØK         IFNE      *BLANKS
     C                   MOVE      '1'           *IN27
     C                   GOTO      START
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       9192
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'
     C                   MOVE      KUKD1         UKD1
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C                   END
      *
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
      *
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UKD1              2
      *
     C                   MOVE      *BLANKS       W2SØK
     C     UKD1          IFEQ      *BLANKS
     C                   MOVE      '1'           *IN27
     C                   ELSE
     C                   MOVE      UKD1          W2KD1
     C                   END
      *
     C                   WRITE     WINDOWSZ
     C*
     C                   ENDSR
     C*
