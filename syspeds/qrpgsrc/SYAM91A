      *
      **************************************************************
      *** PROGRAM SKAL ARBEIDE MED AMETAAVREGNING                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER                                     ***
      **************************************************************
      *
     FAMGEWRK   UF A E           K DISK
     FAMGEBYR   IF   E           K DISK
     FSYAM01FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  DSNVN                  1     30
     D  DSNVN1                 1     10
     D  DSNVN2                11     11
     D  DSNVN3                12     30
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UTNR              8
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
     C                   PARM                    UKNA              8
     C                   PARM                    UNAVN            30
     C                   PARM                    UIMEX             1
     C                   PARM                    UPOS              7
     C                   PARM                    UUNR              8
     C                   PARM                    UEINT
     C                   PARM                    UEUTG
     C                   PARM                    UAINT
     C                   PARM                    UAUTG
     C                   PARM                    UVEKT             5
     C                   PARM                    UTXT              2
     C                   PARM                    UFRAN             3
     C                   PARM                    UVOL              7
     C*
     C                   MOVE      UTNR          W1TNR             8 0
     C                   MOVE      UAVD          W1AVD
     C                   MOVE      UOPD          W1OPD
     C                   MOVE      UKNA          W1KNA
     C                   MOVEL     UNAVN         W1NAVN
     C                   MOVE      UIMEX         W1IMEX            1
     C                   MOVE      UPOS          W1POS
     C                   MOVE      UUNR          W1UNR             8 0
     C                   Z-ADD     W1UNR         W1UNRX
     C*
     C                   MOVE      UAVD          ZEAVD             4 0
     C                   MOVE      UOPD          ZEOPD             7 0
     C                   MOVE      UVEKT         W1VEKT
     C                   MOVE      UTXT          W1VTXT
     C                   MOVE      UFRAN         W1FRAN
     C                   MOVE      UVOL          W1VOL
     C*
     C                   EXSR      INIT
     C*
     C     CHOISE        TAG
     C*
     C                   CLEAR                   W1LI
     C                   CLEAR                   ZRECNO
     C                   CLEAR                   MRECNO
     C*
     C     CHOIS2        TAG
     C                   Z-ADD     07            LINNBR
     C                   Z-ADD     14            POSNBR
     C     CHOIS3        TAG
     C                   CLEAR                   *IN12
     C*  SUBFILE CLEAR
     C     START         TAG
     C                   CLEAR                   TEI999
     C                   CLEAR                   TEU999
     C                   CLEAR                   TAI999
     C                   CLEAR                   TAU999
     C                   CLEAR                   TOEINT
     C                   CLEAR                   TOEUTG
     C                   CLEAR                   TOAINT
     C                   CLEAR                   TOAUTG
     C                   MOVEA     '00'          *IN(91)
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   CLEAR                   ZRECNO
     C                   CLEAR                   MRECNO
     C                   MOVE      '1'           *IN47
     C     START1        TAG
     C* LES ALLE FAKTURALINJENE PÅ OPPDRAGET
     C                   SETOFF                                           38
     C* HENTER INNTEKTER/KOSTNADER PÅ OPPDRAGET
     C     FALES         TAG
     C                   CLEAR                   THEINT
     C                   CLEAR                   THEUTG
     C                   CLEAR                   THAINT
     C                   CLEAR                   THAUTG
     C                   EXSR      LESWRK
     C*
     C     START2        TAG
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   Z-ADD     1             ZRECNO
     C     START3        TAG
     C*
     C     W1POS         IFEQ      '9999'
     C     W1UNR         ANDGE     96
     C                   MOVE      *ON           *IN55
     C                   ELSE
     C                   MOVE      *OFF          *IN55
     C                   END                                                    <W1POS
     C*
     C                   MOVEA     '11'          *IN(91)
     C     MRECNO        IFEQ      0
     C                   Z-ADD     8             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   MOVE      '1'           *IN(94)
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   EXFMT     BUNN
     C                   END
     C***
     C                   SETOFF                                       9899
     C*
     C*** FRISK OPP
     C     *IN05         IFEQ      '1'
     C                   EXSR      LESWRK
     C                   Z-ADD     1             ZRECNO
     C                   GOTO      START3
     C                   END                                                    <*IN05
     C*** NY LINJE
     C     *IN06         IFEQ      '1'
     C*
     C     W1POS         IFEQ      '9999'
     C     W1UNR         ANDGE     96
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      NYLIN
     C                   END                                                    <W1POS
     C*
     C                   EXSR      LESWRK
     C                   GOTO      START3
     C                   END                                                    <*IN06
     C*** TILBAKE
     C     *IN12         IFEQ      '1'
     C                   MOVE      TOEINT        UEINT
     C                   MOVE      TOEUTG        UEUTG
     C                   MOVE      TOAINT        UAINT
     C                   MOVE      TOAUTG        UAUTG
     C                   GOTO      SLUTT
     C                   END                                                    <*IN12
     C*
     C                   SETOFF                                       94
     C*
     C                   SETOFF                                       94
     C*** ROLLUP
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   MOVE      '1'           *IN94
     C                   GOTO      START3
     C                   END                                                    <*IN25
     C*
     C*** ROLLDOWN
     C     *IN26         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   MOVE      '1'           *IN94
     C                   GOTO      START3
     C                   END                                                    <*IN26
     C*
     C     ZRECNO        IFNE      0
     C*
     C     W1POS         IFEQ      '9999'
     C     W1UNR         ANDGE     96
     C                   GOTO      START3
     C                   ENDIF                                                  <W1POS
     C*
     C                   EXSR      HENTSR
     C                   EXSR      LESWRK
     C                   END                                                    <ZRECNO
     C*
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C*
     C*
     C***********************************************
     C     LESWRK        BEGSR
     C***********************************************
     C* ---------------------------------------------
     C* LES ALLE AMETALINJER PÅ WORKFILE
     C* """""""""""""""""""""""""""""""""""""""""""""
     C*
     C                   MOVE      ZRECNO        XRECNO
     C                   CLEAR                   TEI999
     C                   CLEAR                   TEU999
     C                   CLEAR                   TAI999
     C                   CLEAR                   TAU999
     C                   CLEAR                   TOEINT
     C                   CLEAR                   TOEUTG
     C                   CLEAR                   TOAINT
     C                   CLEAR                   TOAUTG
     C*
     C                   CLEAR                   ZRECNO
     C                   CLEAR                   MRECNO
     C                   CLEAR                   ANTF2
     C                   MOVEA     '00'          *IN(91)
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   MOVE      W1TNR         ZTUNR
     C                   MOVE      W1POS         ZPOS
     C                   MOVE      W1UNR         ZUNR
     C     AMGEKY        SETLL     AMGEWRK                                38
     C     WRKTAG        TAG
     C     AMGEKY        READE     AMGEWRK                                38
     C*   DISPLAY SUBFILE
     C   38              GOTO      ENDWRK
     C*
     C                   ADD       1             ZRECNO
     C                   ADD       1             MRECNO
     C                   ADD       1             ANTF2             4 0
     C                   EXSR      ADDS0
     C                   MOVE      ' '           WSNRO
     C                   WRITE     SUBFIL
     C     ZTUNR         IFEQ      AXTUNR
     C     ZKNA          ANDEQ     AXKNA
     C     ZPOS          ANDEQ     AXPOS
     C     ZUNR          ANDEQ     AXUNR
     C                   MOVE      ZRECNO        XRECNO
     C                   END                                                    <ZTUNR
     C* FLERE AMETALINJER PÅ DENNE TUR
     C                   GOTO      WRKTAG
     C*
     C     ENDWRK        TAG
     C     XRECNO        IFGT      MRECNO
     C                   MOVE      MRECNO        ZRECNO
     C                   ELSE
     C                   MOVE      XRECNO        ZRECNO
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     ADDS0         BEGSR
     C***********************************************
     C                   MOVE      AXVK          THAVK
     C                   MOVE      AXLI          THLI
     C                   MOVE      AXLI          W1LI              2 0
     C                   Z-ADD     AXEINT        THEINT
     C                   ADD       AXEINT        TOEINT
     C                   Z-ADD     AXEUTG        THEUTG
     C                   ADD       AXEUTG        TOEUTG
     C*
     C                   Z-ADD     AXAINT        THAINT
     C                   ADD       AXAINT        TOAINT
     C                   Z-ADD     AXAUTG        THAUTG
     C                   ADD       AXAUTG        TOAUTG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     NYLIN         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      LINNBR        OLDLIN
     C                   MOVE      POSNBR        OLDPOS
     C                   CLEAR                   ADDLIN
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     06            POSNBR
     C     NYLI1         TAG
     C                   EXFMT     ADDLIN
     C                   MOVE      '0'           *IN30
     C   12              GOTO      ENDNY
     C*
     C     W1POS         IFNE      '9999'
     C                   EXSR      TESTLI
     C                   END                                                    <W1POS
     C     *IN30         IFEQ      '1'
     C                   GOTO      NYLI1
     C                   END                                                    <*IN30
     C                   ADD       1             W1LI
     C                   MOVE      W1LI          THLI
     C                   ADD       1             MRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   MOVE      MRECNO        ZRECNO
     C                   MOVE      '0'           *IN44
     C                   WRITE     SUBFIL
     C                   MOVEA     '100'         *IN(80)
     C                   EXSR      UPDWRK
     C                   MOVE      XRECNO        ZRECNO
     C*
     C     ENDNY         TAG
     C                   MOVE      OLDLIN        LINNBR
     C                   MOVE      OLDPOS        POSNBR
     C                   ENDSR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   ENDDO                                                  <98 DO
     C     ZRECNO        DIV       12            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     7             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C*    OPTION 2=ENDRE
     C     WSNRO         IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNRO
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     11            POSNBR
     C     TENDRE        TAG
     C                   MOVE      '0'           *IN44
     C                   EXFMT     CHGLIN
     C*
     C*  TEST FOR GYLDIG FRANKATUR
     C*
     C   12              GOTO      SLHENT
     C*
     C     *IN30         IFEQ      '1'
     C                   GOTO      TENDRE
     C                   END                                                    <*IN30
     C*
     C                   UPDATE    SUBFIL
     C                   MOVEA     '010'         *IN(80)
     C                   EXSR      UPDWRK
     C                   END                                                    <WSNRO
     C*    OPTION 4=SLETTE
     C     WSNRO         IFEQ      '4'                                          SLETTE
     C                   Z-ADD     01            LINNBR
     C                   Z-ADD     02            POSNBR
     C                   MOVE      '1'           *IN99
     C                   MOVE      '0'           *IN44
     C                   EXFMT     DLTLIN
     C   12              GOTO      SLHENT
     C                   UPDATE    SUBFIL
     C                   MOVEA     '001'         *IN(80)
     C                   EXSR      UPDWRK
     C                   END                                                    <WSNRO
     C*
     C                   END                                                    <*IN98
     C*
     C     SLHENT        TAG
     C                   ADD       6             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     UPDWRK        BEGSR
     C***********************************************
     C*
     C                   SETOFF                                       83
     C*  ENDRING
     C     *IN81         IFEQ      '1'
     C     AMGEK1        CHAIN     AMGEWRK                            83
     C                   END                                                    <*IN81
     C*
     C                   MOVE      '2'           AXST
     C                   MOVE      W1TNR         AXTUNR
     C                   MOVE      W1KNA         AXKNA
     C                   MOVEL     W1POS         AXPOS
     C                   MOVE      W1UNR         AXUNR
     C                   MOVE      W1AVD         AXAVD
     C                   MOVE      W1OPD         AXOPD
     C                   MOVE      THAVK         AXVK
     C                   MOVE      THEINT        AXEINT
     C                   MOVE      THEUTG        AXEUTG
     C                   MOVE      THAINT        AXAINT
     C                   MOVE      THAUTG        AXAUTG
     C*  NY LINJE
     C     *IN80         IFEQ      '1'
     C                   MOVE      W1LI          AXLI
     C                   WRITE     AXGER
     C                   END                                                    <*IN80
     C*  ENDRING
     C     *IN81         IFEQ      '1'
     C  N83              UPDATE    AXGER
     C                   END                                                    <*IN81
     C*  SLETTING
     C     *IN82         IFEQ      '1'
     C     AMGEK1        CHAIN     AMGEWRK                            83
     C  N83              DELETE    AXGER
     C                   END                                                    <*IN82
     C                   MOVEA     '0000'        *IN(80)
     C                   ENDSR
     C*
     C***********************************************
     C     TESTLI        BEGSR
     C***********************************************
     C* GEBYR
     C                   MOVE      '0'           *IN30
     C*
     C     AGGEKY        CHAIN     AMGEBYR                            36
     C*
     C     *IN36         IFNE      '0'
     C                   MOVE      'TGI0035'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     06            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN36
     C                   GOTO      SLTLI
     C                   END                                                    <*IN36
     C     SLTLI         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    THEINT        UEINT
     C     *LIKE         DEFINE    THEUTG        UEUTG
     C     *LIKE         DEFINE    THAINT        UAINT
     C     *LIKE         DEFINE    THAUTG        UAUTG
     C     *LIKE         DEFINE    THEINT        DFEINT
     C     *LIKE         DEFINE    THEUTG        DFEUTG
     C     *LIKE         DEFINE    THAINT        DFAINT
     C     *LIKE         DEFINE    THAUTG        DFAUTG
     C     *LIKE         DEFINE    THEINT        TEI999
     C     *LIKE         DEFINE    THEUTG        TEU999
     C     *LIKE         DEFINE    THAINT        TAI999
     C     *LIKE         DEFINE    THAUTG        TAU999
     C     *LIKE         DEFINE    LINNBR        OLDLIN
     C     *LIKE         DEFINE    POSNBR        OLDPOS
     C     *LIKE         DEFINE    ZRECNO        MRECNO
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL AMGE (AMGEWRK)
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    AXTUNR        ZTUNR
     C     *LIKE         DEFINE    AXKNA         ZKNA
     C     *LIKE         DEFINE    AXPOS         ZPOS
     C     *LIKE         DEFINE    AXUNR         ZUNR
     C     AMGEKY        KLIST
     C                   KFLD                    W1TNR
     C                   KFLD                    W1KNA
     C                   KFLD                    W1POS
     C                   KFLD                    W1UNR
     C                   KFLD                    W1AVD
     C                   KFLD                    W1OPD
     C*
     C     AMGEK1        KLIST
     C                   KFLD                    W1TNR
     C                   KFLD                    W1KNA
     C                   KFLD                    W1POS
     C                   KFLD                    W1UNR
     C                   KFLD                    W1AVD
     C                   KFLD                    W1OPD
     C                   KFLD                    THLI
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL AMGEBYR
     C*--------------------------------------------------------------
     C     AGGEKY        KLIST
     C                   KFLD                    W1KNA
     C                   KFLD                    W1IMEX
     C                   KFLD                    THAVK
     C*
     C     UTI           ENDSR
     C*
