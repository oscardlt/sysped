      *   Arbeide med kostnadsbilag.
      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/ESKS01 MODULE(*LIBL/ESKS01 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Omskrevet til Jquery tablesort / Multifs
      *              gamle sortteknikker slettet uten erking (se ESKS01.G)
10XXX * 02 YBC PTF10 TITLE
      ********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKOSTA1    IF   E           K DISK    USROPN
     FLEVEL01   IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FARKIVL11  IF   E           K DISK    USROPN
     FARKTXT    IF   E           K DISK    USROPN
     FARKEXT    IF   E           K DISK    USROPN
     FFIRMARC   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D Sort            s             10
     D Frames          s              1
     D VisLChecked     s             10
     D VisLager        s              1
     D KAFNRLINK       s            512
     D KAFLINK1        s            512
     D KAFLINK2        s            512
N02  D TITLE           s            100
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
      * Attest.koder 12 elem. a 4 tegn (XXX,YYY,ZZ , de 3 venstre tegn er attkod)
     D                 DS
     D  AtString               1     48
     D  AT4                    1     48    DIM(12)
      * Attest.koder 12 elem. a 3 tegn
     D                 DS
     D  ATT                    1     36    DIM(12)
      * Indicators for GetHtmlIfsMult subprocedure
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
      * Frames = Y = init / første runde
     C     FRAMES        IFeq      'Y'
     C                   callp     wrtsection('all')
     c                   goto      slutt
     C                   END
     C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')
      * HOVEDRUTINE
     c     ' '           setLL     KOSTA1
     c     ' '           ReadE     KOSTA1                                 35
      *
     c     *IN35         doweq     '0'
      * filter
     c     ATTKODER      Ifne      '*ALL'
     c     KASG          cabeq     '   '         NextRec
     C     KASG          lookup    ATT                                    33
     c     *in(33)       cabne     '1'           NextRec
     c                   end
      * Test om arkiverte dokumenter finnes
     C                   IF        ((XKUNMEDVEDL = 'J') OR (XKUNMEDVEDL = 'j'))
     C                   movel     'KL:'         ARUNDE
     C                   move      KABNR         ARUNDE
     C     Ark11Key      chain     ARKIVL11                           33
     C     *in(33)       CABEQ     '1'           NextRec
     C                   END
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c     NextRec       Tag
     c     ' '           ReadE     KOSTA1                                 35
     c                   end
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('botStd')                         Body/htmlavsl
      *  Burde tømme workfil dersom en avslutter med å "gå ut"
     c     Slutt         tag
     C*                  EXSR      DELWRK
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user = zhbgetvar('user')
     C                   eval      FRAMES   = zhbgetvar('FRAMES')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     KOSTA1
     C                   CLOSE     LEVEL01
     C                   CLOSE     BRPARF
     C                   CLOSE     ARKIVL11
     C                   CLOSE     ARKEXT
     C                   CLOSE     ARKTXT
     C                   CLOSE     FIRMARC
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
      * 1.KALL (eller refr/Frames) - VIS HTML SOM KONTROLLERER FRAMES
     C     FRAMES        IFEQ      'Y'
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/eSpedTop.html -
cccccC                             /syspeddev/html/ESKS00F.html -
cccccC                             /syspeddev/html/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   else
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/eSpedTop.html -
cccccC                             /syspeddev/html/ESKS01.html -
cccccC                             /syspeddev/html/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   endif
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updHTMLvar('VISLager':VISLager)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('ATTKODER':ATTKODER)
     C                   callp     updHTMLvar('SORT':SORT)
     C                   callp     updHTMLvar('FRAMES':FRAMES)
N01  c                   eval      Title='eSped work flow - bilagsattestering'
N01  C                   callp     updhtmlvar('TITLE':Title)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     c     LevKey        chain     LEVEL01                            33
      *
     C                   callp     updHTMLvar('KABNR':
     C                             %trim(%editc(KABNR:'Z')))
     C                   callp     updHTMLvar('KABNR2':
     C                             %trim(%editc(KABNR2:'Z')))
     C                   callp     updHTMLvar('KAPCC':
     C                             %trim(%editc(KAPCC:'Z')))
     C                   callp     updHTMLvar('KAPÅR':
     C                             %trim(%editc(KAPÅR:'Z')))
     C                   callp     updHTMLvar('KABDT':
     C                             %trim(%editW(KABDT:'    .  .  ')))
     C                   callp     updHTMLvar('KALNR':
     C                             %trim(%editc(KALNR:'Z')))
     C                   callp     updHTMLvar('DSLNAVN':LNAVN)
     C                   callp     updHTMLvar('KAVAL':KAVAL)
     C                   callp     updHTMLvar('KABL':
     C                             %trim(%editc(KABL:'J')))
     C                   callp     updHTMLvar('KAFNR':KAFNR)
      * fakt.nr - i utgangspunktet kun et infofelt,
     c                   eval      KAFNRLINK = KAFNR                            FAKTNR-LINK
     c                   eval      KAFLINK1  = *BLANKS                          Løpenr-lnk(bane/dok)
     c                   eval      KAFLINK2  = *BLANKS                          "bodyright"
      * Men dersom det ligger i arkiv så skal det være klikkbart
      * ved mer enn ett arkivert - via prog ESKS03 for link-oversikt
     c                   movel     'KL:'         ARUNDE
     c                   move      KABNR         ARUNDE
     c     Ark11Key      chain     ARKIVL11                           33
     c     *in33         ifeq      '0'                                        minst en FINNES
     C     Ark11Key      READE     ARKIVL11                               33
     c     *in33         ifeq      '1'                                        EN og BARE EN
      *(etter overført Øko blir det en link pr oppdrag, men ved ÅPNE bilag er det en link pr dokum.)
     c     ARTYPE        chain     ARKTXT                             33
     C     ARKLAG        IFNE      *blanks
     c     ArklagKey     chain     arkext
     c                   else
     c     BIFIRM        chain     firmarc                            33
     c                   end
      * Link via FAKTNR-kolonnen
     C                   eval      KAFNRLINK = '<a href="/'
     C                             + %TRIM(ARCANE) + '/'
     C                             + %TRIM(ARLINK)
     C     '.pdf'        SCAN      KAFNRLINK                              33
     C  N33'.PDF'        SCAN      KAFNRLINK                              33
     C  N33              CAT       '.pdf"':0     KAFNRLINK
     C   33              CAT       '"':0         KAFNRLINK
     C                   CAT       'target="bo':1KAFNRLINK
     C                   CAT       'dyright">':0 KAFNRLINK
     C                   CAT       KAFNR:1       KAFNRLINK
     C                   CAT       '</a>':0      KAFNRLINK
      * parametre for javascript knyttet til kolonne Løpenr
     C                   eval      KAFLINK1  = '/'
     C                             + %TRIM(ARCANE) + '/'
     C                             + %TRIM(ARLINK)
     C  N33              CAT       '.pdf':0      KAFLINK1
     C                   EVAL      KAFLINK2 = 'bodyright'
     c                   else
      * det finnes - MEN flere.. link via prog
     c                   eval      KAFNRLINK = '<a href="/sycgip/ESKS03.pgm?'+
     C                                        'user='+%trim(USER)+'&under=' +
     c                                        ARUNDE+'" target="bodyright">' +
     c                                        %trim(KAFNR)+' (+)</a>'
     c                   eval      KAFLINK1  = '/sycgip/ESKS03.pgm?'+
     C                                        'user='+%trim(USER)+'&under=' +
     c                                        ARUNDE
     C                   EVAL      KAFLINK2 = 'bodyright'
     c                   endif                                                  end KUN EN
     c                   endif                                                  Finnes
     C                   callp     updHTMLvar('KAFNRLINK':KAFNRLINK)
     C                   callp     updHTMLvar('KAFLINK1':KAFLINK1)
     C                   callp     updHTMLvar('KAFLINK2':KAFLINK2)
     C                   callp     updHTMLvar('KAFDT':
     C                             %trim(%editW(KAFDT:'    .  .  ')))
     C                   callp     updHTMLvar('KAFFDT':
     C                             %trim(%editW(KAFFDT:'    .  .  ')))
     C                   callp     updHTMLvar('KAMVA':KAMVA)
     C                   callp     updHTMLvar('KABLM     ':
     C                             %trim(%editc(KABLM        :'J')))
     C                   callp     updHTMLvar('KAUSER':KAUSER)
     C                   callp     updHTMLvar('KADTE':
     C                             %trim(%editW(KADTE:'    .  .  ')))
     C                   callp     updHTMLvar('KATME':
     C                             %trim(%editW(KATME:'  :  :  ')))
     C                   callp     updHTMLvar('KAJOUR    ':
     C                             %trim(%editc(KAJOUR       :'Z')))
     C                   callp     updHTMLvar('KADTR':
     C                             %trim(%editW(KADTR:'    .  .  ')))
     C                   callp     updHTMLvar('KASG ':KASG )
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      KOSTA1
     C                   OPEN      LEVEL01
     C                   OPEN      BRPARF
     C                   OPEN      ARKIVL11
     C                   OPEN      ARKEXT
     C                   OPEN      ARKTXT
     C                   OPEN      FIRMARC
      *
     C     LevKey        klist
     C                   kfld                    BIFIRM
     C                   kfld                    KALNR
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C     KundKey       klist
     C                   kfld                    DFTUSER          10
     C                   kfld                    BIKUNR
     C                   kfld                    PAID
     C                   Eval      DFTUSER = '*KUNDFT*'+BIFIRM
     C     GrupKey       klist
     C                   kfld                    DFTUSER          10
     C                   kfld                    BIKNG
     C                   kfld                    PAID
     C     FirmKey       klist
     C                   kfld                    DFTUSER
     C                   kfld                    NULLKU            8 0
     C                   kfld                    PAID
     C     Ark11Key      klist
     C                   kfld                    ARSTAT
     C                   kfld                    ARUNDE
     C                   move      'A'           ARSTAT
     C     ArklagKey     KLIST
     C                   KFLD                    Bifirm
     C                   KFLD                    arklag
      * I utgangspunktet KAN en  jobbe med alle att-koder, men det KAN begrenses
     c                   movel     '*ALL'        ATTKODER         50
      * Brukeren kan kun jobbe for følgende Attestasjonskode.
     C                   MOVE      'KSATT'       PAID
     C     BrParKey      chain     BRPARF                             33
     C     *in33         ifeq      *OFF
     C                   MOVEL     PAVRDA        ATTKODER
     C                   MOVEL     PAVRDA        AtString
     c     1             do        12            Nr                2 0
     c                   movel     AT4(Nr)       ATT(Nr)
     C                   end
     C                   end
      * Hent kode for å KUN jobbe med bilag som har vedlegg
     C                   MOVE      'KVEDL'       PAID
     C     BrParKey      chain     BRPARF                             33
     C   33KundKey       chain     BRPARF                             33
     C   33GrupKey       chain     BRPARF                             33
     C   33FirmKey       chain     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XKUNMEDVEDL       1
     C   33              MOVE      'N'           XKUNMEDVEDL
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      *
     C                   endsr
      *
