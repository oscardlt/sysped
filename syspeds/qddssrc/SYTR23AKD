     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'FOREGÅENDE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTR23AK'
     A                                      COLOR(BLU)
     A N51                              1 21MSGCON(045 DIV2404 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  51                              1 21MSGCON(045 JOV1225 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  5  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            WSVAVD         4Y 0B  5 28DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A N50                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A                                  7  2MSGCON(024 DIV1498 *LIBL/SYMSGF)
     A            WSSFSI         3A  B  7 28DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A N50                                  DSPATR(PR)
     A  51                              9  2MSGCON(024 JOV1224 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A  51        WSFAKT    R        B  9 28REFFLD(TRAVHR/THFAKT *LIBL/TRAVH)
     A  50                                  DSPATR(ND)
     A N50N30
     AON50 30 72                            DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A  51                              9 36'-'
     A  50                                  DSPATR(ND)
     A  51        WSFAKTT   R        B  9 38REFFLD(TRAVHR/THFAKT *LIBL/TRAVH)
     A  50                                  DSPATR(ND)
     A N50N30
     AON50 30 72                            DSPATR(PC)
     A  72                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10 39MSGCON(030 DIV2405 *LIBL/SYMSGF)
     A  50
     AO 51                                  DSPATR(ND)
     A N50N51                          12  2MSGCON(024 JOV1131 *LIBL/SYMSGF)
     A N50 51                          12  2MSGCON(024 JOV1226 *LIBL/SYMSGF)
     A            WSALFA         1A  B 12 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 12 39MSGCON(010 DIV0430 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A                                 13  2MSGCON(024 JOV0759 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A                                 14  2MSGCON(024 JOV0760 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSVEDL         1A  B 14 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 14 39MSGCON(010 DIV0430 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A                                 16  2MSGCON(024 DIV4058 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSPRT         10A  B 16 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 17  2MSGCON(024 DIV4059 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSFORM        10A  B 17 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 18  2MSGCON(024 DIV4060 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSJOBQ         1A  B 18 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50                             23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50                             23 43MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
