********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH PTF8 NY HELP - FUNKSJON
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11132 * 02 YBC PTF11 3 DESIMALER
********************************************************************
N01  A* JOV PTF7 UTVIDET MED EN LINJE - KAN HA 3 FAKTORLINJER
     A*                                     DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('SYGE1101' 'SYGE11' 'SY400LO-
S01  A*                                     /FELLES')
N01  A                                      HLPPNLGRP(HLPSCR SYGE11PNL)
N01  A                                      HLPTITLE('KALKULATOR')
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S01  A*                                     WINDOW(*DFT 16 25 *NOMSGLIN)
N01  A                                      WINDOW(*DFT 17 25 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            ZRECNO         4Y 0O  3  2EDTCDE(4)
S02  A*           SFNUM         13Y 2O  3  7EDTCDE(J)
N02  A            SFNUM         13Y 3O  3  7EDTCDE(J)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  ROLLUP(25)
     A  91                                  ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  1 19SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'CALCULATOR'
     A                                      COLOR(BLU)
     A                                  2  2'Line             value'
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF12(12)
     A                                      SLNO(13)
     A                                      CLRL(03)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
S02  A*           WSNUM         13Y 2B  1  9EDTCDE(M)
N02  A            WSNUM         13Y 3B  1  9EDTCDE(M)
     A                                      COLOR(YLW)
     A            WSARI          1A  B  2  5COLOR(YLW)
S02  A*           WSNUM2        13Y 2B  2  9EDTCDE(M)
N02  A            WSNUM2        13Y 3B  2  9EDTCDE(M)
     A                                      COLOR(YLW)
     A            WSARI2         1A  B  3  5COLOR(YLW)
S02  A*           WSNUM3        13Y 2B  3  9EDTCDE(M)
N02  A            WSNUM3        13Y 3B  3  9EDTCDE(M)
     A                                      COLOR(YLW)
     A                                  4  2'Total:'
S02  A*           SUM           13Y 2O  4 10EDTCDE(M)
N02  A            SUM           13Y 3O  4 10EDTCDE(M)
     A                                      COLOR(WHT)
     A                                  5  2'F3=Avslutt   F12=Avbryt'
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF03(03 'EXIT')
     A                                  1  2'CALCULATOR'
     A                                      COLOR(BLU)
     A                                  2  2'Line             value'
     A                                      COLOR(WHT)
