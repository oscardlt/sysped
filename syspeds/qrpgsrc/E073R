     FENITCMNF  CF   F   80        WORKSTN USROPN
     F                                     INFDS(FEEDBK)
     FENITPWF   UF   E             DISK
     F                                     INFDS(EPWDS)
     FENITPW1   IF   E           K DISK
     F                                     INFDS(EPWDS1)
     F                                     RENAME(ENITPR:ENITP1)
     FEDII2     UF   E           K DISK
     D                 DS
     D  WRRNPB                 1     10  0
     D  WRRNPC                11     20  0
     D FEEDBK          DS
     D  MAJMIN               401    404
     D  MAJCOD               401    402
     D EPWDS           DS
     D  RRNP                 397    400B 0
     D EPWDS1          DS
     D  RRNP1                397    400B 0
     IENITCMNF  AA  09
     I                                  1   80  RDATA
     I                                  2    9  RMSG
      *//////////////
      * MAIN LOGIC //
      *//////////////
      *_____
      * INIT
      *"""""
     C                   EXSR      SRA
      *___________________________________
      * Retreive existing and new password
      *"""""""""""""""""""""""""""""""""""
      * EXISTING
     C     EPWKY1        CHAIN     ENITPW1                            51
     C     *IN51         IFEQ      '1'
     C                   MOVE      '2'           Æ007
     C                   GOTO      END
     C                   ENDIF
     C                   Z-ADD     RRNP1         WRRNPB
     C                   MOVE      EPWPW         WPWPWB
      * NEW
     C     EPWKY2        CHAIN     ENITPW1                            51
     C     *IN51         IFEQ      '1'
     C                   MOVE      '2'           Æ007
     C                   GOTO      END
     C                   ENDIF
     C                   Z-ADD     RRNP1         WRRNPC
     C                   MOVE      EPWPW         WPWPWC
      *__________
      * Open ICFF
      *""""""""""
     C                   OPEN      ENITCMNF                             98
     C                   MOVE      '1'           *IN13
     C     MAJCOD        IFGT      '03'
     C     *IN98         OREQ      '1'
     C                   MOVE      '3'           Æ007
     C                   GOTO      END
     C                   ENDIF
      *_________________________
      * Evoke Signon Transaction
      *"""""""""""""""""""""""""
     C     TAG01         TAG
      * Lage signostreng
     C                   MOVEL(P)  'CESN USE'    LDATA            80
     C                   CAT       'RID=':0      LDATA
     C                   CAT       Æ003:0        LDATA
     C                   CAT       ',PS=':0      LDATA
     C                   CAT       WPWPWB:0      LDATA
     C                   CAT       ',NEWPS':0    LDATA
     C                   CAT       '=':0         LDATA
     C                   CAT       WPWPWC:0      LDATA
      *
     C                   EXCEPT    SIGNON
     C     MAJCOD        CABGT     '03'          END
     C     *IN98         CABEQ     '1'           END
      * RECEIVE "Signon Complete"
     C  N98MAJMIN        DOUEQ     '0308'
     C     MAJMIN        OREQ      '0028'
     C                   READ      ENITCMNF                             9898
     C     MAJCOD        CABGT     '03'          END
     C                   MOVE      RDATA         Æ011
     C                   SELECT
     C     RMSG          WHENEQ    'FHCE3503'                                   PW MISSING
     C     RMSG          OREQ      'TSS7102E'                                   PW MISSING
     C                   MOVE      '3'           Æ007
     C                   GOTO      END
     C     RMSG          WHENEQ    'FHCE3532'                                   PW INVALID
     C     RMSG          OREQ      'TSS7101E'                                   PW INVALID
     C                   MOVE      '3'           Æ007
     C                   GOTO      END
     C     RMSG          WHENEQ    'FHCE3525'                                   PW EXPIRED
     C     RMSG          OREQ      'TSS7110E'                                   PW EXPIRED
     C                   MOVE      '2'           Æ007
     C                   GOTO      END
     C     RMSG          WHENEQ    'FHCE3534'                                   NEW PW INVALID
     C     RMSG          OREQ      'TSS7111E'                                   NEW PW INVAL
     C                   EXSR      SRB
     C                   GOTO      TAG01
     C                   ENDSL
     C                   END
      *_________________
      * SIGNOFF FROM NIT
      *"""""""""""""""""
     C                   EXCEPT    SIGNOF
     C     MAJCOD        CABGT     '03'          END
      * RECEIVE "SIGNOFF IS COMPLETE"
     C  N98MAJMIN        DOUEQ     '0308'
     C     MAJMIN        OREQ      '0028'
     C                   READ      ENITCMNF                             9898
     C     MAJCOD        CABGT     '03'          END
     C                   END
      *_________________________________
      * Change existing and new password
      *"""""""""""""""""""""""""""""""""
      * EXISTING
     C     WRRNPB        CHAIN     ENITPWF                            51
     C     *IN51         IFEQ      '1'
     C                   MOVE      '2'           Æ007
     C                   GOTO      END
     C                   ENDIF
     C                   MOVE      'A'           EPWST
     C                   UPDATE    ENITPR
      * NEW
     C     WRRNPC        CHAIN     ENITPWF                            51
     C     *IN51         IFEQ      '1'
     C                   MOVE      '2'           Æ007
     C                   GOTO      END
     C                   ENDIF
     C                   MOVE      'B'           EPWST
     C                   UPDATE    ENITPR
      * INTERCHANGE ID FILE
     C     TVIKEY        SETLL     EDII2
     C     TVIKEY        READE     EDII2                                  51
     C     *IN51         DOWEQ     '0'
     C     INUSR         IFEQ      Æ003
     C                   MOVEL     WPWPWC        INPW
     C                   UPDATE    EDIIR
     C                   ENDIF
     C     TVIKEY        READE     EDII2                                  51
     C  N51              ENDDO
      *____________
      * END PROGRAM
      *""""""""""""
     C     END           TAG
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *______________
      * Define fields
      *""""""""""""""
     C     *LIKE         DEFINE    EPWST         WPWSTB
     C     *LIKE         DEFINE    EPWST         WPWSTC
     C     *LIKE         DEFINE    EPWPW         WPWPWB
     C     *LIKE         DEFINE    EPWPW         WPWPWC
      *_____________
      * Init av felt
      *"""""""""""""
      * Input fields
     C     *ENTRY        PLIST
     C                   PARM                    Æ002              8            ACC
     C                   PARM                    Æ003              8            Userid SND
     C                   PARM                    Æ007              1            Status
      *                                                    0=OK
      *                                                    1=OK, pw exp soon
      *                                                    2=NOK, pw exp
      *                                                    3=NOK, Other
     C                   PARM                    Æ011             80            Msgtxt
      * Init workfields
     C                   MOVE      '0'           Æ007
     C                   MOVE      'B'           WPWSTB
     C                   MOVE      'C'           WPWSTC
     C     *LIKE         DEFINE    INEX          WNEX
     C     *LIKE         DEFINE    INNETW        WNNETW
     C                   MOVE      'I'           WNEX
     C                   MOVE      '5'           WNNETW
      *_____________
      * Init av keys
      *"""""""""""""
     C     EPWKY1        KLIST
     C                   KFLD                    Æ003
     C                   KFLD                    WPWSTB
     C     EPWKY2        KLIST
     C                   KFLD                    Æ003
     C                   KFLD                    WPWSTC
     C     TVIKEY        KLIST
     C                   KFLD                    WNEX
     C                   KFLD                    WNNETW
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  New PW invalid, get next                             SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Update invalid password to old.
     C     WRRNPC        CHAIN     ENITPWF                            51
     C                   MOVE      'A'           EPWST
     C                   UPDATE    ENITPR
      * Get next new password
     C     EPWKY2        CHAIN     ENITPW1                            51
     C     *IN51         IFEQ      '1'
     C                   MOVE      '2'           Æ007
     C                   GOTO      END
     C                   ENDIF
     C                   Z-ADD     RRNP1         WRRNPC
     C                   MOVE      EPWPW         WPWPWC
     C                   ENDSR
     OENITCMNF  E            SIGNON
     O                                           K8 'SNDDATA '
     O                       LDATA               80
     O          E            SIGNOF
     O                                           K8 'SIGNOF  '
