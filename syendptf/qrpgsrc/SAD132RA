      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE E.F. MANIFEST - LISTE     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADEFCL1  UF   E           K DISK    RENAME(SADEFCFR:SADEFCFR1)
     FSADEFCL2  UF   E           K DISK
     FSADEFCSL1 IF   E           K DISK
     FTURER14   IF   E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD14    IF   E           K DISK    RENAME(HEADFR:HEADFR14)
     FSADH      IF   E           K DISK
     FEDIM2     IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FSTED2D    IF   E           K DISK
     D                 DS
     D  HEAVD                  1      4  0
     D  HEAVD_A                1      4
     D                 DS
     D  HEOPD                  1      7  0
     D  HEOPD_A                1      7
     D                 DS
     D  M0068A                 1      8  0
     D  M0068A_A               1      8
     D                 DS
     D  M0068B                 1      6  0
     D  M0068B_A               1      6
     D                 DS
     D  CLNTK                  1      7  0
     D  CLNTK_A                1      7
     D                 DS
     D  CLVKB                  1      9  0
     D  CLVKB_A                1      9
     D                SDS
     D  UUSR                 254    263
      *  Prototype for 'SAD135R'
     d SAD135R         pr                  extpgm('SAD135R')
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d u0068a                              like(u0068a)
     d u0068b                              like(u0068b)
     d untk_                               like(untk)
     d uvkb_                               like(uvkb)
     d utrid_                              like(utrid)
      *  Prototype for SAD132RA
     d sad132ra        pr
     d upro_                               like(upro)
      *  *ENTRY Interface for Main Procedure
     d sad132ra        pi
     d upro                           8  0
      *
     d uavd            s              4a
     d utdn            s              7a
     d u0068a          s              8a
     d u0068b          s              6a
     d untk            s              7a
     d uvkb            s              9a
     d utrid           s             17a
      *
      /free

         exsr init;

         // Oppdatere SADEFCF fra transport tur
         setll upro head14;
         reade upro head14;
         dow not %eof;                                                             // 1<b
           chain (heavd: heopd) sadefcl2;
           if %found;                                                              // 2<b
             if clpr <> *blanks and clprt = *blanks;                               // 3<b
               kftyp = 'SADEFPR';
               kfkod = clpr;
               chain (kftyp: kfkod) kofast;
               if %found;                                                          // 4<b
                 clprt = kftxt;
               endif;                                                              // 4<e
             endif;                                                                // 3<e
           // Finn oppdrag i ekspressfortolling.
             if clpro = 0 and clst = ' ';                                          // 3<b
             // Oppdater info. hvis oppdrag ikke er koblet til tur før.
               clpro = upro;
               if clsdft = *blanks;                                                // 4<b
                 cllkf = helka;
                 clsdf = hesdf;
                 if tustef = *blanks;                                              // 5<b
                   chain (cllkf: clsdf) sted2d;
                   clsdft = st2nvn;
                 else;                                                             // 5
                   clsdft = tustef;
                 endif;                                                            // 5<e
               endif;                                                              // 4<e
               if clsdtt = *blanks;                                                // 4<b
                 cllkt = hetri;
                 clsdt = hesdt;
                 if tustet = *blanks;                                              // 5<b
                   chain (cllkt: clsdt) sted2d;
                   clsdtt = st2nvn;
                 else;                                                             // 5
                   clsdtt = tustet;
                 endif;                                                            // 5<e
               endif;                                                              // 4<e
               if cltrid = *blanks;                                                // 4<b
                 cltrid = tubiln;
               endif;                                                              // 4<e
               if clvt = *blanks;                                                  // 4<b
                 clvt = hevs1;
               endif;                                                              // 4<e
             endif;                                                                // 3<e
             if clrg <> *blanks and cl0068a <> 0 and cl0068b <> 0                  // 3<b
               and clntk <> 0 and clvkb <> 0
               and clsdft <> *blanks and clsdtt <> *blanks
               and clprt <> *blanks
               or cltrnr <> *blanks and clnas <> *blanks and clnak <> *blanks
               and clsdft <> *blanks and clsdtt <> *blanks;
             // Enten direkte fortolling eller transit
               //and clprt <> *blanks and cletypt <> *blanks
               //and cleid <> *blanks;
               clst = 'O';
             else;                                                                 // 3
               chain (clavd: cltdn) sadefcsl1;
               if %found;                                                          // 4<b
                 clst = 'O';
               else;                                                               // 4
                 clst = ' ';
               endif;                                                              // 4<e
             endif;                                                                // 3<e
             update sadefcfr;
           else;                                                                   // 2
             clear sadefcfr;
           // Finn ikke oppdrag i ekspressfortolling.
             chain (heavd: heopd) sadh;
             if %found;                                                            // 3<b
               clntk = sintk;
               clvkb = sivkb;
               cltrid = sitrid;
             else;                                                                 // 3
               clntk = hent;
               clvkb = hevkt;
               cltrid = tubiln;
             endif;                                                                // 3<e
             clvt  = 'Div. varer';
             cl0068a = 0;
             cl0068b = 0;
             setgt (heavd: heopd) edim2;
             readpe (heavd: heopd) edim2;
             dow not %eof;                                                         // 3<b
               if m1001 = '930' and m1n07 = 'DTK';                                 // 4<b
                 if clrg = *blanks;                                                // 5<b
                   clrg = %subst(m0068:1:9);
                 endif;                                                            // 5<e
                 cl0068a = m0068a;
                 cl0068b = m0068b;
                 cl3039e = m3039e;
                 leave;
               endif;                                                              // 4<e
               readpe (heavd: heopd) edim2;
             enddo;                                                                // 3<e
             sad135r (heavd_a: heopd_a: m0068a_a: m0068b_a: clntk_a: clvkb_a:
                     cltrid);
             chain (heavd: heopd) sadefcl2;
             if %found;                                                            // 3<b
               clpro = upro;
               if clvt = *blanks;                                                  // 4<b
                 if hevs1 = *blanks;                                               // 5<b
                   clvt = 'Diverse';
                 else;                                                             // 5
                   clvt = hevs1;
                 endif;                                                            // 5<e
               endif;                                                              // 4<e
               if clsdft = *blanks;                                                // 4<b
                 cllkf = helka;
                 clsdf = hesdf;
                 if tustef = *blanks;                                              // 5<b
                   chain (cllkf: clsdf) sted2d;
                   clsdft = st2nvn;
                 else;                                                             // 5
                   clsdft = tustef;
                 endif;                                                            // 5<e
               endif;                                                              // 4<e
               if clsdtt = *blanks;                                                // 4<b
                 cllkt = hetri;
                 clsdt = hesdt;
                 if tustet = *blanks;                                              // 5<b
                   chain (cllkt: clsdt) sted2d;
                   clsdtt = st2nvn;
                 else;                                                             // 5
                   clsdtt = tustet;
                 endif;                                                            // 5<e
               endif;                                                              // 4<e
               if cltrid = *blanks;                                                // 4<b
                 cltrid = tubiln;
               endif;                                                              // 4<e
               if clrg <> *blanks and cl0068a <> 0 and cl0068b <> 0                // 4<b
                 and clntk <> 0 and clntk <> 0 and clvkb <> 0
                 and clsdft <> *blanks and clsdtt <> *blanks
                 and clprt <> *blanks
                 or cltrnr <> *blanks and clnas <> *blanks and clnak <> *blanks
                 and clsdft <> *blanks and clsdtt <> *blanks;
                 //and clprt <> *blanks and cletypt <> *blanks
                 //and cleid <> *blanks;
                 clst = 'O';
               else;                                                               // 4
                 clst = ' ';
               endif;                                                              // 4<e
               update sadefcfr;
             endif;                                                                // 3<e
           endif;                                                                  // 2<e
           reade upro head14;
         enddo;                                                                    // 1<e

         // Ta av post i SADEFCF hvis oppdrag er tatt av tur
         setll upro SADEFCL1;
         reade upro SADEFCL1;
         dow not %eof;                                                             // 1<b
           if clavd > 0;                                                           // 2<b
             chain (clavd: cltdn) headf;
             if not %found;                                                        // 3<b
               clpro = 0;
             endif;                                                                // 3<e
           endif;                                                                  // 2<e
           update sadefcfr1;
           reade upro SADEFCL1;
         enddo;                                                                    // 1<e

         *inlr = *on;
         return;

      //*********************************************
       begsr init;

       heavd = 0;
       heopd = 0;
       m0068a = 0;
       m0068b = 0;
       clntk = 0;
       clvkb = 0;
       chain upro turer14;

       endsr;

      /end-free
