********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 videreutviklet extern referanse
10017 * 02 YBC PTF10 SKILLE TET-EKSPORT OG FORHÅNDSVARSLING IMPORT
10250 * 03 YBC PTF10 DOKUMENTKODE ER ENDRET FRA 3A TIL 4A
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11115 * 04 YBC PTF11 PROSENT MED TO DESIMAL FOR GARANTIBEREGNING
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TR012PNL)
     A                                      HLPTITLE('REGISTRERING AV VAREPOSTE-
     A                                      R TET EKSPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5')
     A            TVLI           5Y 0H
     A            TVLN           5Y 0H
     A            TVREFL         5Y 0H
     A            WXLN           5Y 0O  4  5EDTCDE(4)
     A            TVVNT          8S 0O  4 11
     A            TVDK           5A  H
     A            TVVT          48A  H
     A            T2VT          15A  O  4 20
     A            TVVTSK         2A  H
     A            TVVKTB         9Y 0H
     A            TVVKTN         9Y 0O  4 36EDTCDE(4)
     A            TVALK          2A  H
     A            TVBLK          2A  H
     A            TVTDT          6A  H
     A            TVTDR         20A  H
     A            TVTDSK         2A  H
     A            TVTDO         26A  H
     A            TVTDOS         2A  H
S03  A*           TVDTY          3A  O  4 46
N03  A            TVDTY          4A  O  4 46
     A            TVDREF        20A  H
E03  A            WXDREF        19A  O  4 51
     A            TVDSK          2A  H
     A            TVDO          26A  H
     A            TVDOSK         2A  H
     A            TVMTXT        48A  H
     A            TVMSK          2A  H
     A            TVTLO          5A  H
N01  A            TRXEXT        35A  H
     A            TVEXKD         1Y 0H
     A            TVEXLK         2A  H
     A            TVAVD2         4Y 0H
     A            TVTDN2         7Y 0H
     A            TVCNR         17A  H
     A            TVMN          17A  H
     A            TVMNSK         2A  H
E03  A            TVEH           2A  O  4 71
E03  A            XVNTK          5Y 0O  4 74EDTCDE(4)
     A            TVNT           5Y 0H
     A            TVNTEH         5Y 0H
     A            TVFV           2A  H
     A            TVFVNT        11Y 3H
     A            TV01           1A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/TR012R'
     A                                      COLOR(BLU)
S02  A*                                 1 21'VAREPOSTER TET-eksport'
N02  A  39                              1 21'VAREPOSTER FORH.VARSEL'
0N2  A                                      COLOR(WHT)
N02  A N39                              1 21'VAREPOSTER NCTS-eksport'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis'
     A                                      COLOR(BLU)
     A N32                              3  2'Op Linje Varenr.. Varebeskrivelse -
E03  A                                      Nettovekt Dokumenttype/ref........ -
E03  A                                      Kolli'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Postn Varenr.. Varebeskrivelse -
E03  A                                      Nettovekt Dokumenttype/ref........ -
E03  A                                      Kolli'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A N27                                  CF10(10 'PLukk fra tur')
     A                                      CF12(12 'Gå til bilde02')
     A N27                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              4  2'F3=Neste/avslutt   F5=Forny   F6=L-
     A                                      ag ny   F8=Hopp til'
     A                                      COLOR(BLU)
     A  27                              4  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A N36                              4 58'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A N27                              5  2'F10=Hent fra oppdrag   F12=Forrige-
E02  A                                         F17=Ny NCTS kopier fra denne'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A    N36                               CF07(07 'SØKE OPPDRAG')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CF12(12 'Forrige')
     A          H                           HLPPNLGRP(TR012005 TR012PNL)
     A                                      HLPARA(02 001 02 021)
     A          H                           HLPPNLGRP(TR012006A TR012PNL)
     A                                      HLPARA(03 001 03 040)
     A          H                           HLPPNLGRP(TR012006B TR012PNL)
     A                                      HLPARA(03 042 03 080)
     A          H                           HLPPNLGRP(TR012006A TR012PNL)
     A                                      HLPARA(04 001 04 040)
     A          H                           HLPPNLGRP(TR012006B TR012PNL)
     A                                      HLPARA(04 042 04 080)
     A          H                           HLPPNLGRP(TR012006A TR012PNL)
     A                                      HLPARA(05 001 05 040)
     A          H                           HLPPNLGRP(TR012006B TR012PNL)
     A                                      HLPARA(05 042 05 080)
     A          H                           HLPPNLGRP(TR012006A TR012PNL)
     A                                      HLPARA(06 001 06 040)
     A          H                           HLPPNLGRP(TR012006B TR012PNL)
     A                                      HLPARA(06 042 06 080)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(07 001 07 080)
     A          H                           HLPPNLGRP(TR012006 TR012PNL)
     A                                      HLPARA(09 002 09 017)
     A          H                           HLPPNLGRP(TR012007 TR012PNL)
     A                                      HLPARA(09 022 09 036)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(09 042 09 051)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(09 062 09 071)
     A          H                           HLPPNLGRP(TR012010 TR012PNL)
     A                                      HLPARA(10 001 10 057)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(10 062 10 071)
     A          H                           HLPPNLGRP(TR012011 TR012PNL)
     A                                      HLPARA(11 001 11 018)
     A          H                           HLPPNLGRP(TR012012 TR012PNL)
     A                                      HLPARA(11 022 11 038)
     A          H                           HLPPNLGRP(TR012013 TR012PNL)
     A                                      HLPARA(12 001 12 015)
     A          H                           HLPPNLGRP(TR012014 TR012PNL)
     A                                      HLPARA(12 017 12 036)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(12 038 12 039)
     A          H                           HLPPNLGRP(TR012015 TR012PNL)
     A                                      HLPARA(12 042 12 076)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(12 078 12 079)
     A          H                           HLPPNLGRP(TR012016 TR012PNL)
     A                                      HLPARA(13 002 13 012)
     A          H                           HLPPNLGRP(TR012017 TR012PNL)
     A                                      HLPARA(13 022 13 049)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(13 052 13 061)
     A          H                           HLPPNLGRP(TR012018 TR012PNL)
     A                                      HLPARA(14 002 14 035)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(14 042 14 051)
     A          H                           HLPPNLGRP(TR012019 TR012PNL)
     A                                      HLPARA(16 002 16 012)
     A          H                           HLPPNLGRP(TR012020 TR012PNL)
     A                                      HLPARA(16 022 16 044)
     A          H                           HLPPNLGRP(TR012021 TR012PNL)
     A                                      HLPARA(17 002 17 057)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(17 062 17 071)
     A          H                           HLPPNLGRP(TR012022 TR012PNL)
     A                                      HLPARA(18 002 18 026)
     A          H                           HLPPNLGRP(TR012009 TR012PNL)
     A                                      HLPARA(18 032 18 041)
     A          H                           HLPPNLGRP(TR012023 TR012PNL)
     A                                      HLPARA(19 002 19 020)
     A          H                           HLPPNLGRP(TR012024 TR012PNL)
     A                                      HLPARA(20 002 20 011)
     A          H                           HLPPNLGRP(TR012025 TR012PNL)
     A                                      HLPARA(20 022 20 034)
     A          H                           HLPPNLGRP(TR012026 TR012PNL)
     A                                      HLPARA(20 042 20 054)
     A          H                           HLPPNLGRP(TR012027 TR012PNL)
     A                                      HLPARA(21 002 21 011)
     A          H                           HLPPNLGRP(TR012027B TR012PNL)
     A                                      HLPARA(21 015 21 026)
     A          H                           HLPPNLGRP(TR012031 TR012PNL)
     A                                      HLPARA(23 001 23 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2LI           5S 0H
     A                                  1  2'SYSPED/TR012R'
     A                                      COLOR(BLU)
S02  A*                                 1 21'TET-eksport'
N02  A  39                              1 21'Forh.varsel'
N02  A                                      COLOR(WHT)
N02  A N39                              1 21'NCTS-eksport'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Oppdrag'
     A            W2AVD2         4Y 0B  2 10TEXT('Avd. til hentet oppdrag')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2TDN2         7Y 0B  2 17TEXT('Opd. til hentet oppdrag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
N01  A                                  2 32'EXTERN REF:'
N01  A            W2XEXT        35A  B  2 44TEXT('ORDREREF.')
N01  A                                      DSPATR(UL)
N01  A  90                                  DSPATR(RI)
N01  A                                      COLOR(YLW)
     A                                  3  2'2.Avsender'
     A            W2KNS          8Y 0B  3 13TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2TINS        17A  B  3 22TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 42'8.Mottaker'
     A            W2KNK          8Y 0B  3 53TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2TINK        17A  B  3 62TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NAS         30A  B  4  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NAK         30A  B  4 42TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2ADS1        30A  B  5  2TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2ADK1        30A  B  5 42TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2PNS          9A  B  6  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A  55                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PSS         24A  B  6 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A  54                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PNK          9A  B  6 42TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A  53                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PSK         24A  B  6 52TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A  52                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7  2'Landkode'
     A            W2LKS          2A  B  7 12TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 22'Språkkode'
     A            W2SKS          2A  B  7 32TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 42'Landkode'
     A            W2LKK          2A  B  7 52TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  58                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 62'Språkkode'
     A            W2SKK          2A  B  7 72TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A  57                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9  2'Varenr.'
     A            W2VNTI         8A  B  9 10TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2VNT          8S 0H
     A                                  9 22'Dekl.type'
     A            W2DK           5A  B  9 32TEXT('DEKL.TYPE')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9 42'Av.land'
     A            W2ALK          2A  B  9 50TEXT('Avsendersland')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9 62'Be.land'
     A            W2BLK          2A  B  9 70TEXT('Bestemmelsesland')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10  2'V.beskr'
     A            W2VT          48A  B 10 10TEXT('VAREBESKRIVELSE')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A N48                                  COLOR(YLW)
     A  48                                  COLOR(RED)
     A                                 10 62'SpråkKD'
     A            W2VTSK         2A  B 10 70TEXT('SPÅRKKODE')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A N48                                  COLOR(YLW)
     A  48                                  COLOR(RED)
     A                                 11  2'Br.vekt'
     A            W2VKTB         9Y 0B 11 10TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 22'Nt.vekt'
     A            W2VKTN         9Y 0B 11 30TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12  2'40. T.d'
     A            W2TDT          6A  B 12 10TEXT('Tidligere dok.type')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            W2TDR         20A  B 12 17TEXT('Tidligere dok.ref.')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            W2TDSK         2A  B 12 38TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A                                 12 42'Opplysn.'
     A            W2TDO         26A  B 12 51TEXT('Utfylt opplysning')
     A                                      DSPATR(UL)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            W2TDOS         2A  B 12 78TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A                                 13  2'44. Dok'
S03  A*           W2DTY          3A  B 13 10TEXT('DOKUMENTTYPE')
N03  A            W2DTY          4A  B 13 10TEXT('DOKUMENTTYPE')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 13 22'Dok.Ref'
     A            W2DREF        20A  B 13 30TEXT('DOKUMENTREF.')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 13 52'Dok.SK '
     A            W2DSK          2A  B 13 60TEXT('SPRÅKKODE')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 14  2'44. Opl'
     A            W2DO          26A  B 14 10TEXT('Utfylt opplysning')
     A                                      DSPATR(UL)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 14 42'SpråkKD'
     A            W2DOSK         2A  B 14 50TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 15  2'Speielle merknader (Rubr. 44):'
     A                                      COLOR(WHT)
     A                                 16  2'T.O. ID'
     A            W2TLO          5A  B 16 10TEXT('Tilleggsoppl.ID')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 16 22'Eksp. fra EF/Land'
     A            W2EXKD         1Y 0B 16 41TEXT('Eksp. fra EF')
     A                                      DSPATR(UL)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                      EDTCDE(4)
     A            W2EXLK         2A  B 16 43TEXT('Eksp. fra land')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 17  2'Tekst  '
     A            W2MTXT        48A  B 17 10TEXT('Tekst')
     A                                      DSPATR(UL)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 17 62'SpråkKD'
     A            W2MSK          2A  B 17 70TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                 18  2'31. Opl'
     A            W2MN          17A  B 18 10TEXT('MERKE & NR')
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A N46                                  COLOR(YLW)
     A  46                                  COLOR(RED)
     A                                 18 32'SpråkKD'
     A            W2MNSK         2A  B 18 40TEXT('SPRÅKKODE')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A N46                                  COLOR(YLW)
     A  46                                  COLOR(RED)
     A                                 19  2'Cont.nr'
     A            W2CNR         17A  B 19 10TEXT('Containernr')
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI)
     A N28                                  COLOR(YLW)
     A  28                                  COLOR(RED)
     A                                 20  2'Enhet  '
     A            W2EH           2A  B 20 10TEXT('Enhet')
     A                                      DSPATR(UL)
     A  68                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 20 22'Kolli  '
     A            W2NT           5Y 0B 20 30TEXT('Antall')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 20 42'Stykk  '
     A            W2NTEH         5Y 0B 20 50TEXT('Antall')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 21  2'F.vare '
     A            W2FV           2A  B 21 10TEXT('Koder for følsomme varer')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A N45                                  COLOR(YLW)
     A  45                                  COLOR(RED)
     A            W2FVNT        11Y 3B 21 15TEXT('Antall føæsomme varer')
     A                                      DSPATR(UL)
     A  64                                  DSPATR(RI)
     A N45                                  COLOR(YLW)
     A  45                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                 23  2'F4=Søk   F12=Avbryt   F15=Tillegg -
     A                                      opplysning'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                  1  2'SYSPED/TR012R'
     A                                      COLOR(BLU)
S02  A*                                 1 21'TET-eksport'
N02  A  39                              1 21'Forh.varsel'
N02  A                                      COLOR(WHT)
N02  A N39                              1 21'NCTS-eksport'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Oppdrag'
     A            TVAVD2         4Y 0O  2 10TEXT('Avd. til hentet oppdrag')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TVTDN2         7Y 0O  2 17TEXT('Opd. til hentet oppdrag')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
N01  A                                  2 32'EXTERN REF:'
N01  A            TRXEXT        35A  O  2 44TEXT('ORDREREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(WHT)
     A                                  3  2'2.Avsender'
     A            TVKNS          8Y 0O  3 13TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TVTINS        17A  O  3 22TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 42'8.Mottaker'
     A            TVKNK          8Y 0O  3 53TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TVTINK        17A  O  3 62TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVNAS         30A  O  4  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVNAK         30A  O  4 42TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVADS1        30A  O  5  2TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVADK1        30A  O  5 42TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVPNS          9A  O  6  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVPSS         24A  O  6 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVPNK          9A  O  6 42TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TVPSK         24A  O  6 52TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Landkode'
     A            TVLKS          2A  O  7 12TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7 22'Språkkode'
     A            TVSKS          2A  O  7 32TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7 42'Landkode'
     A            TVLKK          2A  O  7 52TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7 62'Språkkode'
     A            TVSKK          2A  O  7 72TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2'Varenr.'
     A            TVVNT          8Y 0O  9 10TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9 22'Dekl.type'
     A            TVDK           5A  O  9 32TEXT('DEKL.TYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9 42'Av.land'
     A            TVALK          2A  O  9 50TEXT('Avsendersland')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9 62'Be.land'
     A            TVBLK          2A  O  9 70TEXT('Bestemmelsesland')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2'V.beskr'
     A            TVVT          48A  O 10 10TEXT('VAREBESKRIVELSE')
     A                                      DSPATR(UL)
     A N48                                  COLOR(WHT)
     A  48                                  COLOR(RED)
     A                                 10 62'SpråkKD'
     A            TVVTSK         2A  O 10 70TEXT('SPÅRKKODE')
     A                                      DSPATR(UL)
     A N48                                  COLOR(WHT)
     A  48                                  COLOR(RED)
     A                                 11  2'Br.vekt'
     A            TVVKTB         9Y 0O 11 10TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11 22'Nt.vekt'
     A            TVVKTN         9Y 0O 11 30TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12  2'40. T.d'
     A            TVTDT          6A  O 12 10TEXT('Tidligere dok.type')
     A                                      DSPATR(UL)
     A N47                                  COLOR(WHT)
     A  47                                  COLOR(RED)
     A            TVTDR         20A  O 12 17TEXT('Tidligere dok.ref.')
     A                                      DSPATR(UL)
     A N47                                  COLOR(WHT)
     A  47                                  COLOR(RED)
     A            TVTDSK         2A  O 12 38TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A N47                                  COLOR(WHT)
     A  47                                  COLOR(RED)
     A                                 12 42'Opplysn.'
     A            TVTDO         26A  O 12 51TEXT('Utfylt opplysning')
     A                                      DSPATR(UL)
     A N47                                  COLOR(WHT)
     A  47                                  COLOR(RED)
     A            TVTDOS         2A  O 12 78TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A N47                                  COLOR(WHT)
     A  47                                  COLOR(RED)
     A                                 13  2'44. Dok'
S03  A*           TVDTY          3A  O 13 10TEXT('DOKUMENTTYPE')
N03  A            TVDTY          4A  O 13 10TEXT('DOKUMENTTYPE')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 13 22'Dok.Ref'
     A            TVDREF        20A  O 13 30TEXT('DOKUMENTREF.')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 13 52'Dok.SK '
     A            TVDSK          2A  O 13 60TEXT('SPRÅKKODE')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 14  2'44. Opl'
     A            TVDO          26A  O 14 10TEXT('Utfylt opplysning')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 14 42'SpråkKD'
     A            TVDOSK         2A  O 14 50TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 15  2'Speielle merknader (Rubr. 44):'
     A                                      COLOR(WHT)
     A                                 16  2'T.O. ID'
     A            TVTLO          5A  O 16 10TEXT('Tilleggsoppl.ID')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 16 22'Eksp. fra EF/Land'
     A            TVEXKD         1Y 0O 16 41TEXT('Eksp. fra EF')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A            TVEXLK         2A  O 16 43TEXT('Eksp. fra land')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 17  2'Tekst  '
     A            TVMTXT        48A  O 17 10TEXT('Tekst')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 17 62'SpråkKD'
     A            TVMSK          2A  O 17 70TEXT('Språkkode')
     A                                      DSPATR(UL)
     A N29                                  COLOR(WHT)
     A  29                                  COLOR(RED)
     A                                 18  2'31. Opl'
     A            TVMN          17A  O 18 10TEXT('MERKE & NR')
     A                                      DSPATR(UL)
     A N46                                  COLOR(WHT)
     A  46                                  COLOR(RED)
     A                                 18 32'SpråkKD'
     A            TVMNSK         2A  O 18 40TEXT('SPRÅKKODE')
     A                                      DSPATR(UL)
     A N46                                  COLOR(WHT)
     A  46                                  COLOR(RED)
     A                                 19  2'Cont.nr'
     A            TVCNR         17A  O 19 10TEXT('Containernr')
     A  28                                  COLOR(RED)
     A N28                                  COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 20  2'Enhet  '
     A            TVEH           2A  O 20 10TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20 22'Kolli  '
     A            TVNT           5Y 0O 20 30TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 20 42'Stykk  '
     A            TVNTEH         5Y 0O 20 50TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 21  2'F.vare '
     A            TVFV           2A  O 21 10TEXT('Koder for følsomme varer')
     A                                      DSPATR(UL)
     A N45                                  COLOR(WHT)
     A  45                                  COLOR(RED)
     A            TVFVNT        11Y 3O 21 15TEXT('Antall føæsomme varer')
     A                                      DSPATR(UL)
     A N45                                  COLOR(WHT)
     A  45                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                 23  2'F12=Forrige   F15=Tillegg opplysni-
     A                                      nger'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A N27                                  CF10(10 'PLukk fra tur')
     A                                      CF12(12 'Gå til bilde02')
     A N27                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A          H                           HLPPNLGRP(TR012002 TR012PNL)
     A                                      HLPARA(01 001 02 080)
     A          H                           HLPPNLGRP(TR012003 TR012PNL)
     A                                      HLPARA(03 002 03 002)
     A          H                           HLPPNLGRP(TR012004 TR012PNL)
     A                                      HLPARA(22 001 23 080)
     A                                  1  2'SYSPED/TR012R'
     A                                      COLOR(BLU)
S02  A*                                 1 21'TET-eksport'
N02  A  39                              1 21'Forh.varsel'
N02  A                                      COLOR(WHT)
N02  A N39                              1 21'NCTS-eksport'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis'
     A                                      COLOR(BLU)
     A N32                              3  2'Op Linje Varenr.. Varebeskrivelse -
     A                                      Nettovekt Dokumenttype/ref....... K-
     A                                      olli'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Postn Varenr.. Varebeskrivelse -
     A                                      Nettovekt Dokumenttype/ref....... K-
     A                                      olli'
     A                                      COLOR(WHT)
     A N27                             22  2'F3=Neste/avslutt   F5=Forny   F6=L-
     A                                      ag ny   F8=Hopp til'
     A                                      COLOR(BLU)
     A  27                             22  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A N36                             22 58'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A N27                             23  2'F10=Hent fra oppdrag   F12=Forrige-
     A                                         F17=Ny sad kopier fra denne'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  3  2'Hopp til varenr. -->'
     A            W3VNT          8Y 0B  3 24DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'Hopp til postnr. -->'
     A            W3LI           5S 0I  4 24DSPATR(UL)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SUBFIL2                   SFL
     A            WSNR           1A  O  5  2
     A            TVLI           5Y 0H
     A            TVLN           5Y 0H
     A            TVREFL         5Y 0H
     A            WXLN           5Y 0O  5  5EDTCDE(4)
     A            TVVNT          8S 0O  5 11
     A            TVVT          48A  H
     A            T2VT          15A  O  5 20
     A            TVVKTN         9Y 0O  5 36EDTCDE(4)
S03  A*           TVDTY          3A  O  5 46
N03  A            TVDTY          4A  O  5 46
     A            TVDREF        20A  H
E03  A            WXDREF        19A  O  5 51
E03  A            TVNT           5Y 0O  5 71EDTCDE(4)
E03  A            TVEH           2A  O  5 77
     A            TV01           1A  H
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N93N94                            ROLLUP(25)
     A            ZRECN2         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/TR012R'
     A                                      COLOR(BLU)
S02  A*                                 1 21'TET-eksport'
N02  A  39                              1 21'Forh.varsel'
N02  A                                      COLOR(WHT)
N02  A N39                              1 21'NCTS-eksport'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Trykk Enter for å bekrefte at du v-
     A                                      algte 4=Slette.'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk F12 for å gå tilbake og endr-
     A                                      e valget.'
     A                                      COLOR(BLU)
     A N32                              4  2'Op Linje Varenr.. Varebeskrivelse -
E03  A                                      Nettovekt Dokumenttype/ref........ -
E03  A                                      Kolli'
     A                                      COLOR(WHT)
     A  32                              4  2'Op Postn Varenr.. Varebeskrivelse -
E03  A                                      Nettovekt Dokumenttype/ref........ -
E03  A                                      Kolli'
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'Gå tilbake')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R HENTPRO
     A                                      WINDOW(*DFT 4 30 *NOMSGLIN)
     A                                      CF12(12 'Gå tilbake')
     A                                      OVERLAY
     A                                  2  2'Tast inn turnr.'
     A            WSPRO          8Y 0B  2 18DSPATR(UL)
     A  30                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30                              4  2'Tastet turnr finnes ikke.'
     A                                      COLOR(WHT)
      *
     A          R TSTGRBL
     A                                      WINDOW(*DFT 6 35 *NOMSGLIN)
     A  99                                  CF10(10 'KANKULATOR')
     A                                      CF12(12 'Gå tilbake')
     A                                      OVERLAY
     A                                  1  2'Total vareverdi   :'
     A            WSTBL         11Y 0O  1 22TEXT('Vareverdi')
     A                                      EDTCDE(4)
     A  99                              2  2'Minst en linje har ikke vareverdi.'
     A                                      COLOR(YLW)
     A                                  3  2'Oppgi garantibeløp:'
     A            WSGBL         11Y 0B  3 22TEXT('Garantibeløp')
     A  30                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  4  2'Eller % av v.verdi:'
S04  A*           WSGPR          3Y 0B  4 22TEXT('% av v.verdi')
N04  A            WSGPR          5Y 2B  4 22TEXT('% av v.verdi')
     A                                      EDTCDE(4)
     A                                  5  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  99                              5 15'F10=Kalkulator'
     A                                      COLOR(BLU)
     A  30                              6  2MSGCON(33 YBC1901 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VARSLING
     A                                      WINDOW(*DFT 5 45 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Brukt garantibeløp er over'
     A                                  2  2'varslingsgrense på'
     A            TGPRM          3Y 0O  2 21EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  2 25'%'
     A                                  3  2'Totalt garantibeløp er'
     A            TGGBL         11Y 0O  3 25EDTCDE(2)
     A                                      COLOR(BLU)
     A                                  4  2'Totalt brukt garantibeløp'
S02  A*                                 5  2'inkludert denne TETen er'
N02  A                                  5  2'inkludert denne NCTS er'
     A            XBEL1         11Y 0O  5 28EDTCDE(2)
     A                                      COLOR(YLW)
