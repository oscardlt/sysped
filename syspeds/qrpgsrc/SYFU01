      *
      *****************************************************************
      *** PROGRAM SOM OVERFØRER LEVERANDØRER MELLOM 2 FIMRMA        ***
      *** I LØSNING 1                                               ***
     FLISTE     O    F   80        PRINTER OFLIND(*INOF)
     FLEVEF     IP   E           K DISK
     F                                     RENAME(LEVER:LEVERI)
     FLEVEL01   IF A E           K DISK
     FKLEVF     O  A E           K DISK
     C     *ENTRY        PLIST
     C                   PARM                    FRAFIR            2
     C                   PARM                    TILFIR            2
      *
     C     LEVKEY        KLIST
     C                   KFLD                    TILFIR
     C                   KFLD                    LEVNR
      *
     C  N99              EXCEPT    PHEAD
     C  N99              SETON                                        99
      *
     C     FIRMA         IFEQ      FRAFIR
     C     AKTKOD        ANDEQ     'A'
     C                   MOVE      LNAVN         LNAVNO           30
     C     LEVKEY        CHAIN     LEVEL01                            33
     C   33              EXSR      ADDPOS
     C  N33
     CAN OF              EXCEPT    PHEAD
     C  N33
     CAN OF              SETOFF                                       OF
     C  N33              EXCEPT    PDET2
     C                   END
     C************************************************************
     C     ADDPOS        BEGSR
     C************************************************************
     C                   MOVE      TILFIR        FIRMA
     C                   MOVE      *ZEROS        KUNDNR
     C                   MOVE      *ZEROS        SITAAR
     C                   MOVE      *ZEROS        SITMND
     C                   MOVE      *ZEROS        SITDAG
     C                   MOVE      *ZEROS        KSALDO
     C                   Z-ADD     2002          DAOAAR
     C                   Z-ADD     12            DAOMND
     C                   Z-ADD     10            DAODAG
     C                   WRITE     LEVER
     C                   WRITE     KLEVR
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C                   EXCEPT    PDET
     C                   ENDSR
     OLISTE     E            PHEAD            02
     O                                           24 'FLYTTE LEVERANDØRER FRA/'
     O                                           38 'TIL FIRMA DATO:'
     O                       UDATE         Y     49
     O                                           60 'SIDE:'
     O                       PAGE          Z     66
     OLISTE     E            PHEAD            02
     O                                           38 'FRA FIRMA:'
     O                       FRAFIR              41
     O                                           55 'TIL FIRMA:'
     O                       TILFIR              58
     OLISTE     E            PHEAD          1 03
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O          E            PDET           1
     O                       LEVNR         Z     10
     O                       LNAVN               41
     O          E            PDET2          1
     O                       LEVNR         Z     10
     O                                           34 '*** NR FINS FRA FØR  ***'
     O                       LNAVNO              70
     O          E            PDET2          1
     O                                           34 '*** IKKE OVERFØRT    ***'
