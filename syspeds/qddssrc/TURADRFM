********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TEST OM SIGNATUR ER GYLDIG
********************************************************************
     A                                      DSPSIZ(27 132 *DS3)
     A                                      PRINT
      *
     A          R VALG
     A                                      CF03(03 'EXIT/AVBRYT')
     A                                      CF12(03)
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/TURADR'
     A                                      COLOR(BLU)
     A                                  1 21'Arbeide med oppdrag/fraktbrev på t-
     A                                      ur..'
     A                                      COLOR(WHT)
     A                                  1121DATE
     A                                      EDTCDE(Y)
     A                                  2121TIME
     A                                  3  2MSGCON(025 JOV0550 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Turnummer:'
     A            WSTUR          8Y 0B  5 13COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7  2'eller...'
     A                                  8  2'MAWB-nr..:'
     A            WSMAWB        15   B  8 13COLOR(YLW)
     A                                 26  2'F3=Avslutt   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            DFTRAN    R    8S00H      REFFLD(DOKUFR/DFTRAN *LIBL/DOKUF)
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4' '5' '6' '7' 'W' -
     A                                      'P' 'M' 'L' 'K')
     A            DFAVD          4Y 0O  6  4EDTCDE(4)
     A            DFOPD          7  0O  6  9EDTCDE(4)
     A  61                                  COLOR(RED)
     A  62                                  COLOR(YLW)
     A  63                                  COLOR(GRN)
     A  61
     AO 62
     AO 63                                  DSPATR(RI)
     A            HENASX         8A  O  6 17
     A            DFNAVL    R        O  6 26REFFLD(DOKUFR/DFNAVL *LIBL/DOKUF)
     A                                      COLOR(WHT)
     A  56                                  DSPATR(RI)
     A            DFAD1L    R        O  6 57REFFLD(DOKUFR/DFAD1L *LIBL/DOKUF)
     A                                      COLOR(WHT)
     A            DFPOUL    R        O  6 88REFFLD(DOKUFR/DFPOUL *LIBL/DOKUF)
     A  55                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A            DFST13        13A  O  6 93COLOR(WHT)
     A            DFNT3          3Y 0O  6107EDTCDE(4)
     A            DFVKT3         3Y 0O  6111EDTCDE(4)
     A            DFNAT5         5A  O  6115
     A            DFCMN          1A  O  6121
     A            DFST           1A  O  6124
     A            DFKDME         1A  O  6127
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/TURADR'
     A                                      COLOR(BLU)
     A                                  1 21'Arbeide med oppdrag/fraktbrev på t-
     A                                      ur: '
     A                                      COLOR(WHT)
     A            WSTUR          8Y 0O  1 60COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 72'MAWB-nr:'
     A                                      COLOR(WHT)
     A            WSMAWB        15A  O  1 82COLOR(YLW)
     A                                  1 99'(fra første oppdr)'
     A                                  1121DATE
     A                                      EDTCDE(Y)
     A            ZRECNO         4S 0O  2 66SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2121TIME
     A                                  3  2'Velg og trykk Enter: 2=Endre, 5=Vi-
     A                                      s fr.br, 6=Endre oppdr, 7=Endre fr.-
     A                                      br, W=Se på web, L=Se T&T-Logg K=Se-
     A                                       Kolli                '
     A                                      COLOR(BLU)
     A                                  4  2'                     M=Prt Merkela-
     A                                      pp                                 -
     A                                                                         -
     A                                                            '
     A                                      COLOR(BLU)
     A                                  5  2'O Avd Oppd.nr Avsender Mottak. nav-
     A                                      n                   Gateaddr       -
     A                                                      P.nr Sted          -
     A                                      Ant Vkt Tran EDI St Ml '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CF03(03 'EXIT/AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF09(09 'BYTTSG')
     A                                      CF10(10 'TURTT')
     A                                      CF11(11 'TUROVERSIKT')
     A                                      CF12(12 'TILBAKE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(25)
     A                                      CLRL(03)
     A                                      OVERLAY
     A  50                              1  2MSGCON(078 JOV1483 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 61'F9=Bytt signatur   '
     A                                      COLOR(BLU)
     A                                  2 81'F10=Legg T&T på tur'
     A                                      COLOR(BLU)
     A                                  2101'F11=Turoversikt'
     A                                      COLOR(BLU)
     A  98                              3  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5'VIS kun poster som tilfredsstiller-
     A                                       underutvalg...                    -
     A                                                              Agent      -
     A                                         Transp. '
     A            W2AVD          4Y 0B  2  4EDTCDE(4)
     A            W2OPD          7Y 0B  2  9EDTCDE(4)
     A            W2AGE          8Y 0B  2 98EDTCDE(4)
     A            W2TRAN         8Y 0B  2112EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT/AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'TURTT')
     A                                      CF11(11 'TUROVERSIKT')
     A                                      CF12(12)
     A                                  1  2'SYSPED/TURADR'
     A                                      COLOR(BLU)
     A                                  1 21'Arbeide med oppdrag/fraktbrev på t-
     A                                      ur: '
     A                                      COLOR(WHT)
     A            WSTUR          8Y 0O  1 60COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1121DATE
     A                                      EDTCDE(Y)
     A                                  2121TIME
     A                                  3  2MSGCON(025 JOV0550 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 28'2=Endre '
     A                                      COLOR(BLU)
     A                                  5  2'O Avd Oppd.nr Avsender Mottak. nav-
     A                                      n                   Gateaddr       -
     A                                                      P.nr Sted          -
     A                                       Ant Vkt Tran EDI St Ml'
     A                                      COLOR(WHT)
     A N50                             15 15'INGEN OPPDRAG / FRAKTBREV PÅ TUREN'
     A  50                             15 15MSGCON(060 JOV0536 *LIBL/SYMSGF)
     A  50                             17 15'Tast F8 for å gjøre nytt Søk/Under-
     A                                      utvalg'
     A                                 26  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26101'F11=Turoversikt'
     A                                      COLOR(BLU)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF10(10 'FRISØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'             Avsender Mottak. navn-
     A                                                         Gateaddr        -
     A                                                     P.nr Sted           -
     A                                       Ant Vkt Transportør   '
     A                                      COLOR(WHT)
     A            DFAVD          4Y 0O  2  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            DFOPD          7Y 0O  2  9EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENASX         8A  O  2 17COLOR(WHT)
     A            WSNAVL    R        B  2 26REFFLD(DOKUFR/DFNAVL *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A            WSAD1L    R        B  2 57REFFLD(DOKUFR/DFAD1L *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A            WSPOUL    R        B  2 88REFFLD(DOKUFR/DFPOUL *LIBL/DOKUF)
     A  92                                  DSPATR(RI)
     A                                      EDTWRD('0   ')
     A                                      COLOR(YLW)
     A            DFST15        15A  O  2 93COLOR(WHT)
     A            WSNT3          3Y 0B  2109COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVKT3         3Y 0B  2113COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTRAN         8Y 0B  2117COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 24'F4=Søk     F10=Frisøk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5'Sett ny signatur (overta) alle opp-
     A                                      drag/fraktbrev..'
     A                                  2  5'Ny signatur :'
     A            W3SG           3   B  2 20
N01  A  99                                  DSPATR(PC)
N01  A  99                                  DSPATR(HI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N01  A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
N01  A                                      COLOR(WHT)
N01  A            MSGNR          7A  P
