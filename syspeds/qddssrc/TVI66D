     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R TVI66DA                   SFL
     A            MMN       R        H
     A            ZOP            1A  I 11  3VALUES(' ' '5' '6')
     A  30                                  DSPATR(PC)
     A  30                                  COLOR(RED)
     A            M0065     R        H
     A            WSIMEX         6   O 11  7
     A            M1N07     R        O 11 14
     A            M0068A    R        O 11 20
     A            M0068B    R        O 11 30EDTCDE(4)
     A            M0068C    R        O 11 38EDTCDE(4)
     A            M3039E    R        O 11 42EDTCDE(4)
     A            M0062     R        O 11 50REFFLD(M0062)
     A            MAVD      R        O 11 67REFFLD(MAVD)
     A                                      EDTCDE(4)
     A            MTDN           7  0O 11 72EDTCDE(4)
     A          R TVI66DB                   SFLCTL(TVI66DA)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      TEXT('TVI Meldingslogg, Heading')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'TVIN'
     A                                      COLOR(BLU)
     A                                  1 28MSGCON(025 TVI0232 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73'TVI66D A'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(009 TVI0233 *LIBL/SYMSGF)
     A            M0004     R        O  3 12
     A                                  3 50MSGCON(013 TVI0235 *LIBL/SYMSGF)
     A            W0020     R        O  3 64REFFLD(S0020 EDIS)
     A                                  4  2MSGCON(009 TVI0234 *LIBL/SYMSGF)
     A            M0010     R        O  4 12
     A                                  4 50MSGCON(013 TVI0235 *LIBL/SYMSGF)
     A                                  5  2'Testind :'
     A            M0035     R        O  5 12
     A                                  5 23MSGCON(009 TVI0237 *LIBL/SYMSGF)
     A            W0036     R        O  5 33REFFLD(S0036 EDIS)
     A                                      EDTCDE(4)
     A                                  5 44MSGCON(019 TVI0238 *LIBL/SYMSGF)
     A            MSR       R        O  5 64
     A                                  6  2'Status  :'
     A            WST       R        O  6 12REFFLD(MST)
     A                                  6 23MSGCON(009 TVI0240 *LIBL/SYMSGF)
     A            WDT       R        O  6 33REFFLD(SDT EDIS)
     A                                  6 44MSGCON(019 TVI0241 *LIBL/SYMSGF)
     A            WTM       R        O  6 64REFFLD(STM EDIS)
     A                                      EDTWRD('0 :  :  ')
     A                                  7  2MSGCON(023 TVI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  7 69SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  8  4MSGCON(009 TVI0705 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8 16MSGCON(021 TVI0243 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 10  2MSGCON(003 TVI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  7MSGCON(011 TVI0231 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 20MSGCON(004 TVI0196 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 30MSGCON(004 TVI0197 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 37'Ver'
     A                                      COLOR(WHT)
     A                                 10 42'Exp'
     A                                      COLOR(WHT)
     A                                 10 50MSGCON(017 TVI0218 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 10 67'SYSPED.ref'
     A                                      COLOR(WHT)
     A          R TVI66DC
     A                                      OVERLAY
     A                                 23  2MSGCON(010 TVI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(010 TVI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 28MSGCON(010 TVI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R TVI66DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
