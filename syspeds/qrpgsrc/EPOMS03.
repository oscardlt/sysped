OBS!! * OBS!!! automatisk mail sendes hos SYSTEMA hardkodet til janottar@systema.no
OBS!! *        HOS CC/I PROD går den til innkjøpers mailadr (blank->kenneth.johansen@europris.no)
      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/EPOMS03 MODULE(*LIBL/EPOMS03
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Statusfelt i EPOTT - T&T-hendeses skal sendes som mail hvis S
10XXX * 02 JOV PTF10 Send mail dersom POL/POD er endret
10XXX * 03 YBC PTF10 IKKE OPPDATER EPOL HVIS LINJE ER PÅ OPPDRAG
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 04 YBC PTF11 ARBEID MED KOLLI (POMS LITAUEN)
11XXX * 05 YBC PTF11 VIS REST VEKT, STYKK, KOLLI,,,
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FEPOH      UF   E           K DISK    USROPN
     FEPOL      UF   E           K DISK    USROPN
     FEPOTT     IF A E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FCundL01   IF   E           K DISK    USROPN
     FSTED2     IF   E           K DISK    USROPN
     FKODTLB    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
     D EHBES           s             10
     D WSIID           s             10
     D WSINV           s             30
     D WSIEM           s             60
     D WSITLF          s             16
     D WSLNAV          s             30
     D WSLAD1          s             30
     D WSLAD2          s             30
     D WSLAD3          s             30
     D WSLAD4          s             30
     D WSLREF          s             30
     D WSLKP           s             36
     D WSLEM           s             80
     D WSLTLF          s             16
     D WSBLNR          s             16
     D WSORIG          s              1
     D WSLBET          s              3
     D WSLBES          s             20
     D WSFLK           s              2
     D WSFSTE          s              5
     D WSTLK           s              2
     D WSTSTE          s              5
     D WSTTD           s             10
     D WSTTDN          s              8  0
     D WSETD           s             10
     D WSETDN          s              8  0
     D WSSTD           s             10
     D WSSTDN          s              8  0
     D WSETA           s             10
     D WSETAN          s              8  0
     D*WSTRKO          s             10
     D WSSTA           S              5
     D StatChgd        S              1
     D WSREAS          S              3
     D WSPRIO          S              1
     D WSFTXT          S             70
     D WSSUBM          S              1
     D WSANTK          S              7  0
     D WSVKT           S              9  2
     D WSM3            S              7  3
     D AVSLUTT         S              1
     D FMELDNR         S              2
     D FEILMELDING     S             80
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D*From            s              8
     D*To              s              8
     D STATUS          S            100
     D REASON          S            100
     D Title           S            100
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
     D DT8                     1      8  0
     D  DTÅ                    1      4  0
     D  DTM                    5      6  0
     D  DTD                    7      8  0
     D                 DS
     D ELETD                   1      8  0
     D  ELMMDD                 5      8  0
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
N04   *
N04  C                   IF        XJOBBKL = 'J'
N04  C                   callp     wrtsection('TopCl21')
N04  C                   callp     wrtsection('TopCl23')
N04  C                   callp     wrtsection('TopCl31')
N04  C                   callp     wrtsection('TopCl33')
N04  C                   callp     wrtsection('TopCl41')
N04  C                   ELSE
N04  C                   callp     wrtsection('TopCl22')
N04  C                   callp     wrtsection('TopCl23')
N04  C                   callp     wrtsection('TopCl32')
N04  C                   callp     wrtsection('TopCl33')
N04  C                   callp     wrtsection('TopCl42')
     C                   exsr      SLTREASON
     C                   callp     wrtsection('bot')
N04  C                   END
     C                   callp     wrtsection('BotCl')
N04   *
     C                   exsr      DSPTT
N04  C                   callp     wrtsection('TopCl9')
N04  C                   IF        XJOBBKL = 'J'
N04  C                   callp     wrtsection('TopCl81')
N04  C                   ELSE
N04  C                   callp     wrtsection('TopCl82')
N04  C                   END
N04  C                   callp     wrtsection('TopCl83')
      *
      * Quit
      * Compute run time
      *
     C     SLUTT         TAG
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      ****************************************************
     C     SLTREASON     BEGSR
      ****************************************************
      * Select new reason-code..
     C                   callp     wrtsection('Reasontop')
      *
     C                   IF        WSREAS = *BLANKS
     C                   callp     updhtmlvar('SLREASON':'')
     C                   callp     updhtmlvar('RKD':'')
     C                   callp     updhtmlvar('RFT':' -- blank -- ')
     C                   callp     wrtsection('Reasonrow')
     C                   END
      *
     C                   MOVE      *ZEROS        XN                3 0
     C                   MOVEL     'POMRC'       PAID
     C                   MOVE      BIBRID        PABRID
     C     BRPARFK       CHAIN     BRPARF                             33
     c   33              Eval      PABRID = '*KUNDFT*'+BIFIRM
     C   33BRPARFK       CHAIN     BRPARF                             33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVEL     PAVRDA        XAN03             3
     C                   callp     updhtmlvar('RKD':XAN03)
     C                   callp     updhtmlvar('RFT':PAVRDA)
     C                   IF        XAN03 = WSREAS
     C                   callp     updhtmlvar('SLREASON':'SELECTED')
     C                   else
     C                   callp     updhtmlvar('SLREASON':'')
     C                   END
     C                   callp     wrtsection('Reasonrow')
     C     BRPARFK       READE     BRPARF                                 33
     C                   ENDDO
      *
     C                   callp     wrtsection('ReasonBot')
      *
     C                   ENDSR
      *
      ****************************************************
     C     DSPTT         BEGSR
      ****************************************************
     C                   callp     wrtsection('TTSTART')
     C     EPOTTK        SETLL     EPOTT
     C     EPOTTK        READE     EPOTT                                  33
     c                   DOW       *IN33 = *OFF
     C                   callp     updHTMLvar('ETDATE':
     C                             %trim(%editw(ETDATE:'    .  .  ')))
     C                   callp     updHTMLvar('ETTIME':
     C                             %trim(%editw(ETTIME:'  :  :  ')))
     C                   callp     updhtmlvar('ETTEXT':ETTEXT)
     C                   callp     updhtmlvar('ETUSER':ETUSER)
     C                   callp     wrtsection('TTROW')
     C     EPOTTK        READE     EPOTT                                  33
     C                   ENDDO
      *
     C                   callp     wrtsection('TTSLUTT')
      *
     C                   ENDSR
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     c                   eval      EHBES    = zhbgetvar('EHBES')
      *
     C                   eval      WSIID   = zhbgetvar('WSIID')
     C     LC:UC         XLATE     WSIID         WSIID
     C                   eval      WSINV   = zhbgetvar('WSINV')
     C     LC:UC         XLATE     WSINV         WSINV
     C                   eval      WSIEM   = zhbgetvar('WSIEM')
     C     LC:UC         XLATE     WSIEM         WSIEM
     C                   eval      WSITLF  = zhbgetvar('WSITLF')
     C     LC:UC         XLATE     WSITLF        WSITLF
     C                   eval      WSLNAV  = zhbgetvar('WSLNAV')
     C     LC:UC         XLATE     WSLNAV        WSLNAV
     C                   eval      WSLAD1  = zhbgetvar('WSLAD1')
     C     LC:UC         XLATE     WSLAD1        WSLAD1
     C                   eval      WSLAD2  = zhbgetvar('WSLAD2')
     C     LC:UC         XLATE     WSLAD2        WSLAD2
     C                   eval      WSLAD3  = zhbgetvar('WSLAD3')
     C     LC:UC         XLATE     WSLAD3        WSLAD3
     C                   eval      WSLAD4  = zhbgetvar('WSLAD4')
     C     LC:UC         XLATE     WSLAD4        WSLAD4
     C                   eval      WSLREF  = zhbgetvar('WSLREF')
     C     LC:UC         XLATE     WSLREF        WSLREF
     C                   eval      WSLKP   = zhbgetvar('WSLKP')
     C     LC:UC         XLATE     WSLKP         WSLKP
     C                   eval      WSLEM   = zhbgetvar('WSLEM')
     C                   eval      WSLTLF  = zhbgetvar('WSLTLF')
     C                   eval      WSBLNR  = zhbgetvar('WSBLNR')
     C     LC:UC         XLATE     WSBLNR        WSBLNR
     C                   eval      WSORIG  = zhbgetvar('WSORIG')
     C     LC:UC         XLATE     WSORIG        WSORIG
     C                   eval      WSLBET  = zhbgetvar('WSLBET')
     C     LC:UC         XLATE     WSLBET        WSLBET
     C                   eval      WSLBES  = zhbgetvar('WSLBES')
     C     LC:UC         XLATE     WSLBES        WSLBES
     C                   eval      WSFLK   = zhbgetvar('WSFLK')
     C     LC:UC         XLATE     WSFLK         WSFLK
     C                   eval      WSFSTE  = zhbgetvar('WSFSTE')
     C     LC:UC         XLATE     WSFSTE        WSFSTE
     C                   eval      WSTLK   = zhbgetvar('WSTLK')
     C     LC:UC         XLATE     WSTLK         WSTLK
     C                   eval      WSTSTE  = zhbgetvar('WSTSTE')
     C     LC:UC         XLATE     WSTSTE        WSTSTE
     C*                  eval      WSTTD   = zhbgetvar('WSTTD')
     C*                  EVAL      WSTTDN  = c2n2(WSTTD)
     C                   eval      WSETD   = zhbgetvar('WSETD')
     C                   EVAL      WSETDN  = c2n2(WSETD)
     C                   eval      WSSTD   = zhbgetvar('WSSTD')
     C                   EVAL      WSSTDN  = c2n2(WSSTD)
     C                   eval      WSETA   = zhbgetvar('WSETA')
     C                   EVAL      WSETAN  = c2n2(WSETA)
     C*                  eval      WSTRKO  = zhbgetvar('WSTRKO')
     C                   eval      WSSTA   = zhbgetvar('WSSTA')
      *
     C                   eval      WSREAS  = zhbgetvar('WSREAS')
     C                   eval      WSPRIO  = zhbgetvar('WSPRIO')
     C                   eval      WSFTXT  = zhbgetvar('WSFTXT')
     C                   eval      WSSUBM  = zhbgetvar('WSSUBM')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     CUNDL01
     C                   CLOSE     EPOH
     C                   CLOSE     EPOL
     C                   CLOSE     EPOTT
     C                   CLOSE     BRPARF
     C                   CLOSE     STED2
     C                   CLOSE     KODTLB
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     c                   callp     gethtmlifs(
cccccC                             '/syspeddev/html/EPOMS03.html':
cccccC                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTOP        begsr
      * VERDIER som kan endres flyttes til WSvariable kun første gang (WSSUBM=blank)
     C     KEY           CHAIN(N)  EPOH                               33
     C                   IF        WSSUBM = ' '
     c                   exsr      GETCHG
     c                   else
      * Y=Submit/kontroll av allerde vist bilde: TEST / Alt OK=>Update og sett var for close..
     c                   exsr      TSTCHG
     c     FMELDNR       ifeq      *blanks
     c                   exsr      UPDCHG
     c                   eval      AVSLUTT = 'Y'                                Gir 'self.close'
     c                   endif
     c                   endif
      *
      * Klargjør neste visning (ved avslutt=Y KUNNE en hoppe det meste her,men lite tid å spare)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BIBESK':BIBESK)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
     C                   callp     updHtmlVar('EHBES':EHBES)
      *
     C                   callp     updHTMLvar('EHDATO':
     C                             %trim(%editw(EHDATO:'    .  .  ')))
     C                   callp     updHTMLvar('EHTTD':
     C                             %trim(%editw(EHTTD:'    .  .  ')))
     C                   callp     updHTMLvar('WSETD':
     C                             %trim(%editw(WSETDN:'    .  .  ')))
      * SJEKK OM DET FINNES AVVIKENDE LINE-ETD
     c                   move      *blanks       LINDT           500
     C     KEY           SETLL     EPOL
     C     KEY           READE(N)  EPOL                                   33
     C                   DOW       *IN33 = *OFF
     C                   IF        ELETD <> EHETD
     c                   if        LINDT = *blanks
     C                   eval      LINDT  = 'Line dates: '
     c*                            + %trim(%editw(ELMMDD:'0  .  '))
     C                             + %trim(%editw(ELETD:'    .  .  '))
     c                   else
     C                   eval      LINDT  = %trim(LINDT) +', '
     c*                            + %trim(%editw(ELMMDD:'   .  '))
     c                             + %trim(%editw(ELETD:'    .  .  '))
     c                   endif
     C                   END
     C     KEY           READE(N)  EPOL                                   33
     C                   ENDDO
     C                   callp     updHTMLvar('LINDT':LINDT)
      *
     C                   callp     updHTMLvar('EHSTD':
     C                             %trim(%editw(EHSTD:'    .  .  ')))
     C                   callp     updHTMLvar('EHETA':
     C                             %trim(%editw(EHETA:'    .  .  ')))
     C                   callp     updHTMLvar('EHLEVN':EHLEVN)
     C                   callp     updHTMLvar('EHLKNI':
     C                             %trim(%editc(EHLKNI:'3')))
     C                   callp     updHTMLvar('EHLVAI':
     C                             %trim(%editc(EHLVAI:'3')))
     C                   callp     updHTMLvar('WSIID':WSIID)
     C                   callp     updHTMLvar('WSINV':WSINV)
     C                   callp     updHTMLvar('WSIEM':WSIEM)
     C                   callp     updHTMLvar('WSITLF':WSITLF)
N04  C                   callp     updHTMLvar('EHLEVN':EHLEVN)
     C                   callp     updHTMLvar('WSLNAV':WSLNAV)
     C                   callp     updHTMLvar('WSLAD1':WSLAD1)
     C                   callp     updHTMLvar('WSLAD2':WSLAD2)
     C                   callp     updHTMLvar('WSLAD3':WSLAD3)
     C                   callp     updHTMLvar('WSLAD4':WSLAD4)
     C                   callp     updHTMLvar('WSLREF':WSLREF)
     C                   callp     updHTMLvar('WSLKP':WSLKP)
     C                   callp     updHTMLvar('WSLEM':WSLEM)
     C                   callp     updHTMLvar('WSLTLF':WSLTLF)
     C                   callp     updHTMLvar('WSBLNR':WSBLNR)
     C                   callp     updHTMLvar('WSORIG':WSORIG)
     C                   callp     updHTMLvar('WSLBET':WSLBET)
     C                   callp     updHTMLvar('WSLBES':WSLBES)
     c*                  eval      From = EHFLK +'-'+EHFSTE
     C*                  callp     updHTMLvar('FROM':FROM)
     c*                  eval      To   = EHTLK +'-'+EHTSTE
     C*                  callp     updHTMLvar('WSTRKO':WSTRKO)
     C*                  callp     updHTMLvar('TO':TO)
     C                   callp     updHTMLvar('WSFLK':WSFLK)
     C                   callp     updHTMLvar('WSFSTE':WSFSTE)
     C                   callp     updHTMLvar('WSTLK':WSTLK)
     C                   callp     updHTMLvar('WSTSTE':WSTSTE)
N04  C                   callp     updHTMLvar('EHGBES':EHGBES)
N04  C                   callp     updHTMLvar('EHVAL':EHVAL)
N05   *
N05   * SUMMER VEKT, STYK,,, KUN FRA LINJER SOM IKKE ER PLUKKET
N05  C                   EVAL      EHANTS = 0
N05  C                   EVAL      EHANTK = 0
N05  C                   EVAL      EHVKT  = 0
N05  C                   EVAL      EHM3   = 0
N05  C     KEY           SETLL     EPOL
N05  C     KEY           READE(N)  EPOL                                   33
N05  C                   DOW       *IN33 = *OFF
N05  C                   IF        ELAVD = 0
N05  C                   EVAL      EHANTS = EHANTS - ELNTSL
N05  C                   EVAL      EHANTK = EHANTK - ELNTKL
N05  C                   EVAL      EHVKT  = EHVKT  - ELVKTL
N05  C                   EVAL      EHM3   = EHM3   - ELM3L
N05  C                   END
N05  C     KEY           READE(N)  EPOL                                   33
N05  C                   ENDDO
N05   *
     C                   callp     updHTMLvar('EHANTS':
     C                             %trim(%editc(EHANTS:'Z')))
     C                   eval(H)   WSANTK  = EHANTK
     C                   callp     updHTMLvar('EHANTK':
     C                             %trim(%editc(WSANTK:'J')))
     C                   eval(H)   WSVKT   = EHVKT
     C                   callp     updHTMLvar('EHVKT':
     C                             %trim(%editc(WSVKT:'J')))
     C                   eval(H)   WSM3    = EHM3
     C                   callp     updHTMLvar('EHM3':
     C                             %trim(%editc(WSM3:'J')))
     c                   select
     c     EHSTA         wheneq    ' '
     c     EHSTA         oreq      '0'
N04  c                   IF        XJOBBKL = 'J'
N04  c                   eval      STATUS = 'Confirmed'
N04  c                   ELSE
     c                   eval      STATUS = 'Pending'
N04  c                   END
     c     EHSTA         wheneq    '1'
     c                   eval      STATUS = 'Contacted'
     c     EHSTA         wheneq    '15'
     c                   eval      STATUS = 'Ready for stuffing'
     c     EHSTA         wheneq    '2'
     c                   eval      STATUS = 'Booked'
     c     EHSTA         wheneq    '3'
     c                   eval      STATUS = 'Confirmed'
     c     EHSTA         wheneq    '4'
     c                   eval      STATUS = 'Shipped'
     c     EHSTA         wheneq    '5'
     c                   eval      STATUS = 'Arrived'
     c     EHSTA         wheneq    '6'
     c                   eval      STATUS = '6*Undefined*'
     c                   endsl
     C                   callp     updHTMLvar('STATUS':STATUS)                  GML/Gjeldende status
     C                   callp     updHTMLvar('WSSTA':WSSTA)                    NY status
     C                   callp     updHTMLvar('WSPRIO':WSPRIO)
     C                   callp     updHTMLvar('WSFTXT':WSFTXT)
      *
     c                   eval      OKBUTTON ='<INPUT TYPE="SUBMIT" '
     c                             +'VALUE="   OK   ">'
     c                   if        EHSTA2 = 'S'
     c                   move      *blanks       OKBUTTON        150
     c                   eval      OKBUTTON ='<b>OK button not allowed due to '
     c                             +'status.</b>'
     c                   endif
     C                   callp     updHTMLvar('OKBUTTON':OKBUTTON)
     C                   callp     updHTMLvar('EHREAS':EHREAS)                  GML/Gjeldende Reason
     C                   callp     updHTMLvar('AVSLUTT':AVSLUTT)
     C                   callp     updHTMLvar('FMELDNR':FMELDNR)
     C                   callp     updHTMLvar('FEILMELDING':FEILMELDING)
     C                   endsr
      *
      ***************************************************
     C     GETCHG        begsr
      ***************************************************
      * Kun data som skal endres trengs over i WS-variable
     C                   eval      WSIID  = EHIID
     C                   eval      WSINV  = EHINV
     C                   eval      WSIEM  = EHIEM
     C                   eval      WSITLF = EHITLF
N04  C                   IF        EHITLF = *BLANKS
N04  C                   MOVE      'EUR'         EHVAL             3
N04  C                   ELSE
N04  C                   eval      EHVAL  = EHITLF
N04  C                   END
     C                   eval      WSLNAV = EHLNAV
     C                   eval      WSLAD1 = EHLAD1
     C                   eval      WSLAD2 = EHLAD2
     C                   eval      WSLAD3 = EHLAD3
     C                   eval      WSLAD4 = EHLAD4
     C                   eval      WSLREF = EHLREF
     C                   eval      WSLKP  = EHLKP
     C                   eval      WSLEM  = EHLEM
     C                   eval      WSLTLF = EHLTLF
     C                   eval      WSBLNR = EHBLNR
     C                   eval      WSORIG = EHORIG
     C                   eval      WSLBET = EHLBET
     C                   eval      WSLBES = EHLBES
     C                   eval      WSFLK  = EHFLK
     C                   eval      WSFSTE = EHFSTE
     C                   eval      WSTLK  = EHTLK
     C                   eval      WSTSTE = EHTSTE
     C                   eval      WSPRIO = EHPRIO
     C                   eval      WSFTXT = EHFTXT
     C*                  eval      WSTTDN = EHTTD
     C                   eval      WSETDN = EHETD
     C*                  eval      WSSTDN = EHSTD
     C                   eval      WSETAN = EHETA
     C*                  eval      WSTRKO = EHTRKO
     C*                  eval      WSSTA  = EHSTA
     c     EHSTA2        ifeq      'S'
     c                   eval      FEILMELDING = 'Previous changes'
     c     EHSTA3        ifeq      'R'
     c                   eval      FEILMELDING = %trim(FEILMELDING)
     c                             +' (and reason)'
     c                   endif
     c                   eval      FEILMELDING = %trim(FEILMELDING)
     c                             +' NOT YET SENT to consignee.'
     c                   endif
     C                   endsr
      *
      ***************************************************
     C     TSTCHG        begsr
      ***************************************************
     C                   MOVE      *blanks       FMELDNR
     C                   MOVE      *blanks       FEILMELDING
      *
      * Diverse tester
      * WSLNAV = Leverandørsnavn
S04  c*                  IF        WSLNAV = *BLANKS
N04  C                   IF        WSLNAV = *BLANKS AND XJOBBKL <> 'J'
     C                   MOVE      '01'          FMELDNR
     c                   eval      FEILMELDING = 'Name of supplier missing.'
     C                   GOTO      UtTst
     C                   ENDIF
      * WSLAD1 = Leverandørsadr.1
S04  c*                  IF        WSLAD1 = *BLANKS
N04  C                   IF        WSLAD1 = *BLANKS AND XJOBBKL <> 'J'
     C                   MOVE      '02'          FMELDNR
     c                   eval      FEILMELDING = 'Address of supplier can not '
     c                             + 'be blanks.'
     C                   GOTO      UtTst
     C                   ENDIF
      * WSTTD = Tidligste Tid Departure
     c*    WSTTDN        ifne      *zeros
     C*                  MOVE      WSTTDN        DT8
     C*                  EXSR      TSTDT
     C*    UFLAGG        IFEQ      1
     C*                  MOVE      '03'          FMELDNR
     c*                  eval      FEILMELDING = 'Invalid Earliest Ship. Date '
     c*                            + '(valid: yyyy.mm.dd)'
     C*                  GOTO      UtTst
     C*                  ENDIF
     C*                  ENDIF
      * ETD-dato kan ikke være blank
     c     WSETDN        ifeq      *zeros
     C                   eval      FMELDNR = '04'
     c                   eval      FEILMELDING = 'ETD cannot be blank'
     c                   goto      UtTst
     C                   endif
      *
     C                   MOVE      WSETDN        DT8
     C                   EXSR      TSTDT
     C     UFLAGG        IFEQ      1
     C                   MOVE      '04'          FMELDNR
     c                   eval      FEILMELDING = 'Invalid ETD date '
     c                             + '(valid: yyyy.mm.dd)'
     C                   GOTO      UtTst
     C                   ENDIF
      *
      * TEST OM ETD ER UTENFOR SHIPPINGVINDU. I SÅ FALL KREVER REASON CODE
      * (MEN KUN DERSOM SHIPPING VINDU ER ANGITT.. (BURDE TESTE INDIVIDULET - EARLEIST/LATEST))
N04  c                   IF        XJOBBKL <> 'J'
     c                   IF        EHTTD <> *ZEROS AND EHSTD <> *ZEROS
     c                   IF        WSETDN <> EHETD AND WSREAS = *BLANKS
     c                   IF        WSETDN < EHTTD OR WSETDN > EHSTD
     C                   MOVE      '11'          FMELDNR
     c                   eval      FEILMELDING = 'ETD is out of shipping '
     c                             + 'window. Please give a reason.'
     C                   GOTO      UtTst
     c                   END
     c                   END
     c                   END
N04  c                   END
      *
      * WSSTD = Siste e Tid Departure
     c*    WSSTDN        ifne      *zeros
     C*                  MOVE      WSSTDN        DT8
     C*                  EXSR      TSTDT
     C*    UFLAGG        IFEQ      1
     C*                  MOVE      '05'          FMELDNR
     c*                  eval      FEILMELDING = 'Invalid Latest Ship. Date '
     c*                            + '(valid: yyyy.mm.dd)'
     C*                  GOTO      UtTst
     C*                  ENDIF
     C*                  ENDIF
      * WSETA = Tidligste Tid Departure
     c*    WSETAN        ifne      *zeros
     C*                  MOVE      WSETAN        DT8
     C*                  EXSR      TSTDT
     C*    UFLAGG        IFEQ      1
     C*                  MOVE      '06'          FMELDNR
     c*                  eval      FEILMELDING = 'Invalid ETA Date '
     c*                            + '(valid: yyyy.mm.dd)'
     C*                  GOTO      UtTst
     C*                  ENDIF
     C*                  ENDIF
N04  C                   IF        XJOBBKL = 'J'
N04  C                   GOTO      UTTST
N04  C                   END
      * WSLBET = Leveringsbetingelse
     c                   IF        WSLBET <> *BLANKS
     C     KODTLBK       CHAIN     KODTLB                             33
     c                   IF        *IN33
     C                   MOVE      '07'          FMELDNR
     c                   eval      FEILMELDING = 'Terms is invalid.'
     C                   GOTO      UtTst
     C                   ENDIF
     c                   IF        WSLBES = *BLANKS
     c                   EVAL      WSLBES = KLBNAV
     C                   ENDIF
     C                   ENDIF
      * WSFSTE = FRA HAVN (POL)
     C     WSFSTE        CHAIN     STED2                              33
     c                   IF        *IN33
     C                   MOVE      '08'          FMELDNR
     c                   eval      FEILMELDING = 'POL - Port of Loading - '
     c                             +'is invalid.'
     C                   GOTO      UtTst
     C                   ELSE
     C                   EVAL      WSFLK = ST2LK
     C                   ENDIF
      * WSTSTE = TIL HAVN (POD)
     C     WSTSTE        CHAIN     STED2                              33
     c                   IF        *IN33
     C                   MOVE      '09'          FMELDNR
     c                   eval      FEILMELDING = 'POD - Port of Discharge - '
     c                             +'is invalid.'
     C                   GOTO      UtTst
     C                   ELSE
     C                   EVAL      WSTLK = ST2LK
     C                   ENDIF
      * Transp. code
     c*    WSTRKO        ifeq      *blanks
     C*                  eval      FMELDNR = '05'
     c*                  eval      FEILMELDING = 'Transport code cannot '
     c*                            + 'be blank.'
     c*                  goto      UtTst
     C*                  endif
      * Status kan kun være 0,1.
      * nå  er input gjort om til Pulldown - umulig med feil...
     c     WSSTA         ifne      ' '
     c     WSSTA         andne     '0'
     c     WSSTA         andne     '1'
     c     WSSTA         andne     '15'
     C                   eval      FMELDNR = '10'
     c                   eval      FEILMELDING = 'Status can only be set to: '
     c                             + '0=Pending, 1=Contacted '
     c                             + '15=Ready for stuffing'
     c                   goto      UtTst
     C                   endif
     c
      *
      *
     C     UtTst         endsr
      *
      **************************************************************
     C     TSTDT         BEGSR
      **************************************************************
     C                   MOVE      DTD           UDD               2 0
     C                   MOVE      DTM           UMM               2 0
     C                   MOVE      DTÅ           UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C                   endsr
      *
      ***************************************************
     C     UPDCHG        begsr
      ***************************************************
N01  C                   EVAL      ETSTAT = ' '
      * NOE er endret = EHSTA2 = S - Send
      * REASON endret = EHSTA3 = R - Reason endret
     C     KEY           CHAIN     EPOH                               33
     C                   IF        *IN33 = *OFF                                 1<-B
      *
      * OPPDATER T&T
     c                   IF        EHLNAV <> WSLNAV OR                           2<-B
     c                             EHLAD1 <> WSLAD1 OR
     c                             EHLAD2 <> WSLAD2 OR
     c                             EHLAD3 <> WSLAD3 OR
     c                             EHLAD4 <> WSLAD4
     C                   EVAL      ETTEXT = 'Suppliers name/address is changed.'
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     c                   IF        EHLREF <> WSLREF                              2<-B
     C                   EVAL      ETTEXT = 'Suppliers reference is changed '
     C                             + 'from ' + %TRIM(EHLREF)
     C                             + ' to ' + %TRIM(WSLREF)
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     c                   IF        EHETD <> WSETDN                               2<-B
     c*ikke endre        EVAL      WSSTA = '1'
     C                   EVAL      WSSTA = '1'
     C                   EVAL      ETTEXT = 'ETD is changed from '
     C                             + %TRIM(%EDITW(EHETD:'    .  .  '))
     C                             + ' to '
     C                             + %TRIM(%EDITW(WSETDN:'    .  .  '))
     C                   EVAL      ETACTI = 'CHD'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     C                   IF        EHBLNR <> WSBLNR                              2<-B
     C                   EVAL      ETTEXT = 'BL-no is changed '
     C                             + 'from ' + %TRIM(EHBLNR)
     C                             + ' to ' + %TRIM(WSBLNR)
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     C                   IF        EHORIG = *BLANKS AND WSORIG <> *BLANKS        2<-B
     C                   EVAL      ETTEXT = 'Orignal is received.'
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     C                   IF        WSORIG = *BLANKS AND EHORIG <> *BLANKS        2<-B
     C                   EVAL      ETTEXT = 'Orignal is not received.'
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     c                   IF        EHLBET <> WSLBET OR                           2<-B
     c                             EHLBES <> WSLBES
     C                   EVAL      ETTEXT = 'Del.terms is changed '
     C                             + 'from ' + %TRIM(EHLBET)
     C                             + '/' + %TRIM(EHLBES)
     C                             + ' to ' + %TRIM(WSLBET)
     C                             + '/' + %TRIM(WSLBES)
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     c                   IF        EHFSTE <> WSFSTE                              2<-B
     C                   EVAL      ETTEXT = 'POL is changed '
     C                             + 'from ' + %TRIM(EHFSTE)
     C                             + ' to ' + %TRIM(WSFSTE)
     C                   EVAL      ETACTI = 'CHG'
N01  C                   EVAL      ETSTAT = 'S'
     C                   EXSR      UPDTT
N01  C                   EVAL      ETSTAT = ' '
     C                   END                                                     2<-E
     c                   IF        EHTSTE <> WSTSTE                              2<-B
     C                   EVAL      ETTEXT = 'POD is changed '
     C                             + 'from ' + %TRIM(EHTSTE)
     C                             + ' to ' + %TRIM(WSTSTE)
     C                   EVAL      ETACTI = 'CHG'
N01  C                   EVAL      ETSTAT = 'S'
     C                   EXSR      UPDTT
N01  C                   EVAL      ETSTAT = ' '
     C                   END                                                     2<-E
      * mail om endret POL eller POD
     c                   IF        EHFSTE <> WSFSTE                              2<-B
     c                   move      *blanks       CMD            1024
     c                   move      *blanks       mailto           60
OBS!!c                   eval      mailTo ='janottar@systema.no'
OBS!!c**                 eval      mailTo ='kenneth.johansen@europris.no'
     c                   if        EHIEM<>*blanks                                 3<-B
OBS!!c**                 eval      mailTo = EHIEM
     c                   endif                                                    3<-E
     c                   eval      CMD='SNDDST TYPE(*LMSG) TOINTNET(('''
     c                             +%trim(mailto)+''')) DSTD(''PortChg: PO '
     c                             +%trim(EHBES)+'   '
     c                             +%trim(EHFSTE)+'-'+%trim(EHTSTE)+'->'
     c                             +%trim(WSFSTE)+'-'+%trim(WSTSTE)
     c                             +''') MSG(''Information about changed Port '
     c                             +'Of Loading and/or Port Of Discharge.'') '
     c                             +'LONGMSG(''On PO '+%trim(EHBES)+' (all '
     c                             +'lines) POL-POD was changed from '
     c                             +%trim(EHFSTE)+'-'+%trim(EHTSTE)+' to '
     c                             +%trim(WSFSTE)+'-'+%trim(WSTSTE)
     c                             +':/N:/N:/Nnoreply@collicare.no'')'
     c                   eval      rc=docmd(%trim(CMD))
     c                   ENDIF                                                   2<-E
      * Er status endret ??
     c                   Eval      StatChgd = 'N'
     c                   IF        EHSTA <> WSSTA AND WSSTA <> *BLANKS           2<-B
     c                   Eval      StatChgd = 'Y'
     C                   EVAL      ETTEXT = 'Status is changed from '
     C                             + %TRIM(EHSTA) + ' to ' + %TRIM(WSSTA)
     C                   MOVE      WSREAS        ETTEXT
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     c                   IF        EHREAS <> WSREAS                              2<-B
     C                   EVAL      ETTEXT = 'Reason code is updated.'
     C                   MOVE      WSREAS        ETTEXT
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
     C                   IF        EHPRIO <> WSPRIO                              2<-B
     C                   EVAL      ETTEXT = 'Priority is changed from '
     C                             + %TRIM(EHPRIO) + ' to ' + %TRIM(WSPRIO)
     C                   EVAL      ETACTI = 'CHG'
     C                   EXSR      UPDTT
     C                   END                                                     2<-E
      *
      *
      * OPPDATER LINJE VED ENDRING AV ETD, Status eller Reason
      *       MEN kun de linjer som har samme ETD som Headeren
     C                   IF        EHETD <> WSETDN                               2<-B
     c**                           or EHSTA <> WSSTA
     c                             or StatChgd = 'Y'
     c                             or EHREAS<> WSREAS
     C     KEY           SETLL     EPOL
     C     KEY           READE     EPOL                                   33
     C                   DOW       *IN33 = *OFF                                   3<-B
S03  C*                  IF        ELETD = EHETD
N03  C                   IF        ELETD = EHETD AND ELAVD = 0                     4<-B
      * T&T
     c                   IF        ELSTA = *BLANKS                                  5<-B
     C                   EVAL      ETTEXT = 'Status is changed from 0'
     C                             + ' to ' + %TRIM(WSSTA)
     C                   ELSE                                                       5
     C                   EVAL      ETTEXT = 'Status is changed from '
     C                             + %TRIM(ELSTA) + ' to ' + %TRIM(WSSTA)
     C                   END                                                        5<-E
     C                   EVAL      ETKUND = ELKUND
     C                   EVAL      ETBES  = ELBES
     C                   EVAL      ETLIN  = ELLIN
     C                   EVAL      ETULIN = ELULIN
     C                   EVAL      ETACTI = 'CHG'
     C                   MOVE      WDT           ETDATE
     C                   MOVE      WTM           ETTIME
     C                   MOVE      BIBRID        ETUSER
     C                   WRITE     EPOTTR
      *
     C                   EVAL      ELETD = WSETDN
     c                   IF        EHETD <> WSETDN                                  5<-B
      *  Kun dato endret - status justeres kun opp (normalt 0->1) på linjer
N04  C                   IF        XJOBBKL = 'J'                                     6<-B
N04  C                   IF        WSSTA = '1' AND ELSTA < 'A'                        7<-B
N04  C                   EVAL      ELSTA = 'A'
N04  C                   END                                                          7<-E
N04  C                   ELSE                                                        6
     C                   if        ELSTA < WSSTA                                      7<-B
     C                   EVAL      ELSTA  = WSSTA
     C                   endif                                                        7<-E
N04  C                   END                                                         6<-E
     C                   ELSE                                                       5
      *  aktiv stat-endr=justeres ubetinget (fanger her IKKE datoendring OG aktiv status NED-just)
     C                   EVAL      ELSTA  = WSSTA
     C                   ENDIF                                                      5<-E
     C                   EVAL      ELREAS = WSREAS
N04  C                   IF        XJOBBKL <> 'J'
     C                   eval      ELSTA2 = 'S'                                 Send
N04  C                   END
     C                   END                                                       4<-E
     C                   UPDATE    EPOLR
     C     KEY           READE     EPOL                                   33
     C                   ENDDO                                                    3<-E
     C                   END                                                     2<-E
      *
      * Oppdatering av 'SEND status' på HEADER-nivå bør utgå.
     c                   if        (EHETD <> WSETDN)                             2<-B
     c**                           or (EHSTA <> WSSTA)
     c                             or (StatChgd = 'Y')
     c                             or (EHREAS<> WSREAS)
N01  c                             or (EHFSTE<> WSFSTE)
N01  c                             or (EHTSTE<> WSTSTE)
N04  C                   IF        XJOBBKL <> 'J'                                 3<-E
     C                   eval      EHSTA2 = 'S'                                 Send
N04  c                   END                                                      3<-E
     c                   endif                                                   2<-E
N04  C                   endif                                                  1<-E
      *
     C                   eval      EHIID  = WSIID
     C                   eval      EHINV  = WSINV
     C                   eval      EHIEM  = WSIEM
     C                   eval      EHITLF = WSITLF
N04  C                   IF        XJOBBKL = 'J' AND EHITLF = *BLANKS
N04  C                   eval      EHITLF = 'EUR'
N04  C                   END
     C                   eval      EHLNAV = WSLNAV
     C                   eval      EHLAD1 = WSLAD1
     C                   eval      EHLAD2 = WSLAD2
     C                   eval      EHLAD3 = WSLAD3
     C                   eval      EHLAD4 = WSLAD4
     C                   eval      EHLREF = WSLREF
     C                   eval      EHLKP  = WSLKP
     C                   eval      EHLEM  = WSLEM
     C                   eval      EHLTLF = WSLTLF
     C                   eval      EHBLNR = WSBLNR
     C                   eval      EHORIG = WSORIG
     C                   eval      EHLBET = WSLBET
     C                   eval      EHLBES = WSLBES
     C                   eval      EHFLK  = WSFLK
     C                   eval      EHFSTE = WSFSTE
     C                   eval      EHTLK  = WSTLK
     C                   eval      EHTSTE = WSTSTE
     C                   eval      EHPRIO = WSPRIO
     C                   eval      EHFTXT = WSFTXT
     C*                  eval      EHTTD  = WSTTDN
N04  C                   SELECT
N04  C                   WHEN      XJOBBKL = 'J' AND EHSTA < '1'
N04  C                   MOVE      'J'           XSENDEDI          1
N04  C                   EVAL      EHSTA = '1'
N04  C                   WHEN      XJOBBKL = 'J' AND EHETD <> WSETDN
N04  C                   MOVE      'J'           XSENDEDI          1
N04  C                   OTHER
N04  C                   MOVE      'N'           XSENDEDI
N04  C                   ENDSL
     C                   eval      EHETD  = WSETDN
     C*                  eval      EHSTD  = WSSTDN
     C*                  eval      EHETA  = WSETAN
     C*                  eval      EHTRKO = WSTRKO
     c     WSSTA         ifne      *blanks                                      1<-B
S04  C*                  IF        WSSTA = '0'                                   2<-B PENDING
N04  C                   IF        WSSTA = '0' OR XJOBBKL = 'J'                  2<-B PENDING
     C                   EVAL      *IN33 = *ON
     C                   ELSE                                                    2    CONTACTED
      * SJEKK OM ALLE LINJER ER CONTACTED. (eller Ready for stuff.. 1 eller 15)
     C     KEY           SETLL     EPOL
     C     KEY           READE(N)  EPOL                                   33
     C                   DOW       *IN33 = *OFF                                   3<-B
      * Max laveste ELSTA
     C                   IF        ELSTA < WSSTA                                   4<-B
     C                   LEAVE
     C                   END                                                       4<-E
     C     KEY           READE(N)  EPOL                                   33
     C                   ENDDO                                                    3<-E
     C                   END                                                     2<-E
      * JA --> OPPDATER STATUS
     C   33              eval      EHSTA  = WSSTA
     c                   endif                                                  1<-E
     c     WSREAS        ifne      *blanks                                      1<-B
     C                   eval      EHREAS = WSREAS
     C                   eval      EHSTA3 = 'R'
     c                   endif                                                  1<-E
     c                   update    EPOHR
N04  C                   IF        XSENDEDI = 'J'
N04   * SEND ORDRSP 03A TIL SKEIDAR
N04  C                   MOVE      EHKUND        UKUND             8
N04  C                   MOVE      *ZEROS        ULIN              7
N04  C                   MOVE      *ZEROS        UULIN             5
N04  C                   CALL      'CC028C'
N04  C                   PARM                    UKUND
N04  C                   PARM                    EHBES
N04  C                   PARM                    ULIN
N04  C                   PARM                    UULIN
N04  C                   END
      *
     C                   endsr
      *
      ***************************************************
     C     UPDTT         BEGSR
      ***************************************************
     C                   EVAL      ETKUND = EHKUND
     C                   EVAL      ETBES  = EHBES
     C*                  EVAL      ETACTI = 'CHG'
     C                   MOVE      WDT           ETDATE
     C                   MOVE      WTM           ETTIME
     C                   MOVE      BIBRID        ETUSER
     C                   WRITE     EPOTTR
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   OPEN      CUNDL01
     C                   OPEN      EPOH
     C                   OPEN      EPOL
     C                   OPEN      EPOTT
     C                   OPEN      BRPARF
     C                   OPEN      STED2
     C                   OPEN      KODTLB
      *
     c     Key           klist
     C                   kfld                    BSKN
     c                   kfld                    EHBES
     c     KunKey        klist
     c                   kfld                    BIFIRM
     c                   kfld                    BSKN
     C     KunKey        chain     CUNDL01
     C     BRPARFK       KLIST
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     EPOTTK        KLIST
     C                   kfld                    EHKUND
     C                   kfld                    EHBES
     C                   kfld                    XETLIN
     C     *LIKE         DEFINE    ETLIN         XETLIN
     c     KODTLBK       klist
     c                   kfld                    KLBUNI
     c                   kfld                    WSLBET
     c                   EVAL      KLBUNI = 'LB'
N04   *
N04  C                   MOVE      BIBRID        PABRID
N04  C                   MOVE      *zeros        PAKUNR
N04  C                   MOVE      'POMKL'       PAID
N04  C                   MOVE      'N'           XJOBBKL           1
N04  C     BRPARFK       chain     BRPARF                             33
N04  C  N33              IF        %SUBST(PAVRDA:1:1) = 'J' OR
N04  C                             %SUBST(PAVRDA:1:1) = 'j'
N04  C                   EVAL      XJOBBKL = 'J'
N04  C                   END
      *
     C                   endsr
      *
