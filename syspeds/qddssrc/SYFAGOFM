     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                  1  3'SYSPED/SYFAGO'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV1402 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 20MSGCON(040 SHU0206 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A                                  4  3MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 29DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('WRONG DEPARTMENT           -
     A                                       !!        .' 99)
     A                                  5  3MSGCON(024 DIV1498 *LIBL/SYMSGF)
     A            WSSG           3A  B  5 29COLOR(YLW)
     A  98                                  ERRMSG('WRONG INITIALS             -
     A                                      !!     .' 98)
     A                                      DSPATR(UL)
     A                                  6  3MSGCON(024 DIV1544 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  6 29DSPATR(UL)
     A  97                                  ERRMSG('WRONG TRIPNUMBER           -
     A                                      !!!.' 97)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7  3MSGCON(024 CBA0100 *LIBL/SYMSGF)
     A            LISJN          1A  B  7 29COLOR(YLW)
     A                                  7 31MSGCON(006 DIV0430 *LIBL/SYMSGF)
     A                                  7 38MSGCON(011 TRO0496 *LIBL/SYMSGF)
     A            BELJN          1A  B  7 50COLOR(YLW)
     A                                  7 53MSGCON(019 TRO0497 *LIBL/SYMSGF)
     A            DADRJN         1A  B  7 73COLOR(YLW)
     A                                  7 75MSGCON(006 DIV0430 *LIBL/SYMSGF)
     A            FAXJN          1A  B  8 29COLOR(YLW)
     A                                  8 31MSGCON(006 DIV0430 *LIBL/SYMSGF)
     A                                  8 38MSGCON(011 TRO0496 *LIBL/SYMSGF)
     A            BELJN2         1A  B  8 50COLOR(YLW)
     A                                  9  3MSGCON(024 CBA0090 *LIBL/SYMSGF)
     A            EDIJN          1A  B  9 29COLOR(YLW)
     A                                  9 31MSGCON(012 DIV0430 *LIBL/SYMSGF)
     A                                 10  3MSGCON(024 SHU0293 *LIBL/SYMSGF)
     A            BRUDDA         1A  B 10 29COLOR(YLW)
     A                                 10 31MSGCON(012 DIV0430 *LIBL/SYMSGF)
     A                                 11  3MSGCON(024 DIV1509 *LIBL/SYMSGF)
     A            WSJBK          1A  B 11 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A                                 11 31MSGCON(012 DIV0430 *LIBL/SYMSGF)
     A                                 12  3MSGCON(024 DIV1510 *LIBL/SYMSGF)
     A            WSPRT         10A  B 12 29DSPATR(UL)
     A                                      COLOR(YLW)
     A N50                             13  3MSGCON(024 SHU0205 *LIBL/SYMSGF)
     A N50        CMR            1A  B 13 29DSPATR(UL)
     A                                      COLOR(YLW)
     A N50                             13 34MSGCON(020 SHU0204 *LIBL/SYMSGF)
     A N50                             14  8'ADDITIONAL INFORMATIONS FOR PRODUS-
     A                                      ING CMR                '
     A                                      DSPATR(RI)
     A N50                             15  8'ENCLOSURE            '
     A N50                             15 41'INSTRUCTIONS       '
     A N50        VED1          30A  B 16  8
     A N50        INS1          30A  B 16 41
     A N50        VED2          30A  B 17  8
     A N50        INS2          30A  B 17 41
     A N50        VED3          30A  B 18  8
     A N50        INS3          30A  B 18 41
     A N50                             19  8'DIVERGING DELIVERYADRESS     '
     A N50        INS4          30A  B 19 41
     A N50        LEV1          30A  B 20  8
     A N50                             20 41'DELIVERYTERM/PLACE'
     A N50        LEV2          30A  B 21  8
     A N50        CFRANK         3A  B 21 41
     A N50        CSTED         26A  B 21 45
     A N50        LEV3          30A  B 22  8
     A N50                             22 41'DATE'
     A N50        CDATO          6S 0B 22 46
     A N50        LEV4          30A  B 23  8
     A                                 23 59'F3 = END JOB'
     A                                      COLOR(BLU)
     A                                  8  3MSGCON(024 SHU0590 *LIBL/SYMSGF)
