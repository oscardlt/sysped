********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10004 * 01 YBC PTF10 LEVERANDØRSNR ER ØKT TIL 5.0
      *              INGEN MERKING
10XXX * 02 YBC PTF10 FEIL TEKST PÅ FUNSJONSTASTER
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTLK)
     A                                      CF03(03 'Avslutt')
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX05PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FLYFRAKTRA-
     A                                      TER')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            ORTNO         11Y 0H
     A            ORTNO2        11Y 0H
     A            ORTNO3        11Y 0H
     A            ORPORD         8Y 0H
     A            ORDTL          8Y 0H
     A            ORSNO          5Y 0H
     A            ORCLI          5Y 0H
     A            ORVOL          5Y 2H
     A            ORPOS1         5A  H
     A            PAPKD          1A  H
     A            ORPUKE         2A  H
     A            XLCBCD         5A  H
     A            ORCLNF         3Y 0H
     A            WSNR           1A  I  6  3VALUES(' ' '1' 'S' '5')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            ORPORT    R        O  6  6REFFLD(GSSORFR/ORPORT *LIBL/GSSORF)
     A            SFPORD         6Y 0O  6 12EDTCDE(4)
     A            SFDTL          6Y 0O  6 19EDTCDE(4)
     A            ORLC      R        O  6 26REFFLD(GSSORFR/ORLC *LIBL/GSSORF)
     A            ORST      R        O  6 32REFFLD(GSSORFR/ORST *LIBL/GSSORF)
     A            ORONO     R        O  6 34REFFLD(GSSORFR/ORONO *LIBL/GSSORF)
     A                                      EDTCDE(4)
     A            ORWT      R        O  6 42REFFLD(GSSORFR/ORWT *LIBL/GSSORF)
     A                                      EDTCDE(4)
     A            POCONO    R        O  6 51REFFLD(GSSPOFR/POCONO *LIBL/GSSPOF)
     A            ORBCD     R        O  6 72REFFLD(GSSORFR/ORBCD *LIBL/GSSORF)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A                                      CF04(04 'SØK')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/GSS080R'
     A                                      COLOR(BLU)
     A                                  1 19'Inquiry Port date'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  2  2'Type options:    1=Pick up  5=Disp-
     A                                      lay S=SYSPED order'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Pick to trip=1'
     A                                      COLOR(BLU)
     A            WSTRIP        11Y 0B  3 17EDTCDE(4)
     A  70                                  COLOR(RED)
     A                                  3 29'St:'
     A                                      COLOR(WHT)
     A            TRST           1A  O  3 33COLOR(YLW)
     A                                  3 35'Boat:'
     A                                      COLOR(WHT)
     A            TRBCD          5A  O  3 41COLOR(YLW)
     A                                  3 47'From:'
     A                                      COLOR(WHT)
     A            TRLOCF         5A  O  3 53COLOR(YLW)
     A                                  3 59'To:'
     A                                      COLOR(WHT)
     A            TRLOCT         5A  O  3 63COLOR(YLW)
     A                                  4  2'Opt Port  Portdt Delive Loc   S Or-
     A                                      d.no  Weight   customer order      -
     A                                       Vesse   '
     A                                      COLOR(WHT)
     A            WSPORT         5A  B  5  6
     A            WSPOWS         6Y 0B  5 12EDTCDE(4)
     A            WSLOC          5A  B  5 26
     A            WSONO          7Y 0B  5 34EDTCDE(4)
     A            WSCONO        20   B  5 51
     A            WSBCD          5A  B  5 72
      *
     A          R BUNN
     A                                      CF10(10 'Print')
     A            MSGNR          7A  P
S02  A*                                23  2MSGCON(18 DIV0020 SYMSGF)
S02  A*                                     COLOR(BLU)
S02  A*                                23 21MSGCON(18 DIV0021 SYMSGF)
S02  A*                                     COLOR(BLU)
S02  A*                                23 40MSGCON(18 DIV0022 SYMSGF)
S02  A*                                     COLOR(BLU)
S02  A*                                23 60MSGCON(18 DIV0023 SYMSGF)
N02  A                                 23  2'F3=End   F4=Search   F5=Refresh'
     A                                      COLOR(BLU)
     A  30        MSGTXY        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF04(04 'SØK')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/GSS080R'
     A                                      COLOR(BLU)
     A                                  1 19'Inquiry Port date'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  2  2'Type options:    1=Pick up  5=Disp-
     A                                      lay  S=SYSPED order'
     A                                      COLOR(BLU)
     A                                  3  2'PICK TO TRIP=1'
     A                                      COLOR(BLU)
     A            WSTRIP        11Y 0B  3 17EDTCDE(4)
     A  70                                  COLOR(RED)
     A                                  3 29'ST:'
     A                                      COLOR(WHT)
     A            TRST           1A  O  3 33COLOR(YLW)
     A                                  3 35'BOAT:'
     A                                      COLOR(WHT)
     A            TRBCD          5A  O  3 41COLOR(YLW)
     A                                  3 47'FROM:'
     A                                      COLOR(WHT)
     A            TRLOCF         5A  O  3 53COLOR(YLW)
     A                                  3 59'TO:'
     A                                      COLOR(WHT)
     A            TRLOCT         5A  O  3 63COLOR(YLW)
     A                                  4  2'Opt Port  Portdt Delive Loc   S Or-
     A                                      d.no  Weight   customer order      -
     A                                       Vesse   '
     A                                      COLOR(WHT)
     A            WSPORT         5A  B  5  6
     A            WSPOWS         6Y 0B  5 12EDTCDE(4)
     A            WSLOC          5A  B  5 26
     A            WSONO          7Y 0B  5 34EDTCDE(4)
     A            WSCONO        20   B  5 51
     A            WSBCD          5A  B  5 72
S02  A*                                23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
S02  A*                                     COLOR(BLU)
S02  A*                                23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
S02  A*                                     COLOR(BLU)
S02  A*                                23 40MSGCON(018 DIV0022 *LIBL/SYMSGF)
S02  A*                                     COLOR(BLU)
S02  A*                                23 60MSGCON(018 DIV0023 *LIBL/SYMSGF)
N02  A                                 23  2'F3=End   F4=Search   F5=Refresh'
     A                                      COLOR(BLU)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2008  '
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R PRTWS
     A                                      WINDOW(*DFT 7 30 *NOMSGLIN)
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Print packinglist'
     A                                      COLOR(WHT)
     A                                  3  2'Printer name:'
     A            WSPRT         10A  B  3 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'No. of copies:'
     A            WSCPY          1  0B  4 17DSPATR(UL)
     A                                  5  2'Send fax ?    '
     A            WSFAX          1A  B  5 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  5 20'Y/N'
     A                                  6  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99        WSMSGN        25A     7  2MSGID(GSS0042 GSSMSGF)
     A                                      COLOR(WHT)
      *
     A          R WINDOWSZ
     A                                      OVERLAY
     A                                      WINDOW(7 25 5 30 *NOMSGLIN)
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF12(12 'tilbake')
     A                                      OVERLAY
     A                                  1  2'1=select order subs(B,D,G)  '
     A                                      COLOR(WHT)
     A            SYVEL1         1A  B  2  2
     A            SYDEL1         1A  B  2  5VALUES(' ' 'B' 'D' 'G')
     A            SYDEN1         1A  O  2  8
     A            SYAVD1         4S 0O  2 10
     A            SYORD1         5S 0O  2 16
     A            SYVEL2         1A  B  3  2
     A            SYDEL2         1A  B  3  5VALUES(' ' 'B' 'D' 'G')
     A            SYDEN2         1A  O  3  8
     A            SYAVD2         4S 0O  3 10
     A            SYORD2         5S 0O  3 16
     A            SYVEL3         1A  B  4  2
     A            SYDEL3         1A  B  4  5VALUES(' ' 'B' 'D' 'G')
     A            SYDEN3         1A  O  4  8
     A            SYAVD3         4S 0O  4 10
     A            SYORD3         5S 0O  4 16
     A                                  5  2'F12=return'
     A                                      COLOR(BLU)
