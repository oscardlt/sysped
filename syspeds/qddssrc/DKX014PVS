********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NOMARG --> ALLE POS. FLYTTES 3 POS. TIL HØYRE
********************************************************************
      *_________________________________________
      * VARELISTE TRANSITTERING/SIKKERHET (TSAD)
      *"""""""""""""""""""""""""""""""""""""""""
     A          R DKX014FV1
     A            PVSFM1        75      1  2
     A            PVSFM2        75      2  2
     A                                  3 73'__________________________________-
     A                                      ____________'
     A                                  4 73'| MRN'
     A                                  4118'|'
     A                                  5  7'__________________________________'
     A                                  5 41'_______________'
     A                                  5 73'|'
     A            XHTRNR        18      5 76BARCODE(CODE128 3 *HRI *HRZ +
     A                                      (*WIDTH 0.010) (*RATIO 3) X'02')
     A                                  5118'|'
     A                                  6  7'|'
     A                                  6 55'|'
     A                                  6 73'|'
     A                                  6118'|'
     A                                  7  7'|   VARELISTE TRANSITTERING/SIKKER-
     A                                      HET - (TSAD)'
     A                                  7 55'|______________'
     A                                  7 73'|'
     A                                  7118'|'
     A                                  8  7'|'
     A                                  8 55'|Blanketter(3)|'
     A                                  8 73'|'
     A                                  8118'|'
     A                                  9  7'|'
     A                                  9  9'Vår ref:'
     A            THAVD          4  0   9 18EDTCDE(Z)
     A                                  9 22'/'
     A            THTDN          7  0   9 23EDTCDE(Z)
     A                                  9 55'|'
     A            XSIDEN         3  0   9 57EDTCDE(Z)
     A                                  9 62'|'
     A            XSIDET         3  0   9 64EDTCDE(Z)
     A                                  9 69'|'
     A                                  9 73'|'
     A                                  9118'|'
     A                                 10  7'|---------------------------------'
     A                                 10 41'----------------------------------'
     A                                 10 75'----------------------------------'
     A                                 10109'---------|'
     A                                 11  7'|V.p.(32)'
     A                                 11 16'|Kolli, pakketype, ant. enhet/stk,-
     A                                       merke&nr(31/1)'
     A                                 11 65'|'
     A                                 11 66'Varebeskrivelse'
     A                                 11118'|'
     A                                 11  7' _________________________________'
     A                                 11 41'__________________________________'
     A                                 11 75'__________________________________'
     A                                 11109'_________ '
     A                                 12  7'|Avsender/eksportør (2)'
     A                                 12 65'|'
     A                                 12 66'Mottaker (8)'
     A                                 12118'|'
     A                                 12  7' _________________________________'
     A                                 12 41'__________________________________'
     A                                 12 75'__________________________________'
     A                                 12109'_________ '
     A                                 13  7'|Avsender (sikkerhet) (S04)'
     A                                 13 65'|'
     A                                 13 66'Mottaker (sikkerhet) (S06)'
     A                                 13118'|'
     A                                 13  7' _________________________________'
     A                                 13 41'__________________________________'
     A                                 13 75'__________________________________'
     A                                 13109'_________ '
     A                                 14  7'|Fremlagte dokumenter og sertifika-
     A                                      ter (44/1)'
     A                                 14 65'|'
     A                                 14 66'Summarisk deklarasjon/Tidligere do-
     A                                      kumenter (40)'
     A                                 14118'|'
     A                                 14 66'__________________________________'
     A                                 14100'__________________ '
     A                                 15  7'|'
     A                                 15 65'|'
     A                                 15 66'Spesielle merknader (44/2)'
     A                                 15118'|'
     A                                 15  7' _________________________________'
     A                                 15 41'__________________________________'
     A                                 15 75'__________________________________'
     A                                 15109'_________ '
     A                                 16  7'|Transportens id. og nasjonalitet -
     A                                      ved grensepassering (18)'
     A                                 16 65'|'
     A                                 16 66'Referensenummer (7)'
     A                                 16118'|'
     A                                 16  7' _________________________________'
     A                                 16 41'__________________________________'
     A                                 16 75'__________________________________'
     A                                 16109'_________ '
     A                                 17  7'|Det aktive trans. id. og nasjonal-
     A                                      itet ved gr.passerin(21)'
     A                                 17 65'|'
     A                                 17 66'Containernummer (31/3)'
     A                                 17 97'|'
     A                                 17 98'Forsegling(S28)'
     A                                 17118'|'
     A                                 17  7' _________________________________'
     A                                 17 41'__________________________________'
     A                                 17 75'__________________________________'
     A                                 17109'_________ '
     A                                 18  7'|Lossested (S18)'
     A                                 18 65'|'
     A                                 18 66'Varenummer (33)'
     A                                 18102'|'
     A                                 18103'Br.vekt kg (35)'
     A                                 18118'|'
     A                                 18  7' _________________________________'
     A                                 18 41'__________________________________'
     A                                 18 75'__________________________________'
     A                                 18109'_________ '
     A                                 19  7'|UNDG(44/4)'
     A                                 19 18'|Mengde f.vare(31)'
     A                                 19 36'|Kode f.vare(31)'
     A                                 19 52'|Bet.tr.kost.'
     A                                 19 65'|'
     A                                 19 66'Dekl.(1)'
     A                                 19 74'|Avs.land(15a)'
     A                                 19 88'|Best.land(17)'
     A                                 19102'|'
     A                                 19103'Br.vekt kg (35)'
     A                                 19118'|'
     A                                 19  7' _________________________________'
     A                                 19 41'__________________________________'
     A                                 19 75'__________________________________'
     A                                 19109'_________ '
     A                                 20  7' _________________________________'
     A                                 20 41'__________________________________'
     A                                 20 75'__________________________________'
     A                                 20109'_________ '
      *
     A          R DKX014FV2                 SPACEB(1)
     A                                     7'|'
     A N98        XLN            4  0     10EDTCDE(Z)
     A                                    16'|'
     A N98        TVNT           5  0     20EDTCDE(Z)
     A N98                                25','
     A N98        TVEH           2        28
     A N98                                30','
     A N98        TVNTEH         5  0     33EDTCDE(Z)
     A N98                                40','
     A            TVMN          42        41
     A                                    65'|'
     A            TVVT          48        67
     A                                   118'|'
      *
     A          R DKX014FV3
     A                                     7'____________________________'
     A                                    35'____________________________'
     A                                    63'____________________________'
     A                                    91'____________________________'
      *
     A          R DKX014FV4                 SPACEB(1)
      * TIN-KODE + NAVN
     A                                     7'|'
     A            TVS1          50         9
     A                                    65'|'
     A            TVK1          50        67
     A                                   118'|'
      *
     A          R DKX014FV5                 SPACEB(1)
     A                                     7'|'
      * ADRESSE 1 + LANDKODE
     A            TVS2          50         9
     A                                    65'|'
     A            TVK2          50        67
     A                                   118'|'
     A                                      SPACEA(1)
     A                                     7'|'
      * POSTNR + POSTSTED
     A            TVS3          50         9
     A                                    65'|'
     A            TVK3          50        67
     A                                   118'|'
      *
     A          R DKX014FV6                 SPACEB(1)
      * TIN-KODE + NAVN
     A                                     7'|'
     A            TVSS1         50         9
     A                                    65'|'
     A            TVKS1         50        67
     A                                   118'|'
      *
     A          R DKX014FV7                 SPACEB(1)
      * ADRESSE 1 + LANDKODE
     A                                     7'|'
     A            TVSS2         50         9
     A                                    65'|'
     A            TVKS2         50        67
     A                                   118'|'
     A                                      SPACEA(1)
      * POSTNR + POSTSTED
     A                                     7'|'
     A            TVSS3         50         9
     A                                    65'|'
     A            TVKS3         50        67
     A                                   118'|'
      *
     A          R DKX014FV8                 SPACEB(1)
     A                                     7'|'
     A            XR441         50         9
     A                                    65'|'
     A            XR401         52        66
     A                                   118'|'
      *
     A          R DKX014FV8A
     A                                    66'__________________________________-
     A                                      __________________'
     A                                      SPACEA(1)
     A                                     7'|'
     A                                    65'|'
     A                                   118'|'
      *
     A          R DKX014FV8B
     A                                    66'__________________________________-
     A                                      __________________'
      *
     A          R DKX014FV9                 SPACEB(1)
     A                                     7'|'
     A            THTAID        27         9
     A            THTALK         2        39
     A                                    65'|'
      *           TVKREF        70
     A            XVKREF        50        67
     A                                   118'|'
      *
     A          R DKX014FVA                 SPACEB(1)
     A                                     7'|'
     A            XHTGID        27         9
     A            XHTGLK         2        39
     A                                    65'|'
     A            XVCNR         17        67
     A                                    97'|'
     A            XHDFKD        20        98
     A                                   118'|'
      *
     A          R DKX014FVB                 SPACEB(1)
     A                                     7'|'
     A            THLOSD        35         9
     A                                    65'|'
     A N27        TVVNT          6  0     67
     A  27                                67'--------'
     A                                   102'|'
     A            TVVKTB        11  3    103EDTCDE(4)
     A                                   118'|'
      *
     A          R DKX014FVC                 SPACEB(1)
     A                                     7'|'
     A            TVFGKD         5         9
     A                                    18'|'
     A            TVFVNT        11  3     20EDTWRD('        ,   ')
     A                                    36'|'
     A            TVFV           2        38
     A                                    52'|'
     A            THTKBM         1        53
     A                                    65'|'
     A            TVDK           5        67
     A                                    74'|'
     A            TVALK          2        76
     A                                    88'|'
     A            TVBLK          2        90
     A                                   102'|'
     A            TVVKTN        11  3    103EDTCDE(4)
     A                                   118'|'
      *
     A          R DKX014FVD
     A            PVSFM3        75     64  2
