     A                                      REF(HEADF)
      *________
      * Heading
      *""""""""
     A          R SYCASS1PA                 SKIPB(2)
     A                                     1'CASS FLYAVREGNING'
     A                                    83DATE EDTCDE(Y)
     A                                    92TIME EDTWRD('0 :  :  ')
     A                                   102'SYCASS1R Side:'
     A                                    +1PAGNBR EDTCDE(Z)
      *
     A                                     1'FAKTURANUMMER:'
     A                                      SPACEB(2)
     A            KAFNR         13        16
     A                                    50'BILAGSNUMMER:'
     A            KABNR          7S 0     64EDTCDE(Z)
      *
     A                                    22'-PREPAID--'
     A                                      SPACEB(2)
     A                                    69'---SYSPED----'
      *
     A                                    14'ORGDES'
     A                                      SPACEB(1)
     A                                    22'WEIGHT_VAL'
     A                                    34'-COLLECT'
     A                                    48'COMMIS'
     A                                    59'AMOUNT'
     A                                    69'AVD'
     A                                    97'BUDSJETT'
     A                                   107'DIFFERANSE'
      *
     A                                     1'AWB-NO'
     A                                      SPACEB(1)
     A                                    14'WEIGHT'
     A                                    24'OTH_AIRL'
     A                                    34'OTH_AGENT'
     A                                    45'INCENTIVE'
     A                                    62'VAT'
     A                                    69'OPD'
     A                                    75'MERKNADER'
     A                                    99'FAKTURERT'
     A                                   110'DIFFERANSE'
      *_____________
      * Detaljlinjer
      *"""""""""""""
     A          R SYCASS1PB                 SPACEB(2)
     A            MAIRPR         3S 0      1
     A                                     4'-'
     A            MAWBNO         8S 0      5
     A            MORIGN         3A       14
     A            MDEST          3A       17
     A            WWHGCP         9S 2     22EDTWRD('       , 0-')
     A            WDACHC         9S 2     33EDTWRD('       , 0-')
     A            WDISC          9S 2     44EDTWRD('       , 0-')
     A            WKABL          9S 2     55EDTWRD('       , 0-')
     A            WEAVD          4S 0     68EDTCDE(Z)
     A            WMARK1        20        75
     A            WBUDSJ         9S 2     96EDTWRD('       , 0-')
     A            WBUDSD         9S 2    107EDTWRD('       , 0-')
      *
     A            WWHGGR         6S 1     13EDTWRD('     ,0')
     A                                      SPACEB(1)
     A            WDCCHP         9S 2     22EDTWRD('       , 0-')
     A            WDISC2         9S 2     44EDTWRD('       , 0-')
     A            WTAXD          9S 2     55EDTWRD('       , 0-')
     A            WEOPD          7S 0     67EDTCDE(Z)
     A            WMARK2        20        75
     A            WFAKT          9S 2     96EDTWRD('       , 0-')
     A            WFAKTD         9S 2    107EDTWRD('       , 0-')
      *________________________
      * SUMLINJE PER FLYSELSKAP
      *""""""""""""""""""""""""
     A          R SYCASS1PC                 SPACEB(2)
     A            WAIRPR         3S 0      1
     A            WK1            9S 2     55EDTWRD('       , 0-')
     A            WB1            9S 2     96EDTWRD('       , 0-')
     A            WB2            9S 2    107EDTWRD('       , 0-')
      *
     A            WF1            9S 2     96EDTWRD('       , 0-')
     A                                      SPACEB(1)
     A            WF2            9S 2    107EDTWRD('       , 0-')
     A                                      SPACEA(2)
      *____________
      * Last Record
      *""""""""""""
     A          R SYCASS1PØ                 SPACEB(2)
     A                                     2'----------------------------------+
     A                                      --------   S i s t e   s i d e n   +
     A                                      -----------------------------------+
     A                                      ----------------'
