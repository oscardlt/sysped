*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
10268 * 02 CB  PTF10 Duplikatkontroll på Agentreferanse (AWB-nr)
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFI10PNL)
     A                                      HLPTITLE('OPPDRAGSREGISTRERING FLYF-
     A                                      RAKT IMPORT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CA12(12 'Avslutt program')
     A                                      CF04(04 'Kopierer data fra et oppdr-
     A                                      ag')
     A                                      CF05(05 'Gå direkte til faktura')
     A                                      CF09(09 'Bytt signatur')
     A                                      CF07(07 'SPØRRING OPPDRAG')
     A                                      CF20(20 'Gå til Import AWB')
     A          H                           HLPPNLGRP(HLPSCR SYFI10PNL)
     A                                      HLPARA(01 001 24 080)
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFI10'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV0596 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2  3MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            UFIRM          2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 19MSGCON(045 DIV0597 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 68TIME
     A                                  3 19MSGCON(045 DIV0598 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(018 DIV0599 *LIBL/SYMSGF)
     A            WSSG           3A  B  9 23TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 11  2MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 23TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13  2MSGCON(018 DIV0601 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 13 23TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N96                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 38MSGCON(020 DIV0604 *LIBL/SYMSGF)
     A                                 15  2MSGCON(018 DIV0602 *LIBL/SYMSGF)
     A            HEAVD          4Y 0B 15 23TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            HEOPD          7Y 0B 15 30TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 15 38MSGCON(020 DIV0036 *LIBL/SYMSGF)
     A                                 18  2MSGCON(018 DIV0603 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 18 23COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSMSG         62A  O 20 15COLOR(RED)
     A                                      DSPATR(BL)
     A                                 22  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(018 DIV0605 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 42MSGCON(018 DIV0033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 62MSGCON(018 DIV0606 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 BBA0041 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R BILDE02
     A N31                                  CF03(03 'Avslutt registrering av op-
     A                                      pdrag')
     A                                      CF04(04 'SØKING')
     A N31                                  CF05(05 'Gå til faktura')
     A                                      CF06(06 'BERGEN M3')
     A N31                                  CF16(16 'Neste')
     A                                      CF07(07 'SPØRRING PÅ OPPDRAG')
     A N31                                  CF09(09 'Ny tur')
     A                                      CF11(11 'DUP-OPPDRAG')
     A                                      CA12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CF17(17 'ARKIV   ')
     A                                      CF18(18 'Freetext')
     A                                      CF15(15 'FrI SØK')
     A  27                                  ALARM
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SYFI10010 SYFI10PNL)
     A  *DS3                                HLPARA(01 001 01 080)
     A  *DS4                                HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(SYFI10015 SYFI10PNL)
     A  *DS3                                HLPARA(02 001 02 080)
     A  *DS4                                HLPARA(02 001 02 080)
     A          H                           HLPPNLGRP(SYFI10020 SYFI10PNL)
     A  *DS3                                HLPARA(03 001 03 033)
     A  *DS4                                HLPARA(03 001 03 033)
     A          H                           HLPPNLGRP(SYFI10025 SYFI10PNL)
     A  *DS3                                HLPARA(03 034 03 080)
     A  *DS4                                HLPARA(03 034 03 080)
     A          H                           HLPPNLGRP(SYFI10030 SYFI10PNL)
     A  *DS3                                HLPARA(04 001 04 080)
     A  *DS4                                HLPARA(04 001 04 080)
     A          H                           HLPPNLGRP(SYFI10035 SYFI10PNL)
     A  *DS3                                HLPARA(05 001 05 080)
     A  *DS4                                HLPARA(05 001 05 080)
     A          H                           HLPPNLGRP(SYFI10040 SYFI10PNL)
     A  *DS3                                HLPARA(07 001 07 023)
     A  *DS4                                HLPARA(07 001 07 023)
     A          H                           HLPPNLGRP(SYFI10045 SYFI10PNL)
     A  *DS3                                HLPARA(07 024 07 080)
     A  *DS4                                HLPARA(07 024 07 080)
     A          H                           HLPPNLGRP(SYFI10050 SYFI10PNL)
     A  *DS3                                HLPARA(08 001 10 080)
     A  *DS4                                HLPARA(08 001 10 080)
     A          H                           HLPPNLGRP(SYFI10055 SYFI10PNL)
     A  *DS3                                HLPARA(11 001 14 080)
     A  *DS4                                HLPARA(11 001 14 080)
     A          H                           HLPPNLGRP(SYFI10060 SYFI10PNL)
     A  *DS3                                HLPARA(16 001 16 032)
     A  *DS4                                HLPARA(16 001 16 032)
     A          H                           HLPPNLGRP(SYFI10065 SYFI10PNL)
     A  *DS3                                HLPARA(16 033 16 080)
     A  *DS4                                HLPARA(16 033 16 080)
     A          H                           HLPPNLGRP(SYFI10070 SYFI10PNL)
     A  *DS3                                HLPARA(18 001 18 080)
     A  *DS4                                HLPARA(18 001 18 080)
     A          H                           HLPPNLGRP(SYFI10075 SYFI10PNL)
     A  *DS3                                HLPARA(19 001 19 080)
     A  *DS4                                HLPARA(19 001 19 080)
     A          H                           HLPPNLGRP(SYFI10080 SYFI10PNL)
     A  *DS3                                HLPARA(20 001 20 080)
     A  *DS4                                HLPARA(20 001 20 080)
     A          H                           HLPPNLGRP(SYFI10085 SYFI10PNL)
     A  *DS3                                HLPARA(21 001 21 036)
     A  *DS4                                HLPARA(21 001 21 036)
     A          H                           HLPPNLGRP(SYFI10090 SYFI10PNL)
     A  *DS3                                HLPARA(21 037 21 080)
     A  *DS4                                HLPARA(21 037 21 080)
     A          H                           HLPPNLGRP(SYFI10095 SYFI10PNL)
     A  *DS3                                HLPARA(22 001 22 080)
     A  *DS4                                HLPARA(22 001 22 080)
     A          H                           HLPPNLGRP(SYFI10999 SYFI10PNL)
     A  *DS3                                HLPARA(23 001 24 080)
     A  *DS4                                HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFI10'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(020 DIV0070 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 46MSGCON(004 DIV0071 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  1 51TEXT('Avdeling')
     A N58                                  DSPATR(PR)
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A  58                              1 56MSGCON(005 YBC0035 *LIBL/SYMSGF)
     A                                      DSPATR(RI)
     A                                      COLOR(YLW)
     A N58        WSOPD          7Y 0O  1 56TEXT('Oppdragsnummer')
     A N29                                  DSPATR(BL)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 64MSGCON(003 DIV0072 *LIBL/SYMSGF)
     A            WSSG           3A  B  1 68TEXT('Signatur')
     A                                      COLOR(YLW)
     A                                  1 72MSGCON(004 DIV0073 *LIBL/SYMSGF)
     A            WSSTD          4Y 0B  1 77TEXT('Kostnadssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2  2MSGCON(005 DIV0074 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  2  8TEXT('Prosjektnummer')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 17'P'
     A            WSPOS1         7A  B  2 19TEXT('Posisjon-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPOS2         4Y 0B  2 27TEXT('Posisjon-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 32MSGCON(002 DIV0076 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  2 35DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 42MSGCON(002 DIV0077 *LIBL/SYMSGF)
     A            WSGN          15A  B  2 45TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 61MSGCON(004 DIV0078 *LIBL/SYMSGF)
     A            WSRFA         15A  B  2 66TEXT('Referanse Agent')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(004 DIV0079 *LIBL/SYMSGF)
     A            WSLKA          2A  B  3  8TEXT('Landkode')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSDF          5A  B  3 12TEXT('Frakt fra')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
S01  A*           WSLKS          2A  O  9 73COLOR(WHT)
     A                                  3 18MSGCON(004 DIV0080 *LIBL/SYMSGF)
     A            FIRLK          2   O  3 23COLOR(WHT)
     A            WSSDT          5A  B  3 27TEXT('Frakt til')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVALT         3   B  3 34
     A                                      DSPATR(UL)
     A  54                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVALP         3  1B  3 38DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
S01  A*           WSLKK          2A  O 13 73COLOR(WHT)
     A                                  3 43MSGCON(003 DIV0081 *LIBL/SYMSGF)
     A            WSHAWB         9A  B  3 47TEXT('House Airway Billno')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 57MSGCON(003 DIV0083 *LIBL/SYMSGF)
     A            WSSDL         20A  B  3 61TEXT('Lossested')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  4  2MSGCON(005 DIV0084 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  4  8TEXT('Kundenr Agent')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  75                                  DSPATR(BL)
     A  75                                  COLOR(RED)
     A N75                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAA         30A  O  4 17TEXT('Navn Agent')
     A  75                                  DSPATR(BL)
     A  75                                  COLOR(RED)
     A N75                                  COLOR(WHT)
     A                                  4 48MSGCON(005 DIV0085 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  4 54TEXT('Kundenr Transportør')
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTRNV        18A  O  4 63COLOR(WHT)
     A                                  5  2MSGCON(005 DIV0086 *LIBL/SYMSGF)
     A            WSTRM          2Y 0B  5  8TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5 12MSGCON(008 DIV0087 *LIBL/SYMSGF)
     A            WSTRI          2A  B  5 21TEXT('Lankode Transport')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5 25MSGCON(005 DIV0088 *LIBL/SYMSGF)
     A            WSTRC          1Y 0B  5 31TEXT('Transport cont')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1)
     A                                  5 34MSGCON(008 DIV0089 *LIBL/SYMSGF)
     A            WSTRCN        17A  B  5 43TEXT('Transport cnr')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(005 SHU0316 *LIBL/SYMSGF)
     A            WSKDAK         1A  B  6  8COLOR(YLW)
     A            WSKNO          8Y 0B  6 10TEXT('Kundenr OPD.GIV')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  56                                  DSPATR(BL)
     A  56                                  COLOR(RED)
     A N56                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAO         30A  O  6 19TEXT('NAVN OPD.GIV')
     A  56                                  DSPATR(BL)
     A  56                                  COLOR(RED)
     A N56                                  COLOR(WHT)
     A                                  7  2MSGCON(004 DIV0090 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  7  8TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  67
     AO 68                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  77                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A  77                                  COLOR(RED)
     A N77                                  COLOR(YLW)
     A                                  7 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANS          1Y 0B  7 22TEXT('Avtalenr Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFS         1A  B  7 27TEXT('Fakturakode Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                  7 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALS         3A  B  7 33TEXT('Valutakode Selger')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNSF         8Y 0B  7 42TEXT('Kundenr Selg.fakm')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  99                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A            WSNASF        30A  O  7 51TEXT('Navn Selg.fakm')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(WHT)
     A                                  8  2MSGCON(004 DIV0095 *LIBL/SYMSGF)
     A            WSNAS         30A  B  8  8TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  77                                  DSPATR(BL)
     A  77                                  COLOR(RED)
     A N77                                  COLOR(YLW)
     A            WSADS1        30A  B  8 42TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A            WSADS2        30A  B  9  8TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS3        30A  B  9 42TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSEDIINF      15A  O 10 62COLOR(RED)
     A                                 10  2MSGCON(012 DIV0592 *LIBL/SYMSGF)
     A            WSLKS          2A  B 10 19COLOR(YLW)
     A            WSSDFF         5A  B 10 22TEXT('Forfrakt fra')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPNFR        30A  B 10 29COLOR(YLW)
     A                                 11  2MSGCON(004 DIV0096 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 11  8TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  62
     AO 61                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  76                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A  76                                  COLOR(RED)
     A N76                                  COLOR(YLW)
     A                                 11 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANK          1Y 0B 11 22TEXT('Avtalenr Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFK         1A  B 11 27TEXT('Fakturakode Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                 11 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALK         3A  B 11 33TEXT('Valutakode Kjøper')
     A                                      DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNKF         8Y 0B 11 42TEXT('Kundenr Kjøp.fakm')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  98                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(YLW)
     A            WSNAKF        30A  O 11 51TEXT('Navn Kjøp.fakm')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(WHT)
     A                                 12  2MSGCON(004 DIV0097 *LIBL/SYMSGF)
     A            WSNAK         30A  B 12  8TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  76                                  DSPATR(BL)
     A  76                                  COLOR(RED)
     A N76                                  COLOR(YLW)
     A            WSADK1        30A  B 12 42TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(YLW)
     A            WSADK2        30A  B 13  8TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 13 42TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14  2MSGCON(015 DIV0593 *LIBL/SYMSGF)
     A            WSLKK          2A  B 14 19COLOR(YLW)
     A            WSSDVT         5A  B 14 22TEXT('Viderefrakt til')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPNTI        30A  B 14 29COLOR(YLW)
     A                                 15  2MSGCON(075 DIV0098 *LIBL/SYMSGF)
     A                                 15  2MSGCON(077 DIV0591 *LIBL/SYMSGF)
     A            WSOT           2A  B 16  2TEXT('Oppdragstype')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFR           3A  B 16  5TEXT('Frankatur')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSGM1         15A  B 16  9TEXT('Godsmerking-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNT           7Y 0B 16 25TEXT('Antall')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVS1         25A  B 16 33TEXT('Vareslag-1')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVKT          9Y 0B 16 59TEXT('Vekt')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSFBV          9Y 0B 16 69TEXT('Fraktberegn.vekt')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSGM2         15A  B 17  9TEXT('Godsmerking-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS2         25A  B 17 33TEXT('Vareslag-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 65'M3:'
     A            WSM3           7Y 3B 17 69TEXT('Kubikk')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                 18  2MSGCON(006 SHU0150 *LIBL/SYMSGF)
     A            WSKF           2A  B 18  9DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKFT1         1A  B 18 12DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFT1A        30A  B 18 15DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFT1B        15A  B 18 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSUSR         10A  O 18 62COLOR(PNK)
     A                                 18 73MSGCON(006 YBC0489 *LIBL/SYMSGF)
     A            WSKDPL         1A  B 18 80TEXT('Planleggingskode')
     A                                      DSPATR(UL)
     A  55                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19  2MSGCON(015 DIV0101 *LIBL/SYMSGF)
     A                                 19 18MSGCON(009 DIV0116 *LIBL/SYMSGF)
     A            WSPK9          1A  B 19 28TEXT('Jetpakke')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 19 30MSGCON(005 DIV0100 *LIBL/SYMSGF)
     A            WSKDL         15A  B 19 36TEXT('Lagerkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19 52MSGCON(008 DIV5022 *LIBL/SYMSGF)
     A            WSVERV         3A  B 19 61COLOR(YLW)
     A  71                                  DSPATR(RI)
     A            WSVERB        13Y 2B 19 65EDTWRD('           ,  ')
     A                                      COLOR(YLW)
     A            WSPK1          1A  B 20  2TEXT('Printerkode-1')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK2          1A  B 20  6TEXT('Printerkode-2')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK3          1A  B 20  9TEXT('Printerkode-3')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK4          1A  B 20 12TEXT('Printerkode-4')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK5          1A  B 20 15TEXT('Printerkode-5')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P' 'S')
     A                                 20 53'Dup av:'
     A            WSAVD0         4Y 0B 20 61TEXT('Avdeling')
     A N58                                  DSPATR(PR)
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 20 66'/'
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A            WSOPD0         7Y 0B 20 68TEXT('Avdeling')
     A N58                                  DSPATR(PR)
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
S01  A*           W2TOLL         2Y 0B 21  2TEXT('Tollsted')
     A            W2TOLL         4Y 0B 21  2TEXT('Tollsted')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
S01  A*                                21  6MSGCON(011 DIV0104 *LIBL/SYMSGF)
     A                                 21  7MSGCON(011 DIV0104 *LIBL/SYMSGF)
     A            W2KDME         1A  B 21 20TEXT('Merkelapp  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'F' 'X' 'L' 'A' 'B')
     A                                 21 22MSGCON(014 DIV0105 *LIBL/SYMSGF)
     A            W2KDE          1A  B 21 40TEXT('Etterkrav  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 21 42MSGCON(011 DIV0106 *LIBL/SYMSGF)
     A            WSNTVS         3Y 0B 21 56TEXT('Antall varepos SAD')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 21 60MSGCON(011 DIV0107 *LIBL/SYMSGF)
     A                                 22  2MSGCON(007 DIV0607 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 22 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSMSG         62A  O 22 19COLOR(RED)
     A                                      DSPATR(BL)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
N02  A          R BILDEAW
N02  A                                      WINDOW(*DFT 4 32 *NOMSGLIN)
N02  A                                      OVERLAY
N02  A                                  1  2'SYFI10FM'
N02  A                                      COLOR(BLU)
N02  A                                  2  2'AWB Nr. finnes fra før.'
N02  A                                      COLOR(WHT)
N02  A                                  3  2'AWB:'
N02  A            WSRFA         15A  O  3  7
N02  A                                  4  2'Vil du beholde dette ?'
N02  A                                      COLOR(WHT)
N02  A            WSJNAW         1A  B  4 25DSPATR(UL)
N02  A                                  4 27'(J/N)'
N02  A          R WINDOWSZ
N02  A                                      WINDOW(5 30 5 30 *NOMSGLIN)
N02  A                                      OVERLAY
