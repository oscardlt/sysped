      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESARKA1 MODULE(*LIBL/ESARKA1 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
ODDEV *ODDEV   PTF9 ODDEVEN-logikk
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 ved dato-les - kun poster med 7 siffer i ARUNDE (finnes også T:...)
10XXX * 02 JOV PTF10 dersom tillatt (hardkodet) vis også arkivert spesifikasjon
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     Farkivl04  IF   E           K DISK    usropn
     Farkivl05  IF   E           K DISK    usropn
     F                                     RENAME(ARKIVR:ARKI05R)
     FFIRMARC   IF   E           K DISK    usropn
     FTRAVH3    IF   E           K DISK    usropn
     FTRAVV6    IF   E           K DISK    usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D user            s             10
     D DATO            s             10
      *
     D FraDT8          s              8  0
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
S01  D*FakNr           s              6
S01  D*FakNrN          s              6  0
S01  D*FakNrA          s              6
N01  D FakNr           s              7
N01  D FakNrN          s              7  0
N01  D FakNrA          s              7
     D TraN            s              8
     D TraNN           s              8  0
     D ArGod           s             40
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  ARUNDE                 1     10
     D  ARfak                  1      7
N01  D  ARUNDE1_2              1      2
     D                 DS
     D  ArDate                 1      8  0
     D  ARdtÅ                  1      4  0
     D  ARdtM                  5      6  0
     D  ARdtD                  7      8  0
      * dummy slik at ARLINK er definer (kommer i PTF)
     D                 DS
     D  Wdata                  1    220
     D  ARLINK                57     96
     D                 DS
     D  XXDATO                 1      8  0
     D  XXDATOD                1      2  0
     D  XXDATOM                3      4  0
     D  XXDATOÅ                5      8  0
     D                 DS
     D  DATON                  1      6  0
     D  DATOND                 1      2  0
     D  DATONM                 3      4  0
     D  DATONÅ                 5      6  0
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
N02   * Vis spesifikasjon ?? foreløpig hardkodet..
N02   * HER KAN en utvide med parameter på firmanivå - som evt spiller på transportør-gruppe
N02   *         og/eller parameter på user-nivå
N02  c*                  move      'N'           VisSpes           1
N02  c                   move      'J'           VisSpes           1
      * Vis liste over arkivposter leveringer
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      * les på type ta - via dato/stigende nr(ArkivL05) eller spesifikt på ett nummer (ArkivL04)
     c                   Move      'ta'          ARTYPE
     c     StrLoop1      tag
     c                   Move      FraDt8        ARDATE
     c                   Move      *blanks       ARUNDE
     C     FakNrN        Ifne      *zeros
     c                   MoveL     FakNrN        ARUNDE
     c                   end
      *
     C                   select
     C     FakNrN        Whenne    *zeros

     C     FakKey        SETLL     ARKIVL04
     c     FakKey        Reade     ARKIVL04                               35
     c                   other
     c     FraKey        setLL     ARKIVL05
     c     FrakeyE       ReadE     ARKIVL05                               35
     c                   endsl
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      * diverse filter legges her..
     C                   move      *zeros        THTNR             8 0
N02  c     ARSTAT        cabne     'A'           NextRec
N02  c     ARLINK        cabeq     *blanks       NextRec
N01  c     ARUNDE1_2     cabeq     'T:'          NextRec
     C                   move      arfak         THFakt
     C     TravHKey      Chain     TRAVH3                             33
     c     *IN33         IFEQ      '1'
     C     THFAKT        Chain     TRAVV6                             33
     c     *IN33         IFEQ      '0'
     c     TVTAVD        ANDEQ     *zeros
     c                   move      TVTUNR        THTNR
     c                   end
     c                   end
     C     *in33         IFEQ      '0'
     C     TRANN         ANDNE     *ZEROS
     C     TRANN         CABne     THTNR         NextRec
     c                   end
      *
      *
      * Ok - skriv
      * (OM krav om sortering kommer opp kan en her skrive til WRKCGI
      *  her og deretter hente inn igjen / skrive til browser sortert)
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
      *
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
      *
     c     NextRec       Tag
S01  C*                  Move      '.'           ARUNDE
     C                   select
     C     FakNrN        Whenne    *zeros
S01  C**   FakKey        SETLL     ARKIVL04
     c     FakKey        Reade     ARKIVL04                               35
     c                   other
      * manipuler Utype slik at alle poster med samme(s.fakt) nr hoppes over
S01  c**   FraKey        setLL     ARKIVL05
     c     FrakeyE       ReadE     ARKIVL05                               35
     c                   endsl
     c                   end
      *
      *
     C     UTLES         TAG
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      DATO     = zhbgetvar('DATO')
     C                   eval      DATON    = c2n2(DATO)
     C                   eval      FakNr    = zhbgetvar('FakNr')
     C                   eval      FakNrN   = c2n2(FakNr)
     C                   eval      TRAN     = zhbgetvar('TRAN')
     C                   eval      TRANN    = c2n2(TRAN)
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
     c     MaxSnd        ifeq      *zeros
     c                   z-add     100           MaxSnd
     c                   end
     c     MaxRcd        ifeq      *zeros
     c                   z-add     1000          MaxRcd
     c                   end
      * Userid.....
     C                   eval      user = zhbgetvar('user')
      * LÅNER ARDTOP for datokonvertering
     C     2000          Add       DATONÅ        ARDtÅ
     C                   move      DATONM        ARDtM
     C                   move      DATOND        ARDtD
     C                   move      ARDate        FraDt8
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     cundl01
     C                   CLOSE     arkivl04
     C                   CLOSE     arkivl05
     C                   CLOSE     firmarc
     C                   CLOSE     travh3
     C                   CLOSE     travv6
     C*                  return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESARKA1':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
      * Søkeparam
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(DATON:'  .  .  ')))
     C                   callp     updHTMLvar('FakNr':FakNr)
     C                   callp     updHtmlVar('TRAN':
     C                             %trim(%editc(TRANN:'Z')))
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     updHTMLvar('ARUNDE':ARUNDE)
     C                   callp     updHtmlVar('TUTRAN':
     C                             %trim(%editc(THTNR:'Z')))
     C                   move      ARdtÅ         XXDATOÅ
     C                   move      ARdtM         XXDATOM
     C                   move      ARdtD         XXDATOD
     C                   callp     updHTMLvar('XXDATO':
     C                             %trim(%editw(XXDATO:'  .  .    ')))
     C                   exsr      LagPDFURL
     C                   callp     updHTMLvar('UPDF':UPDF)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
      *
     C                   OPEN      Cundl01
     C                   OPEN      arkivl04
     C                   OPEN      arkivl05
     C                   OPEN      firmarc
     C                   OPEN      travh3
     C                   OPEN      travv6
      *
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     FakKey        KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C                   KFLD                    arunde
     C     FraKey        KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C                   KFLD                    ardate
S01  C*                  KFLD                    arunde
     C     FraKeyE       KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C     TravHKey      KLIST
     C                   KFLD                    THST
     C                   KFLD                    THFAKT
     C                   MOVE      'O'           THST
      *
     C     KunKey        CHAIN     Cundl01                            33
bhr   * Spesialvri for å kunne deme uten å avsløre kundenavn
bhr  c                   MOVEL     USER          tstjov            4
bhr  c     tstjov        ifeq      'JOVO'
bhr  c                   eval      KNAVN  = 'Jan Ottar Valderhaug'
bhr  c                   end
      *
     c                   read      firmarc                                41
      *
     C                   endsr
      *
     C***********************************************
     C     LagPDFURL     BEGSR
     C***********************************************
     C* genererer bane for lagring (0 i avd/opd ved samlefakt..)
     C                   MOVE      *ZEROS        AAVD              4
     C                   MOVE      *ZEROS        AOPD              7
     C                   move      *blanks       UPDF
s02  c*                  movel     '<a href="'   UPDF            200
N02  c                   movel     '<a href="'   UPDF            500
     C     UPDF          CAT       '/':0         UPDF
     C     UPDF          CAT       arcane:0      UPDF
     C     UPDF          CAT       '/':0         UPDF
     C     ARLINK        IFNE      *Blanks
     C     UPDF          CAT       ArLink:0      UPDF
     c                   else
     C     UPDF          CAT       aavd:0        UPDF
     C     UPDF          CAT       aopd:0        UPDF
     C     UPDF          CAT       artype:0      UPDF
     C     UPDF          CAT       arunde:0      UPDF
     c                   end
     C     '.pdf'        SCAN      updf                                   33
     C  N33'.PDF'        SCAN      updf                                   33
     C     *in33         ifeq      *off
     C     UPDF          CAT       '.pdf" ':0    UPDF
     C                   else
     C     UPDF          CAT       '" ':0        UPDF
     C                   end
s02  C*                  CAT       'target="Sy':0UPDF
N02  C                   CAT       'target="Sy':1UPDF
     C                   CAT       'PDFWind">':0 UPDF
s02  C*    UPDF          CAT       '<img src="':0UPDF
N02  C     UPDF          CAT       '<img src="':1UPDF
     C     UPDF          CAT       '/syspeddev':0UPDF
     C     UPDF          CAT       '/PDF.gif">':0UPDF
N02  C     UPDF          CAT       '</a>':0      UPDF
      *
N02
N02   * ved vis spesifikasjon - hent evt samsvarende tb , reset key FAKKEY til FORBI behandlede
N02  c     VisSpes       ifeq      'J'
N02  c                   Move      'tb'          ARTYPE
N02  c     FakKey        chain     ARKIVL04                           33
N02  c     *in33         ifeq      *off
N02  c                   move      *blanks       PdfExt            4
N02  C     '.pdf'        SCAN      arlink                                 33
N02  C  N33'.PDF'        SCAN      arlink                                 33
N02  C     *in33         ifeq      *off
N02  c                   move      '.pdf'        PdfExt
N02  c                   endif
N02  c                   eval      UPDF=%trim(UPDF)+'&nbsp;&nbsp;&nbsp;'
N02  c                             +'<a href="/'+%trim(arcane)+'/'
N02  c                             +%trim(arlink)+%trim(PdfExt)+'" '
N02  c                             +'target="SyPDFWind"> '
N02  c                             +'<img src="/syspeddev/binders01.jpg"></a>'
N02  c                   endif
N02   * reset key
N02  c                   Move      'ta'          ARTYPE
N02  c     FakKey        chain     ARKIVL04                           33
N02  c                   endif
     c                   endsr
