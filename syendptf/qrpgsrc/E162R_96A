BHR   * Diverse endringer...
      * OBS!!! BASERT PÅ IFTSTA FTA HSD-> F.MAAS
      *    KODE 41 = UTKJØRT  , KODE 21 = LEVERT (POD...)
      * OBS2!! TAR HØYDE FOR AT EN KAN MOTTA BÅDE POD NAVN OG
      *    URL TIL GRAFISK LAGRET SIGNATUR (BASERT PÅ RAMBERG/GEM SYS)
      *****************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 sendingsnr ligger i RFF for Schenker..
10XXX * 02 JOV PTF10 TA HENSYN TIL EVT SIGN I NAD+DP
10XXX * 03 JOV PTF10 event 48 = Ut/bil avgår i SYSPED (og tollpost men da se ETG01R)
10XXX * 04 JOV PTF10 ÅPNET FOR EFTX... (FOR ZVK - VEKT VOL-JUSTERING)
10172 * 05 SH  PTF10 norsk text til Track and trace
      * 06 jov PTF10 takle avd/oppd i CNI-feltet
      * 07 jov PTF10 endre 06   V 06 - I SÅ FALL LOGGFØR KUN PÅ OPPDRAG (IKKE FB/KOLLI)
      * 08 jov PTF10 UTBEDRING AV 06 - I SÅ FALL LOGGFØR KUN PÅ OPPDRAG (IKKE FB/KOLLI)
      * 09 jov PTF10 Aksepter ogå 401-prfix på sendingsnr
      * 10 jov PTF10 Finne match via FRISoK/IFB + location-Håndtering
      * 11 JOV PTF11 STS 13 = PICKED UP AT CONSIGNOR mm
      * 12 JOV PTF11 FEIL KEY I PTF 10
      * 13 SH  PTF11 URS til Signatur
12XXX * 14 YBC PTF12  Oppdater avd/opd på EDIM
********************************************************************
     H DATEDIT(*YMD)
     FFIRMARC   IF   E           K DISK
     FEDIM      UF   E           K DISK
     F*EUNB    IF  E           K        DISK
     F*EUNH    IF  E           K        DISK
     FECNI96A   IF   E           K DISK
     FESTS96A   IF   E           K DISK
n02  FENAD96A   IF   E           K DISK
     FERFF96A   IF   E           K DISK
     FEDTM96A   IF   E           K DISK
     FEFTX96A   IF   E           K DISK
     FEPCI96A   IF   E           K DISK
N10  FELOC96A   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FDOKUF7    UF   E           K DISK
N10  FFRISOL01  IF   E           K DISK
     FDOKUF     IF   E           K DISK
     F                                     RENAME(DOKUFR:DOKUFRXX)
     FHEADF     UF   E           K DISK
     FHEAD39    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD39R)
     FDOKUFM    UF   E           K DISK
     FDOKUFM1   UF   E           K DISK
     F                                     RENAME(DOKUFMR:DOKUF1R)
     FTRACKF    O  A E           K DISK
     FTRACKT    UF   E           K DISK
     D                 DS
     D  C1004                  1     35
     D  C1004B                11     20
     D  C1004C                18     20
N06  D  C1004_1_4              1      4
N06  D  C1004_5_5              5      5
N06  D  C1004_6_12             6     12
N06  D  C1004_13_13           13     13
     D                 DS
     D  D2380                  1     35
     D  XXDATA                 1      8
     D  XXTIDA                 9     12
     D                 DS
     D  FMMRK1                 1     35
     D  FMCLID                 1     18
     D                 DS
     D  DFMÅL                  1     17
     D  DFDTMO                 1      8
     D  DFKLMO                 9     12
     D  DFSGM                 13     17
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D*                                       1  70 TTTEXT
     D  TTTDS                  1     70
     D  TXDATE                 1      5
     D  XXDATO                 7     14
     D  TXTIME                17     19
     D  XXKLOK                21     24
     D  TXBY                  27     29
BHR   * Kvitterinslinja på PDA kan være inntil 35 tegn...
BHR   * OFRER logging av xxx av xxx kolli - kvitteres på FBR.NIVÅ!
bhr  D* XXSIGN                31     53
bhr  D  XXSIGN                31     65
bhr  D* TXSTKO                54     57
bhr  D* XXSTKO                59     61
bhr  D* ZLEV                  62     64
bhr  D* ZLEV1                 62     62
bhr  D* ZLEV2                 63     63
bhr  D* TXAV                  66     67
bhr  D* ZTOT                  68     70
bhr  D* ZTOT1                 68     68
bhr  D* ZTOT2                 69     69
BHR   * Dummy DS for at feltene skal være definert
bhr  D                 DS
bhr  D  TXSTKO                54     57
bhr  D  XXSTKO                59     61
bhr  D  ZLEV                  62     64
bhr  D  ZLEV1                 62     62
bhr  D  ZLEV2                 63     63
bhr  D  TXAV                  66     67
bhr  D  ZTOT                  68     70
bhr  D  ZTOT1                 68     68
bhr  D  ZTOT2                 69     69
     D                 DS
     D  DFBREF                 1     17
     D  DFBRTX                 1      5
     D  DFBRAV                 6      9
     D  DFBRST                10     10
     D  DFBROP                11     17
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *
N03  C     MMN           CHAIN     EDIM                               33
N03  C     *IN33         IFEQ      '0'
N03  C                   MOVE      'F'           MST
N03  C                   UPDATE    EDIMR
N03  C                   END
      *
     C                   EXSR      STATUS
      *
s03  C*    MMN           CHAIN     EDIM                               33
s03  C*    *IN33         IFEQ      '0'
s03  C*                  MOVE      'F'           MST
s03  C*                  UPDATE    EDIMR
s03  C*                  END
      *
     C     FEIL          TAG
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
      *                                                     STATUS/
      *////////////////////////////////////////////////////////////
     C     STATUS        BEGSR
      *
      * LES ALLE CNI-REPETISJONER
     C     MMN           SETLL     ECNI96A
     C     MMN           READE     ECNI96A                                33
     C     *IN33         DOWEQ     '0'
      *
      * HENT/LOGG DATA FOR EN SENDING
     C                   EXSR      ENCNI
      *
     C     MMN           READE     ECNI96A                                33
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    ENCNI  /
      *////////////////////////////////////////////////////////////
     C     ENCNI         BEGSR
     C                   MOVE      *BLANKS       DFBREF
N01   * Schenker/Linjegods har 9999 som fast CNI løpenr - FRAKTBREVSNR KOMMER I RFF+AAM
N01  C     C1490         IFEQ      '9999'
N01  c                   exsr      GetLGsnd
N01  C                   END
      * FINNES AKTUELL SENDING (som f.brev eller ren innland)
N08  c                   move      'N'           ViaOpd            1
     C                   MOVEL     C1004         SENDNR           35
N09  c                   if        %subst(C1004:1:3)='401'
N09  c                             and %subst(C1004:20:1)<>' '
N09  c                   eval      SENDNR=%subst(C1004:4:17)
N09  c                   endif
     C     SENDNR        CHAIN(N)  DOKUF7                             34
     c     *in34         IFEQ      '0'
     C                   MOVE      DFAVD         HEAVD
     C                   MOVE      DFOPD         HEOPD
N14  C     MMN           CHAIN     EDIM
N14  C                   IF        *IN33 = *OFF
N14  C                   EVAL      MAVD  = HEAVD
N14  C                   EVAL      MTDN  = HEOPD
N14  C                   UPDATE    EDIMR
N14  C                   END
     c                   else
      * Kan finnes som ren innland (hvis 17 langt nr, legg til 401)
N12  C                   MOVEL     SENDNR        SENDNR20         20
     C     C1004B        IFNE      *BLANKS
     C     C1004C        ANDEQ     *BLANKS
     C                   MOVEL     C1004         SEND17           17
S12  c*                  eval      SENDNR = '401'+send17
N12  c                   eval      SENDNR20 = '401'+send17
     C                   END
S12  C*                  MOVEL     SENDNR        SENDNR20         20
     C     SENDNR20      chain     Head39                             34
N14  C                   IF        *IN34 = *OFF
N14  C     MMN           CHAIN     EDIM                               33
N14  C                   IF        *IN33 = *OFF
N14  C                   EVAL      MAVD  = HEAVD
N14  C                   EVAL      MTDN  = HEOPD
N14  C                   UPDATE    EDIMR
N14  C                   END
N14  C                   END
      *
N10   * Kun fri søkevei IFB ??
N10  c     *IN34         ifeq      '1'
N12  C                   move      'IFB'         FSKODE
N10  C     IFBKey        chain     FRISOL01                           34
N10  c  N34              move      FSAVD         HEAVD
N10  c  N34              move      FSOPD         HEOPD
N10  C  N34HEAKEY        CHAIN(N)  HEADF                              34
N10  c  N34              move      'J'           ViaOpd            1
N10  c                   endif
      *
N06   *       sysped-ref som cni ??
N06  c     *IN34         ifeq      '1'
N06  c     C1004_5_5     andeq     '/'
S07  c*    C1004_13_13   andeq     '/'
N06  C                   TESTN                   C1004_1_4            35
N06  C                   TESTN                   C1004_6_12           36
N06  c     *in35         ifeq      '1'
N06  c     *in36         andeq     '1'
N06  c                   move      C1004_1_4     HEAVD
N06  c                   move      C1004_6_12    HEOPD
N06  C     HEAKEY        CHAIN(N)  HEADF                              34
N08  c  N34              move      'J'           ViaOpd            1
N06  c                   endif
N06  c                   endif
     C     *IN34         CABEQ     '1'           NXTCNI
     C  N34              MOVE      HEAVD         DFAVD
     C  N34              MOVE      HEOPD         DFOPD
     C  N34              Z-ADD     1             DFFBNR
     C  N34              Z-ADD     HENT          DFNT
     c                   end
      *
     C     *IN34         CABEQ     '1'           NXTCNI
      *
      * STATUS-EVENT (KODE, NORMALT 22 - SLUTT-LEVERT)
     C     KEY5          CHAIN     ESTS96A                            34
     C     *IN34         CABEQ     '1'           NXTCNI
     C                   MOVEL     S9011         XXSTKO            3
      * SIGNERT AV,,,
     C                   MOVE      *BLANKS       XXSIGN
     C                   MOVEL     'X'           XXSIGN
bhr   * BHR / transportnett har et sett med privatdefinerte koder
bhr  c**                 EXSR      BHRKOD
bhr   * TRANSPORTNETT LEGGER NAVN / AVVIK MM. I REASON...
bhr  C     S9012A        IFNE      *BLANKS
bhr  C                   MOVEL     S9012A        XXSIGN
bhr  C                   END
     C     KEY5          SETLL     ERFF96A
     C     KEY5          READE     ERFF96A                                35
     C     *IN35         DOWEQ     '0'
     C     R1153         IFEQ      'ZSN'
     C                   MOVEL     R1154         XXSIGN
     C                   END
     C     KEY5          READE     ERFF96A                                35
     C                   END
N02  c     XXsign        ifeq      'X'
N02  C     KEY5          CHAIN     ENAD96A                            34
N02  C  N34              MOVEL     N3036A        XXSIGN
N02  c                   end
N04  C                   MOVE      *BLANKS       FTEXT            70
N04  C     KEY5          CHAIN     EFTX96A                            34
N04  C  N34              MOVEL     F4440A        FTEXT
N10   * Location - lagring varierer  n 4 lang / postnr
N10  c                   move      *blanks       LOCTxt           35
N10  C     KEY5          CHAIN     ELOC96A                            34
N10  C  N34              eval      LOCTxt= ', '+ L3225                          3225=KODE
N10  c  N34              if        LOCTxt=*blanks
N10  c                   eval      LOCTxt=', '+  L3224                          3224=TEKST
N10  c                   else
N10  c                   eval      LOCTxt=%trim(LOCTxt)+' '+L3224               KODE+TEKST
N10  c                   endif
      * DATO/KLOKKA
     C                   MOVE      *ZEROS        XNDATO            8 0
     C                   MOVE      *ZEROS        XNKLOK            4 0
     C     KEY5          CHAIN     EDTM96A                            34
     C     *IN34         IFEQ      '0'
     C                   TESTN                   XXDATA               35          ÅÅMMDD
     C     *IN35         IFEQ      '1'
     C                   MOVE      XXDATA        XNDATO
     C                   END
     C                   TESTN                   XXTIDA               35
     C     *IN35         IFEQ      '1'
     C                   MOVE      XXTIDA        XNKLOK
     C                   END
     C                   END
      *
N08  c     ViaOpd        ifeq      'J'
N08  C                   EXSR      VIAOPDSR
N08  C                   GOTO      NXTCNI
N08  c                   endif
      *
      * andre statuser enn POD
     C     XXSTKO        IFNE      '21 '
     C     XXSTKO        ANDNE     'ZPI'
     C     XXSTKO        ANDNE     'Z25'
     C                   GOTO      UTOPD
     C                   END
      *
      * DET ER EN POD SOM ER MOTTATT - LOGG ....
      *
      * ER ENKELT-KOLLI OPPGITT
     C                   MOVE      'N'           DETKOL            1
     C     KEY5          SETLL     EPCI96A
     C     KEY5          READE     EPCI96A                                35
     C     *IN35         DOWEQ     '0'
     C     P4233         IFEQ      '18'
     C     P7102A        IFNE      *BLANKS
     C                   MOVEL     P7102A        KOLLID           20
     C                   EXSR      KOLLST
     C                   END
     C     P7102B        IFNE      *BLANKS
     C                   MOVEL     P7102B        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102C        IFNE      *BLANKS
     C                   MOVEL     P7102C        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102D        IFNE      *BLANKS
     C                   MOVEL     P7102D        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102E        IFNE      *BLANKS
     C                   MOVEL     P7102E        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102F        IFNE      *BLANKS
     C                   MOVEL     P7102F        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102G        IFNE      *BLANKS
     C                   MOVEL     P7102G        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102H        IFNE      *BLANKS
     C                   MOVEL     P7102H        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102I        IFNE      *BLANKS
     C                   MOVEL     P7102I        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102J        IFNE      *BLANKS
     C                   MOVEL     P7102J        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C                   END
     C     KEY5          READE     EPCI96A                                35
     C                   END
      *
      * HVIS DETALJKOLLI IKKE  OPPGITT - SETT X PÅ ALLE KLL
     C     DETKOL        IFEQ      'N'
     C                   EXSR      KOLLS2
     C                   END
      *
     C     UTOPD         TAG
      *
      * FINN ANTALL LEVERT/TOTAL OPPDATER STATUSER
     C                   EXSR      KOLL2
      *
     C     NXTCNI        TAG
     C                   ENDSR
N01   *
N01   *////////////////////////////////////////////////////////////
N01   *                                                    GetLGsnd
N01   *////////////////////////////////////////////////////////////
N01  C     GetLGsnd      BEGSR
N01  C     KEY5          SETLL     ERFF96A
N01  C     KEY5          READE     ERFF96A                                35
N01  C     *IN35         DOWEQ     '0'
N01  C     R1153         IFEQ      'AAM'
N01  c                   move      'J'           LG                1
N01  C                   MOVEL     R1154         C1004
N01  C                   END
N01  C     KEY5          READE     ERFF96A                                35
N01  C                   END
N01  C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    KOLLST /
      *////////////////////////////////////////////////////////////
     C     KOLLST        BEGSR
     C                   MOVE      KOLLID        KOLL18           18
     C                   MOVEL     KOLL18        KOLL35           35
      * SETT X=LEVERT PÅ AKTUELT KOLLI
     C     KOLL35        SETLL     DOKUFM1
     C                   READ      DOKUFM1                                33
     C     *IN33         IFEQ      '0'
     C     FMCLID        ANDEQ     KOLL18
     C     FMAVD         ANDEQ     HEAVD
     C     FMOPD         ANDEQ     HEOPD
     C                   MOVE      'J'           DETKOL
     C                   MOVE      'X'           FMLEVE
     C                   UPDATE    DOKUF1R
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                    KOLLS2 /
      *////////////////////////////////////////////////////////////
     C     KOLLS2        BEGSR
      * SETT X=LEVERT I ALLE KOLLI PÅ AKTUELT OPPDRAG.
     C     FMKEY         SETLL     DOKUFM
     C     FMKEY         READE     DOKUFM                                 35
     C     *IN35         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   MOVE      'X'           FMLEVE
     C                   END
     C                   UPDATE    DOKUFMR
     C     FMKEY         READE     DOKUFM                                 35
     C                   END
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    KOLL2  /
      *////////////////////////////////////////////////////////////
     C     KOLL2         BEGSR
     C                   MOVE      *ZEROS        LEV               3 0
     C                   MOVE      *ZEROS        TOT               3 0
      * FINN UT OM ALLE HAR X I FELT FOR LEVERT .
     C     FMKEY         SETLL     DOKUFM
     C     FMKEY         READE     DOKUFM                                 35
     C     *IN35         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   ADD       1             TOT
     C     FMLEVE        IFEQ      'X'
     C                   ADD       1             LEV
     C                   END
     C                   END
     C     FMKEY         READE     DOKUFM                                 35
     C                   END
      *
      *
      * SKRIV TIL TRACK & TRACE...
     C     FIURBL        IFEQ      'J'
     C                   EXSR      TTSR
     C                   END
     C*
      * KUN KODE 21 - LEVERT, SKAL OPPDATERE POD-STAUS
      * (ZPI =POD,POSTEN,  Z25=POD, TRANSPORTNETT.)
     C     XXSTKO        IFNE      '21'
     C     XXSTKO        ANDNE     'ZPI'
     C     XXSTKO        ANDNE     'Z25'
     C                   GOTO      UTLOGG
     C                   END
      *
      * DET ER POD SOM ER REISTRERT....
      * OPPDATER PÅ DOKUF (MINST ETT KOLLI LEVERT - MERK LEVERT...)
     C     SENDNR        CHAIN     DOKUF7                             33
     C     *IN33         IFEQ      '0'
     C     DFSGM         IFEQ      *BLANKS
     C                   MOVEL     XXSIGN        DFSGM
     C                   MOVEL     XNDATO        DFDTMO
     C                   MOVEL     XNKLOK        DFKLMO
     C                   END
     C                   UPDATE    DOKUFR
     C                   END
      *
      * SJEKK OM ALLE FRAKTBREVENE PÅ OPPDRAGET ER KVITTERT
      * (LEV/TOT PÅ OPPDR.NIVÅ BRUKES OM ANT. SENDINGER)
     C                   MOVE      *ZEROS        LEV               3 0
     C                   MOVE      *ZEROS        TOT               3 0
     C     DOKUFK        SETLL     DOKUF
     C     DOKUFK        READE     DOKUF                                  33
     C     *IN33         DOWEQ     '0'
     C     DFMÅL         CABEQ     *BLANKS       UTLOGG
     C                   ADD       1             TOT
     C                   ADD       1             LEV
     C     DOKUFK        READE     DOKUF                                  33
     C                   END
      *
      * FØRST NÅ ALL FBR. OK SETT LEVERT PÅ HEADF (OM IKKE GJORT FØR.)
     C     HEAKEY        CHAIN(N)  HEADF                              33
     C     *IN33         IFEQ      '0'
      * IKKE MELDT FØR....
     C     HESGM         IFEQ      *BLANKS
N02  C     HESGM         oreq      'X'
     C     HEAKEY        CHAIN     HEADF                              33
     C                   MOVEL     XXSIGN        HESGM
     C                   MOVEL     XNDATO        HEDTMO
     C                   MOVEL     XNKLOK        HEKLMO
     C  N33              UPDATE    HEADFR
     C     FIURBL        IFEQ      'J'
     C                   MOVE      *ZEROS        DFFBNR
     C                   EXSR      TTSR
     C                   END
     C                   ELSE
      * MELDT FØR....(SUPPLERING ETTER TIDLIGERE MANKO.)
      * HER LEGGES INN HANDLING VED ETTERRAPPORT AV TIDL. MELDT MANKO
      * ...
      * ...
     C                   END
      * MANKO (FREMDELES...)
     C     LEV           IFNE      TOT
     C* HER LEGGS INN "MANKOHANDLING"....
      * ...
      * ...
     C                   END
      *
     C                   END
      *
     C     UTLOGG        ENDSR
      *
N08
N08  C***********************************************
N08  C     VIAOPDSR      BEGSR
N08  C***********************************************
N08   * POD OPPDATERER OPPDRAGET - MEN KUN FØRSTE GANG
N08  C     XXSTKO        IFEQ      '21'
N08  C     HEAKEY        CHAIN(N)  HEADF                              33
N08  C     *IN33         IFEQ      '0'
N08   * IKKE MELDT FØR....
N08  C     HESGM         IFEQ      *BLANKS
N08  C     HESGM         oreq      'X'
N08  C     HEAKEY        CHAIN     HEADF                              33
N08  C                   MOVEL     XXSIGN        HESGM
N08  C                   MOVEL     XNDATO        HEDTMO
N08  C                   MOVEL     XNKLOK        HEKLMO
N08  C  N33              UPDATE    HEADFR
N08  C                   END
N08  C                   END
N08  C                   END
N08   * OPPDATER T&T UANSETT
N08  C     FIURBL        IFEQ      'J'
N08  C                   MOVE      *ZEROS        DFFBNR
N08  C                   EXSR      TTSR
N08  C                   END
N08  C                   ENDSR
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
     C     STARTTT       TAG
      * LEVERT 0 AV 0 - BETYR AT DET IKKE VAR NOEN KOLLI PÅ
      * SENDINGEN(SOM MATCHET),HENT ANTALL FRA FRAKTBREV, SETT ALT LEV.
     C     LEV           IFEQ      *ZEROS
     C     TOT           ANDEQ     *ZEROS
     C                   Z-ADD     DFNT          LEV
     C                   Z-ADD     DFNT          TOT
     C                   END
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      DFFBNR        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      'POD'         TTACTI
     C     XXSTKO        IFNE      '21'
     C     XXSTKO        ANDNE     'ZPI'
     C     XXSTKO        ANDNE     'Z25'
     C                   MOVE      'PO0'         TTACTI
     C                   END
     C                   MOVE      XNDATO        TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     XNKLOK        TTTIME
     C                   MOVE      ULDT          TTDATL
     C                   MOVE      XULTM         TTTIML
     C                   MOVEL     'SY400USR'    TTUSER
     C                   MOVE      'Date:'       TXDATE
     C                   MOVE      XNDATO        XXDATO
     C                   MOVE      'Ti:'         TXTIME
     C                   MOVE      XNKLOK        XXKLOK
     C                   MOVE      'Sts:'        TXSTKO
     C                   MOVE      'By:'         TXBY
     C                   MOVE      LEV           ZLEV
     C     ZLEV1         IFEQ      '0'
     C                   MOVE      ' '           ZLEV1
     C     ZLEV2         IFEQ      '0'
     C                   MOVE      ' '           ZLEV2
     C                   END
     C                   END
     C                   MOVE      'Av'          TXAV
     C                   MOVE      TOT           ZTOT
     C     ZTOT1         IFEQ      '0'
     C                   MOVE      ' '           ZTOT1
     C     ZTOT2         IFEQ      '0'
     C                   MOVE      ' '           ZTOT2
     C                   END
     C                   END
     C                   MOVE      TTTDS         TTTEXT
n05  C                   MOVE      'Dato:'       TXDATE
n05  C                   MOVE      'Av:'         TXBY
n05  C                   MOVE      TTTDS         TTTEXL           70
     C                   MOVEL     XXSIGN        TTNAME
     C                   MOVEL     XXSTKO        TTEDEV
      *
s13  C*                  EVAL      TTDEPO = *blanks
N13  C                   EVAL      TTDEPO = M0004
N03  c                   select
N11  c     XXSTKO        wheneq    '6  '
N11  C                   EVAL      TTACTI = 'TE2'
N11  C                   EVAL      TTTEXT = 'Assigned to transporter '
N11  C                   EVAL      TTTEXL = 'Tildelt bil/transportør '
N11  c     XXSTKO        wheneq    '13 '
N11  C                   EVAL      TTACTI = 'COL'
N11  C                   EVAL      TTTEXT = 'Collected at consignor '
N11  C                   EVAL      TTTEXL = 'Hentet hos avsender '
N11  c     XXSTKO        wheneq    '20 '
N11  C                   EVAL      TTACTI = 'TE2'
N11  C                   EVAL      TTTEXT = 'Stored at terminal/Warehouse '
N11  C                   EVAL      TTTEXL = 'Lagret på terminal/lager '
N11  c     XXSTKO        wheneq    '45 '
N11  C                   EVAL      TTTEXT = 'Handed in to terminal '
N11  C                   EVAL      TTTEXL = 'Innlevert til terminal '
N11  c     XXSTKO        wheneq    '59 '
N11  C                   EVAL      TTTEXT = 'Handed to sub-transporter '
N11  C                   EVAL      TTTEXL = 'Levert underleverandør '
N03  c     XXSTKO        wheneq    '48 '
N03  C                   EVAL      TTACTI = 'TE2'
N03  C                   EVAL      TTTEXT = 'Departed terminal '
N05  C                   EVAL      TTTEXL = 'Avgått terminal '
N03  C                   EVAL      TTDEPO = M0004
N03  c     XXSTKO        wheneq    '1  '
N03  C                   EVAL      TTACTI = 'TE2'
N03  C                   EVAL      TTTEXT = 'Arrived  terminal '
N05  C                   EVAL      TTTEXL = 'Ankommet  terminal '
N03  C                   EVAL      TTDEPO = M0004
N11  c     XXSTKO        wheneq    '56 '
N11  C                   EVAL      TTTEXT = 'Not delivered '
N11  C                   EVAL      TTTEXL = 'Ikke levert '
N04  c     XXSTKO        wheneq    'ZVK'
N04  C                   EVAL      TTACTI = 'ZVK'
N10  C                   EVAL      TTTEXL = FTEXT
N04  C                   EVAL      TTTEXT = FTEXT
N04  C                   EVAL      TTDEPO = M0004
N03  c                   endsl
N10   * legg til Location
N10  C                   EVAL      TTTEXT = %trim(TTTEXT)+ LOCTxt
N10  C                   EVAL      TTTEXL = %trim(TTTEXL)+ LOCTxt
N03  c                   move      'S'           TTMANU
N13  c     TTACTI        ifeq      'POD'
N13  C                   EVAL      TTDEPO = M0004
N13  C                   END
     C                   WRITE     TRACKFR
N13   * URL
N13  C     TTACTI        IFEQ      'POD'
N13  c     DFFBNR        andeq     *zeros
N13  C                   EXSR      FTXSR
N13  C                   END
      * URL....
BHR   *
BHR   * legg ut egne poster for kode AVV (avvik..)
BHR   *
BHR  C     XXSTKO        IFEQ      'Z26'
BHR  C     XXSTKO        OREQ      'Z28'
BHR  C     XXSTKO        OREQ      'Z29'
BHR  C     XXSTKO        OREQ      'Z30'
BHR  C     XXSTKO        OREQ      'Z31'
BHR  C                   MOVE      'AVV'         TTACTI
BHR  C                   MOVE      *blanks       TTTEXT
BHR  c                   Select
BHR  C     XXSTKO        WHENEQ    'Z26'
s05  c*                  eval      TTTEXT = 'Bomtur'
n05  c                   eval      TTTEXL = 'Bomtur'
n05  c                   eval      TTTEXT = 'Nobody at home'
BHR  C     XXSTKO        WHENEQ    'Z28'
BHR  c                   eval      TTTEXT = S9012A
BHR  C     XXSTKO        WHENEQ    'Z29'
s05  c*                  eval      TTTEXT = 'Ventetid: ' + S9012A
n05  c                   eval      TTTEXL = 'Ventetid: ' + S9012A
n05  c                   eval      TTTEXT = 'Waitingtime: ' + S9012A
BHR  C     XXSTKO        WHENEQ    'Z30'
s05  c*                  eval      TTTEXT = 'Ant utv.paller: ' + S9012A
s05  c                   eval      TTTEXT = 'Nbr of pallet: ' + S9012A
n05  c                   eval      TTTEXL = 'Ant utv.paller: ' + S9012A
BHR  C     XXSTKO        WHENEQ    'Z31'
S05  c*                  eval      TTTEXT = 'Levert ant kll: '
s05  c*
n05  c                   eval      TTTEXT = 'Delivered  Cli: '
n05  c                             + S9012A
n05  c                   eval      TTTEXL = 'Levert ant kll: '
n05  c                             + S9012A
BHR  c                   endsl
N03  c                   move      'S'           TTMANU
BHR  C                   WRITE     TRACKFR
BHR   * send melding til kundesenteret...
BHR  C     XXSTKO        IFEQ      'Z28'
BHR  C     XXSTKO        OREQ      'Z31'
BHR  C                   eval      Avvik  = TTText
BHR  C                   eval      Merknad= ' '
BHR  C                   else
BHR  C                   eval      Avvik  = ' '
BHR  C                   eval      Merknad= TTText
BHR  C                   end
bhr  C                   MOVEL     C1004         SEND17           17
bhr  c     HeaKey        CHAIN(N)  HEADF                              33
BHR  C                   Call      'E162AVVIK'
BHR  C                   PARM                    SEND17
BHR  C                   PARM                    HENAS
BHR  C                   PARM                    HENAK
BHR  C                   PARM                    HEADK1
BHR  C                   PARM                    HEADK3
BHR  C                   PARM                    MERKNAD          70
BHR  C                   PARM                    AVVIK            70
BHR  C                   end
BHR   *
      * OPPDATER ATA (MEN KUN VED FØRSTE LEVERING. )
     C     DFFBNR        IFEQ      *ZEROS
     C     HEAKEY        CHAIN     TRACKT                             33
     C     *IN33         IFEQ      *OFF
     C     XXSTKO        IFEQ      '21 '
     C     XXSTKO        OREQ      'ZPI'
     C     XXSTKO        OREQ      'Z25'
     C     TXATAD        IFEQ      *ZEROS
     C                   MOVE      XNDATO        TXATAD
     C                   MOVE      XNKLOK        TXATAK
     C                   END
     C                   END
     C                   UPDATE    TRACKTR
     C                   END
     C                   END
      * HAR NETTOPP UTFØRT LOGG PÅ ET DUP-FRAKTBREV - OPPDATER OGSÅ DISTR.ORD
     C     DFBRTX        IFEQ      'U.op:'
     C     DFBRAV        ANDGE     '0001'
     C     DFBRAV        ANDLE     '9999'
     C     DFBROP        ANDGE     '0000001'
     C     DFBROP        ANDLE     '9999999'
     c                   move      heavd         gmavd             4 0
     c                   move      heopd         gmopd             7 0
     c                   z-add     dfnt          gmnt              5 0
     c                   move      dffbnr        gmfbnr            3 0
      *
     C                   MOVE      DFBRAV        HEAVD
     C                   MOVE      DFBROP        HEOPD
     C                   Z-add     HENT          DFNT
     C                   move      *zeros        DFFBNR
     C                   MOVE      *BLANKS       DFBREF
     C     TTACTI        IFEQ      'POD'
      * oppdater LEVERT i distr.oppdraget
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      '0'
     C     HESGM         IFEQ      *BLANKS
N02  C     HESGM         oreq      'X'
     C                   MOVEL     XXSIGN        HESGM
     C                   MOVEL     XNDATO        HEDTMO
     C                   MOVEL     XNKLOK        HEKLMO
     C                   END
     C                   UPDATE    HEADFR
     C                   END
     C                   END
     C                   goto      startTT
     C                   END
     C*  LEGG TILBAKE EVT ENDREDE NØKKELFELT
     C     Gmopd         Ifne      *zeros
     c                   move      gmavd         heavd
     c                   move      gmopd         heopd
     c                   z-add     gmnt          dfnt
     c                   move      gmfbnr        dffbnr
     C     HEAKEY        CHAIN(N)  HEADF                              33
     c                   end
     c                   move      *zeros        gmavd             4 0
     c                   move      *zeros        gmopd             7 0
     c                   z-add     *zeros        gmnt              5 0
     c                   move      *zeros        gmfbnr            3 0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          MMN
     C     KEY5          KLIST
     C                   KFLD                    MMN
     C                   KFLD                    YGN
     C                   KFLD                    CREP0
     C     *LIKE         DEFINE    CGN           YGN
     C                   Z-ADD     5             YGN
     C     FMKEY         KLIST
     C                   KFLD                    XXRI              1
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   MOVE      'F'           XXRI
     C     DOKUFK        KLIST
     C                   KFLD                    XXRI              1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      'F'           XXRI
     C     HEAKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
N10  C     IFBKEY        KLIST
N10  C                   KFLD                    FSKODE
N10  C                   KFLD                    SENDNR
N10  C                   move      'IFB'         FSKODE
      *
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     FIFIRM        CHAIN     FIRMARC                            33
      *
     C                   ENDSR
N13  C***********************************************
N13  C     FTXSR         BEGSR
N13  C***********************************************
N13  C     KEY5          SETLL     EFTX96A
N13  C     KEY5          READE     EFTX96A                                35
N13  C     *IN35         DOWEQ     '0'
N13  C     F4451         IFEQ      'URL'
NSH  C     F4451         OREQ      'POD'
N13  C                   EXSR      URLSR
N13  C* ikke skriv flere URS ved flere kolli
N13  C                   Goto      utftxsr
N13  C                   END
N13  C     KEY5          READE     EFTX96A                                35
N13  C                   END
N13  C     UTFTXSR       ENDSR
N13  C***********************************************
N13  C     URLSR         BEGSR
N13  C***********************************************
N13  C                   MOVE      HEAVD         TTAVD
N13  C                   MOVE      HEOPD         TTOPD
N13  C                   z-add     0             TTFBNR
N13  C                   TIME                    XULTID
N13  C                   MOVE      XULDD         ULDD
N13  C                   MOVE      XULMM         ULMM
N13  C                   MOVE      XULÅÅ         ULÅÅ
N13  C                   MOVE      'URS'         TTACTI
N13  C                   EVAL      TTTEXL  = F4440A
N13  C                   EVAL      TTTEXT  = F4440A
N13  C                   MOVE      XNDATO        TTDATE
N13  C                   MOVE      *ZEROS        TTTIME
N13  C                   MOVEL     XNKLOK        TTTIME
N13  C                   MOVE      ULDT          TTDATL
N13  C                   MOVE      XULTM         TTTIML
N13  C                   MOVEL     'SY400USR'    TTUSER
N13  C                   MOVEL     XXSIGN        TTNAME
N13  C                   MOVEL     XXSTKO        TTEDEV
N13   *
N13  C                   EVAL      TTDEPO = M0004
N13  C                   WRITE     TRACKFR
N13  C                   MOVE      'POD'         TTACTI
N13   *
N13  C                   ENDSR
