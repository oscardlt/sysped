     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                  1  2'SYSPED/SYE3FR'
     A                                      COLOR(BLU)
     A            MSG001        40A  O  1 20MSGID(DIV 1402 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
E01  A            MSG040        40A  O  2 20MSGID(SHU 1150 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG003        26A  O  8  2MSGID(DIV 1404 SYMSGF)
     A            WSAVD          4Y 0B  8 32DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A            MSG004        26A  O 10  2MSGID(DIV 1405 SYMSGF)
     A            WSSG           3A  B 10 32DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A            MSG041        25A  O 12  2MSGID(DIV 1618 SYMSGF)
     A            WSSD           3A  B 12 32DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A            WSLNR          3Y 0B 12 37
     A  97                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A            MSG007        26A  O 14  2MSGID(DIV 1408 SYMSGF)
     A            WSJBKØ         1A  B 14 32DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(GRN)
     A            MSG012        10A  O 14 48MSGID(DIV 0259 SYMSGF)
     A            MSG008        26A  O 16  2MSGID(DIV 1409 SYMSGF)
     A            WSPRT         10A  B 16 32DSPATR(UL)
     A                                      COLOR(GRN)
     A N90        MSG203        26A  O 18  2MSGID(DIV 1619 SYMSGF)
     A N90        WSFAK          1A  B 18 32DSPATR(UL)
     A                                      VALUES(' ' 'N' 'J')
     A                                      COLOR(GRN)
     A N90        MSG212        10A  O 18 48MSGID(DIV 0259 SYMSGF)
     A            MSG009        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
