     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  5VALUES(' ' '1' '2' '4')
     A            UTTXT         20   O  6 15
     A            UTUNTY         4   O  6  8
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  3'SYSPED/SYTR20'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV4004 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A N27        MSG103        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27        MSG104        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A  27        MSG111        30A  O  4  2MSGID(DIV 1034 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        78A  O  5  2MSGID(DIV 4005 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN                      CSRLOC(LINNBR POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A            MSG201        18A  O  3  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 62MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSUNTY         4A  B  2  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      TEXT('SIGNATUR')
     A            WSTXT         20A  B  2 15DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      TEXT('NAVN PÅ SIGNATUR')
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        12A  O  2  2MSGID(DIV1216 SYMSGF)
     A            W2UNTY         4A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG302        18A  O  3  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        60A  O  1 15MSGID(DIV1254 SYMSGF)
     A            UNUNTY         4A  O  2  8DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            UNTXT         20A  O  2 15DSPATR(UL)
     A                                      COLOR(WHT)
     A            MSG402        18A  O  3  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  3'SYSPED/SYTR20'
     A                                      COLOR(BLU)
     A            MSG101        45A  O  1 20MSGID(DIV 4004 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A            MSG103        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG104        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        78A  O  5  2MSGID(DIV 4005 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG509        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
