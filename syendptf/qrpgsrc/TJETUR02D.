      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR02DO MODULE(*LIBL/TJETUR02DO *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF12 KALL MED wsunik = ARKIVPOSTER TIL ZFILE/WEB-BOOKING
11XXX * 02 sh  PTF12 HISTORISK ARKIV
11XXX * 03 Jov PTF12 Flere felter i svaret (dato mm)
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FARKIVL11  IF   E           K DISK    USROPN
     FFIRMARC   IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FARKTXT    IF   E           K DISK    USROPN
     FARKEXT    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D WSTUR           S              8
     D BSTUR           S              8  0
N01  D WSUNIK          S              9
N01  D BSUNIK          S              9  0
     D DOCLNK          S            200
N03  D DOCTYP          S             35
     D DOCTXT          S             35
     D Error           S            150
     D JSONstring      s          64000
     D ANTLEST         s              9  0
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

      *
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬wstur¬ : ¬'+%trim(WSTUR)+'¬, '
 N01    +'¬wsunik¬ : ¬'+%trim(WSUNIK)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

      * Hent dokumenter
     c     Error         IFEQ      *blanks
     C                   EXSR      LESDOK
     C                   ENDIF
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C     SLUTT         TAG
     C                   EXSR      EXIT
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         BEGSR
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      WSTUR   = zhbgetvar('WSTUR')
     C                   EVAL      BSTUR   = c2n2(WSTUR)
N01  C                   EVAL      WSUNIK  = zhbgetvar('WSUNIK')
N01  C                   EVAL      BSUNIK  = c2n2(WSUNIK)
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C     ARKIVKEY      KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    ARUNDE
     C                   MOVE      'A'           ARSTAT
     C                   MOVEL     'T:'          ARUNDE
     C                   MOVE      BSTUR         ARUNDE
N01  C                   IF        WSUNIK<>*BLANKS
N01  C                   MOVEL     'Z'           ARUNDE
N01  C*                  MOVE      BSTUR         ARUNDE
N01  C                   MOVE      BSUNIK        ARUNDE
N01  C                   ENDIF
     C     ARKEXTK       KLIST
     C                   KFLD                    ARFIRM
     C                   KFLD                    ARCEXT

     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END
      * Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024
     C*                  MOVE      BIBRID        PABRID
     C*                  MOVE      *ZEROS        PAKUNR
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      ARKIVL11
     C  N50              OPEN      firmarc
     C  N50              OPEN      FIRM
     C  N50              OPEN      arktxt
     C  N50              OPEN      ARKEXT

     C  N50BIFIRM        CHAIN     FIRM                               33

     C     ENDINI        ENDSR
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          BEGSR
     C                   close     BRIDF
     C  N50              CLOSE     ARKIVL11
     C  N50              CLOSE     firmarc
     C  N50              CLOSE     FIRM
     C  N50              CLOSE     arktxt
     C  N50              CLOSE     ARKEXT
     C  N50              CALLP     wrtsection('*fini')
     C                   EVAL      *INlr = *on
     C                   RETURN

     C                   ENDSR
      ***********************************************
     C     genbane       BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       DOCTXT
     C                   MOVE      *BLANKS       DOCLNK
     C                   MOVE      ARKLAG        ARCEXT
     C     ARKEXTK       CHAIN     ARKEXT                             33
     C   33ARFIRM        CHAIN     FIRMARC                            33
N02  c     arbhis        ifne      *blanks
N02  c                   eval      arcane=arbhis
N02  c                   end
      * genererer bane for lagring
     C     DOCLNK        CAT       '/':0         DOCLNK
     C     DOCLNK        CAT       arcane:0      DOCLNK
     C     DOCLNK        CAT       '/':0         DOCLNK
     C     ARLINK        IFNE      *BLANKS
     C     DOCLNK        CAT       ArLink:0      DOCLNK
     C                   ELSE
     C                   END
     C     '.pdf'        SCAN      DOCLNK                                 33
     C  N33'.PDF'        SCAN      DOCLNK                                 33
     C  N33'.'           SCAN      arlink                                 33
     C  N33              IF        ARKLAG = *BLANKS
     C                   CAT       '.pdf':0      DOCLNK
     C                   END
s03  C*                  IF        ARRFK = *BLANKS
s03  C*                  EVAL      DOCTXT = ARTXT
s03  C*                  ELSE
s03  C*                  EVAL      DOCTXT = ARRFK
s03  C*                  ENDIF
N03  C                   EVAL      DOCTYP = ARTXT
N03  C                   EVAL      DOCTXT = ARRFK

     C                   ADD       1             ANTLEST
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
N03     +'¬doctyp¬ : ¬'+%trim(DOCTYP)+'¬, '
        +'¬doctxt¬ : ¬'+%trim(DOCTXT)+'¬, '
N03     +'¬docdat¬ : ¬'+%trim(%editc(ARDATE:'X'))+'¬, '
N03     +'¬doctim¬ : ¬'+%trim(%editc(ARTIME:'X'))+'¬, '
        +'¬doclnk¬ : ¬'+%trim(DOCLNK)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
      **************************************************************
     C     lesdok        BEGSR
      **************************************************************
      * Skriv JSON-LISTE Start
     C                   EVAL      JSONstring ='"getdoctrip" : ['
     C                   CALLP     UPdHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   setoff                                       94
     C     arkivkey      SETLL     ARKIVL11
     C     *IN(94)       doweq     '0'
     C     arkivkey      READE     ARKIVL11                               94
     C     *IN94         IFEQ      '0'

     C     artype        CHAIN     arktxt                             95
     C  n95              EXSR      genbane

     C     NextDoc       TAG
     C                   END
     C                   END
      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
