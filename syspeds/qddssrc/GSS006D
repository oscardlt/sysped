********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 FEIL VARIABELNAVN
10XXX * 02 YBC PTF10 INKL. TRANSIT KAN IKKE KOMBINERER MED GRUPPE
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R GSS006DA                  SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            PCNO           5Y 0H
     A            PCPNO          8Y 0O  5  5EDTCDE(4)
     A            PCBCD          5A  O  5 14
     A            PCCG           2A  O  5 20
     A            PCNM          30A  H
     A            CBNAB         30A  H
     A            WXNM          30A  O  5 23
     A            PCEM          70A  H
     A            WXEM          21   O  5 54
     A            PCAUTO         1A  O  5 76
     A          R GSS006DB                  SFLCTL(GSS006DA)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS006R'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Work with purchaser'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Select option, and ENTER:'
     A                                      COLOR(BLU)
     A                                  3 32'2=Change  4=Delete'
     A                                      COLOR(BLU)
     A                                  4  2'Op ClientNo ShipC CG Name         -
     A                                                        E-mail           -
     A                                           Auto'
     A                                      DSPATR(HI)
      *
     A          R GSS006DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'End')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'New entry')
     A                                      CF08(08 'Search')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=End   F5=Refresh   F6=Create   -
     A                                      F8=Search'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS006DD
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Search')
     A                                      CF12(12 'Previous')
     A                                      SLNO(17)
     A                                      CLRL(08)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  5'Client                            -
     A                                            Ship                         -
     A                                          CG'
     A                                      DSPATR(HI)
     A            WSPNO          8Y 0B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNM          30A  B  2 14DSPATR(UL)
     A            WSBCD          5A  B  2 45DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            XNAB          26A  O  2 51COLOR(WHT)
     A            WSCG           2A  B  2 78DSPATR(UL)
     A  88                                  DSPATR(HI)
     A                                  3  2'Auto:'
     A            WSAUTO         1A  B  3  8DSPATR(UL)
     A                                      VALUES(' ' 'Y' 'N' 'E')
     A                                  3 11'E-mail:'
S01  A*           WSEM          35A  B  3 19DSPATR(UL)
     A            WSEM          70A  B  3 19DSPATR(UL)
     A  87                                  DSPATR(HI)
     A                                      CHECK(LC)
S01  A*                                 3 55'Fax no.'
S01  A*                                     DSPATR(ND)
S01  A*           WSFAX         15A  B  3 63DSPATR(UL)
S01  A*                                     DSPATR(PR)
S01  A*                                     DSPATR(ND)
     A                                  4 10'In Tr:'
     A            WSTR           1A  B  4 17DSPATR(UL)
N02  A  83                                  DSPATR(HI)
     A                                      VALUES('Y' 'N')
     A                                  4 22'Peding order:'
     A            WSPEND         1A  B  4 36DSPATR(UL)
     A                                      VALUES('1' '2' '3')
     A                                  4 38'(1=With 2=Not 3=Only)'
     A                                  4 71'C.page:'
     A                                      DSPATR(ND)
     A            WSCP           1A  B  4 79DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                      VALUES(' ' 'Y' 'N')
     A                                  5  2'CompanyKD:'
     A            WSFIR          3A  B  5 14DSPATR(UL)
     A  86                                  DSPATR(HI)
     A                                  5 22'Country code:'
     A                                      DSPATR(ND)
     A            WSLK           2A  B  5 36DSPATR(UL)
     A  85                                  DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A                                  5 45'Senders user-id:'
     A            WSUSER        10A  B  5 62DSPATR(UL)
     A  84                                  DSPATR(HI)
     A                                  6  2'Choose weekday to send:'
     A            WSUD1          1A  B  6 28DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A            WSUD2          1A  B  6 30DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A            WSUD3          1A  B  6 32DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A            WSUD4          1A  B  6 34DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A            WSUD5          1A  B  6 36DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A            WSUD6          1A  B  6 38DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A            WSUD7          1A  B  6 40DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A                                  6 52'Time to send:'
S01  A*           WSTID          4  0B  6 65DSPATR(UL)
     A            WSTD           4Y 0B  6 66DSPATR(UL)
     A                                      EDTWRD('  :  ')
     A                                  7  2'F4=Search   12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS006DE
     A                                      CF12(12 'Previous')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                  3  2'Search from  --> '
     A            W2NM          30A  I  3 20DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R GSS006DF
     A                                      CF12(12 'Previous')
     A                                      SLNO(17)
     A                                      CLRL(08)
     A                                      OVERLAY
     A                                  1  5'Client                            -
     A                                            Ship                         -
     A                                          CG'
     A                                      DSPATR(HI)
     A            PCPNO          8Y 0O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            PCNM          30A  O  2 14DSPATR(UL)
     A                                      DSPATR(HI)
     A            PCBCD          5A  O  2 45DSPATR(UL)
     A                                      DSPATR(HI)
     A            XNAB          26A  O  2 51COLOR(WHT)
     A            PCCG           2A  O  2 78DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2'Auto:'
     A            PCAUTO         1A  O  3  8DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3 11'E-mail:'
S01  A*           PCEM          35A  O  3 19DSPATR(UL)
N01  A            PCEM          70A  O  3 19DSPATR(UL)
     A                                      DSPATR(HI)
S01  A*                                 3 55'Fax no.'
S01  A*                                     DSPATR(ND)
S01  A*           PCFAX         15A  O  3 63DSPATR(UL)
S01  A*                                     DSPATR(HI)
S01  A*                                     DSPATR(ND)
     A                                  4  2'Incl. transit:'
     A            PCTR           1A  O  4 18DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 22'Peding order:'
     A            PCPEND         1A  O  4 36DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4 38'(1=With 2=Not 3=Only)'
     A                                  4 73'C.pag:'
     A                                      DSPATR(ND)
     A            PCCP           1A  O  4 79DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(ND)
     A                                  5  2'CompanyCD:'
     A            PCFIR          3A  O  5 14DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 22'Country code:'
     A                                      DSPATR(ND)
     A            PCLK           2A  O  5 36DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(ND)
     A                                  5 45'Senders user-id:'
     A            PCUSER        10A  O  5 62DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6  2'Choose weekday to send:'
     A            PCUD1          1A  O  6 28DSPATR(UL)
     A                                      DSPATR(HI)
     A            PCUD2          1A  O  6 30DSPATR(UL)
     A                                      DSPATR(HI)
     A            PCUD3          1A  O  6 32DSPATR(UL)
     A                                      DSPATR(HI)
     A            PCUD4          1A  O  6 34DSPATR(UL)
     A                                      DSPATR(HI)
     A            PCUD5          1A  O  6 36DSPATR(UL)
     A                                      DSPATR(HI)
     A            PCUD6          1A  O  6 38DSPATR(UL)
     A                                      DSPATR(HI)
     A            PCUD7          1A  O  6 40DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 52'Time to send:'
S01  A*           PCTID          4  0O  6 65DSPATR(UL)
N01  A            PCTD           4  0O  6 66DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  :  ')
     A                                  7  2'F12=Previous'
     A                                      COLOR(BLU)
      *
     A          R GSS006DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'End')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'New entry')
     A                                      CF08(08 'Search')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS006R'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Work with purchaser'
     A                                      DSPATR(HI)
     A                                  3  2'Select option, and ENTER:'
     A                                      COLOR(BLU)
     A                                  3 32'2=Endre   4=Delete'
     A                                      COLOR(BLU)
     A                                  4  2'Op ClientNo ShipC CG Name         -
     A                                                        E-mail           -
     A                                           Auto'
     A                                      DSPATR(HI)
     A                                  6  6'Can not find purchaser'
     A                                      COLOR(RED)
     A                                 23  2'F3=End   F5=Refresh   F6=Create   -
     A                                      F8=Search'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
