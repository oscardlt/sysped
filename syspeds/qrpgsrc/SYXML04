      *
      ***********************************************************
      *** PROGRAM SKAL SENDE XML-KVITTERING (MESSAGERECEIVED) ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 34-49 51-99                ***
      ***********************************************************
     FFIRM      IF   E           K DISK
     FEDIRE1K   IF   E           K DISK
     FXMLTILP   O  A E           K DISK    USROPN
     FECMSG1    IF   E           K DISK
     FEDIC      UF A E             DISK
     FEDIS      O  A E             DISK
     FEDISE     O  A E             DISK
     D TXT             S             80    DIM(34) CTDATA PERRCD(1)
     D                 DS
     D  XTXT801                1     80
     D  XT801                  1     80    DIM(80)
     D                 DS
     D  XTXT802                1     80
     D  XT802                  1     80    DIM(80)
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
     D  XMBRC                  2     10
     D                 DS
     D  WDT                    1      8
     D  WDTÅ                   1      4
     D  WDTM                   5      6
     D  WDTD                   7      8
     D                 DS
     D  WTM                    1      6
     D  WTMH                   1      2
     D  WTMM                   3      4
     D  WTMS                   5      6
      *
     D*XINHOUSE        C                   CONST('<InHouse ')
     D XINHOUSE        C                   CONST('<Transport ')
     D  XCOLLAB        C                   CONST('collaboration="')
     D  XMFUNC         C                   CONST('messageFunction="')
     D  XMID           C                   CONST('messageId="')
     D XCONSIGN        C                   CONST('<Consignment')
     D  XCONSID        C                   CONST('consignmentId="')
     D XSPARTY         C                   CONST('<PartyStructure>')
     D  XSPARTY1       C                   CONST('<PartySet>')
     D  XSPARTY1END    C                   CONST('</PartySet>')
     D  XSPARTY2       C                   CONST('<Party')
     D  XSPARTY3       C                   CONST('partyId="')
     D  XSPARTY4       C                   CONST('subClass="')
     D                 DS
     D  RDATA1                 1   1024
     D  RDATA2              1025   2048
     D  RDATA3                 1   2048
     D  RDATAXX              900   1024
     D  IN                     1   2048    DIM(2048)
     D                 DS
     D  XTXT                   1   1024
     D  XT80                   1   1024    DIM(1024)
     D                 DS
     D  XSTEGN                 1     10
     D  XST10                  1     10    DIM(10)
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRGETINFO
     C     XSLUTTLES     CABEQ     'J'           SLUTT
      *
     C     ECMSG1K       CHAIN     ECMSG1                             33
     C     *IN33         CABEQ     *ON           SLUTT
      *
     C                   EXSR      SRINIT
     C                   EXSR      SRBYGGF
      *
     C                   EXSR      SRSTART
      *
     C                   EXSR      SRPARTYS
      *
     C                   EXSR      SRDATSTR
      *
     C                   EXSR      SRSLUTT
     C                   EXSR      SRSEND
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRGETINFO     BEGSR
      ***********************************************
     C     WMBR          SETLL     EDIRE1K
     C     WMBR          READE     EDIRE1K                                50
     C   50              MOVE      'J'           XSLUTTLES
     C   50              GOTO      SLGETINFO
      *
     C                   MOVE      RDATA         RDATA2
     C                   EXSR      LESREC
     C                   Z-ADD     1             WPOS
     C     XINHOUSE      SCAN      RDATA3        WPOS              4 0    51
     C  N51              MOVE      'J'           XSLUTTLES
     C  N51              GOTO      SLGETINFO
      *
      * LES colaboration="
     C     XCOLLAB       SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLGETINFO
     C     XCOLLAB       SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      *
     C                   ADD       15            WPOS
     C                   EXSR      GETTXT
     C                   MOVEL     XTXT          XXCOLLAB         35
      * LES messageFunction="
     C     XMFUNC        SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLGETINFO
     C     XMFUNC        SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      *
     C                   ADD       17            WPOS
     C                   EXSR      GETTXT
     C                   MOVEL     XTXT          XXMFUNC          35
      * LES messageId="
     C     XMID          SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLGETINFO
     C     XMID          SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      *
     C                   ADD       11            WPOS
     C                   EXSR      GETTXT
     C                   MOVEL     XTXT          XXMID            35
      *
      * LES ConsignmentID="
     C     XCONSID       SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLGETINFO
     C     XCONSID       SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      *
     C                   ADD       15            WPOS
     C                   EXSR      GETTXT
     C                   MOVEL     XTXT          XXCONSID         35
      *
      * LES meldingsavsender/mottaker
     C                   MOVE      *BLANKS       XXREFNR          35
     C                   EXSR      SRSPARTY
     C     XSLUTTLES     CABEQ     'J'           SLGETINFO
     C                   EXSR      SRSPARTY
     C     XSLUTTLES     CABEQ     'J'           SLGETINFO
      *
     C     SLGETINFO     ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for PartyStructure                            SRSPARTY /
      *////////////////////////////////////////////////////////////
     C     SRSPARTY      BEGSR
      *
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      * LES <PartySet>
     C     XSPARTY1      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLSRSPARTY
     C     XSPARTY1      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
      *
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      * LES <Party
     C                   ADD       1             WPOS
     C     XSPARTY2      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLSRSPARTY
     C     XSPARTY2      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
      *
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      * LES MessageSender
     C                   MOVE      WPOS          XPOS              4 0
     C     XSPARTY4      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   ADD       10            WPOS
     C                   EXSR      GETTXT
     C                   MOVEL     XTXT          XAVSMOT          30
      * LES partyId="
     C                   MOVE      XPOS          WPOS
     C     XSPARTY3      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLSRSPARTY
     C     XSPARTY3      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
      * LES avsender/mottakers-id
     C                   ADD       9             WPOS
     C                   EXSR      GETTXT
      *
     C                   SELECT
     C                   WHEN      XAVSMOT = 'MessageSender'
     C                   EVAL      S0010 = XTXT
     C                   WHEN      XAVSMOT = 'MessageRecipient'
     C                   EVAL      S0004 = XTXT
     C                   ENDSL
      *
     C     SLSRSPARTY    ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for HENTING AV POSTER                           LESREC /
      *////////////////////////////////////////////////////////////
     C     LESREC        BEGSR
      *
     C                   IF        ((RDATA2 = *BLANKS) AND
     C                              (XSLUTTLES <> *BLANKS))
     C                   MOVE      'J'           XSLUTTLES         1
     C                   ELSE
     C     WMBR          READE     EDIRE1K                                50
     C  N50              DOW       RDATA = *BLANKS
     C     WMBR          READE     EDIRE1K                                50
     C  N50              ENDDO
     C                   MOVE      RDATA2        RDATA1
     C                   IF        *IN50 = *OFF
     C                   MOVE      RDATA         RDATA2
     C                   ELSE
     C                   MOVE      *BLANKS       RDATA2
     C                   MOVE      'S'           XSLUTTLES         1
     C                   END
     C                   END
     C                   IF        WPOS = 0
     C                   Z-ADD     1             WPOS
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for Hent tekst                                  GETTXT /
      *////////////////////////////////////////////////////////////
     C     GETTXT        BEGSR
      *
     C                   MOVE      *BLANKS       XTXT
     C                   MOVE      *ZEROS        XN                4 0
     C                   DOW       ((IN(WPOS) <> '<') AND (IN(WPOS) <> '"'))
     C                   IF        IN(WPOS) = '&'
     C                   EXSR      KVTTEGN
     C                   END
     C                   ADD       1             XN
     C                   MOVE      IN(WPOS)      XT80(XN)
     C                   ADD       1             WPOS
     C                   ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for Konvertering av spesiale tegn              KVTTEGN /
      *////////////////////////////////////////////////////////////
     C     KVTTEGN       BEGSR
      *
     C                   MOVE      *BLANKS       XSTEGN
     C                   MOVE      *ZEROS        XN2               3 0
     C                   DOW       IN(WPOS) <> ';'
     C                   ADD       1             XN2
     C                   MOVE      IN(WPOS)      XST10(XN2)
     C                   ADD       1             WPOS
     C                   ENDDO
      *
     C                   SELECT
     C                   WHEN      XSTEGN = '&amp'
     C                   MOVE      '&'           IN(WPOS)
     C                   WHEN      XSTEGN = '&quot'
     C                   MOVE      '"'           IN(WPOS)
     C                   WHEN      XSTEGN = '&apos'
     C                   MOVE      XAPOS         IN(WPOS)
     C                   WHEN      XSTEGN = '&gt'
     C                   MOVE      '>'           IN(WPOS)
     C                   WHEN      XSTEGN = '&lt'
     C                   MOVE      '>'           IN(WPOS)
     C                   ENDSL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRINIT        BEGSR
      ***********************************************
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       XMBR
     C                   MOVE      'F'           XMBRA
     C                   MOVE      CSN           XMBRB
     C                   MOVE      XMBR          SEMBR
     C                   MOVE      *BLANKS       SDATA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM
      *
     C                   EVAL      S0020 = 'INHTK'
     C                   EVAL      S0026 = 'XML-INH MSGRSV'
     C                   MOVE      XXCONSID      XXCONSID32       32
     C                   CAT       XXCONSID32:0  S0020
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'C'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = '*LIBL'
     C                   EVAL      SFILE = 'EDISE'
     C                   EVAL      SMBR  = SEMBR
     C                   EVAL      SMBRS = 0
     C                   EVAL      S0036 = 1
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRBYGGF       BEGSR
      ***********************************************
     C                   CALL      'SYXML03B'
     C                   OPEN      XMLTILP
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSTART       BEGSR
      ***********************************************
     C                   EVAL      SDATA = TXT(001)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(002)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(003)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = %trim(TXT(004))
     C                             + %trim(XMBR)
     C                             + '"'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(005)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRPARTYS      BEGSR
      ***********************************************
      * <PartyStructure>
     C                   EVAL      SDATA = TXT(007)
     C                   EXSR      TILFIL
      * MessageSender
      * <PartySet>
     C                   EVAL      SDATA = TXT(009)
     C                   EXSR      TILFIL
      * <Party
     C                   EVAL      SDATA = %trim(TXT(011))
     C                             + ' subClass="MessageSender"'
     C                             + ' idType="9"'
     C                             + ' partyId="'
     C                             + %trim(S0004)
     C                             + '">'
     C                             + %trim(TXT(012))
     C                   EXSR      TILFIL
      * </PartySet>
     C                   EVAL      SDATA = TXT(010)
     C                   EXSR      TILFIL
      * MessageRecipient
      * <PartySet>
     C                   EVAL      SDATA = TXT(009)
     C                   EXSR      TILFIL
      * <Party
     C                   EVAL      SDATA = %trim(TXT(011))
     C                             + ' subClass="MessageRecipient"'
     C                             + ' idType="30"'
     C                             + ' partyId="'
     C                             + %trim(S0010)
     C                             + '">'
     C                   EXSR      TILFIL
      *
     C                   EXSR      SRREF
      *
      * </Party>
     C                   EVAL      SDATA = TXT(012)
     C                   EXSR      TILFIL
      * </PartySet>
     C                   EVAL      SDATA = TXT(010)
     C                   EXSR      TILFIL
      * </PartyStructure>
     C                   EVAL      SDATA = TXT(008)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRREF         BEGSR
      ***********************************************
      * <Reference>
     C                   EVAL      SDATA = TXT(013)
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   EVAL      SDATA = %trim(TXT(015))
     C                             + %trim(XXCOLLAB)
     C                             + ':'
     C                             + %trim(XXMFUNC)
     C                             + %trim(TXT(016))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   EVAL      SDATA = %trim(TXT(017))
     C                             + 'MCR'
     C                             + %trim(TXT(018))
     C                   EXSR      TILFIL
      * </Reference>
     C                   EVAL      SDATA = TXT(014)
     C                   EXSR      TILFIL
      * <Reference>
     C                   EVAL      SDATA = TXT(013)
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   EVAL      SDATA = %trim(TXT(015))
     C                             + %trim(XXMID)
     C                             + %trim(TXT(016))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   EVAL      SDATA = %trim(TXT(017))
     C                             + 'ACW'
     C                             + %trim(TXT(018))
     C                   EXSR      TILFIL
      * </Reference>
     C                   EVAL      SDATA = TXT(014)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRDATSTR      BEGSR
      ***********************************************
      * <DateAndTimesStructure>
     C                   EVAL      SDATA = TXT(019)
     C                   EXSR      TILFIL
      * <DateAndTimesSet>
     C                   EVAL      SDATA = TXT(021)
     C                   EXSR      TILFIL
      * <DateAndTimes
     C                   EVAL      SDATA = %trim(TXT(023))
     C                             + ' subClass="MessageDate">'
     C                   EXSR      TILFIL
      * <Year>
     C                   EVAL      SDATA = %trim(TXT(025))
     C                             + %trim(WDTÅ)
     C                             + %trim(TXT(026))
     C                   EXSR      TILFIL
      * <Month>
     C                   EVAL      SDATA = %trim(TXT(027))
     C                             + %trim(WDTM)
     C                             + %trim(TXT(028))
     C                   EXSR      TILFIL
      * <Day>
     C                   EVAL      SDATA = %trim(TXT(029))
     C                             + %trim(WDTD)
     C                             + %trim(TXT(030))
     C                   EXSR      TILFIL
      * <Hour>
     C                   EVAL      SDATA = %trim(TXT(031))
     C                             + %trim(WTMH)
     C                             + %trim(TXT(032))
     C                   EXSR      TILFIL
      * <Minute>
     C                   EVAL      SDATA = %trim(TXT(033))
     C                             + %trim(WTMM)
     C                             + %trim(TXT(034))
     C                   EXSR      TILFIL
      * </DateAndTimes>
     C                   EVAL      SDATA = TXT(024)
     C                   EXSR      TILFIL
      * </DateAndTimesSet>
     C                   EVAL      SDATA = TXT(022)
     C                   EXSR      TILFIL
      * </DateAndTimesStructure>
     C                   EVAL      SDATA = TXT(020)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSLUTT       BEGSR
      ***********************************************
      *
     C                   EVAL      SDATA = TXT(006)
     C                   EXSR      TILFIL
      *
      * Oppdater EDIS
     C                   WRITE     EDISR
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSEND        BEGSR
      ***********************************************
     C                   CLOSE     XMLTILP
     C                   EVAL      XFILNVN = 'NCTK'
     C                   CAT       XMBRC:0       XFILNVN          16
     C                   CALL      'SYXML03S'
     C                   PARM                    XFILNVN
      *
     C                   ENDSR
      *
      ***********************************************
     C     TILFIL        BEGSR
      ***********************************************
     C                   WRITE     EDISER
      *
     C                   MOVE      SDATA         XTPDTA
     C                   WRITE     XMLTILPR
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    WMBR             10
      *
     C     ECMSG1K       KLIST
     C                   KFLD                    ECSR
     C                   KFLD                    EC0065
     C                   KFLD                    EC0054
     C                   KFLD                    S0010
     C                   KFLD                    S0004
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   MOVEL     'S'           ECSR
     C                   MOVEL     'XMLINK'      EC0065
     C                   MOVEL     '1.1'         EC0054
     C                   BITON     '123457'      XAPOS             1
     C                   BITOFF    '06'          XAPOS
      *
     C                   ENDSR
      *
** TXT
<?xml version="1.0" encoding="ISO-8859-1"?>                                     001
<Transport version="1.1" versionBasedOn="3.2" definedBy="BIG"                   002
profile="BringInhouse" domain="Transport" collaboration="Information"           003
language="NO" messageFunction="MessageReceived" messageId="                     004
xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">                          005
</Transport>                                                                    006
<PartyStructure>                                                                007
</PartyStructure>                                                               008
<PartySet>                                                                      009
</PartySet>                                                                     010
<Party                                                                          011
</Party>                                                                        012
<Reference>                                                                     013
</Reference>                                                                    014
<ReferenceNo>                                                                   015
</ReferenceNo>                                                                  016
<ReferenceType>                                                                 017
</ReferenceType>                                                                018
<DateAndTimesStructure>                                                         019
</DateAndTimesStructure>                                                        020
<DateAndTimesSet>                                                               021
</DateAndTimesSet>                                                              022
<DateAndTimes                                                                   023
</DateAndTimes>                                                                 024
<Year>                                                                          025
</Year>                                                                         026
<Month>                                                                         027
</Month>                                                                        028
<Day>                                                                           029
</Day>                                                                          030
<Hour>                                                                          031
</Hour>                                                                         032
<Minute>                                                                        033
</Minute>                                                                       034
