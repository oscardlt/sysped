komp  * v10-versjon endr mht opdnr
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P9XXX * 01 YBC PTF10 summere beløp når fakturaene er like
P9XXX * 02 JOV PTF10 KUN KODE 'FAK' fra Lis-fila er faktura
10329 * 03 YBC PTF10 MENGDE I ANNEN ENHET ER ENDRET FRA 7.0 TIL 9.0
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11020 * 04 YBC PTF11 TARIFFREG. HAR FLERE NYE LAND OG REGIONER
11174 * 05 YBC PTF11 TILLEGGSLINJER MED SAMME POST
********************************************************************
     FLLHEAD01  IF   E           K DISK    RENAME(LLHEADR:LLHEADR01)
     FLLHEAD15  UF   E           K DISK
     FLLLINE    UF   E           K DISK
     FLLAGJD02  UF   E           K DISK
     FLLAGJL01  IF   E           K DISK
     FLLLISF    IF   E           K DISK
     FLLTARF    IF   E           K DISK
     FLLMRF     IF   E           K DISK
     FSTANDI    UF   E           K DISK
     FKODTLB    IF   E           K DISK
     FKODTSI    IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FKODTVAL2  IF   E           K DISK
     FTARI      IF   E           K DISK
     FSADH      UF A E           K DISK
     FSADV      O  A E           K DISK
     FSADF      O  A E           K DISK
     FEDIM      O  A E             DISK
     FEDIC      UF   E             DISK
     FTVIND     UF A E           K DISK
     FFIRM      IF   E           K DISK
     D UAN             S              7    DIM(90)
     D FA              S             17    DIM(900)
     D DT              S              8  0 DIM(900)
     D VK              S              3    DIM(900)
     D BL              S             11  2 DIM(900)
      *
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UDSAVD                15     18  0
     D  UDSSG                 19     21
      *
     D                 DS
     D  SIAVD                  1      4  0
     D  XITDN                  5     11  0
     D  SISG                  12     14
     D  X14S                   1     14
     D                 DS
     D  SI27                   1     27
     D  SIBELC                 1     11  2
     D  SIRAB                 12     15  2
     D                 DS
     D  X0068Æ                 1      9  0
     D  M0068A                10     17  0
     D  M0068B                18     23  0
     D  M0068C                24     25  0
     D  X0068Å                 1     25
     D                 DS
     D  VAFAR                  1     10
     D  VAVAL                  1      3
      *
     C                   EXSR      INIT
      *
     C                   Z-ADD     0             XN                3 0
     C                   MOVE      *BLANKS       UAN
     C     LLHEAD01K     SETLL     LLHEAD01
     C     LLHEAD01K     READE     LLHEAD01                               33
     C                   DOW       *IN33 = *OFF
     C                   EXSR      FILTER
     C                   IF        OMIT <> 'J'
     C                   ADD       1             XN
     C                   MOVE      LHALN         UAN(XN)
NXX  C                   IF        XN = 90
NXX  C                   LEAVE
NXX  C                   END
     C                   END
     C     LLHEAD01K     READE     LLHEAD01                               33
     C  N33              END
      *
     C                   IF        UAN(1) <> *BLANKS                            1<-B
     C                   Z-ADD     10            SIAVD
     C     SIAVD         CHAIN     STANDI                             33
     C                   MOVE      SITDN         XTDN
     C                   ADD       1             SITDN
     C                   UPDATE    STANDIR
     C                   MOVE      *BLANKS       SIST
     C                   MOVE      XTDN          SITDN
     C                   MOVE      'GLG'         SISG
     C     KODTSIK       CHAIN     KODTSI                             33
     C                   MOVE      KSINAV        SITARF
     C                   MOVE      *ZEROS        SIKDH
     C                   MOVE      *BLANKS       SIKDTR
     C                   MOVE      *ZEROS        SIAS
     C                   MOVE      'TVINR'       SIWS
     C                   MOVE      *ZEROS        SIMP
     C                   MOVE      *BLANKS       SIDTØ
     C                   MOVE      *BLANKS       SI0035
     C                   MOVE      *BLANKS       SIMI
     C                   MOVE      WDT           SIDT
     C                   MOVE      *ZEROS        SIBELC
     C                   MOVE      *BLANKS       SIRAB
     C                   EVAL      SIFT1 = 'XX'
     C                   WRITE     SADHR
      * FINN VALUTAKODE
     C     1             DO        90            XN                              2<-B
     C                   IF        UAN(XN) = *BLANKS                              3<-B
     C                   LEAVE
     C                   END                                                      3<-E
     C                   MOVE      UAN(XN)       LHALN
     C     LLHEAD15K     CHAIN(N)  LLHEAD15                           33
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE(N)  LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   IF        LLWNT0 <> 0                                     4<-B
     C     LLAGJD02K     CHAIN(N)  LLAGJD02                           33
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   SELECT                                                     5<-B
     C                   WHEN      XVAL3 = *BLANKS                                  5
     C                   IF        VAVAL = *BLANKS                                   6<-B
     C                   EVAL      XVAL3 = 'NOK'
     C                   ELSE                                                        6
     C                   EVAL      XVAL3 = VAVAL
     C                   END                                                         6<-E
     C                   WHEN      ((XVAL3 <> VAVAL) AND (VAVAL <> *BLANKS))        5
     C                   EVAL      XVAL3 = 'NOK'
     C                   ENDSL                                                      5<-E
     C                   END                                                       4<-E
     C     LHMN          READE(N)  LLLINE                                 33
     C                   ENDDO                                                    3<-E
      *
     C                   ENDDO                                                   2<-E
      *
      * OPPDATER ALLE VARELINJER
     C     1             DO        90            XN                              2<-B
     C                   IF        UAN(XN) = *BLANKS                              3<-B
     C                   LEAVE
     C                   END                                                      3<-E
     C                   MOVE      UAN(XN)       LHALN
     C     LLHEAD15K     CHAIN     LLHEAD15                           33
     C                   ADD       LHKLL         XNTK
     C                   ADD       LHWVEK        XVKB
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE     LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   IF        LLWNT0 <> 0                                     4<-B
     C                   EXSR      OPPDLIN
     C                   MOVE      'SAD'         LLHK
     C                   END                                                       4<-E
     C                   UPDATE    LLLINER
     C     LHMN          READE     LLLINE                                 33
     C                   ENDDO                                                    3<-E
      *
     C                   MOVE      'O'           LHSTA
     C                   MOVE      SIAVD         LHAVD
     C                   MOVE      SITDN         LHOPD
     C                   UPDATE    LLHEADR
     C                   ENDDO                                                   2<-E
      *
     C                   EXSR      OPPDSADF
      *
     C                   EXSR      SNDTVINN
     C                   END                                                    1<-E
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
     C     FILTER        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      'N'           OMIT              1
      *
     C                   IF        ((LHBNA = 'KOMPLETT') AND (UTYPE <> 'K'))
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C                   IF        ((LHBNA <> 'KOMPLETT') AND (UTYPE = 'K'))
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C                   IF        LHALN = 0
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C                   IF        ((LHBLK <> *BLANKS) AND (LHBLK <> 'NO'))
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C     UTFILT        ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     OPPDLIN       BEGSR
      *////////////////////////////////////////////////////////////
     C                   CLEAR                   SADVR
     C                   MOVE      SIAVD         SVAVD
     C                   MOVE      SITDN         SVTDN
     C                   ADD       1             XLI
     C                   MOVE      XLI           SVLI
      *
     C     LLAGJD02K     CHAIN     LLAGJD02                           33
     C                   MOVE      WDT           VDLDAT
     C                   MOVE      SIAVD         VDAVD
     C                   MOVE      SITDN         VDOPD
     C                   EVAL      VDTEXT = LHBNA
     C                   EVAL      VDST   = 'R'
     C                   EVAL      VDBUSE = 'XML-KVT'
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   EVAL      VDST   = 'R'
     C                   UPDATE    LLAGJDR
      *
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   IF        XVAL3 = *BLANKS
     C                   IF        VAVAL = *BLANKS
     C                   EVAL      XVAL3 = 'NOK'
     C                   ELSE
     C                   EVAL      XVAL3 = VAVAL
     C                   END
     C                   END
      *
     C                   IF        XGN = *BLANKS
     C     LLMRFK        CHAIN     LLMRF                              33
     C                   EVAL      XGN = MRGN
     C                   END
      *
     C                   CLEAR                   LLTARFR
     C     LLTARFK       CHAIN     LLTARF                             33
     C                   IF        LTTO <> *BLANKS
     C                   MOVEL     LTTO          SVTOP1
     C                   END
     C                   IF        LTCRI <> *BLANKS
     C                   MOVE      LTCRI         SVCRE1
     C                   END
      *
     C                   MOVEL     LTTANR        SVVNT
     C                   IF        LTLKOP = *BLANKS
     C                   MOVE      SILKA         SVLK
     C                   ELSE
     C                   MOVE      LTLKOP        SVLK
     C                   END
      *
     C                   MOVE      '1'           SVVF
     C                   MOVE      ' '           SVTN
     C                   MOVE      ' '           SVPRE
     C                   IF        LLWVEK < 1
     C                   Z-ADD     1             SVVKTB
     C                   ELSE
     C                   Z-ADD(H)  LLWVEK        SVVKTB
     C                   END
     C     SVVKTB        MULT(H)   0.9           SVVKTN
     C     VAVERD        MULT      LLWNT0        SVBELT
     C                   IF        ((VAVAL <> *BLANKS) AND (VAVAL <> XVAL3))
     C     KODTVAL2K     SETGT     KODTVAL2
     C     VAVAL         READPE    KODTVAL2                               33
     C     *IN33         IFEQ      '0'
     C     SVBELT        MULT(H)   KVAKRS        XWORK            15 5
     C     XWORK         DIV       KVAOMR        SVBELT
     C                   END
     C                   END
      *
     C     KODTS2K       CHAIN     KODTS2                             33
     C  N33              IF        SVCRE1 = 'SER'
     C                   MOVE      KS2PRE        SVPRE
     C                   ELSE
     C                   IF        KS2PRE = ' '
     C                   MOVE      'N'           SVPRE
     C                   ELSE
     C                   MOVE      'J'           SVPRE
     C                   END
     C                   END
S03  C*                  MOVE      LLWNT0        SVNTM
N03  C                   Z-ADD     LLWNT0        SVNTM
     C                   MOVE      ' '           SVPVA
     C                   MOVE      *ZEROS        SVAS
     C                   EXSR      TATEST
     C                   MOVE      '1'           SVMFR
     C                   MOVEL     LLVB          SVVT01
     C                   WRITE     SADVR
      *
     C                   ADD       SVBELT        XBEL3
      * HENT FAKTURANR OG DATO
     C     VAMN          SETGT     LLLISF
     C     VAMN          READPE    LLLISF                                 33
     C                   DOW       *IN33 = *OFF
S02  C*                  IF        LITXT <> *BLANKS
N02  C                   IF        (LITXT<>*BLANKS) and (LIKD='FAK')
     C                   MOVEL     LITXT         XFAKNR           17
     C     1             DO        900           XN2               3 0
     C                   SELECT
     C                   WHEN      FA(XN2) = XFAKNR
N01  C                   EVAL      BL(XN2) = BL(XN2) + VAVERD * LLWNT0
     C                   LEAVE
     C                   WHEN      FA(XN2) = *BLANKS
     C                   MOVE      XFAKNR        FA(XN2)
     C                   MOVE      LITXT         DT(XN2)
     C                   IF        VAVAL = *BLANKS
     C                   MOVE      'NOK'         VK(XN2)
     C                   ELSE
     C                   MOVE      VAVAL         VK(XN2)
     C                   END
     C                   EVAL      BL(XN2) = BL(XN2) + VAVERD * LLWNT0
     C                   LEAVE
     C                   ENDSL
     C                   ENDDO
     C                   LEAVE
     C                   END
     C     VAMN          READPE    LLLISF                                 33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     OPPDSADF      BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      SIAVD         SFAVD
     C                   MOVE      SITDN         SFOPDN
      *
     C                   IF        FA(2) = *BLANKS
     C                   EVAL      XSIFIF = FA(1)
     C                   EVAL      XSIFID = DT(1)
     C                   ELSE
     C     1             DO        900           XN2
     C                   IF        FA(XN2) = *BLANKS
     C                   LEAVE
     C                   END
     C                   EVAL      SFTXT  = FA(XN2)
     C                   EVAL      SFDT   = DT(XN2)
     C                   EVAL      SFBL28 = BL(XN2)
     C                   EVAL      SFVK28 = VK(XN2)
     C     KODTVAL2K1    SETLL     KODTVAL2                           33
     C     SFVK28        READPE    KODTVAL2                               33
     C                   EVAL      SFKR28 = KVAKRS
     C                   EVAL      SFOM28 = KVAOMR
     C                   WRITE     SADFR
     C                   ENDDO
     C                   EVAL      XSIFIF = 'F15  ER  BENYTTET'
     C                   EVAL      XSIFID = 99999999
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SNDTVINN      BEGSR
      ***********************************************
      *
     C     SADHK         CHAIN     SADH                               33
     C                   MOVE      XSIFIF        SIFIF
     C                   MOVE      XSIFID        SIFID
     C                   MOVE      ' '           SIMI
     C                   MOVE      'DFU'         SIMF
     C                   MOVE      'Q'           SIST
     C                   MOVE      XGN           SIGN
     C                   EVAL      SIPOS = '1'
     C                   MOVE      XNTK          SINTK
     C                   MOVE      XVKB          SIVKB
     C                   MOVE      XVAL3         SIVAL3
     C     KODTVAL2K3    SETGT     KODTVAL2
     C     SIVAL3        READPE    KODTVAL2                               33
     C  N33              MOVE      KVAKRS        SIVKU
     C                   MOVE      XBEL3         SIBEL3
     C                   EXSR      CIFSR
     C                   UPDATE    SADHR
      * SEND TIL TVINN
     C                   MOVE      'DFU'         M1N07
     C                   MOVEL     '9  '         M1225
     C                   MOVEL     WDT           M0068A
     C     M0068A        CHAIN     TVIND                              33
     C   33              Z-ADD     M0068A        D0068A
     C   33              Z-ADD     0             D0068B
     C                   ADD       1             D0068B
     C   33              WRITE     TVINDF
     C  N33              UPDATE    TVINDF
     C                   MOVE      D0068B        M0068B
     C                   Z-ADD     1             M0068C
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   MOVE      S0004         M0004
     C                   MOVE      S0010         M0010
     C                   MOVE      S0035         M0035
     c                   z-add     sitdn         XITDN
     C                   EVAL      M0062 = X14S
     C                   MOVE      'CUSDEC'      M0065
     C                   MOVE      *BLANKS       M0068
     C                   MOVEL     SIRG          X0068Æ
     C                   MOVEL     X0068Å        M0068
     C                   MOVE      *ZEROS        M0068D
     C                   MOVE      *ZEROS        M0068E
     C                   MOVE      *ZEROS        M0068F
     C                   MOVE      '929'         M1001
     C                   MOVE      *BLANKS       M1004
     C                   MOVE      SIDTØ         M2005B
     C                   MOVE      S3039D        M3039D
     C                   MOVE      S3039E        M3039E
     C                   MOVE      *ZEROS        M5004D
     C                   MOVE      *BLANKS       M1N08
     C                   Z-ADD     3             M9N01
      *
     C                   MOVE      *ZEROS        MSN
     C                   Z-ADD     CMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVE      *BLANKS       MST
     C                   MOVE      *ZEROS        MDT
     C                   MOVE      *ZEROS        MTM
     C                   MOVE      SIAVD         MAVD
     C                   MOVE      SITDN         MTDN
      *
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
     C***********************************************
     C     CIFSR         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        SIBEL8
     C*  08              GOTO      SLCIF
     C     KODTLBK       CHAIN     KODTLB                             33
     C  N33              MOVE      KLBKT         SILV2
     C     KODTVAL2K3    SETGT     KODTVAL2
     C     SIVAL3        READPE    KODTVAL2                               33
     C   33              GOTO      SLCIF
     C                   MOVE      KVAOMR        XOMRF             3 0
      *
      *** OMREGN FAKTURASUM TIL NORSK KRONE
      *** OG LEGG DETTE PÅ CIFSUM (SIBEL4)
     C     XOMRF         IFNE      0
     C     SIBEL3        MULT      SIVKU         XWORK
      *   AVRUND CIFSUM
     C     XWORK         DIV(H)    XOMRF         XFAKNO            9 0          DEF. BILDE05
     C                   Z-ADD     XFAKNO        SIBEL4
     C                   END
      *** OMREGN BEARBEDINGSKOSTNAD TIL NORSK KRONE
     C*    XOMRR         IFNE      0
     C*    WSBELR        MULT      XKRSR         XWORK
      *   AVRUND CIFSUM
     C*    XWORK         DIV(H)    XOMRR         XFAKNO            9 0          DEF. BILDE05
     C*                  Z-ADD     XFAKNO        SIBELS
     C*                  END
      *
     C                   MOVE      *ZEROS        XKOSTN           15 4
      *
      *** EGET FELT FOR PROSENTRABATT I TILLEGG TIL ANDRE KOSTNAD
     C     SIRAB         IFNE      0
     C     SIBEL4        DIV       100           XWORK
     C     XWORK         MULT      SIRAB         SIBELC
     C     SIDP          IFEQ      49
     C                   MULT(H)   0.2           SIBELC
     C                   END
     C                   SUB(H)    SIBELC        SIBEL4
     C                   ELSE
     C                   MOVE      *ZEROS        SIBELC
     C                   END
     C*
     C*** OMREGN FRAKT TIL NORSK KRONE
     C*** OG LEGG DETTE PÅ CIFSUM (SIBEL4)
     C*    SIBEL1        IFNE      0
     C*    SIBEL1        MULT(H)   XKRS1         XKOSTN
     C*    XKOSTN        DIV(H)    XOMR1         XKOSTN
     C*                  Z-ADD     XKOSTN        SIBEL8
     C*                  ADD(H)    XKOSTN        SIBEL4
     C*    SIDP          IFEQ      49
     C*                  MULT(H)   0.2           XKOSTN
     C*                  END
     C*                  ADD(H)    XKOSTN        SIBELS
     C*                  END
     C*                  MOVE      *ZEROS        XKOSTN
      *
      *** OMREGN ANDRE KOSTNAD TIL NORSK KRONE
      *** OG LEGG DETTE PÅ CIFSUM (SIBEL4)
     C*    SIBEL2        IFNE      0
     C*    SIBEL2        MULT(H)   XKRS2         XKOSTN
     C*    XKOSTN        DIV(H)    XOMR2         XKOSTN
     C*    SIDP          IFEQ      49
     C*                  MULT(H)   0.2           XKOSTN
     C*                  END
     C*    SIFTG2        IFEQ      '+'
     C*    SIFTG2        OREQ      'P'
     C*                  ADD(H)    XKOSTN        SIBEL4
     C*                  ELSE
     C*                  SUB(H)    XKOSTN        SIBEL4
     C*                  END
     C*                  END
     C*                  Z-ADD(H)  XKOSTN        SIBEL9
      *
      *** SJEKK OM FORSIKRING SKAL BEREGNES
     C     KLBFOK        IFEQ      'A'
     C     SIBEL4        MULT      KLBPRM        XWORK
     C     XWORK         DIV(H)    1000          SIBELA
     C     SIDP          IFEQ      49
     C                   MULT(H)   0.2           SIBELA
     C                   END
     C                   ELSE
     C                   MOVE      *ZEROS        SIBELA
     C                   END
     C                   ADD       SIBELA        SIBEL4
     C                   ADD       SIBELA        SIBELS
      *
     C     SLCIF         ENDSR
      *
     C***********************************************
     C     TATEST        BEGSR
     C***********************************************
     C     TARIKM        SETLL     TARI
     C     TARIK         READPE    TARI                                   33
     C     *IN33         DOWEQ     '0'
     C     TADATO        IFLE      SIDT
     C                   LEAVE
     C                   END
     C     TARIK         READPE    TARI                                   33
     C  N33              ENDDO
      *
     C     SVPVA         IFEQ      'F'
     C                   MOVE      *ZEROS        SVAS
     C                   ELSE
     C                   MOVE      TAORDK        SVPVA
     C                   MOVE      TAORDB        SVAS
     C     SVPRE         IFEQ      'A'
     C                   MOVE      TAEØSB        SVAS
     C                   ELSE
     C     SVPRE         IFEQ      'B'
     C     SVPRE         OREQ      'F'
     C                   MOVE      TAEFB         SVAS
     C                   ELSE
     C     SVPRE         IFEQ      'C'
     C     SVPRE         OREQ      'E'
     C                   MOVE      TAEFTB        SVAS
     C                   ELSE
     C     SVPRE         IFNE      'J'
     C     SVPRE         ANDNE     'N'
     C                   EXSR      TATES2
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C*
     C     SVAS          IFEQ      0
     C                   MOVE      'F'           SVPVA
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TATES2        BEGSR
     C***********************************************
      * HELE SUBRUTINE ER OMSKREVET
     C                   SELECT
S04  C*    SVLK          WHENEQ    'HR'                                         KROATIA
N04  C     SVLK          WHENEQ    'SZ'                                         SWAZILAND
     C                   MOVE      TATSJB        SVAS
     C     SVLK          WHENEQ    'TR'                                         TYRKIA
     C                   MOVE      TATYRB        SVAS
     C     SVLK          WHENEQ    'IL'                                         ISRAEL
     C                   MOVE      TAISRB        SVAS
     C     SVLK          WHENEQ    'SG'                                         SINGAPORE
     C                   MOVE      TAELLB        SVAS
     C     SVLK          WHENEQ    'CL'                                         CHILE
     C                   MOVE      TABULB        SVAS
     C     SVLK          WHENEQ    'MK'                                         MAKEDONIA
     C                   MOVE      TAPOLB        SVAS
     C     SVLK          WHENEQ    'KR'                                         SØR KOREA
     C                   MOVE      TAROMB        SVAS
     C     SVLK          WHENEQ    'JO'                                         JORDAN
     C                   MOVE      TAUNGB        SVAS
     C     SVLK          WHENEQ    'TN'                                         TUNISIA
     C                   MOVE      TASLOB        SVAS
     C     SVLK          WHENEQ    'GL'                                         GRØNLAND
     C                   MOVE      TAGRLB        SVAS
     C     SVLK          WHENEQ    'FO'                                         FÆRØYENE
     C                   MOVE      TAFÆRB        SVAS
     C     SVLK          WHENEQ    'IN'                                         INDIA
     C     SVLK          OREQ      'LK'                                         SRI LANKA
     C     SVLK          OREQ      'TH'                                         THAILAND
     C                   MOVE      TAISTB        SVAS
     C     SVLK          WHENEQ    'MA'                                         MAROKKO
     C                   MOVE      TAMARB        SVAS
     C     SVLK          WHENEQ    'MX'                                         MEXICO
     C                   MOVE      TAMEXB        SVAS
     C     SVLK          WHENEQ    'PS'                                         VESTBREDDEN OG GAZA
     C                   MOVE      TAVGAB        SVAS
     C     SVLK          WHENEQ    'LN'                                         LIBANON
     C                   MOVE      TAN01B        SVAS
     C     SVLK          WHENEQ    'EG'                                         EGYPT
     C                   MOVE      TAN03B        SVAS
     C     SVLK          WHENEQ    'CA'                                         CANADA
     C                   MOVE      TAN04B        SVAS
N04  C     SVLK          WHENEQ    'RS'                                         SERBIA
N04  C                   MOVE      TAN05B        SVAS
N04  C     SVLK          WHENEQ    'AL'                                         ALBANIA
N04  C                   MOVE      TAN06B        SVAS
N04  C     SVLK          WHENEQ    'UA'                                         UKRAINA
N04  C                   MOVE      TAN07B        SVAS
N04  C     SVLK          WHENEQ    'PE'                                         PERU
N04  C                   MOVE      TAN08B        SVAS
N04  C     SVLK          WHENEQ    'ME'                                         MONTENEGRO
N04  C                   MOVE      TAN09B        SVAS
N04  C     SVLK          WHENEQ    'HK'                                         HONG KONG
N04  C                   MOVE      TAN10B        SVAS
N04  C     SVLK          WHENEQ    'BW'                                         BOTSWANA
N04  C     SVLK          OREQ      'NA'                                         NAMIBIA
N04  C     SVLK          OREQ      'SZ'                                         SWAZILAND
N04  C                   MOVE      TAN11B        SVAS
     C     KS2MO         WHENEQ    'M'                                          MINST UTVIKLEDE LAND
     C                   MOVE      TAMULB        SVAS
     C     KS2MO         WHENEQ    'O'                                          ORDINÆRE UTV.LAND
     C                   MOVE      TAOULB        SVAS
     C     KS2MO         WHENEQ    'S'                                          SACU
     C                   MOVE      TAOULB        SVAS
N04  C     KS2MO         WHENEQ    'L'                                          LAVERE M.INNT.LAND
N04  C                   MOVE      TAN12B        SVAS
     C                   ENDSL
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *
     C     *ENTRY        PLIST
     C                   PARM                    UTYPE             1
      *
     C     LLHEAD01K     KLIST
     C                   KFLD                    LHSTA
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C     LLHEAD15K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LHALN
     C     LLTARFK       KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLANR
     C                   KFLD                    LTLK
     C                   MOVE      'NO'          LTLK
     C     LLAGJL01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VDMN
     C     LLAGJD02K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LLMRFK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VAMRNR
     C     KODTLBK       KLIST
     C                   KFLD                    KLBUNI
     C                   KFLD                    SILV
     C     KODTSIK       KLIST
     C                   KFLD                    KSIUNI
     C                   KFLD                    SISG
     C     KODTS2K       KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    SVLK
     C     KODTVAL2K     KLIST
     C                   KFLD                    VAVAL
     C                   KFLD                    SIDT
     C     KODTVAL2K1    KLIST
     C                   KFLD                    SFVK28
     C                   KFLD                    SIDT
     C     KODTVAL2K3    KLIST
     C                   KFLD                    SIVAL3
     C                   KFLD                    SIDT
     C     SADHK         KLIST
     C                   KFLD                    SIAVD
     C                   KFLD                    SITDN
     C     TARIK         KLIST
     C                   KFLD                    SVVNT
     C                   KFLD                    X2TAR
     C     TARIKM        KLIST
     C                   KFLD                    SVVNT
     C                   KFLD                    X2TAR
     C                   KFLD                    XADATO
     C                   MOVE      *BLANKS       X2TAR             3
     C                   MOVE      99999999      XADATO            8 0
      *
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C     *LIKE         DEFINE    SITDN         XTDN
     C     *LIKE         DEFINE    SIGN          XGN
     C     *LIKE         DEFINE    SINTK         XNTK
     C     *LIKE         DEFINE    SIVKB         XVKB
     C     *LIKE         DEFINE    SIVAL3        XVAL3
     C     *LIKE         DEFINE    SIBEL3        XBEL3
     C     *LIKE         DEFINE    SIFIF         XSIFIF
     C     *LIKE         DEFINE    SIFID         XSIFID
     C     *LIKE         DEFINE    SVLI          XLI
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        UFIRM
     C                   Z-ADD     10            UDSAVD
     C                   MOVE      'GLG'         UDSSG
     C                   OUT       SAVUDS
     C                   Z-ADD     10            LHPNR
     C                   MOVE      'LB'          KLBUNI
     C                   MOVE      'SI'          KSIUNI
     C                   MOVEL     'S2'          KS2UNI
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   ENDSR
      *
