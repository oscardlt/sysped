      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVI017R MODULE(*LIBL/TSVI017R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVTLV     IF   E           K DISK    USROPN
     FSVTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SVIH_VUFR       S             11  3
     D SVIH_VUVA       S              3
     D SVIH_VUKU       S              7  4
     D SVIH_VUFO       S             11  3
     D SVIH_OVKO       S             11  3
     D SVIH_KARA       S             11  3
     D SVIH_ANRA       S             11  3
     D SVIH_LEKD       S              3
     D SVIH_VAKD       S              3
     D SVIH_VAKU       S              7  4
     D SVIH_FABL       S             11  3
     D SVIV_VATA       S             10
     D SVIV_ULKD       S              2
     D SVIV_FOKD       S              3
     D SVIV_EUP1       S              4
     D SVIV_ANKV       S              9  0
     D SVIV_STVA       S             11  0
     D SVIV_STVA2      S             11  0
     D SVIV_FABL       S             11  3
     D A07             S              7
     D A09             S              9
     D A11             S             11
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d uwtsat          S             10S 3

     D                 DS
     D  Å01                    1     15
     D                                     DIM(5)
     D  Å01A                   1      3
     D  Å01B                   4      6
     D  Å01C                   7      9
     D  Å01D                  10     12
     D  Å01E                  13     15
     D                 DS
     D  Å02                    1     55  3
     D                                     DIM(5)
     D  Å02A                   1     11S 3
     D  Å02B                  12     22S 3
     D  Å02C                  23     33S 3
     D  Å02D                  34     44S 3
     D  Å02E                  45     55S 3
     D                 DS
     D  Å03                    1     45  4
     D                                     DIM(5)
     D  Å03A                   1      9S 4
     D  Å03B                  10     18S 4
     D  Å03C                  19     27S 4
     D  Å03D                  28     36S 4
     D  Å03E                  37     45S 4
     D                 DS
     D  Å04                    1     20
     D                                     DIM(5)
     D  Å04A                   1      4
     D  Å04B                   5      8
     D  Å04C                   9     12
     D  Å04D                  13     16
     D  Å04E                  17     20
     D                 DS
     D  Å05                    1     45  0
     D                                     DIM(5)
     D  Å05A                   1      9S 0
     D  Å05B                  10     18S 0
     D  Å05C                  19     27S 0
     D  Å05D                  28     36S 0
     D  Å05E                  37     45S 0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     c                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   CALL      'JSONFIX'
     c                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   EVAL      JSONstring ='"taxcalc" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

      * Hæmta detta ena ærendet.

     c     Error         IFEQ      *blanks
     C                   EXSR      AVG
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬sviv_stva¬ : ¬'+%trim(%editc(SVIV_STVA:'4'))+'¬, '
          +'¬sviv_stva2¬ : ¬'+%trim(%editc(SVIV_STVA2:'4'))+'¬, '
          +'¬sviva_abk1¬ : ¬'+%trim(Å01A)+'¬, '
          +'¬sviva_abg1¬ : ¬'+%trim(%editc(Å02A:'4'))+'¬, '
          +'¬sviva_abs1¬ : ¬'+%trim(%editc(Å03A:'4'))+'¬, '
          +'¬sviva_abx1¬ : ¬'+%trim(Å04A)+'¬, '
          +'¬sviva_abb1¬ : ¬'+%trim(%editc(Å05A:'4'))+'¬, '
          +'¬sviva_abk2¬ : ¬'+%trim(Å01B)+'¬, '
          +'¬sviva_abg2¬ : ¬'+%trim(%editc(Å02B:'4'))+'¬, '
          +'¬sviva_abs2¬ : ¬'+%trim(%editc(Å03B:'4'))+'¬, '
          +'¬sviva_abx2¬ : ¬'+%trim(Å04B)+'¬, '
          +'¬sviva_abb2¬ : ¬'+%trim(%editc(Å05B:'4'))+'¬, '
          +'¬sviva_abk3¬ : ¬'+%trim(Å01C)+'¬, '
          +'¬sviva_abg3¬ : ¬'+%trim(%editc(Å02C:'4'))+'¬, '
          +'¬sviva_abs3¬ : ¬'+%trim(%editc(Å03C:'4'))+'¬, '
          +'¬sviva_abx3¬ : ¬'+%trim(Å04C)+'¬, '
          +'¬sviva_abb3¬ : ¬'+%trim(%editc(Å05C:'4'))+'¬, '
          +'¬sviva_abk4¬ : ¬'+%trim(Å01D)+'¬, '
          +'¬sviva_abg4¬ : ¬'+%trim(%editc(Å02D:'4'))+'¬, '
          +'¬sviva_abs4¬ : ¬'+%trim(%editc(Å03D:'4'))+'¬, '
          +'¬sviva_abx4¬ : ¬'+%trim(Å04D)+'¬, '
          +'¬sviva_abb4¬ : ¬'+%trim(%editc(Å05D:'4'))+'¬, '
          +'¬sviva_abk5¬ : ¬'+%trim(Å01E)+'¬, '
          +'¬sviva_abg5¬ : ¬'+%trim(%editc(Å02E:'4'))+'¬, '
          +'¬sviva_abs5¬ : ¬'+%trim(%editc(Å03E:'4'))+'¬, '
          +'¬sviva_abx5¬ : ¬'+%trim(Å04E)+'¬, '
          +'¬sviva_abb5¬ : ¬'+%trim(%editc(Å05E:'4'))+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     c                   ENDIF
     c                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

      * Quit
     C                   EXSR      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVTLV
     C  N50              CLOSE     SVTVK

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   ENDSR
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER     =  zhbgetvar('USER')
     C                   EVAL      A11        =  zhbgetvar('SVIH_VUFR')
     C                   EVAL      SVIH_VUFR  =  c2n2(A11)
     C                   EVAL      SVIH_VUVA  =  zhbgetvar('SVIH_VUVA')
     C                   EVAL      A11        =  zhbgetvar('SVIH_VUFO')
     C                   EVAL      SVIH_VUFO  =  c2n2(A11)
     C                   EVAL      A07        =  zhbgetvar('SVIH_VUKU')
     C                   EVAL      SVIH_VUKU  =  c2n2(A07)
     C                   EVAL      A11        =  zhbgetvar('SVIH_OVKO')
     C                   EVAL      SVIH_OVKO  =  c2n2(A11)
     C                   EVAL      A11        =  zhbgetvar('SVIH_KARA')
     C                   EVAL      SVIH_KARA  =  c2n2(A11)
     C                   EVAL      A11        =  zhbgetvar('SVIH_ANRA')
     C                   EVAL      SVIH_ANRA  =  c2n2(A11)
     C                   EVAL      SVIH_LEKD  =  zhbgetvar('SVIH_LEKD')
     C                   EVAL      SVIH_VAKD  =  zhbgetvar('SVIH_VAKD')
     C                   EVAL      A07        =  zhbgetvar('SVIH_VAKU')
     C                   EVAL      SVIH_VAKU  =  c2n2(A07)
     C                   EVAL      A11        =  zhbgetvar('SVIH_FABL')
     C                   EVAL      SVIH_FABL  =  c2n2(A11)
     C                   EVAL      SVIV_VATA  =  zhbgetvar('SVIV_VATA')
     C                   EVAL      SVIV_ULKD  =  zhbgetvar('SVIV_ULKD')
     C                   EVAL      SVIV_FOKD  =  zhbgetvar('SVIV_FOKD')
     C                   EVAL      SVIV_EUP1  =  zhbgetvar('SVIV_EUP1')
     C                   EVAL      A09        =  zhbgetvar('SVIV_ANKV')
     C                   EVAL      SVIV_ANKV  =  c2n2(A09)
     C                   EVAL      A11        =  zhbgetvar('SVIV_STVA')
     C                   EVAL      SVIV_STVA  =  c2n2(A11)
     C                   EVAL      A11        =  zhbgetvar('SVIV_STVA2')
     C                   EVAL      SVIV_STVA2 =  c2n2(A11)
     C                   EVAL      A11        =  zhbgetvar('SVIV_FABL')
     C                   EVAL      SVIV_FABL  =  c2n2(A11)
     c                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
     C                   CALL      BILIBL
     C                   ENDIF

     C                   Z-ADD     0             WTN               2 0
     C                   Z-ADD     0             ZCIFTOT          18 4
     C                   Z-ADD     0             ZCIFTOT2         18 4
     C     *LIKE         DEFINE    SVIH_VUFR     ZVIH_VUFR
     C     *LIKE         DEFINE    SVIH_FABL     ZVIH_FABL
     C     *LIKE         DEFINE    SVVK_KD       ZVVK_KD
     C     *LIKE         DEFINE    SVVK_DTS      ZVVK_DTS
     C     *LIKE         DEFINE    Å05A          WVIVA_ABB
     C                   MOVE      'I'           ZVTA_IE           1
     C                   MOVEA     *ZEROS        Å02
     C                   MOVEA     *ZEROS        Å03
     C                   MOVEA     *ZEROS        Å05
     C     SVTVKKEY      KLIST
     C                   KFLD                    ZVVK_KD
     C                   KFLD                    ZVVK_DTS

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      SVTLV
     C  N50              OPEN      SVTVK
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6

     c                   ENDSR
      *_________________________________________________
      * Avgiftsberækning                             AVG
      *"""""""""""""""""""""""""""""""""""""""""""""""""
     C     AVG           BEGSR
      * Rækna ut totalt CIF værde
     C                   EXSR      SRCIFTOT
      * Starta med Statistiskt værde
     C                   EXSR      SRCIFDET
      * Tull
     C                   EXSR      SRBAAA
      * Andra tullar, skatter och avgifter
     C                   EXSR      SRBAAB
      * Merværdesskatt
     C                   EXSR      SRBAAC
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna CIF totalt                              SRCIFTOT /
      *////////////////////////////////////////////////////////////
     C     SRCIFTOT      BEGSR
      *__________________________________
      * Summera alla kostnader och avdrag
      *""""""""""""""""""""""""""""""""""
      * Hämta mall för aktuell leveransvillkår
     C     SVIH_LEKD     CHAIN     SVTLV                              64
      * Fakturabelopp i Svenska kronor
     C     SVIH_FABL     MULT(H)   SVIH_VAKU     ZVIH_FABL
     C                   EVAL      ZVVK_KD=SVIH_VAKD
     C                   EVAL      ZVVK_DTS=99999999
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  64
     C                   IF        SVVK_OMR > 0
     C     ZVIH_FABL     DIV       SVVK_OMR      ZVIH_FABL
     C                   ENDIF
     C                   EVAL      ZCIFTOT=ZVIH_FABL
     C                   EVAL      ZCIFTOT2=ZVIH_FABL
      * Transport CIF
     C                   IF        SVLV_TR='J'
     C     SVIH_VUFR     MULT      SVIH_VUKU     ZVIH_VUFR
     C                   EVAL      ZVVK_KD=SVIH_VUVA
     C                   EVAL      ZVVK_DTS=99999999
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  64
     C                   IF        SVVK_OMR > 0
     C     ZVIH_VUFR     DIV       SVVK_OMR      ZVIH_VUFR
     C                   ENDIF
     C                   ADD       ZVIH_VUFR     ZCIFTOT
     C                   ENDIF
      * Transport TULL
     C                   IF        SVLV_TR2='J'
     C     SVIH_VUFR     MULT      SVIH_VUKU     ZVIH_VUFR
     C                   EVAL      ZVVK_KD=SVIH_VUVA
     C                   EVAL      ZVVK_DTS=99999999
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  64
     C                   IF        SVVK_OMR > 0
     C     ZVIH_VUFR     DIV       SVVK_OMR      ZVIH_VUFR
     C                   ENDIF
     C                   ADD       ZVIH_VUFR     ZCIFTOT2
     C                   ENDIF
      * Førsækring CIF
     C     SVLV_FS       IFEQ      'J'
     C     SVLV_FS       OREQ      'P'
     C                   ADD       SVIH_VUFO     ZCIFTOT
     C                   ENDIF
      * Førsækring TULL
     C     SVLV_FS2      IFEQ      'J'
     C     SVLV_FS2      OREQ      'P'
     C                   ADD       SVIH_VUFO     ZCIFTOT2
     C                   ENDIF
      * Øvriga kostnader
     C                   IF        SVLV_OK='J'
     C                   ADD       SVIH_OVKO     ZCIFTOT
     C                   ENDIF
      * Kassarabatt
     C                   IF        SVLV_KR='J'
     C                   SUB       SVIH_KARA     ZCIFTOT
     C                   ENDIF
      * Annan rabatt
     C                   IF        SVLV_AR='J'
     C                   SUB       SVIH_ANRA     ZCIFTOT
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna CIF før varupost                        SRCIFDET /
      *////////////////////////////////////////////////////////////
     C     SRCIFDET      BEGSR
     C                   IF        SVIH_FABL = ZCIFTOT
     C                   IF        SVIV_STVA = *ZEROS
     C                   EVAL      SVIV_STVA = SVIV_FABL
     C                   ENDIF
     C                   IF        SVIV_STVA2 = *ZEROS
     C                   EVAL      SVIV_STVA2 = SVIV_FABL
     C                   ENDIF
     C                   ELSE
     C                   IF        SVIH_FABL > 0
     C     SVIV_FABL     DIV       SVIH_FABL     ZFAKTOR          18 8
     C                   ENDIF
     C                   IF        SVIV_STVA = *ZEROS
     C     ZCIFTOT       MULT      ZFAKTOR       SVIV_STVA
     C                   ENDIF
     C                   IF        SVIV_STVA2 = *ZEROS
     C     ZCIFTOT2      MULT      ZFAKTOR       SVIV_STVA2
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Automatisk avgiftsberækning, Tull                 SRBAAA /
      *////////////////////////////////////////////////////////////
     C     SRBAAA        BEGSR
     C                   eval      uvtx15_01=sviv_vata
     C                   eval      ulk=sviv_ulkd
     C                   eval      ufokd=sviv_fokd
     C                   CALL      'SVI303R'
     C                   PARM                    uvtx15_01        10
     C                   PARM                    ulk               2
     C                   PARM                    ufokd             4
     C                   PARM      *ZEROS        uwtsat
     C                   PARM      *BLANKS       uwtfti            2
     C                   PARM      *BLANKS       uwtmti            3
     C                   PARM      *BLANKS       uwtmuc            3
     C     uwtsat        IFGT      *ZEROS
     C                   EVAL      Å01A='A00'
     C                   EVAL      Å03A=uwtsat
     C                   EVAL      Å04A=uwtmuc
     C                   SELECT
     C     uwtmuc        WHENEQ    '   '
     C                   EVAL      Å02A=SVIV_STVA2
     C                   EVAL      Å05A=(SVIV_STVA2 * uwtsat) / 100
     C                   OTHER
     C                   EVAL      Å02A=SVIV_ANKV
     C                   EVAL      Å05A=SVIV_ANKV * uwtsat
     C                   ENDSL
     C     WDT           IFGE      '20160201'
     C                   EVAL      Å04A=uwtmuc
     C                   ELSE
     C                   SELECT
     C     uwtmuc        WHENEQ    'DTN'
     C                   EVAL      Å04A='DG  '
     C     uwtmuc        WHENEQ    'HMT'
     C                   EVAL      Å04A='HM  '
     C     uwtmuc        WHENEQ    'KGM'
     C                   EVAL      Å04A='KG  '
     C     uwtmuc        WHENEQ    'MTQ'
     C                   EVAL      Å04A='KU  '
     C     uwtmuc        WHENEQ    'MTK'
     C                   EVAL      Å04A='KV  '
     C     uwtmuc        WHENEQ    'LTR'
     C                   EVAL      Å04A='L   '
     C     uwtmuc        WHENEQ    'MTR'
     C                   EVAL      Å04A='ME  '
     C     uwtmuc        WHENEQ    'NPR'
     C                   EVAL      Å04A='P   '
     C     uwtmuc        WHENEQ    'PAR'
     C                   EVAL      Å04A='ST  '
     C     uwtmuc        WHENEQ    'TNE'
     C                   EVAL      Å04A='TE  '
     C                   OTHER
     C                   EVAL      Å04A=uwtmuc
     C                   ENDSL
     C                   ENDIF
     C     Å05A          IFLE      *ZEROS
     C                   EVAL      Å01A=*BLANKS
     C                   EVAL      Å02A=*ZEROS
     C                   EVAL      Å03A=*ZEROS
     C                   EVAL      Å04A=*BLANKS
     C                   EVAL      Å05A=*ZEROS
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Andra tullar, skatter och avgifter                 SRBAAB /
      *////////////////////////////////////////////////////////////
     C     SRBAAB        BEGSR
     C                   CALL      'SVI304R'
     C                   PARM                    ulk
     C                   PARM                    uvtx15_01
     C                   PARM      *ZEROS        uwtsat
     C                   PARM      *BLANKS       uwtfti
     C                   PARM      *BLANKS       uwtmti
     C                   PARM      *BLANKS       uwtmuc

     C     uwtsat        IFGT      *ZEROS
     C                   EVAL      Å01B=uwtmti
     C                   EVAL      Å03B=uwtsat
     C                   EVAL      Å04B=uwtmuc
     C                   SELECT
     C     uwtmuc        WHENEQ    '   '
     C                   EVAL      Å02B=SVIV_STVA2
     C                   EVAL      Å05B=(SVIV_STVA2 * uwtsat) / 100
     C                   OTHER
     C                   EVAL      Å02B=SVIV_ANKV
     C                   EVAL      Å05B=SVIV_ANKV * uwtsat
     C                   ENDSL
     C     WDT           IFGE      '20160201'
     C                   EVAL      Å04B=uwtmuc
     C                   ELSE
     C                   SELECT
     C     uwtmuc        WHENEQ    'DTN'
     C                   EVAL      Å04B='DG  '
     C     uwtmuc        WHENEQ    'HMT'
     C                   EVAL      Å04B='HM  '
     C     uwtmuc        WHENEQ    'KGM'
     C                   EVAL      Å04B='KG  '
     C     uwtmuc        WHENEQ    'MTQ'
     C                   EVAL      Å04B='KU  '
     C     uwtmuc        WHENEQ    'MTK'
     C                   EVAL      Å04B='KV  '
     C     uwtmuc        WHENEQ    'LTR'
     C                   EVAL      Å04B='L   '
     C     uwtmuc        WHENEQ    'MTR'
     C                   EVAL      Å04B='ME  '
     C     uwtmuc        WHENEQ    'NPR'
     C                   EVAL      Å04B='P   '
     C     uwtmuc        WHENEQ    'PAR'
     C                   EVAL      Å04B='ST  '
     C     uwtmuc        WHENEQ    'TNE'
     C                   EVAL      Å04B='TE  '
     C                   OTHER
     C                   EVAL      Å04B=uwtmuc
     C                   ENDSL
     C                   ENDIF
     C     Å05B          IFLE      *ZEROS
     C                   EVAL      Å01B=*BLANKS
     C                   EVAL      Å02B=*ZEROS
     C                   EVAL      Å03B=*ZEROS
     C                   EVAL      Å04B=*BLANKS
     C                   EVAL      Å05B=*ZEROS
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Merværdesskatt                                    SRBAAC /
      *////////////////////////////////////////////////////////////
     C     SRBAAC        BEGSR
      *______________________________
      * Læs avgiftslinjer och summera
      *""""""""""""""""""""""""""""""
     C                   Z-ADD     Å05A          WVIVA_ABB
     C                   ADD       Å05B          WVIVA_ABB
     C                   ADD       SVIV_STVA2    WVIVA_ABB
     C                   Z-ADD     SVIV_STVA2    WVIVA_ABB
      * Addera momspost  (ikke ved 4200)
     C     SVIV_EUP1     IFNE      '4200'
     C     WVIVA_ABB     IFGT      *ZEROS
     C                   EVAL      Å01C='1MT'
     C                   EVAL      Å02C=WVIVA_ABB
     C                   IF        Å01B=*BLANKS
     C                   EVAL      Å01B=Å01C
     C                   EVAL      Å02B=Å02C
     C                   EVAL      Å03B=Å03C
     C                   EVAL      Å04B=Å04C
     C                   EVAL      Å05B=Å05C
     C                   EVAL      Å01C=*BLANKS
     C                   EVAL      Å02C=*ZEROS
     C                   EVAL      Å03C=*ZEROS
     C                   EVAL      Å04C=*BLANKS
     C                   EVAL      Å05C=*ZEROS
     C                   ENDIF
     C                   IF        Å01A=*BLANKS
     C                   EVAL      Å01A=Å01B
     C                   EVAL      Å02A=Å02B
     C                   EVAL      Å03A=Å03B
     C                   EVAL      Å04A=Å04B
     C                   EVAL      Å05A=Å05B
     C                   EVAL      Å01B=*BLANKS
     C                   EVAL      Å02B=*ZEROS
     C                   EVAL      Å03B=*ZEROS
     C                   EVAL      Å04B=*BLANKS
     C                   EVAL      Å05B=*ZEROS
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
