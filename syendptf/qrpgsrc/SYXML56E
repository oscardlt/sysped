      *
      ***********************************************************
      *** PROGRAM SKAL SENDE UniversalShipment til CW1        ***
      *** Ved mottak av shipment fra CW1 etter laget oppdrag  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 34-99                      ***
      ***********************************************************
     H DFTACTGRP(*NO)
     FFIRM      IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FSAEH      IF   E           K DISK
     FSAEV4     IF   E           K DISK
     FKODTVAL2  IF   E           K DISK
     FSADAVGE   IF   E           K DISK
     FTARI      IF   E           K DISK
     FEDIC      UF A E             DISK
     FEDIS      UF A E           K DISK
     FEDIM      O  A E             DISK
     FTRACKF    O  A E             DISK
      /copy ifsio_h
     D X_S0020         S             14A
      *
     D FILE_o          s             10I 0
     D String          s            512a
     D rc              s             10i 0
     D Idx             s              5u 0
     D FILNAM          s            100A
     D XCHGAUT         s            200A
     D LF              c                   x'25'
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST
     D                SDS
     D  ZUSER                254    263
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRSNDST
      *
     C                   EXSR      SRUNISHI
      *
     C                   EXSR      OPDEDIM
      *
     C                   EXSR      OPDTRACK
      *
     C                   EXSR      SRCLOSE
      *
     C                   EXSR      SRSNDSL
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRSNDST       BEGSR
      ***********************************************
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       SDATA          1024
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   EVAL      S0004 = X0004
     C                   EVAL      S0010 = X0010
     C                   EVAL      S0020 = %TRIM(X0020) + '-SHI'
     C                   Z-ADD     1             S0036
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'X'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = *BLANKS
     C                   EVAL      SFILE = '*IFS'
     C                   EVAL      SMBR  = *BLANKS
     C                   EVAL      SMBRS  = 0
      *
     C                   EVAL      FILNAM = '/' + %TRIM(FILIBF) + 'X/'
     C                             + %TRIM(S0010) + '/EDISE1K/BACKUP/'
     C                             + %TRIM(S0020) + %TRIM(WDT)
     C                             + %TRIM(WTM) + '.xml'
      * 1208 = UTF-8
     C                   eval      FILE_o = open( %TrimR( FilNam )
     C                               : O_CREAT+O_TRUNC+O_WRONLY
     C                                + O_TEXT_CREAT+O_CCSID+O_TEXTDATA
     C                               : S_IRUSR + S_IWUSR
     C                               : 1208
     C                               : 0)
      *
     C                   EVAL      SIFS   = FILNAM
     C                   WRITE     EDISR
      *
     C                   EVAL      XSSN = SSN
      *
     C                   EVAL      SDATA = '<?xml version="1.0" '
     C                             + 'encoding="UTF-8"?>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRUNISHI      BEGSR
      ***********************************************
      * <UniversalShipment>
     C                   EVAL      SDATA = '<UniversalShipment>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Shipment>'
     C                   EXSR      OPDFIL
      *
     C                   EXSR      SRDATCON
     C                   EXSR      SRCOMINF
     C                   EXSR      SRENTHEACOL
      *
     C                   EVAL      SDATA = '</Shipment>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</UniversalShipment>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRDATCON      BEGSR
      ***********************************************
      * <DataContext>
     C                   EVAL      SDATA = '<DataContext>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<DataTargetCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<DataTarget>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '<Type>CustomsDeclaration</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Key>' + %TRIM(X0020) + '</Key>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '</DataTarget>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</DataTargetCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</DataContext>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCOMINF      BEGSR
      ***********************************************
      * <CommercialInfo>
     C                   EVAL      SDATA = '<CommercialInfo>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<CommercialInvoiceCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<CommercialInvoice>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '<InvoiceNumber>'
     C                             + %TRIM(UAVD) + '/' + %TRIM(UTDN)
     C                             + '</InvoiceNumber>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<InvoiceCurrency>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>' + %TRIM(SEVAL1) + '</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</InvoiceCurrency>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<IncoTerm>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>' + %TRIM(SELV) + '</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</IncoTerm>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<InvoiceAmount>'
     C                             + %TRIM(%EDITC(SEBEL1:'3'))
     C                             + '</InvoiceAmount>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<NoOfPacks>'
     C                             + %TRIM(%EDITC(SENTK:'Z'))
     C                             + '</NoOfPacks>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<PaymentAmount>'                    Total avgift (FF)
     C                             + %TRIM(%EDITC(XBEL:'Z'))
     C                             + '</PaymentAmount>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<PaymentDate/>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Weight>'
     C                             + %TRIM(%EDITC(SEVKB:'Z')) + '</Weight>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<WeightUnit>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>KG</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA ='<Description>Kilograms</Description>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</WeightUnit>'
     C                   EXSR      OPDFIL
      *
     C                   EXSR      SRCOMINVLIN
      *
     C                   EVAL      SDATA = '</CommercialInvoice>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</CommercialInvoiceCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</CommercialInfo>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCOMINVLIN   BEGSR
      ***********************************************
      * <CommercialInvoiceLineCollection>
     C                   EVAL      SDATA = '<CommercialInvoiceLineCollection>'
     C                   EXSR      OPDFIL
      *
     C     SAEHK         SETLL     SAEV4
     C     SAEHK         READE     SAEV4                                  33
     C                   DOW       *IN33 = *OFF
      *
     C                   EVAL      SDATA = '<CommercialInvoiceLine>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<LineNo>'
     C                             + %TRIM(%EDITC(SVLN:'Z')) + '</LineNo>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EntryNumber>'
     C*                            + %TRIM(X0020) + '/' + %TRIM(UREF)
     C                             + %TRIM(X0020) + '/'
     C                             + %TRIM(UAVD) + '-' + %TRIM(UTDN)
     C                             + '</EntryNumber>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EntryLineNumber>'
     C                             + %TRIM(%EDITC(SVLN:'Z'))
     C                             + '</EntryLineNumber>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<ClassificationCode/>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Commodity><Code/></Commodity>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<ContainerMode>'
     C                             + '<Code/>'
     C                             + '</ContainerMode>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<CountryOfOrigin>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>' + %TRIM(SVLK) + '</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</CountryOfOrigin>'
     C                   EXSR      OPDFIL
     C     SVVNT         CHAIN     TARI                               33
     C                   IF        TAENHE = *BLANKS
     C                   EVAL      SDATA = '<CustomsQuantity/>'
     C                   ELSE
     C                   EVAL      SDATA = '<CustomsQuantity>'
     C                             + %TRIM(%EDITC(SVNTM:'Z'))
     C                             + '</CustomsQuantity>'
     C                   END
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<CustomsQuantityUnit>'
     C                   EXSR      OPDFIL
     C                   SELECT
     C                   WHEN      TAENHE = 'L'
     C                   EVAL      SDATA = '<Code>Lit</Code>'
     C                   WHEN      TAENHE = 'KG' OR TAENHE = 'KG,'
     C                   EVAL      SDATA = '<Code>KG</Code>'
     C                   WHEN      TAENHE = 'G'
     C                   EVAL      SDATA = '<Code>G</Code>'
     C                   WHEN      TAENHE = 'STK'
     C                   EVAL      SDATA = '<Code>PCS</Code>'
     C                   WHEN      TAENHE = 'M'
     C                   EVAL      SDATA = '<Code>M</Code>'
     C                   WHEN      TAENHE = 'M2'
     C                   EVAL      SDATA = '<Code>M2</Code>'
     C                   WHEN      TAENHE = 'M3' OR TAENHE = 'M3F'
     C                   EVAL      SDATA = '<Code>M3</Code>'
     C                   WHEN      TAENHE = 'MWH'
     C                   EVAL      SDATA = '<Code>MWH</Code>'
     C                   WHEN      TAENHE = 'PAR'
     C                   EVAL      SDATA = '<Code>PAR</Code>'
     C                   WHEN      TAENHE = 'K'
     C                   EVAL      SDATA = '<Code>CLL</Code>'
     C                   WHEN      TAENHE = 'K'
     C                   EVAL      SDATA = '<Code>' + %TRIM(TAENHE) + '</Code>'
     C                   OTHER
     C                   EVAL      SDATA = '<Code/>'
     C                   ENDSL
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</CustomsQuantityUnit>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Description/>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<HarmonisedCode>'
     C                             + %TRIM(%EDITC(SVVNT:'Z'))
     C                             + '</HarmonisedCode>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<HazardousMaterial>'
     C                             +  '<Code/>'
     C                             +  '</HazardousMaterial>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<InvoiceQuantity/>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<InvoiceQuantityUnit>'
     C                             +  '<Code/>'
     C                             + '</InvoiceQuantityUnit>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<LinePrice>'
     C                             + %TRIM(%EDITC(SVBELT:'4'))
     C                             + '</LinePrice>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<NetWeight>'
     C                             + %TRIM(%EDITC(SVVKTN:'4'))
     C                             + '</NetWeight>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<NetWeightUnit>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>KG</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA ='<Description>Kilograms</Description>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</NetWeightUnit>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<OrderNumber/>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<PartNo/>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<VolumeUnit><Code/></VolumeUnit>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Weight>'
     C                             + %TRIM(%EDITC(SVVKTB:'4'))
     C                             + '</Weight>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<WeightUnit>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>KG</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA ='<Description>Kilograms</Description>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</WeightUnit>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Procedure>'
     C                             + %TRIM(%EDITC(SEDP:'Z'))
     C                             + '</Procedure>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</CommercialInvoiceLine>'
     C                   EXSR      OPDFIL
      *
     C     SAEHK         READE     SAEV4                                  33
     C                   ENDDO
      *
     C                   EVAL      SDATA = '</CommercialInvoiceLineCollection>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRENTHEACOL   BEGSR
      ***********************************************
      * <EntryHeaderCollection>
     C                   EVAL      SDATA = '<EntryHeaderCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EntryHeader>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type><Code>EXP</Code></Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Reference>' + %TRIM(X0020) + '/'
     C*                            + %TRIM(UREF) + '</Reference>'
     C                             + %TRIM(UAVD) + '-' + %TRIM(UTDN)
     C                             + '</Reference>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<TotalAmountPaid>'
     C                             + %TRIM(%EDITC(XBEL:'Z'))
     C                             + '</TotalAmountPaid>'
     C                   EXSR      OPDFIL
      *
     C                   EXSR      SRENTNUMCOL
     C                   EXSR      SRENTLINCOL
      *
     C                   EVAL      SDATA = '</EntryHeader>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</EntryHeaderCollection>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRENTNUMCOL   BEGSR
      ***********************************************
      * <EntryNumberCollection>
     C                   EVAL      SDATA = '<EntryNumberCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EntryNumber>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type><Code>EXP</Code></Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Number>'
     C*                            + %TRIM(X0020) + '/' + %TRIM(UREF)
     C                             + %TRIM(X0020) + '/'
     C                             + %TRIM(UAVD) + '-' + %TRIM(UTDN)
     C                             + '</Number>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EntryIsSystemGenerated>'
     C                             + 'false</EntryIsSystemGenerated>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</EntryNumber>'
     C                   EXSR      OPDFIL
      *
      * Løpenr og ekspedisjonsenhet
     C                   EVAL      SDATA = '<EntryNumber>'
     C                   EXSR      OPDFIL
     C*                  EVAL      SDATA = '<Type><Code>TK</Code></Type>'
     C                   EVAL      SDATA = '<Type><Code>MRN</Code></Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Number>' + %TRIM(UREF)
     C                             + '</Number>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EntryIsSystemGenerated>'
     C                             + 'false</EntryIsSystemGenerated>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</EntryNumber>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '</EntryNumberCollection>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRENTLINCOL   BEGSR
      ***********************************************
      * <EntryLineCollection>
     C                   EVAL      SDATA = '<EntryLineCollection>'
     C                   EXSR      OPDFIL
      *
     C     SAEHK         SETLL     SAEV4
     C     SAEHK         READE     SAEV4                                  33
     C                   DOW       *IN33 = *OFF
      *
     C                   EVAL      SDATA = '<EntryLine>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<LineNumber>'
     C                             + %TRIM(%EDITC(SVLN:'Z'))
     C                             + '</LineNumber>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<CustomsValue>'
     C                             + %TRIM(%EDITC(SVBELT:'4'))
     C                             + '</CustomsValue>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Description>'
     C                             + %TRIM(SVVT01) + '</Description>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<HarmonisedCode>'
     C                             + %TRIM(%EDITC(SVVNT:'Z'))
     C                             + '</HarmonisedCode>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '<EntryLineChargeCollection>'
     C                   EXSR      OPDFIL
      *
      * Avgift
     C                   EVAL      SDATA = '<EntryLineCharge>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>DTY</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Description>Duty</Description>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Type>'
     C                   EXSR      OPDFIL
     C                   IF        SVAVT = 'X'
     C     SADAVGEK      SETLL     SADAVGE
     C     SADAVGEK      READE     SADAVGE                                61
     C     *IN61         DOWEQ     '0'
     C*    AGDTF         IFLE      X_M0068A
     C*    AGDTT         ANDGT     X_M0068A
     C     AGDTF         IFLE      SEDT
     C     AGDTT         ANDGT     SEDT
     C                   LEAVE
     C                   END
     C     SADAVGEK      READE     SADAVGE                                61
     C  N61              ENDDO
     C     *IN61         IFEQ      *OFF
     C     AGPP          ANDEQ     '%'
     C                   Z-ADD     100           XFAKTA            5 0
     C                   ELSE
     C                   Z-ADD     1000          XFAKTA
     C                   END
     C                   EVAL(H)   XTOLAVG = SVBELT * SEVKU / KVAOMR * SVAVTP
     C                             / XFAKTA
     C                   ELSE
     C                   EVAL      XTOLAVG = 0
     C                   END
     C                   EVAL      SDATA = '<Amount>'
     C                             + %TRIM(%EDITC(XTOLAVG:'Z'))
     C                             + '</Amount>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</EntryLineCharge>'
     C                   EXSR      OPDFIL
      *
      * Moms
     C                   EVAL      SDATA = '<EntryLineCharge>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>VAT</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Description>Vat</Description>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Amount>0</Amount>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</EntryLineCharge>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '</EntryLineChargeCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</EntryLine>'
     C                   EXSR      OPDFIL
      *
     C     SAEHK         READE     SAEV4                                  33
     C                   ENDDO
      *
     C                   EVAL      SDATA = '</EntryLineCollection>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
      *
     C                   callp     close( FILE_o )
     C                   EVAL      XCHGAUT = 'CHGAUT OBJ(' + XAPS
     C                             + %TRIM(FILNAM) + XAPS
     C                             + ') USER(*PUBLIC) DTAAUT(*RWX) OBJAUT(*ALL)'
     C                   CALLP     RUNCMD(XCHGAUT:200)
      * CHGATR OBJ('home/cb/a.xml') ATR(*CCSID) VALUE(1208)
     C                   MOVE      *BLANKS       UCMD           1024
     C                   Z-ADD     1024          UCMDL            15 5
     C                   EVAL      UCMD = 'CHGATR OBJ(' + XAPS
     C                             + %TRIM(FILNAM) + XAPS + ') '
     C                             + 'ATR(*CCSID) VALUE(1208)'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSNDSL       BEGSR
      ***********************************************
      *
     C     XSSN          CHAIN     EDIS                               33
     C  N33              MOVE      'X'           SST
     C  N33              UPDATE    EDISR
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRTOTAVG      BEGSR
      ***********************************************
     C     SAEHK         SETLL     SAEV4
     C     SAEHK         READE     SAEV4                                  33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        SVAVT = 'X'                                   2<-B
     C     SADAVGEK      SETLL     SADAVGE
     C     SADAVGEK      READE     SADAVGE                                33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   IF        AGDTF <= SEDT AND AGDTT >= SEDT                 4<-B
     C                   LEAVE
     C                   END                                                       4<-E
     C     SADAVGEK      READE     SADAVGE                                33
     C                   ENDDO                                                    3<-E
     C                   IF        *IN33 = *OFF AND AGPP = '%'                    3<-B
     C                   Z-ADD     100           XFAKTA            5 0
     C                   ELSE                                                     3
     C                   Z-ADD     1000          XFAKTA
     C                   END                                                      3<-E
     C                   EVAL(H)   XTOLAVG = SVBELT * SEVKU / KVAOMR * SVAVTP
     C                             / XFAKTA
     C                   ELSE                                                    2
     C                   EVAL      XTOLAVG = 0
     C                   END                                                     2<-E
     C                   EVAL      XBEL = XBEL + XTOLAVG
     C     SAEHK         READE     SAEV4                                  33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDEDIM       BEGSR
      ***********************************************
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EVAL      M0004 = S0004
     C                   EVAL      M0010 = S0010
     C                   MOVEL     CMN           M0062
     C                   EVAL      M0065 = 'UNISHI'
     C                   EVAL      M0068 = %TRIM(X0020) + '-SHI'
     C                   MOVEL     '929'         M1001
     C                   EVAL      M1004 = X0020
     C                   EVAL      M1225 = 'SHI'
     C                   EVAL      M1N07 = 'DTK'
     C                   EVAL      MSN = SSN
     C                   EVAL      MMN = CMN
     C                   EVAL      MSR = 'S'
     C                   EVAL      MST = 'C'
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      UAVD          MAVD
     C                   MOVE      UTDN          MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDTRACK      BEGSR
      ***********************************************
      *
     C                   EVAL      TTAVD  = MAVD
     C                   EVAL      TTOPD  = MTDN
     C                   EVAL      TTACTI = 'CW1'
     C                   EVAL      TTDATE = MDT
     C                   EVAL      TTTIME = MTM
     C                   EVAL      TTUSER = ZUSER
     C                   EVAL      TTDATL = MDT
     C                   EVAL      TTTIML = MTM
     C                   EVAL      TTEDEV = 'SHI'
     C                   EVAL      TTEDRE = 'EXP'
     C                   EVAL      TTMANU = 'E'
     C                   EVAL      TTNAME = 'SYXML56E'
     C                   EVAL      TTTEXT = 'CW1-Shipment(' + %TRIM(X0020)
     C                             + ') is sent to CW1.'
     C                   EVAL      TTTEXL = 'CW1-Shipment(' + %TRIM(X0020)
     C                             + ') er sendt til CW1.'
     C                   WRITE     TRACKFR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDFIL        BEGSR
      ***********************************************
     C                   EVAL      SDATA=(%trimr(SDATA))+ LF
     c                   callp     write(File_o : %addr(Sdata)
     c                                : %len(%trimr(Sdata)) )
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SAEHK         KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     SAEV4K        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     FRISOKK       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C     SADAVGEK      KLIST
     C                   KFLD                    SVVNT
     C                   KFLD                    AGAKD
     C                   KFLD                    SVAVTS
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UTDN              7
     C                   PARM                    UREF             17
      *
     C     *LIKE         DEFINE    S0004         X0004
     C     *LIKE         DEFINE    S0010         X0010
     C     *LIKE         DEFINE    S0020         X0020
     C     *LIKE         DEFINE    SSN           XSSN
     C                   MOVE      X'7D'         XAPS              1
     C                   MOVE      UAVD          FSAVD
     C                   MOVE      UTDN          FSOPD
     C                   MOVE      'FF'          AGAKD
     C                   MOVE      *ZEROS        XTOLAVG          11 0
     C                   MOVE      *ZEROS        XBEL             11 0
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   EVAL      FSKODE = '*CE'
     C     FRISOKK       CHAIN     FRISOK                             33
     C  N33              MOVEL     FSSOK         X0020                          CW1 ORDRE NR
     C  N33              EVAL      FSKODE = '*CA'
     C  N33FRISOKK       CHAIN     FRISOK                             33
     C  N33              MOVEL     FSSOK         X0010                          CW1 AVSENDER
     C  N33              EVAL      FSKODE = '*CM'
     C  N33FRISOKK       CHAIN     FRISOK                             33
     C  N33              MOVE      FSSOK         X0004                          CW1 MOTTAKER
     C     SAEHK         CHAIN     SAEH                               33
     C     SEVAL1        CHAIN     KODTVAL2                           33
      *
      * BEREGN TOTAL AVGIFT
     C                   EXSR      SRTOTAVG
      *
     C                   ENDSR
      *
