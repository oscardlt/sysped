      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESARKF1 MODULE(*LIBL/ESARKF1
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     Farkivl04  IF   E           K DISK    usropn
     Farkivl05  IF   E           K DISK    usropn
     F                                     RENAME(ARKIVR:ARKI05R)
     FFIRMARC   IF   E           K DISK    usropn
     FFAK9      IF   E           K DISK    usropn
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D U_name          S             10
     D DATO            s             10
     D FraDT8          s              8  0
     D DATOT           s             10
     D TilDT8          s              8  0
     D FakNr           s              7
     D FakNrN          s              7  0
     D Kunr2           s              8
     D Kunr2N          s              8  0
     D Arfak           s             40
     D DATONF          s              6  0
     D DATONT          s              6  0
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D TITLE           C                   CONST('Søk arkiverte fakturaer mm.')
     D TITLEEN         C                   CONST('Inquire archived invoices.')
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
     D                 DS
     D  DATON                  1      6  0
     D  DATOND                 1      2  0
     D  DATONM                 3      4  0
     D  DATONÅ                 5      6  0
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      * 4-Sections /$tablerow
      *
     C                   EXSR      HENTFL
      *
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
      *
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   eval      DATO     = zhbgetvar('DATO')
     C                   eval      DATON    = c2n2(DATO)
     C                   eval      FAKNR    = zhbgetvar('FAKNR')
     C                   eval      FAKNRN   = c2n2(FAKNR)
     C                   eval      KUNR2    = zhbgetvar('KUNR2')
     C                   eval      KUNR2N   = c2n2(KUNR2)
      * Eksternt pålogget via access list..- Overstyrer parameter USERID
     C                   eval      U_Name   = getenv('REMOTE_USER':
     C                             qusec)
     C     U_Name        IFNE      *BLANKS
     C                   eval      WSUSER   = uppify(U_Name)
     C                   END
      * LÅNER ARDTOP for datokonvertering
     C                   EVAL      FraDt8 = 20000000 + DATONÅ * 10000
     C                             + DATONM * 100 + DATOND
     C                   move      DATON         DATONF
      * Evt Tildato
     C                   move      *zeros        DATONT
     C                   move      *zeros        TilDT8
     C                   eval      DATOT    = zhbgetvar('DATOT')
     C     DATOT         ifne      *blanks
     C                   eval      DATON    = c2n2(DATOT)
     C                   EVAL      TilDt8 = 20000000 + DATONÅ * 10000
     C                             + DATONM * 100 + DATOND
     C                   move      DATON         DATONT
     C                   end
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('TITLE':TITLEEN)
     C                   ELSE
     C                   callp     updhtmlvar('TITLE':TITLE)
     C                   END
      * hvis *I*=Intern bruker - finn kunde via egen param (etter 20102007 parm XINTERN)
     c                   Move      BIBESK        TSTINTERN         3
     C     TSTINTERN     Ifeq      '*I*'
     C                   move      'J'           XINTERN
     C                   end
     C     XINTERN       Ifeq      'J'
     C     KUNR2n        Ifne      *zeros
     c                   move      KUNR2N        BIKUNR
     c                   else
     C     FakNrN        Ifne      *zeros
     c     FakNrN        setll     FAK9
     c     FakNrN        reade     FAK9                                   33
     c     *in33         Doweq     *off
     c     FAKDA         ifne      'K'
     c                   move      fakunr        BIKUNR
     c                   move      fakunr        kunr2
     c                   leave
     c                   end
     c     FakNrN        reade     FAK9                                   33
     c                   end
     c                   end
     c                   end
     C     CUNDL01K      CHAIN     Cundl01                            33
     c                   end
      *
     C                   callp     updHTMLvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
     C                   callp     updHtmlVar('WSNA':KNAVN)
      * Søkeparam
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(DATONF:'  .  .  ')))
     C                   callp     updHTMLvar('DATOT':
     C                             %trim(%editw(DATONT:'  .  .  ')))
     C                   callp     updHTMLvar('FAKNR':FAKNR)
     C                   callp     updHTMLvar('Kunr2':Kunr2)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   select
     c     ArType        wheneq    'fa'
     c                   eval      ARFAK = ARUNDE + '  Single faktura.'
     c     ArType        wheneq    'fx'
     c                   eval      ARFAK = ARUNDE + '  Samlefak. spesifikasjon.'
     c     ArType        wheneq    'fs'
     c                   eval      ARFAK = ARUNDE + '  Samlefaktura'
     C                   other
     c                   eval      ARFAK = ARUNDE + '  Faktura'
     C                   endsl
     C                   callp     updHTMLvar('ARFAK':ARFAK)
     C                   callp     updHTMLvar('FAKNR':ARUNDE)
     C                   callp     updHTMLvar('ARDATE':
     C                             %trim(%editw(ARDATE:'    .  .  ')))
     C                   exsr      FakturaURL
     C                   callp     updHTMLvar('UPDF':UPDF)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
     C*  Select a Recd
      *=====================================================================******
     C     RecordSlt     begsr
     C                   move      'N'           selected          1
     C                   move      'Y'           selected
     C                   IF        *IN33 AND *IN33 = *OFF
     C                   MOVE      'N'           SELECTED
     C                   END
      *
     C     RecordSltX    tag
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   IF        XSPRÅK = 'EN'
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/ESARKF1EN.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
     C                   ELSE
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/ESARKF1.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
     C                   END
      *
     C                   endsr
      *=====================================================================******
      * HENT ORDRE
      *=====================================================================******
     C     HENTFL        BEGSR
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
     c                   Move      'fa'          ARTYPE
      *
     c     StrLoop1      tag
     c                   Move      FraDt8        ARDATE
     c                   Move      *blanks       ARUNDE
     C     FakNrN        Ifne      *zeros
     c                   MoveL     FakNrN        ARUNDE
     c                   end
      *
     C                   select
     C     FakNrN        Whenne    *zeros
     C     FakKey        SETLL     ARKIVL04
     c     FakKey        Reade     ARKIVL04                               35
     c                   other
     c     FraKey        setLL     ARKIVL05
     c     FrakeyE       ReadE     ARKIVL05                               35
     c                   endsl
      *
     C     *in35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTPL
     C     FakNrN        Ifeq      *zeros
     C     TilDT8        andne     *zeros
     C     ARDATE        andgt     TilDT8
     C                   leave
     C                   end
     C                   exsr      RecordSlt
     C     selected      ifeq      'Y'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTPL
     C                   endif
      *
      * ved spørring på spesifikt nr - hopp ut etter første ...
     C                   select
     C     FakNrN        Whenne    *zeros
     c                   leave
     c                   other
      * manipuler Utype slik at alle poster med samme(s.fakt) nr hoppes over
     C                   Move      '.'           ARUNDE
     c     FraKey        setLL     ARKIVL05
     c     FrakeyE       ReadE     ARKIVL05                               35
     c                   endsl
     C                   ENDDO                                                  *hival
      * Først single fakturaer / samle / samle spesifikasjon
     C                   select
     c     Artype        Wheneq    'fa'
     c                   move      'fs'          Artype
     c                   goto      StrLoop1
     c     Artype        Wheneq    'fs'
     c                   move      'fx'          Artype
     c                   goto      StrLoop1
     c                   endsl
      *
     C     SLHENTPL      ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
OddEv *
     C                   open      BRPARF                               50
     C                   OPEN      CUNDL01
     C                   OPEN      arkivl04
     C                   OPEN      arkivl05
     C                   OPEN      firmarc
     C                   OPEN      FAK9
      *
     C     FakKey        KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C                   KFLD                    arunde
     C     FraKey        KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C                   KFLD                    ardate
     C                   KFLD                    arunde
     C     FraKeyE       KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     CUNDL01K      CHAIN     CUNDL01                            50
bhr   * Spesialvri for å kunne deme uten å avsløre kundenavn
bhr  c                   MOVEL     WSUSER        tstjov            4
bhr  c     tstjov        ifeq      'JOVO'
bhr  c                   eval      KNAVN  = 'Jan Ottar Valderhaug'
bhr  c                   end
      *
     c                   read      firmarc                                41
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRPARF
     C                   CLOSE     arkivl04
     C                   CLOSE     arkivl05
     C                   CLOSE     firmarc
     C                   CLOSE     FAK9
     C                   close     CUNDL01
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
      ***********************************************
     C     FakturaURL    BEGSR
      ***********************************************
      * genererer bane for lagring (0 i avd/opd ved samlefakt..)
     C                   MOVE      *ZEROS        AAVD              4
     C                   MOVE      *ZEROS        AOPD              7
     c     ArType        ifeq      'fa'
     C                   MOVE      ARAVD         AAVD
     C                   MOVE      AROPD         AOPD
     C                   end
     C                   move      *blanks       UPDF
     c                   movel     '<a href="'   UPDF            200
     C     UPDF          CAT       '/':0         UPDF
     C     UPDF          CAT       arcane:0      UPDF
     C     UPDF          CAT       '/':0         UPDF
     C     ARLINK        IFNE      *Blanks
     C     UPDF          CAT       ArLink:0      UPDF
     c                   else
     C     UPDF          CAT       aavd:0        UPDF
      * TEST LINK
     C                   EVAL      UXLINK = '/' + %TRIM(ARCANE)
     C                             + '/' + AAVD + AOPD
     C                             + %TRIM(ARTYPE)
     C                             + %TRIM(ARUNDE)
     C                             + '.pdf'
     C                   MOVE      'J'           UXOK
     C                   CALL      'CHECKLNK'
     C                   PARM                    UXLINK           50
     C                   PARM                    UXOK              1
      *
     C                   IF        UXOK = 'N'
      * GAMMEL DOKUMENT MED 5 SIFRE OPPDRAGSNR
     C                   MOVE      aopd          AOPD5             5
     C                   CAT       aopd5:0       UPDF
     C                   ELSE
     C     UPDF          CAT       aopd:0        UPDF
     C                   END
     C     UPDF          CAT       artype:0      UPDF
     C     UPDF          CAT       arunde:0      UPDF
     c                   end
     C     '.pdf'        SCAN      updf                                   33
     C  N33'.PDF'        SCAN      updf                                   33
     C     *in33         ifeq      *off
     C     UPDF          CAT       '.pdf" ':0    UPDF
     C                   else
     C     UPDF          CAT       '" ':0        UPDF
     C                   end
     C*                  CAT       'target="Sy':0UPDF
     C*                  CAT       'PDFWind">':0 UPDF
     C                   CAT       'target=':0   UPDF
     C                   CAT       '"_blank">':0 UPDF
     C     UPDF          CAT       '<img src="':0UPDF
     C     UPDF          CAT       '/syspeddev':0UPDF
     C     UPDF          CAT       '/PDF.gif">':0UPDF
     c                   endsr
