********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
********************************************************************
BHR   * Diverse endringer...
      * OBS!!! BASERT PÅ IFTSTA FTA HSD-> F.MAAS
      *    KODE 41 = UTKJØRT  , KODE 21 = LEVERT (POD...)
      * OBS2!! TAR HØYDE FOR AT EN KAN MOTTA BÅDE POD NAVN OG
      *    URL TIL GRAFISK LAGRET SIGNATUR (BASERT PÅ RAMBERG/GEM SYS)
      *****************************************************************
     H DATEDIT(*YMD)
     FFIRMARC   IF   E           K DISK
     FEDIM      UF   E           K DISK
     FECNI97A   IF   E           K DISK
     FESTS97A   IF   E           K DISK
     FERFF97A   IF   E           K DISK
     FEDTM97A   IF   E           K DISK
     FEFTX97A   IF   E           K DISK
     FEPCI97A   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FDOKUF7    UF   E           K DISK
     FDOKUF     IF   E           K DISK
     F                                     RENAME(DOKUFR:DOKUFRXX)
     FHEADF     UF   E           K DISK
     FHEAD39    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD39R)
     FDOKUFM    UF   E           K DISK
     FDOKUFM1   UF   E           K DISK
     F                                     RENAME(DOKUFMR:DOKUF1R)
     FTRACKF    O  A E           K DISK
     FTRACKT    UF   E           K DISK
     D                 DS
     D  C1004                  1     35
     D  C1004B                11     20
     D  C1004C                18     20
     D                 DS
     D  D2380                  1     35
     D  XXDATA                 1      8
     D  XXTIDA                 9     12
     D                 DS
     D  FMMRK1                 1     35
     D  FMCLID                 1     18
     D                 DS
     D  DFMÅL                  1     17
     D  DFDTMO                 1      8
     D  DFKLMO                 9     12
     D  DFSGM                 13     17
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D*                                       1  70 TTTEXT
     D  TTTDS                  1     70
     D  TXDATE                 1      5
     D  XXDATO                 7     14
     D  TXTIME                17     19
     D  XXKLOK                21     24
     D  TXBY                  27     29
BHR   * Kvitterinslinja på PDA kan være inntil 35 tegn...
BHR   * OFRER logging av xxx av xxx kolli - kvitteres på FBR.NIVÅ!
bhr  D* XXSIGN                31     53
bhr  D  XXSIGN                31     65
bhr  D* TXSTKO                54     57
bhr  D* XXSTKO                59     61
bhr  D* ZLEV                  62     64
bhr  D* ZLEV1                 62     62
bhr  D* ZLEV2                 63     63
bhr  D* TXAV                  66     67
bhr  D* ZTOT                  68     70
bhr  D* ZTOT1                 68     68
bhr  D* ZTOT2                 69     69
BHR   * Dummy DS for at feltene skal være definert
bhr  D                 DS
bhr  D  TXSTKO                54     57
bhr  D  XXSTKO                59     61
bhr  D  ZLEV                  62     64
bhr  D  ZLEV1                 62     62
bhr  D  ZLEV2                 63     63
bhr  D  TXAV                  66     67
bhr  D  ZTOT                  68     70
bhr  D  ZTOT1                 68     68
bhr  D  ZTOT2                 69     69
     D                 DS
     D  DFBREF                 1     17
     D  DFBRTX                 1      5
     D  DFBRAV                 6      9
     D  DFBRST                10     10
     D  DFBROP                11     17
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *
      *
     C                   EXSR      STATUS
      *
     C     MMN           CHAIN     EDIM                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'F'           MST
     C                   UPDATE    EDIMR
     C                   END
      *
     C     FEIL          TAG
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
      *                                                     STATUS/
      *////////////////////////////////////////////////////////////
     C     STATUS        BEGSR
      *
      * LES ALLE CNI-REPETISJONER
     C     MMN           SETLL     ECNI97A
     C     MMN           READE     ECNI97A                                33
     C     *IN33         DOWEQ     '0'
      *
      * HENT/LOGG DATA FOR EN SENDING
     C                   EXSR      ENCNI
      *
     C     MMN           READE     ECNI97A                                33
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    ENCNI  /
      *////////////////////////////////////////////////////////////
     C     ENCNI         BEGSR
     C                   MOVE      *BLANKS       DFBREF
      * FINNES AKTUELL SENDING (som f.brev eller ren innland)
     C                   MOVEL     C1004         SENDNR           35
     C     SENDNR        CHAIN(N)  DOKUF7                             34
     c     *in34         IFEQ      '0'
     C                   MOVE      DFAVD         HEAVD
     C                   MOVE      DFOPD         HEOPD
     c                   else
      * Kan finnes som ren innland (hvis 17 langt nr, legg til 401)
     C     C1004B        IFNE      *BLANKS
     C     C1004C        ANDEQ     *BLANKS
     C                   MOVEL     C1004         SEND17           17
     c                   eval      SENDNR = '401'+send17
     C                   END
     C                   MOVEL     SENDNR        SENDNR20         20
     C     SENDNR20      chain     Head39                             34
     C     *IN34         CABEQ     '1'           NXTCNI
     C  N34              MOVE      HEAVD         DFAVD
     C  N34              MOVE      HEOPD         DFOPD
     C  N34              Z-ADD     1             DFFBNR
     C  N34              Z-ADD     HENT          DFNT
     c                   end
      *
     C     *IN34         CABEQ     '1'           NXTCNI
      *
      * STATUS-EVENT (KODE, NORMALT 22 - SLUTT-LEVERT)
     C     KEY5          CHAIN     ESTS97A                            34
     C     *IN34         CABEQ     '1'           NXTCNI
     C                   MOVEL     S9011         XXSTKO            3
      * SIGNERT AV,,,
     C                   MOVE      *BLANKS       XXSIGN
     C                   MOVEL     'X'           XXSIGN
bhr   * BHR / transportnett har et sett med privatdefinerte koder
bhr  c**                 EXSR      BHRKOD
bhr   * TRANSPORTNETT LEGGER NAVN / AVVIK MM. I REASON...
bhr  C     S9012A        IFNE      *BLANKS
bhr  C                   MOVEL     S9012A        XXSIGN
bhr  C                   END
     C     KEY5          SETLL     ERFF97A
     C     KEY5          READE     ERFF97A                                35
     C     *IN35         DOWEQ     '0'
     C     R1153         IFEQ      'ZSN'
     C                   MOVEL     R1154         XXSIGN
     C                   END
     C     KEY5          READE     ERFF97A                                35
     C                   END
      * DATO/KLOKKA
     C                   MOVE      *ZEROS        XNDATO            8 0
     C                   MOVE      *ZEROS        XNKLOK            4 0
     C     KEY5          CHAIN     EDTM97A                            34
     C     *IN34         IFEQ      '0'
     C                   TESTN                   XXDATA               35          ÅÅMMDD
     C     *IN35         IFEQ      '1'
     C                   MOVE      XXDATA        XNDATO
     C                   END
     C                   TESTN                   XXTIDA               35
     C     *IN35         IFEQ      '1'
     C                   MOVE      XXTIDA        XNKLOK
     C                   END
     C                   END
      *
      * andre statuser enn POD
     C     XXSTKO        IFNE      '21 '
     C     XXSTKO        ANDNE     'ZPI'
     C     XXSTKO        ANDNE     'Z25'
     C                   GOTO      UTOPD
     C                   END
      *
      * DET ER EN POD SOM ER MOTTATT - LOGG ....
      *
      * ER ENKELT-KOLLI OPPGITT
     C                   MOVE      'N'           DETKOL            1
     C     KEY5          SETLL     EPCI97A
     C     KEY5          READE     EPCI97A                                35
     C     *IN35         DOWEQ     '0'
     C     P4233         IFEQ      '18'
     C     P7102A        IFNE      *BLANKS
     C                   MOVEL     P7102A        KOLLID           20
     C                   EXSR      KOLLST
     C                   END
     C     P7102B        IFNE      *BLANKS
     C                   MOVEL     P7102B        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102C        IFNE      *BLANKS
     C                   MOVEL     P7102C        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102D        IFNE      *BLANKS
     C                   MOVEL     P7102D        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102E        IFNE      *BLANKS
     C                   MOVEL     P7102E        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102F        IFNE      *BLANKS
     C                   MOVEL     P7102F        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102G        IFNE      *BLANKS
     C                   MOVEL     P7102G        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102H        IFNE      *BLANKS
     C                   MOVEL     P7102H        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102I        IFNE      *BLANKS
     C                   MOVEL     P7102I        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C     P7102J        IFNE      *BLANKS
     C                   MOVEL     P7102J        KOLLID
     C                   EXSR      KOLLST
     C                   END
     C                   END
     C     KEY5          READE     EPCI97A                                35
     C                   END
      *
      * HVIS DETALJKOLLI IKKE  OPPGITT - SETT X PÅ ALLE KLL
     C     DETKOL        IFEQ      'N'
     C                   EXSR      KOLLS2
     C                   END
      *
     C     UTOPD         TAG
      *
      * FINN ANTALL LEVERT/TOTAL OPPDATER STATUSER
     C                   EXSR      KOLL2
      *
     C     NXTCNI        TAG
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
      *                                                    KOLLST /
      *////////////////////////////////////////////////////////////
     C     KOLLST        BEGSR
     C                   MOVE      KOLLID        KOLL18           18
     C                   MOVEL     KOLL18        KOLL35           35
      * SETT X=LEVERT PÅ AKTUELT KOLLI
     C     KOLL35        SETLL     DOKUFM1
     C                   READ      DOKUFM1                                33
     C     *IN33         IFEQ      '0'
     C     FMCLID        ANDEQ     KOLL18
     C     FMAVD         ANDEQ     HEAVD
     C     FMOPD         ANDEQ     HEOPD
     C                   MOVE      'J'           DETKOL
     C                   MOVE      'X'           FMLEVE
     C                   UPDATE    DOKUF1R
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                    KOLLS2 /
      *////////////////////////////////////////////////////////////
     C     KOLLS2        BEGSR
      * SETT X=LEVERT I ALLE KOLLI PÅ AKTUELT OPPDRAG.
     C     FMKEY         SETLL     DOKUFM
     C     FMKEY         READE     DOKUFM                                 35
     C     *IN35         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   MOVE      'X'           FMLEVE
     C                   END
     C                   UPDATE    DOKUFMR
     C     FMKEY         READE     DOKUFM                                 35
     C                   END
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    KOLL2  /
      *////////////////////////////////////////////////////////////
     C     KOLL2         BEGSR
     C                   MOVE      *ZEROS        LEV               3 0
     C                   MOVE      *ZEROS        TOT               3 0
      * FINN UT OM ALLE HAR X I FELT FOR LEVERT .
     C     FMKEY         SETLL     DOKUFM
     C     FMKEY         READE     DOKUFM                                 35
     C     *IN35         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   ADD       1             TOT
     C     FMLEVE        IFEQ      'X'
     C                   ADD       1             LEV
     C                   END
     C                   END
     C     FMKEY         READE     DOKUFM                                 35
     C                   END
      *
      *
      * SKRIV TIL TRACK & TRACE...
     C     FIURBL        IFEQ      'J'
     C                   EXSR      TTSR
     C                   END
     C*
      * KUN KODE 21 - LEVERT, SKAL OPPDATERE POD-STAUS
      * (ZPI =POD,POSTEN,  Z25=POD, TRANSPORTNETT.)
     C     XXSTKO        IFNE      '21'
     C     XXSTKO        ANDNE     'ZPI'
     C     XXSTKO        ANDNE     'Z25'
     C                   GOTO      UTLOGG
     C                   END
      *
      * DET ER POD SOM ER REISTRERT....
      * OPPDATER PÅ DOKUF (MINST ETT KOLLI LEVERT - MERK LEVERT...)
     C     SENDNR        CHAIN     DOKUF7                             33
     C     *IN33         IFEQ      '0'
     C     DFSGM         IFEQ      *BLANKS
     C                   MOVEL     XXSIGN        DFSGM
     C                   MOVEL     XNDATO        DFDTMO
     C                   MOVEL     XNKLOK        DFKLMO
     C                   END
     C                   UPDATE    DOKUFR
     C                   END
      *
      * SJEKK OM ALLE FRAKTBREVENE PÅ OPPDRAGET ER KVITTERT
      * (LEV/TOT PÅ OPPDR.NIVÅ BRUKES OM ANT. SENDINGER)
     C                   MOVE      *ZEROS        LEV               3 0
     C                   MOVE      *ZEROS        TOT               3 0
     C     DOKUFK        SETLL     DOKUF
     C     DOKUFK        READE     DOKUF                                  33
     C     *IN33         DOWEQ     '0'
     C     DFMÅL         CABEQ     *BLANKS       UTLOGG
     C                   ADD       1             TOT
     C                   ADD       1             LEV
     C     DOKUFK        READE     DOKUF                                  33
     C                   END
      *
      * FØRST NÅ ALL FBR. OK SETT LEVERT PÅ HEADF (OM IKKE GJORT FØR.)
     C     HEAKEY        CHAIN(N)  HEADF                              33
     C     *IN33         IFEQ      '0'
      * IKKE MELDT FØR....
     C     HESGM         IFEQ      *BLANKS
     C     HEAKEY        CHAIN     HEADF                              33
     C                   MOVEL     XXSIGN        HESGM
     C                   MOVEL     XNDATO        HEDTMO
     C                   MOVEL     XNKLOK        HEKLMO
     C  N33              UPDATE    HEADFR
     C     FIURBL        IFEQ      'J'
     C                   MOVE      *ZEROS        DFFBNR
     C                   EXSR      TTSR
     C                   END
     C                   ELSE
      * MELDT FØR....(SUPPLERING ETTER TIDLIGERE MANKO.)
      * HER LEGGES INN HANDLING VED ETTERRAPPORT AV TIDL. MELDT MANKO
      * ...
      * ...
     C                   END
      * MANKO (FREMDELES...)
     C     LEV           IFNE      TOT
     C* HER LEGGS INN "MANKOHANDLING"....
      * ...
      * ...
     C                   END
      *
     C                   END
      *
     C     UTLOGG        ENDSR
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
     C     STARTTT       TAG
      * LEVERT 0 AV 0 - BETYR AT DET IKKE VAR NOEN KOLLI PÅ
      * SENDINGEN(SOM MATCHET),HENT ANTALL FRA FRAKTBREV, SETT ALT LEV.
     C     LEV           IFEQ      *ZEROS
     C     TOT           ANDEQ     *ZEROS
     C                   Z-ADD     DFNT          LEV
     C                   Z-ADD     DFNT          TOT
     C                   END
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      DFFBNR        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      'POD'         TTACTI
     C     XXSTKO        IFNE      '21'
     C     XXSTKO        ANDNE     'ZPI'
     C     XXSTKO        ANDNE     'Z25'
     C                   MOVE      'PO0'         TTACTI
     C                   END
     C                   MOVE      XNDATO        TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVEL     XNKLOK        TTTIME
     C                   MOVE      ULDT          TTDATL
     C                   MOVE      XULTM         TTTIML
     C                   MOVEL     'SY400USR'    TTUSER
     C                   MOVE      'Date:'       TXDATE
     C                   MOVE      XNDATO        XXDATO
     C                   MOVE      'Ti:'         TXTIME
     C                   MOVE      XNKLOK        XXKLOK
     C                   MOVE      'Sts:'        TXSTKO
     C                   MOVE      'By:'         TXBY
     C                   MOVE      LEV           ZLEV
     C     ZLEV1         IFEQ      '0'
     C                   MOVE      ' '           ZLEV1
     C     ZLEV2         IFEQ      '0'
     C                   MOVE      ' '           ZLEV2
     C                   END
     C                   END
s01  C*                  MOVE      'Av'          TXAV
n01  C                   MOVE      'Of'          TXAV
     C                   MOVE      TOT           ZTOT
     C     ZTOT1         IFEQ      '0'
     C                   MOVE      ' '           ZTOT1
     C     ZTOT2         IFEQ      '0'
     C                   MOVE      ' '           ZTOT2
     C                   END
     C                   END
     C                   MOVE      TTTDS         TTTEXT
n01  C                   MOVE      'Dato:'       TXDATE
n01  C                   MOVE      'Av:'         TXBY
n01  C                   MOVE      'Av'          TXAV
n01  C                   MOVE      TTTDS         TTTEXL
     C                   MOVEL     XXSIGN        TTNAME
     C                   MOVEL     XXSTKO        TTEDEV
     C                   WRITE     TRACKFR
      * URL....
BHR   *
BHR   * legg ut egne poster for kode AVV (avvik..)
BHR   *
BHR  C     XXSTKO        IFEQ      'Z26'
BHR  C     XXSTKO        OREQ      'Z28'
BHR  C     XXSTKO        OREQ      'Z29'
BHR  C     XXSTKO        OREQ      'Z30'
BHR  C     XXSTKO        OREQ      'Z31'
BHR  C                   MOVE      'AVV'         TTACTI
BHR  C                   MOVE      *blanks       TTTEXT
BHR  c                   Select
BHR  C     XXSTKO        WHENEQ    'Z26'
s01  c*                  eval      TTTEXT = 'Bomtur'
n01  c                   eval      TTTEXT = 'Not at home'
n01  c                   eval      TTTEXL = 'Bomtur'
BHR  C     XXSTKO        WHENEQ    'Z28'
BHR  c                   eval      TTTEXT = S9012A
BHR  C     XXSTKO        WHENEQ    'Z29'
s01  c*                  eval      TTTEXT = 'Ventetid: ' + S9012A
n01  c                   eval      TTTEXT = 'Waitingtime: ' + S9012A
n01  c                   eval      TTTEXL = 'Ventetid: ' + S9012A
BHR  C     XXSTKO        WHENEQ    'Z30'
s01  c*                  eval      TTTEXT = 'Ant utv.paller: ' + S9012A
s01  c                   eval      TTTEXT = 'Nbr of pallets: ' + S9012A
s01  c                   eval      TTTEXL = 'Ant utv.paller: ' + S9012A
BHR  C     XXSTKO        WHENEQ    'Z31'
s01  c*                  eval      TTTEXT = 'Levert ant kll: '
s01  c*                            + S9012A
n01  c                   eval      TTTEXT = 'Delivered nbr of cll:'
n01  c                             + S9012A
n01  c                   eval      TTTEXL = 'Levert ant kll: '
n01  c                             + S9012A
BHR  c                   endsl
BHR  C                   WRITE     TRACKFR
BHR   * send melding til kundesenteret...
BHR  C     XXSTKO        IFEQ      'Z28'
BHR  C     XXSTKO        OREQ      'Z31'
BHR  C                   eval      Avvik  = TTText
BHR  C                   eval      Merknad= ' '
BHR  C                   else
BHR  C                   eval      Avvik  = ' '
BHR  C                   eval      Merknad= TTText
BHR  C                   end
bhr  C                   MOVEL     C1004         SEND17           17
bhr  c     HeaKey        CHAIN(N)  HEADF                              33
BHR  C                   Call      'E162AVVIK'
BHR  C                   PARM                    SEND17
BHR  C                   PARM                    HENAS
BHR  C                   PARM                    HENAK
BHR  C                   PARM                    HEADK1
BHR  C                   PARM                    HEADK3
BHR  C                   PARM                    MERKNAD          70
BHR  C                   PARM                    AVVIK            70
BHR  C                   end
BHR   *
      * OPPDATER ATA (MEN KUN VED FØRSTE LEVERING. )
     C     DFFBNR        IFEQ      *ZEROS
     C     HEAKEY        CHAIN     TRACKT                             33
     C     *IN33         IFEQ      *OFF
     C     XXSTKO        IFEQ      '21 '
     C     XXSTKO        OREQ      'ZPI'
     C     XXSTKO        OREQ      'Z25'
     C     TXATAD        IFEQ      *ZEROS
     C                   MOVE      XNDATO        TXATAD
     C                   MOVE      XNKLOK        TXATAK
     C                   END
     C                   END
     C                   UPDATE    TRACKTR
     C                   END
     C                   END
      * HAR NETTOPP UTFØRT LOGG PÅ ET DUP-FRAKTBREV - OPPDATER OGSÅ DISTR.ORD
     C     DFBRTX        IFEQ      'U.op:'
     C     DFBRAV        ANDGE     '0001'
     C     DFBRAV        ANDLE     '9999'
     C     DFBROP        ANDGE     '0000001'
     C     DFBROP        ANDLE     '9999999'
     c                   move      heavd         gmavd             4 0
     c                   move      heopd         gmopd             7 0
     c                   z-add     dfnt          gmnt              5 0
     c                   move      dffbnr        gmfbnr            3 0
      *
     C                   MOVE      DFBRAV        HEAVD
     C                   MOVE      DFBROP        HEOPD
     C                   Z-add     HENT          DFNT
     C                   move      *zeros        DFFBNR
     C                   MOVE      *BLANKS       DFBREF
     C     TTACTI        IFEQ      'POD'
      * oppdater LEVERT i distr.oppdraget
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         IFEQ      '0'
     C     HESGM         IFEQ      *BLANKS
     C                   MOVEL     XXSIGN        HESGM
     C                   MOVEL     XNDATO        HEDTMO
     C                   MOVEL     XNKLOK        HEKLMO
     C                   END
     C                   UPDATE    HEADFR
     C                   END
     C                   END
     C                   goto      startTT
     C                   END
     C*  LEGG TILBAKE EVT ENDREDE NØKKELFELT
     C     Gmopd         Ifne      *zeros
     c                   move      gmavd         heavd
     c                   move      gmopd         heopd
     c                   z-add     gmnt          dfnt
     c                   move      gmfbnr        dffbnr
     C     HEAKEY        CHAIN(N)  HEADF                              33
     c                   end
     c                   move      *zeros        gmavd             4 0
     c                   move      *zeros        gmopd             7 0
     c                   z-add     *zeros        gmnt              5 0
     c                   move      *zeros        gmfbnr            3 0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          MMN
     C     KEY5          KLIST
     C                   KFLD                    MMN
     C                   KFLD                    YGN
     C                   KFLD                    CREP0
     C     *LIKE         DEFINE    CGN           YGN
     C                   Z-ADD     5             YGN
     C     FMKEY         KLIST
     C                   KFLD                    XXRI              1
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   MOVE      'F'           XXRI
     C     DOKUFK        KLIST
     C                   KFLD                    XXRI              1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      'F'           XXRI
     C     HEAKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     FIFIRM        CHAIN     FIRMARC                            33
      *
     C                   ENDSR
