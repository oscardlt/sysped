      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE UNITREGISTER                 ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-87 95-97       ***
      **************************************************************
      *
     FUNITL01   IF   E           K DISK
     F                                     RENAME(UNITR:UNITR1)
     FUNITL02   IF   E           K DISK
     F                                     RENAME(UNITR:UNITR2)
     FUNIT      IF   E           K DISK
     FTURER2    IF   E           K DISK
     FTRAN      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYTR19FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSDATF                 1      6  0
     D  WSDTFD                 1      2  0
     D  WSDTFM                 3      4  0
     D  WSDTFÅ                 5      6  0
     D                 DS
     D  TUDT                   1      8  0
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D                 DS
     D  WSDATT                 1      6  0
     D  WSDTTD                 1      2  0
     D  WSDTTM                 3      4  0
     D  WSDTTÅ                 5      6  0
     D                 DS
     D  TUDTT                  1      8  0
     D  TUDTTÅ                 1      4  0
     D  TUDTTM                 5      6  0
     D  TUDTTD                 7      8  0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2TRAN        X2TRAN            8 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   51W2BILN        SETLL     UNITL01
     C   52W2UNTY        SETLL     UNITL02
     C   53X2TRAN        SETLL     UNIT
     C     START1        TAG
     C   51              READ      UNITL01                                94
     C   52              READ      UNITL02                                94
     C   53X2TRAN        READE     UNIT                                   94
     C  N94              MOVE      UNBILN        XBILN            10
     C  N94              MOVE      UNTRAN        XTRAN             8 0
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        6
     C                   ADD       1             ZRECNO
     C     LEVEK         CHAIN     TRAN                               44
     C  N44              MOVEL     VMNAVN        WSNAVN
     C   44              MOVE      *BLANKS       WSNAVN
     C                   EXSR      TUR
     C                   WRITE     SUBFIL
     C   51              READ      UNITL01                                94
     C   52              READ      UNITL02                                94
     C   53X2TRAN        READE     UNIT                                   94
     C  N94              MOVE      UNBILN        XBILN            10
     C  N94              MOVE      UNTRAN        XTRAN             8 0
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     *IN29         IFEQ      '0'
     C     W2BILN        ANDEQ     *BLANKS
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   MOVE      '1'           *IN29
     C                   ELSE
     C                   MOVE      '1'           *IN29
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9899
      *
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** SØK
     C   04              DO
     C                   MOVE      X2TRAN        W2TRAN
     C                   CALL      'SYTR04A'
     C                   PARM                    X2TRAN
     C     X2TRAN        IFEQ      W2TRAN
     C                   GOTO      START3
     C                   ELSE
     C                   MOVE      X2TRAN        W2TRAN
     C                   MOVEA     '001'         *IN(51)
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C*** FRISK OPP
     C   05              GOTO      START
      *
      *** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94
     CAN 53UNITKX        CHAIN     UNIT                               94
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C     W2BILN        ANDNE     *BLANKS
     C                   MOVE      W2BILN        UBILNR
     C                   GOTO      SLUTT
     C                   END
     C                   SELECT
     C     W2BILN        WHENNE    *BLANKS
     C                   MOVEA     '100'         *IN(51)
     C                   GOTO      START
     C     W2UNTY        WHENNE    *BLANKS
     C                   MOVEA     '010'         *IN(51)
     C                   GOTO      START
     C     W2TRAN        WHENNE    0
     C                   MOVEA     '001'         *IN(51)
     C                   GOTO      START
     C                   ENDSL
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       9192
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'                                          PLUKK
     C                   MOVE      UNBILN        UBILNR
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UBILNR           10
      *
     C     UNITKX        KLIST
     C                   KFLD                    XTRAN
     C                   KFLD                    XBILN
     C     TURKEY        KLIST
     C                   KFLD                    UNBILN
     C                   KFLD                    U99               8 0
     C                   MOVE      99999999      U99
     C                   MOVE      *ZEROS        WSDATF
     C                   MOVE      *ZEROS        WSDATT
      *
     C     LEVEK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    UNTRAN
     C                   READ      FIRM                                   33
      *
     C                   MOVE      UBILNR        W2BILN
     C                   MOVEA     '100'         *IN(51)
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
     C***********************************************
     C     TUR           BEGSR
     C***********************************************
     C                   SETOFF                                       88
     C     TURKEY        SETLL     TURER2
     C     UNBILN        READPE    TURER2                                 88
     C     *IN88         IFEQ      '0'
     C                   MOVE      TUDTTD        WSDTTD
     C                   MOVE      TUDTTM        WSDTTM
     C                   MOVE      TUDTTÅ        WSDTTÅ
     C                   MOVE      TUDTD         WSDTFD
     C                   MOVE      TUDTM         WSDTFM
     C                   MOVE      TUDTÅ         WSDTFÅ
     C                   ELSE
     C                   MOVE      *ZEROS        WSDATF
     C                   MOVE      *ZEROS        WSDATT
     C                   END
     C                   ENDSR
