     FLL031D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     FKUNKO2    IF   E           K DISK
     FDOKUF     IF   E           K DISK
     FLLHEAD    IF   E           K DISK
     FLLHEAD02  IF   E           K DISK
     F                                     RENAME(LLHEADR:LLHEADR2)
     FLLHEAD03  IF   E           K DISK
     F                                     RENAME(LLHEADR:LLHEADR3)
     FLLHEAD04  IF   E           K DISK
     F                                     RENAME(LLHEADR:LLHEADR4)
     FLLHEAD05  IF   E           K DISK
     F                                     RENAME(LLHEADR:LLHEADR5)
     FLLHEAD06  IF   E           K DISK
     F                                     RENAME(LLHEADR:LLHEADR6)
     FLLHEAD07  IF   E           K DISK
     F                                     RENAME(LLHEADR:LLHEADR7)
     FLLHETXT   IF   E           K DISK
     D EX              S              7  0 DIM(900)
     D                 DS
     D  DF1004                 1     35
     D  XX1004                 1     21
     D                 DS
     D  LHBNA                  1     30
     D  XHBNA                  1     16
     D                 DS
     D  WSTDT                  1      8  0
     D  WSTDTÅ                 1      4  0
     D  WSTDTM                 5      6  0
     D  WSTDTD                 7      8  0
     D                 DS
     D  U1PARM                 1    256
     D  U1GRP                  1      8  0
     D  U1PNR                  9     16  0
     D  U1BNR                 17     33
     D  U1PAK                 34     50
     D  U1ORDR                51     67
     D  U1FLN                 68     84
     D  U1TDT                 85     92  0
     D  U1ALN                 93     99  0
     D  U1AVD                100    103  0
     D  U1OPD                104    110  0
     D  U1UT                 111    115
     D  U1FIRM               116    117
     D  U1FLAG               256    256
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *LIKE         DEFINE    WSTDT         WWTDT
     C     *LIKE         DEFINE    WSBNR         WWBNR
     C     *LIKE         DEFINE    WSALN         WWALN
     C     *LIKE         DEFINE    WSORDR        WWORDR
     C     *LIKE         DEFINE    LHFLN         WSFLN
     C     *LIKE         DEFINE    WSFLN         WWFLN
     C     *LIKE         DEFINE    WSPAK         WWPAK
     C     *LIKE         DEFINE    LHANT0        UANT0
     C     *LIKE         DEFINE    LHANTL        UANTL
     C     *LIKE         DEFINE    LHAEH1        UAEH1
     C     *LIKE         DEFINE    LHANT1        UANT1
     C     *LIKE         DEFINE    LHAEH2        UAEH2
     C     *LIKE         DEFINE    LHANT2        UANT2
     C     *LIKE         DEFINE    LHAEH3        UAEH3
     C     *LIKE         DEFINE    LHANT3        UANT3
     C     *LIKE         DEFINE    LHVEK         UVEK
     C     *LIKE         DEFINE    LHKUB         UKUB
     C     *LIKE         DEFINE    LHTLIN        UTLIN
      *
     C     *ENTRY        PLIST
     C     U1PARM        PARM      U1PARM        U2PARM          256
      *
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     LH02K         KLIST
     C                   KFLD                    WWALN
     C     LH03K         KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WWBNR
     C                   KFLD                    WWTDT
     C     LH03K1        KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WWBNR
     C     LH04K         KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WWPAK
     C                   KFLD                    WWTDT
     C     LH04K1        KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WWPAK
     C     LH05K         KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WWORDR
     C                   KFLD                    WWTDT
     C     LH05K1        KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WWORDR
     C     LH06K         KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WWFLN
     C                   KFLD                    WWTDT
     C     LH06K1        KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C                   KFLD                    WWFLN
     C     LH07K         KLIST
     C                   KFLD                    U1AVD
     C                   KFLD                    U1OPD
     C     LHKEY         KLIST
     C                   KFLD                    U1GRP
     C                   KFLD                    U1PNR
     C     KUNKOK        KLIST
     C                   KFLD                    LHPNR
     C                   KFLD                    KUINTN
     C     DOKUFK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    LHAVD
     C                   KFLD                    LHOPD
     C                   KFLD                    LHFBN
     C                   MOVE      'F'           DFRI
      *
     C                   MOVE      U1TDT         WSTDT
     C                   MOVE      U1BNR         WSBNR
     C                   MOVE      U1PAK         WSPAK
     C                   MOVE      U1ORDR        WSORDR
     C                   MOVE      U1FLN         WSFLN
     C                   MOVE      U1ALN         WSALN
     C                   MOVE      'N'           UOPTXT            1
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   EXSR      SETKEY
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C                   EXSR      LESREC
     C     *IN94         DOWEQ     '0'
      * FILTER...
     C                   EXSR      FILTER
     C     OMIT          CABEQ     'J'           NESTE
      * N61=FRITEKST FINNES - HIGHLIGHT KUNDES ORDRENR.
     C     LHMN          CHAIN     LLHETXT                            61
      *
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
      *
     C     NESTE         TAG
     C                   EXSR      LESREC
     C  N94              END
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   ELSE
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   END
      *
     C     STBILD        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     BLANKREC
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN03         IFEQ      '1'
     C                   MOVE      '3'           U1FLAG
     C                   GOTO      UTHOV
     C                   END
     C     *IN12         IFEQ      '1'
     C                   MOVE      '1'           U1FLAG
     C                   GOTO      UTHOV
     C                   END
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     WSTDT         IFNE      WWTDT
     C     WSBNR         ORNE      WWBNR
     C     WSPAK         ORNE      WWPAK
     C     WSORDR        ORNE      WWORDR
     C     WSFLN         ORNE      WWFLN
     C     WSALN         ORNE      WWALN
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C     *IN52         DOWEQ     '0'
     C                   READC     SUBFIL                                 52
     C  N52              EXSR      OPTION
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C                   GOTO      STBILD
      *
     C     UTHOV         ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     SETKEY        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      WSTDT         WWTDT
     C                   MOVE      WSBNR         WWBNR
     C                   MOVE      WSPAK         WWPAK
     C                   MOVE      WSORDR        WWORDR
     C                   MOVE      WSFLN         WWFLN
     C                   MOVE      WSALN         WWALN
      *
     C                   SELECT
     C     WSBNR         WHENNE    *BLANKS
     C     LH03K         SETLL     LLHEAD03
     C     WSPAK         WHENNE    *BLANKS
     C     LH04K         SETLL     LLHEAD05
     C     WSORDR        WHENNE    *BLANKS
     C     LH05K         SETLL     LLHEAD04
     C     WSFLN         WHENNE    *BLANKS
     C     LH06K         SETLL     LLHEAD06
     C     WSALN         WHENNE    *ZEROS
     C     LH02K         SETLL     LLHEAD02
     C                   OTHER
     C     LH07K         SETLL     LLHEAD07
     C                   ENDSL
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     LESREC        BEGSR
      *////////////////////////////////////////////////////////////
     C                   SELECT
     C     WWBNR         WHENNE    *BLANKS
     C     LH03K1        READE     LLHEAD03                               94
     C     WWPAK         WHENNE    *BLANKS
     C     LH04K1        READE     LLHEAD05                               94
     C     WWORDR        WHENNE    *BLANKS
     C     LH05K1        READE     LLHEAD04                               94
     C     WWFLN         WHENNE    *BLANKS
     C     LH06K1        READE     LLHEAD06                               94
     C     WWALN         WHENNE    *ZEROS
     C     LH02K         READE     LLHEAD02                               94
     C                   OTHER
     C     LH07K         READE     LLHEAD07                               94
     C                   ENDSL
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     FILTER        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      'N'           OMIT              1
      *
     C     1             DO        900           XN                3 0
     C     EX(XN)        IFEQ      0
     C                   LEAVE
     C                   ELSE
     C     EX(XN)        IFEQ      LHMN
     C                   MOVE      'J'           OMIT
     C                   LEAVE
     C                   END
     C                   END
     C                   END
      *
     C     WSALN         IFEQ      0
      * FILTER...
     C     WSTDT         IFNE      *ZEROS
     C     WSTDT         ANDGT     LHTDT
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C     WSBNR         IFNE      *BLANKS
     C     WSBNR         ANDNE     LHBNR
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C     WSPAK         IFNE      *BLANKS
     C     WSPAK         ANDNE     LHPAK
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C     WSORDR        IFNE      *BLANKS
     C     WSORDR        ANDNE     LHORDR
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C     WSFLN         IFNE      *BLANKS
     C     WSFLN         ANDNE     LHFLN
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C                   ELSE
      *
     C     WSALN         IFNE      LHALN
     C                   MOVE      'J'           OMIT
     C                   END
      *
     C                   END
      *
     C     UTFILT        ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
      * VIS HEADING ELLER LINER
     C     WSNR          IFEQ      '5'
     C     WSNR          OREQ      '7'
     C                   EXSR      VISLH
     C                   GOTO      SLOPT
     C                   END
      *
     C     SLOPT         ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     VISLH         BEGSR
      *////////////////////////////////////////////////////////////
     C     LHMN          CHAIN     LLHEAD                             33
     C     LHMN          CHAIN     LLHETXT                            61
      * HENT INTERNT KUNDENR/VADR FØR VISNING
     C     LHBNR         IFEQ      *BLANKS
     C                   MOVE      *ZEROS        KUKUN2
     C                   MOVE      *ZEROS        KUVADR
     C                   ELSE
     C                   MOVEL     LHBNR         KUINTN
     C     KUNKOK        CHAIN     KUNKO2                             90
     C                   END
     C     WSNR          CABEQ     '7'           STVIS2
     C     DOKUFK        CHAIN     DOKUF                              33
     C   33              MOVE      *BLANKS       DF1004
      *
     C     STVIS         TAG
     C                   EXFMT     VISREC
     C     *IN12         CABEQ     '1'           SLVIS
     C     *IN18         IFEQ      '1'
     C                   CALL      'LL035R'
     C                   PARM                    LHMN
     C                   PARM                    UOPTXT
     C                   GOTO      STVIS
     C                   END
      *
     C     STVIS2        TAG
     C                   EXSR      TILLN
     C     UFLAGG        CABNE     3             STVIS
      *
     C     SLVIS         TAG
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TILLN         BEGSR
      *////////////////////////////////////////////////////////////
     C                   Z-ADD     1             UFLAGG
     C                   CALL      'LL012R'
     C                   PARM                    LHMN
     C                   PARM                    U1FIRM
     C                   PARM                    U1PNV
     C                   PARM                    UANT0
     C                   PARM                    UANTL
     C                   PARM                    UAEH1
     C                   PARM                    UANT1
     C                   PARM                    UAEH2
     C                   PARM                    UANT2
     C                   PARM                    UAEH3
     C                   PARM                    UANT3
     C                   PARM                    UVEK
     C                   PARM                    UKUB
     C                   PARM                    UTLIN
     C                   PARM                    UKONTR            1
     C                   PARM                    UFLAGG            1 0
      *
     C                   ENDSR
      *
