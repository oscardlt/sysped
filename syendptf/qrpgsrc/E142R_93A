********************************************************************
10172 *  IFTMCS FRA SCHENKER
********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12031 * 01 JOV PTF11 NY STD MAPPING
********************************************************************
     FERFF93A   IF   E           K DISK
     FECNT93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FETCC93A   IF   E           K DISK
     FHEADF     UF   E           K DISK
     FTRACKF    O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N20              EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT93A                                51
     C     *IN51         DOWEQ     '0'
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     C6066         A
     C                   EXSR      GETNUM

     C                   SELECT
     C     C6069         WHENEQ    '11'
     C                   Z-ADD     ZNUM          XXNT              7 0
     C     C6069         WHENEQ    '7'
     C                   Z-ADD     ZNUM          XXVKT             9 0
     C     C6069         WHENEQ    'ZFV'
     C                   Z-ADD     ZNUM          XXFVKT            9 1
     C     C6069         WHENEQ    '15'
     C     C6411         IFEQ      'DMQ'
     C     ZNUM          DIV(H)    1000          XXM3              7 3
     C                   ELSE
     C                   Z-ADD     ZNUM          XXM3
     C                   END
     C     C6069         WHENEQ    'ZLM'
     C     C6411         IFEQ      'DMT'
     C     ZNUM          DIV(H)    10            XXLM              4 2
     C                   ELSE
     C                   Z-ADD     ZNUM          XXLM
     C                   END
     C                   ENDSL

     C  N51KEYMS0        READE     ECNT93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          ZMN

     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     OPDKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD

     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     c     *LIKE         DEFINE    HENT          GMNT
     c     *LIKE         DEFINE    HEVKT         GMVKT
     c     *LIKE         DEFINE    HEM3          GMM3
     c     *LIKE         DEFINE    HELM          GMLM
      * Get time
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA13                                               MOA13 /
      *////////////////////////////////////////////////////////////
     C     MOA13         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     13            ZGN2
     C     KEYMS2        SETLL     EMOA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EMOA93A                                53
     C     *IN53         DOWEQ     '0'

     C                   MOVEA     *BLANKS       A
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM

     C                   SELECT
     C     M5025         WHENEQ    '23'
     C                   Z-ADD     ZNUM          FrtAmt           11 2
     C                   ENDSL

     C  N53KEYMS2        READE     EMOA93A                                53
     C  N53              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD10                                               NAD10 /
      *////////////////////////////////////////////////////////////
     C     NAD10         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'

      *Les TCC under NAD
     C                   Z-ADD     NREP0         ZREPB0
     C                   EXSR      TCC13


     C     KEYMS0        READE     ENAD93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TCC13                                               TCC13 /
      *////////////////////////////////////////////////////////////
     C     TCC13         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     13            ZGN1
     C     KEYMS1        SETLL     ETCC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETCC93A                                52
     C     *IN52         DOWEQ     '0'

      *Les MOA under TCC
     C                   Z-ADD     TREP0         ZREPC0
     C                   Z-ADD     TREP1         ZREPC1
     C                   EXSR      MOA13

     C  N52KEYMS1        READE     ETCC93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED        RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   EXSR      CNT00
     C                   EXSR      NAD10
     C                   EXSR      UNT00
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNT00   Write to SYSPSED                            UNT0D /
      *////////////////////////////////////////////////////////////
     C     UNT00         BEGSR
      * fin oppdrag via RFF                       R1153        R1154
      *  Msg.no   Group no  Repet No Lvl 0  Re ! eference q  Reference number
      * 4915099       3              1         !    AAM      70716484913140263
      * 4915099       3              2         !    CO       1040/0044746/KO
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     3             ZGN0
     C     KEYMS0        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF93A                                51
     C     *IN51         DOWEQ     '0'

     C                   SELECT
     C     R1153         WHENEQ    'AAM'
     C                   MOVEL     R1154         SendNr           17
     C     R1153         WHENEQ    'CO '
     C                   MOVEL     R1154         AvdOPd           12
     c                   Movel     AvdOpd        HEAVD
     c                   Move      AvdOpd        HEOPD
     C                   ENDSL

     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *_____________
      * Update HEADF
      *"""""""""""""
     c                   move      'N'           OpdOpd            1
     c     OPDKEY        chain     Headf                              33
     c     *in33         ifeq      *off
      * mer enn 10 % økning i vekt eller M3 = oppdater
     c     HEVKT         mult      1.1           VKTgrense        11 1
     c     HEM3          mult      1.1           M3grense          9 4
     c                   if        XXVKT > VKTgrense
     c                             or XXM3 > M3grense
     c                   move      HENT          GMNT
     c                   move      HEVKT         GMVKT
     c                   move      HEM3          GMM3
     c                   move      HELM          GMLM
     c                   if        XXNT  > HENT
     c                   z-add     XXNT          HENT
     c                   endif
     c                   if        XXVKT > HEVKT
     c                   z-add     XXVKT         HEVKT
     c                   endif
     c                   if        XXM3  > HEM3
     c                   z-add     XXM3          HEM3
     c                   endif
     c                   if        XXLM  > HELM
     c                   z-add     XXLM          HELM
     c                   endif
     c                   move      'J'           OpdOpd
     c                   endif
     c                   update    Headfr
     c                   endif
      *________________
      * Write to TRACKF - VEKT M3 MM
      *""""""""""""""""
     C                   CLEAR                   TRACKFR
     C                   EVAL      TTACTI='ICS'
     C                   EVAL      TTAVD=HEAVD
     C                   EVAL      TTOPD=HEOPD
     C                   EVAL      TTFBNR=0
     C                   MOVEL     WDATO         TTDATE
     C                   MOVEL     WTID          TTTIME
     C                   MOVEL     TTDATE        TTDATL
     C                   MOVEL     TTTIME        TTTIML
     C                   EVAL      TTUSER='SYEDIUSR'
      * Ved minst en verdi endret   vis GM.. -> NY..
      * Vis også de verdier som ikke er endret (fordi  mindre)
      *     Bruker må bare vite at M3=1,35->1,1 IKKE har medført nedskriving av verdien
     c                   If        OpdOpd = 'J'                                 Oppdatert oppdrag
     C                   EVAL      TTTEXL='IFTMCS: '
     C                             +' Ant: '+ %trim(%editc(GMNT:'Z'))
     C                             +   '->' + %trim(%editc(XXNT:'Z'))
     C                             +' Vkt: '+ %trim(%editc(GMVKT:'Z'))
     C                             +   '->' + %trim(%editc(XXVKT:'Z'))
     C                             +' M3: ' + %trim(%editw(GMM3:'   0 ,   '))
     C                             +   '->' + %trim(%editw(XXM3:'   0 ,   '))
     c                   If        XXLM <> 0
     C                   EVAL      TTTEXL=%trim(TTTEXL)
     C                             +' Lm: ' + %trim(%editw(GMLM:' 0 ,  '))
     C                             +   '->' + %trim(%editw(XXLM:' 0 ,  '))
     C                   endif
     C                   else
      * Ingen endring - kun rapporter Schenkers verdier
     C                   EVAL      TTTEXL='IFTMCS:'
     C                             +' Ant=' + %trim(%editc(XXNT:'Z'))
     C                             +' Vkt=' + %trim(%editc(XXVKT:'Z'))
     C                             +' M3='  + %trim(%editw(XXM3:'   0 ,   '))
     c                             +' (Oppdr. ikke endret)'
     c                   If        XXLM <> 0
     C                   EVAL      TTTEXL=%trim(TTTEXL)
     C                             +' Lm='  + %trim(%editw(XXLM:' 0 ,  '))
     C                   endif
     C                   endif
     C                   EVAL      TTTEXT=TTTEXL
      *
     C                   WRITE     TRACKFR
      *
      * MOA
     C                   if        FrtAmt<>0
     C                   EVAL      TTTEXL='IFTMCS: '
     C                             +' Fraktvekt='
     c                             + %trim(%editw(XXFVKT:'        , '))         9,1
     C                             +' Forventet fraktbelastning='
     C                             + %trim(%editw(FrtAmt:'         ,  '))       11,2
     C                   EVAL      TTTEXT=TTTEXL
      *
     C                   WRITE     TRACKFR
     C                   endif
     C                   ENDSR
