********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 ubane256
11XXX * 02 SH  PTF11 ubane256  (feil i 01)
      *%%%%%%%%%%%%%%%%%%%
12XXX * 03 JOV PTF12 dersom det finnes LAT/LONG knyttet til en hendelse - lenke for vis kart
12XXX * 04 JOV PTF12 mauelel (*LE)tekster = vis både Local+english
      * 05 JOV PTF12 Hvis POD og GifOK = 'N' ved HO / det finnes VF: sjekk VF avd_opd.gif/jpg
********************************************************************
      *
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE15R MODULE(*LIBL/TJGE15R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FBRPARF    IF   e           k disk    USROPN
     FGSSCGL02  IF   E           K DISK    USROPN
     FTRACKL01  IF   E           K DISK    USROPN
     FTRACKL02  IF   E           K DISK    USROPN
     F                                     RENAME(TRACKFR:TRACKFR2)
     FKOFAST    IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D KN              S              8  0 DIM(30)
     D AVD             S              4
     D AVDN            S              4S 0
     D OPD             S              7
     D OPDN            S              7S 0
     D FAKLNK          S            200
     D Error           S            150
     D JSONstring      s          64000
     D ANTLEST         s              9  0
     D SNR             s             17
S26  D*UtText          s            150
N26  D UtText          s          15000
N26  D LatLong         s             30
N26  D MapLnk          s            256
N26  D MtilPar         s             50
N26  D Mwidth          s              5
N26  D Mheight         s              5
N26   * Definere  hex appostrof
N26  D Ap              c                   x'7D'
     D                 DS
     D  HXSNDN                 1     20
     D  HXSNR17                4     20
     D                 DS
     D  WTTD                   1    792    DIM(99)
     D                 DS
     D  WTTT                   1    792    DIM(99)
     D                 DS
     D  WTTX                   1  14850    DIM(99)
     D                 DS
     D  WTTG                   1  14850    DIM(99)
     D                 DS
     D  WTTG2                  1  14850    DIM(99)
     D                 DS
     D  DSAVOP                 1     12
     D  DSAVD                  1      4
     D  Strek                  5      5
     D  DSOPD                  6     12
     D  DSOPD5                 6     10
     D                 DS
     D  KFTXT                  1     50
     D  KFTXTB                 5     49
     D  VisWEB                50     50
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

      *
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

      * Hent fakturar
     c     Error         IFEQ      *blanks
     C                   EXSR      TRACKINFO
     C                   ENDIF
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         BEGSR
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      AVD    = zhbgetvar('AVD')
     C                   EVAL      AVDN   = c2n2(AVD)
     C                   EVAL      OPD    = zhbgetvar('OPD')
     C                   EVAL      OPDN = c2n2(OPD)
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     TrackKey      KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C     PODKEY        KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   MOVEL     'POD'         TTACTI
     C     FRIKEY        KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    WFSKODE
     C     *LIKE         DEFINE    FSKODE        WFSKODE
     C                   MOVEL     'IFB'         WFSKODE

     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END
      * Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C   50              eval      Error = 'Invalid userID'
     C                   OPEN      FIRM
     C                   OPEN      BRPARF
     C                   OPEN      GSSCGL02
     C                   OPEN      KOFAST
     C                   OPEN      TRACKL01
     C                   OPEN      TRACKL02
     C                   OPEN      HEADF
     C                   OPEN      FRISOK


     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     ActKey        KLIST
     C                   KFLD                    KFTYPT           10
     C                   KFLD                    KFKOD
     C                   MOVEL     'TRACKT'      KFTYPT

     C                   EVAL      PABRID = WSUSER

     C                   MOVEL     'NOTES'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR
     C                             ((PAVRDA <> 'j') AND
     C                              (PAVRDA <> 'J')))
     C                   MOVE      'N'           XWRKNOTES         1
     C                   ELSE
     C                   MOVE      'J'           XWRKNOTES         1
     C                   END

     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END

     C                   MOVE      *ZEROS        KN
     C                   MOVE      *ZEROS        XN2               3 0
     C                   MOVEL     'KUNGR'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA = *BLANKS))
     C                   MOVE      BIKUNR        KN(1)
     C                   ELSE
     C                   MOVEL     PAVRDA        CGCG
     C     CGCG          SETLL     GSSCGL02
     C     CGCG          READE     GSSCGL02                               33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN2
     C                   MOVE      CGCNO         KN(XN2)
     C     CGCG          READE     GSSCGL02                               33
     C                   ENDDO
     C                   END
      * LÅNER ARKTY-PARAMETEREN FOR Å AVGJØRE OM BRUKER KAN SE FAKTURALINK
      * sjekk om arkivtypene fa,fs,fx finnes(på førstelinjer) Hvis ikke, N i se fakturalink
      * (sjekk av arkivtyper for å se pdf-dok, sker generelt annet sted)
     C                   MOVE      'N'           OKFaktLink        1
     C                   MOVE      'ARKTY'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      '0'
     C     'fa'          scan      Pavrda                                 33
     C  N33'fs'          scan      Pavrda                                 33
     C  N33'fx'          scan      Pavrda                                 33
     C   33              MOVE      'J'           OKFaktLink
     C                   END

     C     ENDINI        ENDSR
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          BEGSR
     C                   CLOSE     BRIDF
     C                   CLOSE     FIRM
     C                   CLOSE     BRPARF
     C                   CLOSE     GSSCGL02
     C                   CLOSE     KOFAST
     C                   CLOSE     HEADF
     C                   CLOSE     FRISOK
     C                   CALLP     wrtsection('*fini')
     C                   EVAL      *INlr = *on

     C                   ENDSR
      *=====================================================================******
      *  TT2
      *=====================================================================******
     C     TT2           BEGSR
      * Skriv JSON-LISTE Start
     C                   EVAL      JSONstring ='"gettrackandtrace" : ['
     C                   CALLP     UPdHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EVAL      WN=*ZEROS
     C                   DO        99            WN                2 0
     C                   IF        WTTD(WN)=*BLANKS
     C                   LEAVE
     C                   ENDIF
      /free
       if WN=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
        +'¬wttd¬ : ¬'+%trim(WTTD(WN))+'¬, '
        +'¬wttt¬ : ¬'+%trim(WTTT(WN))+'¬, '
        +'¬wttx¬ : ¬'+%trim(WTTX(WN))+'¬, '
        +'¬wttg¬ : ¬'+%trim(WTTG(WN))+'¬, '
        +'¬wttg2¬ : ¬'+%trim(WTTG2(WN))+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDDO

      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
      *=====================================================================******
      *  TRACKINFO
      *=====================================================================******
     C     TRACKINFO     BEGSR

     C                   Z-ADD     0             Wn
      * Finn antall PODs på oppdraget (vis kun siste)
     C                   MOVE      *ZEROS        PodNr             3 0
     C                   MOVE      *ZEROS        antpod            3 0
     C                   MOVE      *ZEROS        TTFBNR
     C     TrackKey      CHAIN     HEADF                              33
     C     FRIKEY        SETLL     FRISOK
     C     FRIKEY        READE     FRISOK                                 33
     C  N33              MOVEL     FSSOK         SNR
     C     podkey        setll     trackl02
     C     podkey        reade     trackl02                               50
     C     *IN50         doweq     *OFF
     C                   ADD       1             antpod
     C     podkey        reade     trackl02                               50
     C                   END

     C                   EVAL      *IN50=*OFF
     C     TrackKey      setll     trackl01
     C     TrackKey      reade     trackl01                               50
     C     *IN50         doweq     *OFF
     C                   MOVE      ' '           VisWEB
     C                   EVAL      KFKOD = TTACTI
     C     ACTKey        CHAIN     KOFAST
      * Vis kun hendelser på oppdr.nivå - ved POD vis kun siste..
     C     TTFBNR        IFNE      0
     C                   MOVE      ' '           VisWEB
     C                   ELSE
     C     TTACTI        IFEQ      'POD'
     C                   ADD       1             PodNr
     C     PodNr         IFNE      AntPOD
     C                   MOVE      ' '           VisWEB
     C                   END
     C                   END
     C                   END

     C     VISWEB        IFEQ      'J'
     C     TTACTI        IFEQ      'POD'
     C     XSPRÅK        IFEQ      'EN'
     C                   EVAL      UTTEXT = KFTXTB + TTTEXT
     C                   ELSE
     C                   EVAL      UTTEXT = KFTXTB + TTTEXL
     C                   END
N26   * Hvik Geopos er oppgitt - vis lenke til kart ..
n26  c                   if        TTBRE<>0 and TTLEN<>0
n26  c                   exsr      GetMapLnk
n26  c                   eval      UTTEXT = %trim(UTTEXT)+' '+MapLnk
n26  c                   endif
n22  C                   CALLP     updHTMLvar2('TEXT'
n22  C                                         :%ADDr(UTTEXT)
n22  C                                         :%len(UTTEXT))
     C                   ELSE
     C     XSPRÅK        IFEQ      'EN'
     C                   EVAL      UTTEXT = TTTEXT
     C                   ELSE
     C                   EVAL      UTTEXT = TTTEXL
     C                   END
N04   * manuell tekst / begge linjer uavh. av språk (*LE = Local+English )
N04  c                   if        TTEDEV = '*LE'
N04  C                   EVAL      UTTEXT = %trim(TTTEXL)+' ' + TTTEXT
N04  c                   endif
     C                   END
     C     WN            IFLE      99
     C                   ADD       1             WN
     C                   MOVE      TTDATE        WTTD(WN)
     C                   MOVE      TTTIME        WTTT(WN)
     C                   EVAL      WTTX(WN)=UTTEXT
     C                   EVAL      WTTG(WN)=*BLANKS
     C                   EVAL      WTTG2(WN)=*BLANKS
     C                   ENDIF
      * Dersom det er kun ett fraktbrevsnr kan evt.  gif vises
     C     HXSNR17       IFNE      *BLANKS
     C                   EVAL      SNR  = HXSNR17
     C                   END
     C     TTacti        IFEQ      'POD'
     C     snr           andne     'FLERE'
     C     snr           andne     *BLANKS
     C                   EXSR      getgif
     C                   END
      * VEDL TRMODUL/UTLAND KAN SENDNR VÆRE  AVD_OPPDNR
     C     TTacti        IFEQ      'POD'
     C     GIFOK         andne     'J'
     C                   MOVEL     Snr           SnrGm            17
     C                   MOVE      AVDN          DSAVD
     C                   MOVE      '_'           Strek
     C                   MOVE      OPDN          DSOPD
     C                   EVAL      snr    = DSAVOP
     C                   EXSR      getgif
     C                   MOVEL     SnrGm         Snr
     C                   END

      * Vis evt gif ved COL/TEI/TEU/TE2
     C     TTacti        IFEQ      'COL'
     C     TTacti        oreq      'TEI'
     C     TTacti        oreq      'TEU'
     C     TTacti        oreq      'TE2'
     C                   EXSR      getgif2
     C     GIFOK         IFNE      'J'
     C                   MOVEL     Snr           SnrGm
     C                   MOVE      AVDN          DSAVD
     C                   MOVE      '_'           Strek
     C                   MOVE      OPDN          DSOPD
     C                   EVAL      snr    = DSAVOP
     C                   EXSR      getgif2
     C                   MOVEL     SnrGm         Snr
     C                   ENDIF
     C                   ENDIF
N18   * Vis Bilde - VELDIG foreløpig
N18   * eks   /syspeddev/signatur/Bilde_COL_0080_0201491.jpg
N18  c     TTacti        ifeq      'COL'
N18  c     TTacti        oreq      'TEI'
N18  c     TTacti        oreq      'TEU'
N18  c     TTacti        oreq      'TE2'
N18  c     TTacti        oreq      'POD'
N18  c                   move      HEAVD         DSAVD
N18  C                   move      '_'           Strek
N18  c                   move      HEOPD         DSOPD
N18  c                   eval      ubane  = '/syspeddev/signatur/Bilde_'
N18  C                             +TTACTI+'_'+DSAvOp+'.jpg'
N18  c                   CALL      'BHRURLCL'
N18  c                   Parm                    ubane
N18  c                   Parm                    GIFOKB            1
N18  c     GIFOKB        ifeq      'J'
N18  C     WN            IFLE      99
N18  C                   EVAL      WTTG2(WN)=UBANE
N18  C                   ENDIF
N18  c                   end
N18  C                   endif

     C                   END
     C     TrackKey      reade     trackl01                               50
     C                   END

     C                   EXSR      TT2

     C                   ENDSR
      *=====================================================================******
      *  Hent evt grafisk signatur
      *=====================================================================******
     C     GETGIF        BEGSR

     C                   MOVE      'N'           GIFOK             1
s01  C*                  MOVE      *BLANKS       UBANE            70
s02  C*                  MOVE      *BLANKS       UBANE           156
n02  C                   MOVE      *BLANKS       UBANE           256
     C                   MOVEL     '/syspeddev/' UBANE
     C     UBANE         CAT       'signatur/':0 UBANE
     C     UBANE         CAT       SNR:0         UBANE
     C     UBANE         CAT       '.gif':0      UBANE
     C                   CALL      'BHRURLCL'
     C                   PARM                    UBANE
     C                   PARM                    GIFOK
      * gif fantes ikke, prøv med bmp.
     C     GIFOK         IFNE      'J'
s01  C*                  MOVE      *BLANKS       UBANE            70
n01  C                   MOVE      *BLANKS       UBANE
     C                   MOVEL     '/syspeddev/' UBANE
     C     UBANE         CAT       'signatur/':0 UBANE
     C     UBANE         CAT       SNR:0         UBANE
     C     UBANE         CAT       '.bmp':0      UBANE
     C                   CALL      'BHRURLCL'
     C                   PARM                    UBANE
     C                   PARM                    GIFOK
     C                   END
      * bmp fantes ikke, prøv med jpg.
     C     GIFOK         IFNE      'J'
s01  C*                  MOVE      *BLANKS       UBANE            70
N01  C                   MOVE      *BLANKS       UBANE
     C                   MOVEL     '/syspeddev/' UBANE
     C     UBANE         CAT       'signatur/':0 UBANE
     C     UBANE         CAT       SNR:0         UBANE
     C     UBANE         CAT       '.jpg':0      UBANE
     C                   CALL      'BHRURLCL'
     C                   PARM                    UBANE
     C                   PARM                    GIFOK
     C                   END
N05   *
N05   * Hvis GIFOK=N og det er Moroppdrag med VF under - sjekk sig på VF-oppd(TKsmart/eventgrebber)
N05  c                   if        GIFOK <> 'J'
N05  c                             and TRSLAG='HO' and TROPD2<>0
N05  c                             and TTACTI='POD'
N05  c                             and %subst(Snr:5:1)='_'
N05  c                   move      TRAVD2        DSAVD
N05  C                   move      '_'           Strek
N05  c                   move      TROPD2        DSOPD
N05  c                   eval      snr    = DSAVOP
N05  c                   move      *blanks       ubane
N05  C                   movel     '/syspeddev/' ubane
N05  C     ubane         CAT       'signatur/':0 ubane
N05  C     ubane         CAT       SNR:0         ubane
N05  C     ubane         CAT       '.gif':0      ubane                          fra TKeventgrabber
N05  c                   CALL      'BHRURLCL'
N05  c                   Parm                    ubane
N05  c                   Parm                    GIFOK
N05  c                   if        GIFOK <> 'J'
N05  c                   move      *blanks       ubane
N05  C                   movel     '/syspeddev/' ubane
N05  C     ubane         CAT       'signatur/':0 ubane
N05  C     ubane         CAT       SNR:0         ubane
N05  C     ubane         CAT       '.jpg':0      ubane                          fra TKsmart
N05  c                   CALL      'BHRURLCL'
N05  c                   Parm                    ubane
N05  c                   Parm                    GIFOK
N05  c                   endif
N05  c                   endif
N05   *
     C     GIFOK         IFEQ      'J'
     C                   MOVE      *BLANKS       uttext
     C     uttext        CAT       UBANE:0       uttext
     C     WN            IFLE      99
     C                   EVAL      WTTG(WN)=UTTEXT
     C                   ENDIF
     C                   MOVE      *BLANKS       uttext
     C                   END
     C                   ENDSR
      *=================================================================
     C     GETGIF2       BEGSR

     C                   MOVE      'N'           GIFOK             1
s01  C*                  MOVE      *BLANKS       UBANE            70
n01  C                   MOVE      *BLANKS       UBANE
     C                   MOVEL     '/syspeddev/' UBANE
     C     UBANE         CAT       'signatur/':0 UBANE
     C     UBANE         CAT       TTACTI:0      UBANE
     C     UBANE         CAT       '_':0         UBANE
     C     UBANE         CAT       SNR:0         UBANE
     C     UBANE         CAT       '.gif':0      UBANE
     C                   CALL      'BHRURLCL'
     C                   PARM                    UBANE
     C                   PARM                    GIFOK
      * gif fantes ikke, prøv med bmp.
     C     GIFOK         IFNE      'J'
s01  C*                  MOVE      *BLANKS       UBANE            70
N01  C                   MOVE      *BLANKS       UBANE
     C                   MOVEL     '/syspeddev/' UBANE
     C     UBANE         CAT       'signatur/':0 UBANE
     C     UBANE         CAT       TTACTI:0      UBANE
     C     UBANE         CAT       '_':0         UBANE
     C     UBANE         CAT       SNR:0         UBANE
     C     UBANE         CAT       '.bmp':0      UBANE
     C                   CALL      'BHRURLCL'
     C                   PARM                    UBANE
     C                   PARM                    GIFOK
     C                   END
      * bmp fantes ikke, prøv med jpg.
     C     GIFOK         IFNE      'J'
s01  C*                  MOVE      *BLANKS       UBANE            70
n01  C                   MOVE      *BLANKS       UBANE
     C                   MOVEL     '/syspeddev/' UBANE
     C     UBANE         CAT       'signatur/':0 UBANE
     C     UBANE         CAT       TTACTI:0      UBANE
     C     UBANE         CAT       '_':0         UBANE
     C     UBANE         CAT       SNR:0         UBANE
     C     UBANE         CAT       '.jpg':0      UBANE
     C                   CALL      'BHRURLCL'
     C                   PARM                    UBANE
     C                   PARM                    GIFOK
     C                   END
     C     GIFOK         IFEQ      'J'
     C                   MOVE      *BLANKS       uttext
     C                   MOVEL     '<p><img bord'uttext
     C     uttext        CAT       'er="0" sr':0 uttext
     C     uttext        CAT       'c="':0       uttext
     C     uttext        CAT       UBANE:0       uttext
     C     uttext        CAT       '"></p>':0    uttext
     C     WN            IFLE      99
     C                   EVAL      WTTG(WN)=UTTEXT
     C                   ENDIF
     C                   MOVE      *BLANKS       uttext
     C                   END
     C                   ENDSR
N26   ***********************************************************
N26  C     GetMapLnk     begsr
N26   ***********************************************************
N26   * https://www.google.no/maps/?q=60.627935,8.559211      -  normal kart (eller m=map)
N26   * https://www.google.no/maps/?q=60.627935,8.559211&t=h  -h=hybrid (s=satelite,p=terrain)
N26  c                   eval      LatLong=%trim(%editw(TTBRE:'  .      '))
N26  c                                +','+%trim(%editw(TTLEN:'   .      '))
N26  c                   eval      MtilPar='&t=k'
N26  c                   eval      Mwidth='1200'
N26  c                   eval      Mheight='800'
N26  c                   eval      MapLnk='<a href="#" OnClick="'
N26  c                             +'window.open('
N26  c                             +Ap+'https://www.google.no/maps/?'
N26  c                             +'q='+%trim(LatLong)
N26  c                             +%trim(MtilPar)
N26  c                             +Ap
N26  c                             +','+Ap+'MapWindow'+Ap
N26  c                             +','+Ap+'width='+%trim(Mwidth)
N26  c                             +',height='+%trim(Mheight)
N26  c                             +',left=170,top=30,scrollbars,resizable'+Ap
N26  c                             +')">Se i kart</a>'
N26  C                   endsr
