      *********************************************************************
      *  Inngang til Print enkel label
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYTEIW5 MODULE(*LIBL/SYTEIW5 *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 UsrSpcNam
********************************************************************
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           k disk    usropn
     FHEADF     IF   E           K DISK    USROPN
     FHEAD39    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR39)
     FDOKUF7    IF   E           K DISK    USROPN
     FDOKUFM    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Varibles
     D WSUSER          S             10
N01  D UsrSpcName      s             10
     D WSSNDNR         S             20
     D xxavd           s             10
     D xxopd           s             10
     D WSNYML          S              1
     D MELDING         S             70
     D AVSLUTT         S              1
     D Cmd             S             70
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  PRT                    1     50
     D  LABTYPE                1      1
     D  LABOUTQ                2     11
     D  TRANSPORTØR           12     31
      *--------------------------------------------------------------------
      * Prolog common to CGI programs   (C-specs)
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      *
      *=====================================================================
      * MAIN PROCESS LINE
      *=====================================================================
     C                   time                    timedata1
      * Hent / repeter UserID
     C                   EVAL      WSUSER   = zhbgetvar('user')
N01  C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSSNDNR  = zhbgetvar('WSSNDNR')
     C                   EVAL      WSNYML   = zhbgetvar('WSNYML')
     C                   eval      WSNYML   = uppify(WSNYML)
     C                   EVAL      PRT      = zhbgetvar('PRT')
      * Sett libr.list etter UserID
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN     BRIDF                              33
     C                   IF        BILIBL = *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
     C                   open      BRPARF
     C                   open      HEADF
     C                   open      HEAD39
     C                   open      DOKUF7
     C                   open      DOKUFM
     C                   open      CUNDL01
      *
      * Initier felte når inn første gang
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     DOKUFMK       klist
     C                   kfld                    FMRI
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C                   MOVE      'F'           FMRI
     C     HeadfKey      klist
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C     BrParKey      klist
     C                   kfld                    BIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      'LABTY'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   MOVE      PAVRDA        PRT
      * Set/Read skeleton output html, etc.
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SYTEIW5'
      *
      * Hent svar-skjema
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   IF        LABOUTQ = *blanks
     C                   Eval      MELDING   = 'Det er ikke knyttet printer '+
     C                             'til din bruker - Kontakt systemansvarlig.'
     C                   MOVE      'N'           AVSLUTT
     C                   goto      IkkeSkriv
     C                   END
      *
     c     '/'           scan      WSSNDNR       xx                2 0
     c     xx            ifne      *zeros
     c                   eval      xxavd  = %SUBST(WSSNDNR:1:XX-1)
     c                   eval      xxopd  = %SUBST(WSSNDNR:XX+1:10)
     C                   eval      HEAVD    = c2n2(xxavd)
     C                   eval      HEOPD    = c2n2(xxopd)
     c     HEADFKey      chain     headf                              33
     c                   else
     c                   eval      DF1004 = WSSNDNR
     c     DF1004        chain     DOKUF7                             33
     c  N33              eval      HEAVD  = DFAVD
     c  N33              eval      HEOPD  = DFOPD
     c   33              eval      DF1004 = *blanks
     c   33WSSNDNR       chain     head39                             33
     c                   end
      *
     C     DOKUFMK       SETLL     DOKUFM
     C     DOKUFMK       READE     DOKUFM                                 33
     C                   SELECT
     C                   WHEN      *IN33
      * INGEN MERKELAPPER FINNES FRA FØR. GENERERE NYE MERKELAPPER
     C                   MOVE      'J'           WSNYML
     C                   MOVE      'J'           AVSLUTT
     C                   WHEN      FMST = 'E'
      * EKSTERNE MERKELAPPER FINNES. SKRIVE KOPI AV MERKELAPPER
     C                   IF        WSNYML = ' '
     C                   MOVE      'N'           AVSLUTT
     C                   ELSE
     C                   MOVE      'J'           AVSLUTT
     C                   END
     C                   MOVE      'N'           WSNYML
     C                   WHEN      WSNYML = ' '
      * INTERNE MERKELAPPER FINNES. BER BRUKER VELGE OM DE SKAL NYE.
     C                   MOVE      'N'           AVSLUTT
     C                   OTHER
     C                   MOVE      'J'           AVSLUTT
     C                   ENDSL
      * printerstyring
     C                   IF        AVSLUTT = 'J'
     C                   EVAL      RC = DOCMD('OVRPRTF FILE(ZBARC) OUTQ('+
     C                             %trim(LABOUTQ)+') SPLFOWN(*JOB) ' +
     C                             'OVRSCOPE(*JOB)')
     C                   exsr      Full_Lab
     C                   END
      *
      * sett html-variable
     c     IkkeSkriv     tag
     c     CUNDL01K      chain     CUNDL01                            33
     C                   callp     updhtmlvar('USER':WSUSER:
     C                             InitHTMLVars)
N01  C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('WSSNDNR':WSSNDNR)
     C                   callp     updhtmlvar('WSNYML':WSNYML)
     C                   callp     updhtmlvar('MELDING':MELDING)
     C                   callp     updHTMLvar('AVSLUTT':AVSLUTT)
     C                   callp     updHTMLvar('WSNA':KNAVN)
     C                   callp     updHTMLvar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('BOLD':' ')
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      * Write HTML-section
     C                   callp     wrtsection('top')
     C                   IF        LABOUTQ <> *blanks
     C                   IF        FMST = 'E'
     C                   callp     wrtsection('nyml1b')
     C                   ELSE
     C                   callp     wrtsection('nyml1')
     C                   END
     C                   END
     C                   callp     wrtsection('nyml2')
     C                   IF        LABOUTQ = *blanks
     C                   callp     wrtsection('nyml3b')
     C                   ELSE
     C                   callp     wrtsection('nyml3')
     C                   END
     C                   callp     wrtsection('nyml4')
      * dummy fini-section
     C                   callp     wrtsection('*fini')
      * Quit
     C                   close     BRIDF                                50
     C                   CLOSE     BRPARF
     C                   CLOSE     HEADF
     C                   CLOSE     HEAD39
     C                   CLOSE     DOKUF7
     C                   MOVE      *ON           *INLR
      *
     C                   return
      *
      *******************************************************************
     c     Full_Lab      begsr
      *******************************************************************
     c                   IF        DF1004 = *blanks
     c                   move      'HE'          VMLAYO                         basert på Headf
     C                   move      HEAVD         DFAVD
     C                   move      HEOPD         DFOPD
     C                   move      *zeros        DFFBNR
     c                   else
     c                   move      '  '          VMLAYO                         basert på fbrev
     c                   end
     c                   IF        LABTYPE = 'E'
     C                   MOVE      'SYFA12SE  '  LABPGM           10
     c                   else
     C                   MOVE      'SYFA12SN  '  LABPGM           10
     c                   end
      *
     C                   IF        WSNYML = 'J'
     C                   MOVE      'N'           UUKOPI            1
     C                   ELSE
     C                   MOVE      'J'           UUKOPI
     C                   END
      *
     C                   Call      LABPGM
     C                   PARM                    BIFIRM
     C                   PARM                    VMLAYO            2
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    DFFBNR
     C                   PARM                    UUKOPI
      *
     c                   endsr
      *
