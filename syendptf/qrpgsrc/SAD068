      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED CONTAINERNR                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-83 95-97
      ***********************************************************
      *
     FSADVCN    UF A E           K DISK
     FSAD068FM  CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D XAN07           S              7
     D FBAREA          DS
     D  WSSUBN               378    379B 0
     D  WSCLOC               382    383B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SADVCNK       SETLL     SADVCN
     C     SADVCNK       READE(N)  SADVCN                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SADVCNK       READE(N)  SADVCN                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C  N30              Z-ADD     4             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              DO
     C   27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C   06              DO
     C                   EXSR      CRTCN
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C   27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     OPDFIL        BEGSR
      ***********************************************
     C     SADVCNK       SETLL     SADVCN
     C     SADVCNK       READE     SADVCN                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SADVCNR
     C     SADVCNK       READE     SADVCN                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UCNR
     C     YRECNO        IFGT      0
     C     1             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     SVCNR         IFNE      '*DELETE'
     C     UCNR          IFEQ      *BLANKS
     C                   MOVE      SVCNR         UCNR
     C                   END
     C                   WRITE     SADVCNR
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     SVCNR         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGCN
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     SVCNR         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTCN
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     TESTB         BEGSR
      ***********************************************
      *
     C                   SELECT
     C                   WHEN      WSCNR = *BLANKS OR                           MANGLE CONTAINERNR
     C                             %SUBST(WSCNR:14:4) <> *BLANKS                FOR LANG CONTAINER
     C                   EVAL      *IN90 = *ON
     C                   WHEN      %SUBST(WSCNR:1:1) < 'A' AND                  FØRSTE 4 <> A-Z
     C                             %SUBST(WSCNR:1:1) > 'Z' OR
     C                             %SUBST(WSCNR:2:1) < 'A' AND
     C                             %SUBST(WSCNR:2:1) > 'Z' OR
     C                             %SUBST(WSCNR:3:1) < 'A' AND
     C                             %SUBST(WSCNR:3:1) > 'Z' OR
     C                             %SUBST(WSCNR:4:1) < 'A' AND
     C                             %SUBST(WSCNR:4:1) > 'Z'
     C                   EVAL      *IN90 = *ON
     C                   OTHER
      *
     C                   SELECT
     C                   WHEN      %SUBST(WSCNR:5:1) = ' ' OR                   FORMAT AAAA 123456 7
     C                             %SUBST(WSCNR:5:1) = '-'
     C                   EVAL      XAN07 = %SUBST(WSCNR:6:6)
     C                             + %SUBST(WSCNR:13:1)
     C                   WHEN      %SUBST(WSCNR:12:6) <> *BLANKS                FOR LANG CONTAINER
     C                   EVAL      *IN90 = *ON
     C                   OTHER                                                  FORMAT AAAA1234567
     C                   EVAL      XAN07 = %SUBST(WSCNR:5:7)
     C                   ENDSL
      *
     C     1             DO        7             XN3               3 0          TEST OM TALL
     C                   IF        %SUBST(XAN07:XN3:1) < '0' OR
     C                             %SUBST(XAN07:XN3:1) > '9'
     C                   EVAL      *IN90 = *ON                                  FEIL: IKKE TALL
     C                   LEAVE
     C                   END
      *
     C                   ENDDO
     C                   ENDSL
      *
     C                   IF        *IN90
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     63            POSNBR
     C                   EVAL      *IN30 = *ON
     C                   MOVE      'YBC2030'     MSGNR
     C                   ELSE
     C                   IF        %SUBST(WSCNR:5:1) = ' ' OR                   FORMAT AAAA 123456 7
     C                             %SUBST(WSCNR:5:1) = '-'
     C                   EVAL      UCONNR = %SUBST(WSCNR:1:4)
     C                             + %SUBST(WSCNR:6:6)
     C                             + %SUBST(WSCNR:13:1)
     C                   ELSE
     C                   EVAL      UCONNR = WSCNR
     C                   END
     C                   CALL      'SYGE124'
     C                   PARM                    UCONNR           11
     C                   PARM                    UCTLSIF           1
     C                   PARM                    UOK               1
     C                   IF        UOK <> 'J'
     C                   EVAL      *IN30 = *ON
     C                   EVAL      *IN90 = *ON
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      'YBC2031'     MSGNR
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVWCN        BEGSR
      ***********************************************
     C                   MOVE      WSCNR         SVCNR
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTCN         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSCNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCRT         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0'           *IN(90)
     C                   MOVE      '0'           *IN30
      *
     C   12              GOTO      SLCRT
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCRT
      *
     C                   EXSR      MOVWCN
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
      *
     C     SLCRT         ENDSR
      *
      ***********************************************
     C     CHGCN         BEGSR
      ***********************************************
     C                   MOVE      SVCNR         WSCNR
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0'           *IN(90)
     C                   MOVE      '0'           *IN30
      *
     C   12              GOTO      SLCHG
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCHG
     C                   EXSR      MOVWCN
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     DLTCN         BEGSR
      ***********************************************
     C                   MOVE      '1'           *IN99
     C                   EVAL      SVCNR = '*DELETE'
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SADVCNK       KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    ULI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
     C                   PARM                    UCNR             17
     C     *IN27         PARM      *IN27         UIN27             1
      * *IN27 PÅ -> OPPDATERING
      * *IN27 AV -> SPØRRING
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      UAVD          SVAVD
     C                   MOVE      UTDN          SVTDN
     C                   MOVE      ULI           SVLI
      *
     C                   ENDSR
      *
