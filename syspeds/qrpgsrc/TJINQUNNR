      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQUNNR MODULE(*LIBL/TJINQUNNR *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FADRUNNRF  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D unnr            S              4
     D embg            S              3
     D indx            S              1
     D GetVal          S              1                                         J=Get/validate
     D FullInfo        S              1                                         J=Ret. full info
     D MatchOnly       S              1                                         J=Kun eksakt match
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D MaxLes          S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬unnr¬ : ¬'+%trim(unnr)+'¬, '
        +'¬embg¬ : ¬'+%trim(embg)+'¬, '
        +'¬indx¬ : ¬'+%trim(indx)+'¬, '
        +'¬getVal¬ : ¬'+%trim(getVal)+'¬, '
        +'¬fullInfo¬ : ¬'+%trim(fullInfo)+'¬, '
        +'¬matchOnly¬ : ¬'+%trim(matchOnly)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"unNumbers" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta UNkoder
      *
     c     Error         ifeq      *blanks
     C     unnr          SETLL     ADRUNNRF
     C                   READ      ADRUNNRF                               55
     C     *IN55         DOWEQ     *OFF
      * bedt om kun match ??
     c                   if        MatchOnly = 'J'                              full matc
     c                             or MatchOnly = 'E'                           Unnr/Emb-match
     c                             or MatchOnly = 'U'                           Unnr match
      * kun lik unnr (uavh av emb/indx)
     c                   if        ADUNNR<>unnr
     c                   goto      utles
     c                   endif
      * kun lik un/emb
     c                   if        MatchOnly = 'J'                              full matc
     c                             or MatchOnly = 'E'                           Unnr/Emb-match
     c                   if        ADEMBG<>embg
     c                   goto      next
     c                   endif
     c                   endif
      * ekssakt Un/emb/indx
     c                   if        MatchOnly = 'J'                              full matc
     c                   if        ADINDX<>indx
     c                   goto      next
     c                   endif
     c                   endif
     c                   endif
      * posten skal med ..
     C                   Add       1             AntLest
     c     Antlest       cabgt     maxles        Utles
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬adunnr¬ : ¬'+%trim(ADUNNR)+'¬, '
          +'¬adembg¬ : ¬'+%trim(ADEMBG)+'¬, '
          +'¬adindx¬ : ¬'+%trim(ADINDX)+'¬, '
          +'¬adfakt¬ : ¬'+%trim(%editc(ADFAKT:'Z'))+'¬,'
          +'¬adtextK¬ : ¬'+%trim(%subst(ADTEXT:1:50))+'¬ ';
       if FullInfo='J';
        JSONstring=%trim(JSONstring)+', '
          +'¬adtext¬ : ¬'+%trim(ADTEXT)+'¬, '
          +'¬adklas¬ : ¬'+%trim(ADKLAS)+'¬, '
          +'¬adsedd¬ : ¬'+%trim(ADSEDD)+'¬, '
          +'¬adtres¬ : ¬'+%trim(ADTRES)+'¬ ';
       endif;
       JSONstring=%trim(JSONstring)+'}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ved get/Validate  er det kun en
     c     GetVal        ifeq      'J'
     c                   leave
     c                   endif

     C     NEXT          TAG

     C                   READ      ADRUNNRF                               55
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     ADRUNNRF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      unnr    = zhbgetvar('UNNR')
     C                   EVAL      embg    = zhbgetvar('EMBG')
     C                   EVAL      indx    = zhbgetvar('INDX')
     C                   EVAL      GetVal  = zhbgetvar('GetVal')
     C                   EVAL      FullInfo= zhbgetvar('FullInfo')
     C                   EVAL      MatchOnly= zhbgetvar('MatchOnly')
     C
     c                   endsr
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     UnEmKey       KLIST
     C                   KFLD                    unnr
     C                   KFLD                    embg
     C     UnEmInKey     KLIST
     C                   KFLD                    unnr
     C                   KFLD                    embg
     C                   KFLD                    indx

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      ADRUNNRF
      *
     c                   IF        MAXLES = 0
     c                   z-add     100           MAXLES
     c                   ENDIF
      *
     C                   IF        *IN50=*OFF
     C                   IF        GetVal='J'
     C     unnr          CHAIN     ADRUNNRF                           30
     C   30              eval      Error = 'Given UNNR not found'
      *
     c                   if        embg <> *blanks
     C     UnEmKey       CHAIN     ADRUNNRF                           30
     C   30              eval      Error = 'Given UNNR/EmbGrp not found'
     c                   endif
     c                   if        indx <> *blanks
     C     UnEmInKey     CHAIN     ADRUNNRF                           30
     C   30              eval      Error = 'Given UNNR/EmbGrp/Index not found'
     c                   endif
     C                   ENDIF
     C                   ENDIF
     c                   endsr
