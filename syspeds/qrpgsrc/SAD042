      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TOLLVESENETS LØPENR./DATO ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 06 07 09-11 13-24       ***
      *** LEDIGE INDIKATORER 27-32 34-39 41-89 95-97          ***
      ***********************************************************
      *
     FSADTRH    UF   E           K DISK
     FHEADF     UF   E           K DISK
     FSAD042FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
     C                   GOTO      START6
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2TDN         YTDN              7 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SATHK1        SETLL     SADTRH
     C     START1        TAG
     C     W2AVD         READE(N)  SADTRH                                 94
     C  N94              MOVE      SETDN         YTDN
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     W2AVD         READE(N)  SADTRH                                 94
     C  N94              MOVE      SETDN         YTDN
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      ***
      *
     C                   SETOFF                                       9899
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *
      *** FRISK OPP
     C   05              GOTO      START
      *** SØK FRA
     C   08              GOTO      START6
      *
      *** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94SATHK3        SETLL     SADTRH
     C  N94W2AVD         READE(N)  SADTRH                                 94
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      GETSE
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C     W2AVD         CABEQ     0             START6
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
      *
      *** TEST OM MAN HAR TASTET NOE
     C     WSDTG         COMP      *BLANKS                                90
     C   90WSTLL         COMP      *BLANKS                                90
     C   90WSTLE         COMP      *BLANKS                                90
     C   90WSCTN         COMP      0                                      90
      *
     C                   ENDSR
      *
     C***********************************************
     C     GETSE         BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLGET
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGSE
     C   12              GOTO      SLGET
     C                   END
     C                   END
      *
     C     SLGET         TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGSE         BEGSR
     C***********************************************
     C                   EXSR      MOVSEW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       90
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 90              GOTO      STCHG
     C  N12              EXSR      OPDSE
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPDSE         BEGSR
     C***********************************************
     C     SATHK         CHAIN     SADTRH                             33
     C  N33              DO
     C                   MOVE      WSDTG         SEDTG
     C                   MOVE      WSTLL         SETLL
     C                   MOVE      WSTLE         SETLE
     C                   MOVE      WSCTN         SECTN
     C                   EXCEPT    NYSE
     C                   END
     C     HEADFK        CHAIN     HEADF                              33
     C  N33              DO
     C                   MOVE      WSTLL         HETLL
     C                   MOVE      WSTLE         HETLE
     C                   EXCEPT    NYHEAD
     C                   END
      *
     C   40              UPDATE    SUBFIL
     C                   EXSR      MOVB2
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVSEW        BEGSR
     C***********************************************
     C                   MOVE      SEDTG         WSDTG
     C                   MOVE      SETLL         WSTLL
     C                   MOVE      SETLE         WSTLE
     C                   MOVE      SECTN         WSCTN
     C                   SETON                                        40
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSTLL
     C                   MOVE      *BLANKS       WSTLE
     C                   MOVE      *BLANKS       WSDTG
     C                   MOVE      *ZEROS        WSCTN
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SATHK         KLIST
     C                   KFLD                    W2AVD
     C                   KFLD                    SETDN
     C     SATHK1        KLIST
     C                   KFLD                    W2AVD
     C                   KFLD                    W2TDN
     C     SATHK3        KLIST
     C                   KFLD                    W2AVD
     C                   KFLD                    YTDN
     C     HEADFK        KLIST
     C                   KFLD                    W2AVD
     C                   KFLD                    SETDN
      *
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   WRITE     NYWRK
      *
     C                   ENDSR
      *
     OHEADFR    E            NYHEAD
     O                       HETLL
     O                       HETLE
     OSADTRHR   E            NYSE
     O                       SEDTG
     O                       SETLL
     O                       SETLE
     O                       SECTN
