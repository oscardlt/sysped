     A*%%TS  SD  20141014  161303  JOVO        REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P5105A* 01 YBC PTF5 LENGDE PÅ ADRESSENR ER ØKT FRA 3.0 TIL 5.0
P6XXXA* 02 YBC PTF6 NY FIL FOR LEVERANDØRS KUNDEREF.
P70XXA* 03 YBC PTF7 ENDRET LEDETEKST
P8033A* 04 YBC PTF8 NYE FELTER
XXXXXA*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
11XXXA* 05 YBC PTF11 OPSJON S -> SEND DANSK IMPORT FORTOLLING
11XXXA* 06 JOV PTF11 VED SPØRRING/56 ON PROTECT
11XXXA* 08 YBC PTF11 VIS TOTAL BRUTTOVEKT
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S04  A*                                     WINDOW(*DFT 12 52 *NOMSGLIN)
S08  A*                                     WINDOW(*DFT 16 62 *NOMSGLIN)
N08  A                                      WINDOW(*DFT 16 68 *NOMSGLIN)
     A                                      OVERLAY
     A*
S05  A*           WSNR           1A  B  5  2VALUES(' ' '1' '2')
     A          R SUBFIL                    SFL
     A*%%TS  SD  20140820  161518  SYSTEMA     REL-V7R1M0  5770-WDS
S05  A*           WSNR           1A  B  5  2VALUES(' ' '1' '2')
S07  A*           WSNR           1A  B  5  2VALUES(' ' '1' '2' 'S')
N07  A            WSNR           1A  B  5  2VALUES(' ' '1' '2' '4' 'S')
     A            MRMRNR         5Y 0O  5  5EDTCDE(4)
     A  50                                  COLOR(YLW)
     A            XLNA          17   O  5 11
     A  50                                  COLOR(YLW)
     A            MRDT           8Y 0O  5 29EDTWRD('    .  .  ')
     A  50                                  COLOR(YLW)
     A            MRTM           4Y 0O  5 40EDTWRD('  :  ')
     A  50                                  COLOR(YLW)
     A            MRST           1A  O  5 46COLOR(RED)
N04  A            XREF          15   O  5 48
     A  50                                  COLOR(YLW)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20141014  161303  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0011)
     A  *DS4                                SFLSIZ(0011)
     A  *DS3                                SFLPAG(0010)
     A  *DS4                                SFLPAG(0010)
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CA08(08 'SØK')
     A                                      CA12(12 'Avslutt program')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/LL023R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 YBC1282 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 44SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(008 YBC1283 *LIBL/SYMSGF)
     A            UKJED          8Y 0O  2 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            UNKJE         30A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(050 YBC1284 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(050 YBC1285 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
S04  A*                                11  2MSGCON(50 YBC1286 SYMSGF)
N04  A                                 15  2MSGCON(50 YBC1286 SYMSGF)
     A                                      COLOR(BLU)
S04  A* 30        MSGTXT        50A  O 12  2MSGID(&MSGNR SYMSGF)
N04  A  30        MSGTXT        50A  O 16  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VARELI
     A*%%TS  SD  20140620  142946  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søking')
     A  40                                  CF11(11 'EU-FRAKT')
N08  A  56                                  CF10(10 'ÅPNE FOR ENDRING')
     A                                      CA12(12 'AVBRYT')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(017 YBC1287 *LIBL/SYMSGF)
     A            WSLNR         17A  B  1 20DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A            VADRNR         5Y 0O  1 39EDTCDE(4)
     A                                  2  2MSGCON(017 YBC1288 *LIBL/SYMSGF)
     A            WSLNA         30A  B  2 20DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                  3  2MSGCON(017 YBC1289 *LIBL/SYMSGF)
     A            WSLAD1        30A  B  3 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                  4  2MSGCON(017 YBC1290 *LIBL/SYMSGF)
     A            WSLAD2        30A  B  4 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                  5  2MSGCON(017 YBC1291 *LIBL/SYMSGF)
     A            WSLPN          4S 0B  5 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A            WSLAD3        24A  B  5 26DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
N08  A                                  5 51'Br.vkt:'
N08  A            WSVKTB         7Y 2O  5 59COLOR(WHT)
N08  A                                      EDTCDE(4)
     A                                  6  2MSGCON(017 YBC1292 *LIBL/SYMSGF)
     A            WSDT           8Y 0B  6 20DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                      EDTWRD('    .  .  ')
     A                                  6 32'Frakt utenf.EU:'
     A            WSFRUE         9Y 2B  6 48
     A N82
     AAN83
     AA 55                                  DSPATR(PC)
     A N55                                  DSPATR(PR)
     A N55                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSFVUE         3A  B  6 59
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A N55                                  DSPATR(PR)
     A N55                                  COLOR(WHT)
     A                                  7  2MSGCON(017 YBC1294 *LIBL/SYMSGF)
     A            WSTM           4Y 0B  7 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                  7 32'Frakt innen.EU:'
     A            WSFRIE         9Y 2B  7 48
     A N55                                  DSPATR(PR)
     A N55                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSFVIE         3A  B  7 59
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A N55                                  DSPATR(PR)
     A N55                                  COLOR(WHT)
     A                                  8  2MSGCON(017 YBC1295 *LIBL/SYMSGF)
     A            WSLREF        25A  B  8 20DSPATR(UL)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
E08  A  40                              8 46MSGCON(014 YBC1293 *LIBL/SYMSGF)
E08  A  40        WSMRNR         5Y 0O  8 61DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
S03  A*                                 9  2MSGCON(17 YBC1296 SYMSGF)
S04  A*                                 9  2MSGCON(17 JOV1261 SYMSGF)
     A                                  9  2MSGCON(017 YBC1296 *LIBL/SYMSGF)
     A            WSKREF        25A  B  9 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
E08  A  55N56                           9 46'F11=Ford.F12=Avbr'
     A                                      COLOR(BLU)
S03  A*                                10  2MSGCON(17 YBC1297 SYMSGF)
S04  A*                                10  2MSGCON(17 JOV1262 SYMSGF)
     A                                 10  2MSGCON(017 YBC1297 *LIBL/SYMSGF)
     A            WSFT1         30A  B 10 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A            WSFT2         30A  B 11 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                 12  2'Godsnr:'
     A            WSGN          15A  B 12 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                 12 37'Mottager:'
     A            WSMOTT        15A  B 12 47DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                 13  2'MRN-nr:'
     A            WSTRNR        18A  B 13 20DSPATR(UL)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                 14  2'Påbegynt tid:'
     A            WSPTDF         4Y 0B 14 20DSPATR(UL)
     A  86                                  DSPATR(HI)
     A  86                                  DSPATR(PC)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                      EDTWRD('0 .  ')
     A                                 14 27'Ferdig tid:'
     A            WSPTDT         4Y 0B 14 39DSPATR(UL)
     A  85                                  DSPATR(HI)
     A  85                                  DSPATR(PC)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A                                      EDTWRD('0 .  ')
     A                                 15  2'Signatur:'
     A            WSPSG         30A  B 15 20DSPATR(UL)
     A  84                                  DSPATR(HI)
     A  84                                  DSPATR(PC)
N06  A  56                                  DSPATR(PR)
N06  A  56                                  COLOR(WHT)
E06  A N56                                  COLOR(YLW)
     A N30                             16  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30N56                          16 21MSGCON(012 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S07  A* 30        MSGTXT        50A  O 16  2MSGID(&MSGNR SYMSGF)
N07  A  30        MSGTXT        65A  O 16  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A  40N56                          16 36'F11=Fordel EU-frakt'
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'AVBRYT')
     A                                      SLNO(10)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  2MSGCON(10 YBC1298 SYMSGF)
     A            W2LNR         17A  B  1 13DSPATR(UL)
     A                                  1 31MSGCON(09 YBC1299 SYMSGF)
     A            W2DT           8  0B  1 41DSPATR(UL)
     A                                      EDTWRD('    .  .  ')
     A                                  2  2MSGCON(17 YBC1300 SYMSGF)
     A            W2MRNR         5  0B  2 20DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  2 28MSGCON(05 YBC1301 SYMSGF)
     A            W2ÅPEN         1A  B  2 34DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  2 36'(J/N)'
     A N30                              3  2MSGCON(30 YBC1302 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      CA03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CA08(08 'Søk')
     A                                      CA12(12 'Avslutt program')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL023R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(30 YBC1282 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(08 YBC1283 SYMSGF)
     A            UKJED          8Y 0O  2 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            UNKJE         30A  O  2 20DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(50 YBC1285 SYMSGF)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(50 YBC1286 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
