      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR05AD MODULE(*LIBL/TJETUR05AD *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Flytt json-feilmelding til ETTER utføreslen + utvid melding
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D USER            S             10
     D WSTUR           S              8
     D WBTUR           S              8  0
     D WSAVD           S              4
     D WBAVD           S              4  0
     D WSOPD           S              7
     D WBOPD           S              7  0
     D WMODE           S              1
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE

     C                   EXSR      INIT
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬wmode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬wstur¬ : ¬'+%trim(WSTUR)+'¬, '
        +'¬wsavd¬ : ¬'+%trim(WSAVD)+'¬, '
        +'¬wsopd¬ : ¬'+%trim(WSOPD)+'¬, ';
S01     //+'¬error¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

S01  C*                  EVAL      JSONstring ='"tripadddel" : ['
S01  C*                  CALLP     updHTMLvar2('JSONstring'
S01  C*                                        :%addr(JSONstring)
S01  C*                                        :%len(JSONstring))
S01  C*                  callp     wrtsection('JSONlin')
      * Find/send lines
     c     Error         IFEQ      *blanks
     C                   SELECT
     C                   WHEN      WMODE='D'
     C                   EXSR      SRREMOVE
     C                   WHEN      WMODE='A'
     C                   EXSR      SRPICKUP
     C                   ENDSL
     C                   ENDIF
      *
S01  c*                  eval      JSONstring =']'
N01  c                   eval      JSONstring ='¬error¬ : ¬'
N01  c                                        +%trim(ERROR)+'¬ '
N01  c                   call      'JSONFIX'
N01  c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     PARSE         BEGSR
     C                   EVAL      USER     = zhbgetvar('USER')
     C                   EVAL      WMODE    = zhbgetvar('WMODE')
     C                   EVAL      WSTUR    = zhbgetvar('WSTUR')
     C                   EVAL      WBTUR    = c2n2(WSTUR)
     C                   EVAL      WSAVD    = zhbgetvar('WSAVD')
     C                   EVAL      WBAVD    = c2n2(WSAVD)
     C                   EVAL      WSOPD    = zhbgetvar('WSOPD')
     C                   EVAL      WBOPD    = c2n2(WSOPD)
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C                   MOVE      *ON           *INLR
     C                   return
     C                   ENDSR
      *=====================================================================******
      *  SRREMOVE
      *=====================================================================******
     C     SRREMOVE      BEGSR

     C                   CALL      'SYGE21B'
     C                   PARM                    WBTUR
     C                   PARM                    WBAVD
     C                   PARM                    WBOPD
     C                   PARM                    IND30             1
     C
     C     IND30         ifeq      '1'
S01  C*                  eval      Error = 'Misslykke - sjekk status mm'
N01  C                   eval      Error = 'Misslykket - sjekk status mm'
     C                   ENDIF

     C                   ENDSR
      *=====================================================================******
      *  SRPICKUP
      *=====================================================================******
     C     SRPICKUP      BEGSR
     C                   CALL      'SYGE21'
     C                   PARM                    WBAVD
     C                   PARM                    WBOPD
     C                   PARM                    WBTUR
     C                   PARM                    IND30             1
     C
     C     IND30         ifeq      '1'
S01  C*                  eval      Error = 'Misslykke - sjekk status mm'
N01  C                   eval      Error = 'Misslykket - sjekk status / '
N01  C                                   + 'farlig-gods-samlasting mm '
     C                   ENDIF

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C   50              eval      Error = 'Invalid userID'

     C                   ENDSR
