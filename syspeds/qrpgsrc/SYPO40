      *
      ***********************************************************
      *** PROGRAM FOR PRISJUSTERING AV BRUTTOFRAKTER          ***
      ***************************************************************
     FSONET     UF   E           K DISK
     FFIRM      IF   E           K DISK
     FSONES     UF   E           K DISK
     FSYPO40FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  ZNUMB                  1     13  2
     D  ZNUMBB                11     13  2
     C*
     C                   EXSR      INIT
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     29            POSNBR
     C*
     C     START         TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       30
     C                   SETOFF                                       707172
     C                   SETOFF                                       737475
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
      *
     C                   EXSR      TESTB1
      *
     C   30              GOTO      START
     C*
     C   05              EXSR      JUSTER
     C                   GOTO      START
      *
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     JUSTER        BEGSR
     C***********************************************
     C*
     C     PLUMIN        IFEQ      '-'
     C                   Z-SUB     WSPROS        WSPROS
     C                   END
     C     WSPROS        DIV       100           OEK               7 4
     C     1             ADD       OEK           OEK
     C*
     C                   SETOFF                                       33
     C     *LOVAL        SETLL     SONES
     C     *IN33         DOWEQ     '0'
     C     NESTE         TAG
     C                   READ      SONES                                  33
     C*
     C     *IN33         IFEQ      '0'
     C*
     C     KSPROD        IFNE      '*'
     C     WSPROD        ANDNE     SSPROD
     C                   GOTO      NESTE
     C                   END
     C*
     C     KSAVDE        IFNE      '*'
     C     WSAVDE        ANDNE     SSAVDE
     C                   GOTO      NESTE
     C                   END
     C*
     C* MINDRE ENN FRAVERDI
     C*
     C     KSZONO        IFNE      '*'
     C     WSZON1        ANDGT     SSZONO
     C                   GOTO      NESTE
     C                   END
     C*
     C* STØRRE ENN TILVERDI
     C*
     C     KSZONO        IFNE      '*'
     C     WSZON2        ANDLT     SSZONO
     C                   GOTO      NESTE
     C                   END
     C*
     c     WSMM          cabeq     'N'           SkipMM
     C     PLUMIN        IFEQ      'B'
     C     SSMINI        IFNE      0
     C                   ADD(H)    WSPROS        SSMINI
     C                   END
     C     SSMAXI        IFNE      0
     C                   ADD(H)    WSPROS        SSMAXI
     C                   END
     C     SSGRPR        IFNE      0
     C                   ADD(H)    WSPROS        SSGRPR
     C                   END
     C                   ELSE
     C     PLUMIN        IFNE      'M'
     C     SSMINI        MULT(H)   OEK           SSMINI
     C     SSMAXI        MULT(H)   OEK           SSMAXI
     C     SSGRPR        MULT(H)   OEK           SSGRPR
     C                   ELSE
     C     SSMINI        DIV(H)    OEK           SSMINI
     C     SSMAXI        DIV(H)    OEK           SSMAXI
     C     SSGRPR        DIV(H)    OEK           SSGRPR
     C                   END
     C     SSMINI        IFNE      *ZEROS
     C                   Z-ADD     SSMINI        ZNUMB
     C                   EXSR      ROUND
     C                   MOVE      ZNUMB         SSMINI
     C                   END
     C     SSMAXI        IFNE      *ZEROS
     C                   Z-ADD     SSMAXI        ZNUMB
     C                   EXSR      ROUND
     C                   MOVE      ZNUMB         SSMAXI
     C                   END
     C     SSGRPR        IFNE      *ZEROS
     C                   Z-ADD     SSGRPR        ZNUMB
     C                   EXSR      ROUND
     C                   MOVE      ZNUMB         SSGRPR
     C                   END
     C                   END
     C     SkipMM        tag
     C                   UPDATE    SONESR
     C                   ADD       1             ANTJUS
     C*
     C                   EXSR      JUSDET
     C                   END
     C                   END
     C                   MOVE      *ZEROS        WSAVDE
     C                   MOVE      *BLANKS       WSZON1
     C                   MOVE      *BLANKS       WSZON2
     C                   MOVE      *BLANKS       WSPROD
     C                   MOVE      *ZEROS        WSPROS
     C                   ENDSR
     C*
     C***********************************************
     C     JUSDET        BEGSR
     C***********************************************
     C     DETKEY        KLIST
     C                   KFLD                    SSPROD
     C                   KFLD                    SSAVDE
     C                   KFLD                    SSZONO
     C*
     C                   SETOFF                                       34
     C     DETKEY        SETLL     SONET
     C     *IN34         DOWEQ     '0'
     C     DETKEY        READE     SONET                                  34
     C     *IN34         IFEQ      '0'
      * vektgrp-begrensning(WSTIVG da alltid oppgitt)  og denne post er UTENFOR.. skipp
     c  N33WSTIVG        ifne      *zeros
     c     SOVEKT        iflt      WSFRVG
     c     SOVEKT        orgt      WSTIVG
     C                   goto      skipp
     C                   end
     C                   end
     C     PLUMIN        IFEQ      'B'
     C     SOPRIC        IFNE      0
     C                   ADD       WSPROS        SOPRIC
     C                   END
     C                   ELSE
     C     PLUMIN        IFNE      'M'
     C     SOPRIC        MULT(H)   OEK           SOPRIC
     C                   ELSE
     C     SOPRIC        DIV(H)    OEK           SOPRIC
     C                   END
     C                   Z-ADD     SOPRIC        ZNUMB
     C                   EXSR      ROUND
     C                   MOVE      ZNUMB         SOPRIC
     C                   END
     c     Skipp         tag
     C                   UPDATE    SONETR
     C                   END
     C                   END
     C*
     C                   CALL      'SYPO13'
     C                   PARM                    SSPROD
     C                   PARM                    SSAVDE
     C                   PARM                    SSZONO
      *
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDEQ     33
     C                   MOVEL     WSPROD        XUKOD             5
     C                   CALL      'SYKUKD'
     C                   PARM                    XUTYP
     C                   PARM                    UUPDAT
     C                   PARM                    XUKOD
     C                   MOVEL     XUKOD         WSPROD
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     33
     C     POSNBR        ANDLE     36
     C                   CALL      'SYFA14B'
     C                   PARM                    WSAVDE
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     33
     C     POSNBR        ANDLE     37
     C                   CALL      'SYPO02X'
     C                   PARM                    WSZON1
     C                   Z-ADD     40            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     44
     C                   CALL      'SYPO02X'
     C                   PARM                    WSZON2
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C                   READ      FIRM                                   33
     C                   MOVE      '+'           PLUMIN
     C                   MOVEL     'PRODTYPE'    XUTYP            10
     C                   MOVE      'N'           UUPDAT            1
     C                   MOVE      'J'           WSMM
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     ROUND         BEGSR
     C***********************************************
     C*
     C* Param. = ZKODE (1,2,3,4)  = Roundingcode
     C*          ZNUMB workfield 13 long 2 digits
     C*         (ZNUMBB) is the 3 last pos. of ZNUMB (via Data Struct.)
     C*
     C* Code 2 = To nearest  5
     C*
     C     ZKODE         IFEQ      2                                            B1
     C     ZNUMBB        IFLT      2.50                                           B2
     C                   MOVE      '000'         ZNUMB
     C                   ELSE
     C     ZNUMBB        IFGE      2.50                                             B3
     C     ZNUMBB        ANDLE     7.49
     C                   MOVE      '500'         ZNUMB
     C                   ELSE
     C                   ADD       10            ZNUMB
     C                   MOVE      '000'         ZNUMB
     C                   END                                                        E3
     C                   END                                                      E2
     C                   END                                                      E2
     C*
     C* Code 3 = To nearest 10
     C*
     C     ZKODE         IFEQ      3
     C     ZNUMBB        IFGE      5.00                                           B2
     C                   ADD       10            ZNUMB
     C                   END                                                      E2
     C                   MOVE      '000'         ZNUMB
     C                   END                                                    E1
     C*
     C* Code 1 = To nearest HELE KRONE
     C*
     C     ZKODE         IFEQ      1
     C                   ADD       .50           ZNUMB
     C                   MOVE      '00'          ZNUMB
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   Z-ADD     29            POSNBR
     C*
     C     WSPROD        IFNE      *BLANKS
     C     KSPROD        ANDEQ     '*'
     C                   SETON                                        7030
     C                   MOVE      'SHU0474'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   GOTO      UTT
     C                   END
     C     WSAVDE        IFNE      *ZEROS
     C     KSAVDE        ANDEQ     '*'
     C                   SETON                                        7130
     C                   MOVE      'SHU0474'     MSGNR
     C                   Z-ADD     9             LINNBR
     C                   GOTO      UTT
     C                   END
     C     WSZON1        IFNE      *BLANKS
     C     KSZONO        ANDEQ     '*'
     C                   SETON                                        7230
     C                   MOVE      'SHU0474'     MSGNR
     C                   Z-ADD     11            LINNBR
     C                   GOTO      UTT
     C                   END
     C     WSZON2        IFNE      *BLANKS
     C     KSZONO        ANDEQ     '*'
     C                   SETON                                        7230
     C                   MOVE      'SHU0474'     MSGNR
     C                   Z-ADD     11            LINNBR
     C                   GOTO      UTT
     C                   END
     C     WSZON1        IFNE      *BLANKS
     C     WSZON1        ANDGT     WSZON2
     C                   SETON                                        7230
     C                   MOVE      'SHU0475'     MSGNR
     C                   Z-ADD     11            LINNBR
     C                   GOTO      UTT
     C                   END
     C     PLUMIN        IFNE      '+'
     C     PLUMIN        ANDNE     '-'
     C     PLUMIN        ANDNE     'M'
     C     PLUMIN        ANDNE     'B'
     C                   SETON                                        7330
     C                   MOVE      'SHU0476'     MSGNR
     C                   Z-ADD     13            LINNBR
     C                   GOTO      UTT
     C                   END
     C     WSPROS        IFEQ      *ZEROS
     C                   SETON                                        7430
     C                   MOVE      'SHU0477'     MSGNR
     C                   Z-ADD     13            LINNBR
     C                   GOTO      UTT
     C                   END
     C     ZKODE         IFGT      3
     C                   SETON                                        7530
     C                   MOVE      'SHU0478'     MSGNR
     C                   Z-ADD     15            LINNBR
     C                   GOTO      UTT
     C                   END
      * fra-til vektgruppe - til ikke oppgit -> 999999999
     c     WSFRVG        ifne      *zeros
     c     WSTIVG        andeq     *zeros
     c                   z-add     999999999     WSTIVG
     C                   end
     c     WSMM          ifne      'N'
     C                   MOVE      'J'           WSMM
     C                   end
     C     UTT           ENDSR
