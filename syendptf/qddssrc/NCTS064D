     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R NC064D01
     A N27                                  CF04(04 'Søking')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF10(10 'Se TVINN logg')
     A                                      CA12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/NCTS064R'
     A                                      COLOR(BLU)
     A N27                              1 18'Vedl. av lossemerknader'
     A                                      COLOR(WHT)
     A  27                              1 18'Spør. på lossemerknader'
     A                                      COLOR(WHT)
     A                                  1 47'Ref.'
     A            UAVD           4Y 0O  1 52COLOR(WHT)
     A                                      EDTCDE(4)
     A            UTDN           7Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A            TISG           3A  O  1 65COLOR(WHT)
     A                                  2  2'MRN-nr.'
     A            TITRNR        18A  O  2 13TEXT('MRN-nr')
     A                                      COLOR(WHT)
     A                                  2 32'Godsnr.'
     A            TIGN          35A  O  2 40COLOR(WHT)
     A                                  3  2'T.sted dest:'
     A            TITSB          8A  O  3 15COLOR(WHT)
     A                                  3 32'Trader dest:'
     A            TITINT        17A  O  3 45COLOR(WHT)
     A                                  3 65'LK dest:'
     A            TIBLK          2A  O  3 74COLOR(WHT)
     A                                  3 77'Container:'
     A            TIKDC          1A  O  3 88COLOR(WHT)
     A                                  3 92'Innland:'
     A            TITMIN         1Y 0O  3101COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3105'Brt vekt:'
     A            TIVKB         13Y 3O  3115COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2'Holder:'
     A            TITINH        17A  O  4 15TEXT('Holders identitet')
     A                                      COLOR(WHT)
     A            TINAH         30A  O  4 33COLOR(WHT)
     A            TITIRH        17A  O  4 64COLOR(WHT)
     A                                  5  2'Selger:'
     A            TITINS        17A  O  5 15TEXT('Selgers identitet')
     A                                      COLOR(WHT)
     A            TINAS         30A  O  5 33COLOR(WHT)
     A                                  5 66'Kjøper:'
     A            TITINK        17A  O  5 74TEXT('Kjøpers identitet')
     A                                      COLOR(WHT)
     A            TINAK         30A  O  5 92COLOR(WHT)
     A                                  6  2'Container:'
     A            TICNR         17A  O  6 15COLOR(WHT)
     A                                  6 35'Seals:'
     A            TIDANT         4Y 0O  6 42COLOR(WHT)
     A                                      EDTCDE(4)
     A            TIDFKD        20A  O  6 47COLOR(WHT)
     A                                  6 72'Godsref.:'
     A            TIGINR         5Y 0O  6 82COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7  2'Transport:'
     A            TITRM          2Y 0O  7 15TEXT('Tr.id. type')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TITAID        35A  O  7 18TEXT('Tr.id.nr')
     A                                      COLOR(WHT)
     A            TITALK         2A  O  7 54TEXT('Tr.id.LK')
     A                                      COLOR(WHT)
     A                                  8  2'Supp.dok.:'
     A            TISDTY         4A  O  8 15TEXT('Support dokument type')
     A                                      COLOR(WHT)
     A            TISDRF        70A  O  8 30TEXT('Support dokument ref')
     A                                      COLOR(WHT)
     A            TISDIN        35A  H
     A                                  9  2'Tr.dok.:'
     A            TITDTY         4A  O  9 15TEXT('Transport dokument type')
     A                                      COLOR(WHT)
     A            TITDRF        70A  O  9 30TEXT('Transport dokument ref')
     A                                      COLOR(WHT)
     A                                 10  2'Till.ref.:'
     A            TIADTY         4A  O 10 15TEXT('Tilleggs ref. type')
     A                                      COLOR(WHT)
     A            TIADRF        70A  O 10 30TEXT('Tilleggs ref. ref')
     A                                      COLOR(WHT)
      *
     A                                 12  2'T.sted dest:'
     A            WSTSB          8A  B 12 15COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  99                                  DSPATR(RI)
     A                                 12 32'Trader dest:'
     A            WSTINT        17A  B 12 45COLOR(YLW)
     A  27                                  DSPATR(PR)
     A  98                                  DSPATR(RI)
     A                                 13  2'Konform:'
     A            WSULKF         1Y 0B 13 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(3)
     A                                      VALUES(0 1)
     A                                 13 14'(0/1)'
     A                                 13 22'Komplett:'
     A            WSULKM         1Y 0B 13 32DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(3)
     A                                      VALUES(0 1)
     A                                 13 34'(0/1)'
     A                                 13 42'LosseDT:'
     A            WSULDT         6Y 0B 13 52DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14  2'Merknad:'
     A            WSULMR       250A  B 14 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 16  2'Brt vekt:'
     A            WSVKB         11Y 3B 16 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
     A                                 16 32'Container:'
     A            WSCNR         17A  B 16 43DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 16 72'Seal:'
     A            WSULST         1Y 0B 16 82DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(3)
     A                                      VALUES(0 1)
     A            WSDANT         4Y 0B 16 84DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSDFKD        20A  B 16 89DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17  2'Godsref.:'
     A            WSGINR         5Y 0B 17 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18  2'Tr.midl:'
     A            WSTATY         2Y 0B 18 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTAID        35A  B 18 15DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTALK         2A  B 18 61DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19  2'Sup.dok.:'
     A            WSSDTY         4A  B 19 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSDRF        70A  B 19 17DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSSDIN        35A  B 19 88DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 20  2'Tr.dok:'
     A            WSTDTY         4A  B 20 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTDRF        70A  B 20 17DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 21  2'Till.ref:'
     A            WSADTY         4A  B 21 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSADRF        70A  B 21 17DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A N27                             26  1'F4=Søking'
     A                                      COLOR(BLU)
     A N36                             26 11'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A                                 26 25'F10=NCTS'
     A                                      COLOR(BLU)
     A                                 26 35'F12=Forrige'
     A                                      COLOR(BLU)
     A                                 26 49'F15=Tillegg kode'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NC064D02
     A                                      WINDOW(*DFT 6 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                      CA23(23 'BEKREFT')
     A                                  1  2'NCTS064R'
     A                                      COLOR(BLU)
     A                                  2  2'Avdeling:'
     A            UAVD           4  0O  2 12EDTCDE(4)
     A                                  2 22'Oppd.nr.:'
     A            UTDN           7Y 0O  2 32EDTCDE(4)
     A                                  3  2'MRN-nr..:'
     A            TITRNR        18A  O  3 12TEXT('Signatur')
     A                                  4  2'Dekl. ligger i "postkasse" for sen-
     A                                      ding til NCTS.'
     A                                      COLOR(RED)
     A                                  5  2'F23=Fjerne fra "postkasse".'
     A                                      COLOR(YLW)
     A                                  6  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R NC064D03
     A                                      WINDOW(*DFT 4 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'NCTS064R'
     A                                      COLOR(BLU)
     A                                  2  2'Avdeling:'
     A            UAVD           4  0O  2 12EDTCDE(4)
     A                                  2 22'Oppd.nr.:'
     A            UTDN           7Y 0O  2 32EDTCDE(4)
     A                                  3  2'Total bruttovekt:'
     A            WSVKB         11  3O  3 20DSPATR(UL)
     A                                      EDTCDE(4)
