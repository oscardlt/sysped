     A*****************************************************************
     A*   KJØREKVITTERING/OPPDRAGSFORMULAR                            *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=132, OVERFLOW=72      *
     A* P00512  TRM3 RETTET
     A*****************************************************************
     A          R HEADP                     SKIPB(1)
     A            HOHT1         80A        1
     A                                      SPACEB(1)
     A            HOHT2         80A        1
     A                                      SPACEB(1)
     A            HOHT3         80A        1
     A                                      SPACEB(1)
     A            HOHT4         80A        1
     A                                      SPACEB(1)
     A            HOHT5         80A        1
     A                                      SPACEB(1)
     A            HOHT6         80A        1
     A                                      SPACEB(1)
     A            HOHT7         80A        1
     A                                      SPACEB(1)
     A                                     1MSGCON(132 JOV0055 SYMSGF)
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1MSGCON(132 PTR0030 SYMSGF)
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1MSGCON(132 PRI9999 SYMSGF)
     A                                      HIGHLIGHT
     A          R HEAD0                     SKIPB(2)
     A N52                                 1MSGCON(132 JOV0055 SYMSGF)
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                     1MSGCON(132 PTR0030 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 PRI9999 SYMSGF)
     A                                      HIGHLIGHT
     A          R DET1                      SPACEB(2)
     A*BILNR / SJÅFØR
     A  50        TUBILN        10         1HIGHLIGHT
     A  50        TUHENG        10        11HIGHLIGHT
     A  50        DITUAV         4  0     21EDTCDE(Z)
     A  50                                25'/'
     A  50        DITUR          8  0     26
     A  50        DIRUND         8  0     35EDTCDE(Z)
     A  50        XXSJN1        15        44
     A*REISEKASSE
     A  50        XXRNBR         5        60
     A  50        XXRVAL         3        65
     A  50        XXRBEL         9  2     69EDTCDE(Z)
     A  50        XXRFLE         1        79
     A*FERJE
     A  50        XXFROM         5        84
     A  50        XXTO           5        90
     A  50        XXDAMN         4  0     96EDTWRD('  .  ')
     A  50        XXTIME         4  0    102EDTWRD('  :  ')
     A  50        XXFERR        10       108
     A  50        XXFFLE         1       119
     A*TILLATELSER
     A          R DETTIL                    SPACEB(1)
     A N51                                 1MSGCON(20 SHU0005 SYMSGF)
     A            TINR          15A       21
     A            TITYPE         7A       40
SV5  A*           TIDAY          2  0     50EDTCDE(Z)
SV5  A*                                   52'.'
SV5  A*           TIMONT         2  0     53EDTCDE(Z)
SV5  A*                                   55'.'
SV5  A*           TIYEAR         2  0     56EDTCDE(Z)
NV5  A            XIDATE         6  0     50EDTCDE(Y)
     A*BRUTTO UTFAKTURERT
     A          R DETFAK                    SPACEB(1)
     A                                     1MSGCON(25 JOV0058 SYMSGF)
     A            FICURR         3A       82
     A            FABL           9  2     91EDTCDE(J)
     A*REKVISISJONER (OG DUPOPPDRAG)
     A          R DETREK                    SPACEB(1)
     A N51                                 1MSGCON(20 JOV0056 SYMSGF)
     A            BUST           1        21
     A            BUTXT         35        25
     A            BUBILN        10        61
SV5  A*           BUDATO         6  0     72EDTWRD('  .  .  ')
NV5  A            XUFEDT         6  0     72EDTWRD('  .  .  ')
     A            BUVAL          3A       82
     A            BUBL           9  2     91EDTCDE(J)
     A                                   105MSGCON(10 JOV0059 SYMSGF)
     A            BUBLST         1A      117
     A  55                               119MSGCON(14 JOV0057 SYMSGF)
     A          R LUFT                      SPACEB(1)
     A                                     1' '
     A          R DET2                      SPACEB(1)
     A            HEAVD          4  0      1
     A                                     5'/'
     A            HEOPD          7  0      6
     A                                    13'/'
     A            HESG           3A       14
     A            KNAVN         30A       18
     A                                    44' '
     A            XXSTEF        20A       45
     A            XRSDFD         6S 0     66EDTCDE(Y)
     A            TRSTAF         1A       75
     A                                    77'->'
     A            XXSTET        20A       80
     A            XRSDTD         6S 0    101EDTCDE(Y)
     A            TRSTAE         1A      110
     A            XXVKT          5  0    112EDTCDE(Z)
     A            HEM3           7  3    118EDTWRD('    ,   ')
     A            HELM           4  2    127EDTWRD('  ,  ')
     A* ___________________
     A* INNHENTING FOR...
     A*""""""""""""""""""""
     A          R DETINF                    SPACEB(1)
     A                                    16'('
     A                                    17MSGCON(16 JOV0128 SYMSGF)
     A            TRAVD0         4  0     34
     A                                    38'/'
     A            TROPD0         7  0     39
     A            HSFROM        15A       54
     A                                    77'->'
     A            HSTO          15A       89
     A                                   104')'
     A* ___________________
     A* UTKJØRING  FOR...
     A*""""""""""""""""""""
     A          R DETUTF                    SPACEB(1)
     A                                    16'('
     A                                    17MSGCON(16 JOV0129 SYMSGF)
     A            TRAVD0         4  0     34
     A                                    38'/'
     A            TROPD0         7  0     39
     A            HSFROM        15A       54
     A                                    77'->'
     A            HSTO          15A       89
     A                                   104')'
     A* ___________________
     A* INNH ORDNES AV...
     A*""""""""""""""""""""
     A          R DETINA                    SPACEB(1)
     A                                    16'('
     A                                    17MSGCON(16 JOV0130 SYMSGF)
     A            TRAVD1         4  0     34
     A                                    38'/'
     A            TROPD1         7  0     39
     A            HSFROM        15A       54
     A                                    77'->'
     A            HSTO          15A       89
     A* 72= ** IKKE SATT PÅ BIL **
     A  72                               104MSGCON(25 JOV0132 SYMSGF)
     A  72                               130')'
     A N72        HEPRO          8  0    104
     A N72                               112')'
     A* ___________________
     A* UTKJ ORDNES AV...
     A*""""""""""""""""""""
     A          R DETUTA                    SPACEB(1)
     A                                    16'('
     A                                    17MSGCON(16 JOV0131 SYMSGF)
     A            TRAVD2         4  0     34
     A                                    38'/'
     A            TROPD2         7  0     39
     A            HSFROM        15A       54
     A                                    77'->'
     A            HSTO          15A       89
     A* 72= ** IKKE SATT PÅ BIL **
     A  72                               104MSGCON(25 JOV0132 SYMSGF)
     A  72                               130')'
     A N72        HEPRO          8  0    104
     A N72                               112')'
