     A*%%TS  SD  20141008  165709  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R DKT057DA                  SFL
     A            ZOP            1A  I  8  2VALUES(' ' '2' '4' '5')
     A            DKVK_KD        3A  O  8  4
     A            DKVK_KRS      10Y 6O  8  8EDTCDE(K)
     A            DKVK_OMR       7Y 0O  8 22EDTCDE(K)
     A            DKVK_DTS       8Y 0O  8 33EDTCDE(4)
     A            DKVK_DTE       8Y 0O  8 42EDTCDE(4)
     A                                      DSPATR(ND)
     A*
     A          R DKT057DB                  SFLCTL(DKT057DA)
     A*%%TS  SD  20140130  113538  CB          REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0013)
     A  *DS4                                SFLSIZ(0013)
     A  *DS3                                SFLPAG(0012)
     A  *DS4                                SFLPAG(0012)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/DKT057'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 30'Underhold af SKAT/TOLD'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2 35'Valutakurser'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Vælg og tryk Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'2=Ændre'
     A                                      COLOR(BLU)
     A                                  6 33'Gyldighedsdato'
     A                                      COLOR(WHT)
     A                                  7  4'Sort'
     A                                      COLOR(WHT)
     A                                  7 16'Kurs'
     A                                      COLOR(WHT)
     A                                  7 25'Faktor'
     A                                      COLOR(WHT)
     A                                  7 33'Fra'
     A                                      COLOR(WHT)
     A*                                 7 42'Til'
     A*                                     COLOR(WHT)
     A                                  5 14'4=Slette'
     A                                      COLOR(BLU)
     A*
     A          R DKT057DC
     A*%%TS  SD  20140130  113538  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                 23  2'F3=Afslut'
     A                                      COLOR(BLU)
     A                                 23 14'F4=Sog'
     A                                      COLOR(BLU)
     A                                 23 23'F6=Oprette ny'
     A                                      COLOR(BLU)
     A                                 23 39'F12=Annuler'
     A                                      COLOR(BLU)
     A*
     A          R DKT057DD
     A*%%TS  SD  20141008  165709  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            ZVVK_KD        3A  B 21  4
     A  31                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A            ZVVK_KRS      10Y 6B 21  8EDTCDE(K)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            ZVVK_OMR       7Y 0B 21 22EDTCDE(K)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A            ZVVK_DTS       8Y 0B 21 33EDTCDE(4)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A            ZVVK_DTE       8Y 0B 21 42EDTCDE(4)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                 23  2'F3=Afslut'
     A                                      COLOR(BLU)
     A                                 23 14'F4=Sog'
     A                                      COLOR(BLU)
     A                                 23 23'F12=Annuler'
     A                                      COLOR(BLU)
     A*
     A          R DKT057DD2
     A*%%TS  SD  20141008  165709  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            ZVVK_KD        3A  O 21  4
     A            ZVVK_KRS      10Y 6B 21  8EDTCDE(K)
     A  32                                  DSPATR(RI)
     A                                      DSPATR(PC)
     A            ZVVK_OMR       7Y 0B 21 22EDTCDE(K)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A            ZVVK_DTS       8Y 0O 21 33EDTCDE(4)
     A            ZVVK_DTE       8  0O 21 42EDTCDE(4)
     A                                      DSPATR(ND)
     A                                 23  2'F3=Afslut'
     A                                      COLOR(BLU)
     A                                 23 14'F4=Sok'
     A                                      COLOR(BLU)
     A                                 23 23'F12=Annuler'
     A                                      COLOR(BLU)
      *
     A          R DKT057DE
     A                                 20  2' '
     A                                 21  2' '
      *
     A          R DKT057DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
