/*N01 LASER-LABEL           */
             PGM        PARM(&USR &FI &POREF &LIN &ULIN &LTYP &KOP +
                          &LAY &UNIK)
             DCL        VAR(&SPL) TYPE(*CHAR) LEN(10) +
                          VALUE('EPOMS04DP')
             DCL        VAR(&USR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FI) TYPE(*CHAR) LEN(2)
             DCL        VAR(&LTYP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LAY) TYPE(*CHAR) LEN(2)
             DCL        VAR(&POREF) TYPE(*CHAR) LEN(18)
             DCL        VAR(&LIN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ULIN) TYPE(*CHAR) LEN(5)
             DCL        VAR(&KN) TYPE(*CHAR) LEN(8)
             DCL        VAR(&BES) TYPE(*CHAR) LEN(10)
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TMP196) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TMP196M) TYPE(*CHAR) LEN(50)
             DCL        VAR(&KOP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(50)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             CHGVAR     VAR(&TMP196) VALUE('TMP' *CAT &UNIK)
             CHGVAR     VAR(&TMP196M) VALUE('/QSYS.LIB/QTEMP.LIB/' +
                          *CAT &TMP196 *CAT '.FILE/' *CAT &TMP196 +
                          *CAT '.MBR')
             IF         COND(&LAY = '  ') THEN(CHGVAR VAR(&LAY) +
                          VALUE('WW'))

             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)

             CHGVAR     VAR(&KN) VALUE(%SUBSTRING(&POREF 1 8))
             CHGVAR     VAR(&BES) VALUE(%SUBSTRING(&POREF 9 10))

             CLRPFM     FILE(QTEMP/&TMP196)
             MONMSG     MSGID(CPF3142) EXEC(CRTPF +
                          FILE(QTEMP/&TMP196) RCDLEN(196) +
                          SIZE(1000000 10000))
             OVRDBF     FILE(TMP196) TOFILE(QTEMP/&TMP196)


 /*LEGG INN SPOOL-INNHOLD*/
 /*ULIKT SPOOLFILENAVN/PROGRAM VED ZEBRA / ELTRON    */
             IF         COND(&LTYP = 'Z') THEN(DO)
 /*LEGG INN START-HTML*/
 /* kun zebra stripe takler HTML for øyeblikket...*/
             CALL       PGM(SPLHTMSTA)
 /*ZEBRA (ZPL II KODE)   */
             CHGVAR     VAR(&STMF) VALUE('/syspeddev/temp/ZBARC' +
                          *TCAT &POREF *TCAT '.html')
             OVRPRTF    FILE(ZBARC) OUTQ(SYCGIOUTQ) HOLD(*YES) +
                          SPLFOWN(*JOB)
             CALL       PGM(EPOMS04E) PARM(&USR &FI &LAY &KN &BES +
                          &LIN &ULIN &KOP)
             CPYSPLF    FILE(ZBARC) TOFILE(QTEMP/&TMP196) +
                          JOB(&QNBR/&QUSER/&QJOB) SPLNBR(*LAST) +
                          MBROPT(*ADD) CTLCHAR(*NONE)
             MONMSG     MSGID(CPF0000)
                        ENDDO
 /* LASER-LABEL           */
             IF         COND(&LTYP = 'L') THEN(DO)
 /*FJERN TIDLIGERE LISTE*/
 STDLTL:     DLTSPLF    FILE(EPOMD04DP) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(SLDLTL))
 /*          GOTO       CMDLBL(STDLTL)  */
 SLDLTL:
 /*LEGG INN START-HTML*/
             CHGVAR     VAR(&STMF) VALUE('/syspeddev/temp/LASLAB' +
                          *TCAT &POREF *TCAT '.pdf')
             OVRPRTF    FILE(EPOMS04DP) PAGESIZE(70 80) LPI(8) +
                          CPI(10) OVRFLW(70) OUTQ(SYCGIOUTQ) +
                          DEVTYPE(*AFPDS) +
                          FORMTYPE(*STD) SPLFOWN(*JOB)
             CALL       PGM(EPOMS04D) PARM(&USR &FI &LAY &KN &BES +
                          &LIN &ULIN &KOP)
             ADDLIBLE   LIB(MMAIL)
             MONMSG     MSGID(CPF0000)
 /* gidder ikke hensynta INFOPRINT-metode  */
             DSPJOBLOG  OUTPUT(*PRINT)
             CALL       PGM(INF001CL) PARM(&SPL &QNBR &QUSER &QJOB +
                          &STMF)
             DSPJOBLOG  OUTPUT(*PRINT)
             MONMSG     MSGID(CPF0000)
             DLTSPLF    FILE(EPOMD04DP) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
             RMVLIBLE   LIB(MMAIL)
             MONMSG     MSGID(CPF0000)
                        ENDDO
             IF         COND(&LTYP = 'E') THEN(DO)
 /*ELTRON (MODELLER( LP2844 M.M.) SOM IKKE KJØRER ZPL II KODE)   */
/*           CHGVAR     VAR(&STMF) VALUE('/syspeddev/temp/ELTRON' +         */
/*                        *TCAT &POREF *TCAT &LIN *TCAT &ULIN *TCAT '.txt') */
             CHGVAR     VAR(&STMF) VALUE('/syspeddev/temp/ELTRON' +
                          *TCAT &POREF *TCAT '.txt')
             OVRPRTF    FILE(SYLA03P) OUTQ(SYCGIOUTQ) HOLD(*YES) +
                          SPLFOWN(*JOB)
             CALL       PGM(EPOMS04F) PARM(&USR &FI &LAY &KN &BES +
                          &LIN &ULIN &KOP)
             CPYSPLF    FILE(SYLA03P) TOFILE(QTEMP/&TMP196) +
                          JOB(&QNBR/&QUSER/&QJOB) SPLNBR(*LAST) +
                          MBROPT(*ADD) CTLCHAR(*NONE)
             MONMSG     MSGID(CPF0000)
                        ENDDO

 /*LEGG INN STOPP  -HTML*/
 /* kun zebra stripe takler HTML for øyeblikket...*/
             IF         COND(&LTYP = 'Z') THEN(DO)
             CALL       PGM(SPLHTMSTO)
             ENDDO
 /*              */
 /*              */
 /*KOPIER TIL IFS*/
 /*              */
             IF         COND(&LTYP *NE 'L') THEN(DO)
             CPYTOSTMF  FROMMBR(&TMP196M) TOSTMF(&STMF) +
                          STMFOPT(*REPLACE) CVTDTA(*AUTO) +
                          STMFCODPAG(*STDASCII)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CHGAUT     OBJ(&STMF) USER(QTMHHTTP) DTAAUT(*RWX) +
                          OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)

             DLTOVR     FILE(TMP196)
             CLRPFM     FILE(QTEMP/&TMP196)
             MONMSG     MSGID(CPF0000)
             ENDPGM
