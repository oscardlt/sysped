     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '5')
     A            SVLI           5Y 0H
     A            SVLN           5Y 0H
     A            SVVKTB         9Y 0H
     A            SVNT           6Y 0H
     A            SVEH           4A  H
     A            SVTOPL        45A  H
     A            WXLN           5Y 0O  5  5EDTCDE(4)
     A            SVVNT          8S 0O  5 11TEXT('Varenr Tolltariff')
     A            SVVKTN         9Y 0O  5 20TEXT('Vekt Netto')
     A                                      EDTCDE(4)
     A            SVNTM          7Y 0O  5 30TEXT('Mengde')
     A                                      EDTCDE(4)
     A            SVVT          15A  O  5 38TEXT('Varetekst')
     A            SVFT          17A  O  5 54
     A            SV01           1A  O  5 72COLOR(RED)
     A                                      DSPATR(BL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD-TRANSITT'
     A                                      COLOR(WHT)
     A                                  1 29MSGCON(11 DIV1927 SYMSGF)
     A            UAVD           4Y 0O  1 41COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 46'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 48COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(40 DIV1934 SYMSGF)
     A                                      COLOR(BLU)
     A N32                              4  2'Op Linje Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merk E & NR.     -
     A                                       Sjø'
     A                                      COLOR(WHT)
     A  32                              4  2'Op Postn Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merk E & NR.     -
     A                                       Sjø'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'Hopp til')
     A                                      CF11(11 'Se oppdrag')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 42'F11=Se oppdrag'
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(18 DIV1914 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF01(01 'BYTT AVDELNGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'Hopp til')
     A                                      CF11(11 'Se oppdrag')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SAD-TRANSITT'
     A                                      COLOR(WHT)
     A                                  1 29MSGCON(11 DIV1927 SYMSGF)
     A            UAVD           4Y 0O  1 41COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 46'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 48COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(40 DIV1934 SYMSGF)
     A                                      COLOR(BLU)
     A N32                              4  2'Op Linje Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merk E & NR.     -
     A                                       Sjø'
     A                                      COLOR(WHT)
     A  32                              4  2'Op Postn Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merk E & NR.     -
     A                                       Sjø'
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(18 DIV1914 SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2'F11=Se oppdrag'
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R CLEARS
     A                                      SLNO(20)
     A                                      CLRL(05)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2'Hopp til linje   -->'
     A            W3LN           5Y 0I  1 24DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2'Hopp til varenr. -->'
     A            W3VNT          8Y 0B  2 24DSPATR(UL)
     A                                  3  2'Hopp til postnr. -->'
     A            W3LI           5S 0I  3 24DSPATR(UL)
     A                                  4  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2'   Varenr.. Brutto vekt Netto vekt-
     A                                       Mengde.'
     A                                      COLOR(WHT)
     A            SVVNT          8S 0O  2  5TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A            SVVKTB         9Y 0O  2 14TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVVKTN         9Y 0O  2 26TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVNTM          7Y 0O  2 36TEXT('Mengde')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  4'V.B'
     A            SVFT          17A  O  3  8TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVEH           4A  O  3 33TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SVNT           6Y 0O  3 26TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SVVT          15A  O  3 38TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  4'T.O'
     A            SVTOPL        45A  O  4  8TEXT('TILLEGGSOPPLYSNING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSMELD         8A  O  4 72COLOR(RED)
     A                                      DSPATR(BL)
     A                                  5  4'ENTER=Tilbake'
     A                                      COLOR(BLU)
