      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR11U MODULE(*LIBL/TJETUR11U *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FTUFE      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FTUFE1     IF   E           K DISK    USROPN
     F                                     RENAME(TUFER:TUFER1)
     FKOSBU     UF A E           K DISK    USROPN
     FTURER     IF   E           K DISK    USROPN
     FKOSTT     UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WSFAJN          S              1
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15

     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
          +'¬feavd¬ : ¬'+%trim(%editc(feavd:'Z'))+'¬, '
          +'¬fetur¬ : ¬'+%trim(%editc(fetur:'Z'))+'¬, '
          +'¬fefrom¬ : ¬'+%trim(fefrom)+'¬, '
          +'¬feto¬ : ¬'+%trim(feto)+'¬, '
          +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
          +'¬errMsg¬ : ¬'+%trim(Error)+'¬}';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     TUFE
     C  N50              CLOSE     KOSBU
     C  N50              CLOSE     KOSTT
     C  N50              CLOSE     TURER

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      wsuser     = zhbgetvar('USER')
     C                   EVAL      wsfajn     = zhbgetvar('WSFAJN')
     C                   EVAL      WA         = zhbgetvar('feavd')
     C                   EVAL      feavd      = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('fetur')
     C                   EVAL      fetur      = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('fefirm')             Leveran/FergeSelskap
     C                   EVAL      fefirm     = c2n2(WA)
     C                   EVAL      fefrom     = zhbgetvar('fefrom')
     C                   EVAL      feto       = zhbgetvar('feto')
     C                   EVAL      feleng     = zhbgetvar('feleng')
     C                   EVAL      WA         = zhbgetvar('fedate')
     C                   EVAL      fedate     = c2n2(WA)
     C                   EVAL      fecurr     = zhbgetvar('fecurr')
     C                   EVAL      WA         = zhbgetvar('fepri1')
     C                   EVAL      fepri1     = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('fepri2')
     C                   EVAL      fepri2     = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('feprin')
     C                   EVAL      feprin     = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('fetref')
     C                   EVAL      fetref     = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('ferund')
     C                   EVAL      ferund     = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('fedat2')
     C                   EVAL      fedat2     = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('fetime')
     C                   EVAL      fetime     = c2n2(WA)
     C                   EVAL      febiln     = zhbgetvar('febiln')
     C* Mod = add elelr delete
     C                   EVAL      WMODE  = zhbgetvar('MODE')
      *
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     TurKey        KLIST
     C                   KFLD                    Xfeavd
     C                   KFLD                    Xfetur
      *
     C     TUFEK         KLIST
     C                   KFLD                    Xfeavd
     C                   KFLD                    Xfetur
     C                   KFLD                    Xfefrom
     C                   KFLD                    Xfeto
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      TUFE
     C  N50              OPEN      KOSBU
     C  N50              OPEN      KOSTT
     C  N50              OPEN      TURER
      * dummy for å få feltnavn som arbeids/inputvariable
     C     *IN50         IFEQ      *OFF
     C     *IN50         ANDEQ     *ON
     C                   OPEN      tufe1
     C                   READ      tufe1
     C                   ENDIF
      *
      *
     c                   endsr
      *
      *********************************************************
     C     TEST          BEGSR
      *********************************************************
      *
     C                   EVAL      Xfetref  = *zeros
     C                   EVAL      Xfeavd   = feavd
     C                   EVAL      Xfetur   = fetur
     C                   EVAL      Xfefrom  = fefrom
     C                   EVAL      Xfeto    = feto
      *
     c     turkey        chain     TURER
     c                   if        not %found(TURER)
     C                   eval      Error = 'Ugyldig turnummer '
     c                   goto      UtTest
     c                   endif
      *
     C     TUFEK         CHAIN(n)  TUFE
      *
      * MODE A=Add   U=Update   D=Delete
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     c                   if        %found(TUFE)
     C                   eval      Error = 'Ferje-overfarten finnes allerede'
     c                   goto      UtTest
     c                   endif
     C                   EXSR      SRAU
     C     WMODE         WHENEQ    'U'
     c                   if        not %found(TUFE)
     C                   eval      Error = 'Finner ikke ferje-overfarten '
     c                   goto      UtTest
     c                   endif
     C                   EXSR      SRAU
     C     WMODE         WHENEQ    'D'
     c                   if        not %found(TUFE)
     C                   eval      Error = 'Finner ikke ferje-overfarten '
     c                   goto      UtTest
     c                   endif
     C                   EXSR      SRD
     C                   OTHER
     C                   eval      Error = 'Feil Mode'
     C                   ENDSL
      *
     C     UtTest        ENDSR
      *
      *********************************************************
     C     SRAU          BEGSR
      *********************************************************
     C     TUFEK         CHAIN     TUFE
      *
     C     Xfetref       ifeq      *zeros
     c                   eval      KTTYP  = 'Å'
     C     KTTYP         CHAIN     KOSTT                              33
     C   33              Z-ADD     1000000       KTNR
     C   33              WRITE     KOSTTR
     C  N33              ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
     C                   EVAL      Xfetref  =  KTNR
     C                   endif
      *
     C                   EVAL      Xfefirm  = fefirm                            Leveran/FergeSelskap
     C                   EVAL      Xfeleng  = feleng
     C                   EVAL      Xfedate  = fedate
     C                   EVAL      Xfecurr  = fecurr
     C                   EVAL      Xfepri1  = fepri1
     C                   EVAL      Xfepri2  = fepri2
     C                   EVAL      Xfeprin  = feprin
     C                   EVAL      Xferund  = TURUND
     C                   EVAL      Xfedat2  = fedat2
     C                   EVAL      Xfetime  = fetime
     C                   EVAL      Xfebiln  = TUBILN
     c
     c                   if        %found(TUFE)
     c                   update    TUFER
     c                   else
     c                   write     TUFER
     c                   endif
     c* Også KOSBU
     c     Xfetref       chain     KOSBU
     C                   EVAL      bubnr    = Xfetref
     C                   EVAL      bupCc    = BUPCC
     C                   EVAL      bupÅr    = BUPÅR
     C                   EVAL      bupMn    = BUPMN
     C                   EVAL      bulnr    = FEFIRM
     C                   EVAL      buval    = FECURR
     C                   EVAL      bubl     = FEPRI1
     C                   EVAL      buvk     = 'FER'
      * siste tegn setts lik bilkode - så i praksis vlri buvk f.eks FE5 , FE7  osv..
      * MEN dersom den IKKE skal faktureres legges den ut som FEI = Intern
     C                   if        TUBILK <> *blanks
     C                   eval      %subst(buvk:3:1)=%subst(tubilk:3:1)
     C                   endif
N01  C                   if        WSFAJN = 'N'
     C                   EVAL      buvk     = 'FEI'
N01  C                   endif
     C                   EVAL      butavd   = Xfeavd
     C                   EVAL      butunr   = Xfetur
     C                   EVAL      bublst   = 'E'
     C                   EVAL      butype   = 'E'
     C                   EVAL      butxt    = '*FERJE* '+FEFROM+' '+FETO+' '
     C                                      + %editc(FEDAT2:'X')+' '
     C                                      + %editc(FETIME:'X')
     C                   EVAL      bubilk   = TUBILK
     C                   EVAL      bubiln   = TUBILN
     C                   EVAL      bufedt   = FEDAT2
     C                   EVAL      bufer    = 'F'
      *
     c                   if        %found(KOSBU)
     c                   update    KOSBUR
     c                   else
     c                   write     KOSBUR
     c                   endif


     C                   ENDSR
      *
      *********************************************************
     C     SRD           BEGSR
      *********************************************************
      * Delet
     C     TUFEK         CHAIN     TUFE
     c                   if        %found(TUFE)
     c                   delete    TUFER
     c     Xfetref       chain     KOSBU
     c                   if        %found(KOSBU)
     C                   delete    KOSBUR
     C                   endif
     c                   endif
      *
     C                   ENDSR
