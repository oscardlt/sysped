      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT MANIFEST (VISE B/L OG OPPD.) ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 06-11 13 14 16-26       ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-49 52 55-90 95-97 ***
      ***********************************************************
      *
     FBILLOF    UF   E           K DISK
     FBILLOFL3  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF3L)
     FBILLOFL4  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF4L)
     FBILLOFL9  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF9L)
     FHEADF     IF   E           K DISK
     FSYSJ27FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D LS              S              5    DIM(5)
     D LO              S              5    DIM(10)
     D F4              S             75    DIM(12)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HENT                   1      7  0
     D  XNT                    1      5  0
     D                 DS
     D  HEVKT                  1      9  0
     D  XVKT                   1      7  0
     D                 DS
     D  XDTD                   1      2  0
     D  XDTM                   3      4  0
     D  XDTÅ                   5      6  0
     D  XDT                    1      6  0
     D                 DS
     D  BLREIS                 1     14
     D  XREIS                  1      8
     D                 DS
     D  UAVD                   1      4  0
     D  UBK                    5      9
     D  UREIS                 11     24
     D  UDT                   25     30  0
     D  LSH                   31     55
     D  LOH                   56    105
     D  KNAVN                106    135
     D  XPAR1                  1    256
     D                 DS
     D  FAXNRP                 1     21
     D  FILEN                 22     33
     D  PTIME                 34     41
     D  PDATE                 42     49
     D  UFAXSY                50     50
     D  UFAX                  51     51
     D  UCVP                  52     52
     D  XPAR2                  1    256
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      FSUBF1
     C                   EXSR      FSUBF2
     C                   EXSR      FSUBF3
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** AVSLUTT
     C   12              GOTO      SLUTT
     C*** SKRIVE UT
     C   15              DO
     C                   EXSR      PRINT
     C                   GOTO      SLUTT
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C   15              GOTO      SLUTT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     1             UFLAGG
     C   15              Z-ADD     5             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     FSUBF1        BEGSR
     C***********************************************
      *-------------------------------------------------------
      * FINN B/L VIA BLLSH OG BLVH MED AVGANGSDATO OG LOKALBÅT
      *-------------------------------------------------------
     C     BILLLK        SETLL     BILLOFL4
     C     BILLLK        READE     BILLOFL4                               94
     C     *IN94         DOWEQ     '0'
     C     BLST          IFNE      ' '
     C     BLST          ANDNE     'P'
     C     BLST          ANDNE     'X'
     C                   GOTO      STFS11
     C                   END
     C   50UREIS         CABNE     BLREIS        STFS11
     C     LSH           IFNE      *BLANKS
     C     1             DO        5             XN                3 0
     C     LS(XN)        IFEQ      BLLSH
     C     LS(XN)        ANDNE     *BLANKS
     C                   GOTO      STFS12
     C                   END
     C                   END
     C                   GOTO      STFS11
     C                   END
     C     STFS12        TAG
     C     LOH           IFNE      *BLANKS
     C     1             DO        10            XN
     C     LO(XN)        IFEQ      BLVH
     C     LO(XN)        ANDNE     *BLANKS
     C                   GOTO      STFS13
     C                   END
     C                   END
     C                   GOTO      STFS11
     C                   END
      *
     C     STFS13        TAG
     C                   ADD       1             ZRECNO
     C                   EXSR      HOPD
     C                   WRITE     SUBFIL
      *
     C     STFS11        TAG
     C     BILLLK        READE     BILLOFL4                               94
     C  N94              END
      *
     C                   ENDSR
      *
     C***********************************************
     C     FSUBF2        BEGSR
     C***********************************************
      *----------------------------------------------------------
      * FINN B/L VIA BLLSH OG BLLOH MED AVGANGSDATO OG OVERSJØBÅT
      * OG VIA-HAVN = BLANK
      *----------------------------------------------------------
     C     BILLLK        SETLL     BILLOFL3
     C     BILLLK        READE     BILLOFL3                               94
     C     *IN94         DOWEQ     '0'
     C     BLST          IFNE      ' '
     C     BLST          ANDNE     'P'
     C     BLST          ANDNE     'X'
     C                   GOTO      STFS21
     C                   END
     C     BLVH          CABNE     *BLANKS       STFS21
     C   50UREIS         CABNE     BLREIS        STFS21
     C     LSH           IFNE      *BLANKS
     C     1             DO        5             XN                3 0
     C     LS(XN)        IFEQ      BLLSH
     C     LS(XN)        ANDNE     *BLANKS
     C                   GOTO      STFS22
     C                   END
     C                   END
     C                   GOTO      STFS21
     C                   END
     C     STFS22        TAG
     C     LOH           IFNE      *BLANKS
     C     1             DO        10            XN
     C     LO(XN)        IFEQ      BLLOH
     C     LO(XN)        ANDNE     *BLANKS
     C                   GOTO      STFS23
     C                   END
     C                   END
     C                   GOTO      STFS21
     C                   END
      *
     C     STFS23        TAG
     C                   ADD       1             ZRECNO
     C                   EXSR      HOPD
     C                   WRITE     SUBFIL
      *
     C     STFS21        TAG
     C     BILLLK        READE     BILLOFL3                               94
     C  N94              END
      *
     C                   ENDSR
      *
     C***********************************************
     C     FSUBF3        BEGSR
     C***********************************************
      *----------------------------------------------------------
      * FINN B/L VIA BLVH OG BLLOH MED VIA-HAVNDATO OG OVERSJØBÅT
      *----------------------------------------------------------
     C     BILLLK        SETLL     BILLOFL9
     C     BILLLK        READE     BILLOFL9                               94
     C     *IN94         DOWEQ     '0'
     C     BLST          IFNE      ' '
     C     BLST          ANDNE     'P'
     C     BLST          ANDNE     'X'
     C                   GOTO      STFS31
     C                   END
     C     BLVH          CABEQ     *BLANKS       STFS31
     C   50UREIS         CABNE     BLREIS        STFS31
     C     LSH           IFNE      *BLANKS
     C     1             DO        5             XN                3 0
     C     LS(XN)        IFEQ      BLVH
     C     LS(XN)        ANDNE     *BLANKS
     C                   GOTO      STFS32
     C                   END
     C                   END
     C                   GOTO      STFS31
     C                   END
     C     STFS32        TAG
     C     LOH           IFNE      *BLANKS
     C     1             DO        10            XN
     C     LO(XN)        IFEQ      BLLOH
     C     LO(XN)        ANDNE     *BLANKS
     C                   GOTO      STFS33
     C                   END
     C                   END
     C                   GOTO      STFS31
     C                   END
      *
     C     STFS33        TAG
     C                   ADD       1             ZRECNO
     C                   EXSR      HOPD
     C                   WRITE     SUBFIL
      *
     C     STFS31        TAG
     C     BILLLK        READE     BILLOFL9                               94
     C  N94              END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HOPD          BEGSR
     C***********************************************
     C     HEADFK        CHAIN     HEADF                              33
     C   33              MOVE      *ZEROS        HENT
     C   33              MOVE      *ZEROS        HEVKT
     C*
     C     BLST          COMP      'S'                                    51
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HHENT         BEGSR
     C***********************************************
     C                   Z-ADD     0             UFLAGG
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     BLLB          IFEQ      '*DLT '
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
     C                   EXSR      CHGDOC
     C   03
     COR 12
     COR 15              GOTO      SLHENT
     C                   END
      *
     C   50WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XREIS         IFEQ      '*DELETE '
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
     C                   EXSR      DLTDOC
     C                   END
      *
     C   50WSNR          IFEQ      'X'
     C                   MOVE      ' '           WSNR
     C     XREIS         IFNE      '*DELETE '
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
     C                   EXSR      GETDOC
     C                   END
      *
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGDOC        BEGSR
     C***********************************************
     C                   SETON                                        99
     C     BILLK         CHAIN(N)  BILLOF                             33
     C  N33BLST          IFNE      'S'
     C                   Z-ADD     1             UFLAGG
     C                   CALL      'SYSJ25A'
     C     UAVD          PARM      UAVD          UAVD2             4 0
     C                   PARM                    BLOPD
     C                   PARM                    BLSG
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      0
     C                   CALL      'SYSJ27B'
     C     UAVD          PARM      UAVD          UAVD2             4 0
     C                   PARM                    BLOPD
     C                   PARM                    UFLAGG
     C                   END
     C     UFLAGG        COMP      3                                      03
     C     UFLAGG        COMP      1                                      12
     C     UFLAGG        COMP      5                                      15
     C   15              EXSR      PRINT
     C                   END
     C*
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     GETDOC        BEGSR
     C***********************************************
     C                   SETON                                        99
     C     BILLK         CHAIN     BILLOF                             33
     C  N33BLST          IFNE      'S'
     C                   MOVE      UREIS         BLREIS
     C                   UPDATE    BILLOFR
     C                   END
     C*
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTDOC        BEGSR
     C***********************************************
     C                   SETON                                        99
     C     BILLK         CHAIN     BILLOF                             33
     C  N33BLST          IFNE      'S'
     C                   MOVE      *BLANKS       BLREIS
     C                   UPDATE    BILLOFR
     C                   MOVE      '*DELETE '    XREIS
     C                   END
     C*
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C                   CALL      'SYSJ27C2'
     C                   PARM                    UPAR1
     C                   PARM                    UPAR2
     C                   PARM                    UCVP1
     C                   PARM                    UCVP2
     C                   PARM                    UPRT33
     C                   PARM                    UPT33
     C                   PARM                    UPRT34
     C                   PARM                    UPT34
     C                   PARM                    F4
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    BLOPD
     C     BILLK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    BLOPD
     C     BILLLK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UBK
     C                   KFLD                    UDT8
     C*
     C     *ENTRY        PLIST
     C     XPAR1         PARM                    UPAR1           256
     C     XPAR2         PARM                    UPAR2           256
     C                   PARM                    UCVP1           160
     C                   PARM                    UCVP2           160
     C                   PARM                    UPRT33           10
     C                   PARM                    UPT33            10
     C                   PARM                    UPRT34           10
     C                   PARM                    UPT34            10
     C                   PARM                    F4
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      *ZEROS        HENT
     C                   MOVEA     LSH           LS
     C                   MOVEA     LOH           LO
     C*
     C     UREIS         COMP      *BLANKS                            5050
     C                   MOVE      UDT           XMMDD             4 0
     C                   MOVE      XMMDD         UDT1              2 0
     C                   MOVEL     XMMDD         UDT2              2 0
     C                   MOVEL     UDT           UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          XDTD
     C                   MOVE      UDT2          XDTM
     C                   MOVE      UDT3          XDTÅ
      *
     C     UDT           IFGT      501231
     C     UDT           ADD       19000000      UDT8              8 0
     C                   ELSE
     C     UDT           ADD       20000000      UDT8
     C                   END
     C*
     C                   ENDSR
     C*
