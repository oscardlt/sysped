     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R BILDE1
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'send ønsket fax')
     A                                      CF12(12 'Avslutt')
     A  27                                  CF17(17 'SENDE FAX UTEN COVERPAGE')
     A                                  1  3'SYSPED/SYSJ56'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 YBC0435 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 64TIME
     A                                      COLOR(BLU)
     A                                  3  3MSGCON(024 YBC0385 *LIBL/SYMSGF)
     A            BOAVD          4Y 0O  3 28DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 33'/'
     A            BOOPD          7Y 0O  3 35DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  3MSGCON(025 YBC0438 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0O  5 30EDTCDE(4)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            KNAVN         30A  O  5 40DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6  3MSGCON(025 SHU0167 *LIBL/SYMSGF)
     A            WSFAXN        15A  B  6 30DSPATR(UL)
     A  99                                  DSPATR(PC)
     A                                  6 52MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B  6 65
     A                                  6 68'='
     A            FAXRET         6A  O  6 70
     A                                  7  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B  7 11CHECK(LC)
     A  27                              8  3MSGCON(010 SHU0345 *LIBL/SYMSGF)
     A  27        WSCVP1        40A  B  8 17CHECK(LC)
     A  27                              9  3MSGCON(010 SHU0346 *LIBL/SYMSGF)
     A  27        WSCVP2        40A  B  9 17CHECK(LC)
     A  27        WSCVP3        40A  B 10 17CHECK(LC)
     A  27        WSCVP4        40A  B 11 17CHECK(LC)
     A  27                             12  3MSGCON(010 SHU0347 *LIBL/SYMSGF)
     A  27        WSCVP5        40A  B 12 17CHECK(LC)
     A  27        WSCVP6        40A  B 13 17CHECK(LC)
     A  27        WSCVP7        40A  B 14 17CHECK(LC)
     A  27                             15  3MSGCON(010 SHU0348 *LIBL/SYMSGF)
     A  27        WSCVP8        40A  B 15 17CHECK(LC)
     A                                 17  3MSGCON(025 SHU0173 *LIBL/SYMSGF)
     A            WSTIME         6Y 0B 17 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 18  3MSGCON(025 SHU0174 *LIBL/SYMSGF)
     A            WSDATE         6Y 0B 18 30DSPATR(UL)
     A                                      EDTCDE(4)
     A N27                             23  3MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23 23MSGCON(018 SHU0265 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                             23  3MSGCON(077 YBC0389 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1996  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE2
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'send ønsket fax')
     A                                      CF12(12 'Avslutt')
     A                                      CF17(17 'SENDE FAX UTEN COVERPAGE')
     A                                  1  3'SYSPED/SYSJ56'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 YBC0435 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 64TIME
     A                                      COLOR(BLU)
     A                                  2  3MSGCON(024 YBC0385 *LIBL/SYMSGF)
     A            BOAVD          4Y 0O  2 28DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 33'/'
     A            BOOPD          7Y 0O  2 35DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  3MSGCON(025 YBC0438 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0B  3 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            KNAVN         30A  O  3 40
     A                                  4  3MSGCON(025 SHU0167 *LIBL/SYMSGF)
     A            WSFAXN        15A  B  4 30DSPATR(UL)
     A  99                                  DSPATR(PC)
     A                                  4 46MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B  4 59
     A                                  4 62'='
     A            FAXRET         6A  O  4 64
     A                                  5  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B  5 11CHECK(LC)
     A                                  6  3'Til:'
     A            WSCNV         30A  B  6 17DSPATR(UL)
     A            WSCAD1        30A  B  7 17DSPATR(UL)
     A            WSCAD2        30A  B  8 17DSPATR(UL)
     A            WSCAD3        30A  B  9 17DSPATR(UL)
     A                                 10  3'Attn:'
     A            WSCATT        30A  B 10 17DSPATR(UL)
     A                                 11  3'Besjed til mottager:'
     A            WSCF01        75A  B 12  3DSPATR(UL)
     A            WSCF02        75A  B 13  3DSPATR(UL)
     A            WSCF03        75A  B 14  3DSPATR(UL)
     A            WSCF04        75A  B 15  3DSPATR(UL)
     A            WSCF05        75A  B 16  3DSPATR(UL)
     A            WSCF06        75A  B 17  3DSPATR(UL)
     A            WSCF07        75A  B 18  3DSPATR(UL)
     A            WSCF08        75A  B 19  3DSPATR(UL)
     A            WSCF09        75A  B 20  3DSPATR(UL)
     A            WSCF10        75A  B 21  3DSPATR(UL)
     A            WSCF11        75A  B 22  3DSPATR(UL)
     A                                 23  2MSGCON(060 YBC0339 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(078 SHU0169 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
