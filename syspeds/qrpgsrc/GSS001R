********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10217 * 01 YBC PTF10 DEFAULT PRINTER FOR PLUKKLISTE OG MANIFEST
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED BRUKER-ID                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-14             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-81 95-97    ***
      ***********************************************************
     FKODTA     IF   E           K DISK
     FGSSBIF    UF A E           K DISK
     FGSSCCF    IF   E           K DISK
     FGSSFIF    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FGSS001D   CF   E             WORKSTN
     F                                     SFILE(GSS001DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BINM                   1     30
     D  WXNM                   1     16
     D  XDLT                   1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     GSS001DB
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2USID        YUSID            10
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     GBIK1         SETLL     GSSBIF
     C     START1        TAG
     C                   READ(N)   GSSBIF                                 94
     C   94              MOVE      *BLANKS       BINM
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        16
     C                   ADD       1             ZRECNO
     C                   WRITE     GSS001DA
     C                   READ(N)   GSSBIF                                 94
     C  N94              MOVE      BIUSID        YUSID
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     GSS001DG
     C                   ELSE
     C                   WRITE     GSS001DB
     C                   EXFMT     GSS001DC
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXSR      MOVB2
     C                   MOVE      UPRT          WSPRT
     C                   MOVE      UPRT          WSPRTM
N01  C                   MOVE      UPRT          WSPRTP
N01  C                   MOVE      UPRT          WSPRTI
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94GBIK3         SETLL     GSSBIF
     C  N94              READ(N)   GSSBIF                                 94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     GSS001DD
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVE      '0'           *IN82
     C                   MOVE      '0'           *IN30
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     GSS001DE
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSUSID        COMP      *BLANKS                                90
     C   90              MOVE      'GSS0001'     MSGNR
     C   90              MOVE      '1'           *IN30
     C  N90GBIKEY        CHAIN(N)  GSSBIF                             33
     C  N90
     CANN33              DO
     C                   MOVE      'GSS0002'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   90              Z-ADD     22            LINNBR
     C   90              Z-ADD     2             POSNBR
     C                   END
      *
     C     WSNM          COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'GSS0003'     MSGNR
     C   89
     CANN30              Z-ADD     22            LINNBR
     C   89
     CANN30              Z-ADD     13            POSNBR
     C   89              SETON                                        30
      *
     C     WSCG          IFNE      *BLANKS
     C     WSCG          CHAIN     GSSCCF                             88
     C   88
     CANN30              MOVE      'GSS0004'     MSGNR
     C   88
     CANN30              Z-ADD     22            LINNBR
     C   88
     CANN30              Z-ADD     44            POSNBR
     C   88              SETON                                        30
     C                   END
      *
     C     WSCNO         IFNE      0
     C     KUND1K        CHAIN     CUNDL01                            87
     C   87
     CANN30              MOVE      'GSS0005'     MSGNR
     C   87
     CANN30              Z-ADD     22            LINNBR
     C   87
     CANN30              Z-ADD     47            POSNBR
     C   87              SETON                                        30
     C                   END
      *
     C     WSFC          CHAIN     GSSFIF                             86
     C   86
     CANN30              MOVE      'GSS0006'     MSGNR
     C   86
     CANN30              Z-ADD     22            LINNBR
     C   86
     CANN30              Z-ADD     56            POSNBR
     C   86              SETON                                        30
      *
     C     WSPRT         COMP      *BLANKS                                85
     C   85
     CANN30              MOVE      'GSS0007'     MSGNR
     C   85
     CANN30              Z-ADD     22            LINNBR
     C   85
     CANN30              Z-ADD     60            POSNBR
     C   85              SETON                                        30
      *
     C     WSPRTM        COMP      *BLANKS                                84
     C   84
     CANN30              MOVE      'GSS0008'     MSGNR
     C   84
     CANN30              Z-ADD     22            LINNBR
     C   84
     CANN30              Z-ADD     71            POSNBR
     C   84              SETON                                        30
      *
     C     KODTAK        CHAIN     KODTA                              83
     C   83
     CANN30              MOVE      'SPA0001'     MSGNR
     C   83
     CANN30              Z-ADD     23            LINNBR
     C   83
N01  CANN30              Z-ADD     31            POSNBR
S01  C*NN30              Z-ADD     44            POSNBR
     C   83              SETON                                        30
      *
     C     WSLOC         IFNE      *BLANKS
     C     WSLOC         CHAIN     STED2                              82
     C   82
     CANN30              MOVE      'SPS0107'     MSGNR
     C   82
     CANN30              Z-ADD     23            LINNBR
     C   82
N01  CANN30              Z-ADD     41            POSNBR
S01  C*NN30              Z-ADD     64            POSNBR
     C   82              SETON                                        30
     C                   END
      *
     C   30              GOTO      SLTB1
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     GBIKEY        CHAIN     GSSBIF                             33
     C  N33
     CANN40              UPDATE    GSSBIFR
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWBI
     C  N33              UPDATE    GSSBIFR
     C   33              WRITE     GSSBIFR
     C   40              UPDATE    GSS001DA
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     GSS001DA
     C                   WRITE     GSS001DB
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     GSS001DA                               98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       16            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGBI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTBI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'GSS0009'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGBI         BEGSR
     C***********************************************
     C                   EXSR      MOVBIW
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     13            POSNBR
     C     STCHG         TAG
     C                   EXFMT     GSS001DD
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVE      '0'           *IN82
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
      *
     C   12              DO
     C                   UPDATE    GSS001DA
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTBI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     GSS001DF
     C  N12              DO
     C     GBIK2         CHAIN     GSSBIF                             33
     C  N33              DELETE    GSSBIFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    GSS001DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWBI        BEGSR
     C***********************************************
     C                   MOVE      WSUSID        BIUSID
     C                   MOVE      WSNM          BINM
     C     WSCNO         IFNE      0
     C                   MOVE      *BLANKS       WSCG
     C                   END
     C                   MOVE      WSCG          BICGR
     C                   MOVE      WSCNO         BICNO
     C                   MOVE      WSFC          BIFC
     C                   MOVE      WSPRT         BIPRT
     C                   MOVE      WSPRTM        BIPRTM
N01  C                   IF        WSPRTP = *BLANKS
N01  C                   MOVE      WSPRT         BIPRTP
N01  C                   ELSE
N01  C                   MOVE      WSPRTP        BIPRTP
N01  C                   END
N01  C                   IF        WSPRTI = *BLANKS
N01  C                   MOVE      WSPRT         BIPRTI
N01  C                   ELSE
N01  C                   MOVE      WSPRTI        BIPRTI
N01  C                   END
     C                   MOVE      WSAVD         BIAVD
     C                   MOVE      WSLOC         BILOC
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVBIW        BEGSR
     C***********************************************
     C                   MOVE      BIUSID        WSUSID
     C                   MOVE      BINM          WSNM
     C                   MOVE      BICGR         WSCG
     C                   MOVE      BICNO         WSCNO
     C                   MOVE      BIFC          WSFC
     C                   MOVE      BIPRT         WSPRT
     C                   MOVE      BIPRTM        WSPRTM
N01  C                   MOVE      BIPRTP        WSPRTP
N01  C                   MOVE      BIPRTI        WSPRTI
     C                   MOVE      BIAVD         WSAVD
     C                   MOVE      BILOC         WSLOC
     C                   SETON                                        40
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       BIUSID
     C                   MOVE      *BLANKS       BINM
     C                   MOVEL     '*DELETE'     XDLT
     C                   MOVE      *BLANKS       BICGR
     C                   MOVE      *ZEROS        BICNO
     C                   MOVE      *BLANKS       BIFC
     C                   MOVE      *BLANKS       BIPRT
     C                   MOVE      *BLANKS       BIPRTM
N01  C                   MOVE      *BLANKS       BIPRTP
N01  C                   MOVE      *BLANKS       BIPRTI
     C                   MOVE      *ZEROS        BIAVD
     C                   MOVE      *BLANKS       BILOC
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSUSID
     C                   MOVE      *BLANKS       WSNM
     C                   MOVE      *BLANKS       WSCG
     C                   MOVE      *ZEROS        WSCNO
     C                   MOVE      *BLANKS       WSFC
     C                   MOVE      *BLANKS       WSPRT
     C                   MOVE      *BLANKS       WSPRTM
N01  C                   MOVE      *BLANKS       WSPRTP
N01  C                   MOVE      *BLANKS       WSPRTI
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *BLANKS       WSLOC
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSCNO
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG            1 0
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     44
     C     POSNBR        ANDLE     45
     C                   CALL      'GSS002R'
     C                   PARM                    UFLAGG
     C                   PARM                    WSCG
     C                   Z-ADD     47            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     47
     C     POSNBR        ANDLE     54
     C                   EXSR      SØKUNR
     C                   Z-ADD     56            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     56
     C     POSNBR        ANDLE     58
     C                   CALL      'GSS004R'
     C                   PARM                    UFLAGG
     C                   PARM                    WSFC
     C                   Z-ADD     60            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      23
     C     POSNBR        ANDGE     44
     C     POSNBR        ANDLE     47
     C                   CALL      'SYFA14B'
     C                   PARM                    WSAVD
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      23
     C     POSNBR        ANDGE     64
     C     POSNBR        ANDLE     68
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    WSLOC
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'GSS0013'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     GBIKEY        KLIST
     C                   KFLD                    WSUSID
     C     GBIK1         KLIST
     C                   KFLD                    W2USID
     C     GBIK2         KLIST
     C                   KFLD                    BIUSID
     C     GBIK3         KLIST
     C                   KFLD                    YUSID
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSCNO
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WSAVD
     C                   MOVE      'A'           KOAUNI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPRT             10
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
