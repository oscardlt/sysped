     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4')
     A            ECFIR          2A  H
     A            ECSR           1A  H
     A            EC0065         6A  H
     A            ECCNO          8  0O  6  5EDTCDE(4)
     A            KNAVN         30A  O  6 14
     A            ECLIB         10A  O  6 45
     A            ECPGM         10A  H
     A            XXKN           8  0O  6 56EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/IE001R'
     A                                      COLOR(BLU)
     A                                  1 19'Arbeid med partsregister'
     A                                      DSPATR(HI)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Mottpart                       -
     A                                               Bibliotek  K.nr hos mottpa-
     A                                      rt'
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   Mottpart                       -
     A                                               Bibliotek  K.nr hos mottpa-
     A                                      rt'
     A                                      COLOR(WHT)
     A            WSCNO          8  0B  2  5DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNA          30A  O  2 14DSPATR(UL)
     A                                      DSPATR(HI)
     A            WSLIB         10A  B  2 45DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSKN           8  0B  2 56DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'   Mottpart                       -
     A                                                Bibliotek  K.nr hos mottp-
     A                                      art'
     A            ECCNO          8  0O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            ECLIB         10A  O  2 45DSPATR(UL)
     A                                      COLOR(WHT)
     A            XXKN           8  0O  2 56DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                  1  2'SYSPED/IE001R'
     A                                      COLOR(BLU)
     A                                  1 19'Arbeid med partsregister'
     A                                      DSPATR(HI)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Mottpart                       -
     A                                                Bibliotek  K.nr hos mottp-
     A                                      art'
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
