      ***********************************************************
      *** PROGRAM SKAL SENDE XML-KVITTERING                   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 34-49 51-99                ***
      ***********************************************************
     FFIRM      IF   E           K DISK
     FXMLINHL1  UF   E           K DISK
     FXMLTILP   O  A E           K DISK    USROPN
     FECMSG1    IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK
     FDOKUF     IF   E           K DISK    USROPN
     FDOKUFM    IF   E           K DISK    USROPN
     FDOKUFV    IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FFAK2      IF   E           K DISK    USROPN
     FKODTGE    IF   E           K DISK    USROPN
     FEDIC      UF A E             DISK    USROPN
     FEDIS      O  A E             DISK    USROPN
     FEDIM      O  A E             DISK    USROPN
     FEDISE     O  A E             DISK    USROPN
     D TXT             S             80    DIM(142) CTDATA PERRCD(1)
     D KLID            S             35    DIM(90)
     D GEB1            S              3    DIM(90)
     D GEB2            S             41    DIM(90)
     D GEB3            S             13  2 DIM(90)
     D GEB4            S              3    DIM(90)
     D GEB5            S             13  2 DIM(90)
     D FNUM            S              7  0 DIM(90)
     D                 DS
     D  XTXT801                1     80
     D  XT801                  1     80    DIM(80)
     D                 DS
     D  XTXT802                1     80
     D  XT802                  1     80    DIM(80)
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
     D  XMBRC                  2     10
     D                 DS
     D  WDT                    1      8
     D  WDTÅ                   1      4
     D  WDTM                   5      6
     D  WDTD                   7      8
     D                 DS
     D  WTM                    1      6
     D  WTMH                   1      2
     D  WTMM                   3      4
     D  WTMS                   5      6
     D                 DS
     D  HEAVD                  1      4  0
     D  XHEAVD                 1      4
     D  XXSTREKK1              5      5
     D  HEOPD                  6     12  0
     D  XHEOPD                 6     12
     D  XXSTREKK2             13     13
     D  HESG                  14     16
     D  SYSPEDREF              1     16
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTOPÅ                1      4
     D  HEDTOPM                5      6
     D  HEDTOPD                7      8
     D                 DS
     D  TRSDFD                 1      8  0
     D  TRSDFDÅ                1      4
     D  TRSDFDM                5      6
     D  TRSDFDD                7      8
     D                 DS
     D  TRSDFK                 1      4  0
     D  TRSDFKH                1      2
     D  TRSDFKM                3      4
     D                 DS
     D  TRSDTD                 1      8  0
     D  TRSDTDÅ                1      4
     D  TRSDTDM                5      6
     D  TRSDTDD                7      8
     D                 DS
     D  TRSDTK                 1      4  0
     D  TRSDTKH                1      2
     D  TRSDTKM                3      4
     D                 DS
     D  FSSOK                  1     35
     D  FSSOK25                1     25
     D  FSSOK10               26     35
     D  FS10                  26     35    DIM(10)
     D                 DS
     D  FMMRK1                 1     35
     D  FMMRK101               1     18
     D  FMMRK102              18     35
     D  FMMRK103              19     35
      *
     C                   EXSR      INIT
      *
     C     XMLINHL1K     CHAIN     XMLINHL1                           50
     C                   IF        *IN50 = *OFF
     C                   IF        (((XMST <> ' ') AND
     C                               (XMST <> 'A')) AND
     C                               (XMST <> 'C'))
     C                   UPDATE    XMLINHR
     C                   GOTO      SLUTT
     C                   END
     C     XMLINHL1K     CHAIN     HEADF                              50
     C  N50HXEDIK        COMP      'X'                                5050
     C                   IF        *IN50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM
     C                   MOVE      'E'           XMST
     C                   MOVE      WDT           XMDTS
     C                   MOVE      WTM           XMTMS
     C                   UPDATE    XMLINHR
     C                   END
     C                   END
     C   50              GOTO      SLUTT
      *
     C                   EXSR      SRINIT
     C                   EXSR      SRBYGGF
      *
     C                   EXSR      SRSTART
      *
     C                   EXSR      SRCONSTR
      *
     C                   EXSR      SRPARTYS
      *
     C                   EXSR      SRDATSTR
      *
     C                   EXSR      SRSLUTT
     C                   EXSR      SRCLOSE
     C                   EXSR      SRSEND
      * Oppdater XMLUFF
     C                   MOVE      'C'           XMST
     C                   MOVE      WDT           XMDTS
     C                   MOVE      WTM           XMTMS
     C                   UPDATE    XMLINHR
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRINIT        BEGSR
      ***********************************************
     C                   OPEN      ECMSG1
     C                   OPEN      FRISOK
     C                   OPEN      DOKUF
     C                   OPEN      DOKUFM
     C                   OPEN      DOKUFV
     C                   OPEN      FAK2
     C                   OPEN      KODTGE
     C                   OPEN      EDIC
     C                   OPEN      EDIS
     C                   OPEN      EDIM
     C                   OPEN      EDISE
      *
     C                   MOVE      'INS'         XFSKODEINS        3
     C     FRISOKK6      CHAIN     FRISOK                             33
     C   33              MOVE      'INB'         XFSKODEINS
     C   33FRISOKK6      CHAIN     FRISOK                             33
     C                   MOVEL     FSSOK         XAN15            15
     C                   EVAL      ECIIDC = XAN15
      *
     C     ECMSG1K       CHAIN     ECMSG1                             33
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       XMBR
     C                   MOVE      'F'           XMBRA
     C                   MOVE      CSN           XMBRB
     C                   MOVE      XMBR          SEMBR
     C                   MOVE      *BLANKS       SDATA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM
      *
     C                   EVAL      S0004 = ECIIDI
     C                   EVAL      S0010 = ECIIDC
     C                   EVAL      S0020 = 'INHPK'
     C                   EVAL      S0026 = 'XML-INH INSINF'
     C                   CAT       XHEAVD:0      S0020
     C                   CAT       XHEOPD:0      S0020
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'C'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = '*LIBL'
     C                   EVAL      SFILE = 'EDISE'
     C                   EVAL      SMBR  = SEMBR
     C                   EVAL      SMBRS = 0
     C                   EVAL      S0036 = 1
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRBYGGF       BEGSR
      ***********************************************
     C                   CALL      'SYXML03B'
     C                   OPEN      XMLTILP
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSTART       BEGSR
      ***********************************************
     C                   EVAL      SDATA = TXT(001)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(002)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(003)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = %trim(TXT(004))
     C                             + %trim(XMBR)
     C                             + '"'
     C                   IF        XMST = ' '
     C                   CAT       TXT(141):1    SDATA
     C                   ELSE
     C                   CAT       TXT(142):1    SDATA
     C                   END
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(005)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONSTR      BEGSR
      ***********************************************
     C*    HEADFK        CHAIN     HEADF                              33
      * HENT INNGÅENDE PACKAGE-ID
     C                   MOVE      *BLANKS       KLID
     C                   MOVE      *ZEROS        XANTKL            3 0
     C                   MOVE      *ZEROS        XTOTORVEKT        9 0
     C     FRISOKK4      SETLL     FRISOK
     C     FRISOKK4      READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XANTKL
     C                   MOVEL     FSSOK25       KLID(XANTKL)
     C                   EXSR      SRORVEKT
     C     FRISOKK4      READE     FRISOK                                 33
     C                   ENDDO
      *
     C                   EXSR      SRSUMGEB
      *
      * <ConsignmentStructure>
     C                   EVAL      SDATA = TXT(007)
     C                   EXSR      TILFIL
      * <ConsignmentSet>
     C                   EVAL      SDATA = TXT(009)
     C                   EXSR      TILFIL
      * ConsignmentSet
     C                   EXSR      SRCONSET
      * ConsignmentGoodsItemSet
     C                   EXSR      SRCGISET
      * Party (Consignor/Consignee)
     C                   EXSR      SRPARTYM
      * DangerousGoods
     C                   EXSR      SRDGOODS
      * TransportLeg
     C                   EXSR      SRTRLEG
      * TermOfDelivery
     C                   EXSR      SRTOD
      * Total Charges
     C                   EXSR      SRCHARGET
      * Service
     C                   EXSR      SRSERVICE
      * DateAndTimes
     C                   EXSR      SRDAT
      * MessageReferance
     C                   EXSR      OPDEDIM
      * </ConsignmentSet>
     C                   EVAL      SDATA = TXT(010)
     C                   EXSR      TILFIL
      *
      * </ConsignmentStructure>
     C                   EVAL      SDATA = TXT(008)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONSET      BEGSR
      ***********************************************
     C     DOKUFK        CHAIN     DOKUF                              33
     C                   IF        *IN33
     C     FRISOKK1      CHAIN     FRISOK                             33
     C                   DOW       *IN33 = *OFF
     C                   MOVEL     FSSOK         XAN03             3
     C                   IF        XAN03 = '707'
     C                   LEAVE
     C                   END
     C     FRISOKK1      READE     FRISOK                                 33
     C                   ENDDO
     C   33              EVAL      DF1004 = HERFA
     C  N33              EVAL      DF1004 = FSSOK
     C                   END
      * <Consignment
     C                   EVAL      XTXT801 = DF1004
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(011))
     C                             + ' consignmentId="'
     C                             + %trim(XTXT802)
     C                             + '" idType="730">'
     C                   EXSR      TILFIL
      * <TotalGrossWeight
     C                   EVAL      SDATA = %trim(TXT(013))
     C                             + ' unitCode="KGM">'
     C                             + %trim(%editc(HEVKT:'Z'))
     C                             + %trim(TXT(014))
     C                   EXSR      TILFIL
      * <TotalVolume
     C                   IF        HEM3 > 0
     C     HEM3          MULT      1000          XXM3              7 0
     C                   EVAL      SDATA = %trim(TXT(015))
     C                             + ' unitCode="DMQ">'
     C                             + %trim(%editc(XXM3:'3'))
     C                             + %trim(TXT(016))
     C                   EXSR      TILFIL
     C                   END
      * <NumberOfPackages
     C                   EVAL      SDATA = %trim(TXT(017))
     C                             + ' unitCode="PCE">'
     C                             + %trim(%editc(HENT:'Z'))
     C                             + %trim(TXT(018))
     C                   EXSR      TILFIL
      * Reference
     C                   EXSR      SRREF
      * FreeText
     C                   EXSR      SRFRTEXT
      * </Consignment>
     C                   EVAL      SDATA = TXT(012)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRREF         BEGSR
      ***********************************************
     C     FRISOKK2      SETLL     FRISOK
     C                   EVAL      *IN33  = *OFF
     C                   EVAL      FSKODE = 'AAS'
     C                   EVAL      FSSOK  = SYSPEDREF
      *
     C                   DOW       *IN33 = *OFF
      *
     C                   IF        FSKODE = 'IFB'
     C                   MOVEL     FSSOK         XAN03             3
     C                   IF        XAN03 <> '707'
     C                   GOTO      NESTEREF
     C                   END
     C                   END
      *
     C     FSKODE        IFNE      'TOD'
     C     FSKODE        ANDNE     'ORD'
     C     FSKODE        ANDNE     '730'
     C     FSKODE        ANDNE     'FF '
     C     FSKODE        ANDNE     'VEK'
     C     FSKODE        ANDNE     'INB'
     C     FSKODE        ANDNE     'INH'
     C     FSKODE        ANDNE     'INS'
     C     FSKODE        ANDNE     'RIG'
     C     FSKODE        ANDNE     'HAK'
     C     FSKODE        ANDNE     'MR1'
     C     FSKODE        ANDNE     'MR2'
     C*                  IF        ((FSKODE <> 'CU') AND (FSKODE <> 'HAK'))
     C***                IF        FSKODE <> 'CU'
      * <Reference>
     C                   EVAL      SDATA = TXT(019)
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   IF        FSKODE = 'XXX'
     C*                  IF        ((FSKODE = 'XXX') OR (FSKODE = 'HAK'))
     C                   EVAL      XTXT801 = FSSOK25
     C                   ELSE
     C                   EVAL      XTXT801 = FSSOK
     C                   END
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(021))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(022))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   SELECT
     C                   WHEN      FSKODE = 'XXX'
     C                   MOVEL     FSSOK10       XXKODE            3
     C                   WHEN      FSKODE = 'IFB'
     C                   EVAL      XXKODE = 'CMR'
     C                   OTHER
     C                   EVAL      XXKODE = FSKODE
     C                   END
     C                   EVAL      SDATA = %trim(TXT(023))
     C                             + %trim(XXKODE)
     C                             + %trim(TXT(024))
     C                   EXSR      TILFIL
      * </Reference>
     C                   EVAL      SDATA = TXT(020)
     C                   EXSR      TILFIL
      *
     C                   END
      *
     C     NESTEREF      TAG
      *
     C     FRISOKK2      READE     FRISOK                                 33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRFRTEXT      BEGSR
      ***********************************************
     C     FRISOKK2      SETLL     FRISOK
     C     FRISOKK2      READE     FRISOK                                 33
      *
     C                   DOW       *IN33 = *OFF
      *
     C     FSKODE        IFEQ      'RIG'
      * <FreeText>
     C                   EVAL      SDATA = TXT(025)
     C                   EXSR      TILFIL
      * <Usage>AGW</Usage>
     C                   EVAL      SDATA = %TRIM(TXT(027))
     C                             + 'AGW'
     C                             + %TRIM(TXT(028))
     C                   EXSR      TILFIL
      * <Text>???</Text>
     C                   EVAL      SDATA = %TRIM(TXT(029))
     C                             + %TRIM(FSSOK)
     C                             + %TRIM(TXT(030))
     C                   EXSR      TILFIL
      * </FreeText>
     C                   EVAL      SDATA = TXT(026)
     C                   EXSR      TILFIL
     C                   LEAVE
     C                   END
      *
     C     FRISOKK2      READE     FRISOK                                 33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCGISET      BEGSR
      ***********************************************
     C     DOKUFVK       SETLL     DOKUFV
     C     DOKUFVK       READE     DOKUFV                                 33
     C                   Z-ADD     0             XKLNR             3 0
     C                   DOW       *IN33 = *OFF
      * <ConsignmentGoodsItemSet>
     C                   EVAL      SDATA = TXT(031)
     C                   EXSR      TILFIL
      * <GoodsItem
     C                   ADD       1             XKLNR
     C                   EVAL      SDATA = %trim(TXT(033))
     C                             + ' goodsItemId="'
     C                             + %trim(%editc(XKLNR:'Z'))
     C                             + '">'
     C                   EXSR      TILFIL
      * <GoodsDescription>
     C                   EVAL      XTXT801 = FVVT
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(035))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(036))
     C                   EXSR      TILFIL
      * <NumberOfPackages>
     C                   EVAL      SDATA = %trim(TXT(037))
     C                             + '1'
     C                             + %trim(TXT(038))
     C                   EXSR      TILFIL
      * <PackageTypeCode>
     C                   IF        FVPAKN = *BLANKS
     C                   EVAL      FVPAKN = 'PK'
     C                   END
     C                   EVAL      SDATA = %trim(TXT(039))
     C                             + %trim(FVPAKN)
     C                             + %trim(TXT(040))
     C                   EXSR      TILFIL
      * <GrossWeight
     C                   EVAL      SDATA = %trim(TXT(041))
     C                             + ' unitCode="KGM">'
     C                             + %trim(%editc(FVVKT:'Z'))
     C                             + %trim(TXT(042))
     C                   EXSR      TILFIL
      * <Volume
     C                   IF        FVVOL > 0
     C     FVVOL         MULT      1000          XXM3              7 0
     C                   EVAL      SDATA = %trim(TXT(043))
     C                             + ' unitCode="DMQ">'
     C                             + %trim(%editc(XXM3:'Z'))
     C                             + %trim(TXT(044))
     C                   EXSR      TILFIL
     C                   END
      * </GoodsItem>
     C                   EVAL      SDATA = TXT(034)
     C                   EXSR      TILFIL
      *
      *
     C     DOKUFMK       CHAIN     DOKUFM                             33
     C   33              IF        TROPD2 <> 0
     C     DOKUFMKDUP    CHAIN     DOKUFM                             33
     C                   END
      * <Package
     C                   EVAL      XTXT801 = FMMRK1
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   IF        (((FMMRK101 <> *BLANKS) AND
     C                               (FMMRK102 <> *BLANKS)) AND
     C                               (FMMRK103 = *BLANKS))
     C                   EVAL      XIDTYPE = '30'
     C                   ELSE
     C                   EVAL      XIDTYPE = '24'
     C                   END
     C                   EVAL      SDATA = %trim(TXT(045))
     C                             + ' idType="'
     C                             + %trim(XIDTYPE)
     C                             + '" packageId="'
     C                             + %trim(XTXT802)
     C                             + '">'
     C                   EXSR      TILFIL
      * <Height
     C                   IF        FVHOY > 0
     C     FVHOY         DIV       10            XNR72             7 2
     C                   EVAL      SDATA = %trim(TXT(047))
     C                             + ' unitCode="DMT">'
     C                             + %trim(%editc(XNR72:'3'))
     C                             + %trim(TXT(048))
     C                   EXSR      TILFIL
     C                   END
      * <Width
     C                   IF        FVBRD > 0
     C     FVBRD         DIV       10            XNR72
     C                   EVAL      SDATA = %trim(TXT(049))
     C                             + ' unitCode="DMT">'
     C                             + %trim(%editc(XNR72:'3'))
     C                             + %trim(TXT(050))
     C                   EXSR      TILFIL
     C                   END
      * <Length
     C                   IF        FVLEN > 0
     C     FVLEN         DIV       10            XNR72
     C                   EVAL      SDATA = %trim(TXT(051))
     C                             + ' unitCode="DMT">'
     C                             + %trim(%editc(XNR72:'3'))
     C                             + %trim(TXT(052))
     C                   EXSR      TILFIL
     C                   END
      * <PackageTypeCode>
     C                   EVAL      SDATA = %trim(TXT(053))
     C                             + %trim(FVPAKN)
     C                             + %trim(TXT(054))
     C                   EXSR      TILFIL
      * <PackageTypeText>
     C                   EVAL      XTXT801 = FVVT
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(055))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(056))
     C                   EXSR      TILFIL
      * <GrossWeight
     C                   EVAL      SDATA = %trim(TXT(041))
     C                             + ' unitCode="KGM">'
     C                             + %trim(%editc(FVVKT:'Z'))
     C                             + %trim(TXT(042))
     C                   EXSR      TILFIL
      * <Volume
     C                   IF        FVVOL > 0
     C     FVVOL         MULT      1000          XXM3              7 0
     C                   EVAL      SDATA = %trim(TXT(043))
     C                             + ' unitCode="DMQ">'
     C                             + %trim(%editc(XXM3:'Z'))
     C                             + %trim(TXT(044))
     C                   EXSR      TILFIL
     C                   END
      * Reference
     C                   EXSR      SRREFPAC
      * Charges
     C                   EXSR      SRCHARGE
      * </Package>
     C                   EVAL      SDATA = TXT(046)
     C                   EXSR      TILFIL
      * </ConsignmentGoodsItemSet>
     C                   EVAL      SDATA = TXT(032)
     C                   EXSR      TILFIL
      *
     C     DOKUFVK       READE     DOKUFV                                 33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRREFPAC      BEGSR
      ***********************************************
      *
     C                   IF        XIDTYPE = '30'
      * HENT REF FRA FRISOK MED KODE = 'HAK'
     C                   EVAL      FMMRK1 = KLID(XKLNR)
     C                   END
      *
     C                   IF        FMMRK1 <> *BLANKS
      * <Reference>
     C                   EVAL      SDATA = TXT(019)
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   EVAL      SDATA = %trim(TXT(021))
     C                             + %trim(FMMRK1)
     C                             + %trim(TXT(022))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   EVAL      SDATA = %trim(TXT(023))
     C                             + '24'
     C                             + %trim(TXT(024))
     C                   EXSR      TILFIL
      * </Reference>
     C                   EVAL      SDATA = TXT(020)
     C                   EXSR      TILFIL
      *
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRPARTYM      BEGSR
      ***********************************************
      * <Party
      * Consignor
     C                   EVAL      SDATA = %trim(TXT(057))
     C                             + ' subClass="Consignor"'
     C                             + ' partyId="'
     C                             + %trim(%editc(HEKNS:'Z'))
     C                             + '" idType="87">'
     C                   EXSR      TILFIL
      * <Name>
     C                   EVAL      XTXT801 = HENAS
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(059))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(060))
     C                   EXSR      TILFIL
      * <Address
     C                   EVAL      SDATA = %trim(TXT(061))
     C                             + ' subClass="PhysicalAddress">'
     C                   EXSR      TILFIL
      *
     C                   MOVE      *BLANKS       XPN               9
     C                   MOVE      *BLANKS       XPS              30
     C     ' '           SCAN      HEADS3        XPOS              3 0    33
     C  N33              Z-ADD     1             XPOS
     C                   IF        XPOS > 1
     C                   IF        XPOS > 10
     C                   Z-ADD     10            XPOS
     C                   END
     C                   EVAL      XPN = %SUBST(HEADS3:1:XPOS-1)
     C                   EVAL      XPOS = XPOS + 1
     C                   END
     C                   EVAL      XPS = %SUBST(HEADS3:XPOS:31-XPOS)
      * <PostalCode>
     C                   EVAL      SDATA = %trim(TXT(063))
     C                             + %trim(XPN)
     C                             + %trim(TXT(064))
     C                   EXSR      TILFIL
      * <City>
     C                   EVAL      XTXT801 = XPS
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(065))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(066))
     C                   EXSR      TILFIL
      * <Country>
     C                   EVAL      SDATA = %trim(TXT(067))
     C                             + %trim(HELKA)
     C                             + %trim(TXT(068))
     C                   EXSR      TILFIL
      * <Street>
     C                   EVAL      XXSTREET = HEADS1
     C                   IF        HEADS2 <> *BLANKS
     C                   CAT       ',':0         XXSTREET         72
     C                   CAT       HEADS2:1      XXSTREET
     C                   END
     C                   EVAL      XTXT801 = XXSTREET
     C                   Z-ADD     70            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(069))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(070))
     C                   EXSR      TILFIL
      * </Address>
     C                   EVAL      SDATA = TXT(062)
     C                   EXSR      TILFIL
      * Contact
     C                   EXSR      SRCONTACT
      * </Party>
     C                   EVAL      SDATA = TXT(058)
     C                   EXSR      TILFIL
      * <Party
      * Consignee
     C                   EVAL      SDATA = %trim(TXT(057))
     C                             + ' subClass="Consignee"'
     C                             + ' partyId="'
     C                             + %trim(%editc(HEKNK:'Z'))
     C                             + '" idType="87">'
     C                   EXSR      TILFIL
      * <Name>
     C                   EVAL      XTXT801 = HENAK
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(059))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(060))
     C                   EXSR      TILFIL
      * <Address
     C                   EVAL      SDATA = %trim(TXT(061))
     C                             + ' subClass="PhysicalAddress">'
     C                   EXSR      TILFIL
      *
     C                   MOVE      *BLANKS       XPN               9
     C                   MOVE      *BLANKS       XPS              30
     C     ' '           SCAN      HEADK3        XPOS              3 0    33
     C  N33              Z-ADD     1             XPOS
     C                   IF        XPOS > 1
     C                   IF        XPOS > 10
     C                   Z-ADD     10            XPOS
     C                   END
     C                   EVAL      XPN = %SUBST(HEADK3:1:XPOS-1)
     C                   EVAL      XPOS = XPOS + 1
     C                   END
     C                   EVAL      XPS = %SUBST(HEADK3:XPOS:31-XPOS)
      * <PostalCode>
     C                   EVAL      SDATA = %trim(TXT(063))
     C                             + %trim(XPN)
     C                             + %trim(TXT(064))
     C                   EXSR      TILFIL
      * <City>
     C                   EVAL      XTXT801 = XPS
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(065))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(066))
     C                   EXSR      TILFIL
      * <Country>
     C                   EVAL      SDATA = %trim(TXT(067))
     C                             + %trim(HETRI)
     C                             + %trim(TXT(068))
     C                   EXSR      TILFIL
      * <Street>
     C                   EVAL      XXSTREET = HEADK1
     C                   IF        HEADK2 <> *BLANKS
     C                   CAT       ',':0         XXSTREET         72
     C                   CAT       HEADK2:1      XXSTREET
     C                   END
     C                   EVAL      XTXT801 = XXSTREET
     C                   Z-ADD     70            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(069))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(070))
     C                   EXSR      TILFIL
      * </Address>
     C                   EVAL      SDATA = TXT(062)
     C                   EXSR      TILFIL
      * Contact
     C                   EXSR      SRCONTACT
      * </Party>
     C                   EVAL      SDATA = TXT(058)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONTACT     BEGSR
      ***********************************************
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRDGOODS      BEGSR
      ***********************************************
     C                   GOTO      SLSRDGODS
      * <DangerousGoods>
     C                   EVAL      SDATA = TXT(121)
     C                   EXSR      TILFIL
      * <UNNo>
     C                   EVAL      SDATA = %trim(TXT(123))
     C                             + %trim(TRFA11)
     C                             + %trim(TXT(124))
     C                   EXSR      TILFIL
      * <Commodity>
     C                   EVAL      SDATA = %trim(TXT(125))
     C                             + %trim(TRFA12)
     C                             + %trim(TXT(126))
     C                   EXSR      TILFIL
      * <Class>
     C                   EVAL      SDATA = %trim(TXT(127))
     C                             + %trim(TRFA12)
     C                             + %trim(TXT(128))
     C                   EXSR      TILFIL
      * <Group>
     C                   EVAL      SDATA = %trim(TXT(129))
     C                             + %trim(TRFA12)
     C                             + %trim(TXT(130))
     C                   EXSR      TILFIL
      * </DangerousGoods>
     C                   EVAL      SDATA = TXT(122)
     C                   EXSR      TILFIL
      *
     C     SLSRDGODS     ENDSR
      *
      ***********************************************
     C     SRTRLEG       BEGSR
      ***********************************************
      * <TransportLeg>
     C                   EVAL      SDATA = TXT(075)
     C                   EXSR      TILFIL
      * <TransportLegType>
     C                   EVAL      SDATA = %trim(TXT(077))
     C                             + '20'
     C                             + %trim(TXT(078))
     C                   EXSR      TILFIL
      * <MeansOfTransport>
     C                   EVAL      SDATA = TXT(079)
     C                   EXSR      TILFIL
      * <MeansOfTransportId>
     C                   IF        HEHAWB <> *BLANKS
     C                   EVAL      SDATA = %trim(TXT(119))
     C                             + %trim(HEHAWB)
     C                             + %trim(TXT(120))
     C                   EXSR      TILFIL
     C                   END
      * <MeansOfTransportType>
     C                   EVAL      SDATA = %trim(TXT(081))
     C                             + '31'
     C                             + %trim(TXT(082))
     C                   EXSR      TILFIL
      * </MeansOfTransport>
     C                   EVAL      SDATA = TXT(080)
     C                   EXSR      TILFIL
      * </TransportLegType>
     C                   EVAL      SDATA = TXT(076)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRTOD         BEGSR
      ***********************************************
      * <TermsOfDelivery>
     C                   EVAL      SDATA = TXT(083)
     C                   EXSR      TILFIL
      * <TODConditionCode>
     C                   EVAL      SDATA = %trim(TXT(085))
     C                             + %trim(HEFR)
     C                             + %trim(TXT(086))
     C                   EXSR      TILFIL
      * <TODConditionCodeList>
     C                   EVAL      SDATA = %trim(TXT(087))
     C                             + 'IncoTerms2000'
     C                             + %trim(TXT(088))
     C                   EXSR      TILFIL
      * <TODLocation>
     C     FRISOKK3      CHAIN     FRISOK                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      SDATA = %trim(TXT(089))
     C                             + %trim(FSSOK)
     C                             + %trim(TXT(090))
     C                   EXSR      TILFIL
     C                   END
      * </TermsOfDelivery>
     C                   EVAL      SDATA = TXT(084)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCHARGE      BEGSR
      ***********************************************
     C                   IF        XANTGEB > 0
      * <Charges>
     C                   EVAL      SDATA = TXT(131)
     C                   EXSR      TILFIL
      *
     C     1             DO        XANTGEB       XN                3 0
      * <ChargeItem>
     C                   IF        FNUM(XN) = 0
     C                   EVAL      SDATA = TXT(133)
     C                   CAT       '>':0         SDATA
     C                   ELSE
     C                   EVAL      SDATA = %TRIM(TXT(133))
     C                             + ' invoiceNumber="'
     C                             + %TRIM(%editc(FNUM(XN):'Z'))
     C                             + '">'
     C                   END
     C                   EXSR      TILFIL
      * <ChargeCode>
     C                   EVAL      SDATA = %trim(TXT(135))
     C                             + %trim(GEB1(XN))
     C                             + %trim(TXT(136))
     C                   EXSR      TILFIL
      * <ChargeText>
     C                   EVAL      SDATA = %trim(TXT(137))
     C                             + %trim(GEB2(XN))
     C                             + %trim(TXT(138))
     C                   EXSR      TILFIL
      * <NetAmount
     C                   IF        XKLNR = XANTKL
     C                   EVAL      XBEL = GEB3(XN) - GEB5(XN)
     C                   ELSE
     C                   EVAL(H)   XBEL = GEB3(XN) * FVVKT / XTOTORVEKT
     C                   EVAL      GEB5(XN) = GEB5(XN) + XBEL
     C                   END
     C                   EVAL      SDATA = %trim(TXT(139))
     C                             + ' currencyIdentificationCode="'
     C                             + %trim(GEB4(XN))
     C                             + '">'
     C                             + %trim(%editc(XBEL:'N'))
     C                             + %trim(TXT(140))
     C                   EXSR      TILFIL
      * </ChargeItem>
     C                   EVAL      SDATA = TXT(134)
     C                   EXSR      TILFIL
      *
     C                   ENDDO
      * </Charges>
     C                   EVAL      SDATA = TXT(132)
     C                   EXSR      TILFIL
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCHARGET     BEGSR
      ***********************************************
     C                   IF        XANTGEB > 0
      * <Charges>
     C                   EVAL      SDATA = TXT(131)
     C                   EXSR      TILFIL
     C     1             DO        XANTGEB       XN
      * <ChargeItem>
     C                   IF        FNUM(XN) = 0
     C                   EVAL      SDATA = TXT(133)
     C                   CAT       '>':0         SDATA
     C                   ELSE
     C                   EVAL      SDATA = %TRIM(TXT(133))
     C                             + ' invoiceNumber="'
     C                             + %TRIM(%editc(FNUM(XN):'Z'))
     C                             + '">'
     C                   END
     C                   EXSR      TILFIL
      * <ChargeCode>
     C                   EVAL      SDATA = %trim(TXT(135))
     C                             + %trim(GEB1(XN))
     C                             + %trim(TXT(136))
     C                   EXSR      TILFIL
      * <ChargeText>
     C                   EVAL      SDATA = %trim(TXT(137))
     C                             + %trim(GEB2(XN))
     C                             + %trim(TXT(138))
     C                   EXSR      TILFIL
      * <NetAmount
     C                   EVAL      SDATA = %trim(TXT(139))
     C                             + ' currencyIdentificationCode="'
     C                             + %trim(FAVAL)
     C                             + '">'
     C                             + %trim(%editc(GEB3(XN):'N'))
     C                             + %trim(TXT(140))
     C                   EXSR      TILFIL
      * </ChargeItem>
     C                   EVAL      SDATA = TXT(134)
     C                   EXSR      TILFIL
      *
     C                   ENDDO
      * </Charges>
     C                   EVAL      SDATA = TXT(132)
     C                   EXSR      TILFIL
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSERVICE     BEGSR
      ***********************************************
      * <Service>
     C                   EVAL      SDATA = TXT(091)
     C                   EXSR      TILFIL
      * <BasicServiceCode>
     C                   SELECT
     C                   WHEN      TRFA11 <> *BLANKS
     C                   MOVE      'SPS'         XXBSC             3
     C                   WHEN      HEKDPL = 'E'
     C                   MOVE      'PRI'         XXBSC
     C                   OTHER
     C                   MOVE      'STD'         XXBSC
     C                   END
     C                   EVAL      SDATA = %trim(TXT(093))
     C                             + %trim(XXBSC)
     C                             + %trim(TXT(094))
     C                   EXSR      TILFIL
      * <AdditionalServiceCode>
      * </Service>
     C                   EVAL      SDATA = TXT(092)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRDAT         BEGSR
      ***********************************************
      * <DateAndTimes    TransportOrderDate
     C                   EVAL      SDATA = %trim(TXT(099))
     C                             + ' subClass="TransportOrderDate">'
     C                   EXSR      TILFIL
      * <Year>
     C                   EVAL      SDATA = %trim(TXT(101))
     C                             + %trim(HEDTOPÅ)
     C                             + %trim(TXT(102))
     C                   EXSR      TILFIL
      * <Month>
     C                   EVAL      SDATA = %trim(TXT(103))
     C                             + %trim(HEDTOPM)
     C                             + %trim(TXT(104))
     C                   EXSR      TILFIL
      * <Day>
     C                   EVAL      SDATA = %trim(TXT(105))
     C                             + %trim(HEDTOPD)
     C                             + %trim(TXT(106))
     C                   EXSR      TILFIL
      * <Hour>
      * <Minute>
      * </DateAndTimes>
     C                   EVAL      SDATA = TXT(100)
     C                   EXSR      TILFIL
      *
      * <DateAndTimes    DeliveryDate
     C                   EVAL      SDATA = %trim(TXT(099))
     C                             + ' subClass="DeliveryDate">'
     C                   EXSR      TILFIL
      * <Year>
     C                   EVAL      SDATA = %trim(TXT(101))
     C                             + %trim(TRSDTDÅ)
     C                             + %trim(TXT(102))
     C                   EXSR      TILFIL
      * <Month>
     C                   EVAL      SDATA = %trim(TXT(103))
     C                             + %trim(TRSDTDM)
     C                             + %trim(TXT(104))
     C                   EXSR      TILFIL
      * <Day>
     C                   EVAL      SDATA = %trim(TXT(105))
     C                             + %trim(TRSDTDD)
     C                             + %trim(TXT(106))
     C                   EXSR      TILFIL
      * <Hour>
     C                   EVAL      SDATA = %trim(TXT(107))
     C                             + %trim(TRSDTKH)
     C                             + %trim(TXT(108))
     C                   EXSR      TILFIL
      * <Minute>
     C                   EVAL      SDATA = %trim(TXT(109))
     C                             + %trim(TRSDTKM)
     C                             + %trim(TXT(110))
     C                   EXSR      TILFIL
      * </DateAndTimes>
     C                   EVAL      SDATA = TXT(100)
     C                   EXSR      TILFIL
      *
      * <DateAndTimes    PickupDate
     C                   EVAL      SDATA = %trim(TXT(099))
     C                             + ' subClass="PickupDate">'
     C                   EXSR      TILFIL
      * <Year>
     C                   EVAL      SDATA = %trim(TXT(101))
     C                             + %trim(TRSDFDÅ)
     C                             + %trim(TXT(102))
     C                   EXSR      TILFIL
      * <Month>
     C                   EVAL      SDATA = %trim(TXT(103))
     C                             + %trim(TRSDFDM)
     C                             + %trim(TXT(104))
     C                   EXSR      TILFIL
      * <Day>
     C                   EVAL      SDATA = %trim(TXT(105))
     C                             + %trim(TRSDFDD)
     C                             + %trim(TXT(106))
     C                   EXSR      TILFIL
      * <Hour>
     C                   EVAL      SDATA = %trim(TXT(107))
     C                             + %trim(TRSDFKH)
     C                             + %trim(TXT(108))
     C                   EXSR      TILFIL
      * <Minute>
     C                   EVAL      SDATA = %trim(TXT(109))
     C                             + %trim(TRSDFKM)
     C                             + %trim(TXT(110))
     C                   EXSR      TILFIL
      * </DateAndTimes>
     C                   EVAL      SDATA = TXT(100)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRPARTYS      BEGSR
      ***********************************************
      * <PartyStructure>
     C                   EVAL      SDATA = TXT(111)
     C                   EXSR      TILFIL
      * MessageSender
      * <PartySet>
     C                   EVAL      SDATA = TXT(113)
     C                   EXSR      TILFIL
      * <Party
     C                   EVAL      SDATA = %trim(TXT(057))
     C                             + ' subClass="MessageSender"'
     C                             + ' idType="9"'
     C                             + ' partyId="'
     C                             + %trim(S0004)
     C                             + '">'
     C                             + %trim(TXT(058))
     C                   EXSR      TILFIL
      * </PartySet>
     C                   EVAL      SDATA = TXT(114)
     C                   EXSR      TILFIL
      * MessageRecipient
      * <PartySet>
     C                   EVAL      SDATA = TXT(113)
     C                   EXSR      TILFIL
      * <Party
     C                   EVAL      SDATA = %trim(TXT(057))
     C                             + ' subClass="MessageRecipient"'
     C                             + ' idType="30"'
     C                             + ' partyId="'
     C                             + %trim(S0010)
     C                             + '">'
     C                             + %trim(TXT(058))
     C                   EXSR      TILFIL
      * </PartySet>
     C                   EVAL      SDATA = TXT(114)
     C                   EXSR      TILFIL
      * </PartyStructure>
     C                   EVAL      SDATA = TXT(112)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRDATSTR      BEGSR
      ***********************************************
      * <DateAndTimesStructure>
     C                   EVAL      SDATA = TXT(115)
     C                   EXSR      TILFIL
      * <DateAndTimesSet>
     C                   EVAL      SDATA = TXT(117)
     C                   EXSR      TILFIL
      * <DateAndTimes
     C                   EVAL      SDATA = %trim(TXT(099))
     C                             + ' subClass="MessageDate">'
     C                   EXSR      TILFIL
      * <Year>
     C                   EVAL      SDATA = %trim(TXT(101))
     C                             + %trim(WDTÅ)
     C                             + %trim(TXT(102))
     C                   EXSR      TILFIL
      * <Month>
     C                   EVAL      SDATA = %trim(TXT(103))
     C                             + %trim(WDTM)
     C                             + %trim(TXT(104))
     C                   EXSR      TILFIL
      * <Day>
     C                   EVAL      SDATA = %trim(TXT(105))
     C                             + %trim(WDTD)
     C                             + %trim(TXT(106))
     C                   EXSR      TILFIL
      * <Hour>
     C                   EVAL      SDATA = %trim(TXT(107))
     C                             + %trim(WTMH)
     C                             + %trim(TXT(108))
     C                   EXSR      TILFIL
      * <Minute>
     C                   EVAL      SDATA = %trim(TXT(109))
     C                             + %trim(WTMM)
     C                             + %trim(TXT(110))
     C                   EXSR      TILFIL
      * </DateAndTimes>
     C                   EVAL      SDATA = TXT(100)
     C                   EXSR      TILFIL
      * </DateAndTimesSet>
     C                   EVAL      SDATA = TXT(118)
     C                   EXSR      TILFIL
      * </DateAndTimesStructure>
     C                   EVAL      SDATA = TXT(116)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSLUTT       BEGSR
      ***********************************************
      *
     C                   EVAL      SDATA = TXT(006)
     C                   EXSR      TILFIL
      *
      * Oppdater EDIS
     C                   WRITE     EDISR
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
     C                   CLOSE     ECMSG1
     C                   CLOSE     DOKUF
     C                   CLOSE     DOKUFM
     C                   CLOSE     DOKUFV
     C                   CLOSE     FRISOK
     C                   CLOSE     FAK2
     C                   CLOSE     KODTGE
     C                   CLOSE     EDIC
     C                   CLOSE     EDIS
     C                   CLOSE     EDIM
     C                   CLOSE     EDISE
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSEND        BEGSR
      ***********************************************
     C                   CLOSE     XMLTILP
     C                   EVAL      XFILNVN = 'NCIC'
     C                   CAT       XMBRC:0       XFILNVN          16
     C                   CALL      'SYXML03S'
     C                   PARM                    XFILNVN
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRORVEKT      BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        XORVEKT           9 0
     C     1             DO        10            XN2               3 0
     C                   SELECT
     C                   WHEN      FS10(XN2) = ' '
     C                   LEAVE
     C                   WHEN      ((FS10(XN2) >= '0') AND (FS10(XN2) <= '9'))
     C                   MULT      10            XORVEKT
     C                   MOVE      FS10(XN2)     XORVEKT
     C                   ENDSL
     C                   ENDDO
     C                   ADD       XORVEKT       XTOTORVEKT
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSUMGEB      BEGSR
      ***********************************************
     C     FRISOKK5      CHAIN     FRISOK                             33
     C                   IF        *IN33
     C                   Z-ADD     200000428     XKN               8 0
     C                   ELSE
     C                   MOVEL     FSSOK         XKN
     C                   END
      *
     C                   MOVE      *ZEROS        XANTGEB           3 0
     C     FAK2K         SETLL     FAK2
     C     FAK2K         READE     FAK2                                   33
     C                   DOW       *IN33 = *OFF
     C                   IF        (((FAKDA <> 'K') AND
     C                               (FASK <> 'I')) AND
     C                               (FABELV <> 0))
     C                   IF        ((FAKUNR <> XKN) AND (HEDTOP <= 20081023))
     C                   EVAL      XKN = FAKUNR
     C                   END
      *
     C                   IF        FAKUNR = XKN
     C                   ADD       1             XANTGEB
     C                   MOVE      FAVK          GEB1(XANTGEB)
     C                   MOVE      FAFAKT        FNUM(XANTGEB)
     C     KODTGEK       CHAIN     KODTGE                             33
     C                   EVAL      XAN41 = KGENOT
     C                   IF        FAVT <> *BLANKS
     C                   CAT       ',':0         XAN41            41
     C                   CAT       FAVT:0        XAN41
     C                   END
     C                   MOVE      XAN41         GEB2(XANTGEB)
     C                   MOVE      FABELV        GEB3(XANTGEB)
     C                   MOVE      FAVAL         GEB4(XANTGEB)
     C                   END
     C                   END
      *
     C     FAK2K         READE     FAK2                                   33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDEDIM       BEGSR
      ***********************************************
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EVAL      M0004 = S0004
     C                   EVAL      M0010 = S0010
     C                   MOVEL     CMN           M0062
     C                   EVAL      M0065 = 'XMLINH'
     C                   MOVEL     SYSPEDREF     M0068
     C                   MOVEL     'KVI'         M1001
     C                   MOVEL     HERFA         M1004
     C                   MOVE      'KVI'         M1225
     C                   EVAL      MSN = SSN
     C                   EVAL      MMN = CMN
     C                   EVAL      MSR = 'S'
     C                   EVAL      MST = 'C'
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   EVAL      MAVD = HEAVD
     C                   EVAL      MTDN = HEOPD
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     TILFIL        BEGSR
      ***********************************************
     C                   WRITE     EDISER
      *
     C                   MOVE      SDATA         XTPDTA
     C                   WRITE     XMLTILPR
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVST         BEGSR
      ***********************************************
     C     XN53          SUB       4             XN54              3 0
     C     XN53          SUB       5             XN55              3 0
     C     XN53          SUB       6             XN56              3 0
     C                   Z-ADD     0             XN52              3 0
     C                   MOVE      *BLANKS       XTXT802
     C     1             DO        80            XN51              3 0
     C                   IF        (((((XT801(XN51) = '&') OR
     C                                 (XT801(XN51) = '"')) OR
     C                                 (XT801(XN51) = XAPS)) OR
     C                                 (XT801(XN51) = '<')) OR
     C                                 (XT801(XN51) = '>'))
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   SELECT
     C                   WHEN      XT801(XN51) = '&'
     C                   IF        XN51 < XN55
     C                   MOVEA     '&amp;'       XT802(XN52)
     C                   ADD       4             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '"'
     C                   IF        XN51 < 75
     C                   MOVEA     '&quot;'      XT802(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = XAPS
     C                   IF        XN51 < XN56
     C                   MOVEA     '&apos;'      XT802(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '>'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&gt;'        XT802(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '<'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&lt;'        XT802(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   ENDSL
     C                   END
     C                   ELSE
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   MOVE      XT801(XN51)   XT802(XN52)
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     XMLINHL1K     KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     ECMSG1K       KLIST
     C                   KFLD                    ECSR
     C                   KFLD                    EC0065
     C                   KFLD                    EC0054
     C                   KFLD                    ECIIDC
     C     DOKUFK        KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    DFFBNR
     C     DOKUFVK       KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     DOKUFMK       KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    FVAVD
     C                   KFLD                    FVOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
     C     DOKUFMKDUP    KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    TRAVD2
     C                   KFLD                    TROPD2
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
     C     FRISOKK1      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XFSKODE
     C     FRISOKK2      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FAK2K         KLIST
     C                   KFLD                    XMAVD
     C                   KFLD                    XMOPD
     C     FRISOKK3      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XFSKODETOD
     C     FRISOKK4      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XFSKODEHAK
     C     FRISOKK5      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XFSKODEINH
     C     FRISOKK6      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XFSKODEINS
     C     KODTGEK       KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FAVK
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   MOVE      ' '           XXST              1
     C                   MOVE      'F'           FVRI
     C                   Z-ADD     1             DFFBNR
     C                   MOVE      'IFB'         XFSKODE           3
     C                   MOVE      'TOD'         XFSKODETOD        3
     C                   MOVE      'HAK'         XFSKODEHAK        3
     C                   MOVE      'INH'         XFSKODEINH        3
     C                   MOVE      *BLANKS       XIDTYPE          10
     C                   MOVE      'GE'          KGEUNI
     C                   MOVE      *ZEROS        XBEL             13 2
     C                   MOVEL     'S'           ECSR
     C                   MOVEL     'XMLINH'      EC0065
     C                   MOVEL     '1.1'         EC0054
     C                   MOVE      '/'           XXSTREKK1
     C                   MOVE      '/'           XXSTREKK2
     C                   BITON     '123457'      XAPS              1
     C                   BITOFF    '06'          XAPS
      *
     C                   ENDSR
      *
** TXT
<?xml version="1.0" encoding="ISO-8859-1"?>                                     001
<Transport collaboration="Instruction" messageFunction="InstructionConfirmation"002
versionBasedOn="3.2" language="no" domain="Transport" profile="BringInhouse"    003
definedBy="BIG" messageId="                                                     004
version="1.1" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">            005
</Transport>                                                                    006
<ConsignmentStructure>                                                          007
</ConsignmentStructure>                                                         008
<ConsignmentSet>                                                                009
</ConsignmentSet>                                                               010
<Consignment                                                                    011
</Consignment>                                                                  012
<TotalGrossWeight                                                               013
</TotalGrossWeight>                                                             014
<TotalVolume                                                                    015
</TotalVolume>                                                                  016
<NumberOfPackages                                                               017
</NumberOfPackages>                                                             018
<Reference>                                                                     019
</Reference>                                                                    020
<ReferenceNo>                                                                   021
</ReferenceNo>                                                                  022
<ReferenceType>                                                                 023
</ReferenceType>                                                                024
<FreeText>                                                                      025
</FreeText>                                                                     026
<Usage>                                                                         027
</Usage>                                                                        028
<Text>                                                                          029
</Text>                                                                         030
<ConsignmentGoodsItemSet>                                                       031
</ConsignmentGoodsItemSet>                                                      032
<GoodsItem                                                                      033
</GoodsItem>                                                                    034
<GoodsDescription>                                                              035
</GoodsDescription>                                                             036
<NumberOfPackages>                                                              037
</NumberOfPackages>                                                             038
<PackageTypeCode>                                                               039
</PackageTypeCode>                                                              040
<GrossWeight                                                                    041
</GrossWeight>                                                                  042
<Volume                                                                         043
</Volume>                                                                       044
<Package                                                                        045
</Package>                                                                      046
<Height                                                                         047
</Height>                                                                       048
<Width                                                                          049
</Width>                                                                        050
<Length                                                                         051
</Length>                                                                       052
<PackageTypeCode>                                                               053
</PackageTypeCode>                                                              054
<PackageTypeText>                                                               055
</PackageTypeText>                                                              056
<Party                                                                          057
</Party>                                                                        058
<Name>                                                                          059
</Name>                                                                         060
<Address                                                                        061
</Address>                                                                      062
<PostalCode>                                                                    063
</PostalCode>                                                                   064
<City>                                                                          065
</City>                                                                         066
<CountryCode>                                                                   067
</CountryCode>                                                                  068
<Street>                                                                        069
</Street>                                                                       070
<Contact>                                                                       071
</Contact>                                                                      072
<ContactName>                                                                   073
</ContactName>                                                                  074
<TransportLeg>                                                                  075
</TransportLeg>                                                                 076
<TransportLegType>                                                              077
</TransportLegType>                                                             078
<MeansOfTransport>                                                              079
</MeansOfTransport>                                                             080
<MeansOfTransportType>                                                          081
</MeansOfTransportType>                                                         082
<TermsOfDelivery>                                                               083
</TermsOfDelivery>                                                              084
<TODConditionCode>                                                              085
</TODConditionCode>                                                             086
<TODConditionCodeList>                                                          087
</TODConditionCodeList>                                                         088
<TODLocation>                                                                   089
</TODLocation>                                                                  090
<Service>                                                                       091
</Service>                                                                      092
<BasicServiceCode>                                                              093
</BasicServiceCode>                                                             094
<AdditionalServiceCode>                                                         095
</AdditionalServiceCode>                                                        096
<Priority>                                                                      097
</Priority>                                                                     098
<DateAndTimes                                                                   099
</DateAndTimes>                                                                 100
<Year>                                                                          101
</Year>                                                                         102
<Month>                                                                         103
</Month>                                                                        104
<Day>                                                                           105
</Day>                                                                          106
<Hour>                                                                          107
</Hour>                                                                         108
<Minute>                                                                        109
</Minute>                                                                       110
<PartyStructure>                                                                111
</PartyStructure>                                                               112
<PartySet>                                                                      113
</PartySet>                                                                     114
<DateAndTimesStructure>                                                         115
</DateAndTimesStructure>                                                        116
<DateAndTimesSet>                                                               117
</DateAndTimesSet>                                                              118
<MeansOfTransportId>                                                            119
</MeansOfTransportId>                                                           120
<DangerousGoods>                                                                121
</DangerousGoods>                                                               122
<UNNo>                                                                          123
</UNNo>                                                                         124
<Commodity>                                                                     125
</Commodity>                                                                    126
<Class>                                                                         127
</Class>                                                                        128
<Group>                                                                         129
</Group>                                                                        130
<Charges>                                                                       131
</Charges>                                                                      132
<ChargeItem                                                                     133
</ChargeItem>                                                                   134
<ChargeCode>                                                                    135
</ChargeCode>                                                                   136
<ChargeText>                                                                    137
</ChargeText>                                                                   138
<NetAmount                                                                      139
</NetAmount>                                                                    140
updateIndicator="Original"                                                      141
updateIndicator="Update"                                                        142
