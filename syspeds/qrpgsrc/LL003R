      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE GRUPPE/KUNDE-REGISTER     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FLLGRPK01  UF A E           K DISK
     FLLGRPK02  IF   E           K DISK
     F                                     RENAME(LLGRPKR:LLGRPKR2)
     FLLGRPK03  IF   E           K DISK
     F                                     RENAME(LLGRPKR:LLGRPKR3)
     FLLGRPK04  IF   E           K DISK
     F                                     RENAME(LLGRPKR:LLGRPKR4)
     FLLGRPK05  IF   E           K DISK
     F                                     RENAME(LLGRPKR:LLGRPKR5)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FLL003D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D XTXT01          C                   CONST('Kunde uten gr.-
     D                                     tilhørighet')
     D                 DS
     D  XGNV                   1     25
     D  XDLT                   1      7
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2GRP         XGRP              8 0
     C  N27              MOVE      *BLANKS       W2GSOK
     C                   MOVE      W2GSOK        XGSOK            10
     C   27              MOVE      *BLANKS       W2PSOK
     C                   MOVE      W2PSOK        XPSOK            10
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   27LGK02K        SETLL     LLGRPK02
     C   28LGK03K        SETLL     LLGRPK03
     C   29LGK04K        SETLL     LLGRPK04
     C     START1        TAG
     C   27              READ      LLGRPK02                               94
     C   28              READ      LLGRPK03                               94
     C   29LGK14K        READE     LLGRPK04                               94
     C   29
     CANN94GKPNR         CABEQ     0             START1
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C     CUNG1K        CHAIN     CUNDL01                            33
     C  N33              MOVEL     KNAVN         XGNV
     C   33              MOVEL     XTXT01        XGNV
     C     CUNP1K        CHAIN     CUNDL01                            33
     C  N33              MOVEL     KNAVN         XPNV
     C   33              MOVE      *BLANKS       XPNV
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C   27              READ      LLGRPK02                               94
     C   28              READ      LLGRPK03                               94
     C   29LGK14K        READE     LLGRPK04                               94
     C   29
     CANN94GKPNR         CABEQ     0             ST21
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   ELSE
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9998
     C                   SETOFF                                       30
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** SØK
     C   04              DO
     C                   EXSR      SØKDT1
     C                   GOTO      START3
     C                   END
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C     START4        TAG
     C   06              DO
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFNE      ' '                                          1<-B
     C     W2GRP         ORNE      XGRP
     C     W2GSOK        ORNE      XGSOK
     C     W2PSOK        ORNE      XPSOK
     C     W2NR          IFEQ      ' '                                           2<-B
     C                   EXSR      NYSØK
     C                   GOTO      START
     C                   ELSE                                                    2
     C                   MOVE      '1'           *IN06
     C                   MOVE      ' '           W2NR
     C                   GOTO      START4
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C     WSNR          IFEQ      '3'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     SUBCTL
     C                   GOTO      START5
     C                   END
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     START5        TAG
     C                   MOVE      '0'           *IN40
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C     *IN12         IFEQ      '1'
     C                   EXSR      MOVB2
     C                   ELSE
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKDT2
     C                   GOTO      START5
     C                   END
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           START5
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     NYSØK         BEGSR
      ***********************************************
     C                   SELECT
     C     W2GRP         WHENEQ    0
     C     W2GSOK        ANDNE     *BLANKS
     C     W2GSOK        ANDNE     XGSOK
     C                   MOVEA     '100'         *IN(27)
     C     W2GRP         WHENNE    0
     C     W2GRP         ORNE      XGRP
     C                   MOVEA     '001'         *IN(27)
     C                   OTHER
     C                   MOVEA     '010'         *IN(27)
     C                   ENDSL
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     WSGRP         IFEQ      0
     C                   MOVE      *BLANKS       WSGSOK
     C                   MOVEL     'A'           WSGSOK
     C                   MOVEL     XTXT01        WSGNV            30
     C                   ELSE
     C     CUWG1K        CHAIN     CUNDL01                            90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'SPK0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   ELSE
     C     WSGSOK        IFEQ      *BLANKS
     C                   MOVE      SONAVN        WSGSOK
     C                   END
     C                   MOVE      KNAVN         WSGNV
     C                   END
     C                   END
      *
     C     WSPNR         IFEQ      0
     C                   MOVE      *BLANKS       WSPSOK
     C                   MOVE      *BLANKS       WSPNV            30
     C                   ELSE
     C  N40WSPNR         CHAIN     LLGRPK05                           89
     C     *IN40         IFEQ      '0'
     C     *IN89         ANDEQ     '0'
     C                   MOVE      'YBC0872'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN89
     C                   ELSE
     C     CUWP1K        CHAIN     CUNDL01                            89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'SPK0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   ELSE
     C     WSPSOK        IFEQ      *BLANKS
     C                   MOVE      SONAVN        WSPSOK
     C                   END
     C                   MOVE      KNAVN         WSPNV
     C                   END
     C                   END
     C                   END
      *
     C  N40*IN30         IFEQ      '0'
     C     LGK01K        CHAIN(N)  LLGRPK01                           33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'YBC0618'     MSGNR
     C                   SETON                                        3090
     C                   END
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     LGK01K        CHAIN     LLGRPK01                           33
     C                   EXSR      MOVWGK
      *
     C     *IN40         IFEQ      '1'
     C                   UPDATE    LLGRPKR
     C                   UPDATE    SUBFIL
     C                   ELSE
     C                   WRITE     LLGRPKR
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   END
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGGK
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '3'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              MOVE      ' '           WSNR
     C   98              GOTO      SLHENT
     C                   EXSR      MOVGKW
     C                   GOTO      SLH
     C                   END
      *
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTGK
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '8'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      WRKGK
     C   12              GOTO      SLHENT
     C                   END
      *
     C   27WSNR          IFEQ      '9'
     C     XDLT          COMP      '*DELETE'                              98
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C     *IN98         IFEQ      '0'
     C                   MOVEA     '001'         *IN(27)
     C                   MOVE      GKGRP         W2GRP
     C                   MOVE      *BLANKS       W2GSOK
     C                   MOVE      *BLANKS       W2PSOK
     C                   GOTO      START
     C                   ELSE
     C                   GOTO      SLHENT
     C                   END
     C                   END
      *
     C     WSNR          IFEQ      'I'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DEAKTM
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      'D'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DEAKTA
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      'A'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   CALL      'LL049R'
     C                   PARM                    GKPNR
     C                   PARM                    XPNV
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C     SLH           ENDSR
      *
     C***********************************************
     C     CHGGK         BEGSR
     C***********************************************
     C                   EXSR      MOVGKW
     C                   MOVE      '1'           *IN40
     C                   Z-ADD     14            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C   04              GOTO      STCHG
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTGK         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     LGK11K        CHAIN     LLGRPK01                           33
     C  N33              DELETE    LLGRPKR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     WRKGK         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   CALL      'LL036R'
     C                   PARM                    FIFIRM
     C                   PARM                    XPNV
     C                   PARM                    GKPNR
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DEAKTM        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   CALL      'LL038R'
     C                   PARM                    FIFIRM
     C                   PARM                    GKGRP
     C                   PARM                    GKPNR
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DEAKTA        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   CALL      'LL039R'
     C                   PARM                    GKGRP
     C                   PARM                    GKPNR
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWGK        BEGSR
     C***********************************************
     C                   MOVE      WSGRP         GKGRP
     C                   MOVE      WSPNR         GKPNR
     C                   MOVE      WSGSOK        GKGSOK
     C                   MOVE      WSPSOK        GKPSOK
     C                   MOVEL     WSGNV         XGNV
     C                   MOVEL     WSPNV         XPNV
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVGKW        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      GKGRP         WSGRP
     C                   MOVE      GKPNR         WSPNR
     C                   MOVE      GKGSOK        WSGSOK
     C                   MOVE      GKPSOK        WSPSOK
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        GKGRP
     C                   MOVE      *ZEROS        GKPNR
     C                   MOVE      *BLANKS       GKGSOK
     C                   MOVE      *BLANKS       GKPSOK
     C                   MOVE      *BLANKS       XGNV
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *BLANKS       XPNV
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSGRP
     C                   MOVE      *ZEROS        WSPNR
     C                   MOVE      *BLANKS       WSGSOK
     C                   MOVE      *BLANKS       WSPSOK
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDT1        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     12
     C                   MOVE      *BLANKS       USØKNV           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W2GRP
     C                   PARM                    FIFIRM
     C                   PARM                    USØKNV
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLSØK1
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK1        ENDSR
      *
     C***********************************************
     C     SØKDT2        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     12
     C                   MOVE      *BLANKS       USØKNV           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSGRP
     C                   PARM                    FIFIRM
     C                   PARM                    USØKNV
     C     WSGRP         IFNE      0
     C     CUWG1K        CHAIN     CUNDL01                            33
     C     *IN33         IFEQ      '0'
     C                   MOVE      SONAVN        WSGSOK
     C                   END
     C                   END
     C                   Z-ADD     40            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     47
     C                   MOVE      *BLANKS       USØKNV           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSPNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKNV
     C     WSPNR         IFNE      0
     C     CUWP1K        CHAIN     CUNDL01                            33
     C     *IN33         IFEQ      '0'
     C                   MOVE      SONAVN        WSPSOK
     C                   END
     C                   END
     C                   Z-ADD     5             POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK2        ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUNG1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    GKGRP
     C     CUNP1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    GKPNR
     C     CUWG1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSGRP
     C     CUWP1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSPNR
     C     LGK01K        KLIST
     C                   KFLD                    WSGRP
     C                   KFLD                    WSPNR
     C     LGK11K        KLIST
     C                   KFLD                    GKGRP
     C                   KFLD                    GKPNR
     C     LGK02K        KLIST
     C                   KFLD                    W2GSOK
     C     LGK03K        KLIST
     C                   KFLD                    W2PSOK
     C     LGK04K        KLIST
     C                   KFLD                    W2GRP
     C                   KFLD                    W2PSOK
     C     LGK14K        KLIST
     C                   KFLD                    W2GRP
      *
     C     *ENTRY        PLIST
     C                   PARM                    UTYPE             8
      *
     C                   SELECT
     C     UTYPE         WHENEQ    'SØKGRP  '
     C                   MOVEA     '100'         *IN(27)
     C     UTYPE         WHENEQ    'SØKKUNDE'
     C                   MOVEA     '010'         *IN(27)
     C                   OTHER
     C                   MOVE      UTYPE         W2GRP
     C                   MOVEA     '001'         *IN(27)
     C                   END
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
