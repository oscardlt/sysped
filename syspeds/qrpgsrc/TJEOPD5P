      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJEOPD5P MODULE(*LIBL/TJEOPD5P *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKODTW     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WA              S              4
     D WAVD            S              4  0
     D JSONstring      s          32000
     D Error           S            150
     D                 DS
     D  KOWXXX                 1     41
     D  KOWFET                 1      1
N07  D  KOWVLF                 2      2
N08  D  KOWSOT                 3      4

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(WAVD:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"stdParametere" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      /free
       JSONstring=
           '{¬opdTyp¬ : ¬'+%trim(KOWSOT)+'¬,'
          +'¬klokkeJN¬ : ¬'+%trim(KLOKKE)+'¬,'
          +'¬test1¬ : ¬'+%trim(TEST1 )+'¬,'
          +'¬test2¬ : ¬'+%trim(TEST2 )+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KODTW
     C  N50              CLOSE     BRPARF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA      = zhbgetvar('AVD')
     C                   EVAL      WAVD    = c2n2(WA)
     C
     c                   endsr
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     KODTWK        KLIST
     C                   KFLD                    KOWUNI
     C                   KFLD                    WAVD
     C                   EVAL      KOWUNI  = 'W'
     C     BRPARKeyUs    klist
     C                   kfld                    WSUSER
     C                   kfld                    xxKUNR            8 0
     C                   kfld                    PAID
     C     BRPARKeyFi    klist
     C                   kfld                    FIuser           10
     C                   kfld                    xxKUNR            8 0
     C                   kfld                    PAID
     C                   EVAL      FiUser  = '*KUNDFT*'+BIFIRM

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      KODTW
     C  N50              OPEN      BRPARF
      * hent parametre kun dersom filer er åpnet
     C                   IF        *IN50=*OFF
     C                   EXSR      GetPar
     C                   ENDIF
      *
     C                   ENDSR
      *
      *********************************************************
     C     GetPar        Begsr
      *********************************************************
      * standard oppdr-type
     C     WAVD          ifne      *zeros
     C     KODTWK        CHAIN     KODTW                              33
     c                   endif
      * Krav om klokkeslett
     C                   movel     'N'           KLOKKE            1            Krav om klokkeslett
     C                   eval      PAID = 'OPKL '
     C     BRPARKeyUs    CHAIN     BRPARF                             33
     C   33BRPARKeyFi    CHAIN     BRPARF                             33
     C  N33              movel     PAVRDA        KLOKKE            1            Krav om klokkeslett
      * indikering av videre...
     c                   move      'N'           Test1             1
     c                   move      'J'           Test2             1
     C                   ENDSR
      *
