      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TRANSPORTØRER                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-87             ***
      **************************************************************
      *
     FAVRNR     UF A E           K DISK
     FKOSTT     UF A E           K DISK
     FTRAVV     UF A E           K DISK
     FTRAVV2    IF   E           K DISK
     F                                     RENAME(TRAVVR:TRAV2R)
     FTRAVV5    IF   E           K DISK
     F                                     RENAME(TRAVVR:TRAV5R)
     FTRAVH1    UF   E           K DISK
     FTRAN      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FTURER     UF   E           K DISK
     FKOSBU     UF A E           K DISK
     FSYTR12FB  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  TVFRA                  1      5
     D  XDLT                   1      5
     D                 DS
     D  DSTXT                  1     35
     D  TUSTEF                11     15
     D  DSTXTB                17     17
     D  TUSTET                19     23
     D  TUNAT                 25     54
     C*
     C                   EXSR      INIT
     C*
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2TYP         YTYP
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   55TRAVVV        SETLL     TRAVV2
     C  N55TRAVVV        SETLL     TRAVV5
     C     START1        TAG
     C   55TRAVVV        READE     TRAVV2                                 94
     C  N55TRAVVV        READE     TRAVV5                                 94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        12
     C                   ADD       1             ZRECNO
     C     TVBLL         COMP      *ZEROS                                 96    *RØD
     C  N96TVTYP         IFEQ      'A'
     C     TVAVES        COMP      'A'                                    96    *RØD
     C                   END
     C                   WRITE     SUBFIL
     C   55TRAVVV        READE     TRAVV2                                 94
     C  N55TRAVVV        READE     TRAVV5                                 94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     6             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C* SORTERINGSMODUS..
     C     *IN02         IFEQ      *ON
     C     *IN55         IFEQ      *ON
     C                   MOVE      *OFF          *IN55
     C                   ELSE
     C                   MOVE      *ON           *IN55
     C                   END
     C                   GOTO      START
     C                   END
     C*** AVSLUTT - UT AV EVT - OPTION-2-LOOP I SYTR12
     C   12              MOVE      'J'           UF12
     C   12              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     08            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*** MELDE  AVREGNET
     C* VILKÅR I DDS - N88(SPØR = N) OG N87 (TYPE TRANSP. = 1)
     C   10              EXSR      AVRLEV
     C   10              GOTO      SLUTT
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C     *IN55         IFEQ      *ON
     C  N94TRAVVV        READPE    TRAVV2                                 94
     C  N94TRAVVV        READE     TRAVV2                                 94
     C                   ELSE
     C  N94TRAVVV        READPE    TRAVV5                                 94
     C  N94TRAVVV        READE     TRAVV5                                 94
     C                   END
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   04              DO
     C                   CALL      'SYFI10D'
     C                   PARM                    WSVK
     C                   Z-ADD     21            POSNBR
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C  N12              EXSR      SUMDET
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C* N40 = NY = ALLTID TYPE G = TILLEGG - HENT NESTE NR
     C  N40              DO
     C     TRAVHK        CHAIN     TRAVH1                             33
     C  N33              ADD       1             THJUSA
     C  N33              UPDATE    TRAVHR
     C  N33              MOVE      THJUSA        WSLNR
     C                   MOVE      'G'           WSTYP
     C                   END
     C* TESTS.......
     C                   Z-ADD     22            LINNBR
     C                   MOVEA     '0'           *IN(30)
     C                   MOVEA     '00000000'    *IN(70)
     C*
     C     WSOPD         IFNE      *ZEROS
     C     WSOAVD        IFEQ      *ZEROS
     C                   MOVE      TVTAVD        WSOAVD
     C                   END
     C     HEADFK        CHAIN     HEADF                              70
     C   70              DO
     C                   MOVE      'SPO0001'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLTB
     C                   END
     C                   END
     C* ENTEN KODE ELLER TEKST
     C     WSVK          IFNE      *BLANKS
     C     KODGEK        CHAIN     KODTGE                             71
     C                   ELSE
     C     WSTXT         COMP      *BLANKS                                71
     C                   END
     C   71              DO
     C                   MOVE      'DIV2219'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     24            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
     C* KAN HA ENDRET BRUTTO BELØP... - REKALKULER NETTO
     C     WSBLLB        IFNE      TVBLLB
     C     100           SUB       WSPRO         XPRFAK            7 4
     C                   DIV       100           XPRFAK
     C     WSBLLB        MULT      XPRFAK        WSBLL
     C                   Z-ADD(H)  WSBLL         XXBLLL           11 0          *AVRUNDING
     C                   Z-ADD     XXBLLL        WSBLL
     C                   Z-ADD     WSBLLB        TVBLLB
     C                   Z-ADD     WSBLL         TVBLL
     C                   ELSE
     C* KAN HA ENDRET NETTO BELØP... - REKALKULER NETTO
     C     WSBLL         IFNE      TVBLL
     C     100           SUB       WSPRO         XPRFAK            7 4
     C                   DIV       100           XPRFAK
     C     WSBLL         DIV       XPRFAK        WSBLLB
     C                   Z-ADD(H)  WSBLLB        XXBLLL           11 0          *AVRUNDING
     C                   Z-ADD     XXBLLL        WSBLLB
     C                   Z-ADD     WSBLLB        TVBLLB
     C                   Z-ADD     WSBLL         TVBLL
     C                   END
     C                   END
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSIK        CHAIN     TRAVV                              33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    TRAVVR
     C   33              WRITE     TRAVVR
     C     TVTYP         IFEQ      'A'
     C     TRAVHK        CHAIN     TURER                              33
     C  N33              DO
     C                   MOVE      TVAVES        TUTAKO
     C                   MOVE      FICURR        TUTVAL
     C                   Z-ADD     TVBLL         TUTBEL
     C* NÅR TRANSP.TYPE 1 (SENDER FAKT) OPPDAT. EVT. KOSBU-POST
     C     VMINCR        IFEQ      1
     C     VMINCR        OREQ      3
     C                   EXSR      KOST
     C                   END
     C                   UPDATE    TURERR
     C                   END
     C                   END
     C     TVBLL         COMP      *ZEROS                                 96    *RØD
     C  N96TVTYP         IFEQ      'A'
     C     TVAVES        COMP      'A'                                    96    *RØD
     C                   END
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     KOST          BEGSR
     C***********************************************
     C*
     C                   Z-ADD     TUTREF        KTNR
     C*
     C* TILDELER REFNUMMER
     C*
     C     KTNR          CHAIN     KOSBU                              34
     C  N34              UPDATE    KOSBUR
     C   34              MOVE      *ZEROS        TUTREF
     C     TUTREF        IFEQ      *ZEROS
     C     'Å'           CHAIN     KOSTT                              34
     C   34              MOVE      'Å'           KTTYP
     C   34              Z-ADD     1000000       KTNR
     C   34              WRITE     KOSTTR
     C  N34              ADD       1             KTNR
     C  N34              UPDATE    KOSTTR
     C                   END
     C*
     C     KTNR          CHAIN     KOSBU                              34
     C                   MOVE      FICURR        BUVAL
     C                   Z-ADD     TVBLL         BUBL
     C                   MOVE      TVAVES        BUTYPE
     C                   MOVE      *ZEROS        BUTNR
     C                   MOVE      TURMND        BUPMN
     C                   MOVE      TURAAR        BUPÅR
     C                   MOVE      TURACC        BUPCC
     C                   MOVE      'FRA'         BUVK
     C                   MOVE      VMTRLE        BULNR
     C                   MOVE      TUAVD         BUTAVD
     C                   MOVE      TUPRO         BUTUNR
     C                   MOVE      'E'           BUBLST
     C                   MOVE      KTNR          BUBNR
     C                   MOVE      *BLANKS       BUTXT
     C                   MOVEL     '* TUR *'     BUTXT
     C                   MOVE      TUBILN        BUBILN
     C                   MOVE      TUDT          BUFEDT
     C                   MOVE      TUBILK        BUBILK
     C                   MOVE      'T'           BUFER                          *AUTOM. FRA TUR/FERG
     C   34              WRITE     KOSBUR
     C  N34              UPDATE    KOSBUR
     C                   MOVE      KTNR          TUTREF
     C                   ENDSR
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       12            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     12            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C  N88WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C  N88WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C  N98TVTYP         COMP      'A'                                    98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C   88WSNR          IFEQ      '5'                                          SE PÅ
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C     TVBLL         COMP      *ZEROS                                 96    *RØD
     C  N96TVTYP         IFEQ      'A'
     C     TVAVES        COMP      'A'                                    96    *RØD
     C   96XDLT          COMP      '*DEL*'                            9696
     C                   END
     C                   ADD       7             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C   99
     CANN12              EXSR      SUMDET
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      TVTYP         WSTYP
     C                   MOVE      TVLNR         WSLNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSIK        CHAIN     TRAVV                              33
     C  N33              DELETE    TRAVVR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     12            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     TVBLL         COMP      *ZEROS                                 96    *RØD
     C  N96TVTYP         IFEQ      'A'
     C     TVAVES        COMP      'A'                                    96    *RØD
     C                   END
     C   96XDLT          COMP      '*DEL*'                            9696
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   04              DO
     C                   CALL      'SYFI10D'
     C                   PARM                    WSVK
     C                   Z-ADD     21            POSNBR
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     12            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   ENDSR
     C*
     C***********************************************
     C     SUMDET        BEGSR
     C***********************************************
     C     TRAVHK        CHAIN     TRAVH1                             33
     C* BLANK FILVARIABLE
     C                   MOVE      *ZEROS        THAVT
     C                   MOVE      *ZEROS        THBRT
     C                   MOVE      *ZEROS        THINH
     C                   MOVE      *ZEROS        THUTK
     C                   MOVE      *ZEROS        THRKV
     C                   MOVE      *ZEROS        THBUD
     C                   MOVE      *ZEROS        THJUS
     C                   MOVE      *ZEROS        THTOT
     C* AKKUMULER NYE TURVERDIER
     C     TRAVVV        SETLL     TRAVV2
     C     TRAVVV        READE     TRAVV2                                 94
     C     *IN94         DOWEQ     '0'
     C* SUM AVREGNING ER ENTEN AVTALE PÅ TUREN / ELLER SUM ANDRE TYPER.
     C     TUTAKO        IFEQ      'A'
     C     TVTYP         IFEQ      'A'
     C                   Z-ADD     TVBLL         THTOT
     C                   END
     C                   ELSE
     C     TVTYP         IFNE      'A'
     C                   ADD       TVBLL         THTOT
     C                   END
     C                   END
     C* SUM DEN ENKELTE TYPE OPPDATERES ALLTID UAVHENGIG AV OM DE SKAL
     C* INNGÅ I TOTALEN
     C     TVTYP         IFEQ      'B'
     C                   ADD       TVBLL         THBRT
     C                   END
     C     TVTYP         IFEQ      'C'
     C                   ADD       TVBLL         THINH
     C                   END
     C     TVTYP         IFEQ      'D'
     C                   ADD       TVBLL         THUTK
     C                   END
     C     TVTYP         IFEQ      'E'
     C                   ADD       TVBLL         THRKV
     C                   END
     C     TVTYP         IFEQ      'F'
     C                   ADD       TVBLL         THBUD
     C                   END
     C     TVTYP         IFEQ      'G'
     C                   ADD       TVBLL         THJUS
     C                   END
     C*
     C     TRAVVV        READE     TRAVV2                                 94
     C  N94              END
     C*
     C* JUSTER TRAVH POSTEN MED NYE VERDIER
     C  N33              UPDATE    TRAVHR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        TVTAVD
     C                   MOVE      *ZEROS        TVTUNR
     C                   MOVE      *BLANKS       TVTYP
     C                   MOVE      *ZEROS        TVLNR
     C                   MOVE      *ZEROS        TVOAVD
     C                   MOVE      *ZEROS        TVOPD
     C                   MOVE      *BLANKS       TVVK
     C                   MOVE      *BLANKS       TVTXT
     C                   MOVE      *ZEROS        TVPRO
     C                   MOVE      *BLANKS       TVAVES
     C                   MOVE      *BLANKS       TVOPKO
     C                   MOVE      *BLANKS       TVVAL
     C                   MOVE      *ZEROS        TVBL
     C                   MOVE      *ZEROS        TVBLL
     C                   MOVE      *ZEROS        TVBLLB
     C                   MOVE      *BLANKS       TVFRA
     C                   MOVE      *BLANKS       TVTIL
     C*
     C                   MOVEL     '*DEL*'       XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      'G'           WSTYP
     C                   MOVE      *ZEROS        WSLNR
     C                   MOVE      *ZEROS        WSOAVD
     C                   MOVE      *ZEROS        WSOPD
     C                   MOVE      *BLANKS       WSVK
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      *ZEROS        WSPRO
     C                   MOVE      *BLANKS       WSAVES
     C                   MOVE      *BLANKS       WSOPKO
     C                   MOVE      FICURR        WSVAL
     C                   MOVE      *ZEROS        WSBLL
     C                   MOVE      *ZEROS        WSBL
     C                   MOVE      *ZEROS        WSBLLB
     C                   MOVE      *BLANKS       WSFRA
     C                   MOVE      *BLANKS       WSTIL
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C                   MOVE      TVTYP         WSTYP
     C                   MOVE      TVLNR         WSLNR
     C                   MOVE      TVOAVD        WSOAVD
     C                   MOVE      TVOPD         WSOPD
     C                   MOVE      TVVK          WSVK
     C                   MOVE      TVTXT         WSTXT
     C                   MOVE      TVPRO         WSPRO
     C                   MOVE      TVAVES        WSAVES
     C                   MOVE      TVOPKO        WSOPKO
     C                   MOVE      TVVAL         WSVAL
     C                   MOVE      TVBL          WSBL
     C                   MOVE      TVBLL         WSBLL
     C                   MOVE      TVBLLB        WSBLLB
     C                   MOVE      TVFRA         WSFRA
     C                   MOVE      TVTIL         WSTIL
     C*
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C* FIXES VARIABLES
     C                   MOVE      UVTAVD        TVTAVD
     C                   MOVE      UVTUNR        TVTUNR
     C                   MOVE      WSTYP         TVTYP
     C                   MOVE      WSLNR         TVLNR
     C                   MOVE      WSOAVD        TVOAVD
     C                   MOVE      WSOPD         TVOPD
     C                   MOVE      WSVK          TVVK
     C                   MOVE      WSTXT         TVTXT
     C                   MOVE      WSPRO         TVPRO
     C                   MOVE      WSAVES        TVAVES
     C                   MOVE      WSOPKO        TVOPKO
     C                   MOVE      WSFRA         TVFRA
     C                   MOVE      WSTIL         TVTIL
     C     TVTYP         IFNE      'B'
     C*BRUTTO=NETTO (I LOK VALUTA) VED ALT ANNET ENN B(=OPPDRAG)
     C                   MOVE      WSBLL         WSBLLB
     C*(VED JUSTERING SETT VALUTA LIK FICURR)
     C     TVTYP         IFEQ      'G'
     C                   MOVE      FICURR        WSVAL
     C                   MOVE      WSBLL         WSBL
     C                   END
     C                   END
     C                   MOVE      WSBLL         TVBLL
     C                   MOVE      WSBLLB        TVBLLB
     C                   MOVE      *ZEROS        TVFAKT
     C                   MOVE      ' '           TVST
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     AVRLEV        BEGSR
     C***********************************************
     C* MERKER AVR. AV LEVERAND.TYPE MED 'A' I TRAVH SLIK AT POSTENE
     C* IKKE SKAL FORVEKSLES MED POSTER FOR "KREDITNOTA-TRANSPORTØRER"
     C*
     C     'T'           CHAIN     AVRNR                              33
     C   33              MOVE      'T'           AVKODE
     C   33              Z-ADD     1             AVAVNR
     C   33              WRITE     AVRNRR
     C  N33              ADD       1             AVAVNR
     C  N33              UPDATE    AVRNRR
     C*
     C     TRAVHK        CHAIN     TRAVH1                             33
     C  N33              MOVE      AVAVNR        THAVNR
     C  N33              MOVE      'A'           THST
     C  N33              UPDATE    TRAVHR
     C* DERSOM TUR PEKER PÅ KOSBU-POST OPPDATER DENNE.
     C* ELLERS LAG NY.
     C     TRAVHK        CHAIN     TURER                              33
     C  N33              DO
     C                   MOVE      AVAVNR        TUAVNR
     C                   MOVE      'X'           TUAVRE
     C     TUTREF        IFEQ      *ZEROS
     C     'Å'           CHAIN     KOSTT                              33
     C   33              MOVE      'Å'           KTTYP
     C   33              Z-ADD     1000000       KTNR
     C   33              WRITE     KOSTTR
     C  N33              ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
     C                   Z-ADD     KTNR          TUTREF
     C                   END
     C                   UPDATE    TURERR
     C*
     C     TUTREF        CHAIN     KOSBU                              33
     C  N33              DO
     C                   Z-ADD     THTOT         BUBL
     C                   MOVEL     '*AVREGN*'    BUTXT
     C                   UPDATE    KOSBUR
     C                   END
     C   33              DO
     C                   MOVE      ' '           BUST
     C                   MOVE      TUTREF        BUBNR
     C                   MOVE      TURMND        BUPMN
     C                   MOVE      TURAAR        BUPÅR
     C                   MOVE      TURACC        BUPCC
     C                   MOVE      'FRA'         BUVK
     C                   Z-ADD     THTOT         BUBL
     C                   MOVE      FICURR        BUVAL
     C                   MOVE      VMTRLE        BULNR
     C                   MOVE      'A'           BUTYPE
     C                   MOVE      'T'           BUBLST
     C                   MOVE      TUSG          BUSG
     C                   MOVE      *BLANKS       BUTXT
     C                   MOVE      '-'           DSTXTB
     C                   MOVEL     DSTXT         BUTXT
     C                   MOVEL     '*AVREGN*'    BUTXT
     C                   MOVE      *ZEROS        BUOAVD
     C                   MOVE      *ZEROS        BUOPD
     C                   MOVE      TUAVD         BUTAVD
     C                   MOVE      TUPRO         BUTUNR
     C                   MOVE      TUBILN        BUBILN
     C                   MOVE      TUBILK        BUBILK
     C                   MOVE      'T'           BUFER
     C                   MOVE      TUDT          BUFEDT
     C                   MOVE      *ZEROS        BUBNR2                         *FØRT BNR
     C                   MOVE      *ZEROS        BUBNR3                         *"SLAVE" AV.
     C                   MOVE      *ZEROS        BULNR3                         SLAVE-LØPENR
     C                   MOVE      *ZEROS        BUBLFD                         *FORDELT BEL
     C                   WRITE     KOSBUR
     C                   END
     C*
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UVTAVD
     C                   PARM                    UVTUNR
     C                   PARM                    USPØR             1
     C                   PARM                    UF12              1
     C* UF12 HAR KUN BETYDNING NÅR EN KOMMER INN FRA SYTR12 -
     C* RETUR TIL SYTR12 VIA F12-TAST BETYR AT EN GÅR UT AV EN EVT.
     C* SERIE MED TURER SOM ALLE MERKET MED "2" FOR ENDRE
     C                   MOVE      ' '           UF12
     C     *LIKE         DEFINE    TVTAVD        UVTAVD
     C     *LIKE         DEFINE    TVTUNR        UVTUNR
     C     *LIKE         DEFINE    TVTYP         YTYP
     C     *LIKE         DEFINE    TVLNR         YLNR
     C     *LIKE         DEFINE    TVLNR         Y2LNR
     C     *LIKE         DEFINE    TVLNR         WSLNR
     C                   MOVE      ' '           YTYP
     C                   MOVE      *ZEROS        YLNR
     C                   MOVE      *ZEROS        Y2LNR
     C                   MOVE      *ZEROS        WSLNR
     C                   MOVE      'GE'          YGEUNI            2
     C* KEY FOR CHAIN
     C     KODSIK        KLIST
     C                   KFLD                    UVTAVD
     C                   KFLD                    UVTUNR
     C                   KFLD                    WSTYP
     C                   KFLD                    WSLNR
     C* KEY FOR 1. GANG + SØK + RULL
     C     TRAVVV        KLIST
     C                   KFLD                    UVTAVD
     C                   KFLD                    UVTUNR
     C                   KFLD                    YYST
     C                   MOVE      ' '           YYST              1
     C*
     C     TRAVHK        KLIST
     C                   KFLD                    UVTAVD
     C                   KFLD                    UVTUNR
     C     TRANK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    THTNR
     C     KODGEK        KLIST
     C                   KFLD                    YGEUNI
     C                   KFLD                    WSVK
     C     HEADFK        KLIST
     C                   KFLD                    WSOAVD
     C                   KFLD                    WSOPD
     C*
     C     TRAVHK        CHAIN     TURER                              33
     C  N33TUAVRE        COMP      ' '                                9595
     C  N33
     CAN 95              MOVE      'J'           USPØR
     C  N33              UPDATE    TURERR
     C     TRAVHK        CHAIN     TRAVH1                             33
     C  N33              UPDATE    TRAVHR
     C*
     C                   READ      FIRM                                   33
     C     TRANK         CHAIN     TRAN                               33
     C     USPØR         COMP      'J'                                    88    *SPØR
     C     VMINCR        COMP      1                                  8787      *IKKE F10
     C*
     C                   ENDSR
     C*
