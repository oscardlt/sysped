      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKE004R MODULE(*LIBL/TDKE004R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     d WhereC          s           1024a
     d FirstW          s              1A
     d cQuote          c                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D Avd             S              4S 0
     D Avda            S              4
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d x               S             10  0
     d MyPoDS          ds                  Qualified  dim(999)
     d  fsavd                         4S 0
     d  fsopd                         7S 0
     d  fssok                        35
     d  fsdtop                        8S 0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"extList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' t2.dkeh_syop is null';
         if avd > 0;

         Exsr  AddAnd;
         avda = %editc(AVD:'X');
         Eval  WhereC =  %trim(WhereC)  +
         ' t1.fsavd = ' + avda;
         endif;

         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' t1.fssok <>' + cQuote + ' ' + cQuote;

         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' t1.fskode=' + cQuote + '*CE' + cQuote;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select FSAVD, FSOPD, FSSOK, FSDTOP ' +
                             'from frisok t1 LEFT JOIN dkeh t2 on ' +
                ' t1.fsavd=t2.dkeh_syav and t1.fsopd=t2.dkeh_syop ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'FSSOK ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
         Exec SQL CLOSE A33;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         avda    = zhbgetvar('AVD');
         avd     = c2n2(avda);
        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).fssok = *blanks;
         leave;
       endif;

           chain (mypods(x).fsavd: mypods(x).fsopd) HEADF;
           if not %found;
              henas = *blanks;
              henak = *blanks;
           endif;


        // Valider extern ref
           error=' ';

           if error = *blanks;
           if henas <> *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬fssok¬ : ¬'+%trim(mypods(x).fssok)+'¬, '
          +'¬fsavd¬ : ¬'+%trim(%editc(mypods(x).fsavd:'Z'))+'¬, '
          +'¬fsopd¬ : ¬'+%trim(%editc(mypods(x).fsopd:'Z'))+'¬, '
          +'¬fsdtop¬ : ¬'+%trim(%editc(mypods(x).fsdtop:'4'))+'¬, '
          +'¬henas¬ : ¬'+%trim(henas)+'¬, '
          +'¬henak¬ : ¬'+%trim(henak)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;
        endif;

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         open headf;
         else;

           error = 'Invalid userID';
         endif;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close headf;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
