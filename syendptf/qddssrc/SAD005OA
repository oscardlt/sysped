*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12053 * 01 YBC PTF12 FLERE BELØP-FELTER ER ENDRET FOR Å TA STØRRE VERDI
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Gå til forrige bilde (SADH-
     A                                      VARE)')
     A                                  1  2'SYSPED/SAD005OA'
     A                                      COLOR(BLU)
     A                                  1 29MSGCON(018 DIV1725 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Avdeling'
     A            SIAVD          4Y 0O  3 11TEXT('AVDELING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 19'Tolldeklarasjonsnr.'
     A            SITDN          7Y 0O  3 39TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 48'Tarifør'
     A            SISG           3A  O  3 56TEXT('Signatur')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(016 DIV1917 *LIBL/SYMSGF)
     A            WSDT           6S 0O  4 19TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(010 DIV1728 *LIBL/SYMSGF)
     A            SIKNS          8Y 0O  6 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SINAS         30A  O  6 23TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2MSGCON(010 DIV1733 *LIBL/SYMSGF)
     A            SIKNK          8Y 0O  7 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SINAK         30A  O  7 23TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  6MSGCON(006 DIV1734 *LIBL/SYMSGF)
     A            SIRG          11A  O  8 13TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10  2MSGCON(008 DIV1729 *LIBL/SYMSGF)
     A            SINTK          7Y 0O 10 13TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 10 23MSGCON(012 DIV1732 *LIBL/SYMSGF)
     A            SIVKB          9Y 0O 10 36TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20'Valutakode Beløp......... +/-'
     A                                 13  2MSGCON(016 DIV1735 *LIBL/SYMSGF)
     A            SIVAL1         3A  O 13 20TEXT('Valutakode Frakt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SIBEL1    R        O 13 33REFFLD(SADHR/SIBEL1 SYSPEDF/SADH)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14  5'Andre kostnad'
     A            SIVAL2         3A  O 14 20TEXT('Val.kode A. kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SIBEL2        11Y 2O 14 33TEXT('Beløp A. kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SIFTG2         1A  O 14 47TEXT('Fortegn +/- a.kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  2'22. Fakturasum'
     A            SIVAL3         3A  O 15 20TEXT('Val.kode Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SIBEL3        13Y 2O 15 31TEXT('Beløp Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 17  2MSGCON(010 DIV1751 *LIBL/SYMSGF)
     A            SIGN          15A  O 17 14TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2'Totall toll'
S01  A*           SIBEL5    R        O 19 14REFFLD(SADHRCMP/SIBEL5 SYSPEDO/SADH-
S01  A*                                     CMP)
N01  A            SIBEL5        11Y 0O 19 14
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 19 26'Totall moms'
S01  A*           SIBEL6    R        O 19 38REFFLD(SADHRCMP/SIBEL6 SYSPEDO/SADH-
S01  A*                                     CMP)
N01  A            SIBEL6        11Y 0O 19 36
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 19 51'Totall avgift'
     A            SIBEL7    R        O 19 65REFFLD(SADHRCMP/SIBEL7 SYSPEDO/SADH-
     A                                      CMP)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 21  2'Godkjenelsesdato'
     A            SIDTG         10A  O 21 20TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 33'Løpenr.'
     A            SITLL         10A  O 21 41TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 55'Eksp.sted'
     A            SITLE          6A  O 21 65TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23  2'F1=Bytte avdelinger,  F3=Avslutt, -
     A                                       F12=Forrige'
     A                                      COLOR(BLU)
