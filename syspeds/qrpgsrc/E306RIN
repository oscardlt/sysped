********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
10172 * 02 JOV PTF10 KAN SENDE OGS≈ INNHENTINGSDUPP
********************************************************************
      *****************************************************************
      * SEND GODSLISTE SOM IFTMIN I STEDET FOR IFCSUM                 *
      *****************************************************************
     FHEAD14  IF  E           K        DISK
     FDOKUF   UF  E           K        DISK                      A
     FECMSG   IF  E           K        DISK
     FFIRM    IF  E                    DISK
     FTRACKF  O   E                    DISK                      A
     I            DS
     I                                        1  35 WU0068
     I                                        1   40WUAVD
     I                                        5  120WUPRO
     I                                       13  200WUKNA
     I            DS
     I                                        1  20 HXSNDN
     I                                        1   3 HXSEAN
     I                                        1  10 HXSN10
     I                                        4  20 HXSN17
      * FINNER USERNAME
      *"""""""""""""""""""""""""""
     I           SDS
     I                                      244 253 ZJOB
     I                                      254 263 ZUSER
     I            DS
     I                                        1  140XULTID
     I                                        1   60XULTM
     I                                        7   80XULDD
     I                                        9  100XULMM
     I                                       11  140XUL≈≈
     I            DS
     I                                        1   40UL≈≈
     I                                        5   60ULMM
     I                                        7   80ULDD
     I                                        1   80ULDT
     I            DS
     I                                        1  140WTIME
     I                                        1   60WTID
     I                                        7   80WDD
     I                                        9  100WMM
     I                                       11  140W≈≈
     I            DS
     I                                        1   80WDATO
     I                                        1   40W≈
     I                                        5   60WM
     I                                        7   80WD
      * Track/Trace-tekst
     I              'Sendt til agent MEN- C         TTXED3
     I              ' SOM IFTMIN(via GL).'
     I              'Sendt Transport¯r  - C         TTTED3
     I              ' SOM IFTMIN(via GL).'
N01  I              'Sent to agent but a- C         TTXEDE
N01  I              's IFTMIN(via GL).'
N01  I              'Sent transporter   - C         TTTEDE
N01  I              ' as IFTMIN(via GL).'
      */////////////
      * MAIN LOGIC /
      */////////////
     C                     EXSR SRINIT
      *
     C           *IN41     CASEQ'1'       HOVED
     C                     END
     C                     MOVE '1'       *INLR
      *
      *
      *
      *////////////////////////////////////////////////////////////
     C           HOVED     BEGSR
      *////////////////////////////////////////////////////////////
     C* FOR ALLE OPPD P≈ TUREN
     C           WBRUDD    IFNE 'T'
     C           HEADKE    SETLLHEAD14
     C           HEADKE    READEHEAD14                   35
     C                     ELSE
     C           WUPRO     SETLLHEAD14
     C           WUPRO     READEHEAD14                   35
     C                     END
     C           *IN35     DOWEQ'0'                                8 0
     C                     MOVE HEAVD     DFAVD
     C                     MOVE HEOPD     DFOPD                                    FM NETTE.
     C                     Z-ADD1         DFFBNR                      51
     C           DOKUFK    CHAINDOKUF                33
      * MIDLERTIDIG FOR ≈ RETTE(V/OMKJÿRING) TIDLIGERE MISS
     C           *IN33     IFEQ '0'                                                FM VENLO.
     C           DFNAVM    IFEQ *BLANKS
     C                     DELETDOKUFR
     C                     SETON                     33
     C                     ELSE
     C                     UPDATDOKUFR
     C                     END                                    /
     C                     END                                    /
     C           *IN33     IFEQ '1'                               /
     C                     EXSR SKAPFB
     C                     END
     C                     MOVE WU0068    W0068  35
     C                     CALL 'SYFA12TR'
     C                     PARM           FIFIRM
     C                     PARM           DFAVD
     C                     PARM           DFOPD
     C                     PARM           DFFBNR
     C                     PARM           ECIIDI             AVS    35
     C                     PARM           ECIIDC             MOTTAK 35
     C                     PARM           W0068              AVD/TUR/AG
      * Skriv til TRACK/Trace fil??
     C           FIURBL    IFEQ 'J'
     C                     EXSR TTSR
     C                     END
      *
     C           WBRUDD    IFNE 'T'
     C           HEADKE    READEHEAD14                   35
     C                     ELSE
     C           WUPRO     READEHEAD14                   35
     C                     END
     C                     END
      *
     C                     ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C           SKAPFB    BEGSR
      *////////////////////////////////////////////////////////////
      * FRAKTBREV 1 FINNES IKKE - SKAP DET.
     C                     CLEARDOKUFR
      *
     C           HXSEAN    IFEQ '401'
N02  C           HXSEAN    OREQ '402'
     C                     MOVELHXSN17    DF1004
     C                     ELSE
     C                     MOVELHXSN10    DF1004
     C                     END
     C                     MOVE 'P'       DFST
     C           HEFR      IFEQ 'S'
     C           HEFR      OREQ 'M'
     C                     MOVELHEFR      DFBELA
     C                     END
     C                     MOVE HEKNS     DFKNSS
     C                     MOVE HENAS     DFNAVS
     C                     MOVE HEADS1    DFAD1S
     C                     MOVE *BLANKS   DFAD2S
     C                     MOVELHEADS3    TMPPUL  4
     C                     TESTN          TMPPUL     77
     C           *IN77     IFEQ '1'
     C                     MOVE TMPPUL    DFPNLS            LASTESTED
     C                     MOVE HEADS3    TMPAD3 25
     C                     MOVELTMPAD3    DFAD3S
     C                     ELSE
     C                     MOVELHEADS3    DFAD3S
     C                     END
     C                     MOVE HENT      DFNTLA
     C                     MOVE HENAK     DFNAVL
     C                     MOVE HEADK1    DFAD1L
     C                     MOVE *BLANKS   DFAD2L
     C                     MOVELHEADK3    TMPPUL  4
     C                     TESTN          TMPPUL     77
     C           *IN77     IFEQ '1'
     C                     MOVE TMPPUL    DFPOUL            UTL.POSTNR
     C                     MOVE HEADK3    TMPAD3 25
     C                     MOVELTMPAD3    DFADUL            UTL.STED
     C                     ELSE
     C                     MOVELHEADK3    DFADUL
     C                     END
     C                     MOVE HEKNK     DFKNSM
     C                     MOVE HENAK     DFNAVM
     C                     MOVE HEADK1    DFAD1M
     C                     MOVE HEADK3    DFAD3M
     C                     MOVELHEGM1     DFGM
     C                     MOVE HENT      DFNT
     C                     MOVE HEVS1     DFVS
     C                     MOVE HEVKT     DFVKT
     C                     MOVE HELM      DFLM
     C                     MOVE HEM3      DFM3
     C                     MOVELHEGM2     DFGM2
     C                     MOVELHEVS2     DFVS2
     C                     MOVE HEFBV     DFVKTF
     C                     MOVE HESG      DFSG
     C                     MOVE 'F'       DFRI
     C                     MOVE HEAVD     DFAVD
     C                     MOVE HEOPD     DFOPD
     C                     Z-ADD1         DFFBNR
     C*
     C                     WRITEDOKUFR
     C                     ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C           SRINIT    BEGSR
      *////////////////////////////////////////////////////////////
      * INPUT PARAMETERS
     C           *ENTRY    PLIST
     C                     PARM           WAVD    4
     C                     PARM           WPRO    8
     C                     PARM           WKNA    8
     C                     PARM           WBRUDD  1
      *
     C                     MOVE WAVD      WUAVD   40
     C                     MOVE WPRO      WUPRO   80
     C                     MOVE WKNA      WUKNA   80
      * HENTE DAGENS DATO OG TID
     C                     TIME           WTIME
     C                     Z-ADDW≈≈       W≈
     C                     Z-ADDWDD       WD
     C                     Z-ADDWMM       WM
      * INIT AV ARBEIDSFELT
     C           *LIKE     DEFN ECSR      WCSR
     C           *LIKE     DEFN EC0065    WC0065
     C                     MOVE 'S'       WCSR
     C                     MOVEL'IFTMIN'  WC0065
     C                     READ FIRM                     51
      *
      * DEF AV KEYS
     C           HEADKE    KLIST
     C                     KFLD           WUPRO
     C                     KFLD           WUKNA
     C           DOKUFK    KLIST
     C                     KFLD           DFRI
     C                     KFLD           DFAVD
     C                     KFLD           DFOPD
     C                     KFLD           DFFBNR
     C                     MOVE 'F'       DFRI
     C                     Z-ADD1         DFFBNR
     C           ECMKEY    KLIST
     C                     KFLD           FIFIRM
     C                     KFLD           WUKNA
     C                     KFLD           WCSR
     C                     KFLD           WC0065
      *
      * VED IFTMIN VIA GODSLISTE M≈ ALLTID KUNDENR AGENT (TRANSP VED T)
      * V∆RE GITT. HVIS IKKE FINNER IKKE SYSTEMET RETT ID FRA / TIL
     C           WUKNA     CABEQ0         SRA9
      *_________________________________
      * Send IFTMIN ? and wich release ?
      *"""""""""""""""""""""""""""""""""
     C           ECMKEY    CHAINECMSG                51
      * 0 I AGENT VED TRANSP.GODSLISTE
     C           WBRUDD    IFEQ 'T'
     C                     MOVE *ZEROS    WUKNA
     C                     END
     C           *IN51     CABEQ'1'       SRA9
     C                     MOVE '1'       *IN41
     C           SRA9      ENDSR
      *
     C***********************************************
     C           TTSR      BEGSR
     C***********************************************
      * EDI- SENDT
     C                     MOVE HEAVD     TTAVD
     C                     MOVE HEOPD     TTOPD
     C                     MOVE *ZEROS    TTFBNR
     C                     TIME           XULTID
     C                     MOVE XULDD     ULDD
     C                     MOVE XULMM     ULMM
     C                     MOVE XUL≈≈     UL≈≈
     C                     MOVE 'ED3'     TTACTI
     C                     MOVE ULDT      TTDATE
     C                     MOVE XULTM     TTTIME
     C                     MOVE TTDATE    TTDATL
     C                     MOVE TTTIME    TTTIML
     C                     MOVE ZUSER     TTUSER
s01  C*                    MOVELTTXED3    TTTEXT
n01  C                     MOVELTTXEDE    TTTEXT
n01  C                     MOVELTTXED3    TTTEXL
     C           WBRUDD    IFEQ 'T'
s01  C*                    MOVELTTTED3    TTTEXT
n01  C                     MOVELTTTEDE    TTTEXT
n01  C                     MOVELTTTED3    TTTEXL
     C                     END
     C                     WRITETRACKFR
     C                     ENDSR
