      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE PRODUKTKODER FLY          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FKODTFLP   UF A E           K DISK
     FSYEF55FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KOFMNV                 1     20
     D  KOSFN7                 1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2TMKO        YSFSI             1
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSF1        SETLL     KODTFLP
     C     START1        TAG
     C                   READ(N)   KODTFLP                                94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      KODTFLP                                94
     C  N94              UPDATE    KODTFLPR
     C  N94              MOVE      KOFMKO        YSFSI
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     17            POSNBR
     C                   MOVE      *BLANKS       WSTMKO
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSF3        SETLL     KODTFLP
     C  N94              READ      KODTFLP                                94
     C  N94              UPDATE    KODTFLPR
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSTMKO        COMP      *BLANKS                                90
     C   90              MOVE      'SPK0029'     MSGNR
     C  N90KODSFK        CHAIN     KODTFLP                            33
     C  N90
     CANN33              DO
     C                   UPDATE    KODTFLPR
     C                   SETON                                        90
     C                   MOVE      'SPK0024'     MSGNR
     C                   END
     C   90              Z-ADD     17            POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C  N30WSTMNV        COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SPK0030'     MSGNR
     C   89
     CANN30              Z-ADD     39            POSNBR
     C   89              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSFK        CHAIN     KODTFLP                            33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    KODTFLPR
     C   33              WRITE     KODTFLPR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      KOFMKO        WSTMKO
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSFK        CHAIN     KODTFLP                            33
     C  N33              DELETE    KODTFLPR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KOFMKO
     C                   MOVE      *BLANKS       KOFMK2
     C                   MOVE      *BLANKS       KOFMK3
     C                   MOVE      *BLANKS       KOFMK4
     C                   MOVE      *BLANKS       KOFMK5
     C                   MOVE      *BLANKS       KOFMK6
     C                   MOVE      *BLANKS       KOFMK7
     C                   MOVE      *BLANKS       KOFMNV
     C                   MOVEL     '*DELETE'     KOSFN7
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSTMKO
     C                   MOVE      *BLANKS       WSTMK2
     C                   MOVE      *BLANKS       WSTMK3
     C                   MOVE      *BLANKS       WSTMK4
     C                   MOVE      *BLANKS       WSTMK5
     C                   MOVE      *BLANKS       WSTMK6
     C                   MOVE      *BLANKS       WSTMK7
     C                   MOVE      *BLANKS       WSTMNV
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      KOFMKO        WSTMKO
     C                   MOVE      KOFMK2        WSTMK2
     C                   MOVE      KOFMK3        WSTMK3
     C                   MOVE      KOFMK4        WSTMK4
     C                   MOVE      KOFMK5        WSTMK5
     C                   MOVE      KOFMK6        WSTMK6
     C                   MOVE      KOFMK7        WSTMK7
     C                   MOVE      KOFMNV        WSTMNV
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     23            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      YSFUNI        KOFMUN
     C                   MOVE      WSTMKO        KOFMKO
     C                   END
     C*
     C                   MOVE      WSTMK2        KOFMK2
     C                   MOVE      WSTMK3        KOFMK3
     C                   MOVE      WSTMK4        KOFMK4
     C                   MOVE      WSTMK5        KOFMK5
     C                   MOVE      WSTMK6        KOFMK6
     C                   MOVE      WSTMK7        KOFMK7
     C                   MOVE      WSTMNV        KOFMNV
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODSFK        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    WSTMKO
     C     KODSF1        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    W2TMKO
     C     KODSF2        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    KOFMKO
     C     KODSF3        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    YSFSI
     C*
     C                   MOVE      'FLPR'        YSFUNI            4
     C*
     C                   ENDSR
     C*
