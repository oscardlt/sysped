     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(LLHEAD LLHEADR)
     A                                      PRINT
      *
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'AVBRYT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL031R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1413 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(13 YBC1415 SYMSGF)
     A            U1PNR          8Y 0O  2 16EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            U1PNV         30A  O  2 26DSPATR(HI)
     A                                  2 73TIME
     A                                  4  2MSGCON(78 YBC1427 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC1428 SYMSGF)
     A                                      COLOR(WHT)
     A            WSBNR         17A  B  6  5
     A            WSTDT          8Y 0B  6 23EDTWRD('    .  .  ')
     A            WSPAK     R        B  6 34REFFLD(LLHEADR/LHPAK LLHEAD)
     A            WSORDR    R        B  6 52REFFLD(LLHEADR/LHORDR LLHEAD)
     A            WSALN     R        B  6 70REFFLD(LLHEADR/LHALN LLHEAD)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SUBFIL                    SFL
     A            LHMN           7S 0H
     A            WSNR           1A  B  7  2VALUES(' ' '5' '7')
     A            LHBNR     R        O  7  5
     A  61                                  COLOR(WHT)
     A            LHTDT          8  0O  7 23EDTWRD('    .  .  ')
     A            LHPAK     R        O  7 34
     A  62                                  COLOR(WHT)
     A            LHORDR    R        O  7 52
     A  63                                  COLOR(WHT)
     A            LHALN     R        O  7 70EDTCDE(4)
     A  65                                  COLOR(WHT)
     A            LHAVD     R        O  7 76
     A  66                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            LHBNA     R        H
     A            XHBNA         16A  O  8  2DSPATR(UL)
     A            LHST1     R        O  8 19DSPATR(UL)
     A            LHANT0    R        O  8 21EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHFLN     R        O  8 29DSPATR(UL)
     A  64                                  COLOR(WHT)
     A            LHWVEK    R        O  8 55EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHWKUB    R        O  8 64EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHOPD     R        O  8 73DSPATR(UL)
     A  66                                  COLOR(WHT)
     A                                      EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'Forny')
     A                                      CA12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL031R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1413 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(13 YBC1415 SYMSGF)
     A            U1PNR          8Y 0O  2 16EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            U1PNV         30A  O  2 26DSPATR(HI)
     A            ZRECNO         4S 0O  2 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 73TIME
     A                                  3  2MSGCON(50 YBC1429 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1427 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC1428 SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            WSBNR         17A  B  6  5
     A            WSTDT          8Y 0B  6 23EDTWRD('    .  .  ')
     A            WSPAK     R        B  6 34REFFLD(LLHEADR/LHPAK LLHEAD)
     A            WSORDR    R        B  6 52REFFLD(LLHEADR/LHORDR LLHEAD)
     A            WSALN     R        B  6 70REFFLD(LLHEADR/LHALN LLHEAD)
     A                                      EDTCDE(4)
      *
     A          R BOTREC
     A                                      OVERLAY
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
      *
     A          R VISREC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA12(12 'Annuller')
     A N61                                  CA18(18 'Nortis')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZMSGNO         7A  P
     A                                  1  2'SYSPED/LL031R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1413 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(13 YBC1415 SYMSGF)
     A            U1PNR          8Y 0O  2 16EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            U1PNV         30A  O  2 26DSPATR(HI)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(16 YBC1430 SYMSGF)
     A            LHSTA     R        O  3 19COLOR(WHT)
     A                                  4  2MSGCON(16 YBC1431 SYMSGF)
     A            LHMN      R        O  4 19DSPATR(UL)
     A  61                                  COLOR(WHT)
     A N61                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(16 YBC1432 SYMSGF)
     A            LHBNR     R        O  5 19
     A            KUKUN2         8Y 0O  5 37EDTCDE(4)
     A            KUVADR         5Y 0O  5 46EDTCDE(4)
     A            LHBNA     R        O  6 19
     A            LHBA1     R        O  6 51
     A            LHBA2     R        O  7 19
     A            LHBPN     R        O  7 51EDTCDE(4)
     A            LHBA3     R        O  7 57
     A                                  8 19MSGCON(16 YBC1433 SYMSGF)
     A            LHBLK     R        O  8 36
     A                                  8 42MSGCON(17 YBC1434 SYMSGF)
     A            LHBPU     R        O  8 60
     A                                  9  2MSGCON(16 YBC1435 SYMSGF)
     A            LHTDT     R        O  9 19EDTWRD('    .  .  ')
     A                                  9 41MSGCON(18 YBC1436 SYMSGF)
     A            LHTN      R        O  9 60EDTCDE(4)
     A                                  9 69'Prod.'
     A            LHPRKD    R        O  9 75
     A                                 10  2MSGCON(16 YBC1437 SYMSGF)
     A            LHPAK     R        O 10 19
     A                                 10 41'Plassert:'
     A            LHPSD     R        O 10 51
     A                                 11  2MSGCON(16 YBC1438 SYMSGF)
     A            LHORDR    R        O 11 19
     A                                 11 41'Påbegynt kl.'
     A            LHPTDF    R        O 11 54EDTWRD('  .  ')
     A                                 11 62'Ferdig kl.'
     A            LHPTDT    R        O 11 73EDTWRD('  .  ')
     A                                 12  2MSGCON(16 YBC1439 SYMSGF)
     A            LHFLN     R        O 12 19
     A                                 12 41'Signatur:'
     A            LHPSG     R        O 12 51
     A                                 13  2MSGCON(16 YBC1440 SYMSGF)
     A            LHALN     R        O 13 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(16 YBC1441 SYMSGF)
     A            LHAVD     R        O 14 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 24'/'
     A                                      COLOR(WHT)
     A            LHOPD     R        O 14 26EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 34'/'
     A                                      COLOR(WHT)
     A            LHFBN     R        O 14 36EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 42MSGCON(11 YBC1442 SYMSGF)
     A            XX1004        21   O 14 54COLOR(WHT)
     A                                 15  2MSGCON(16 YBC1443 SYMSGF)
     A            LHANT0    R        O 15 19EDTCDE(4)
     A                                 15 30MSGCON(14 YBC1444 SYMSGF)
     A            LHANTL    R        O 15 45EDTCDE(4)
     A                                 15 53'STK'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 30MSGCON(16 YBC1446 SYMSGF)
     A            LHANT1    R        O 16 47EDTCDE(4)
     A            LHAEH1    R        O 16 53DSPATR(HI)
     A                                 17 30MSGCON(16 YBC1447 SYMSGF)
     A            LHANT2    R        O 17 47EDTCDE(4)
     A            LHAEH2    R        O 17 53DSPATR(HI)
     A                                 18 30MSGCON(16 YBC1448 SYMSGF)
     A            LHANT3    R        O 18 47EDTCDE(4)
     A            LHAEH3    R        O 18 53DSPATR(HI)
     A                                 19  2MSGCON(16 YBC1449 SYMSGF)
     A            LHVEK     R        O 19 19EDTCDE(M)
     A                                 19 42MSGCON(16 YBC1450 SYMSGF)
     A            LHWVEK    R        O 19 59EDTCDE(M)
     A                                 20  2MSGCON(16 YBC1451 SYMSGF)
     A            LHKUB     R        O 20 19EDTCDE(M)
     A                                 20 42MSGCON(16 YBC1452 SYMSGF)
     A            LHWKUB    R        O 20 59EDTCDE(M)
     A                                 21  2MSGCON(16 YBC1453 SYMSGF)
     A            LHST1     R        O 21 19
     A                                 21 42MSGCON(16 YBC1454 SYMSGF)
     A            LHTLIN    R        O 21 59EDTCDE(4)
     A                                 22  2MSGCON(16 YBC1455 SYMSGF)
     A            LHKLL     R        O 22 19EDTCDE(4)
     A            LHMLP     R        O 22 26EDTCDE(4)
     A            LHST2     R        O 22 32
     A                                 22 42MSGCON(16 YBC1456 SYMSGF)
     A            LHBPAL    R        O 22 59EDTCDE(4)
     A            LHAPAL    R        O 22 66EDTCDE(4)
     A                                 23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A N61                             23 22'F18=Notis'
     A                                      COLOR(BLU)
     A  30        ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
