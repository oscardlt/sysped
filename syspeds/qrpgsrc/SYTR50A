********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 Fått med stedskoden  ..
********************************************************************
      *
      ************************************************************
      * GÅR IGJENNOM ALLE TURER MED STATUS " " /  "P" / "A"      *
      *       DERSOM AVGANG PÅ GJELDENDE DAG ELLER FØR           *
      *       OG ANKOMST GJELDENDE DAG ELLER SENERE --> JOURNAL  *
      * GÅR IGJENNOM ALLE TURER MED STATUS "B" (GÅTT) -          *
      *       ALLE MED ANKOMST GJELDENDE DAG EL. SENERE ->JOURNAL*
      ************************************************************
      *
     FTURER     IF   E           K DISK
     FTURER1    IF   E           K DISK
     F                                     RENAME(TURERR:TURE1R)
     FTURER2    IF   E           K DISK
     F                                     RENAME(TURERR:TURE2R)
     FBILK      IF   E           K DISK
     FSTED2     IF   E           K DISK
     FSTED2D    IF   E           K DISK
     F                                     RENAME(STED2R:STEDDR)
     FFIRM      IF   E           K DISK
     FUNITL01   IF   E           K DISK
     FDAYJO     UF A E           K DISK
     FDAYJO1    IF   E           K DISK
     F                                     RENAME(DAYJOR:DAYJ1R)
     D                 DS
     D  UDTÅ                   1      4  0
     D  UDTM                   5      6  0
     D  UDTD                   7      8  0
     D  UDT                    1      8  0
     C*
     C                   EXSR      INIT
     C                   EXSR      PÅTUR
     C* LEDIGLISTE SKRIVES IKKE NÅR HISTORISK LISTE SKRIVES
     C     WSDT8         IFGE      UDT
     C                   EXSR      LEDIG
     C                   EXSR      HENVEK
     C                   END
     C                   EXSR      DUPVEK
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*
     C*
     C***********************************************
     C     PÅTUR         BEGSR
     C***********************************************
     C* TURER MED STATUS ' ' (ÅPEN)
     C                   SETOFF                                           94
     C                   MOVE      ' '           YST
     C     TURE1K        SETLL     TURER1
     C     YST           READE     TURER1                                 94
     C*
     C     *IN94         DOWNE     '1'
     C     WSDT8         IFGE      TUDT
     C     WSDT8         ANDLE     TUDTT
     C                   EXSR      ENTUR
     C                   END
     C     YST           READE     TURER1                                 94
     C  N94              END
     C*
     C*
     C* TURER MED STATUS 'P' (UNDER PLANL.)
     C                   SETOFF                                           94
     C                   MOVE      'P'           YST
     C     TURE1K        SETLL     TURER1
     C     YST           READE     TURER1                                 94
     C*
     C     *IN94         DOWNE     '1'
     C     WSDT8         IFGE      TUDT
     C     WSDT8         ANDLE     TUDTT
     C                   EXSR      ENTUR
     C                   END
     C     YST           READE     TURER1                                 94
     C  N94              END
     C*
     C*
     C* TURER MED STATUS 'A'(FERD. DISP.)
     C                   SETOFF                                           94
     C                   MOVE      'A'           YST
     C     TURE1K        SETLL     TURER1
     C     YST           READE     TURER1                                 94
     C*
     C     *IN94         DOWNE     '1'
     C     WSDT8         IFGE      TUDT
     C     WSDT8         ANDLE     TUDTT
     C                   EXSR      ENTUR
     C                   END
     C     YST           READE     TURER1                                 94
     C  N94              END
     C*
     C*
     C* TURER MED STATUS 'B' (GÅTT) O
     C                   SETOFF                                           94
     C                   MOVE      'B'           YST
     C     TURE1K        SETLL     TURER1
     C     YST           READE     TURER1                                 94
     C*
     C     *IN94         DOWNE     '1'
     C     WSDT8         IFLE      TUDTT
     C                   EXSR      ENTUR
     C                   END
     C     YST           READE     TURER1                                 94
     C  N94              END
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     ENTUR         BEGSR
     C***********************************************
     C* LEGG UT ALLE I UTGANGSPUNKTET - FEIL AVD/BILK FJERNES SENERE
     C                   MOVE      WSDT8         DADATO
     C* INFO FRA TURER TIL DAG-JOURNALPOST :
     C                   MOVE      TUBILK        DABILK
     C                   MOVE      ' '           DADIST
     C                   MOVE      TUBILN        DABILN
     C                   MOVE      TUHENG        DAHENG
     C* PÅ EGNE BILER SKRIV SJÅFØRS NAVN - FREMMEDE TRANSPORTØRS NAVN
     C     TUBILK        IFEQ      '001'
     C                   MOVEL     TUSJN1        DASJN1
     C                   ELSE
     C                   MOVEL     TUNAT         DASJN1
     C                   END
     C                   MOVE      TUAVD         DAAVD
     C                   MOVE      TUPRO         DAPRO
     C                   MOVE      TURUND        DARUND
     C* CHAIN OPP BILKODE - HENT BILKODETEKST
     C     TUBILK        IFNE      GMBILK
     C     TUBILK        CHAIN     BILK                               33
     C  N33              MOVE      TUBILK        GMBILK
     C  N33              MOVE      BKTXT         GMTXT
     C                   END
     C                   MOVE      GMTXT         DABTXT
     C* HENT ALLTID STEDSNAVN FRA REGISTER UTFRA STEDSKODE
     C                   MOVE      *BLANKS       DASDF
N01  C                   MOVE      *BLANKS       DASDFK
N01  C                   MOVE      *BLANKS       DASDFL
     C                   MOVE      *BLANKS       DASDT
N01  C                   MOVE      *BLANKS       DASDTK
N01  C                   MOVE      *BLANKS       DASDTL
     C                   MOVEL     TUSONF        WSLKFR            2
     C     WSLKFR        IFEQ      *BLANKS
     C     TUSTEF        CHAIN     STED2                              33
     C                   ELSE
     C     ST2DFK        CHAIN     STED2D                             33
     C                   END
     C  N33              MOVEL     ST2NVN        DASDF
N01  C  N33              MOVE      ST2KOD        DASDFK
N01  C  N33              MOVE      ST2LK         DASDFL
     C                   MOVEL     TUSONT        WSLKTO            2
     C     WSLKTO        IFEQ      *BLANKS
     C     TUSTET        CHAIN     STED2                              33
     C                   ELSE
     C     ST2DTK        CHAIN     STED2D                             33
     C                   END
     C  N33              MOVEL     ST2NVN        DASDT
N01  C  N33              MOVE      ST2KOD        DASDTK
N01  C  N33              MOVE      ST2LK         DASDTL
     C                   MOVE      TUDT          DADTF
     C                   MOVE      TUDTT         DADTT
     C*
     C                   WRITE     DAYJOR
     C*
     C     ENDTUR        ENDSR
     C***********************************************
     C     LEDIG         BEGSR
     C***********************************************
     C*  97 ON - DET ER VALGT BEGRENSNINGER I AVDELING SOM SKAL MED
     C*  98 ON - DET ER VALGT BEGRENSNINGER I BILKODER SOM SKAL MED
     C* ALLE BILER SOM IKKE ER MED PÅ LISTA
     C*
     C                   SETOFF                                           94
     C                   MOVE      *BLANKS       UNBILN
     C     UNBILN        SETLL     UNITL01
     C                   READ      UNITL01                                94
     C*
     C     *IN94         DOWNE     '1'                                          B1
     C*EKSKLUDER P.G.A AVDELING/BILKODE VALGT?
     C*UNIT MED NULL I AVDELING SKAL ALDRI VÆRE MED
     C     UNAVD         IFEQ      *ZEROS
     C                   SETON                                        99
     C                   ELSE
     C                   MOVE      UNTRME        XXBILK            3
     C                   MOVE      UNAVD         XXAVD             4 0
     C                   EXSR      EXCLUD
     C                   END
     C* TRANSPORTMÅTE 99 - DUMMYVERDI FOR..UNITEN ER IKKE AKTIV
     C  N99UNTRMA        IFNE      99                                            B2
     C*EKSKLUDER P.G.A FINNES AKTIV
     C*(DAYJO INNEH. ALLE AKTIVE UAVH. AV AVD/BILK (SKRELLES SENERE))
     C                   MOVE      WSDT8         DADATO
     C                   MOVE      UNBILN        DABILN
     C                   MOVE      UNTILH        DAHENG
     C                   MOVE      ' '           DADIST
     C                   MOVE      UNTRME        DABILK
     C     DAYJOK        CHAIN     DAYJO                              95
     C  N99
     CAN 95              DO                                                       B3
     C* UNITEN ER LEDIG OG SKAL PÅ LISTA......
     C* VIA"SISTE TUR" I UNITREG. FINN UT OM DET ER SPESIELLE MERKNADER
     C* SOM SKAL GIS (GIS I TILSTEDSFELTET):
     C*   * NÅR DEN ER LEDIG, MEN EN FRAMTIDIG TUR ER BOOKET
     C*         VIA SISTE TUR FØR LISTEDATO FINNES HVOR DEN ER LEDIG...
     C*   * HVIS OPERATØR HAR GLEMT Å LUKKE FORIGE AVSLUTTEDE TUR ..
     C*   * HVIS DET ER ÅPNET EN NY "UDEFINERT" TUR (DATO=0)
     C*         VIA SISTE TUR FØR LISTEDATO FINNES HVOR DEN ER LEDIG...
     C                   EXSR      MOVB1
     C     UNSPRO        IFNE      *ZEROS                                          B4
     C     TURERK        CHAIN     TURER                              33
     C  N33TUST          IFEQ      ' '                                              B5
     C     TUST          OREQ      'P'
     C* SISTE TUR ER ÅPEN **(TILDATO =0/ < LISTEDATO/ > LISTEDATO )
     C     TUDTT         IFEQ      *ZEROS                                            B6
     C* KAN VÆRE AT EN ER I FERD MED Å OPPRETTE EN NY. (TILDATO=0)..
     C* HENT TILSTED/DATO FRA NEST SISTE
     C                   MOVE      XXÅPEN        DASDT
     C                   EXSR      NESTSI
     C                   ELSE
     C     TUDTT         IFLT      DADATO                                             B7
     C* KAN VÆRE AT OPERATØR HAR GLEMT Å SETTE "A" (TILDATO < DADATO..)
     C                   MOVE      XXLUKK        DASDT
     C                   END                                                          E7
     C                   END                                                         E6
     C                   END                                                        E5
     C     TUST          IFEQ      'D'
     C                   EXSR      NESTSI
     C                   END
     C* HVIS BÅDE FRA/TIL DATO STØRRE ENN LISTEDATO ER DET EN FRAMTIDIG
     C* (SANSYNLIGVIS MED STATUS ÅPEN)
     C  N33TUDT          IFGT      DADATO                                           B5
     C     TUDTT         ANDGT     DADATO
     C                   MOVE      TUDT          XXFRAM
     C                   MOVE      XXFRAM        DASDT
     C                   EXSR      NESTSI
     C                   END                                                        E5
     C* DERSOM DEN HADDE HENGER PÅ VED SISTE TUR SKAL DETTE VISES
     C  N33DABILN        IFEQ      TUBILN                                           B5
     C                   MOVE      TUHENG        DAHENG
     C                   END                                                        E5
     C* DEN ER LEDIG FRA DET STED/DEN TID DEN PÅ SISTE TUR KOM TIL
     C  N33              MOVE      TUDTT         DADTF
     C                   MOVEL     TUSONT        WSLKTO            2
     C     WSLKTO        IFEQ      *BLANKS
     C  N33TUSTET        CHAIN     STED2                              33
     C                   ELSE
     C  N33ST2DTK        CHAIN     STED2D                             33
     C                   END
     C  N33              MOVEL     ST2NVN        DASDF
N01  C  N33              MOVE      ST2KOD        DASDFK
N01  C  N33              MOVE      ST2LK         DASDFL
     C* HVILKEN SJÅFØR (EGNE)/TRANSPORTØR(ANDRE) SOM SIST HADDE UNITEN
     C  N33TUBILK        IFEQ      '001'                                            B5
     C                   MOVEL     TUSJN1        DASJN1
     C                   ELSE
     C                   MOVEL     TUNAT         DASJN1
     C                   END                                                        E5
     C                   END                                                       E4
     C                   WRITE     DAYJOR
     C                   END                                                      E3
     C                   END                                                     E2
     C                   READ      UNITL01                                94
     C  N94              END                                                    E1
     C*
     C                   ENDSR
     C***********************************************
     C     NESTSI        BEGSR
     C***********************************************
     C     SISKEY        KLIST
     C                   KFLD                    DABILN
     C                   KFLD                    DADATO
     C*
     C* FINN SIST UTFØRTE TUR FØR LISTEDATO........
     C* (ANTAR AT DET IKKE FINNES NOE TUR PÅ SELVE LISTEDATOEN...
     C*  .. HVIS SÅ FANTES VILLE BILEN IKKE VÆRE I LEDIG..KATEGORIEN)
     C     SISKEY        SETLL     TURER2
     C     NES           TAG
     C     DABILN        READPE    TURER2                                 33
     C   33              MOVE      *BLANKS       TUST
     C     TUST          CABEQ     'D'           NES
     C   33              MOVE      *BLANKS       TUBILN
     C   33              MOVE      *BLANKS       TUSTET
     C   33              MOVE      *BLANKS       TUBILK
     C   33              MOVE      *ZEROS        TUDTT
     C   33              MOVE      *OFF          *IN33
     C  N33              MOVE      TUAVD         DAAVD
     C  N33              MOVE      TUPRO         DAPRO
     C                   ENDSR
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C* BLAN ALLE DATA ANNET ENN BILNR /DATO/BILKODE/HEGER V/UDISP
     C* AVD/TURNR VIL FOR EN LEDIG BIL VISE SIST UTFØRTE TUR......
     C                   MOVE      'U'           DADIST
     C                   MOVE      *BLANKS       DASJN1
     C                   MOVE      *ZEROS        DADTF
     C                   MOVE      *BLANKS       DASDF
N01  C                   MOVE      *BLANKS       DASDFK
N01  C                   MOVE      *BLANKS       DASDFL
     C                   MOVE      *ZEROS        DADTT
     C                   MOVE      *BLANKS       DASDT
N01  C                   MOVE      *BLANKS       DASDTK
N01  C                   MOVE      *BLANKS       DASDTL
     C                   MOVE      UNSAVD        DAAVD
     C                   MOVE      UNSPRO        DAPRO
     C                   MOVE      *BLANKS       DABTXT
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     EXCLUD        BEGSR
     C***********************************************
     C*
     C* SJEKK OM POSTEN EKSKUDERES PGA. KUN AVDELING  / KUN BILKODE....
     C*
     C                   SETOFF                                       99
     C* AVDELINGSBEGRENSNING ????
     C   97              DO
     C     WSAV01        IFNE      *ZEROS
     C     WSAV01        COMP      XXAVD                              9999
     C                   END
     C   99WSAV02        IFNE      *ZEROS
     C     WSAV02        COMP      XXAVD                              9999
     C                   END
     C   99WSAV03        IFNE      *ZEROS
     C     WSAV03        COMP      XXAVD                              9999
     C                   END
     C   99WSAV04        IFNE      *ZEROS
     C     WSAV04        COMP      XXAVD                              9999
     C                   END
     C   99WSAV05        IFNE      *ZEROS
     C     WSAV05        COMP      XXAVD                              9999
     C                   END
     C   99WSAV06        IFNE      *ZEROS
     C     WSAV06        COMP      XXAVD                              9999
     C                   END
     C   99WSAV07        IFNE      *ZEROS
     C     WSAV07        COMP      XXAVD                              9999
     C                   END
     C   99WSAV08        IFNE      *ZEROS
     C     WSAV08        COMP      XXAVD                              9999
     C                   END
     C   99WSAV09        IFNE      *ZEROS
     C     WSAV09        COMP      XXAVD                              9999
     C                   END
     C   99WSAV10        IFNE      *ZEROS
     C     WSAV10        COMP      XXAVD                              9999
     C                   END
     C                   END
     C  N99
     CAN 98              DO
     C     WSBK01        IFNE      *BLANKS
     C     WSBK01        COMP      XXBILK                             9999
     C                   END
     C   99WSBK02        IFNE      *BLANKS
     C     WSBK02        COMP      XXBILK                             9999
     C                   END
     C   99WSBK03        IFNE      *BLANKS
     C     WSBK03        COMP      XXBILK                             9999
     C                   END
     C   99WSBK04        IFNE      *BLANKS
     C     WSBK04        COMP      XXBILK                             9999
     C                   END
     C   99WSBK05        IFNE      *BLANKS
     C     WSBK05        COMP      XXBILK                             9999
     C                   END
     C                   END
     C***   99 ON ----- TURENS BILKODE ER IKKE EN AV DE VALGTE
     C                   ENDSR
     C*
     C***********************************************
     C     HENVEK        BEGSR
     C***********************************************
     C* OM EN LEDIG BIL HAR HENGER - SJEKK OM HENGEREN ER FRAKOBLET??
     C* (SISTE TUR FOR HENGER ULIK SISTE TUR FOR TREKKVOGN....
     C*  BETYR HENGEREN HAR VÆRT I AKTIVITET MENS BILEN UTE AV TRAFIKK)
     C                   MOVE      WSDT8         DADATO
     C                   MOVE      *BLANKS       DABILK
     C                   MOVE      ' '           DADIST
     C                   MOVE      *BLANKS       DABILN
     C                   SETOFF                                           94
     C     DAYJOK        SETLL     DAYJO
     C                   READ      DAYJO                                  94
     C     *IN94         DOWNE     '1'
     C     DAHENG        IFNE      *BLANKS
     C     DADIST        ANDEQ     'U'
     C     DABILN        CHAIN(N)  UNITL01                            95
     C  N95              MOVE      UNSPRO        XXPRO             8 0
     C     DAHENG        CHAIN(N)  UNITL01                            95
     C  N95UNSPRO        IFNE      XXPRO
     C                   MOVE      *BLANKS       DAHENG
     C                   UPDATE    DAYJOR
     C                   END
     C                   END
     C                   READ      DAYJO                                  94
     C  N94              END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DUPVEK        BEGSR
     C***********************************************
     C*  FJERN DUPLIKATER SOM SKYLDES AT LEDIG ENHET I UNITREGISTERET
     C*  OGSÅ KAN FINNES SOM HENGER PÅ EN ANNEN UNIT(LEDIG ELLER AKTIV)
     C                   MOVE      WSDT8         DADATO
     C                   MOVE      *BLANKS       DABILK
     C                   MOVE      ' '           DADIST
     C                   MOVE      *BLANKS       DABILN
     C                   SETOFF                                           94
     C     DAYJOK        SETLL     DAYJO
     C                   READ      DAYJO                                  94
     C     *IN94         DOWNE     '1'
     C* FINNES UDISPONERT ENHET OGSÅ SOM HENGER PÅ EN ANNEN ???
     C* (DAYJO1 ER INDEX SOM GÅR KUN PÅ HENGERNR.)
     C     DADIST        IFEQ      'U'
     C     DAHENG        IFEQ      *BLANKS
     C     DABILN        CHAIN     DAYJO1                             95
     C  N95              DELETE    DAYJOR
     C                   END
     C* FJERN AKTIVE ENHETER SOM IKKE TILHØRER RETT AVD/BILK
     C                   ELSE
     C                   MOVE      DABILK        XXBILK            3
     C                   MOVE      DAAVD         XXAVD             4 0
     C                   EXSR      EXCLUD
     C   99              DELETE    DAYJOR
     C                   END
     C                   READ      DAYJO                                  94
     C  N94              END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM      WSDT          WSDT              6 0
     C                   PARM      WSAV01        WSAV01            4 0
     C                   PARM      WSAV02        WSAV02            4 0
     C                   PARM      WSAV03        WSAV03            4 0
     C                   PARM      WSAV04        WSAV04            4 0
     C                   PARM      WSAV05        WSAV05            4 0
     C                   PARM      WSAV06        WSAV06            4 0
     C                   PARM      WSAV07        WSAV07            4 0
     C                   PARM      WSAV08        WSAV08            4 0
     C                   PARM      WSAV09        WSAV09            4 0
     C                   PARM      WSAV10        WSAV10            4 0
     C                   PARM      WSBK01        WSBK01            3
     C                   PARM      WSBK02        WSBK02            3
     C                   PARM      WSBK03        WSBK03            3
     C                   PARM      WSBK04        WSBK04            3
     C                   PARM      WSBK05        WSBK05            2
     C                   PARM      WSOPPD        WSOPPD            1
     C                   PARM      WSDOC         WSDOC            12
     C*
     C     TURERK        KLIST
     C                   KFLD                    UNSAVD
     C                   KFLD                    UNSPRO
     C     TURE1K        KLIST
     C                   KFLD                    YST
     C                   KFLD                    YAVD
     C                   KFLD                    YPRO
     C*
     C*
     C     DAYJOK        KLIST
     C                   KFLD                    DADATO
     C                   KFLD                    DABILK
     C                   KFLD                    DADIST
     C                   KFLD                    DABILN
     C     ST2DFK        KLIST
     C                   KFLD                    WSLKFR
     C                   KFLD                    TUSTEF
     C     ST2DTK        KLIST
     C                   KFLD                    WSLKTO
     C                   KFLD                    TUSTET
     C*
     C     WSDT          IFGT      501231
     C     WSDT          ADD       19000000      WSDT8             8 0
     C                   ELSE
     C     WSDT          ADD       20000000      WSDT8             8 0
     C                   END
     C*
     C                   MOVE      ' '           YST               1
     C                   MOVE      *ZEROS        YAVD              4 0
     C                   MOVE      *ZEROS        YPRO              8 0
     C* LEDIGLISTE SKAL IKKE SKRIVES DERSOM                            NE
     C* DAGBOK ER PR. DATO ELDRE (MINDRE) ENN DAGENS
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          UDTÅ
     C                   ELSE
     C     UYEAR         ADD       2000          UDTÅ
     C                   END
     C                   MOVE      UMONTH        UDTM
     C                   MOVE      UDAY          UDTD
     C*
     C                   MOVEL     '** OBS! '    XXÅPEN           20
     C                   MOVEL     'ÅPEN TUR'    XXÅPE2           12
     C                   MOVE      '! **'        XXÅPE2
     C                   MOVE      XXÅPE2        XXÅPEN
     C                   MOVEL     '** OBS! '    XXLUKK           20
     C                   MOVEL     'BØR LUKK'    XXLUK2           12
     C                   MOVE      'ES**'        XXLUK2
     C                   MOVE      XXLUK2        XXLUKK
     C                   MOVEL     '**FRAMTI'    XXFRAM           20
     C                   MOVEL     'DIG:'        XXFRA2           12
     C                   MOVE      XXFRA2        XXFRAM
     C                   MOVE      *BLANKS       GMBILK            3
     C                   MOVE      *BLANKS       GMTXT            20
     C                   READ      FIRM                                   33
     C*
     C* 97 -ON BEGRENSNING PÅ AVDELINGER SOM SKAL MED PÅ DAGBOK
     C     WSAV01        COMP      *ZEROS                             9797
     C* 98 -ON BEGRENSNING PÅ BILKODER SOM SKAL MED PÅ DAGBOK
     C     WSBK01        COMP      *BLANKS                            9898
     C                   ENDSR
     C*
