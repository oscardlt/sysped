      *
      ************************************************************
      *** PROGRAM SKAL LESE VALGBEGREPENE FOR Å SKRIVE UT      ***
      ***                   GODSLISTE                          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04-26                       ***
      *** LEDIGE INDIKATORER 27-97                             ***
      ************************************************************
     FCUNDC     IF   E             DISK
     FFIRM      IF   E           K DISK
     FFAXLK     IF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FKUNDL01   UF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR2)
     FUNITL01   IF   E           K DISK
     FTURER     IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTW     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTH     IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FFAXBIGFM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  UDAG                   1      2  0
     D  UMND                   3      4  0
     D  UAAR                   5      6  0
     D  UDATO                  1      6  0
     D                 DS
     D  F1                     1     21
     D                                     DIM(21)
     D  FAXNRP                 1     21
     D                 DS
     D  F2                     1     15
     D                                     DIM(15)
     D  FAXNR                  1     15
     D  FAX05A                 1      4
     D                 DS
     D  F3                     1      6
     D                                     DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D                UDS
     D  UFIRM                  1      2
     D  KOVFIR                 3      4
     D  KOVPRO                13     13
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UPRO                  22     29  0
     D* XBLA1/XBLA2 ER IKKE I BRUK. HVIS TAR NOEN POSISJONER TIL BRUK
     D* HUSK Å SYNKE STØRRELSE PÅ XBLA1/XBL2
     D  XBLA1                 70    256
     D  XBLA2                257    510
     D  CMR                   30     30
     D  INS1                  56     85
     D  VED2                  86    115
     D  INS2                 116    145
     D  VED3                 146    175
     D  INS3                 176    205
     D  INS4                 206    235
     D  CFRANK               236    238
      * LEV.ADRESSE STYRES FRA BESTILLINGSBILDE
     D  LEV1                 265    294
     D  LEV2                 295    324
     D  LEV3                 325    354
     D  LEV4                 355    384
     D  CSTED                385    410
     D  CDATO                411    416  0
     D  VED1                 417    446
     D  UPRT11               821    830
     D  UPT11                831    840
     D  UPRT12               841    850
     D  UPT12                851    860
     D  UPRT13               861    870
     D  UPT13                871    880
     D                 DS
     D  USØKKN                 1     30
     D  USØKKO                 1      2
     D  URRN                   3     12
     D  epofax                13     13
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyz')
     D epotxt          C                   CONST('Sender over godslist-
     D                                     e via epost')
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     I*
     C*
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPLNR
     C     KODTWK        KLIST
     C                   KFLD                    WSWUNI
     C                   KFLD                    WSAVD
     C     KODTVK        KLIST
     C                   KFLD                    WSVUNI
     C                   KFLD                    WSAVD
     C     KODTHK        KLIST
     C                   KFLD                    WSHUNI
     C                   KFLD                    WSAVD
     C     KODSFK        KLIST
     C                   KFLD                    WSFUNI
     C                   KFLD                    WSSG
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUKNA
     C     KUNKE2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    kundnr
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UJBKØ             1
     C                   PARM                    UIN03             1
     C                   PARM                    UIN04             1
     C                   PARM                    UIN01             1
     C                   PARM                    FAXNRU           21
     C                   PARM                    AAAVD             4
     C                   PARM                    AATUR             8
     C                   PARM                    KNAVN            30
     C                   PARM                    PDATE
     C                   PARM                    PTIME
     C                   PARM                    WSEDIT            1
     C                   PARM                    UUDOK             8
     C                   PARM                    KUNRA             8
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    COVP              1
     C                   PARM                    ULAND             2
     C                   PARM                    EPOST            70
     C                   PARM                    EPOSTJ            1
     C                   MOVE      'N'           WSEDIT
     C                   MOVE      '0'           UIN01
     C                   MOVE      'J'           UJBKØ
     C*
     C                   MOVE      'P'           WSPUNI            1
     C                   MOVE      'W'           WSWUNI            1
     C                   MOVE      'V'           WSVUNI            1
     C                   MOVE      'H'           WSHUNI            1
     C                   MOVE      'SF'          WSFUNI            2
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      USG           WSSG
     C     AAAVD         IFNE      *BLANKS
     C     AATUR         IFNE      *BLANKS
     C                   MOVE      AAAVD         WSAVD
     C                   MOVE      AATUR         WSPRO
     C                   END
     C                   END
     C* BLANKE DE FELTENE SOM IKKE ER I BRUK
     C                   MOVE      *BLANKS       XBLA1
     C                   MOVE      *BLANKS       XBLA2
     C                   MOVE      UDAY          UDAG
     C                   MOVE      UMONTH        UMND
     C                   MOVE      UYEAR         UAAR
     C                   MOVE      UDATO         CDATO
     C*
     C                   READ      FIRM                                   99
     C                   MOVE      FIFIRM        UFIRM
      * TRANSP.MODUL INSTALLERT?? (F7-SØK I BILREGISTER TILLATT)
     C     FITRAN        COMP      'J'                                    60
     C*
     C* 1. RUNDETEST
     C*
     C     TUKEY         CHAIN     TURER                              97
     C     KUNRA         IFNE      *ZEROS
     C     KUNRA         ANDNE     *BLANKS
     C                   MOVE      KUNRA         TUKNA
     C                   END
     C     KUNKEY        CHAIN(N)  CUNDL01                            33
     C                   MOVE      ULAND         WSLAND
     C  N33WSLAND        IFEQ      *BLANKS
     C                   MOVE      SYLAND        WSLAND
     C                   END
     C  N33FAXNR         IFEQ      *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     c                   movel     syepos        epost
     C                   END
     C     FAXNRU        IFNE      *BLANKS
     C                   MOVEL     FAXNRU        FAXNR
     C                   END
     C     WSLAND        IFNE      *BLANKS
     C     WSLAND        CHAIN     FAXLK                              33
     C     FAX05A        IFEQ      FAX05B
     C                   MOVE      *BLANKS       FAXRET
     C                   END
     C                   END
     C*
     C     START         TAG
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE
     C                   EXSR      KDTFMT
     C                   EXSR      KDTFM2
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   MOVEA     '00'          *IN(96)
     C*
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C*
     C   04              DO
     C     LINNBR        IFeq      10
     C     LO:UP         XLATE     EPOST         usok
     C                   CALL      'VCUNDC2R'
     C                   PARM                    FIFIRM
     C                   PARM                    USOK             30
     C                   PARM                    URRN2            10 0
     c                   parm                    epofax            1
     C     URRN2         IFNE      *ZEROS
     C                   MOVE      URRN2         URRN
     C                   MOVE      'KP'          USØKKO
     c                   else
     c                   movel     usok          epost
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       FAXNR
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    KUNDNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     c     kunke2        chain     cundl01                            31
     C  N31              MOVE      TFAXNR        FAXNR
     c                   end
     C* RETUR MED KONTAKTPERS
     C     USØKKO        IFEQ      'KP'
     C                   MOVE      URRN          RRN              10 0
     C     RRN           CHAIN     CUNDC                              31
     c     epofax        ifne      'E'
     C  N31              MOVEL     CFAX          FAXNR
     c                   else
     c                   move      *blanks       faxnr
     c                   end
     c     epofax        ifne      'F'
     C  N31              MOVEL     CEMAIL        EPOST
     c                   else
     c                   move      *blank        epost
     c                   end
     C  N31              MOVE      CCOMPN        KUNDNR
     C                   END
     C                   END
     C     KODTVK        CHAIN     KODTV                              99
     C  N99KODTWK        CHAIN     KODTW                              99
     C  N99KODTHK        CHAIN     KODTH                              99
     C  N99              Z-ADD     22            WSPLNR            3 0
     C  N99KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT11
     C  N99              MOVE      KOPTY         UPT11
     C  N99              Z-ADD     23            WSPLNR
     C  N99KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT12
     C  N99              MOVE      KOPTY         UPT12
     C  N99              Z-ADD     24            WSPLNR
     C  N99KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT13
     C  N99              MOVE      KOPTY         UPT13
     C   99              GOTO      START
     C     WSSG          IFNE      'ALL'
     C     KODSFK        CHAIN     KODTSF                             98
     C   98              GOTO      START
     C                   END
     C*
     C* HENTER OPPLYSNINGER FRA TURREGISTER
     C*
     C     TUKEY         KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPRO
     C*
     C     TUKEY         CHAIN     TURER                              97
     C   97              GOTO      START
     C     WSBILN        IFEQ      *BLANKS
     C     KUNRA         IFNE      *ZEROS
     C     KUNRA         ANDNE     *BLANKS
     C                   MOVE      KUNRA         TUKNA
     C                   END
     C     KUNKEY        CHAIN(N)  CUNDL01                            33
     C  N33WSLAND        IFEQ      *BLANKS
     C                   MOVE      SYLAND        WSLAND
     C                   END
     C  N33FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     c                   movel     syepos        epost
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C*SJEKKER LANDKODE MOTTAKER
     C*
     C     WSLAND        IFNE      *BLANKS
     C     WSLAND        CHAIN     FAXLK                              33
     C     FAX05A        IFEQ      FAX05B
     C                   MOVE      *BLANKS       FAXRET
     C                   END
     C                   END
      *
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        ANDEQ     *ZEROS
     C                   MOVE      'SHU0388'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      START
     C                   END
     C*
     C     WSTIME        IFEQ      *ZEROS
     C     WSDATE        ANDNE     *ZEROS
     C                   MOVE      'SHU0388'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      START
     C                   END
     C*
     C     WSTIME        IFLT      0
     C     WSTIME        ORGT      235959
     C                   MOVE      'SHU0389'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      START
     C                   END
     C*
     C*** TEST DATO
     C     WSDATE        IFNE      *ZEROS
     C                   CALL      'TESTDATO'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      96
     C   96
     CANN30              MOVE      'SPD0001'     MSGNR
     C   96              SETON                                        30
     C   30              GOTO      START
     C                   END
     C*
      * SØK I BILREGISTER
     C     *IN09         IFEQ      '1'
     C     *IN60         ANDEQ     '1'
     C                   MOVE      *BLANKS       TFAXNR
     C                   MOVE      *ZEROS        TUKNA
     C* HVIS BILNR ANGITT-SJEKK GYLDIGHET
     C     WSBILN        IFNE      *BLANKS
     C     WSBILN        CHAIN     UNITL01                            33
     C   33              MOVE      *BLANKS       WSBILN
     C                   END
     C* GYLDIG BILNR IKKE GITT I KNAVN-LINJA - SØK I BILREGISTER
     C     WSBILN        IFEQ      *BLANKS
     C                   CALL      'SYTR19'
     C                   PARM                    WSBILN
     C                   END
     C* GYLDIG BILNR ER TILSTEDE- VIS EVT AUTOPLUKK FAXNR+TEKST
     C     WSBILN        IFNE      *BLANKS
     C                   MOVE      'P'           UMODUS            1
     C                   CALL      'SYTR09'
     C                   PARM                    WSBILN
     C                   PARM                    TFAXNR
     C                   PARM                    UTEXT            20
     C                   PARM                    UMODUS
     C*
     C     TFAXNR        IFNE      *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     C                   MOVEL     WSBILN        KNAVN
     C                   MOVE      UTEXT         KNAVN
     C                   END
     C                   END
     C                   END
     C*
     C* F10 = SEND FAX
     C*
     C  N10              GOTO      START
     C*
     C* KALLER PROGRAM FOR COVERPAGE
     C*
     C     EPOST         IFne      *BLANKS
     c                   move      'J'           wscvrp
     c                   end
     C     EPOST         IFEQ      *BLANKS
     C     WSCVRP        IFEQ      'J'
     C     WSCVRP        OREQ      'Y'
     C                   MOVE      'J'           COVP
     C                   MOVEL     FIFT          COV05
     C                   MOVE      *BLANKS       COV01
     C                   MOVE      *BLANKS       COV03
     C                   MOVE      *BLANKS       COV04
     C                   MOVE      *BLANKS       COV07
     C                   MOVE      *BLANKS       COV08
     C* HENTER BRUKERNAVN AVSENDER
     C                   CALL      'SYGE12CL'
     C                   PARM                    USE              10
     C                   PARM                    NVN              40
     C                   MOVEL     NVN           COV06
     C                   MOVEL     KNAVN         COV02
     C                   MOVEL     ADR1          COV03
     C* KALLER PROGRAM FOR INNTASTING AV COVERPAGE
     C                   CALL      'FAXCVP'
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C* A = AVBRYT
     C     FUNK          IFEQ      'A'
     C                   GOTO      START
     C                   END
     C                   END
     C*EPOST COVERPAGE
     C                   ELSE
     C                   MOVE      'J'           COVP
     c                   movel     epotxt        cov01
     C                   CALL      'EPOCVP'
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C     FUNK          CABEQ     'A'           start
     C                   END
     C* EVENTUELLT OPPDATER KUNDEFIL
     C     KUNDNR        IFNE      *ZEROS
     C     FAXNR         IFNE      *BLANKS
     C     TFAXNR        IFEQ      *BLANKS
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD
     C     KUNKEY        CHAIN     KUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD2
     C                   END
     C                   END
     C                   END
     C*
     C*
     C                   MOVE      WSAVD         UAVD
     C                   MOVE      WSSG          USG
     C                   MOVE      WSPRO         UPRO
     C                   MOVE      ' '           UJBKØ
     C                   MOVE      '1'           UIN03
     C                   MOVE      '0'           UIN04
     C     UPT11         IFEQ      *BLANKS
     C                   MOVEL     '*STD'        UPT11
     C                   END
     C     UPRT12        IFEQ      *BLANKS
     C                   MOVEL     'QPRINT '     UPRT12
     C                   END
     C     UPT12         IFEQ      *BLANKS
     C                   MOVEL     '*STD'        UPT12
     C                   END
     C*
     C     UPRT13        IFEQ      *BLANKS
     C                   MOVEL     'QPRINT '     UPRT13
     C                   END
     C     UPT13         IFEQ      *BLANKS
     C                   MOVEL     '*STD'        UPT13
     C                   END
     C                   MOVE      WSAVD         AAAVD
     C                   MOVE      WSPRO         AATUR
     C*
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        IFNE      *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVEL     *BLANKS       PDATE
     C                   MOVEL     WSTIME        PTIME
     C                   MOVEL     WSDATE        PDATE
     C                   END
     C                   END
     C* LEGGER EVENTUELLT LANDKODENS PREFIX FORAN FAXNUMMER
     C     FAXRET        IFNE      *BLANKS
     C                   EXSR      FAXFIX
     C                   MOVEL     FAXNRP        FAXNRU
     C                   ELSE
     C                   MOVEL     FAXNR         FAXNRP
     C                   MOVEL     FAXNRP        FAXNRU
     C                   END
     C*
     C                   MOVE      'N'           EPOSTJ            1
     C     EPOST         IFNE      *BLANKS
     C                   MOVE      'J'           EPOSTJ
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      CDATO         XDTFMT            4 0
     C                   MOVEL     CDATO         UDT1              2 0
     C                   MOVEL     XDTFMT        UDT2              2 0
     C                   MOVE      XDTFMT        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVEL     UDT1          CDATO
     C                   MOVEL     UDT2          XDTFMT
     C                   MOVE      UDT3          XDTFMT
     C                   MOVE      XDTFMT        CDATO
     C*
     C                   ENDSR
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   Z-ADD     1             A1                2 0
     C                   Z-ADD     1             A2                2 0
     C                   Z-ADD     1             A3                2 0
     C                   DO        6
     C     F3(A3)        IFNE      *BLANKS
     C                   MOVEL     F3(A3)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A3
     C                   END
     C                   DO        15
     C     F2(A2)        IFNE      *BLANKS
     C                   MOVEL     F2(A2)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A2
     C                   END
     C                   ENDSR
     C***********************************************
     C     KDTFM2        BEGSR
     C***********************************************
     C                   MOVE      WSDATE        XDTFMT            4 0
     C                   MOVEL     WSDATE        UDT1              2 0
     C                   MOVEL     XDTFMT        UDT2              2 0
     C                   MOVE      XDTFMT        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
     C*
     OKUNDR     E            UPD
     O                       TFAXNR
     OKUNDR2    E            UPD2
     O                       TFAXNR
