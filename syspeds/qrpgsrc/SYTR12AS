********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 ny parameter syge13
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL lage EN KOSBU-POST - FORVENTET AVR.       ***
      ***        PÅ ETT OPPDRAG VED TR.KODE 4/5                 ***
      **************************************************************
      *  UMID = 'J' - Midlertidig kall fra oppdrag - lag åpen post
      *  UMID = 'N' - Endelig  kall fra sett på tur - lag / LÅS med kode A
      *  UMID = 'S' - Kall fra fjern fra tur  - Slett
      *               (dersom så INCR lik 4/5 så vil en ny bli skapt)
      *
     FKOSTT     UF   E           K DISK
     FKOSBU12   UF A E           K DISK
     FHEADF     IF   E           K DISK
     FTURER14   IF   E           K DISK
     D                 DS
     D  BUTXT                  1     35
     D  BUTX_20                1     20
     D  BUTX_7                29     35
     D                 DS
     D  HXHEAD                 1    256
     D  XXUR                   1      1
     D  XXFR                   2      4
     D  XXKDPL                 5      5
     D  XXLKA                  6      7
     D  XXSDF                  8     12
     D  XXTRI                 13     14
     D  XXSDT                 15     19
     D  XXLKS                 20     21
     D  XXSDFF                22     26
     D  XXLKK                 27     28
     D  XXSDVT                29     33
     D  XXVKT                 34     42  0
     D  XXFBV                 43     51  0
     D  XXM3                  52     58  3
     D  XXLM                  59     62  2
     D  XXNT                  63     69  0
     D  XXVERV                70     72
     D  XXVERB                73     75  2
     D                 DS
     D  HEDTOP                 1      8  0
     D  OPDACC                 1      2  0
     D  OPDAAR                 3      4  0
     D  OPDMND                 5      6  0
      *
     C                   EXSR      INIT
     C     HeadKey       CHAIN     HEADF                              33
     C     *IN33         CABEQ     *ON           SLUTT
     c                   exsr      SNDAVT
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     SndAvt        BEGSR
     C***********************************************
     c     UTUNR         comp      *zeros                                 33
     C  N33UTUNR         CHAIN     TURER14                            33
     C     *IN33         IFEQ      *ON
     C                   MOVE      OPDACC        TURACC
     C                   MOVE      OPDAAR        TURAAR
     C                   MOVE      OPDMND        TURMND
     C                   MOVEL     'X'           TUBILN
     c                   end
     C*
     c                   move      *zeros        AntBuTV           3 0
     C     HeadKey       SETLL     KOSBU12
     C     HeadKey       READE     KOSBU12                                34
     C     *in34         doweq     *off
      * Linje finnes mer ER alt ferdig - hopp ut
     C     BUTX_7        IFEQ      '* OPD *'
     C     BUST          IFEQ      'F'
     C     BUST          Oreq      'A'
     C                   update    KOSBUR
     c                   add       BUBL          UBEL                           vis beløp i oppdr.
     C                   goto      Utavt
     C                   end
     C                   end
     C     BUTX_7        IFEQ      '* OPD *'
     C     BUST          andeq     ' '
      * Slett etter fjerning fra tur
     C     UMID          IFEQ      'S'
     C                   delete    KOSBUR
     c                   else
     C     UINCR         IFEQ      4
     C     UINCR         oreq      5
     C     BUTYPE        ifeq      'E'
     C                   exsr      BERFRP
     C                   END
     C                   exsr      MovBu
     c                   add       1             AntBuTV           3 0
     c                   add       BUBL          UBEL
     C                   END
     C                   update    KOSBUR
     C                   END
     c                   else
      * andre enn * OPD * m status blank
     C                   update    KOSBUR
     c                   end
     C     HeadKey       READE     KOSBU12                                34
     C                   END
     C*
     C     AntBuTv       IFEQ      0
     C     UINCR         IFEQ      4
     C     UINCR         oreq      5
     C                   Exsr      CrtBu
     c                   add       BUBL          UBEL
     C                   END
     C                   END
     C*
     C     UtAvt         ENDSR
     C*
     C***********************************************
     C     CrtBu         BEGSR
     C***********************************************
     C* LAG NY (TYPES SETTER TIL A AV MovBu dersom endelig...)
     C     'Å'           CHAIN     KOSTT                              34
     C  N34              ADD       1             KTNR
     C  N34              UPDATE    KOSTTR
     C                   CLEAR                   KOSBUR
     C                   MOVE      KTNR          BUBNR
     C                   MOVE      'E'           BUTYPE
     C                   exsr      BERFRP
     C                   exsr      MovBu
     C                   WRITE     KOSBUR
     C                   ENDSR
     C*
     C***********************************************
     C     MovBu         BEGSR
     C***********************************************
      * De fleste felt i KOsbu byttes ubetinget ..
     C                   MOVE      'NOK'         BUVAL
     C                   MOVE      TURACC        BUPCC
     C                   MOVE      TURAAR        BUPÅR
     C                   MOVE      TURMND        BUPMN
     C                   MOVE      TUBILN        BUBILN
     C                   MOVE      UTRAN         BUTNR
     C                   MOVE      UTRLE         BULNR
     C     UMID          IFEQ      'N'
     C     BUBL          ANDNE     0
     C                   MOVE      'A'           BUTYPE
     C                   END
     C                   MOVE      'FRA'         BUVK
     C                   MOVE      HESG          BUSG
     C                   MOVE      TUAVD         BUTAVD
     C                   MOVE      TUPRO         BUTUNR
     C                   MOVE      HEAVD         BUOAVD
     C                   MOVE      HEOPD         BUOPD
     C                   MOVE      'T'           BUBLST
     C                   MOVE      *BLANKS       BUTXT
     C                   EVAL      BUTXT = HXSNDN
     C                   MOVE      '* OPD *'     BUTXT
     C                   MOVE      TUBILK        BUBILK
     C                   MOVE      TUBILN        BUBILN
     C                   MOVE      TUDT          BUFEDT
     C                   MOVE      'O'           BUFER                          *AUTOM. FRA TUR/FERG
     C                   ENDSR
     C*
     C******************************************
     C     BERFRP        BEGSR
     C******************************************
     C* POSTNUMMERBASERT KALKULASJON
     C                   MOVE      'H'           XXUR
     C                   MOVE      'NO'          XXLKA
     C                   move      HESDF         XXSDF
     C     HESDFF        ifne      *blanks
     C                   move      HESDFF        XXSDF
     c                   end
     C                   MOVE      'NO'          XXTRI
     C                   move      HESDT         XXSDT
     C     HESDVT        ifne      *blanks
     C                   move      HESDVT        XXSDT
     c                   end
     C                   MOVE      *BLANKS       XXLKS
     C                   MOVE      *BLANKS       XXSDFF
     C                   MOVE      *BLANKS       XXLKK
     C                   MOVE      *BLANKS       XXSDVT
     C                   MOVE      *BLANKS       XXVERV
     C                   MOVE      *ZEROS        XXVERB
     C                   MOVE      *ZEROS        N5                5 0
     C                   MOVE      *ZEROS        NOPD              7 0
     C                   Z-ADD     hevkt         XXVKT
     C                   Z-ADD     hefbv         XXfbv
     C                   Z-ADD     heM3          XXM3
     C                   Z-ADD     heLM          XXLM
     C                   Z-ADD     heNT          XXNT
      * Finn frakt i kundersfraktavtale
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      'FRA'         UGEB              3
     C                   MOVE      'FRA'         UGEBKO            3
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      HXHEAD        UHEAD           256
     C                   Z-ADD     UTRKU         UUKN              8 0
     C                   CALL      'SYGE14'
     C                   PARM                    UGEBKO
     C                   PARM                    UGEB
     C                   PARM                    UAVD              4 0
     C                   PARM                    NOPD
     C                   PARM                    UUKN
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    UAVNR             9 0
     C                   PARM                    UAVMSG           50
     C                   PARM                    USKA              1
     C                   PARM                    UHEAD
      * Ikke direkte avtale..     SLÅ OPP I BRUTTO.
     C     UUBELØ        IFEQ      0
     C                   MOVE      *BLANKS       USONE
     C                   MOVE      *BLANKS       UUVAL
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE13'
     C                   PARM                    UGEB
     C                   PARM                    UAVD
     C                   PARM                    NOPD
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UUKN
     C                   PARM                    UHEAD
N01  C                   PARM                    atdum             1
      * brutto utten rabatt gjelder IKKE som tilslag på TRANSP-avtale
     C     UUBELØ        IFNE      *ZEROS
     C     URABAT        ANDEQ     *ZEROS
     C                   move      *ZEROS        UUBELØ
     C                   END
     C                   END
      *
     C                   z-add     UUBELØ        BUBL
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C**
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UTUNR             8 0
     C                   PARM                    UTRAN             8 0
     C                   PARM                    UTRKU             8 0
     C                   PARM                    UTRLE             8 0
     C                   PARM                    UINCR             1 0          ulik 4/5=Fjern
     C                   PARM                    UMID              1            A / E
     C                   PARM                    UBEL             13 2          retur beløp
     C                   PARM                    UBNR              7 0          Når EN er skapt
     c                   move      *zeros        UBEL
     c                   move      *zeros        UBNR
     C     HeadKey       KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   ENDSR
     C*
