*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0511A* 01 JOV PTF5 F7=VEDL. FR.TILLEG (VAL/OLJE..)
P8068A* 04 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA36PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FRAKTRATER-
     A                                       LAND')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA36'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC0187 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  5  2MSGCON(20 DIV1633 SYMSGF)
     A            WSKNR          8Y 0B  5 25DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  5 40MSGCON(30 YBC0129 SYMSGF)
     A                                  7  2MSGCON(30 YBC0130 SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(20 DIV1634 SYMSGF)
     A            WSFRA          5A  B  8 25TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A                                 10  2MSGCON(20 YBC0131 SYMSGF)
     A            WSTIL          5A  B 10 25DSPATR(UL)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '3' '4')
     A            FTFRA          5A  O  5  6
     A  34                                  COLOR(WHT)
     A            FTFRAN         3A  O  5 15
     A            FTOPDT         2A  O  5 25
     A            FTSK           1A  O  5 34
     A            FTTIL          5A  O  5 38
     A  35                                  COLOR(WHT)
     A            FTAVTN         1Y 0O  5 47
     A            FTKODE         1A  O  5 55
     A            FTKOLL         1A  O  5 59
     A            FBX           36   H
     A            FTMIN          5Y 0H
     A            FTMAX1         5Y 0H
     A            FTPRPR         5Y 0H
     A            FMX           36   H
     A            FTMAX2         5Y 0H
     A            FTMAX3         5Y 0H
     A            FTFAST         2Y 0H
EV5  A            FTDT           8Y 0H
     A            FTMM           1Y 0H
     A            FTVAL          3   H
     A            FTLMOM         5Y 0H
     A            FTM3OM         5Y 0H
     A            FVKX          60   H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA36'
     A                                      COLOR(BLU)
      * 27 AV = BRUTTO FRAKTAVTALER
     A N27                              1 20MSGCON(40 YBC0188 SYMSGF)
     A                                      COLOR(WHT)
      * 27 AV = KUNDERS FRAKTAVTALER
     A  27                              1 20MSGCON(40 YBC0189 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              2  2MSGCON(10 DIV0027 SYMSGF)
     A  27        WSKNR          8  0O  2 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  27        WSKNV         30   O  2 24COLOR(WHT)
     A                                  3  2MSGCON(60 YBC0126 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0190 SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A*%%TS  SD  20000519  130058  JOVO        REL-V4R4M0  5769-PW1
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF07(07 'FR,TILLEGG')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'SLETTE ALLE AVT.')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2 62MSGCON(018 YBC0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 JOV1028 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VARELI
     A*%%TS  SD  20111017  152102  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
     A          H                           HLPPNLGRP(SYFA36015 SYFA36PNL)
     A                                      HLPARA(05 001 08 080)
     A          H                           HLPPNLGRP(SYFA36020 SYFA36PNL)
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPPNLGRP(SYFA36025 SYFA36PNL)
     A                                      HLPARA(10 001 12 080)
     A          H                           HLPPNLGRP(SYFA36030 SYFA36PNL)
     A                                      HLPARA(13 001 14 080)
     A          H                           HLPPNLGRP(SYFA36035 SYFA36PNL)
     A                                      HLPARA(15 001 15 080)
     A          H                           HLPPNLGRP(SYFA36040 SYFA36PNL)
     A                                      HLPARA(16 001 16 025)
     A          H                           HLPPNLGRP(SYFA36045 SYFA36PNL)
     A                                      HLPARA(16 026 16 080)
     A          H                           HLPPNLGRP(SYFA36050 SYFA36PNL)
     A                                      HLPARA(17 001 17 010)
     A          H                           HLPPNLGRP(SYFA36055 SYFA36PNL)
     A                                      HLPARA(17 011 17 025)
     A          H                           HLPPNLGRP(SYFA36060 SYFA36PNL)
     A                                      HLPARA(17 026 17 056)
     A          H                           HLPPNLGRP(SYFA36065 SYFA36PNL)
     A                                      HLPARA(17 057 17 080)
     A          H                           HLPPNLGRP(SYFA36070 SYFA36PNL)
     A                                      HLPARA(18 001 18 080)
     A          H                           HLPPNLGRP(SYFA36999 SYFA36PNL)
     A                                      HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFA36'
     A                                      COLOR(BLU)
     A N27                              1 20MSGCON(040 YBC0188 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(040 YBC0189 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A  27                              3  2MSGCON(010 DIV0027 *LIBL/SYMSGF)
     A  27        WSKNR          8Y 0O  3 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  27        WSKNV         30A  O  3 24COLOR(WHT)
     A  40                              5  2MSGCON(020 YBC0142 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N40                              5  2MSGCON(020 YBC0143 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(020 DIV1635 *LIBL/SYMSGF)
     A            W2FRAN         3A  B  7 25TEXT('FRANKATUR')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                  8  2MSGCON(020 DIV1636 *LIBL/SYMSGF)
     A            W2OPDT         2A  B  8 25TEXT('OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                  8 40MSGCON(032 DIV1650 *LIBL/SYMSGF)
     A                                  9  2MSGCON(020 YBC0165 *LIBL/SYMSGF)
     A            W2SK           1A  B  9 25TEXT('SELGER/KJØPER')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                      VALUES(' ' 'S' 'K' 'A')
     A                                 10  2MSGCON(020 DIV1634 *LIBL/SYMSGF)
     A            W2FRA          5A  B 10 25TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A N40        W2FRSO         1A  B 10 44VALUES(' ' 'X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N40                             10 46MSGCON(025 JOV0016 *LIBL/SYMSGF)
     A                                 11  2MSGCON(020 YBC0131 *LIBL/SYMSGF)
     A            W2TIL          5A  B 11 25DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A N40        W2TISO         1A  B 11 44VALUES(' ' 'X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A N40                             11 46MSGCON(025 JOV0016 *LIBL/SYMSGF)
     A                                 13  5MSGCON(075 YBC0191 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WVK1           5Y 0B 14 10DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK2           5Y 0B 14 16DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK3           5Y 0B 14 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK4           5Y 0B 14 28DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK5           5Y 0B 14 34DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK6           5Y 0B 14 40DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK7           5Y 0B 14 46DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK8           5Y 0B 14 52DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK9           5Y 0B 14 58DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK10          5Y 0B 14 64DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK11          5Y 0B 14 70DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WVK12          5Y 0B 14 76DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB1            5Y 0B 15  9DSPATR(UL)
     A  40                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTWRD('     -')
     A            WB2            5Y 0B 15 16DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB3            5Y 0B 15 22DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB4            5Y 0B 15 28DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB5            5Y 0B 15 34DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB6            5Y 0B 15 40EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A            WB7            5Y 0B 15 46DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB8            5Y 0B 15 52DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB9            5Y 0B 15 58DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB10           5Y 0B 15 64DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB11           5Y 0B 15 70DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WB12           5Y 0B 15 76DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  27                             16  3'P=%'
     A  27        W2KODE         1A  B 16  7TEXT('KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'P')
     A                                 16 10MSGCON(008 DIV1490 *LIBL/SYMSGF)
     A            W2MIN          5Y 0B 16 19TEXT('MINIMUM PRIS')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 26MSGCON(008 DIV1639 *LIBL/SYMSGF)
     A            W2MAX1         5Y 0B 16 35TEXT('MAXIMUM PRIS-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 42MSGCON(007 DIV1643 *LIBL/SYMSGF)
     A            W2MAX2         5Y 0B 16 50TEXT('MAXIMUM PRIS-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 56MSGCON(009 DIV1644 *LIBL/SYMSGF)
     A            W2MAX3         5Y 0B 16 66TEXT('MAXIMUM PRIS-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 17  3MSGCON(004 DIV1642 *LIBL/SYMSGF)
     A            W2KOLL         1A  B 17  8TEXT('KOLLI')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17 11MSGCON(002 DIV1648 *LIBL/SYMSGF)
     A            W2PRPR         5Y 0B 17 15TEXT('PR. ANTALL KG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 17 21MSGCON(003 DIV1645 *LIBL/SYMSGF)
     A                                 17 26MSGCON(027 DIV1646 *LIBL/SYMSGF)
     A            W2FAST         2Y 0B 17 54TEXT('FASTPRIS T.O.M VEKTGRUPPE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(LE 12)
     A                                 17 57MSGCON(007 DIV5023 *LIBL/SYMSGF)
     A            W2MM           1Y 0B 17 65DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 2 3)
     A                                 17 67MSGCON(006 DIV5024 *LIBL/SYMSGF)
     A            W2VAL          3A  B 17 74DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18  3MSGCON(014 DIV5025 *LIBL/SYMSGF)
     A            W2LMOM         5Y 0B 18 18DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18 25MSGCON(015 DIV5026 *LIBL/SYMSGF)
     A            W2M3OM         5Y 0B 18 41DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18 48MSGCON(012 SHU0486 *LIBL/SYMSGF)
     A            WSDTU          6Y 0B 18 61COLOR(YLW)
     A                                      EDTCDE(4)
     A  47                             20  5MSGCON(075 YBC0191 *LIBL/SYMSGF)
     A  47        WSMIMA         3A  O 21  5COLOR(WHT)
     A  47        FM01           5Y 0O 21 10EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM02           5Y 0O 21 16EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM03           5Y 0O 21 22EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM04           5Y 0O 21 28EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM05           5Y 0O 21 34EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM06           5Y 0O 21 40EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM07           5Y 0O 21 46EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM08           5Y 0O 21 52EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM09           5Y 0O 21 58EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM10           5Y 0O 21 64EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM11           5Y 0O 21 70EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM12           5Y 0O 21 76EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
S04  A*         H                           HLPDOC('SYFA36075' 'SYFA36' 'SY400L-
S04  A*                                     O/MAINT')
N04  A          H                           HLPPNLGRP(SYFA36075 SYFA36PNL)
     A                                      HLPARA(01 001 22 080)
S04  A*         H                           HLPDOC('SYFA36999' 'SYFA36' 'SY400L-
S04  A*                                     O/MAINT')
N04  A          H                           HLPPNLGRP(SYFA36999 SYFA36PNL)
     A                                      HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA36'
     A                                      COLOR(BLU)
     A N27                              1 20MSGCON(40 YBC0188 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(40 YBC0189 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  5 25MSGCON(10 YBC0137 SYMSGF)
     A                                  5 40MSGCON(10 YBC0138 SYMSGF)
     A  27                              6  2MSGCON(10 DIV0027 SYMSGF)
     A  27        WSKNR          8  0O  6 25COLOR(WHT)
     A                                      EDTCDE(4)
     A            W2KNR          8  0B  6 40COLOR(YLW)
     A  80                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6 52MSGCON(7 YBC0159 SYMSGF)
     A                                  6 60MSGCON(15 YBC0149 SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(20 DIV1635 SYMSGF)
     A            WKFRAN         3A  O  7 25TEXT('FRANKATUR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2FRAN         3A  B  7 40TEXT('FRANKATUR')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 60MSGCON(12 YBC0139 SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(20 DIV1636 SYMSGF)
     A            WKOPDT         2A  O  8 25TEXT('OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2OPDT         2A  B  8 40TEXT('OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8 60MSGCON(12 YBC0139 SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  9  2MSGCON(20 YBC0165 SYMSGF)
     A            WKSK           1A  O  9 25TEXT('SELGER/KJØPER')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2SK           1A  B  9 40TEXT('SELGER/KJØPER')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'S' 'K' 'A')
     A                                  9 60MSGCON(12 YBC0139 SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 10  2MSGCON(20 DIV1634 SYMSGF)
     A            WKFRA          5A  O 10 25TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2FRA          5A  B 10 40TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2FRSO         1   B 10 44VALUES(' ' 'X')
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A                                 10 60MSGCON(12 YBC0139 SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 11  2MSGCON(20 YBC0131 SYMSGF)
     A            WKTIL          5A  O 11 25DSPATR(UL)
     A                                      COLOR(WHT)
     A            W2TIL          5A  B 11 40DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TISO         1A  B 11 44VALUES(' ' 'X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 60MSGCON(12 YBC0139 SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 15 40MSGCON(35 YBC0150 SYMSGF)
     A                                      COLOR(BLU)
     A                                 16 40MSGCON(35 YBC0151 SYMSGF)
     A                                      COLOR(BLU)
     A                                 17 40MSGCON(35 YBC0152 SYMSGF)
     A                                      COLOR(BLU)
     A                                 18 40MSGCON(35 YBC0153 SYMSGF)
     A                                      COLOR(BLU)
     A                                 19 40MSGCON(35 YBC0154 SYMSGF)
     A                                      COLOR(BLU)
     A                                 21 40MSGCON(35 YBC0160 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SYFA36'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC0155 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A  27                              3  2MSGCON(10 DIV0027 SYMSGF)
     A  27        WSKNR          8  0O  3 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  27        WSKNV         30   O  3 24COLOR(WHT)
     A                                  5  2MSGCON(40 YBC0144 SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(20 DIV1635 SYMSGF)
     A            FTFRAN         3A  O  7 25TEXT('FRANKATUR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(20 DIV1636 SYMSGF)
     A            FTOPDT         2A  O  8 25TEXT('OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(20 YBC0165 SYMSGF)
     A            FTSK           1A  O  9 25TEXT('SELGER/KJØPER')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(20 DIV1634 SYMSGF)
     A            FTFRA          5A  O 10 25TEXT('DESTINASJON FRA')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(20 YBC0131 SYMSGF)
     A            FTTIL          5A  O 11 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  5MSGCON(075 YBC0191 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            FRVK1          5Y 0O 14 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK2          5Y 0O 14 16DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK3          5Y 0O 14 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK4          5Y 0O 14 28DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK5          5Y 0O 14 34DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK6          5Y 0O 14 40DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK7          5Y 0O 14 46DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK8          5Y 0O 14 52DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK9          5Y 0O 14 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK10         5Y 0O 14 64DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK11         5Y 0O 14 70DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FRVK12         5Y 0O 14 76DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB01           5Y 0O 15 10DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            FB02           5Y 0O 15 16DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB03           5Y 0O 15 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB04           5Y 0O 15 28DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB05           5Y 0O 15 34DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB06           5Y 0O 15 40EDTCDE(4)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            FB07           5Y 0O 15 46DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB08           5Y 0O 15 52DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB09           5Y 0O 15 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB10           5Y 0O 15 64DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB11           5Y 0O 15 70DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FB12           5Y 0O 15 76DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16  3'P=%'
     A            FTKODE         1A  O 16  7TEXT('KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 10MSGCON(008 DIV1490 *LIBL/SYMSGF)
     A            FTMIN          5Y 0O 16 19TEXT('MINIMUM PRIS')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16 26MSGCON(008 DIV1639 *LIBL/SYMSGF)
     A            FTMAX1         5Y 0O 16 35TEXT('MAXIMUM PRIS-1')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16 42MSGCON(007 DIV1643 *LIBL/SYMSGF)
     A            FTMAX2         5Y 0O 16 50TEXT('MAXIMUM PRIS-2')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16 56MSGCON(009 DIV1644 *LIBL/SYMSGF)
     A            FTMAX3         5Y 0O 16 66TEXT('MAXIMUM PRIS-3')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17  3MSGCON(004 DIV1642 *LIBL/SYMSGF)
     A            FTKOLL         1A  O 17  8TEXT('KOLLI')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17 11MSGCON(002 DIV1648 *LIBL/SYMSGF)
     A            FTPRPR         5Y 0O 17 15TEXT('PR. ANTALL KG')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17 21MSGCON(003 DIV1645 *LIBL/SYMSGF)
     A                                 17 26MSGCON(027 DIV1646 *LIBL/SYMSGF)
     A            FTFAST         2Y 0O 17 54TEXT('FASTPRIS T.O.M VEKTGRUPPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17 57MSGCON(007 DIV5023 *LIBL/SYMSGF)
     A            FTMM           1Y 0O 17 65DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17 67MSGCON(006 DIV5024 *LIBL/SYMSGF)
     A            FTVAL          3A  O 17 74DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 18  3MSGCON(014 DIV5025 *LIBL/SYMSGF)
     A            FTLMOM         5Y 0O 18 18DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18 25MSGCON(015 DIV5026 *LIBL/SYMSGF)
     A            FTM3OM         5Y 0O 18 41DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  47                             20  5MSGCON(075 YBC0191 *LIBL/SYMSGF)
     A  47        WSMIMA         3A  O 21  5COLOR(WHT)
     A  47        FM01           5Y 0O 21 10EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM02           5Y 0O 21 16EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM03           5Y 0O 21 22EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM04           5Y 0O 21 28EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM05           5Y 0O 21 34EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM06           5Y 0O 21 40EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM07           5Y 0O 21 46EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM08           5Y 0O 21 52EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM09           5Y 0O 21 58EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM10           5Y 0O 21 64EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM11           5Y 0O 21 70EDTCDE(4)
     A                                      COLOR(WHT)
     A  47        FM12           5Y 0O 21 76EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R NYVARE
     A*%%TS  SD  20000519  130058  JOVO        REL-V4R4M0  5769-PW1
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF07(07 'FR,TILLEGG')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA36'
     A                                      COLOR(BLU)
     A N27                              1 20MSGCON(040 YBC0188 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(040 YBC0189 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              2  2MSGCON(010 DIV0027 *LIBL/SYMSGF)
     A  27        WSKNR          8Y 0O  2 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  27        WSKNV         30A  O  2 24COLOR(WHT)
     A                                  3  2MSGCON(060 YBC0126 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 YBC0190 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27                              6  2MSGCON(078 YBC0136 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A  27                              6  2MSGCON(078 YBC0135 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22 42MSGCON(018 JOV1028 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SUBFIL2                   SFL
     A            XJN            1A  H
     A            FTKNR          8Y 0H
     A            FTFRAN         3A  O  5  6
     A N29                                  COLOR(RED)
     A            FTOPDT         2A  O  5 16
     A N29                                  COLOR(RED)
     A            FTSK           1A  O  5 25
     A N29                                  COLOR(RED)
     A            FTFRA          5A  O  5 29
     A N29                                  COLOR(RED)
     A            FTTIL          5A  O  5 38
     A N29                                  COLOR(RED)
     A            FTAVTN         1Y 0O  5 47
     A N29                                  COLOR(RED)
     A            FTKODE         1A  O  5 55
     A N29                                  COLOR(RED)
     A            FTKOLL         1A  O  5 59
     A N29                                  COLOR(RED)
     A            FBX           36   H
     A            FTMIN          5Y 0H
     A            FTMAX1         5Y 0H
     A            FTPRPR         5Y 0H
     A            FMX           36   H
     A            FTMAX2         5Y 0H
     A            FTMAX3         5Y 0H
     A            FTFAST         2Y 0H
EV5  A            FTDT           8Y 0H
     A            FTMM           1Y 0H
     A            FTVAL          3   H
     A            FTLMOM         5Y 0H
     A            FTM3OM         5Y 0H
     A            FVKX          60   H
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  61                                  SFLDSP
     A  62                                  SFLDSPCTL
     A  63                                  SFLCLR
     A  64                                  SFLEND
     A  61N63N64                            ROLLUP(25)
S04  A*         H                           HLPDOC('SYFA36080' 'SYFA36' 'SY400L-
S04  A*                                     O/MAINT')
N04  A          H                           HLPPNLGRP(SYFA36080 SYFA36PNL)
     A                                      HLPARA(01 001 20 080)
     A            ZRECN2         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA36'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC0156 SYMSGF)
     A                                      COLOR(WHT)
     A  28                              2  2MSGCON(10 DIV0027 SYMSGF)
     A  28        W2KNR          8  0O  2 14COLOR(WHT)
     A                                      EDTCDE(4)
     A  28        KNAVN         30   O  2 24COLOR(WHT)
     A                                  4  6MSGCON(74 YBC0192 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'AVBRYD')
     A                                      CF21(21 'OVERSKRIVE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  31                              3 22MSGCON(45 YBC0145 SYMSGF)
     A                                      COLOR(BLU)
     A N31                              3 22MSGCON(25 YBC0157 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 4 30 *NOMSGLIN)
     A                                      CF10(10 'SLETTE')
     A                                      CF12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2MSGCON(25 YBC0158 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(18 YBC0148 SYMSGF)
     A                                  4  2MSGCON(18 DIV0024 SYMSGF)
