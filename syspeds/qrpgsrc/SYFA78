********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 KODE FOR CHARGE MOT FLYSELSKAP
********************************************************************
      *
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE FUEL SURCHARGE            ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 95-97 99 ***
      ***********************************************************
      *
     FKODTFC    UF A E           K DISK
     FKODTFS    IF   E           K DISK
     FSYFA78FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  KFCFTX                 1     17
     D  XDLT                   1      7
     C*
     c     nyini         tag
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODFCK        SETLL     KODTFC
     C     START1        TAG
     C     XUNI          READE(n)  KODTFC                                 94
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        17
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     XUNI          READE(n)  KODTFC                                 94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   12              GOTO      nyini
     C   03              GOTO      nyini
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              GOTO      START5
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODFC2        CHAIN     KODTFC                             33
     C  N33              DELETE    KODTFCR
     C                   EXSR      MOVB1
     C                   MOVE      '*DELETE'     XDLT
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ENDRE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C* TEST FLYSELSKAPSNR
     C  N40              DO
     C     WSFNR         COMP      *ZEROS                                 90
     C   90              MOVE      'TRO0503'     MSGNR
     C   90              SETON                                        30
     C   30              GOTO      SLTB1
     C                   END
     C* TEST OM FLYSELSKAP ER REGISTRERT
     C  N40              DO
     C     KODFS2        CHAIN     KODTFS                             90
     C   90              MOVE      'TRO0502'     MSGNR
     C   90              SETON                                        30
     C   30              GOTO      SLTB1
     C                   END
     C*
     C   30              GOTO      SLTB1
     C*
     C     KODFCK        CHAIN     KODTFC                             33
      * ny og finnes gå ut
     C  N33
     CANN40              GOTO      SLTB1
      *
      * endring og ikke finnes slett gammel
      *
     c   40
     cAN 33              do
     c     gmkey         CHAIN     kodtFC                             34
     c  N34              delete    kodtFCR
     c                   end
     C                   EXSR      MOVWFS
     C  N33              UPDATE    KODTFCR
     C   33              WRITE     KODTFCR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVFSW
     C                   MOVE      ' '           WSNR
     C                   MOVEA     '0000'        *IN(87)
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     XDLT          COMP      '*DELETE'                              98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWFS        BEGSR
     C***********************************************
     C                   MOVE      XUNI          KFCUNI
     C                   MOVE      WSFNR         KFCFNR
     C                   MOVE      WSAVG         KFCAVG
     C                   MOVE      WSVKT         KFCVKT
     C                   MOVE      WSIO1         KFCIO1
     C                   MOVE      WSLK1         KFCLK1
     C                   MOVE      WSLK2         KFCLK2
     C                   MOVE      WSLK3         KFCLK3
     C                   MOVE      WSLK4         KFCLK4
     C                   MOVE      WSLK5         KFCLK5
     C                   MOVE      WSFRA         KFCFRA
     C                   MOVE      WSTIL         KFCTIL
     C                   MOVE      WSSTA         KFCSTA
     C                   MOVE      WSMIN         KFCMIN
     C                   MOVE      WSFTX         KFCFTX
N01  C                   MOVE      WSOTH         KFCOTH
N01  C     KFCOTH        IFEQ      *BLANKS
N01  C                   MOVE      'MY'          KFCOTH
N01  C                   ENDIF
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVFSW        BEGSR
     C***********************************************
     C                   MOVE      KFCFNR        WSFNR
     C                   MOVE      KFCAVG        WSAVG
     C                   MOVE      KFCVKT        WSVKT
     C                   MOVE      KFCIO1        WSIO1
     C                   MOVE      KFCLK1        WSLK1
     C                   MOVE      KFCLK2        WSLK2
     C                   MOVE      KFCLK3        WSLK3
     C                   MOVE      KFCLK4        WSLK4
     C                   MOVE      KFCLK5        WSLK5
     C                   MOVE      KFCFRA        WSFRA
     C                   MOVE      KFCTIL        WSTIL
     C                   MOVE      KFCMIN        WSMIN
     C                   MOVE      KFCSTA        WSSTA
     C                   MOVE      KFCFTX        WSFTX
N01  C                   MOVE      KFCOTH        WSOTH
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        KFCFNR
     C                   MOVE      *ZEROS        KFCAVG
     C                   MOVE      *BLANKS       KFCVKT
     C                   MOVE      *BLANKS       KFCIO1
     C                   MOVE      *BLANKS       KFCLK1
     C                   MOVE      *BLANKS       KFCLK2
     C                   MOVE      *BLANKS       KFCLK3
     C                   MOVE      *BLANKS       KFCLK4
     C                   MOVE      *BLANKS       KFCLK5
     C                   MOVE      *ZEROS        KFCFRA
     C                   MOVE      *ZEROS        KFCTIL
     C                   MOVE      *ZEROS        KFCMIN
     C                   MOVE      *BLANKS       KFCSTA
     C                   MOVE      *BLANKS       KFCFTX
N01  C                   MOVE      *BLANKS       KFCOTH
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSFNR
     C                   MOVE      *ZEROS        WSAVG
     C                   MOVE      *BLANKS       WSVKT
     C                   MOVE      *BLANKS       WSIO1
     C                   MOVE      *BLANKS       WSLK1
     C                   MOVE      *BLANKS       WSLK2
     C                   MOVE      *BLANKS       WSLK3
     C                   MOVE      *BLANKS       WSLK4
     C                   MOVE      *BLANKS       WSLK5
     C                   MOVE      *ZEROS        WSFRA
     C                   MOVE      *ZEROS        WSTIL
     C                   MOVE      *BLANKS       WSSTA
     C                   MOVE      *ZEROS        WSMIN
     C                   MOVE      *BLANKS       WSFTX
N01  C                   MOVE      *BLANKS       WSOTH
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODFS2        KLIST
     C                   KFLD                    YUNI
     C                   KFLD                    WSFNR
     C     KODFCK        KLIST
     C                   KFLD                    XUNI
     C                   KFLD                    WSFNR
     C                   KFLD                    WSFRA
     C     GMKEY         KLIST
     C                   KFLD                    XUNI
     C                   KFLD                    WSFNR
     C                   KFLD                    KFCFRA
     C     KODFC2        KLIST
     C                   KFLD                    XUNI
     C                   KFLD                    KFCFNR
     C                   KFLD                    KFCFRA
     C*
     c                   move      '  '          WSUNIK
     c                   exfmt     BILDE01
     c     *in03         ifeq      '1'
     c                   seton                                        lr
     c                   return
     c                   end
     C     wsunik        ifeq      *blanks
     c                   move      'FC'          WSUNIK
     c                   end
     C                   MOVE      wsunik        XUNI              2
     C*                  MOVE      'FC'          XUNI              2
     C                   MOVE      'FS'          YUNI              2
     C*
     C                   ENDSR
     C*
