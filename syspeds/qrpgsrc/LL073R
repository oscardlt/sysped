     FISTAL76   IF   E           K DISK
     FHBESL01   IF   E           K DISK
     FISTATOL3  UF   E           K DISK
     FDBESL01   IF   E           K DISK
     FLEVEL01   IF   E           K DISK
     FVAREL01   IF   E           K DISK
     FLAGEL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FLLBL1F    O    E             DISK
     FLLTARF    UF A E           K DISK
     FLLLSDF    IF   E           K DISK
     FLLVARE01  UF A E           K DISK
     FLLAGJF    O  A E             DISK
     FLLAGJL03  UF   E           K DISK
     F                                     RENAME(LLAGJR:LLAGJR3)
     FLLLISF    O  A E             DISK
     FLLMRT     UF A E           K DISK
     FLLMRF     O  A E             DISK
     FLLAGJD    O  A E             DISK
     FLLAGJD01  UF   E           K DISK
     F                                     RENAME(LLAGJDR:LLAGJDR1)
     FLLHEAD    UF A E           K DISK
     FLLLINE    UF A E           K DISK
     FLLTELL    UF A E             DISK
     D UJA             S              4    DIM(400)
     D UBN             S             14    DIM(100)
     D                 DS
     D XTXT01          C                   CONST('ALLOKERT P.L:')
     D XTXT02          C                   CONST('JUSTERING FRA L1')
     D                 DS
     D  VALLOK                 1     25
     D  VALSD                  1      5
     D  VALPOS                 6     25
     D                 DS
     D  LITXT                  1     35
     D  LITXTT1                1     35    DIM(35)
     D                 DS
     D  LITXTNY                1     35
     D  LITXTT2                1     35    DIM(35)
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
      *
     C                   EXSR      SRINIT
      *
     C                   Z-SUB     1             XENRGM            7 0
     C     1             DO        100           XN                5 0
     C     UBN(XN)       IFEQ      *ZEROS
     C                   LEAVE
     C                   END
     C                   MOVE      UJA(XN)       XAAR              4 0
     C                   MOVE      UBN(XN)       XBNR              7 0
     C                   MOVEL     UBN(XN)       XENR              7 0
     C                   IF        ((XENR = 0) OR (XENR <> XENRGM))
     C                   MOVE      *ZEROS        XLEVNR            8 0
     C                   END
     C                   IF        XENR = 0
     C                   Z-SUB     1             XENRGM            7 0
     C                   ELSE
     C                   MOVEL     UBN(XN)       XENRGM            7 0
     C                   END
      *
     C     IST03K        CHAIN     ISTATOL3                           33
     C                   MOVE      'P'           JOSTAT
     C                   MOVE      WDT           JOBDAT
     C                   MOVEL     WTM           JOBTID
     C                   UPDATE    ISTATOLR
      *
     C     DBE01K        CHAIN     HBESL01                            33
     C     DBE01K        SETLL     DBESL01
     C     DBE01K        READE     DBESL01                                33
     C     *IN33         DOWEQ     '0'
     C     XLEVNR        IFNE      LEVNR
     C     LEVERT        ANDNE     0
     C                   MOVE      LEVNR         XLEVNR            8 0
     C                   EXSR      SRHODE
     C                   END
     C                   EXSR      SRLIN
     C     DBE01K        READE     DBESL01                                33
     C  N33              ENDDO
      *
     C                   MOVE      'N'           XJUST             1
      *
     C                   ENDDO
      *
      *
     C     LHMN          IFNE      0
     C     LHMN          CHAIN     LLHEAD                             33
     C                   MOVE      XHANT0        LHANT0
     C                   MOVE      XHANTL        LHANTL
     C                   MOVE      XHWVEK        LHWVEK
     C                   MOVE      XHWKUB        LHWKUB
     C                   MOVE      XHTLIN        LHTLIN
     C                   UPDATE    LLHEADR
     C                   END
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *
      *////////////////////////////////////////////////////////////
      * SR for HODE                                        SRHODE /
      *////////////////////////////////////////////////////////////
     C     SRHODE        BEGSR
      *
     C*                    CLEARLLMRFR
      *
      * BEVAR VALKOD FRA ISTAF VED HENTING AV LEV.NAVN
     C                   MOVEL     VALKOD        XALKOD            3
     C     LEVE1K        CHAIN     LEVEL01                            33
     C                   MOVEL     XALKOD        VALKOD
      *
      * OPPRETT MOTTAKSRAPPORT
     C     LMRTK         CHAIN     LLMRT                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      MTNR          MRMRNR
     C                   ADD       1             MTNR
     C                   UPDATE    LLMRTR
     C                   ELSE
     C                   Z-ADD     1             MRMRNR
     C                   Z-ADD     2             MTNR
     C                   MOVE      XKJED         MTGRUP
     C                   MOVE      XPNR          MTKJED
     C                   WRITE     LLMRTR
     C                   END
      *
      * OPPRETT ARBEIDSFIL
     C                   MOVE      MRMRNR        BLMRNR
     C                   MOVE      UFIRMA        BLFIRM
     C                   MOVE      XBNR          BLBN
     C                   MOVE      LEVNR         BLLN
     C                   WRITE     LLBL1FR
      *
      * OPPDATER MOTTAKSRAPPORT
     C                   MOVE      'P'           MRST
     C                   MOVE      XKJED         MRGRUP
     C                   MOVE      XPNR          MRKJED
     C                   MOVE      LNAVN         MRLNA
     C                   MOVE      ADR1          MRLAD1
     C                   MOVE      ADR2          MRLAD2
     C                   MOVE      POSTNR        MRLPN
     C                   MOVEL     ADR3          MRLAD3
     C                   MOVE      JOUDAT        MRDT
     C                   MOVEL     JOUTID        MRTM
     C                   MOVE      *BLANKS       MRLREF
     C                   MOVEL     UFIRMA        MRLREF
     C                   CAT       UJA(XN):1     MRLREF
     C                   MOVEL     UBN(XN)       XAN07             7
     C                   CAT       XAN07:1       MRLREF
     C                   MOVE      *BLANKS       MRKREF
     C                   MOVE      *BLANKS       MRFT1
     C                   MOVE      *BLANKS       MRFT2
     C                   WRITE     LLMRFR
      *
      * OPPDATER LAGER
     C                   MOVE      XKJED         VAGRUP
     C                   MOVE      XPNR          VAKJED
     C                   MOVE      MRMRNR        VAMRNR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for LINJE                                        SRLIN /
      *////////////////////////////////////////////////////////////
     C     SRLIN         BEGSR
      *
      * VED IKKE-TOLLAGER (IKKE 21) PLUKK MED ENGANG
     C     LAGNR         IFNE      '21'
     C     UTLAGER       ANDNE     'J'
     C     LHMN          ANDEQ     0
     C                   MOVEL     VALKOD        XALKOD            3
     C                   EXSR      LAGPHD
     C                   MOVEL     XALKOD        VALKOD
     C                   END
      *
     C     VARE1K        CHAIN     VAREL01                            33
      * OPPDATER ARTIKKELREGISTER
     C     LVA1K         CHAIN     LLVARE01                           33
     C     *IN33         IFEQ      '1'
     C                   CLEAR                   LLVARER
     C                   MOVE      XKJED         LVGRUP
     C                   MOVE      XPNR          LVPNR
     C                   MOVE      VARENR        LVANR
     C                   MOVEL     VARTE1        LVVB
     C                   MOVE      'STK'         LVEH0
     C                   MOVEL     'STYKK'       LVEH0T
     C                   MOVE      'ESK'         LVEH1
     C                   MOVEL     'ESKE'        LVEH1T
     C                   MOVE      'KRT'         LVEH2
     C                   MOVEL     'KARTONG'     LVEH2T
     C                   MOVE      'PAL'         LVEH3
     C                   MOVEL     'PALL'        LVEH3T
     C                   MOVE      'STK'         LVMEH
     C                   Z-ADD     0             LVMIN
     C                   END
     C                   SELECT
     C     BVEKT         WHENLT    0.01
     C     LVVKT0        ANDEQ     0
     C                   Z-ADD     0.01          LVVKT0
     C     BVEKT         WHENGE    0.01
     C                   Z-ADD     BVEKT         LVVKT0
     C                   ENDSL
     C     VOLUM         DIV(H)    1000          LVKUB0
     C     LAGNR         IFEQ      '21'
     C     UTLAGER       OREQ      'J'
     C                   ADD       LEVERT        LVSALD
     C                   END
     C     *IN33         IFEQ      '1'
     C                   WRITE     LLVARER
     C                   ELSE
     C                   UPDATE    LLVARER
     C                   END
      * OPPDATER TARIFF
     C     LTARK         CHAIN     LLTARF                             33
     C     *IN33         IFEQ      '1'
     C                   CLEAR                   LLTARFR
     C                   MOVE      XKJED         LTGRUP
     C                   MOVE      XPNR          LTPNR
     C                   MOVE      VARENR        LTANR
     C                   MOVE      'NO'          LTLK
     C                   MOVEL     TTARIF        LTTANR
     C                   MOVEL     LAND          LTLKOP
     C                   WRITE     LLTARFR
     C                   ELSE
     C                   MOVEL     TTARIF        XTARIF            8
     C     XTARIF        IFNE      *BLANKS
     C     XTARIF        ANDNE     *ZEROS
     C                   MOVEL     TTARIF        LTTANR
     C                   END
     C     LAND          IFNE      *BLANKS
     C                   MOVEL     LAND          LTLKOP
     C                   END
     C                   UPDATE    LLTARFR
     C                   END
      *
      * OPPDATER LAGERINNHOLD
     C     3             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   MOVE      LLREC1        VAMN
     C                   MOVE      UGRPA         VAGRUP
     C                   MOVE      UPNRA         VAKJED
      *
     C                   MOVE      JOUDAT        VADATO
     C                   MOVE      VARENR        VAANR
     C                   MOVE      LEVVVA        VAANRL
     C                   Z-ADD     LEVERT        VAOANT
     C     LAGNR         IFEQ      '21'
     C     UTLAGER       OREQ      'J'
     C                   Z-ADD     LEVERT        VASALD
     C                   Z-ADD     LEVERT        VASAE0
     C                   ELSE
     C                   Z-ADD     0             VASALD
     C                   Z-ADD     0             VASAE0
     C                   END
     C                   MOVEL     VARTE1        VAVARE
     C     VALKOD        IFEQ      *BLANKS
     C                   MOVEL     'NOK'         VAFAR
     C                   ELSE
     C                   MOVEL     VALKOD        VAFAR
     C                   END
     C                   Z-ADD     KOSTPR        VAVERD
     C                   MOVE      *BLANKS       VALLOK
     C                   MOVEL     LAGNR         VALSD
     C     LAGE1K        CHAIN     LAGEL01                            33
     C     *IN33         IFEQ      '0'
     C                   MOVEL     LPLASS        VALPOS
     C                   END
     C     VALPOS        IFEQ      *BLANKS
     C                   MOVEL     '.'           VALPOS
     C                   END
     C     LLSDK         CHAIN     LLLSDF                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      LSLLT         VALLT
     C                   ELSE
     C                   MOVE      *BLANKS       VALLT
     C                   END
     C                   MOVEL     'FLATFIL'     VABUSE
     C                   MOVE      WDT           VABDAT
     C                   MOVE      WTM           VABKL
     C                   MOVE      LVVKT0        VAVKT0
     C                   MOVE      LVKUB0        VAKUB0
     C                   MOVE      LVVKF0        VAVKF0
     C                   MOVE      LVKUF0        VAKUF0
     C                   MOVE      LVVKT1        VAVKT1
     C                   MOVE      LVKUB1        VAKUB1
     C                   MOVE      LVVKF1        VAVKF1
     C                   MOVE      LVKUF1        VAKUF1
     C                   MOVE      LVAN01        VAAN01
     C                   MOVE      LVVKT2        VAVKT2
     C                   MOVE      LVKUB2        VAKUB2
     C                   MOVE      LVVKF2        VAVKF2
     C                   MOVE      LVKUF2        VAKUF2
     C                   MOVE      LVAN02        VAAN02
     C                   MOVE      LVAN12        VAAN12
     C                   MOVE      LVVKT3        VAVKT3
     C                   MOVE      LVKUB3        VAKUB3
     C                   MOVE      LVVKF3        VAVKF3
     C                   MOVE      LVKUF3        VAKUF3
     C                   MOVE      LVAN03        VAAN03
     C                   MOVE      LVAN23        VAAN23
     C                   MOVE      LVMIN         VAMIN
     C     VAOANT        MULT      VAVKT0        VAVKTS
     C     VAVKTS        IFLT      0.5
     C                   Z-ADD     0.5           VAVKTS
     C                   END
     C     VAOANT        MULT      VAKUB0        VAKUBS
      *
     C                   WRITE     LLAGJR
      *
     C                   MOVE      VAMN          LIMN
     C                   Z-ADD     1             LILN
     C                   MOVE      'FAK'         LIKD
     C                   MOVEL     KRNR          LITXT
     C     1             DO        35            XN1               3 0
     C                   IF        LITXTT1(XN1) <> *BLANKS
     C                   MOVE      *ZEROS        XN3               3 0
     C                   MOVE      *BLANKS       LITXTNY
     C     XN1           DO        35            XN2               3 0
     C                   ADD       1             XN3
     C                   MOVE      LITXTT1(XN2)  LITXTT2(XN3)
     C                   ENDDO
     C                   EVAL      LITXT = LITXTNY
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   WRITE     LLLISFR
      *
      * VED IKKE-TOLLAGER (IKKE 21) PLUKK MED ENGANG
     C     LAGNR         IFNE      '21'
     C     UTLAGER       ANDNE     'J'
     C                   EXSR      LAGPLN
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for JUSTERING                                   SRJUST /
      *////////////////////////////////////////////////////////////
     C     SRJUST        BEGSR
     C     IST76K        SETLL     ISTAL76
     C     IST76K        READE     ISTAL76                                33
     C     *IN33         DOWEQ     '0'
      *
     C     LAGL3K        SETLL     LLAGJL03
     C     LAGL3K        READE     LLAGJL03                               34
     C     *IN34         DOWEQ     '0'
     C     VAMRNR        IFEQ      MRMRNR
     C                   LEAVE
     C                   END
     C                   UPDATE    LLAGJR3
     C     LAGL3K        READE     LLAGJL03                               34
     C  N34              ENDDO
     C     *IN34         IFEQ      '1'
     C                   MOVE      'J'           XJUST2            1
     C                   MOVE      LEVERT        XLEVER           11 3
     C                   MOVE      *ZEROS        LEVERT
     C                   MOVE      MRMRNR        VAMRNR
     C                   EXSR      SRLIN
     C                   MOVE      XLEVER        LEVERT
     C     LAGL3K        SETLL     LLAGJL03
     C     LAGL3K        READE     LLAGJL03                               34
     C                   ELSE
     C                   MOVE      'N'           XJUST2
     C                   END
      *
     C     *IN34         DOWEQ     '0'
     C     VAMRNR        IFEQ      MRMRNR
     C     VASALD        IFEQ      0
      * INN TIL NORGE (ALLE ER PLUKKET)
     C                   UPDATE    LLAGJR3
     C                   EXSR      SRJUL2
     C     XNESTE        IFEQ      'J'
     C                   GOTO      XLESN
     C                   END
     C                   ELSE
      * INN PÅ LAGER (INGEN ER PLUKKET)
     C                   ADD       LEVERT        VASALD
     C                   UPDATE    LLAGJR3
     C                   EXSR      SRJUL1
     C                   END
     C                   LEAVE
     C                   END
     C                   UPDATE    LLAGJR3
     C     XLESN         TAG
     C     LAGL3K        READE     LLAGJL03                               34
     C  N34              ENDDO
      *
     C     IST76K        READE     ISTAL76                                33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for JUSTERING AV LINJE                          SRJUL1 /
      *////////////////////////////////////////////////////////////
     C     SRJUL1        BEGSR
      *
     C                   MOVE      UGRPA         VDGRUP
     C                   MOVE      UPNRA         VDKJED
     C                   MOVE      VAMN          VDMN
     C                   Z-ADD     1             VDVN
     C                   Z-SUB     LEVERT        VDLANT
     C                   Z-SUB     LEVERT        VDSAE0
     C                   MOVE      WDT           VDLDAT
     C                   MOVE      *BLANKS       VDTEXT
     C                   MOVEL     XTXT02        VDTEXT
     C                   MOVE      XJNR          XJNRA             5
     C                   CAT       XJNRA:1       VDTEXT
     C                   MOVE      'OSJUS'       VDLTYP
     C                   MOVEL     'EDI'         VDBUSE
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   WRITE     LLAGJDR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for JUSTERING AV LINJE 2                        SRJUL2 /
      *////////////////////////////////////////////////////////////
     C     SRJUL2        BEGSR
      *
     C                   Z-ADD     1             VDVN
     C     LAGD1K        CHAIN     LLAGJD01                           35
     C     VDVNL         IFEQ      0
     C                   MOVE      'J'           XNESTE            1
     C                   GOTO      SLJUL2
     C                   ELSE
     C                   MOVE      'N'           XNESTE
     C                   END
     C     XJUST2        IFEQ      'N'
     C                   EXSR      SRJULP
     C                   END
     C                   MULT      -1            LEVERT
     C     VDLANT        IFEQ      LEVERT
      * ENDRE PLUKKLINJE TIL JUSTERINGSLINJE
     C                   MOVE      *BLANKS       VDTEXT
     C                   MOVEL     XTXT02        VDTEXT
     C                   MOVE      XJNR          XJNRA             5
     C                   CAT       XJNRA:1       VDTEXT
     C                   MOVE      'OSJUS'       VDLTYP
     C                   MOVE      WDT           VDLDAT
     C                   MOVE      VDMNL         XDMNL             7 0
     C                   MOVE      VDVNL         XDVNL             3 0
     C                   MOVE      *ZEROS        VDMNL
     C                   MOVE      *ZEROS        VDVNL
     C                   UPDATE    LLAGJDR1
     C                   MOVE      XDMNL         VDMNL
     C                   MOVE      XDVNL         VDVNL
     C                   ELSE
      * TREKK FRA JUSTERING FRA PLUKKLISTE
     C                   SUB       LEVERT        VDLANT
     C                   SUB       LEVERT        VDSAE0
     C                   UPDATE    LLAGJDR1
      * LAGE EN LINJE FOR JUSTERING
     C                   ADD       1             VDVN
     C                   Z-ADD     LEVERT        VDLANT
     C                   Z-ADD     LEVERT        VDSAE0
     C                   MOVE      *BLANKS       VDTEXT
     C                   MOVEL     XTXT02        VDTEXT
     C                   MOVE      XJNR          XJNRA             5
     C                   CAT       XJNRA:1       VDTEXT
     C                   MOVE      'OSJUS'       VDLTYP
     C                   MOVE      WDT           VDLDAT
     C                   MOVE      VDMNL         XDMNL             7 0
     C                   MOVE      VDVNL         XDVNL             3 0
     C                   MOVE      *ZEROS        VDMNL
     C                   MOVE      *ZEROS        VDVNL
     C                   WRITE     LLAGJDR
     C                   MOVE      XDMNL         VDMNL
     C                   MOVE      XDVNL         VDVNL
     C                   END
      *
     C     SLJUL2        TAG
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for JUSTERING AV PLUKKLISTE                     SRJULP /
      *////////////////////////////////////////////////////////////
     C     SRJULP        BEGSR
      *
     C     LLINK         CHAIN     LLLINE                             35
      * TREKK ANTALL/VEKT/KUBIKK FRA PLUKKLISTE
     C                   SUB       LLANT0        XHANT0
     C                   SUB       LLANTL        XHANTL
     C                   SUB       LLWVEK        XHWVEK
     C                   SUB       LLWKUB        XHWKUB
      * BEREGN NY ANTALL/VEKT/KUBIKK PÅ LINJE
     C                   ADD       LEVERT        LLWNT0
     C                   ADD       LEVERT        LLWNTL
     C     LLWNT0        IFEQ      0
     C                   MOVE      *ZEROS        LLWVEK
     C                   MOVE      *ZEROS        LLWKUB
     C                   MOVE      *BLANKS       LLLOK
     C                   MOVE      *ZEROS        LLNTLK
     C                   SUB       1             XHTLIN
     C                   ELSE
     C     LLWNT0        MULT      VAVKT0        LLWVEK
     C     LLWVEK        IFLT      0.5
     C                   Z-ADD     0.5           LLWVEK
     C                   END
     C     LLWNT0        MULT      VAKUB0        LLWKUB
     C                   END
     C                   UPDATE    LLLINER
      *
      * BEREGN NY ANTALL/VEKT/KUBIKK PÅ PLUKKLISTE
     C                   ADD       LLWNT0        XHANT0
     C                   ADD       LLWNTL        XHANTL
     C                   ADD       LLWVEK        XHWVEK
     C                   ADD       LLWKUB        XHWKUB
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for PLUKKLISTE HODE                             LAGPHD /
      *////////////////////////////////////////////////////////////
     C     LAGPHD        BEGSR
      *
     C     1             CHAIN     LLTELL                             33                    1
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   MOVE      LLREC1        LHMN
      *
     C                   MOVE      'E'           LHOPR
     C                   MOVE      UGRPA         LHGRP
     C                   MOVE      UPNRA         LHPNR
      *
     C     CUND1K        CHAIN     CUNDL01                            33                    1
     C                   MOVE      KNAVN         LHBNA
     C                   MOVE      ADR1          LHBA1
     C                   MOVE      ADR2          LHBA2
     C                   MOVE      POSTNR        LHBPN
     C                   MOVEL     ADR3          LHBA3
     C                   MOVE      SYLAND        LHBLK
     C                   MOVEL     SYPOGE        LHBPU
      *
     C                   MOVE      WDT           LHTDT
     C                   MOVEL     'TRANSITT'    LHPAK
     C                   MOVEL     UFIRMA        LHORDR
     C                   MOVEL     UBN(XN)       XAN07             7
     C                   CAT       XAN07:1       LHORDR
     C                   MOVE      WDT           LHMDT
      *
     C     2             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   Z-ADD     LLREC1        LHALN
     C                   MOVE      LHALN         UALN
     C                   MOVE      'K'           LHST1
     C                   MOVE      'J'           LHST2
     C                   MOVE      'P'           LHST3
      *
     C                   WRITE     LLHEADR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for PLUKKLISTE LINJE                            LAGPLN /
      *////////////////////////////////////////////////////////////
     C     LAGPLN        BEGSR
      *
     C                   MOVE      UGRPA         VDGRUP
     C                   MOVE      UPNRA         VDKJED
     C                   MOVE      VAMN          VDMN
     C                   Z-ADD     1             VDVN
     C                   MOVE      LHMN          VDMNL
     C                   ADD       1             XLLVN
     C                   MOVE      XLLVN         VDVNL
     C                   MOVE      VAOANT        VDLANT
     C                   MOVE      VAOANT        VDLANT
     C                   MOVE      VAOANT        VDSAE0
     C                   MOVE      99999999      VDLDAT
     C                   MOVE      *BLANKS       VDTEXT
     C                   MOVEL     XTXT01        VDTEXT
     C                   CAT       LHPAK:1       VDTEXT
     C                   MOVE      LHALN         VDTEXT
     C                   MOVE      'PLIST'       VDLTYP
     C                   MOVEL     'EDI'         VDBUSE
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   WRITE     LLAGJDR
      *
     C                   MOVEL     LHMN          LLMN
     C                   MOVE      XLLVN         LLVN
     C                   MOVEL     VAANR         LLANR
     C                   MOVE      VAVARE        LLVB
     C     XJUST         IFEQ      'J'
     C                   Z-ADD     XLEVER        LLANT0
     C                   ELSE
     C                   MOVE      VAOANT        LLANT0
     C                   END
     C                   MOVE      *ZEROS        LLANT3
     C                   MOVE      *ZEROS        LLANT2
     C                   MOVE      *ZEROS        LLANT1
     C     XJUST         IFEQ      'J'
     C                   Z-ADD     XLEVER        LLANTL
     C                   ELSE
     C                   MOVE      VAOANT        LLANTL
     C                   END
     C                   Z-ADD     VAVKTS        LLVEK
     C                   Z-ADD     VAKUBS        LLKUB
     C                   MOVE      LLANT0        LLWNT0
     C                   MOVE      LLANTL        LLWNTL
     C                   MOVE      LLVEK         LLWVEK
     C                   MOVE      LLKUB         LLWKUB
     C                   MOVE      VALLOK        LLLOK
     C                   Z-ADD     1             LLNTLK
     C                   MOVE      VAVERD        LLPRI
      *
     C                   MOVE      'ESK'         LLAEH1
     C                   MOVE      'KRT'         LLAEH2
     C                   MOVE      'PAL'         LLAEH3
      *
     C                   WRITE     LLLINER
      *
     C                   ADD       LLANT0        XHANT0
     C                   ADD       LLANTL        XHANTL
     C                   ADD       LLWVEK        XHWVEK
     C                   ADD       LLWKUB        XHWKUB
     C                   ADD       1             XHTLIN
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
     C     *ENTRY        PLIST
     C                   PARM                    UGRPA             8
     C                   PARM                    UPNRA             8
     C                   PARM                    UFIRMA            2
     C                   PARM                    UJA
     C                   PARM                    UBN
     C                   PARM                    UALN              5
     C                   PARM                    UTLAGER           1
      *
     C     *LIKE         DEFINE    LLVN          XLLVN
     C     *LIKE         DEFINE    LHANT0        XHANT0
     C     *LIKE         DEFINE    LHANTL        XHANTL
     C     *LIKE         DEFINE    LHWVEK        XHWVEK
     C     *LIKE         DEFINE    LHWKUB        XHWKUB
     C     *LIKE         DEFINE    LHTLIN        XHTLIN
      * DEF AV KEYS
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    XPNR
     C     LVA0K1        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    XPNR
     C     LMRTK         KLIST
     C                   KFLD                    XKJED
     C                   KFLD                    XPNR
     C     LVA1K         KLIST
     C                   KFLD                    XKJED
     C                   KFLD                    XPNR
     C                   KFLD                    VARENR
     C     LLSDK         KLIST
     C                   KFLD                    VALSD
     C     LTARK         KLIST
     C                   KFLD                    XKJED
     C                   KFLD                    XPNR
     C                   KFLD                    VARENR
     C                   KFLD                    XLK
     C     LAGL3K        KLIST
     C                   KFLD                    XKJED
     C                   KFLD                    XPNR
     C                   KFLD                    VARENR
     C                   KFLD                    VADATO
     C     LAGD1K        KLIST
     C                   KFLD                    XKJED
     C                   KFLD                    XPNR
     C                   KFLD                    VAMN
     C                   KFLD                    VDVN
     C     LLINK         KLIST
     C                   KFLD                    VDMNL
     C                   KFLD                    VDVNL
     C     IST76K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    XAAR
     C                   KFLD                    XJNR              5 0
     C     IST03K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    XAAR
     C                   KFLD                    XBNR
     C     DBE01K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    BTYPE
     C                   KFLD                    XAAR
     C                   KFLD                    XBNR
     C                   MOVE      'B'           BTYPE
     C     LEVE1K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    LEVNR
     C     VARE1K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    VARENR
     C     LAGE1K        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    VARENR
     C                   KFLD                    LAGNR
      *
     C                   MOVE      UGRPA         XKJED             8 0
     C                   MOVE      UPNRA         XPNR              8 0
     C                   MOVE      'NO'          XLK               2
     C                   MOVE      'B'           BTYPE
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C     1             CHAIN(N)  LLTELL                             33
     C                   IF        *IN33
     C                   Z-ADD     1001          LLREC1
     C                   WRITE     LLTELLR
     C                   END
     C     2             CHAIN(N)  LLTELL                             33
     C                   IF        *IN33
     C                   Z-ADD     1001          LLREC1
     C                   WRITE     LLTELLR
     C                   END
     C     3             CHAIN(N)  LLTELL                             33
     C                   IF        *IN33
     C                   Z-ADD     1001          LLREC1
     C                   WRITE     LLTELLR
     C                   END
      *
     C                   ENDSR
      *
