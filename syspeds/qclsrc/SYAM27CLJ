             PGM        PARM(&KOPI &MSGTXT)
             DCL        VAR(&KOPI) TYPE(*CHAR) LEN(1) /* Hvis '1', +
                          skal skrive ut kopifaktura. */
             DCL        VAR(&MSGTXT) TYPE(*CHAR) LEN(12) /* +
                          Meldingtekst */
             DCL        VAR(&PRT1) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til faktura              */
             DCL        VAR(&PT1) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til spesifikasjon           */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7) /* UNIKKODE +
                          TIL REGISTER SORT. */
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
    /* */
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
    /* ORDNER PRINTERFILER */
             RTVDTAARA  DTAARA(*LDA (681 10)) RTNVAR(&PRT1)
             RTVDTAARA  DTAARA(*LDA (691 10)) RTNVAR(&PT1)
             OVRPRTF    FILE(SYAM27PF) DEVTYPE(*SCS) PAGESIZE(72 80) +
                          CPI(10) OVRFLW(72) OUTQ(&PRT1) FORMTYPE(&PT1)

    /* VED ORIGINAL BENYTTES WORKFILE (KOPI=LES FRA FAIN) */
             IF         COND(&KOPI *EQ '0') +
             THEN(DO)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &TIME)

             RMVM       FILE(AMWSAML2) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(AMWSAML) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(AMWSAML) MBR(&UNIK)
             ADDLFM     FILE(AMWSAML2) MBR(&UNIK) DTAMBRS((AMWSAML +
                          (&UNIK)))
             OVRDBF     FILE(AMWSAML) TOFILE(AMWSAML) MBR(&UNIK)
             OVRDBF     FILE(AMWSAML2) TOFILE(AMWSAML2) MBR(&UNIK)
             ENDDO

    /* SKRIVE UT FAKTURA */
             CALL       PGM(SYAM27) PARM(&PRANT &KOPI)

             IF         COND(&KOPI *EQ '0') +
             THEN(DO)
             RMVM       FILE(AMWSAML2) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(AMWSAML) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ENDDO

             CHGVAR     VAR(&MSGTXT) VALUE(&PRANT *CAT &PRT1)

             ENDPGM
