      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE16R MODULE(*LIBL/TJGE16R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 avd-kode+LK medsendes ..
********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
     FKODTALF   IF   E           K DISK    USROPN
     F                                     RENAME(KOAR:KOARA)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WIE             S              1
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D WA              S             15
     D WNAMN           S             33

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬ie¬ : ¬'+%trim(WIE)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(WAVD:'Z'))+'¬, '
        +'¬namn¬ : ¬'+%trim(WNAMN)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"avdelningar" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta avdelningar
      *
     c     Error         ifeq      *blanks
     C     *LOVAL        SETLL     KODTA
     C     WNAMN         SETLL     KODTALF
     C     WNAMN         IFEQ      *BLANKS
     C                   READ      KODTA                                  55
     C                   ELSE
     C                   READ      KODTALF                                55
     C                   ENDIF
     C     *IN55         DOWEQ     *OFF


     C                   IF        WAVD > 0
     C     KOAAVD        CABLT     WAVD          NEXT
     C                   ENDIF
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(KOAAVD:'Z'))+'¬, '
          +'¬namn¬ : ¬'+%trim(KOANVN)+'¬,'
          +'¬koaIE¬ : ¬'+%trim(KOAIE)+'¬,'
          +'¬koaLK¬ : ¬'+%trim(KOALK)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C     NEXT          TAG

     C     WNAMN         IFEQ      *BLANKS
     C                   READ      KODTA                                  55
     C                   ELSE
     C                   READ      KODTALF                                55
     C                   ENDIF
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KODTA
     C  N50              CLOSE     KODTALF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WIE = zhbgetvar('IE')
     C                   EVAL      WA      = zhbgetvar('AVD')
     C                   EVAL      WAVD    = c2n2(WA)
     C                   EVAL      WNAMN   = zhbgetvar('NAMN')
     C
     c                   endsr
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     KODTAKEY      KLIST
     C                   KFLD                    WKOAUNI
     C                   KFLD                    WAVD
     C     *LIKE         DEFINE    KOAUNI        WKOAUNI
     C     *LIKE         DEFINE    KOAAVD        WAVD
     C                   EVAL      WKOAUNI = 'A'

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      KODTA
     C  N50              OPEN      KODTALF
     C                   IF        *IN50=*OFF
     C                   IF        WAVD > 0
     C*    KODTAKEY      CHAIN     KODTA                              50
     C*  50              eval      Error = 'Department not found'
     C                   ENDIF
     C                   ENDIF
     c                   endsr
