      *********************************************************************
CRTPG+*  CRTPGM SYSPED/TSVI019R MODULE(*LIBL/TSVI019R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVIH      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D SVIH_SYAVA      S              4
     D SVIH_SYAV       S              4  0
     D SVIH_SYOPA      S              7
     D SVIH_SYOP       S              7  0
     D SVIH_MTYP       S              3
     D JSONstring      S          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬svih_syav¬ : ¬'+%trim(%editc(SVIH_SYAV:'Z'))+'¬, '
        +'¬svih_syop¬ : ¬'+%trim(%editc(SVIH_SYOP:'Z'))+'¬, '
        +'¬svih_mtyp¬ : ¬'+%trim(SVIH_MTYP)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"mTypUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVIH

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      SVIH_SYAVA = zhbgetvar('AVD')
     C                   EVAL      SVIH_SYAV  = c2n2(SVIH_SYAVA)
     C                   EVAL      SVIH_SYOPA = zhbgetvar('OPD')
     C                   EVAL      SVIH_SYOP  = c2n2(SVIH_SYOPA)
     C                   EVAL      SVIH_MTYP=   zhbgetvar('SVIH_MTYP')
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SVIHKey       klist
     C                   kfld                    SVIH_SYAV
     C                   kfld                    SVIH_SYOP

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVIH

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hæmta ærende
     C     SVIHKey       CHAIN     SVIH                               55
      * Update
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XSVIH_SYST=*BLANKS
     C                   EVAL      XSVIH_MTYP=SVIH_MTYP
     C                   UPDATE    SVIHR
     C                   ENDSR
