      *
      ***********************************************************
      *** PROGRAM SKAL REGISTRERE KURERMANIFEST DEFAULT (HODE)***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-02 06-08 10 11 13-26
      *** LEDIGE INDIKATORER 27-29 31 32 34-47 50-79          ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADKMDEF  UF A E           K DISK
     FTELL      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FTRAN      IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FTRKODL01  IF   E           K DISK
     FSAD119D   CF   E             WORKSTN INFDS(FBAREA)
     D                SDS
     D  UUSR                 254    263
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSATDDD                1      2  0
     D  WSATDDM                3      4  0
     D  WSATDDA                5      6  0
     D  WSATDD                 1      6  0
     D                 DS
     D  KMATDDC                1      2  0
     D  KMATDDA                3      4  0
     D  KMATDDM                5      6  0
     D  KMATDDD                7      8  0
     D  KMATDD                 1      8  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRA                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  KMDTRC                 1      2  0
     D  KMDTRA                 3      4  0
     D  KMDTRM                 5      6  0
     D  KMDTRD                 7      8  0
     D  KMDTR                  1      8  0
     D                 DS
     D  WSATDTH                1      2  0
     D  WSATDTM                3      4  0
     D  WSATDTS                5      6  0
     D  WSATDT                 1      6  0
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
     D                 DS
     D  POSTNR                 1      4  0
     D  POSTNR_A               1      4
     D                 DS
     D  WSTM                   1      2  0
     D  WSTM_A                 1      2
      *
      *  Prototype for 'SOEKKUN'
     d SOEKKUN         pr                  extpgm('SOEKKUN')
     d wsknd_                              like(wsknd)
     d fifirm_                             like(fifirm)
     d wsnad_                              like(wsnad)
      *  Prototype for 'SYFI10L'
     d SYFI10L         pr                  extpgm('SYFI10L')
     d wslkd_                              like(wslkd)
     d usped_                              like(usped)
      *  Prototype for 'SYFAKDA'
     d SYFAKDA         pr                  extpgm('SYFAKDA')
     d kftyp_                              like(kftyp)
     d uupdat_                             like(uupdat)
     d xkfkod_                             like(xkfkod)
      *  Prototype for 'TR002R'
     d TR002R          pr                  extpgm('TR002R')
     d uunik_                              like(uunik)
     d ukode_                              like(ukode)
     d utype2_                             like(utype2)
      *  Prototype for 'SYTR04A'
     d SYTR04A         pr                  extpgm('SYTR04R')
     d wsknc_                              like(wsknc)
      *  Prototype for 'TESTDATO'
     d TESTDATO        pr                  extpgm('TESTDATO')
     d xdg_                                like(XDG)
     d xmn_                                like(XMN)
     d xar_                                like(XAR)
     d flagg_                              like(FLAGG)
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for SAD119R
     d sad119r         pr
     d uavd_                               like(uavd)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  *ENTRY Interface for Main Procedure
     d sad119r         pi
     d uavd                           4  0
     d utype                          3a
     d uflagg                         1  0
      *
     d uunik           s                   like(tkunik)
     d ukode           s                   like(tkkode)
     d wdt             s              8a
     d wtm             s              6a
     d usped           s              1a
     d uupdat          s              1a
     d utype2          s              3a
     d flagg           s              3a
     d xkfkod          s                   like(kfkod)
     d xdg             s              2  0
     d xmn             s              2  0
     d xar             s              2  0
     d i               s              3s 0
      *
      /free

         exsr init;

         dow *in03 = *off and *in12 = *off;
           exfmt bilde01;
           //*in(80) = '00000000000000000000';
           for i = 80 to 99;
             *in(i) = *off;
           endfor;
           *in30 = *off;

           select;
           when *in04;
             exsr soekdta;
           when *in12;
           when *in03 = *off;
             if *in49;
               leave;
             else;
               exsr testb1;
               if *in30 = *off;
                 exsr opdavd;
                 leave;
               endif;
             endif;
           endsl;

         enddo;

         select;
         when *in03;
           uflagg = 3;
         when *in12;
           uflagg = 1;
         other;
           uflagg = 0;
         endsl;

         *inlr = *on;
         return;

      //*********************************************
       begsr soekdta;

         linnbr = wscloc / 256;
         posnbr = wscloc - (%int(linnbr) * 256);

         select;
         when linnbr = 4 and posnbr >= 15 and posnbr <= 22;
         // SØK DEKLARANT
           soekkun (wsknd: fifirm: wsnad);
           if wsknd <> 0;
             exsr testd;
           endif;
           linnbr = 7;
           posnbr = 15;
         when linnbr = 6 and posnbr >= 91 and posnbr <= 92;
         // SØK DEKLARANT LANDKODE
           syfi10l (wslkd: usped);
           linnbr = 7;
           posnbr = 15;
         when linnbr = 9 and posnbr >= 15 and posnbr <= 16;
         // SØK modell av transport
           kftyp = 'SADKMMODTR';
           uupdat = 'N';
           xkfkod = *blanks;
           syfakda (kftyp: uupdat: xkfkod);
           wstm_a = xkfkod;
           posnbr = 30;
         when linnbr = 10 and posnbr >= 15 and posnbr <= 22;
         // SØK tollsted
           ukode = wstd1;
           tr002r (uunik: ukode: utype2);
           wstd1 = ukode;
           posnbr = 38;
         when linnbr = 10 and posnbr >= 38 and posnbr <= 45;
         // SØK tollsted
           ukode = wstda;
           tr002r (uunik: ukode: utype2);
           wstda = ukode;
           linnbr = 12;
           posnbr = 15;
         when linnbr = 12 and posnbr >= 15 and posnbr <= 22;
         // SØK transportør
           sytr04a (wsknc);
           if wsknc <> 0;
             exsr testc;
           endif;
           linnbr = 15;
           posnbr = 15;
         when linnbr = 14 and posnbr >= 91 and posnbr <= 92;
         // SØK TRANSPORTØR LANDKODE
           syfi10l (wslkc: usped);
           linnbr = 15;
           posnbr = 15;
         other;
           msgnr = 'DIV1037';
           *in30 = *on;
         endsl;

       endsr;

      //*********************************************
       begsr testb1;

      //*** TEST AVDELING
       if *in48 = *off;
         chain(n) wsavd tell;
         if not %found;
           *in99 = *on;
         endif;
       endif;

      //*** TEST DEKLARANT
       if wsknd <> 0;
         exsr testd;
       endif;

       if wsrgd = *blanks;
         *in97 = *on;
       endif;
       if wsnad = *blanks;
         *in96 = *on;
       endif;
       if wsadd1 = *blanks;
         *in95 = *on;
       endif;
       if wsadd3 = *blanks;
         *in94 = *on;
       endif;

       if wslkd <> *blanks;
         chain ('S2 ': wslkd) kodts2;
         if not %found;
           *in93 = *on;
         endif;
       endif;

       if wscmd <> *blanks and wscmdt = *blanks;
         *in92 = *on;
       endif;

       if wstm <> 0;
         kfkod = wstm_a;
         chain ('SADKMMODTR': kfkod) kofast;
         if not %found;
           *in91 = *on;
         endif;
       endif;

       if wsatdd <>  0;
         xar = wsatdda;
         xmn = wsatddm;
         xdg = wsatddd;
         testdato (xdg: xmn: xar: flagg);
         if flagg = '1';
           *in90 = *on;
         endif;
       endif;

       if wsatdth > 24 or wsatdts >= 60 or wsatdtm >= 60;
         *in89 = *on;
       endif;

       if wstd1 <> *blanks;
         ukode = wstd1;
         chain (uunik: ukode) trkodl01;
         if not %found;
           *in88 = *on;
         endif;
       endif;

       if wstda <> *blanks;
         ukode = wstda;
         chain (uunik: ukode) trkodl01;
         if not %found;
           *in87 = *on;
         endif;
       endif;

      //*** TEST TRANSPORTØR
       if wsknc <> 0;
         exsr testc;
       endif;

       if *in33 and *in33 = *off;
         if wsrgc = *blanks;
           *in85 = *on;
         endif;
         if wsnac = *blanks;
           *in84 = *on;
         endif;
         if wsadc1 = *blanks;
           *in83 = *on;
         endif;
         if wsadc3 = *blanks;
           *in82 = *on;
         endif;
         chain ('S2 ': wslkc) kodts2;
         if not %found;
           *in81 = *on;
         endif;
       endif;

       if wscmc = *blanks and wscmct <> *blanks;
         *in80 = *on;
       endif;

       exsr hmsg;

       endsr;

      //***********************************************
       begsr hmsg;

       select;
       when *in99;
         msgnr = 'SPA0001';
         *in30 = *on;
         linnbr = 2;
         posnbr = 7;
       when *in98;
         msgnr = 'YBC2037';
         *in30 = *on;
         linnbr = 4;
         posnbr = 15;
       when *in97;
         msgnr = 'YBC2038';
         *in30 = *on;
         linnbr = 4;
         posnbr = 33;
       when *in96;
         msgnr = 'YBC2039';
         *in30 = *on;
         linnbr = 5;
         posnbr = 15;
       when *in95;
         msgnr = 'YBC2040';
         *in30 = *on;
         linnbr = 5;
         posnbr = 46;
       when *in94;
         msgnr = 'YBC2041';
         *in30 = *on;
         linnbr = 6;
         posnbr = 56;
       when *in93;
         msgnr = 'YBC2042';
         *in30 = *on;
         linnbr = 6;
         posnbr = 91;
       when *in92;
         msgnr = 'YBC2043';
         *in30 = *on;
         linnbr = 7;
         posnbr = 72;
       when *in91;
         msgnr = 'SPT0030';
         *in30 = *on;
         linnbr = 9;
         posnbr = 15;
       when *in90;
         msgnr = 'YBC2044';
         *in30 = *on;
         linnbr = 9;
         posnbr = 72;
       when *in89;
         msgnr = 'YBC2045';
         *in30 = *on;
         linnbr = 9;
         posnbr = 85;
       when *in88;
         msgnr = 'YBC1768';
         *in30 = *on;
         linnbr = 10;
         posnbr = 15;
       when *in87;
         msgnr = 'YBC1768';
         *in30 = *on;
         linnbr = 10;
         posnbr = 15;
       when *in86;
         msgnr = 'YBC1768';
         *in30 = *on;
         linnbr = 10;
         posnbr = 38;
       when *in85;
         msgnr = 'YBC2047';
         *in30 = *on;
         linnbr = 12;
         posnbr = 33;
       when *in84;
         msgnr = 'YBC2048';
         *in30 = *on;
         linnbr = 13;
         posnbr = 15;
       when *in83;
         msgnr = 'YBC2049';
         *in30 = *on;
         linnbr = 13;
         posnbr = 46;
       when *in82;
         msgnr = 'YBC2050';
         *in30 = *on;
         linnbr = 14;
         posnbr = 56;
       when *in81;
         msgnr = 'YBC2051';
         *in30 = *on;
         linnbr = 14;
         posnbr = 91;
       when *in80;
         msgnr = 'YBC2052';
         *in30 = *on;
         linnbr = 15;
         posnbr = 72;
       endsl;

       endsr;

      //***********************************************
       begsr movkmw;

       if utype <> 'CPY';
         WSAVD   = KMAVD;
       endif;
       WSDTRD  = KMDTRD;
       WSDTRM  = KMDTRM;
       WSDTRA  = KMDTRA;
       WSKND   = KMKND;
       WSRGD   = KMRGD;
       WSNAD   = KMNAD;
       WSADD1  = KMADD1;
       WSADD2  = KMADD2;
       WSPND   = KMPND;
       WSADD3  = KMADD3;
       WSLKD   = KMLKD;
       WSCMD   = KMCMD;
       WSCMDT  = KMCMDT;
       WSTM    = KMTM;
       WSTRID  = KMTRID;
       WSATDDD = KMATDDD;
       WSATDDM = KMATDDM;
       WSATDDA = KMATDDA;
       WSATDT  = KMATDT;
       WSTRRF  = KMTRRF;
       WSTD1   = KMTD1;
       WSTDA   = KMTDA;
       WSKNC   = KMKNC;
       WSRGC   = KMRGC;
       WSNAC   = KMNAC;
       WSADC1  = KMADC1;
       WSADC2  = KMADC2;
       WSPNC   = KMPNC;
       WSADC3  = KMADC3;
       WSLKC   = KMLKC;
       WSCMC   = KMCMC;
       WSCMCT  = KMCMCT;

       endsr;

      //***********************************************
       begsr movwkm;

       KMPRO   = 0;
       KMDTRD  = WSDTRD;
       KMDTRM  = WSDTRM;
       KMDTRA  = WSDTRA;
       KMDTRC  = 20;
       KMKND   = WSKND;
       KMRGD   = WSRGD;
       KMNAD   = WSNAD;
       KMADD1  = WSADD1;
       KMADD2  = WSADD2;
       KMPND   = WSPND;
       KMADD3  = WSADD3;
       KMLKD   = WSLKD;
       KMCMD   = WSCMD;
       KMCMDT  = WSCMDT;
       KMTM    = WSTM;
       KMTRID  = WSTRID;
       KMATDDD = WSATDDD;
       KMATDDM = WSATDDM;
       KMATDDA = WSATDDA;
       KMATDDC = 20;
       KMATDT  = WSATDT;
       KMTRRF  = WSTRRF;
       KMTD1   = WSTD1;
       KMTDA   = WSTDA;
       KMKNC   = WSKNC;
       KMRGC   = WSRGC;
       KMNAC   = WSNAC;
       KMADC1  = WSADC1;
       KMADC2  = WSADC2;
       KMPNC   = WSPNC;
       KMADC3  = WSADC3;
       KMLKC   = WSLKC;
       KMCMC   = WSCMC;
       KMCMCT  = WSCMCT;

       endsr;

      //***********************************************
       begsr testd;

       if wsknd = 0;               // FEIL: Kundenr er 0.
         *in98 = *on;
       else;
         chain (fifirm: wsknd) cundl01;
         if %found;                // OK: Hent info. fra kunderegister.
           wsnad  = knavn;
           wsadd1 = adr1;
           wsadd2 = adr2;
           wsadd3 = adr3;
           wspnd  = *blanks;
           if postnr = 0;
             wspnd = sypoge;
           else;
             wspnd = postnr_a;
           endif;
           if syfor9 <> *zeros;
             wsrgd = syrg;
           endif;
         else;                     // FEIL: Kundenr finnes ikke i kunderegister
           *in98 = *on;
         endif;
       endif;

       endsr;

      //***********************************************
       begsr testc;

       if wsknc = 0;                   // FEIL: Transportørnr er 0.
         *in86 = *on;
       else;
         chain (fifirm: wsknc) tran;
         if %found;                    // OK: Transportørnr finnes.
           if vmtrku = 0;              // FEIL: Kundenr til transportør er 0.
             *in86 = *on;
           else;
             chain (fifirm: vmtrku) cundl01;
             if %found;                // OK: Hent inof. fra kunderegister
               wsnac  = knavn;
               wsadc1 = adr1;
               wsadc2 = adr2;
               wsadc3 = adr3;
               wspnc  = *blanks;
               if postnr = 0;
                 wspnc = sypoge;
               else;
                 wspnc = postnr_a;
               endif;
               if syfor9 <> *zeros;
                 wsrgc = syrg;
               endif;
             else;                     // FEIL: Kundenr finnes ikke i kunderegister.
               *in86 = *on;
             endif;
           endif;
         else;                         // FEIL: Transportør finnes ikke.
           *in86 = *on;
         endif;
       endif;

       endsr;

      //***********************************************
       begsr opdavd;

       chain wsavd sadkmdef;

       // FLYTTER SKJERMVARIABLE TIL FILVARIABLE
       exsr movwkm;

       // ikke finnes -> ny avdeling
       if not %found (sadkmdef);
         uavd  = wsavd;
         kmavd  = wsavd;
         kmdtr  = %dec(wdt:8:0);
         write sadkmdefr;
         wsdtrd = kmdtrd;
         wsdtrm = kmdtrm;
         wsdtra = kmdtra;
       else;
         update sadkmdefr;
       endif;

       endsr;

      //***********************************************
       begsr init;

       setll *loval firm;
       read firm;

       syge15c (wdt: wtm);

       usped = 'N';
       uunik = '106';
       utype2 = 'ANK';
       wsdtr = 0;
       wsatdt = 0;
       wsatdd = 0;
       kmdtr = 0;
       kmatdd = 0;

       select;
       when utype = 'DSP';
         *in49 = *on;
         wsavd = uavd;
       when utype = 'CHG';
         *in48 = *on;
         wsavd = uavd;
       when utype = 'CPY';
         wsavd = 0;
       endsl;

       chain uavd tell;
       if %found;
         kmpro = teturn;
       endif;
       chain(n) uavd sadkmdef;
       if not %found;
         kmdtr = 0;
         kmatdd = 0;
       endif;
       exsr movkmw;

       write windowsz;

       endsr;

      /end-free
