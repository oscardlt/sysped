     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLFRI' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  4VALUES(' ' '1' '2' '4')
     A            BEOPDL         7  0O  5 15EDTCDE(4)
     A            BENA          30   O  5 29
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ62'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV3139 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(30 DIV3002 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3140 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ62'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV3139 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(20 DIV3141 SYMSGF)
     A            WSOPDL         7Y 0B  3 24DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNA          30   B  3 33DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                  4  2MSGCON(3 DIV3142 SYMSGF)
     A            WSKNA          8Y 0B  4  6DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNAA         30   B  4 19DSPATR(UL)
     A            WSAD1A        30   B  4 51DSPATR(UL)
     A            WSAD2A        30   B  5 19DSPATR(UL)
     A            WSAD3A        30   B  5 51DSPATR(UL)
     A                                  6  2MSGCON(3 DIV3143 SYMSGF)
     A            WSKNM          8Y 0B  6  6DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNAM         30   B  6 19DSPATR(UL)
     A            WSAD1M        30   B  6 51DSPATR(UL)
     A            WSAD2M        30   B  7 19DSPATR(UL)
     A            WSAD3M        30   B  7 51DSPATR(UL)
     A                                  8  2MSGCON(3 DIV3144 SYMSGF)
     A            WSKNN          8Y 0B  8  6DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNAN         30   B  8 19DSPATR(UL)
     A            WSAD1N        30   B  8 51DSPATR(UL)
     A            WSAD2N        30   B  9 19DSPATR(UL)
     A            WSAD3N        30   B  9 51DSPATR(UL)
     A                                 10  2MSGCON(16 DIV3145 SYMSGF)
     A            WSKDP          5   B 10 19DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                 10 28MSGCON(10 DIV3146 SYMSGF)
     A            WSMN          17   B 10 41DSPATR(UL)
     A                                 12  2MSGCON(12 DIV3147 SYMSGF)
     A                                 13  2MSGCON(55 DIV3148 SYMSGF)
     A            WSFT          20   B 13 59DSPATR(UL)
     A                                 14  2MSGCON(34 DIV3149 SYMSGF)
     A            WSKDSA         1   B 14 37DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                 15  2MSGCON(34 DIV3150 SYMSGF)
     A            WSKDRA         1   B 15 37DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                 16  2MSGCON(34 DIV3151 SYMSGF)
     A            WSKDTF         1   B 16 37DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                 17  2MSGCON(11 DIV3152 SYMSGF)
     A            WSKDCP         1   B 17 14DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                 17 17MSGCON(10 DIV3153 SYMSGF)
     A            WSNTBS        15   B 17 28DSPATR(UL)
     A                                 17 45MSGCON(5 DIV3154 SYMSGF)
     A            WSSD           5   B 17 51DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                 17 58MSGCON(5 DIV3155 SYMSGF)
     A            WSDT           6Y 0B 17 64DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      EDTCDE(9)
     A                                 18  2MSGCON(10 DIV3156 SYMSGF)
     A            WSKNS          8Y 0B 18 14DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSNAS         30   B 18 24DSPATR(UL)
     A                                 19  2MSGCON(18 DIV3157 SYMSGF)
     A            WSADL         30   B 19 24DSPATR(UL)
     A                                 23  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE02
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SYSJ62'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV3139 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(20 DIV3141 SYMSGF)
     A            BEOPDL         7Y 0O  3 24DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BENA          30   B  3 33DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(3 DIV3142 SYMSGF)
     A            BEKNA          8Y 0O  4  6DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BENAA         30   O  4 19DSPATR(UL)
     A            BEAD1A        30   O  4 51DSPATR(UL)
     A            BEAD2A        30   O  5 19DSPATR(UL)
     A            BEAD3A        30   O  5 51DSPATR(UL)
     A                                  6  2MSGCON(3 DIV3143 SYMSGF)
     A            BEKNM          8Y 0O  6  6DSPATR(UL)
     A                                      EDTCDE(4)
     A            BENAM         30   O  6 19DSPATR(UL)
     A            BEAD1M        30   O  6 51DSPATR(UL)
     A            BEAD2M        30   O  7 19DSPATR(UL)
     A            BEAD3M        30   O  7 51DSPATR(UL)
     A                                  8  2MSGCON(3 DIV3144 SYMSGF)
     A            BEKNN          8Y 0O  8  6DSPATR(UL)
     A                                      EDTCDE(4)
     A            BENAN         30   O  8 19DSPATR(UL)
     A            BEAD1N        30   O  8 51DSPATR(UL)
     A            BEAD2N        30   O  9 19DSPATR(UL)
     A            BEAD3N        30   O  9 51DSPATR(UL)
     A                                 10  2MSGCON(16 DIV3145 SYMSGF)
     A            BEKDP          5   O 10 19DSPATR(UL)
     A                                 10 28MSGCON(10 DIV3146 SYMSGF)
     A            BEMN          17   O 10 41DSPATR(UL)
     A                                 12  2MSGCON(12 DIV3147 SYMSGF)
     A                                 13  2MSGCON(55 DIV3148 SYMSGF)
     A            BEFT          20   O 13 59DSPATR(UL)
     A                                 14  2MSGCON(34 DIV3149 SYMSGF)
     A            BEKDSA         1   O 14 37DSPATR(UL)
     A                                 15  2MSGCON(34 DIV3150 SYMSGF)
     A            BEKDRA         1   O 15 37DSPATR(UL)
     A                                 16  2MSGCON(34 DIV3151 SYMSGF)
     A            BEKDTF         1   O 16 37DSPATR(UL)
     A                                 17  2MSGCON(11 DIV3152 SYMSGF)
     A            BEKDCP         1   O 17 14DSPATR(UL)
     A                                 17 17MSGCON(10 DIV3153 SYMSGF)
     A            BENTBS        15   O 17 28DSPATR(UL)
     A                                 17 45MSGCON(5 DIV3154 SYMSGF)
     A            BESD           5   O 17 51DSPATR(UL)
     A                                 17 58MSGCON(5 DIV3155 SYMSGF)
EV5  A            BEDT           8Y 0H
NV5  A            WEDT           6Y 0O 17 64DSPATR(UL)
     A                                      EDTCDE(9)
     A                                 23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYSJ62'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV3139 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(30 DIV3002 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3140 SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R SØKLNR
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(30 DIV3158 SYMSGF)
     A            W2NA          30A  I  2 34DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
