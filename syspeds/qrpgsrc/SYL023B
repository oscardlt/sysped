      *********************************************************************
      * ARBEID MED PLUKKLISTE (HEADING)
      * PROGRAM BEHANDLER INPUT FRA SYL025 OG VISER UTVALGT HEADLING
      * OBS! LAG KUN MODUL (OPT 15) AV SYL023B IKKE PROGRAM.
      *     (INNGÅR SOM MODUL I PGM SYL024, SYL025A og SYL027)
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FLLMRF     IF   E           K DISK    USROPN
     FLLAGJL07  IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FBRPARF    IF   E           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
      * Variables used to load the external html source member
     D Lib             S             10
     D Fn              S             10
     D Mbr             S             10
      *
     D TB              S              1    DIM(10)
     D Spaces          S             12a   inz('&nbsp;&nbsp;')
     D FEILMELDING     S             80

     D BWSMRNR         S              5  0
     D BWSLPN          S              4  0
     D WSMN            S              7
     D W2MN            S              7
     D BWSMN           S              7S 0
     D BWSSALD         S              7S 0
     D LASTWSMN        S              7
     D LVVG            S             30
     D RecordCnt       S              5S 0
     D lstbrd          S              1
     D                 DS
     D XXREF                   1    107
     D XXMN                    1      7
     D XXMRNR                  8     12
     D XXKJED                 13     20
     D XXKNV                  21     50
     D XXLNR                  51     67
     D XXLNA                  68     97
     D XXUSER                 98    107
     D                 DS
     D VALLOK                  1     25
     D VALOK1                  1      5
     D VALOK2                  6     25
     D                 DS
     D PAVRDA                  1     50
     D PAVRDA1                 1      5
     D PAVRDA2                 6     50
     D PAVRDA3                 1      3
     D PAVRDA4                 4     50
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      */copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Initier
     C                   EXSR      INIT
      * Set output skeleton html member name
     C                   EXSR      VISLINJE
      * Send output buffer and quit
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     LLMRF
     C                   close     LLAGJL07
     C                   close     CUNDL01
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * VIS LINJER
      *=====================================================================
     C     VISLINJE      begsr
      *
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * LAGERSTED
     C                   EXSR      HENTLSD
      * LOKASJONTYPE
     C                   EXSR      HENTLTY
      *------------------                                                   ******
      * Find number of Recds matching selection criteria
     C                   exsr      Recdsnumb
      *------------------                                                   ******
      * Write HTML sections
      *
      * 2-Section /$howmany
     C                   exsr      SetHowMany
     C                   callp     wrtsection('howmany')
      * 2a-Section /$0found
     C     RecordCnt     ifeq      0
     C                   MOVE      *ZEROS        XXMN
     C                   MOVE      BWSMRNR       XXMRNR
     C                   MOVE      MRKJED        XXKJED
     C                   MOVE      KNAVN         XXKNV
     C                   MOVE      MRLNR         XXLNR
     C                   MOVE      MRLNA         XXLNA
     C                   MOVE      WSUSER        XXUSER
     C                   callp     updhtmlvar('XXREF':XXREF)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   callp     wrtsection('0found')
     C                   goto      endhtml
     C                   endif
      * 3-Section /$tablestr
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
     C     LLAGJL07K1    SETLL     LLAGJL07
     C     LLAGJL07K     READPE    LLAGJL07                               50
     C     *in50         doweq     '0'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C     LLAGJL07K     READPE    LLAGJL07                               50
     C  N50              ENDDO                                                  *hival
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      * 5a-Section /$foundsome
     C     RecordCnt     ifgt      0
     C                   exsr      SetFndSome
     C                   callp     wrtsection('foundsome')
     C                   endif
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton TIL LINJE
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYL024'
     C                   CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      *
     C                   callp     updhtmlvar('WSLNR':MRLNR)
     C                   callp     updhtmlvar('WSLNA':MRLNA)
      *
     C                   callp     updhtmlvar('WSLREF':MRLREF)
     C                   callp     updhtmlvar('WSKREF':MRKREF)
      *
     C                   MOVE      *ZEROS        XXMN
     C                   MOVE      BWSMRNR       XXMRNR
     C                   MOVE      MRKJED        XXKJED
     C                   MOVE      KNAVN         XXKNV
     C                   MOVE      MRLNR         XXLNR
     C                   MOVE      MRLNA         XXLNA
     C                   MOVE      WSUSER        XXUSER
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WSGRP':
     C                             %trim(%editc(MRGRUP:'Z')))
     C                   callp     updhtmlvar('WSPNR':
     C                             %trim(%editc(MRKJED:'Z')))
     C                   callp     updhtmlvar('MRMRNR':
     C                             %trim(%editc(MRMRNR:'Z')))
     C                   callp     updhtmlvar('XXREF':XXREF)
     C                   callp     updHtmlVar('WSANR':WSANR)
     C                   callp     updHtmlVar('WSVB':WSVB)
     C                   callp     updhtmlvar('WSOANT':WSOANT)
     C                   callp     updhtmlvar('WSLOK2':WSLOK2)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     updhtmlvar('FEILMELDING':FEILMELDING)
      *
     C                   endsr
      *=====================================================================******
     C*  HENT LAGERSTEDKODER
      *=====================================================================******
     C     HENTLSD       begsr
     C                   MOVEL     'LLLOK'       PAID
      *
     C                   IF        ((FEILMELDING = *BLANKS) OR
     C                              (WSLOK1 = *BLANKS))
     C                   callp     updHtmlVar('WSLOKKD':'')
     C                   IF        PAVRDA = 'EN'
     C                   callp     updHtmlVar('WSLOKTXT':'Select place')
     C                   ELSE
     C                   callp     updHtmlVar('WSLOKTXT':'Velg lagersted')
     C                   END
     C                   callp     wrtsection('loksltrow')
     C                   ELSE
      *
     C                   IF        WSLOK1 <> *BLANKS
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   DOW       *IN50 = *OFF
     C                   IF        WSLOK1 = PAVRDA1
     C                   callp     updHtmlVar('WSLOKKD':PAVRDA1)
     C                   callp     updHtmlVar('WSLOKTXT':PAVRDA2)
     C                   callp     wrtsection('loksltrow')
     C                   LEAVE
     C                   END
     C     BRPARFK       READE     BRPARF                                 50
     C                   ENDDO
     C                   END
      *
     C                   END
      *
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   DOW       *IN50 = *OFF
     C                   IF        ((WSLOK1 <> PAVRDA1) OR
     C                              (FEILMELDING = *BLANKS))
     C                   callp     updHtmlVar('WSLOKKD':PAVRDA1)
     C                   callp     updHtmlVar('WSLOKTXT':PAVRDA2)
     C                   callp     wrtsection('loksltrow')
     C                   END
     C     BRPARFK       READE     BRPARF                                 50
     C                   ENDDO
     C                   callp     wrtsection('loksltend')
      *
     C                   endsr
      *=====================================================================******
     C*  HENT LOKASJONSTYPE
      *=====================================================================******
     C     HENTLTY       begsr
     C                   MOVEL     'LLLTY'       PAID
      *
     C                   IF        WSLLT <> *BLANKS
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   DOW       *IN50 = *OFF
     C                   IF        WSLLT = PAVRDA3
     C                   callp     updHtmlVar('WSLLT1':PAVRDA3)
     C                   callp     updHtmlVar('WSLLT2':PAVRDA4)
     C                   callp     wrtsection('lltsltrow')
     C                   LEAVE
     C                   END
     C     BRPARFK       READE     BRPARF                                 50
     C                   ENDDO
     C                   END
      *
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   DOW       *IN50 = *OFF
     C                   IF        WSLLT <> PAVRDA3
     C                   callp     updHtmlVar('WSLLT1':PAVRDA3)
     C                   callp     updHtmlVar('WSLLT2':PAVRDA4)
     C                   callp     wrtsection('lltsltrow')
     C                   END
     C     BRPARFK       READE     BRPARF                                 50
     C                   ENDDO
     C                   callp     wrtsection('lltsltend')
      *
     C                   endsr
      *=====================================================================******
     C*  Find number of Recds matching search criteria
      *=====================================================================******
     C     Recdsnumb     begsr
     C                   eval      RecordCnt = 0
     C     LLAGJL07K     SETLL     LLAGJL07
     C     LLAGJL07K     READE     LLAGJL07                               50
     C     *in50         DoWeq     '0'
     C                   eval      RecordCnt = RecordCnt + 1
     C     LLAGJL07K     READE     LLAGJL07                               50
     C                   enddo                                                  *hival
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$howmany
      *=====================================================================******
     C     SetHowMany    begsr
     C* Overall Recds count
     C     RecordCnt     ifgt      0
     C                   callp     updhtmlvar('RecordCnt':
     C                             %trim(%editw(RecordCnt:'   0 ')))
     C                   else
     C                   callp     updhtmlvar('RecordCnt':'Ingen')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
     C                   MOVE      VAMN          XXMN
     C                   MOVE      BWSMRNR       XXMRNR
     C                   MOVE      MRKJED        XXKJED
     C                   MOVE      KNAVN         XXKNV
     C                   MOVE      MRLNR         XXLNR
     C                   MOVE      MRLNA         XXLNA
     C                   MOVE      WSUSER        XXUSER
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('XXREF':XXREF)
     C                   callp     updhtmlvar('VAMN':WSMN)
      * ARTIKKEL
     C                   callp     updhtmlvar('VAANR':VAANR)
     C                   callp     updhtmlvar('VAVB':VAVARE)
      * ANTLL STYKK
     C                   callp     updhtmlvar('VAOANT':
     C                             %trim(%editC(VAOANT:'Z')))
     C                   callp     updhtmlvar('VALOK1':VALOK1)
     C                   callp     updhtmlvar('VALOK2':VALOK2)
     C                   callp     updhtmlvar('VALLT':VALLT)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome    begsr
      * Repeat background color
     C                   MOVE      *ZEROS        XXMN
     C                   MOVE      WSUSER        XXUSER
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('XXREF':XXREF)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      *
     C                   endsr
      *=====================================================================
      *  Retrieve environment variables
      *=====================================================================
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
     C     *ENTRY        PLIST
     C                   PARM                    BWSMRNR
     C                   PARM                    GKGRP             8 0
     C                   PARM                    BIKUNR            8 0
     C                   PARM                    WSUSER           10
     C                   PARM                    UsrSpcName       10
     C                   PARM                    FIRMA
     C                   PARM                    BIFARG           10
     C                   PARM                    WSANR            15
     C                   PARM                    WSVB             35
     C                   PARM                    WSSALD            7
     C                   PARM                    WSOANT            7
     C                   PARM                    WSLOK1            5
     C                   PARM                    WSLOK2           20
     C                   PARM                    WSLLT             3
     C                   PARM                    FMELDNR           1
     C                   PARM                    FEILMELDING      80
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   open      LLMRF                                50
     C                   open      LLAGJL07                             50
     C                   open      CUNDL01                              50
     C                   open      BRPARF                               50
     C     LLMRFK        klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C                   kfld                    BWSMRNR
     C     LLAGJL07K     klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C                   kfld                    MRMRNR
     C     LLAGJL07K1    klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C                   kfld                    MRMRNR
     C                   kfld                    XMN
     C     CU01K         klist
     C                   kfld                    FIRMA
     C                   kfld                    BIKUNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     LLMRFK        CHAIN     LLMRF                              60
     C     CU01K         CHAIN     CUNDL01                            60
     C                   MOVE      WSUSER        PABRID
     C                   MOVEL     'SPRÅK'       PAID
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
     C                   MOVE      9999999       XMN               7 0
      *
     C                   endsr
      *
