     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/UNITFX)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 75 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            RECNUM        10S 0H
     A            WSNR           1A  I  7  2VALUES(' ' '1' '4')
     A            UFFAX     R        O  7  5REFFLD(UNITFXR/UFFAX  *LIBL/UNITFX)
     A            UFMOB     R        O  7 21REFFLD(UNITFXR/UFMOB *LIBL/UNITFX)
     A            UFTXT     R        O  7 37REFFLD(UNITFXR/UFTXT *LIBL/UNITFX)
     A            UFEPO         70A  O  8  5DSPATR(UL)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0050)
     A                                      SFLPAG(0003)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYTR09'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(045 JOV0781 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 10MSGCON(010 JOV0788 *LIBL/SYMSGF)
     A            UFBILN    R        O  2 21REFFLD(UNITFXR/UFBILN *LIBL/UNITFX)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(040 JOV0782 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 47SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(050 SHU1320 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2FAX     R        B  5  5REFFLD(UNITFXR/UFFAX *LIBL/UNITFX)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(HI)
     A            W2MOB     R        B  5 21REFFLD(UNITFXR/UFMOB *LIBL/UNITFX)
     A                                      DSPATR(UL)
     A            W2TXT     R        B  5 37REFFLD(UNITFXR/UFTXT *LIBL/UNITFX)
     A                                      DSPATR(UL)
     A            W2EPO         70A  B  6  2DSPATR(UL)
     A  89                                  DSPATR(HI)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(11)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(029 JOV0784 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYTR09'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(045 JOV0781 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 10MSGCON(010 JOV0788 *LIBL/SYMSGF)
     A            UFBILN    R        O  2 21REFFLD(UNITFXR/UFBILN *LIBL/UNITFX)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(040 JOV0782 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(050 SHU1320 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2FAX     R        B  5  5REFFLD(UNITFXR/UFFAX *LIBL/UNITFX)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(HI)
     A            W2MOB     R        B  5 21REFFLD(UNITFXR/UFMOB *LIBL/UNITFX)
     A                                      DSPATR(UL)
     A            W2TXT     R        B  5 37REFFLD(UNITFXR/UFTXT *LIBL/UNITFX)
     A                                      DSPATR(UL)
     A  89                                  DSPATR(HI)
     A            W2EPO         70A  B  6  2DSPATR(UL)
     A N30                             11  2MSGCON(029 JOV0784 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O 11  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
