      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE19                    ***
      *** Write-Off Notification                                    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH05  UF   E           K DISK
     FTVINF     O  A E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM2     IF   E           K DISK    RENAME(EDIMR:EDIMR2)
     F                                     PREFIX(X_)
     FKODTSI    IF   E           K DISK
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104rd
     d ncts104rd       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104rd       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d xn              s              3s 0
     d x_thtrnr        s                   like(thtrnr)
     d x_IE19          s              1a
     d x_sn            s              3s 0
     d wdt             s              8
     d wtm             s              6
     d uemne           s             40
     d utxt            s            256
     d uep             s             70
     d                 ds
     d x_wdt                   1      8  0
     d x_wdta                  1      8
     d                 ds
     d x_txt                   1    512
     d x_tx                    1    490    dim(7)
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
      *----------------------------------------------------------------------
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC019C';
             x_ie19 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'CustomsOfficeOfDeparture';
             exsr srxml2;
           when xrnv = 'HolderOfTheTransitProcedure';
           when xrnv = 'Guarantor';
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie19 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;
         xn    = 0;
         x_txt = *blanks;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
           when xrnv = 'discrepanciesNotificationDate';
             x_wdta = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
           when xrnv = 'discrepanciesNotificationText';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             x_txt  = xrvrd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             f0077 = 'INF';
             ft(1) = 'Discrepancies date: ' + %trim(x_wdta);
             ft(2) = 'Customs office of departure: ' + %trim(xrverd);
             ft(3) = x_tx(1);
             ft(4) = x_tx(2);
             ft(5) = x_tx(3);
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         chain x_thtrnr nctseh05;
         if %found;
           thst019 = 'J';
           update nctsehr;

           setgt (thavd: thtdn) edim2;
           readpe (thavd: thtdn) edim2;
           dow not %eof;
             if x_m0068a <> 0;
               leave;
             endif;
             readpe (thavd: thtdn) edim2;
           enddo;
         endif;

         m0065  = 'XMLRES';
         m0068  = x_thtrnr;
         m1001  = 'TET';
         m1225  = '019';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x_m0068a;
         m0068b = x_m0068b;
         m1n07  = '019';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = 'T19';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'Discrepancies Notification: '
                  + %trim(%editw(x_wdt:'    .  .  '));
         tttexl = 'Avviks melding: '
                  + %trim(%editw(x_wdt:'    .  .  '));
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
