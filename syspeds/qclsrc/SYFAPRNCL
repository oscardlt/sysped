/* N01 'S' I FELT BAK LOSSELISTE-PRINT SKILLEARK(ZO) FOR ALLE OPPD PÅ TUREN */
/* N02             EGEN BETILLING MERKELAPPER                               */
             PGM
             DCL        VAR(&SAKODE) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SATYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&KOWFET) TYPE(*CHAR) LEN(1)
             DCL        VAR(&AWB) TYPE(*LGL) LEN(1) /* MAWB-liste +
                          type. */
             DCL        VAR(&KOPIE) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &KOPI = 1, betyr det kopiering av +
                          flyfraktbrev-eksport. */
             DCL        VAR(&CM) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in01 på, betyr det avslutt program +
                          fra valgbilde. */
             DCL        VAR(&IN03) TYPE(*LGL) LEN(1) /* Hvis &in03 +
                          på, skal losseliste  være stående. */
             DCL        VAR(&KOPI) TYPE(*CHAR) LEN(1) /* Hvis &KOPI +
                          =J, skal KOPIER SKRIVES          . */
             DCL        VAR(&IN04) TYPE(*LGL) LEN(1) /* Hvis &in04 +
                          på, skal printetype være 4224. */
             DCL        VAR(&IN05) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in05 på, betyr programkallinng fra +
                          BORDERAUR. */
             DCL        VAR(&IN06) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in06 på, betyr programkalleing fra +
                          MANEFEST. */
             DCL        VAR(&IN07) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in07 på, betyr programkallinng fra +
                          KREDITERING. */
             DCL        VAR(&IN08) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          If &in08=1 , meens invoicing on order- +
                          type         */
    /* */
             DCL        VAR(&LG) TYPE(*CHAR) LEN(1) /* Hvis 'L', +
                          benytter linjegods fraktbrev. ellers vanlig +
                          frakbrev. */
             DCL        VAR(&EKSP) TYPE(*CHAR) LEN(1) /* Hvis 'J', +
                          benytter ekspress fraktbrev. */
             DCL        VAR(&LT) TYPE(*CHAR) LEN(1) /* Hvis 'L', +
                          benytter linjegodstollpass. */
             DCL        VAR(&KODE) TYPE(*CHAR) LEN(1) VALUE('F') /* +
                          Kode = 'F', 'T'. */
             DCL        VAR(&VB) TYPE(*CHAR) LEN(1) /* Hvis &VB = +
                          'J', skal sende valgbilde ut til bruker. */
             DCL        VAR(&PO) TYPE(*CHAR) LEN(1) /* Hvis &PO = +
                          'J', skal sortere på postnr. */
    /* FØLGENDE 5 PARAMETRE LEDIGE FOR ANNET BRUK (SYFA12) */
             DCL        VAR(&VBST) TYPE(*CHAR) LEN(1) /* LEDIG   */

             DCL        VAR(&VBLI) TYPE(*CHAR) LEN(1) /* LEDIG   */

             DCL        VAR(&VBEK) TYPE(*CHAR) LEN(1) /* LEDIG   */

             DCL        VAR(&TPST) TYPE(*CHAR) LEN(1) /* LEDIG   */

             DCL        VAR(&TPLI) TYPE(*CHAR) LEN(1) /* LEDIG   */

    /* */
             DCL        VAR(&OF) TYPE(*CHAR) LEN(1) /* +
                          Oppdragsformular. */
             DCL        VAR(&VF) TYPE(*CHAR) LEN(1) /* Vanlig +
                          fraktbrev. */
             DCL        VAR(&TPFB) TYPE(*CHAR) LEN(1) /* Tollpass +
                          fraktbrev. */
             DCL        VAR(&FP) TYPE(*CHAR) LEN(1) /* Forpassing +
                          SAD. */
             DCL        VAR(&IFFB) TYPE(*CHAR) LEN(1) /* Innenland +
                          flyfraktbrev. */
             DCL        VAR(&LOSS) TYPE(*CHAR) LEN(1) /* Losseliste. */
             DCL        VAR(&FFAK) TYPE(*CHAR) LEN(1) /* Ferdig +
                          Ferdig meldte fakturaer. */
             DCL        VAR(&MS) TYPE(*CHAR) LEN(2) /* Meldesedler */
             DCL        VAR(&JBK) TYPE(*CHAR) LEN(1) /* Jobbkø. */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7)
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&EDS) TYPE(*CHAR) LEN(39)  +
                                  VALUE('*BLANKS')
/*N01*/      DCL        VAR(&SKIL) TYPE(*CHAR) LEN(1)
/*N01*/      DCL        VAR(&TYP) TYPE(*CHAR) LEN(2) VALUE('ZO')
/*N01*/      DCL        VAR(&TUR) TYPE(*CHAR) LEN(8)
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* HENTE AVDELING, SIGNATUR OG FIRMAKODE */
             CALL       PGM(SYFA00) PARM(&IN01)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* HENTE OPPLYSNINGER OM HVA SLAGS UTSKRIFT MAN ØNSKER */
             CALL       PGM(SYFAPR) PARM(&JBK &IN01 &IN03 &IN04 &VBST +
                          &VBLI &VBEK &TPST &TPLI &OF &VF &TPFB &FP +
                          &IFFB &LOSS &FFAK &KOWFET &CM &SAKODE &SATYPE)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* N01 - PARAMERTRE FOR PRINT AV SKILLEARK                      */
             RTVDTAARA  DTAARA(*LDA (80 1)) RTNVAR(&SKIL)
             RTVDTAARA  DTAARA(*LDA (22 8)) RTNVAR(&TUR)
    /* ============================================================ */
    /* PRINT OF ORDER-SPESIFICATION */
    /* */
    /* ============================================================ */
    /* SKRIVER UT OPPDRAGSFORMULAR */
             IF         COND(&OF = 'J') THEN(DO)
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA40CLJ)) JOB(OPPDRAG) +
                          JOBD(*USRPRF) JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFA40CLJ)
                        ENDDO
                        ENDDO
    /* ============================================================ */
    /* PRINT OF WAY-BILL */
             CHGVAR     VAR(&VB) VALUE(' ')
             CHGVAR     VAR(&KODE) VALUE('F')
    /* */
    /* ============================================================ */
    /* SKRIVER UT VANLIG FRAKTBREV */
             IF         COND(&VF = 'J') THEN(DO)
    /* STANDARD */
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA12CLJ) PARM(&KODE &VB &EKSP +
                          &LG &PO)) JOB(FRBREV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFA12CLJ) PARM(&KODE &VB &EKSP &LG &PO)
                        ENDDO
                        ENDDO
    /* ============================================================ */
    /* */
             CHGVAR     VAR(&KODE) VALUE('T')
    /* */
    /* ============================================================ */
    /* TOLLPASSFRAKTBREV */
             IF         COND(&TPFB = 'J') THEN(DO)
    /* STANDARD */
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA12CLJ) PARM(&KODE &VB &EKSP +
                          &LT &PO)) JOB(TOLLPASS) JOBD(*USRPRF) +
                          JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFA12CLJ) PARM(&KODE &VB &EKSP &LT &PO)
                        ENDDO
                        ENDDO
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* FORPASSING PÅ SAD */
             IF         COND(&FP = 'J') THEN(DO)
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA41CLJ)) JOB(FORPASSING) +
                          JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFA41CLJ)
                        ENDDO
                        ENDDO
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* FLYFRAKTBREV INNLAND*/
    /* S07 SLETTET */
    /*       IF         COND(&JBK = 'J') THEN(DO)                   */
    /*       SBMJOB     CMD(CALL PGM(SYFA43CLJ)) JOB(FRBREV) +      */
    /*                    JOBQ(SYJOBQPRT)                           */
    /*                  ENDDO                                       */
    /*       ELSE       CMD(DO)                                     */
    /*       CALL       PGM(SYFA43CLJ)                              */
    /*                  ENDDO                                       */
    /*                  ENDDO                                       */
    /* FLYFRAKTBREV INNLAND*/
             IF         COND(&IFFB = 'J') THEN(DO)
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYEF14CLI) PARM(&AWB &KOPIE +
                          &KOWFET)) JOB(FRBREV) JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYEF14CLI) PARM(&AWB &KOPIE &KOWFET)
                        ENDDO
                        ENDDO
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* LOSSELISTE */
             IF         COND(&LOSS = 'J') THEN(DO)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &TIME)
             RMVM       FILE(ASSAM) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(ASSAM) MBR(&UNIK)
             OVRDBF     FILE(ASSAM) TOFILE(ASSAM) MBR(&UNIK)
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFI48CLJ) PARM(&IN03 &IN04 &EDS)) +
                          JOB(LOSSELI) JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFI48CLJ) PARM(&IN03 &IN04 &EDS)
                        ENDDO
             RMVM       FILE(ASSAM) MBR(&UNIK)
             MONMSG     MSGID(CPF0000)
                        ENDDO
    /* SKILLEARK- SAMMEN MED/UAVHENGIG AV LOSSELISTE */
/*N01*/      IF         COND(&SKIL = 'S') THEN(DO)
/*N01*/      CALL       PGM(ARCA000T) PARM(&TYP &TUR)
/*N01*/                 ENDDO
    /* SKILLEARK- TYPE 2                             */
/*N02*/      IF         COND(&SAKODE *NE '  ') THEN(DO)
/*N02*/      CALL       PGM(ARCA000T) PARM(&SAKODE &TUR)
/*N02*/                 ENDDO
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* SKRIVER UT FERDIGMELDTE FAKTURAER */
             IF         COND(&FFAK = 'J') THEN(DO)
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA20CLJ) PARM(&FFAK &IN05 +
                          &IN06 &IN07 &IN08 &KOPI)) JOB(FAKTURA) +
                          JOBD(*USRPRF) JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFA20CLJ) PARM(&FFAK &IN05 &IN06 &IN07 +
                          &IN08 &KOPI)
                        ENDDO
                        ENDDO
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* SKRIVER UT MELDESEDLER */
             RTVDTAARA  DTAARA(*LDA (49 2)) RTNVAR(&MS) /* Hente +
                          oppdragstype fra UDS. */
             IF         COND(&MS *NE '  ') THEN(DO)
             CHGVAR     VAR(&FFAK) VALUE(' ')
             CHGVAR     VAR(&IN08) VALUE('1')
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA20CLJ) PARM(&FFAK &IN05 +
                          &IN06 &IN07 &IN08 &KOPI)) JOB(MELDES) +
                          JOBD(*USRPRF) JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFA20CLJ) PARM(&FFAK &IN05 &IN06 &IN07 +
                          &IN08 &KOPI)
                        ENDDO
                        ENDDO
    /* SKRIVER UT CMR-FRAKTBREV          */
             IF         COND(&CM = 'J') THEN(DO)
             CHGDTAARA  DTAARA(*LDA (30 5)) VALUE('00000')
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYEX43CLJ) PARM(&IN01 &IN01)) +
                          JOB(CMR) JOBD(*USRPRF) JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYEX43CLJ) PARM(&IN01 &IN01)
                        ENDDO
                        ENDDO
    /* ============================================================ */
    /* */
 SLUTT:      ENDPGM
