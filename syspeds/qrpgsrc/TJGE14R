      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE14R MODULE(*LIBL/TJGE14R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FBRPARF    IF   e           k disk    USROPN
     FGSSCGL02  IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FKODFRI    IF   E           K DISK    USROPN
     FKOFAST    IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FTURER14   IF   E           K DISK    USROPN
     FDOKUF7    IF   E           K DISK    USROPN
     FTRAN      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D KN              S              8  0 DIM(30)
     D AVD             S              4
     D AVDN            S              4S 0
     D OPD             S              7
     D OPDN            S              7S 0
     D part            S              1
     D FAKLNK          S            200
     D Error           S            150
     D JSONstring      s          64000
     D ANTLEST         s              9  0
     D WSSOK           S             80
     D WSSOKURL        S            200
     D                 DS
     D  HXSNDN                 1     20
     D  HXSNR17                4     20
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

      *
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬part¬ : ¬'+%trim(PART)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

      * Hent fakturar
     c     Error         IFEQ      *blanks
     C                   EXSR      DSPTT
     C                   ENDIF
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         BEGSR
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      AVD    = zhbgetvar('AVD')
     C                   EVAL      AVDN   = c2n2(AVD)
     C                   EVAL      OPD    = zhbgetvar('OPD')
     C                   EVAL      OPDN = c2n2(OPD)
     C                   EVAL      PART   = zhbgetvar('PART')
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     HEADFK        klist
     C                   kfld                    AVDN
     C                   kfld                    OPDN
     C     TranKey       klist
     C                   kfld                    BIFIRM
     C                   kfld                    DFTRAN
     C     KOFASTK1      klist
     C                   kfld                    KFTYP1           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP1 = 'TRACKPREFI'
     C     KOFASTK2      klist
     C                   kfld                    KFTYP2           10
     C                   kfld                    KFKOD
     C                   EVAL      KFTYP2 = 'TRACKSUFFI'

     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END
      * Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C   50              eval      Error = 'Invalid userID'
     C                   OPEN      FRISOK
     C                   OPEN      FIRM
     C                   OPEN      BRPARF
     C                   OPEN      GSSCGL02
     C                   OPEN      KODFRI
     C                   OPEN      KOFAST
     C                   OPEN      HEADF
     C                   OPEN      TURER14
     C                   OPEN      DOKUF7
     C                   OPEN      TRAN


     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID

     C                   EVAL      PABRID = WSUSER

     C                   MOVEL     'NOTES'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR
     C                             ((PAVRDA <> 'j') AND
     C                              (PAVRDA <> 'J')))
     C                   MOVE      'N'           XWRKNOTES         1
     C                   ELSE
     C                   MOVE      'J'           XWRKNOTES         1
     C                   END

     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END

     C                   MOVE      *ZEROS        KN
     C                   MOVE      *ZEROS        XN2               3 0
     C                   MOVEL     'KUNGR'       PAID
     C     BRPARFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA = *BLANKS))
     C                   MOVE      BIKUNR        KN(1)
     C                   ELSE
     C                   MOVEL     PAVRDA        CGCG
     C     CGCG          SETLL     GSSCGL02
     C     CGCG          READE     GSSCGL02                               33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN2
     C                   MOVE      CGCNO         KN(XN2)
     C     CGCG          READE     GSSCGL02                               33
     C                   ENDDO
     C                   END
      * LÅNER ARKTY-PARAMETEREN FOR Å AVGJØRE OM BRUKER KAN SE FAKTURALINK
      * sjekk om arkivtypene fa,fs,fx finnes(på førstelinjer) Hvis ikke, N i se fakturalink
      * (sjekk av arkivtyper for å se pdf-dok, sker generelt annet sted)
     C                   MOVE      'N'           OKFaktLink        1
     C                   MOVE      'ARKTY'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      '0'
     C     'fa'          scan      Pavrda                                 33
     C  N33'fs'          scan      Pavrda                                 33
     C  N33'fx'          scan      Pavrda                                 33
     C   33              MOVE      'J'           OKFaktLink
     C                   END

     C     ENDINI        ENDSR
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          BEGSR
     C                   CLOSE     BRIDF
     C                   CLOSE     FRISOK
     C                   CLOSE     FIRM
     C                   CLOSE     BRPARF
     C                   CLOSE     GSSCGL02
     C                   CLOSE     KODFRI
     C                   CLOSE     KOFAST
     C                   CLOSE     HEADF
     C                   CLOSE     TURER14
     C                   CLOSE     DOKUF7
     C                   CLOSE     TRAN
     C                   CALLP     wrtsection('*fini')
     C                   EVAL      *INlr = *on

     C                   ENDSR
      *=====================================================================******
      *  Display TRACK & TRACE
      *=====================================================================******
     C     DSPTT         BEGSR
      * Skriv JSON-LISTE Start
     C                   EVAL      JSONstring ='"getfriesokeveier" : ['
     C                   CALLP     UPdHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EVAL      Snr = *BLANKS
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      WSSOK=*BLANKS
     C                   EVAL      WSSOK = FSSOK
     C                   EVAL      WSSOKURL=*BLANKS
      * Skal denne med med hensyn til part, f.eksempel S=Selger K=Kjøper.........
     C     PART          IFNE      *BLANKS
     C     PART          SCAN      FSDOKK                                 32
     C     *IN32         CABEQ     *OFF          NEXTFRI1
     C                   ENDIF
      * Koden må finnes
     C     FSKODE        CHAIN     KODFRI                             33
     C     *IN33         CABEQ     '1'           NEXTFRI1
     C                   IF        FSKODE = 'IFB'
     C                   EXSR      TstFbr
     C                   IF        Snr = *BLANKS
     C                   MOVEL     FSSOK         Snr              17
     C                   ELSE
     C                   EVAL      Snr    = 'FLERE'
     C                   ENDIF
     C                   ENDIF
      * Har denne frisok-kode peker til Track&Trace-URL-definisjon?
     C                   EVAL      KFKOD = KFSTTU
     C     KOFASTK1      CHAIN     KOFAST                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      WSSOKURL = KFTXT
      * AppEND linje 2 (engelsk tekst når siet tegn er A)
     C                   MOVE      KFTXTE        TstAppEND         1
     C     TstAppEND     IFEQ      'A'
     C                   MOVE      ' '           KFTXTE
     C                   CAT       KFTXTE:0      WSSOKURL
     C                   END
     C                   CAT       FSSOK:0       WSSOKURL
      * Suffix i søkestrengen..
     C     KOFASTK2      CHAIN     KOFAST                             33
     C                   IF        *IN33 = *OFF
     C                   CAT       KFTXT:0       WSSOKURL
     C                   MOVE      KFTXTE        XTXTA             1
     C                   MOVE      ' '           KFTXTE
     C     XTXTA         IFEQ      'A'
     C                   CAT       KFTXTE:0      WSSOKURL
     C                   END
     C                   END

     C                   ELSE
     C                   EVAL      WSSOKURL=*BLANKS
     C                   ENDIF
     C                   ADD       1             ANTLEST
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
        +'¬kfsotx¬ : ¬'+%trim(KFSOTX)+'¬, '
        +'¬wssok¬ : ¬'+%trim(WSSOK)+'¬, '
        +'¬wssokurl¬ : ¬'+%trim(WSSOKURL)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
     C     NextFri1      TAG
     C     HEADFK        READE     FRISOK                                 33
     C  N33              ENDDO
      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')


     C                   ENDSR
      ***********************************************
     C     TSTFBR        BEGSR
      ***********************************************
     C     HEADFK        CHAIN     HEADF                              33
      * Test om transportør på fraktbrev overstyrer T&T-URL-kode
      * hvis innlandsmodul  og dette er inl-fraktbrevet
     C     HEUR          IFEQ      'H'
     C     HEpro         andne     *ZEROS
     C     HXSNR17       andeq     FSSOK
     C     hepro         CHAIN     Turer14                            33
     C     *IN33         IFEQ      '0'
     C                   MOVE      TUKNT2        DFTRAN
     C                   goto      TraTag
     C                   ENDIF
     C                   ENDIF

     C     FSSOK         CHAIN     dokuf7                             33
     C     *IN33         CABEQ     '1'           UtTstFbr
     C     TraTag        tag
     C     DFTRAN        CABEQ     0             UtTstFbr
     C     TranKey       CHAIN     Tran                               33
     C     *IN33         CABEQ     '1'           UtTstFbr
     C     VMINTP        CABEQ     *BLANKS       UtTstFbr
     C                   EVAL      KFSTTU  = VMINTP
     C                   EVAL      KFSOTX  = 'Fr.brevsnr ' + DFNAT
     C     UtTstFbr      ENDSR
