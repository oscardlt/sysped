     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A*%%TS  SD  20120417  124950  SVEIN       REL-V7R1M0  5770-WDS
     A            WSFIRM    R        H      REFFLD(FERRR/FEFIRM *LIBL/FERR)
     A            FECURR    R        H      REFFLD(FERRR/FECURR *LIBL/FERR)
     A            FEPRI1    R        H      REFFLD(FERRR/FEPRI1 *LIBL/FERR)
     A            FEPRI2    R        H      REFFLD(FERRR/FEPRI2 *LIBL/FERR)
SV5  A*           FEDAY     R        H      REFFLD(FERRR/FEDAY *LIBL/FERR)
SV5  A*           FEMONT    R        H      REFFLD(FERRR/FEMONT *LIBL/FERR)
SV5  A*           FEYEAR    R        H      REFFLD(FERRR/FEYEAR *LIBL/FERR)
     A            FEDATE    R        H      REFFLD(FERRR/FEDATE *LIBL/FERR)
     A            WSNR           1A  B  6  2VALUES(' ' '1')
     A            WSDATE         6Y 0B  6  4EDTCDE(4)
N01  A            WSFAJN         1   B  6 11VALUES(' ' 'N' 'J')
     A            WSTIME         4Y 0O  6 13EDTCDE(4)
     A            FEFROM    R        O  6 20REFFLD(FERRR/FEFROM *LIBL/FERR)
     A            FETO      R        O  6 26REFFLD(FERRR/FETO *LIBL/FERR)
     A            FELENG    R        O  6 32REFFLD(FERRR/FELENG *LIBL/FERR)
     A            FENAME        17A  O  6 36
     A            FETXT         80A  B  7  1
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20120417  130710  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0029)
     A                                      SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYTR16'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3783 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            FEAVD          4S 0O  2  2COLOR(WHT)
     A            FETUR          8S 0O  2  8COLOR(WHT)
     A            UURUND         8S 0O  2 18COLOR(WHT)
     A                                  2 67TIME
     A            ZRECNX         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV3784 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(045 DIV3785 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  4MSGCON(060 SHU0020 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2FROM    R        B  2  4REFFLD(FERRR/FEFROM *LIBL/FERR)
     A                                      COLOR(WHT)
     A            W2TO      R        B  2 10REFFLD(FERRR/FETO *LIBL/FERR)
     A                                      COLOR(WHT)
     A            W2LEN          3A  B  2 16COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
