     H DATEDIT(*DMY)
      *
      **************************************************************
      *** PROGRAM SKAL VEDL./SØKE VALUTAKURSER                   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-65 71-90       ***
      ***                    95-97                               ***
      *** INDIKATOR 99 PÅ = SØKING                               ***
      ***           99 AV = VEDLIKEHOLD                          ***
      **************************************************************
      *
     FFIRM      IF   E           K DISK
     FVALUAL01  UF A E           K DISK
     FV200AMFM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  VALTEK                 1     15
     D  XVTEK                  1      4
     D                 DS
     D  VALDAG                 1      2  0
     D  VALMND                 3      4  0
EV5  D  VALAAR                 5      8  0
     D  WSDT2                  1      6  0
     D                 DS
     D  WSVDAG                 1      2  0
     D  WSVMND                 3      4  0
     D  WSVAAR                 5      6  0
     D  WSDT                   1      6  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2VKOD        YVKOD             3
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     VALUF1        SETLL     VALUAL01
     C     START1        TAG
     C     FIFIRM        READE(N)  VALUAL01                               94
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   EXSR      KDTFM2
     C                   WRITE     SUBFIL
     C     FIFIRM        READE(N)  VALUAL01                               94
     C  N94              MOVE      VALKOD        YVKOD
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   WRITE     BUNN
     C                   END
     C                   SETOFF                                       3098
     C                   EXFMT     SUBCTL
     C*
     C*** AVSLUTT
     C     ST41          TAG
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY VALUTAKODE
     C   06              GOTO      START5
     C*** SØK FRA VALUTAKODE
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94VALUF3        SETLL     VALUAL01
     C  N94FIFIRM        READE(N)  VALUAL01                               94
     C                   GOTO      START2
     C                   END
     C*
     C                   EXSR      HENT
     C   98              GOTO      START3
     C   99              MOVE      VALKOD        UVKOD
     C   99              GOTO      SLUTT
     C*
     C*** SLETT POST
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     VALUF2        CHAIN     VALUAL01                           33
     C  N33              DELETE    VALUR
     C                   EXSR      MOVB1
     C                   MOVEL     '*DLT'        VALTEK
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ POST/ ENDRE POST
     C     START5        TAG
     C                   EXSR      KDTFM1
     C                   EXFMT     VARELI
     C                   EXSR      KDTFM1
     C                   SETOFF                                       30
     C                   MOVEA     '0000'        *IN(66)
     C   03              GOTO      SLUTT
     C   12              DO
     C   40              EXSR      KDTFM2
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
     C*
     C*** SØK FRA VALUTAKODE
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C* TEST VALUTAKODE
     C  N40              DO
     C     WSVKOD        COMP      *BLANKS                                70
     C  N70              MOVE      WSVKOD        VALKOD
     C   70              MOVE      'SPV0007'     MSGNR
     C   70              SETON                                        30
     C   70              GOTO      SLTB
     C                   END
     C*
     C* TEST VALUTA KURS KJØP
     C     WSVKU1        COMP      *ZEROS                                 68
     C   68              MOVE      'SPK0026'     MSGNR
     C   68              SETON                                        30
     C   68              GOTO      SLTB
     C*
     C* DERSOM SALGSKURS IKKE OPPGITT - KOPIER KJØPSKURS
     C     WSVKU2        IFEQ      *ZEROS
     C                   Z-ADD     WSVKU1        WSVKU2
     C                   END
     C*
     C* TEST VALUTA OMREGNINGSFAKTOR
     C     WSOFAK        COMP      *ZEROS                                 67
     C   67              MOVE      'SPF0012'     MSGNR
     C   67              SETON                                        30
     C   67              GOTO      SLTB
     C*
     C* TEST VALUTA TEKST
     C     WSVTEK        COMP      *BLANKS                                69
     C   69              MOVE      'SPT0010'     MSGNR
     C   69              SETON                                        30
     C   69              GOTO      SLTB
     C*
     C* TEST VALUTA DATO
     C     WSDT          COMP      *ZEROS                                 66
     C  N66              DO
     C                   MOVE      WSVDAG        UDT1
     C                   MOVE      WSVMND        UDT2
     C                   MOVE      WSVAAR        UDT3
     C                   CALL      'TESTDATO'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      66
     C                   END
     C   66              MOVE      'SPD0001'     MSGNR
     C   66              SETON                                        30
     C   66              GOTO      SLTB
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     VALUFK        CHAIN     VALUAL01                           33
     C                   EXSR      MOVWVA
     C  N33              UPDATE    VALUR
     C   33              WRITE     VALUR
     C                   EXSR      KDTFM2
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98
     CAN 99WSNR          IFEQ      '1'
     C                   GOTO      SLHENT
     C                   END
     C  N98
     CANN99WSNR          IFEQ      '2'
     C     XVTEK         COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      KDTFM2
     C                   EXSR      MOVVAW
     C                   MOVE      ' '           WSNR
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98
     CANN99WSNR          IFEQ      '4'
     C     XVTEK         COMP      '*DLT'                                 98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       AKTKOD
     C                   MOVE      *BLANKS       FIRMA
     C                   MOVE      *BLANKS       VALTEK
     C                   MOVE      *ZEROS        VALKU1
     C                   MOVE      *ZEROS        VALKU2
     C                   MOVE      *ZEROS        OMRFAK
     C                   MOVE      *ZEROS        VALDAG
     C                   MOVE      *ZEROS        VALMND
     C                   MOVE      *ZEROS        VALAAR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSVKOD
     C                   MOVE      *BLANKS       WSVTEK
     C                   MOVE      *ZEROS        WSVKU1
     C                   MOVE      *ZEROS        WSVKU2
     C                   MOVE      *ZEROS        WSOFAK
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVVAW        BEGSR
     C***********************************************
     C                   MOVE      VALKOD        WSVKOD
     C                   MOVE      VALTEK        WSVTEK
     C                   MOVE      VALKU1        WSVKU1
     C                   MOVE      VALKU2        WSVKU2
     C                   MOVE      OMRFAK        WSOFAK
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWVA        BEGSR
     C***********************************************
     C                   MOVE      'A'           AKTKOD
     C   33              MOVE      *ZEROS        JOURNR
     C   33              MOVE      FIFIRM        FIRMA
     C   33              MOVE      WSVKOD        VALKOD
     C                   MOVE      WSVTEK        VALTEK
     C                   MOVE      WSVKU1        VALKU1
     C                   MOVE      WSVKU2        VALKU2
     C                   MOVE      WSOFAK        OMRFAK
     C                   MOVE      WSVDAG        VALDAG
     C                   MOVE      WSVMND        VALMND
SV5  C*                    MOVE WSVAAR    VALAAR
NV5  C     WSVAAR        IFGT      50
NV5  C     WSVAAR        ADD       1900          VALAAR
NV5  C                   ELSE
NV5  C     WSVAAR        ADD       2000          VALAAR
NV5  C                   END
     C*
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C* HENT FIRMARECORD
     C                   MOVE      *BLANKS       FIFIRM
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C* DEKLARER KEYS
     C     VALUFK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSVKOD
     C     VALUF1        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    W2VKOD
     C     VALUF2        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    VALKOD
     C     VALUF3        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    YVKOD
     C*
     C     *ENTRY        PLIST
     C                   PARM                    USØK              1
     C                   PARM                    UVKOD             3
     C     USØK          COMP      'J'                                    99
     C*
     C* LEGG IN TEKST FOR HVILKET REGISTER SOM VEDLIKEHOLDES
     C     FIVALK        IFEQ      'S'
     C                   MOVEL     'Spedisjo'    WSTXT
     C                   MOVE      'n  '         WSTXT
     C                   ELSE
     C                   MOVEL     'Økonomi '    WSTXT
     C                   END
     C* INITIER VALUTADATO/NY/ENDRING- EVT. BRUKERENDRING "TEKKES MED"
     C                   MOVE      UDAY          WSVDAG
     C                   MOVE      UMONTH        WSVMND
     C                   MOVE      UYEAR         WSVAAR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM1        BEGSR
     C***********************************************
     C                   MOVE      WSVDAG        UDT1              2 0
     C                   MOVE      WSVMND        UDT2              2 0
     C                   MOVE      WSVAAR        UDT3              2 0
     C                   EXSR      KDTFMT
     C                   MOVE      UDT1          WSVDAG
     C                   MOVE      UDT2          WSVMND
     C                   MOVE      UDT3          WSVAAR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM2        BEGSR
     C***********************************************
     C                   MOVE      VALDAG        UDT1
     C                   MOVE      VALMND        UDT2
     C                   MOVE      VALAAR        UDT3
     C                   EXSR      KDTFMT
     C                   MOVE      UDT1          VALDAG
     C                   MOVE      UDT2          VALMND
     C                   MOVE      UDT3          VALAAR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
