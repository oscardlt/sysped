     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  4VALUES(' ' '2')
     A            KUNDNR         8Y 0O  4 12TEXT('Kundenummer')
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  4 21TEXT('Kundenavn')
     A            KCCCC          4Y 2O  4 53EDTWRD('  ,  ')
     A  99                                  DSPATR(ND)
     A            KCCMI          5Y 0O  4 59EDTCDE(4)
     A  99                                  DSPATR(ND)
     A            KCCVA          4Y 2O  4 67EDTWRD('  ,  ')
     A  99                                  DSPATR(ND)
     A                                  4 75'N'
     A N99                                  DSPATR(ND)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 57SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFI11'
     A                                      COLOR(BLU)
     A            MSG101        50A  O  1 19MSGID(DIV 1558 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A            MSG102        30A  O  2  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG103        15A  O  2 34MSGID(DIV 1963 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 71TIME
     A            MSG104        78A  O  3  2MSGID(DIV 1563 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG201        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG301        10A  O  2  2MSGID(DIV 1564 SYMSGF)
     A            KUNDNR         8Y 0O  2 13TEXT('Kundenummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MSG302        10A  O  2 23MSGID(DIV 1566 SYMSGF)
     A            WCCCC          4Y 2B  2 34EDTWRD('  ,  ')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG303        10A  O  2 41MSGID(DIV 1567 SYMSGF)
     A            WCCMI          5S 0B  2 52DSPATR(UL)
     A            MSG304        10A  O  2 61MSGID(DIV 1568 SYMSGF)
     A            WCCVA          4Y 2B  2 72EDTWRD('  ,  ')
     A                                      DSPATR(UL)
     A            MSG305        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG601        17A  O  2  2MSGID(DIV 1565 SYMSGF)
     A            W2NAVN        10A  I  2 20DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG602        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFI11'
     A                                      COLOR(BLU)
     A            MSG501        50A  O  1 19MSGID(DIV 1558 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A            MSG502        30A  O  2  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG503        15A  O  2 34MSGID(DIV 1963 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A            MSG504        78A  O  3  2MSGID(DIV 1563 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 42MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG510        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
