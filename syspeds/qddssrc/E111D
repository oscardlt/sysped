     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      ALTPAGEUP
     A                                      ALTPAGEDWN
     A                                      CF03(03 'End')
     A                                      CF12(12 'Cancel')
     A                                      HELP
      *
     A          R E111DB                    SFL
     A            MMN            9S 0H
     A            WMS           17A  H
     A            ZMOTT         15A  H
     A            ZNOSH          5A  H
     A            ZALT           1A  B  8  3VALUES(' ' 'd' 'D' '1' '4' '5' '6')
     A  41                                  DSPATR(ND)
     A  41                                  DSPATR(PR)
     A            ZAVS          15A  O  8  6
     A            ZDEP          10A  O  8 22
     A            ZDST          10A  O  8 33
     A            ZDT            8A  O  8 44
     A            ZREF          17   O  8 53
     A            ZBIL           9   O  8 71
      *
     A          R E111DC                    SFLCTL(E111DB)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 26MSGCON(029 EDI0446 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 74'E111D B'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(029 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  4MSGCON(008 EDI0701 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 15MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 26MSGCON(009 EDI0705 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 38MSGCON(010 EDI0706 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 44MSGCON(007 EDI0449 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(003 EDI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  6'Agent        '
     A                                      COLOR(WHT)
     A                                  7 22MSGCON(003 EDI0452 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 33MSGCON(004 EDI0453 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 44MSGCON(004 EDI0454 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 53MSGCON(009 EDI0191 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 71MSGCON(009 EDI0456 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E111DD
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 28MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E111DF
     A                                      OVERLAY
     A                                      CF04(04 'Søk agent')
     A            MSGNR          7A  P
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 27MSGCON(029 EDI0446 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 74'E111D F'
     A                                      COLOR(BLU)
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 33MSGCON(015 EDI0460 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  5  2MSGCON(036 EDI0425 *LIBL/SYMSGF)
     A            ÆSAVD          4Y 0B  5 41EDTCDE(4)
     A  31                                  DSPATR(PC)
     A  31                                  COLOR(RED)
     A                                  6  4MSGCON(034 EDI0498 *LIBL/SYMSGF)
     A            ÆSSG           3A  B  6 41
     A  32                                  DSPATR(PC)
     A  32                                  COLOR(RED)
     A                                  7  4MSGCON(034 EDI0463 *LIBL/SYMSGF)
     A            ÆWRKOP         1A  B  7 41VALUES(' ' 'J' 'N' 'Y')
     A                                  7 55MSGCON(006 EDI0607 *LIBL/SYMSGF)
     A                                  9  4MSGCON(034 EDI0465 *LIBL/SYMSGF)
     A            ÆTURNR         8Y 0B  9 41EDTCDE(4)
     A  34                                  DSPATR(PC)
     A  34                                  COLOR(RED)
     A                                  9 55MSGCON(021 EDI0466 *LIBL/SYMSGF)
     A                                 10  6MSGCON(032 EDI0467 *LIBL/SYMSGF)
     A            ÆAGNR          8Y 0B 10 41EDTCDE(4)
     A  35                                  DSPATR(PC)
     A  35                                  COLOR(RED)
     A                                 11  6MSGCON(032 EDI0468 *LIBL/SYMSGF)
     A            ÆEGN          11A  B 11 41
     A  36                                  DSPATR(PC)
     A  36                                  COLOR(RED)
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(014 EDI0804 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 32MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E111DG                    SFL
     A            XALT           1A  B 10  3VALUES(' ' '2' '4')
     A            XEAVD          4S 0O 10  5
     A            XEOPD          7S 0O 10 10
     A            XEPOS          7   O 10 18
     A            XENAS         27   O 10 26
     A            XENAK         27   O 10 54
      *
     A          R E111DH                    SFLCTL(E111DG)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  81N83N84                            ROLLUP(25)
     A  81N83N84                            ROLLDOWN(26)
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND(*MORE)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 26MSGCON(031 EDI0446 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 74'E111D G'
     A                                      COLOR(BLU)
     A            XRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(010 EDI0471 *LIBL/SYMSGF)
     A            ÆTURNR         8Y 0O  3 13EDTCDE(4)
     A                                  3 24MSGCON(010 EDI0472 *LIBL/SYMSGF)
     A            XAVS          15A  O  3 35
     A                                  3 53MSGCON(004 EDI0479 *LIBL/SYMSGF)
     A            XDEP          10A  O  3 58
     A                                  4 24MSGCON(010 EDI0474 *LIBL/SYMSGF)
     A            XMOTT         15A  O  4 35
     A                                  4 53MSGCON(004 EDI0475 *LIBL/SYMSGF)
     A            XDST          10A  O  4 58
     A                                  6  2MSGCON(029 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  4MSGCON(007 EDI0702 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7 14MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  9  2MSGCON(003 EDI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  6MSGCON(003 EDI0484 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9 10MSGCON(005 EDI0483 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9 18'Pos'
     A                                      COLOR(WHT)
     A                                  9 26MSGCON(008 EDI0481 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9 54MSGCON(008 EDI0482 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E111DI
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                 23  2MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
