      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED GARANTI BEGRENSNING        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FTRUHGB    UF A E           K DISK
     FKODTS2    IF   E           K DISK
     FTR017D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UGBGU         IFNE      *BLANKS
     C                   MOVE      UGBGU         TGGBGU
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     TRHGBK        SETLL     TRUHGB
     C     TRHGBK        READE(N)  TRUHGB                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     TRHGBK        READE(N)  TRUHGB                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C   03
     COR 12              GOTO      SLUTT
      *
     C   04              DO
     C                   CALL      'SYFI10L'
     C                   PARM                    W2GBGU
     C                   PARM                    USPED
     C                   GOTO      START3
     C                   END
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C                   EXSR      CRTGB
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     TRHGBK        SETLL     TRUHGB
     C     TRHGBK        READE     TRUHGB                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    TRUHGBR
     C     TRHGBK        READE     TRUHGB                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UGBGU
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     TGGBGU        IFNE      '*D'
     C                   MOVE      TGGBGU        UGBGU
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     TGGBGU        IFNE      '*D'
     C     UGBGU         IFEQ      *BLANKS
     C                   MOVE      TGGBGU        UGBGU
     C                   ELSE
     C                   WRITE     TRUHGBR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     TGGBGU        COMP      '*D'                                   98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGGB
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     TGGBGU        COMP      '*D'                                   98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTGB
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTGB         BEGSR
     C***********************************************
     C                   MOVE      W2GBGU        KS2LK
     C     KODS2K        CHAIN     KODTS2                             90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'SPL0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLCRT
     C                   END
      *
     C                   MOVE      W2GBGU        TGGBGU
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *BLANKS       W2GBGU
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGGB         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      TGGBGU        WSGBGU
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVE      '0'           *IN90
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   CALL      'SYFI10L'
     C                   PARM                    WSGBGU
     C                   PARM                    USPED
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C                   MOVE      WSGBGU        KS2LK
     C     KODS2K        CHAIN     KODTS2                             90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'SPL0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
      *
     C                   MOVE      WSGBGU        TGGBGU
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTGB         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      '*D'          TGGBGU
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TRHGBK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     KODS2K        KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    KS2LK
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UGBGU             2
     C                   PARM                    UTYPE             3
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      'S2 '         KS2UNI
     C                   MOVE      'J'           USPED             1
     C                   MOVE      UAVD          TGAVD
     C                   MOVE      UTDN          TGTDN
     C     UTYPE         COMP      'DSP'                                  27
      *
     C                   ENDSR
      *
