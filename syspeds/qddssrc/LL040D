********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P06XXX* 01 YBC PTF6 F7=ENDRE HEADING AV PLUKKSEDDEL
P8XXX * 02 YBC PTF8 LISTENR ER ENDRET FRA 5.0 TIL 7.0
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R BILDE01
     A                                      CA03(03 'Exit')
E01  A                                      CF05(05 'Endre HEADING')
N01  A                                      CF07(07 'Endre LINJE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL040R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(50 YBC1488 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  5  2MSGCON(19 YBC1489 SYMSGF)
E02  A            WSALN          7Y 0B  5 22DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(19 YBC1490 SYMSGF)
     A            WSPS           1A  B  7 22DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  7 32'(J/N)'
     A                                  8  5MSGCON(16 YBC1491 SYMSGF)
     A            WSPRTP        10A  B  8 22DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                 10  2MSGCON(19 YBC1492 SYMSGF)
     A            WSKLL          5  0B 10 22DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(19 YBC1493 SYMSGF)
     A            WSBPAL         5  0B 11 22DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(19 YBC1494 SYMSGF)
     A            WSAPAL         5  0B 12 22DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 14  2MSGCON(19 YBC1495 SYMSGF)
     A            WSML           1A  B 14 22DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 14 32'(J/N)'
     A                                 15  5MSGCON(16 YBC1491 SYMSGF)
     A            WSPRTM        10A  B 15 22DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                 17  2MSGCON(19 YBC1496 SYMSGF)
     A            WSMLP          5  0B 17 22DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(78 YBC1497 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
