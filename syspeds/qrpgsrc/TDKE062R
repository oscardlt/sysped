      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKE062R MODULE(*LIBL/TDKE062R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKEFZL01  UF A E           K DISK    USROPN
     F                                     RENAME(DKEFR:DKEFZR)
     FDKEVZL01  UF A E           K DISK    USROPN
     F                                     RENAME(DKEVR:DKEVZR)
     FDKEF      O  A E             DISK    USROPN
     FDKEV      IF A E           K DISK    USROPN
     FDKTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D SYAVA           S              4
     D SYAV            S              4  0
     D SYOPA           S              7
     D SYOP            S              7  0
     D WMODE           S              1
     D UNIKA           S              9
     D REFF            S             17
     D JSONstring      S          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬unik¬ : ¬'+%trim(UNIKA)+'¬, '
        +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
        +'¬avd¬ : ¬'+%trim(SYAVA)+'¬, '
        +'¬opd¬ : ¬'+%trim(SYOPA)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errmsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"updexternfakt" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKEFZL01
     C  N50              CLOSE     DKEVZL01
     C  N50              CLOSE     DKEF
     C  N50              CLOSE     DKEV
     C  N50              CLOSE     DKTVK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      SYAVA = zhbgetvar('AVD')
     C                   EVAL      SYAV  = c2n2(SYAVA)
     C                   EVAL      SYOPA = zhbgetvar('OPD')
     C                   EVAL      SYOP  = c2n2(SYOPA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      REFF  = zhbgetvar('REFF')
     C                   EVAL      UNIKA   = zhbgetvar('UNIK')
     C                   EVAL      UNIK  = c2n2(UNIKA)
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C     *LIKE         DEFINE    DKEF_UNIK     UNIK
     C     *LIKE         DEFINE    DKVK_DTS      ZKVK_DTS
     C     *LIKE         DEFINE    DKEV_SYLI     ZKEV_SYLI
     C                   EVAL      ZKVK_DTS=99999999
     C     DKEFKEY       KLIST
     C                   KFLD                    UNIK
     C                   KFLD                    REFF
     C     DKTVKKEY      KLIST
     C                   KFLD                    DKEF_VAKD
     C                   KFLD                    ZKVK_DTS
     C     DKEVKEY       KLIST
     C                   KFLD                    DKEF_SYAV
     C                   KFLD                    DKEF_SYOP

      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKEFZL01
     C  N50              OPEN      DKEVZL01
     C  N50              OPEN      DKEF
     C  N50              OPEN      DKEV
     C  N50              OPEN      DKTVK

     c                   endsr
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hæmta extern faktura
     C     DKEFKEY       CHAIN     DKEFZL01                           55
      * MODE D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    DKEFZR

     C     DKEFKEY       SETLL     DKEVZL01
     C     DKEFKEY       READE     DKEVZL01                               56
     C     *IN56         DOWEQ     *OFF
     C                   DELETE    DKEVZR
     C     DKEFKEY       READE     DKEVZL01                               56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      DKEF_SYAV=SYAV
     C                   EVAL      DKEF_SYOP=SYOP
     C                   IF        DKEF_FATY=*BLANKS
     C                   EVAL      DKEF_FATY='N380'
     C                   ENDIF
     C     DKTVKKEY      SETGT     DKTVK
     C                   READP     DKTVK                                  57
     C     *IN57         IFEQ      *OFF
     C                   EVAL      DKEF_VAKU=DKVK_KRS
     C                   ENDIF
     C                   UPDATE    DKEFZR
     C                   WRITE     DKEFR

      * Hitta høgsta varupostnummret
     C                   EVAL      *IN57=*OFF
     C     DKEVKEY       SETLL     DKEV
     C     DKEVKEY       READE     DKEV                                   57
     C     *IN57         DOWEQ     *OFF
     C                   EVAL      ZKEV_SYLI=DKEV_SYLI
     C     DKEVKEY       READE     DKEV                                   57
     C                   ENDDO
      * Skriv til produktionsfil
     C     DKEFKEY       SETLL     DKEVZL01
     C     DKEFKEY       READE     DKEVZL01                               56
     C     *IN56         DOWEQ     *OFF
     C                   IF        DKEV_SYAV=*ZEROS
     C                   EVAL      DKEV_SYAV=SYAV
     C                   EVAL      DKEV_SYOP=SYOP
     C                   ADD       1             ZKEV_SYLI
     C                   EVAL      DKEV_SYLI=ZKEV_SYLI
     C                   EVAL      DKEV_32=ZKEV_SYLI
     C                   UPDATE    DKEVZR
     C                   EXSR      SRUP
     C                   ENDIF
     C     DKEFKEY       READE     DKEVZL01                               56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update, skriv till produktionsfiler              SRUP
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUP          BEGSR
     C                   WRITE     DKEVR
     C                   ENDSR
