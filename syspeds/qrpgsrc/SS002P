      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/SS002P MODULE(*LIBL/SS002P *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 LES POST I WRKCGI MED KEY
10XXX * 02 YBC PTF10 OPPDATER ORSNM PÅ WEB
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 SJEKK HVEM MOTTAR LISTE FØRST
11XXX * 04 YBC PTF11 AVSENDERS EPOSTADR. HENTES FRA SORT=0
********************************************************************
      *********************************************************************
      *ENDRINGER:
ODDEV *ODDEV   PTF9 ODDEVEN-logikk
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           k disk    usropn
     FWRKCGI    IF   E           K DISK    usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D WSuser          s             10
     D WINT            S              6
     D KEY2            s             30
     D WSKDB           s              5
     D WSNAB           s             30
     D WSSTD           s              5
     D SEND            s              1
     D WSNEDDT         s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FEILTXT         s             80
     D ANTPO           s              5  0
     D TOTCLI          s              5  0
     D TOTWT           s              7  1
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D PAVRDA                  1     50
     D PAVRDA1_5               1      5
     D PAVRDA6_50              6     50
     D                 DS
     D WSSHCO                  1    540
     D WSSHTNA                 1     30
     D WSSHTA1                31     60
     D WSSHTA2                61     90
     D WSSHTA3                91    120
     D WSSHTA4               121    150
     D WSSHTA5               151    180
     D WSCOMM1               181    240
     D WSCOMM2               241    300
     D WSCOMM3               301    360
     D WSCOMM4               361    420
     D WSCOMM5               421    480
     D WSCOMM6               481    540
     D WSdummy               541    541
     D                 DS
     D  Wkey2                  1     30
     D  Wkey2_10               1     10
     D                 DS
     D  DataDs                 1    220
     D  POCONO                 1     20
     D  ORLC                  25     29
     D  ORSNM                 31     60
     D  ORDTL                 63     70  0
     D  ORSTAT                72    101
     D  ORCLI                103    107  0
     D  ORWT                 111    117  1
     D                 DS
     D  TimeTmp                1     26
     D  TimeTmpÅ               1      4
     D  TimeTmpM               6      7
     D  TimeTmpD               9     10
     D  TimeTmpT              12     16
     D  TimeTmpTT             12     13
     D  TimeTmpTM             15     16
     D  TimeTmpTS             18     18
     D                 DS
     D  Idag                   1      8
     D  IdagTmpÅ               1      4
     D  IdagTmpM               5      6
     D  IdagTmpD               7      8
     D                 DS
     D  IdagTID                1      6
     D  IdagTT                 1      2
     D  IdagTM                 3      4
     D  IdagTTM                3      4
     D  IdagTS                 5      6
     D                 DS
     D  TID                    1     16
     D  Time1D                 1      2
     D  Time1P1                3      3
     D  Time1M                 4      5
     D  Time1P2                6      6
     D  Time1Å                 7     10
     D  Time1B                11     11
     D  Time1T                12     16
     D  Time1P3               14     14
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
     C* HVIS send (submit fra side som alt er vist.) generer pdf
     C     SEND          ifeq      'Y'
     C                   move      'E'           WSDUMMY
     C                   move      'E'           WSEND             1
     C                   call      'SS002PC'
     C                   parm                    KEY2
     C                   parm                    WSuser
     C                   parm                    WSKDB
     C                   parm                    WSNAB
     C                   parm                    WSSTD
N04  C                   parm                    MailAVS
     C                   parm                    Mail1
     C                   parm                    Mail2
     C                   parm                    Mail3
     C                   parm                    Mail4
     C                   parm                    Mail5
     C                   parm                    WSNEDDT
     C                   parm                    WSSHCO
     C                   parm                    WSEND
N01  C                   parm                    WINT
     c                   goto      slutt
     c                   end
      * Hvis IKKE send - vis liste
      * Vis liste over registrete leveringer
     C                   Move      *zeros        AntPO
     C                   Move      *zeros        TOTCLI
     C                   Move      *zeros        TOTWT
     c     WCgiKey       setLL     WRKCGI
S01  c*                  Read      WRKCGI                                 35
N01  C     WINTEG        READE     WRKCGI                                 35
     C   35              CLEAR                   WRKCGIR
     C  N35Wkey2_10      comp      XBCDLC                             3535
     c     *IN35         doweq     '0'
     C                   movel     WDATA         DataDs
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntPO
     C                   Add       ORCLI         TOTCLI
     C                   Add       ORWT          TOTWT
S01  c*                  Read      WRKCGI                                 35
N01  C     WINTEG        READE     WRKCGI                                 35
     C  N35Wkey2_10      comp      XBCDLC                             3535
     c                   end
      *
     C     SLUTT         TAG
     C                   exsr      SetBot
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
     C                   eval      KEY2     = zhbgetvar('KEY2')
     C                   eval      WSuser   = zhbgetvar('user')
     C                   eval      WINT     = zhbgetvar('WINT')
     C                   eval      WINTEG  = c2n2(WINT)
     C                   eval      WSKDB    = zhbgetvar('WSKDB')
     C                   eval      WSNAB    = zhbgetvar('WSNAB')
     C                   eval      WSSTD    = zhbgetvar('WSSTD')
     C                   eval      SEND     = zhbgetvar('SEND')
     C                   eval      WSNEDDT   = zhbgetvar('WSNEDDT')
     C                   eval      WSSHTNA   = zhbgetvar('WSSHTNA')
     C                   eval      WSSHTA1   = zhbgetvar('WSSHTA1')
     C                   eval      WSSHTA2   = zhbgetvar('WSSHTA2')
     C                   eval      WSSHTA3   = zhbgetvar('WSSHTA3')
     C                   eval      WSSHTA4   = zhbgetvar('WSSHTA4')
     C                   eval      WSSHTA5   = zhbgetvar('WSSHTA5')
     C                   eval      WSCOMM1   = zhbgetvar('WSCOMM1')
     C                   eval      WSCOMM2   = zhbgetvar('WSCOMM2')
     C                   eval      WSCOMM3   = zhbgetvar('WSCOMM3')
     C                   eval      WSCOMM4   = zhbgetvar('WSCOMM4')
     C                   eval      WSCOMM5   = zhbgetvar('WSCOMM5')
     C                   eval      WSCOMM6   = zhbgetvar('WSCOMM6')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   close     BRPARF
     C                   CLOSE     WRKCGI
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SS002P':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('SEND':SEND)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('WINT':WINT)
     C                   callp     updhtmlvar('WSKDB':WSKDB)
     C                   callp     updhtmlvar('WSNAB':WSNAB)
     C                   callp     updhtmlvar('WSSTD':WSSTD)
N04  C                   callp     updhtmlvar('MailAVS':MailAVS)
     C                   callp     updhtmlvar('Mail1':Mail1)
     C                   callp     updhtmlvar('Mail2':Mail2)
     C                   callp     updhtmlvar('Mail3':Mail3)
     C                   callp     updhtmlvar('Mail4':Mail4)
     C                   callp     updhtmlvar('Mail5':Mail5)
     C                   callp     updhtmlvar('WSNEDDT':'')
     C                   callp     updhtmlvar('WSSHTNA':'')
     C                   callp     updhtmlvar('WSSHTA1':'')
     C                   callp     updhtmlvar('WSSHTA2':'')
     C                   callp     updhtmlvar('WSSHTA3':'')
     C                   callp     updhtmlvar('WSSHTA4':'')
     C                   callp     updhtmlvar('WSSHTA5':'')
     C                   callp     updhtmlvar('WSCOMM1':'')
     C                   callp     updhtmlvar('WSCOMM2':'')
     C                   callp     updhtmlvar('WSCOMM3':'')
     C                   callp     updhtmlvar('WSCOMM4':'')
     C                   callp     updhtmlvar('WSCOMM5':'')
     C                   callp     updhtmlvar('WSCOMM6':'')
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   callp     updhtmlvar('ORLC':ORLC)
     C                   callp     updhtmlvar('ORSTAT':ORSTAT)
N02  C                   callp     updhtmlvar('ORSNM':ORSNM)
     C                   callp     updhtmlvar('ORDTL':
     C                             %trim(%editW(ORDTL:'    .  .  ')))
     C                   callp     updhtmlvar('ORCLI':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   callp     updhtmlvar('ORWT':
     C                             %trim(%editc(ORWT:'4')))
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$bot
      *=====================================================================
     C     Setbot        begsr
     C                   callp     updHTMLvar('ANTPO':
     C                             %trim(%editc(ANTPO:'Z')))
     C                   callp     updHTMLvar('TOTCLI':
     C                             %trim(%editc(TOTCLI:'Z')))
     C                   callp     updHTMLvar('TOTWT':
     C                             %trim(%editc(TOTWT:'4')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      WRKCGI
      *
     C     WCgiKey       klist
     C                   kfld                    WINTEG
     C                   kfld                    WKEY2
     C                   move      KEY2          WKEY2
     C                   movel     KEY2          XBCDLC           10
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Hent MAILTO-ADRESSE (sett . i pos 51 for at variabelen ikke skal bli crappet)
N04  C                   EVAL      MailAVS = *BLANKS
     C                   EVAL      Mail1 = *BLANKS
     C                   EVAL      Mail2 = *BLANKS
     C                   EVAL      Mail3 = *BLANKS
     C                   EVAL      Mail4 = *BLANKS
     C                   EVAL      Mail5 = *BLANKS
     c                   Move      '.'           Mail1
     c                   Move      '.'           Mail2
     c                   Move      '.'           Mail3
     c                   Move      '.'           Mail4
     c                   Move      '.'           Mail5
     C                   move      *zeros        MNR               3 0
     C                   MOVE      'SSPLM'       PAID
N03   *
N03  C                   MOVE      'N'           XEGENEP           1
N03  C     BrParKey      SETLL     BRPARF
N03  C     BrParKey      READE     BRPARF                                 33
N03  C                   DOW       *IN33 = *OFF
N03  C                   IF        PAVRDA1_5 <> *BLANKS AND PAVRDA1_5 = WSSTD
N03  C                   EVAL      XEGENEP = 'J'
N03  c                   LEAVE
N03  c                   END
N03  C     BrParKey      READE     BRPARF                                 33
N03  c                   ENDDO
N03   *
     C     BrParKey      SETLL     BRPARF
     C     BrParKey      READE     BRPARF                                 33
     C     *IN33         DOWEQ     '0'
N03  c                   IF        XEGENEP = 'J'
S04  C*                  IF        PAVRDA1_5 = *BLANKS OR PAVRDA1_5 <> WSSTD
N04  C                   IF        (PAVRDA1_5 = *BLANKS OR PAVRDA1_5 <> WSSTD)
N04  C                             AND PASORT <> 0
N03  c                   goto      NextMadr
N03  c                   END
N03  c                   ELSE
     c     PAVRDA1_5     IFNE      *blanks
     c     PAVRDA1_5     ANDNE     WSSTD
N04  c     PASORT        ANDEQ     0
     c                   goto      NextMadr
     c                   end
N03  c                   END
N04  C                   IF        PASORT = 0 AND MAILAVS = *BLANKS
N04  C                   MOVEL     PAVRDA6_50    MailAVS          51
N04  C                   ELSE
     C                   ADD       1             MNR
     C                   SELECT
     C     MNR           wheneq    1
     c                   MoveL     PAVRDA6_50    Mail1            51
     C     MNR           wheneq    2
     c                   MoveL     PAVRDA6_50    Mail2            51
     C     MNR           wheneq    3
     c                   MoveL     PAVRDA6_50    Mail3            51
     C     MNR           wheneq    4
     c                   MoveL     PAVRDA6_50    Mail4            51
     C     MNR           wheneq    5
     c                   MoveL     PAVRDA6_50    Mail5            51
     c                   endsl
N04  C                   END
     c     NextMadr      tag
     C     BrParKey      READE     BRPARF                                 33
     C                   END
N04   *
N04  C                   IF        MailAVS = *BLANKS
N04  C                   EVAL      MailAVS = MAIL1
N04  C                   END
N04  c                   Move      '.'           MailAVS
N04  C                   IF        Mail1 = *BLANKS
N04  C                   EVAL      Mail1 = MAILAVS
N04  C                   END
      *
     C                   move      *zeros        ORDTL
     C                   move      *zeros        ORCLI
     C                   move      *zeros        ORWT
     C                   endsr
      *
