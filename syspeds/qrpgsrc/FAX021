      *
      *
     Ffaxlog02  IF   E           k DISK
     FCUNDC     IF   E             DISK
     FFAX021FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     FFIRM      IF   E           K DISK
     FFAXLK     IF   E           K DISK
     FFAXMANP   O  A E           K DISK    USROPN
     FFAXMANL1  IF   E           K DISK
     F                                     RENAME(FAXMANR:FAXMARL1)
     FFAXMANT   UF A E           K DISK
     FxROSL01   UF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FKUNDL01   UF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR2)
     FUNITL01   IF   E           K DISK
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  F1                     1     21
     D                                     DIM(21)
     D  FAXNRP                 1     21
     D                 DS
     D  USØKKN                 1     30
     D  USØKKO                 1      2
     D  URRN                   3     12
     D  epofax                13     13
     D                 DS
     D  F2                     1     15
     D                                     DIM(15)
     D  FAXNR                  1     15
     D  FAX05A                 1      4
     D                 DS
     D  F3                     1      6
     D                                     DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D                 DS
     D  UDATO                  1      8  0
     D  UACC                   1      2  0
     D  UAAR                   3      4  0
     D  UMND                   5      6  0
     D  UDAG                   7      8  0
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyz')
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     C                   EXSR      INIT
     C   40
     CAN 30              MOVE      'N'           UKODE
     C   40
     CAN 30              GOTO      SLUTT
     C     FEIL          TAG
     C*
     C     FRA           IFEQ      *BLANK
     C                   MOVEL     UNVN          FRA
     C                   END
     C*
     C     STARTB        TAG
     C     SIDE          IFEQ      'A'
     C                   EXFMT     BILDE01
     C   25              MOVE      'B'           SIDE
     C   25              GOTO      STARTB
     C                   ELSE
     C                   EXFMT     BILDE02
     C   25              MOVE      'A'           SIDE
     C   25              GOTO      STARTB
     C                   END
     C   40WSSKRI        ifeq      'Y'
     C                   MOVE      UNIK          UNIKA             9
     C                   EXSR      SKRIVS
     c                   move      ' '           wsskri
     C                   END
     C   40
     CANN03              GOTO      FEIL
     C                   EXSR      KDTFMT
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     FORT          TAG
     C*
     C   03              GOTO      SLUTT
     C*
     C*
     C* SØKING
     C*
     C*
     C   05              DO
     C                   MOVE      'S'           SOKTYP
     C*  Navn-søk
     C*  --------
     C                   MOVEL     KNAVN         UUSOEK
     C                   MOVEL     'J'           UPLUKK            1
     C                   MOVE      FIFIRM        UFIRMA
     C                   CALL      'SS0022'
     C                   PARM                    UFIRMA            2
     C                   PARM                    USELGE            3
     C                   PARM                    UPLUKK            1
     C                   PARM                    UUSOEK           10
     C                   PARM                    KUNDNR            8 0
     C                   PARM                    KNAVN
     C                   PARM                    UTIL
     C                   PARM                    UTLF             21
     C                   PARM                    UFAX             21
     C                   PARM                    UMODE             1
     C                   PARM                    URETUR            1
      *
     C                   END
     C   04              DO
     c                   move      *blanks       EPOFAX
     C     LINNBR        IFGE      5
     c     til           ifne      *blanks
     C     LO:UP         XLATE     til           usok
     c                   else
     C     LO:UP         XLATE     EPOST         usok
     c                   end
     C                   CALL      'VCUNDC2R'
     C                   PARM                    FIFIRM
     C                   PARM                    USOK             30
     C                   PARM                    URRN2            10 0
     c                   parm                    epofax            1
     C     URRN2         IFNE      *ZEROS
     C                   MOVE      URRN2         URRN
     C                   MOVE      'KP'          USØKKO
     c                   else
     c                   movel     usok          epost
     C                   END
     C                   ELSE
     C*
     C                   MOVE      KNAVN         USØKKN
     C                   MOVE      'L'           SOKTYP
     C                   CALL      'SOEKKUN'
     C                   PARM                    KUNDNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   END
     C*
     C* RETUR MED KONTAKTPERS
     C     USØKKO        IFEQ      'KP'
     C                   MOVE      URRN          RRN              10 0
     C     RRN           CHAIN     CUNDC                              31
     c     epofax        ifne      'E'
     C  N31              MOVEL     CFAX          FAXNR
     c                   else
     c                   move      *blanks       faxnr
     c                   end
     c     epofax        ifne      'F'
     C  N31              MOVEL     CEMAIL        EPOST
     c                   else
     c                   move      *blank        epost
     c                   end
     C  N31              MOVE      CCOMPN        KUNDNR
     c  n31til           ifeq      *blanks
     c                   movel     cconta        til
     c                   end
     C                   END
     C     KUNKEY        CHAIN(N)  CUNDL01                            31
     C  N31WSLAND        IFEQ      *BLANKS
     C                   MOVE      SYLAND        WSLAND
     C                   END
     C  N31FAXNR         IFEQ      *BLANKS
     c     epofax        ifne      'E'
     C                   MOVE      TFAXNR        FAXNR
     C                   end
     C                   END
     c                   z-add     8             linnbr
     c                   z-add     18            posnbr
     C                   END
      *
      * SØK I BILREGISTER
     C     *IN09         IFEQ      '1'
     C     *IN60         ANDEQ     '1'
     C                   MOVE      *BLANKS       TFAXNR
     C                   MOVE      *ZEROS        KUNDNR
     C                   MOVEL     KNAVN         UUSOEK
     C* HVIS BILNR ANGITT-SJEKK GYLDIGHET
     C     UUSOEK        IFNE      *BLANKS
     C     UUSOEK        CHAIN     UNITL01                            33
     C   33              MOVE      *BLANKS       UUSOEK
     C                   END
     C* GYLDIG BILNR IKKE GITT I KNAVN-LINJA - SØK I BILREGISTER
     C     UUSOEK        IFEQ      *BLANKS
     C                   CALL      'SYTR19'
     C                   PARM                    UUSOEK
     C                   END
     C* GYLDIG BILNR ER TILSTEDE- VIS EVT AUTOPLUKK FAXNR+TEKST
     C     UUSOEK        IFNE      *BLANKS
     C                   MOVE      'P'           UMODUS            1
     C                   CALL      'SYTR09'
     C                   PARM                    UUSOEK
     C                   PARM                    TFAXNR
     C                   PARM                    UTEXT            20
     C                   PARM                    UMODUS
     C*
     C     TFAXNR        IFNE      *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     C                   MOVEL     UUSOEK        KNAVN
     C                   MOVE      UTEXT         KNAVN
     C                   END
     C                   END
     C                   END
     C*
     C*
     C* TESTER BILDET
     C*
     C                   EXSR      TESTB1
     C   30              GOTO      FEIL
     C*
     C*
     C* SEND DOKUMENT
     C*
     C   10              DO
     C* TILDELER DOKUMENTNAVN
     C*
     C* SENDER AVGÅRDE FAXEN
     C*
     C     FAXRET        IFNE      *BLANKS
     C                   EXSR      FAXFIX
     C                   ELSE
     C                   MOVEL     FAXNR         FAXNRP
     C                   END
     C     FIFIRM        CHAIN     FAXMANT                            44
     C   44              Z-ADD     1             UNIK
     C  N44              ADD       1             UNIK
     C   44              WRITE     FAXMANTR
     C  N44              UPDATE    FAXMANTR
     C                   OPEN      FAXMANP
     C                   MOVE      UDATO         PUDATE
     C                   EXCEPT    SKRIV
     C                   CLOSE     FAXMANP
     C                   MOVE      UNIK          UNIKA             9
     C     EPOST         IFNE      *BLANKS
     C                   MOVE      'J'           EPOSTJ            1
     C                   END
     C                   CALL      'FAX022CL'
     C                   PARM                    UNIKA
     C                   PARM                    FAXNRP
     C                   PARM                    PDATE
     C                   PARM                    PTIME
     C                   PARM                    KNAVN
     C                   PARM                    EPOST
     C                   PARM                    EPOSTJ
     C                   PARM                    EMNE
     C*
     C* PRINTER FAXKSEN HVIS ØNSKELIG SOM ET KOPIBILAG
     C*
     C     WSSKRI        IFEQ      'Y'
     C                   EXSR      SKRIVS
     C                   END
     C*
     C                   EXSR      BL
     C                   END
     C                   GOTO      FEIL
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C***********************************************
     C     BL            BEGSR
     C***********************************************
     C                   CLEAR                   BILDE01
     C                   CLEAR                   BILDE02
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      'SHU0171'     MSGNR
     C                   SETON                                        30
     C                   ENDSR
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   SETOFF                                       713073
     C                   MOVEA     '00'          *IN(96)
      *
     C* TESTER VED UTFYLLT KUNDNUMMER
      *
     C     KUNDNR        IFNE      *ZEROS
     C     SOKTYP        IFEQ      'S'
     C     KUNKEY        CHAIN(N)  xROSL01                            70
     C  N70KNAVN         IFEQ      *BLANKS
     C                   MOVEL     PNAME         KNAVN
     C                   ENDIF
     C  N70WSLAND        IFEQ      *BLANKS
     C                   MOVE      PCOUNT        WSLAND
     C                   END
     C  N70FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   MOVE      PFAXNO        FAXNR
     C                   END
     C                   END
     C     SOKTYP        IFEQ      'L'
     C     KUNKEY        CHAIN(N)  CUNDL01                            70
     C  N70WSLAND        IFEQ      *BLANKS
     C                   MOVE      SYLAND        WSLAND
     C                   END
     C  N70FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     C                   END
     C                   END
     C   70              MOVE      'SPK0001'     MSGNR
     C   70              SETON                                        30
     C*
     C     TIL           IFEQ      *BLANKS
     C                   MOVEL     UTIL          TIL
     C                   ENDIF
     C     FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   MOVEL     UFAX          FAXNR
     C                   ENDIF
     C                   END
     C*
     C     FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   SETON                                        71
     C   71              MOVE      'SHU0165'     MSGNR
     C   71              SETON                                        30
     C                   END
     C*
      *
      * BÅDE FAX OG EPOST-ADRESSE LAGT INN
     C     FAXNR         IFNE      *BLANKS
     C     EPOST         ANDNE     *BLANKS
     C                   SETON                                        3071
     C   30              MOVE      'SHU0538'     MSGNR
     C   30              GOTO      SLTB
     C                   END
      *
     C*
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        ANDEQ     *ZEROS
     C                   MOVE      'SHU0388'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C     WSTIME        IFEQ      *ZEROS
     C     WSDATE        ANDNE     *ZEROS
     C                   MOVE      'SHU0388'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C     WSTIME        IFLT      0
     C     WSTIME        ORGT      235959
     C                   MOVE      'SHU0389'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C*** TEST DATO
     C     WSDATE        IFNE      *ZEROS
     C                   CALL      'TESTDATO'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      96
     C   96
     CANN30              MOVE      'SPD0001'     MSGNR
     C   96              SETON                                        30
     C   30              GOTO      SLTB
     C                   END
     C*
     C*
     C* OPPDATERER KUNDEREGISTER
     C*
     C  N30KUNDNR        IFNE      *ZEROS
     C     FAXNR         IFNE      *BLANKS
     C     SOKTYP        IFEQ      'S'
     C     PFAXNO        IFEQ      *BLANKS
     C     KUNKEY        CHAIN     xROSL01                            33
     C  N33              MOVE      FAXNR         PFAXNO
     C  N33              EXCEPT    UPD
     C                   END
     C                   END
     C     SOKTYP        IFEQ      'L'
     C     FAXNR         IFNE      *BLANKS
     C     TFAXNR        IFEQ      *BLANKS
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD1
     C     KUNKEY        CHAIN     KUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD2
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C*
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        IFNE      *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVEL     *BLANKS       PDATE
     C                   MOVEL     WSTIME        PTIME
     C                   MOVEL     WSDATE        PDATE
     C                   END
     C                   END
     C     WSLAND        IFNE      *BLANKS
     C     WSLAND        CHAIN     FAXLK                              33
     C     FAX05A        IFEQ      FAX05B
     C                   MOVE      *BLANKS       FAXRET
     C                   END
     C                   END
     C     SLTB          ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    TIL           UTIL
     C*
     C     MANKEY        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    UDATOU
     C     MANKE2        KLIST
     C                   KFLD                    UUNIK
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     *ENTRY        PLIST
     C                   PARM                    UUNIK             6
     C                   PARM                    ÆDATO6            6 0
     C                   PARM                    UKODE             1
     C     ÆDATO6        IFEQ      0
     C                   MOVE      *ZEROS        UDATOU            8 0
     C                   ELSE
     C                   MOVE      ÆDATO6        UDATOU
     C     ÆDATO6        IFGT      501231
     C                   MOVEL     '19'          UDATOU
     C                   ELSE
     C                   MOVEL     '20'          UDATOU
     C                   END
     C                   END
     C*
     C*
     C                   CALL      'SYGE12CL'
     C                   PARM                    UUSE             10
     C     UNVN          PARM      UNVN          UNVN             40
     C*
     C* KODE=S =SE PÅ
     C*
     C                   MOVE      'L'           SOKTYP            1
     C     UKODE         IFEQ      'S'
     C                   MOVE      '1'           *IN40
     C     MANKEY        CHAIN     FAXMANL1                           30
     C   30MANKE2        CHAIN     FAXMANL1                           30
     C  N30              MOVE      'J'           UKODE
     C  N30              MOVEL     FAXNRP        FAXNR
     c     manke2        chain     faxlog02                           33
     c  n33              movel     lonavn        epost
     C                   END
      *
     C                   MOVE      UYEAR         UAAR
     C                   MOVE      UMONTH        UMND
     C                   MOVE      UDAY          UDAG
     C     UYEAR         IFGT      50
     C                   MOVE      '19'          UACC
     C                   ELSE
     C                   MOVE      '20'          UACC
     C                   END
     C*
     C*
     C* KODE=C =KOPIER
     C*
     C     UKODE         IFEQ      'C'
     C     MANKEY        CHAIN     FAXMANL1                           30
     C   30MANKE2        CHAIN     FAXMANL1                           30
     C  N30              MOVEL     FAXNRP        FAXNR
     C                   END
     C*
     C                   READ      FIRM                                   33
     C                   MOVEL     FIFT          FINAVN           40
      * TRANSP.MODUL INSTALLERT?? (F7-SØK I BILREGISTER TILLATT)
     C     FITRAN        COMP      'J'                                    60
     C                   MOVE      'A'           SIDE              1
     C                   ENDSR
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   Z-ADD     1             A1                2 0
     C                   Z-ADD     1             A2                2 0
     C                   Z-ADD     1             A3                2 0
     C                   DO        6
     C     F3(A3)        IFNE      *BLANKS
     C                   MOVEL     F3(A3)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A3
     C                   END
     C                   DO        15
     C     F2(A2)        IFNE      *BLANKS
     C                   MOVEL     F2(A2)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A2
     C                   END
     C                   ENDSR
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDATE        XDTFMT            4 0
     C                   MOVEL     WSDATE        UDT1              2 0
     C                   MOVEL     XDTFMT        UDT2              2 0
     C                   MOVE      XDTFMT        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SKRIVS        BEGSR
     C***********************************************
     C                   EXFMT     BSKRIV
     C*
     C     *IN12         IFEQ      *OFF
     C                   CALL      'FAX022C2'
     C                   PARM                    UNIKA
     C                   PARM                    DEV
     C                   PARM                    HOLD
     C                   MOVE      'SHU0399'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   ENDIF                                                  <- *IN12
     C                   MOVE      *OFF          *IN12
     C*
     C*
     C                   ENDSR
     OPROSR     E            UPD
     O                       PFAXNO
     OFAXMANR   EADD         SKRIV
     O                       UNIK
     O                       FAXNRP
     O                       PTIME
     O                       PDATE
     O                       KUNDNR
     O                       KNAVN
     O                       TIL
     O                       FRA
     O                       EMNE
     O                       FINAVN
     O                       LI01
     O                       LI02
     O                       LI03
     O                       LI04
     O                       LI05
     O                       LI06
     O                       LI07
     O                       LI08
     O                       LI09
     O                       LI10
     O                       LI11
     O                       LI12
     O                       LI13
     O                       LI14
     O                       LI15
     O                       LI16
     O                       LI17
     O                       LI18
     O                       LI19
     O                       LI20
     O                       LI21
     O                       LI22
     O                       LI23
     O                       LI24
     O                       PUDATE
     OKUNDR     E            UPD1
     O                       TFAXNR
     OKUNDR2    E            UPD2
     O                       TFAXNR
