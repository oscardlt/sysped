      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE USERID -> STANDARDVERDIER ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 02 TH PTF8 NY HELP - FUNKSJON KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S02  A*                                     HLPDOC('TAG0010' 'STSTUR' 'SY400LO/-
S02  A*                                     TRANS')
N02  A                                      HLPPNLGRP(HLPSCR STSTUR)
N02  A                                      HLPTITLE('MANUELL OPPFØLGING AV MAN-
N02  A                                      KO - TURNIVÅ')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  3VALUES(' ' '1' '9' 'Å')
     A            STTYPE         5A  O  6  5TEXT('TERMKODE')
     A            STPRO          8S 0O  6 11TEXT('TUR')
     A            STSTAT         1A  O  6 21TEXT('TERMKODE')
     A            STANOP         4Y 0O  6 56TEXT('ANT OPPD. PÅ TUR')
     A                                      EDTCDE(4)
     A            MANKO          4Y 0O  6 62TEXT('AMNKO')
     A                                      EDTCDE(4)
     A            SFBESK        30A  O  6 24
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/STSTUR'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold SKYTING uferdige turer-
     A                                      /manko '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'1=Neste nivå 9=Godkjenn(Sett statu-
     A                                      s G, send evt delmanko-mail) Å=Åpne'
     A                                      COLOR(BLU)
     A                                  5  2'Valg    Turnummer St  Beskrivelse -
     A                                                         Ant Op/Manko    -
     A                                              '
     A                                      COLOR(WHT)
     A                                  2 19'(Ved inngående er turnr gjerne LØP-
     A                                      ENR)  '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A N98 30     MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 43MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 66'F1=Help '
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98N30                           4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            W2PRO          8Y 0B  2 11EDTCDE(4)
     A            W2STAT         1A  B  2 21VALUES(' ' 'M' 'N' 'G' 'X' '*')
     A                                  2  2'Søk--->'
     A                                  2 23'M=Kun Manko, N=Kun Nattportmanko G-
     A                                      =Kun Godkjent manko    '
     A                                  3 23'Blank=Kun Åpne X=Kun OK/ferdige   -
     A                                                             '
     A                                  1 11'Tur/Løpe Sta'
     A                                      COLOR(WHT)
     A                                  1 56'(*Vis  alle ) '
     A                                      COLOR(WHT)
     A                                  1 25'VIS'
     A                                  1 29'KUN'
     A                                  1 33'de'
     A                                  1 36'med'
     A                                  1 40'status...'
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/STSTUR'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold SKYTING uferdige turer-
     A                                      /manko'
     A                                      COLOR(WHT)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2'1=Neste nivå 9=Godkjenn(Sett statu-
     A                                      s G, send evt delmanko-mail) Å=Åpne-
     A                                       '
     A                                      COLOR(BLU)
     A                                  5  2'Valg    Turnummer St  Beskrivelse -
     A                                                         Ant Op/Manko    -
     A                                              '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
