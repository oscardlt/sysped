********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10005 * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRE FRA 2.0 TIL 4.0
XXXXXX*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
11016 * 02 YBC PTF11 TO NYE EKSPEDISJONS ENHETER FOR OMBEREGNING
12248 * 03 YBC PTF12 VEKT, KOLLI ER UTVIDET.
12282 * 04 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
********************************************************************
     A*__________________________________
     A* SYSPED / Tolldeklarasjoner EKSPORT
     A*""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R STANDER
     A            SEST      R               REFFLD(ST)
     A            SEAVD          4S 0       COLHDG('AVDELING')
     A            SETDN     R               REFFLD(TDN)
     A            SEDTY     R               REFFLD(DTY)
     A            SEDP      R               REFFLD(DP)
N04  A            SEDP2          2S 0       COLHDG('Siste 2 siffer pros')
     A            SEKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            SENAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            SEADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            SEADS2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Selger')
     A            SEADS3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Selger')
S03  A*           SENTK     R               REFFLD(NTK)
S03  A*           SEVKB     R               REFFLD(VKT9)
S03  A*                                     COLHDG('Bruttovekt')
N03  A            SENTK         10S 0       COLHDG('KOLLI')
N03  A            SEVKB         11S 0       COLHDG('Vekt Brutto')
     A            SEKDC     R               REFFLD(KDC)
     A            SESG      R               REFFLD(SG)
     A            SEKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            SERG          11A         COLHDG('FORETAKSNR.')
     A            SENAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            SEADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            SEADK2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Kjøper')
     A            SEADK3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Kjøper')
     A            SELKA     R               REFFLD(LKA)
     A                                      COLHDG('Landk avs/utf-land')
     A            SETLF     R               REFFLD(TLF)
     A                                      COLHDG('Telefon Deklarant')
     A            SENAD     R               REFFLD(NA)
     A                                      COLHDG('Navn Deklarant')
     A            SELV      R               REFFLD(LV)
     A            SELVT     R               REFFLD(LVT)
     A            SETRID    R               REFFLD(TRID)
     A            SELKT     R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
     A            SEVAL1    R               REFFLD(VAL)
     A                                      COLHDG('Val.kode Faktsum')
     A            SEBEL1    R               REFFLD(BEL13)
     A                                      COLHDG('Beløp Faktsum')
     A            SEVKU     R               REFFLD(VKU)
S01  A*           SEKDH     R               REFFLD(KDH)
N01  A            SEKDH          4S 0       COLHDG('TOLLSTEDSKODE')
S04  A*           SETST     R               REFFLD(TST)
N04  A            SETST          2S 0       COLHDG('Transaksjonstype')
     A            SEBEL2    R               REFFLD(BEL11)
     A                                      COLHDG('Beløp A. kost')
     A            SEFTG2    R               REFFLD(KDFT)
     A                                      COLHDG('Fortegn +/- a.kost')
     A            SETRM     R               REFFLD(TRM)
     A            SEFIF     R               REFFLD(FIF)
     A            SEFID     R               REFFLD(DT)
     A                                      COLHDG('Finans oppl.fakdat')
     A            SEKTA     R               REFFLD(KTA)
     A            SEKTB     R               REFFLD(KTB)
     A            SEGN          15          COLHDG('GODSNR.')
     A            SEFT1     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-1')
     A            SEFT2     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-2')
     A            SEFT3     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-3')
     A            SEDST     R               REFFLD(DST)
     A            SEDTG     R               REFFLD(DTG)
     A            SETLL     R               REFFLD(TLL)
     A            SETLE     R               REFFLD(TLE)
     A            SESKI     R               REFFLD(KDSKI)
     A            SELS      R               REFFLD(LS)
     A            SEKDLS         2A         COLHDG('Lagringssted kode')
     A            SEDT      R               REFFLD(DT)
     A                                      COLHDG('Registreringsdato')
      * 1. POS AV SELV2 ER BRUKT TIL Å VISE OM SKRIVER UT UTLEVERINGSATTEST
      * SOM TOLLKVITTERING
     A            SELV2     R               REFFLD(LV)
     A            SEKDDK    R               REFFLD(KDDK)
     A            SEGKD          1S 0       COLHDG('GARATIKODE')
     A            SEGFT1        30          COLHDG('GARATI FRITEKST-1')
     A            SEGFT2        30          COLHDG('GARATI FRITEKST-2')
     A            SEPOS          8          COLHDG('POSISJON')
     A            SEFTB         17          COLHDG('FRITEKST TIL B.LAND')
     A            SELKB     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL B.LAND')
     A            SEFTM         17          COLHDG('F.T. MIDLETS ID LAND')
     A            SELKM     R               REFFLD(LKA)
     A                                      COLHDG('LK MIDLETS ID LAND')
     A            SEKDFT        17          COLHDG('FRITEKST TOLLSTED')
     A            SETARF        16          COLHDG('TARIFFØRSNAVN')
     A            SELKAT        17          COLHDG('F.T. AVS/UTF.LAND')
     A            SEVAL2    R               REFFLD(VAL)
     A                                      COLHDG('Valutakode A.KOST')
     A            SEBEL3    R               REFFLD(BEL9)
     A                                      COLHDG('Andre kost.omber')
     A            SEWS      R               REFFLD(WS)
      *
      * PRINTERSTYRING
     A            SEDL           1          COLHDG('BRUKE DATALISTE ?')
     A            SETOLK         1          COLHDG('BRUKE TOL.KOM. ?')
     A            SEA4           1          COLHDG('BRUK A4 ? J/N')
      *
      * TVINN - VARIABLER
     A            S0004     R               REFFLD(N0004)
     A            S0010     R               REFFLD(N0010)
     A            S0035     R               REFFLD(N0035)
     A            S3039D    R               REFFLD(N3039D)
     A            S3039E    R               REFFLD(N3039E)
N02  A            S3039EO1  R               REFFLD(N3039E)
N02  A                                      COLHDG('EKSP.ENHET FOR SO')
N02  A            S3039EO2  R               REFFLD(N3039E)
N02  A                                      COLHDG('EKSP.ENHET FOR EB/RE')
     A            S0026     R               REFFLD(N0026)
      *
     A            SEKTC          1A         COLHDG('IKKE TOLLKREDITT')
     A            SEEKST         1A         COLHDG('EKSTERNE DATA')
     A          K SEAVD
