********************************************************************
      * GENERERE MAIL FOR VARSLING OM HENDELSE (T&T) PÅ SENDING
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 VED INNLAND-OGSÅ SENDINGSNUMMER, GODSNR KUN HVIS UTFYLT
10XXX *              Senders ref også i EMNE-feltet
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 02 JOV PTF11 Send NORSK/Local Lang tekst, Ved TTEDEV='*LE' send Local + Engelsk
      *              OBS! Alle ESTSTT.. + SYTT01 (+ SYTT05)/SYSO02  bør oppdateres samtidig
10XXX * 03 SH  PTF11 skriv til track&trace
********************************************************************
     FFIRM      IF   E           K DISK
     FHEADF     IF   E           K DISK
N03  FTRACKF    O  A E           K DISK
     Farksrc    O    E             DISK    USROPN
     F                                     RENAME(arksrc:arcdat)
N03  D                 DS
N03  D  XULTID                 1     14  0
N03  D  XULTM                  1      6  0
N03  D  XULDD                  7      8  0
N03  D  XULMM                  9     10  0
N03  D  XULÅÅ                 11     14  0
N03  D                 DS
N03  D  ULÅÅ                   1      4  0
N03  D  ULMM                   5      6  0
N03  D  ULDD                   7      8  0
N03  D  ULDT                   1      8  0
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *
     D EMNE            S             70
     D POS             S             15
     C                   exsr      INIT
      *
     c     HeaKey        chain     HEADF                              35
     c     *in35         ifeq      *off
     c                   exsr      SndMail
     c                   endif
N03  c                   exsr      ttsr
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *
      ******************************************************************************
     C     SndMail       BEGSR
      ******************************************************************************
      *  Fyll opp sourcefil som brukes som body
     C                   open      arksrc
     c                   move      *zeros        srcseq
      *
     C                   eval      SRCDTA = 'MELDING FRA ' + %TRIM(FIFT)
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'OM HENDELSE PÅ DIN '
     C                             + 'SENDING MED SENDERS/OPPDR.GIVERS REF: '
     C                             + HERFA
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'På din sending (se under) er det '+
     C                             'nylig registrert følgende hendelse/info:'
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '----------------------------------'
     C                                    + '----------------------------------'
     C                                    + '-----'
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
N02  C                   eval      SRCDTA = TTTEXL
N02  c                   add       1             srcseq
N02  C                   WRITE     arcdat
      *
N02  c                   if        TTEDEV = '*LE' and TTTEXT <> *blanks
     C                   eval      SRCDTA = TTTEXT
     c                   add       1             srcseq
     C                   WRITE     arcdat
N02  c                   endif
      *
     C                   eval      SRCDTA = '----------------------------------'
     C                                    + '----------------------------------'
     C                                    + '-----'
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   movel     TTTIME        TTTIM4            4 0
     C                   eval      SRCDTA = 'Hendelses-Dato/Tid: '
     C                             + %trim(%editw(TTDATE:'    .  .  '))
     C                             + '   Kl: ' + %trim(%editw(TTTIM4:'  :  '))
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '*** Sendingsdetaljer *** : '
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Vår ref:  '+
     C                             %trim(%editc(HEAVD:'Z'))+'/'+
     C                             %trim(%editc(HEOPD:'Z'))+'/'+HESG
     c                   add       1             srcseq
     C                   WRITE     arcdat
N01   *
N01  c     HXSNDN        ifne      *blanks
N01  C                   move      *blanks       SNDN17           17
N01  C                   IF        (%SUBST(HXSNDN:1:3)='401') AND
N01  C                             (%SUBST(HXSNDN:20:1)<>' ')
N01  C                   eval      SNDN17=%SUBST(HXSNDN:4:17)
N01  c                   else
N01  C                   eval      SNDN17=HXSNDN
N01  c                   endif
N01  C                   eval      SRCDTA = 'Send.nr:  ' + SNDN17
N01  c                   add       1             srcseq
N01  C                   WRITE     arcdat
N01  c                   endif
N01   *
N01  C                   IF        %SUBST(HEGN:5:1) <> *BLANKS
     C     HEPOS2        IFEQ      *ZEROS
     C                   EVAL      POS = HEPOS1
     C                   ELSE
     C                   EVAL      POS = %trim(HEPOS1) +'/' +
     C                             %trim(%editc(HEPOS2:'Z'))
     C                   END
      * ...
     C                   eval      SRCDTA = 'Godsnummer: ' + HEGN +
     C                             '  Posisjon: ' + POS
     c                   add       1             srcseq
     C                   WRITE     arcdat
N01  c                   endif
      *
     C                   eval      SRCDTA = *blanks
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Avs.: ' + HENAS
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADS1
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADS2
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADS3
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Mot.: ' + HENAK
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADK1
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADK2
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = '      ' + HEADK3
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Varebeskrivelse: ' +
     C                             %trim(%editc(HENT:'Z')) + ' ' +
     c                             %trim(HEVS1) +' '+ %trim(HEVS2)
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Vekt: ' +
     C                             %trim(%editc(HEVKT:'Z'))
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = *blanks
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
      *
     C                   eval      SRCDTA = 'Denne melding er maskinelt/'
     C                             +'automatisk sendt ut fra '
     C                             + FIFT
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   eval      SRCDTA = 'Evt svar på mailen blir '
     C                             +'IKKE lest.'
     c                   add       1             srcseq
     C                   WRITE     arcdat
      *
     C                   close     arksrc
      *
     C                   eval      EMNE = 'MELDING OM HENDELSE PÅ DIN SENDING'
N01  C                             +'  '+HERFA
      *
     c                   movel     'W'           UUnik             7
     c                   move      ZJOBNR        UUnik
     C                   CALL      'ESTSTTMC'
     C                   PARM                    UUnik
     C                   PARM                    Emne
     C                   PARM                    XXavNa           50
     C                   PARM                    XXavAd           50
     C                   PARM      CCONTA        XXmoNa           50
     C                   PARM      CEmail        XXmoAd           50
     C                   ENDSR
      *
      *
      ******************************************************************************
     C     INIT          BEGSR
      ******************************************************************************
     C     *entry        PLIST
     c                   parm                    HEAVD
     c                   parm                    HEOPD
     c                   parm                    TTACTI            3
     c                   parm                    TTDATE            8 0
     c                   parm                    TTTIME            6 0
     c                   parm                    TTDATL            8 0
     c                   parm                    TTTIML            6 0
N02  c                   parm                    TTEDEV            3
N02  c                   parm                    TTTEXL           70
     c                   parm                    TTTEXT           70
     c                   parm                    TTUSER           10
     c                   parm                    XXAvNa           50
     c                   parm                    XXAvAd           50
     c                   parm                    CCONTA           30
     c                   parm                    CEmail           70
      *
     c     HeaKey        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
      *
     c     *loval        setll     FIRM
     c                   read      FIRM
     C                   ENDSR
N03  C***********************************************
N03  C     TTSR          BEGSR
N03  C***********************************************
N03  C                   TIME                    XULTID
N03  C                   MOVE      XULDD         ULDD
N03  C                   MOVE      XULMM         ULMM
N03  C                   MOVE      XULÅÅ         ULÅÅ
N03   *
N03  C                   MOVE      HEAVD         TTAVD
N03  C                   MOVE      HEOPD         TTOPD
N03  C                   TIME                    XULTID
N03  C                   MOVE      XULDD         ULDD
N03  C                   MOVE      XULMM         ULMM
N03  C                   MOVE      XULÅÅ         ULÅÅ
N03  C                   MOVE      'EMA'         TTACTI
N03  C                   MOVE      ULDT          TTDATE
N03  C                   MOVE      XULTM         TTTIME
N03  C                   MOVE      TTDATE        TTDATL
N03  C                   MOVE      TTTIME        TTTIML
N03  C                   EVAL      TTTEXT = 'EMAIL sent to '+ %trim(CEMAIL)
N03  C                   EVAL      TTTEXL = 'EPOST sendt til '+ %trim(CEMAIL)
N03  C                   WRITE     TRACKFR
N03  C                   ENDSR
