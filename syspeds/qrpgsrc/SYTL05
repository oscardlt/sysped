********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SVENSK GODSREGISTRERING
********************************************************************
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE MERKNADSJOURNAL              ***
      *** FUNKSJON FOR SØKING ER OGSÅ LAGT INN                   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29    32 34-39 41-88 95-97       ***
      **************************************************************
      *
N01  FFIRM      IF   E           K DISK
     FMERKNF    UF A E           K DISK
     FGODSJL05  IF   E           K DISK
     FSYTL05FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  GOMOTM                 1     28
     D  XDLT                   1      5
     D                 DS
     D  UDSGN                  1     15
     D  GOGN1                  1      4
     D  GOGN2                  5      9
     D  GOGN3                 10     12
     D  GOGN4                 13     15
     D                 DS
     D  WSTRDT                 1      8  0
     D  WSTRCC                 1      2  0
     D  WSTRAA                 3      4  0
     D  WSTRMM                 5      6  0
     D  WSTRDD                 7      8  0
     D                 DS
     D  WSDATO                 1      6  0
     D  WSTRD2                 1      2  0
     D  WSTRM2                 3      4  0
     D  WSTRA2                 5      6  0
     C*
     C                   EXSR      INIT
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     inikey        SETLL     MERKNF
     C     START1        TAG
     C     REKEY         READE(N)  MERKNF                                 94
     C  N94              MOVE      GOPOS         YPOS
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     REKEY         READE(N)  MERKNF                                 94
     C  N94              MOVE      GOPOS         YPOS
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C*                    Z-ADD4         POSNBR
     C*                    EXFMTNYWRK
     C*** LAGE NY
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94              MOVE      GOPOS         YPOS              4 0
     C  N94REKEY2        SETLL     MERKNF
     C  N94REKEY         READE(N)  MERKNF                                 94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C                   MOVEA     '000000'      *IN(70)
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C   12              EXSR      MOVB2
     C*
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C  N12              Z-ADD     6             LINNBR
     C  N12              Z-ADD     18            POSNBR
     C  N12              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   SETOFF                                       707172
     C                   SETOFF                                       737475
     C                   SETOFF                                       7677
     C* N40 = NY
      *
      * MERKNADSKODE MÅ UTFYLLES
      *
     C     WSMKOD        IFNE      'NE'
     C     WSMKOD        ANDNE     'DI'
     C                   MOVE      'SHU0960'     MSGNR
     C                   SETON                                        3077
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
      *
      * POSISJON MÅ UTFYLLES
      *
     C     WSPOS         IFEQ      0
     C                   MOVE      'SHU0899'     MSGNR
     C                   SETON                                        3070
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
      * ANT KOLLI MÅ UTFYLLES
      *
     C     WSANTK        IFEQ      0
     C                   MOVE      'SHU0900'     MSGNR
     C                   SETON                                        3071
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
      * VARESLAG MÅ UTFYLLES
      *
     C     WSVSLA        IFEQ      *BLANKS
     C                   MOVE      'SHU0901'     MSGNR
     C                   SETON                                        3072
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
      * MERKET FYLLES AUTOMATISK VED BLANK
      *
     C     WSMERK        IFEQ      *BLANKS
     C     WSMKOD        IFEQ      'NE'
     C                   MOVEL     'NE=OVERT'    XX13             13
     C                   MOVE      'ALLIG'       XX13
     C                   MOVEL     XX13          WSMERK
     C                   END
     C     WSMKOD        IFEQ      'DI'
     C                   MOVEL     'DI=MANKO'    WSMERK
     C                   END
     C                   END
      *
      * STED  MÅ UTFYLLES
      *
     C     WSSTED        IFEQ      *BLANKS
     C                   MOVE      'SHU0905'     MSGNR
     C                   SETON                                        3074
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
      * MOTTAGER  MÅ UTFYLLES
      *
     C     WSMOTT        IFEQ      *BLANKS
     C                   MOVE      'SHU0903'     MSGNR
     C                   SETON                                        3075
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
      * MERKNAD   MÅ UTFYLLES
      *
     C     WSMERK        IFEQ      *BLANKS
     C                   MOVE      'SHU0904'     MSGNR
     C                   SETON                                        3076
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     18            POSNBR
     C                   GOTO      SLTB
     C                   END
     C                   MOVE      WSPOS         GOPOS
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     STKEY         CHAIN     MERKNF                             33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    MERKNFR
     C   33              WRITE     MERKNFR
     c   33              add       1             nestep
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C                   SETOFF                                       30
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
      * 2= ENDRE
      *
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
      *
      * 4= SLETTE
      *
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
      *
      * 5= sepå
      *
     C     WSNR          IFEQ      '5'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVSIW
     C                   SETON                                        9931
     C                   EXFMT     VARELI
     c                   setoff                                       31
     C***
     C   12              GOTO      SLHENT
     C                   END
      *
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C*
     C                   EXFMT     VAREL3
     C  N12              DO
     C     STKEY         CHAIN     MERKNF                             33
     C  N33              DELETE    MERKNFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     c                   Z-ADD     6             LINNBR
     C                   Z-ADD     18            POSNBR
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C                   MOVEA     '000000'      *IN(70)
     C                   SETOFF                                       30
     C*
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C*
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C*
     C                   MOVEL     '*DEL*'       XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      'DI'          WSMKOD
     C                   MOVE      nestep        WSPOS
     C                   MOVE      *ZEROS        WSPOS2
     C                   MOVE      *ZEROS        WSANTK
     C                   MOVEL     GOTYPE        WSVSLA
     C                   MOVEL     GOAVG         WSSTED
     C                   MOVEL     GOMOTT        WSMOTT
     C                   MOVE      *BLANKS       WSMERK
     C                   MOVE      *BLANKS       WSMERB
     C                   MOVE      *BLANKS       WSMERC
     C                   MOVE      *BLANKS       WSMERD
     C                   MOVE      *BLANKS       WSMER1
     C                   MOVEL     'ADR'         WSMER1
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C                   MOVE      GOPOS         WSPOS
     C                   MOVE      GOPOS2        WSPOS2
     C                   MOVE      GOANTK        WSANTK
     C                   MOVE      GOMKOD        WSMKOD
     C                   MOVE      GOVSLA        WSVSLA
     C                   MOVE      GOSTED        WSSTED
     C                   MOVE      GOMOTM        WSMOTT
     C                   MOVE      GOMERK        WSMERK
     C                   MOVE      GOMER1        WSMER1
     C                   MOVE      GOMERB        WSMERB
     C                   MOVE      GOMERC        WSMERC
     C                   MOVE      GOMERD        WSMERD
     C*
     C*
     C                   SETON                                        40
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C*
     C* FIXES VARIABLES
     C                   MOVE      WSPOS         GOPOS
     C                   MOVE      WSPOS2        GOPOS2
     C                   MOVE      WSANTK        GOANTK
     C                   MOVE      WSMKOD        GOMKOD
     C                   MOVE      WSVSLA        GOVSLA
     C                   MOVE      WSSTED        GOSTED
     C                   MOVE      WSMOTT        GOMOTM
     C                   MOVE      WSMERK        GOMERK
     C                   MOVE      WSMER1        GOMER1
     C                   MOVE      WSMERB        GOMERB
     C                   MOVE      WSMERC        GOMERC
     C                   MOVE      WSMERD        GOMERD
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    GOGN             15
     C                   PARM                    UTRNR            20
     C                   PARM                    UTRDT             8 0
     C                   MOVE      GOGN          UDSGN
     C                   MOVE      UTRNR         GOTRNR
     C                   MOVE      UTRDT         WSTRDT
     C                   MOVE      WSTRAA        WSTRA2
     C                   MOVE      WSTRMM        WSTRM2
     C                   MOVE      WSTRDD        WSTRD2
     C     STKEY         KLIST
     C                   KFLD                    GOGN
     C                   KFLD                    GOTRNR
     C                   KFLD                    GOPOS
     C     inikey        KLIST
     C                   KFLD                    GOGN
     C                   KFLD                    GOTRNR
     C                   KFLD                    minpos            4 0
     C     maxkey        KLIST
     C                   KFLD                    GOGN
     C                   KFLD                    GOTRNR
     C                   KFLD                    maxpos
     c                   z-add     9999          maxpos            4 0
N01   *
N01  C     *LOVAL        SETLL     FIRM
N01  C                   READ      FIRM                                   33
N01  C     FILAND        COMP      'SE'                                   39
N01   *
     c     maxkey        setll     merknf
     c     rekey         readpe    merknf                                 33
     c     gopos         add       1             nestep            4 0
     C     REKEY         KLIST
     C                   KFLD                    GOGN
     C                   KFLD                    GOTRNR
     C     REKEY2        KLIST
     C                   KFLD                    GOGN
     C                   KFLD                    GOTRNR
     C                   KFLD                    YPOS
     C     REKEY         CHAIN     GODSJL05                           33
     C                   ENDSR
     C*
