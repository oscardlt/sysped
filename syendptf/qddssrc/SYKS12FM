     A*%%TS  SD  20181002  072522  JOVO        REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYKS12PNL)
     A                                      HLPTITLE('REKVISISJONER / BUDSJETTE-
     A                                      RTE KOSTNADER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            BUFEDT         8S 0H
     A            BUBNR3         7S 0H
S03  A*           WSNRO          1A  B  6  2VALUES(' ' '1' '2' '4' '6' '7' '8')
N03  A            WSNRO          1A  B  6  2VALUES(' ' '1' '2' '4' '6' '7' '8' -
N03  A                                      'O' 'F')
     A  32                                  DSPATR(ND)
     A  32                                  DSPATR(PR)
     A            XXKDM          1A  B  6  4
     A  32                                  DSPATR(ND)
     A  32                                  DSPATR(PR)
     A                                      VALUES(' ' 'J' 'Y' 'N')
     A            BUST      R        O  6  6REFFLD(KOSBUR/BUST *LIBL/KOSBU)
     A            BUBNR     R        O  6  8REFFLD(KOSBUR/BUBNR *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A  51                                  COLOR(WHT)
     A            BUVK      R        O  6 16REFFLD(KOSBUR/BUVK *LIBL/KOSBU)
     A            BUBL      R        O  6 20REFFLD(KOSBUR/BUBL *LIBL/KOSBU)
     A                                      EDTCDE(L)
     A            BUVAL     R        O  6 32REFFLD(KOSBUR/BUVAL *LIBL/KOSBU)
     A            BULNR     R        O  6 36REFFLD(KOSBUR/BULNR *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A  53
     AO 56                                  COLOR(WHT)
     A            BUTYPE    R        O  6 45REFFLD(KOSBUR/BUTYPE *LIBL/KOSBU)
     A            BUBLST    R        O  6 48REFFLD(KOSBUR/BUBLST *LIBL/KOSBU)
     A            BUSG      R        O  6 51REFFLD(KOSBUR/BUSG *LIBL/KOSBU)
     A            BUTAVD    R        O  6 55REFFLD(KOSBUR/BUTAVD *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A  52                                  COLOR(WHT)
     A            BUTUNR    R        O  6 60REFFLD(KOSBUR/BUTUNR *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A  52                                  COLOR(WHT)
     A            BUOAVD    R        O  6 69REFFLD(KOSBUR/BUOAVD *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A  59                                  COLOR(WHT)
     A            BUOPD     R        O  6 74REFFLD(KOSBUR/BUOPD *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A  59                                  COLOR(WHT)
     A            BUTXT     R        O  7  4REFFLD(KOSBUR/BUTXT *LIBL/KOSBU)
     A  53 60
     AO 57 60                               COLOR(WHT)
     A            BUBILN    R        O  7 40REFFLD(KOSBUR/BUBILN *LIBL/KOSBU)
     A  57N60                               COLOR(WHT)
     A            XXFEDT         6S 0O  7 51
     A            BUTNR     R        O  7 58REFFLD(KOSBUR/BUTNR *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A  54                                  COLOR(WHT)
     A            BUPMN     R        O  7 67REFFLD(KOSBUR/BUPMN *LIBL/KOSBU)
     A            BUPÅR     R        O  7 70REFFLD(KOSBUR/BUPÅR *LIBL/KOSBU)
     A            BUBNR2    R        O  7 73REFFLD(KOSBUR/BUBNR2 *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A N31                                  COLOR(WHT)
     A  31                                  COLOR(RED)
     A            LNAVN         30A  O  8  9DSPATR(UL)
     A            VMNAVN        30A  O  8 50DSPATR(UL)
     A                                  8  3MSGCON(005 SHU0820 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 44MSGCON(005 SHU0821 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20150723  073313  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0006)
     A  *DS4                                SFLSIZ(0006)
     A  *DS3                                SFLPAG(0005)
     A  *DS4                                SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYKS120500 SYKS12PNL)
     A  *DS3                                HLPARA(04 001 04 003)
     A  *DS4                                HLPARA(04 001 04 003)
     A          H                           HLPPNLGRP(SYKS120510 SYKS12PNL)
     A  *DS3                                HLPARA(04 004 04 005)
     A  *DS4                                HLPARA(04 004 04 005)
     A          H                           HLPPNLGRP(SYKS120520 SYKS12PNL)
     A  *DS3                                HLPARA(04 006 04 007)
     A  *DS4                                HLPARA(04 006 04 007)
     A          H                           HLPPNLGRP(SYKS120530 SYKS12PNL)
     A  *DS3                                HLPARA(04 008 04 018)
     A  *DS4                                HLPARA(04 008 04 018)
     A          H                           HLPPNLGRP(SYKS120540 SYKS12PNL)
     A  *DS3                                HLPARA(04 020 04 043)
     A  *DS4                                HLPARA(04 020 04 043)
     A          H                           HLPPNLGRP(SYKS120550 SYKS12PNL)
     A  *DS3                                HLPARA(04 044 04 049)
     A  *DS4                                HLPARA(04 044 04 049)
     A          H                           HLPPNLGRP(SYKS120560 SYKS12PNL)
     A  *DS3                                HLPARA(04 051 04 080)
     A  *DS4                                HLPARA(04 051 04 080)
     A          H                           HLPPNLGRP(SYKS120570 SYKS12PNL)
     A  *DS3                                HLPARA(05 001 05 056)
     A  *DS4                                HLPARA(05 001 05 056)
     A          H                           HLPPNLGRP(SYKS120580 SYKS12PNL)
     A  *DS3                                HLPARA(05 058 05 065)
     A  *DS4                                HLPARA(05 058 05 065)
     A          H                           HLPPNLGRP(SYKS120590 SYKS12PNL)
     A  *DS3                                HLPARA(05 067 05 080)
     A  *DS4                                HLPARA(05 067 05 080)
     A          H                           HLPPNLGRP(SYKS120500 SYKS12PNL)
     A  *DS3                                HLPARA(06 001 06 003)
     A  *DS4                                HLPARA(06 001 06 003)
     A          H                           HLPPNLGRP(SYKS120510 SYKS12PNL)
     A  *DS3                                HLPARA(06 004 06 005)
     A  *DS4                                HLPARA(06 004 06 005)
     A          H                           HLPPNLGRP(SYKS120520 SYKS12PNL)
     A  *DS3                                HLPARA(06 006 06 007)
     A  *DS4                                HLPARA(06 006 06 007)
     A          H                           HLPPNLGRP(SYKS120530 SYKS12PNL)
     A  *DS3                                HLPARA(06 008 06 018)
     A  *DS4                                HLPARA(06 008 06 018)
     A          H                           HLPPNLGRP(SYKS120540 SYKS12PNL)
     A  *DS3                                HLPARA(06 020 06 043)
     A  *DS4                                HLPARA(06 020 06 043)
     A          H                           HLPPNLGRP(SYKS120550 SYKS12PNL)
     A  *DS3                                HLPARA(06 044 06 049)
     A  *DS4                                HLPARA(06 044 06 049)
     A          H                           HLPPNLGRP(SYKS120560 SYKS12PNL)
     A  *DS3                                HLPARA(06 051 06 080)
     A  *DS4                                HLPARA(06 051 06 080)
     A          H                           HLPPNLGRP(SYKS120570 SYKS12PNL)
     A  *DS3                                HLPARA(07 001 07 056)
     A  *DS4                                HLPARA(07 001 07 056)
     A          H                           HLPPNLGRP(SYKS120580 SYKS12PNL)
     A  *DS3                                HLPARA(07 058 07 065)
     A  *DS4                                HLPARA(07 058 07 065)
     A          H                           HLPPNLGRP(SYKS120590 SYKS12PNL)
     A  *DS3                                HLPARA(07 067 07 080)
     A  *DS4                                HLPARA(07 067 07 080)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYKS12'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2244 *LIBL/SYMSGF)
     A N95                                  COLOR(WHT)
     A  95                                  COLOR(RED)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 34MSGCON(015 DIV2364 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 50MSGCON(015 DIV2386 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A  97                              3  2MSGCON(015 DIV2237 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 18MSGCON(015 DIV2226 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(015 DIV2227 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 50MSGCON(018 DIV2291 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N03  A                                  3 69MSGCON(012 JOV1681 *LIBL/SYMSGF)
N03  A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV2243 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(078 DIV2290 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A  97                                  CF02(02 'SWITCHE PLUKKET/IKKE')
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF09(09 'DIVERSE FERJE')
     A                                      CF10(10 'PLUKK INNG. BELØP')
     A                                      CF11(11 'PLUKK BUDS. BELØP')
     A                                      CF12(12 'ANNULER PLUKK')
     A                                      CF08(08 'NYTT SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF07(07 'AWB-modus')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYKS120900 SYKS12PNL)
     A                                      HLPARA(03 062 03 080)
     A          H                           HLPPNLGRP(SYKS120900 SYKS12PNL)
     A                                      HLPARA(04 001 04 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A  97                              1  2MSGCON(026 JOV0312 *LIBL/SYMSGF)
     A  97        KABNR          7S 0O  1 30COLOR(WHT)
     A  97                              1 38MSGCON(010 JOV0315 *LIBL/SYMSGF)
     A  97        KALNR     R        O  1 49REFFLD(KOSTAR/KALNR *LIBL/KOSTA)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  97                              1 58MSGCON(008 JOV0316 *LIBL/SYMSGF)
     A  97        KAFNR     R        O  1 67REFFLD(KOSTAR/KAFNR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A  97N88                           2  2MSGCON(026 JOV0313 *LIBL/SYMSGF)
     A  97N88     KAVAL          3A  O  2 30COLOR(WHT)
     A  97N88     XXKABL    R        O  2 35REFFLD(KOSTAR/KABL *LIBL/KOSTA)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A  97                              2 58MSGCON(012 JOV0317 *LIBL/SYMSGF)
     A  97        KAPMN     R        O  2 71REFFLD(KOSTAR/KAPMN *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A  97        KAPÅR     R        O  2 74REFFLD(KOSTAR/KAPÅR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A  97N88                           3  2MSGCON(026 JOV0314 *LIBL/SYMSGF)
     A  97 88                           3  2MSGCON(026 JOV0508 *LIBL/SYMSGF)
     A  97        XXVAL          3A  O  3 30COLOR(WHT)
     A  97        UREST     R        O  3 35REFFLD(KOSTAR/KABL *LIBL/KOSTA)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A N97                              3 62MSGCON(018 DIV2363 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  97 55                           3 62MSGCON(018 JOV0509 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  97N55                           3 62MSGCON(018 JOV0510 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N60                              4 42MSGCON(038 JOV0848 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  60                              4 42MSGCON(038 JOV0849 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A N95                                  COLOR(WHT)
     A  95                                  COLOR(RED)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      CF04(04 'Søk')
     A                                      CF13(13 'BEKREFT INNH')
     A                                      CF14(14 'BEKREFT UTKJ.')
     A                                      CF15(15 'IKKE INN/UTK.')
     A                                      SLNO(20)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYKS120510 SYKS12PNL)
     A                                      HLPARA(03 001 03 014)
     A          H                           HLPPNLGRP(SYKS120530 SYKS12PNL)
     A                                      HLPARA(03 016 03 018)
     A          H                           HLPPNLGRP(SYKS120540 SYKS12PNL)
     A                                      HLPARA(03 020 03 043)
     A          H                           HLPPNLGRP(SYKS120550 SYKS12PNL)
     A                                      HLPARA(03 044 03 049)
     A          H                           HLPPNLGRP(SYKS120560 SYKS12PNL)
     A                                      HLPARA(03 051 03 080)
     A          H                           HLPPNLGRP(SYKS120570 SYKS12PNL)
     A                                      HLPARA(04 001 04 056)
     A          H                           HLPPNLGRP(SYKS120580 SYKS12PNL)
     A                                      HLPARA(04 058 04 067)
     A          H                           HLPPNLGRP(SYKS120590 SYKS12PNL)
     A                                      HLPARA(04 069 04 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 JOV0340 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(078 JOV0341 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSST      R        B  3  2REFFLD(KOSBUR/BUST *LIBL/KOSBU)
     A                                      DSPATR(ND)
     A            WSBNR     R        B  3  6REFFLD(KOSBUR/BUBNR *LIBL/KOSBU)
     A                                      DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSKDM          1A  B  3 14DSPATR(UL)
     A                                      VALUES(' ' 'J' 'Y' 'N')
     A                                      COLOR(YLW)
     A            WSVK      R        B  3 16REFFLD(KOSBUR/BUVK *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A            WSBL      R        B  3 20REFFLD(KOSBUR/BUBL *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(M)
     A            WSVAL     R        B  3 32REFFLD(KOSBUR/BUVAL *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A            WSLNR     R     Y  B  3 36REFFLD(KOSBUR/BULNR *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSTYPE    R        B  3 45REFFLD(KOSBUR/BUTYPE *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A  76                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            WSBLST    R        B  3 48REFFLD(KOSBUR/BUBLST *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A            WSSG      R        B  3 51REFFLD(KOSBUR/BUSG *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A            WSTAVD    R     Y  B  3 55REFFLD(KOSBUR/BUTAVD *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTUNR    R     Y  B  3 60REFFLD(KOSBUR/BUTUNR *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A  78                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSOAVD    R     Y  B  3 69REFFLD(KOSBUR/BUOAVD *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSOPD     R     Y  B  3 74REFFLD(KOSBUR/BUOPD *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A  75                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSTXT     R        B  4  4REFFLD(KOSBUR/BUTXT *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A            WSBILN    R        B  4 40REFFLD(KOSBUR/BUBILN *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A            WSFEDT         6Y 0B  4 51COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(4)
     A  80                                  DSPATR(RI)
     A            WSTNRB         1A  B  4 58VALUES(' ' 'B')
     A                                      COLOR(YLW)
     A            WSTNR     R        B  4 60REFFLD(KOSBUR/BUTNR *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A            WSPMN     R     Y  B  4 69REFFLD(KOSBUR/BUPMN *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A  77                                  DSPATR(RI)
     A            WSPÅR     R     Y  B  4 72REFFLD(KOSBUR/BUPÅR *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A  77                                  DSPATR(RI)
     A            WSBILK    R        B  4 75REFFLD(KOSBUR/BUBILK *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A N30                              5  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              5 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  3MSGCON(078 DIV2243 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            BUST      R        O  2  4REFFLD(KOSBUR/BUST *LIBL/KOSBU)
     A                                      COLOR(WHT)
     A            BUBNR     R        O  2  6REFFLD(KOSBUR/BUBNR *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            BUVK      R        O  2 14REFFLD(KOSBUR/BUVK *LIBL/KOSBU)
     A                                      COLOR(WHT)
     A            BUBL      R        O  2 18REFFLD(KOSBUR/BUBL *LIBL/KOSBU)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            BUVAL     R        O  2 30REFFLD(KOSBUR/BUVAL *LIBL/KOSBU)
     A                                      COLOR(WHT)
     A            BULNR     R        O  2 34REFFLD(KOSBUR/BULNR *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            BUTYPE    R        O  2 45REFFLD(KOSBUR/BUTYPE *LIBL/KOSBU)
     A                                      COLOR(WHT)
     A            BUBLST    R        O  2 48REFFLD(KOSBUR/BUBLST *LIBL/KOSBU)
     A                                      COLOR(WHT)
     A            BUSG      R        O  2 51REFFLD(KOSBUR/BUSG *LIBL/KOSBU)
     A                                      COLOR(WHT)
     A            BUTAVD    R        O  2 55REFFLD(KOSBUR/BUTAVD *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            BUTUNR    R        O  2 60REFFLD(KOSBUR/BUTUNR *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            BUOAVD    R        O  2 69REFFLD(KOSBUR/BUOAVD *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            BUOPD     R        O  2 74REFFLD(KOSBUR/BUOPD *LIBL/KOSBU)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            BUTXT     R        O  3  4REFFLD(KOSBUR/BUTXT *LIBL/KOSBU)
     A                                      COLOR(WHT)
     A                                  3 60MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            BUBILN    R        O  3 40REFFLD(KOSBUR/BUBILN *LIBL/KOSBU)
     A                                      COLOR(WHT)
EV5  A            BUFEDT    R        H      REFFLD(KOSBUR/BUFEDT *LIBL/KOSBU)
NV5  A            XXFEDT         6  0O  3 51
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A          R NYWRK
     A  97                                  CF02(02 'SWITCHE PLUKKET/IKKE')
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF09(09 'DIVERSE FERJE')
     A                                      CF08(08 'NYTT SØK')
     A                                      CF07(07 'AWB-modus')
     A          H                           HLPPNLGRP(SYKS120500 SYKS12PNL)
     A                                      HLPARA(04 001 04 003)
     A          H                           HLPPNLGRP(SYKS120510 SYKS12PNL)
     A                                      HLPARA(04 004 04 005)
     A          H                           HLPPNLGRP(SYKS120520 SYKS12PNL)
     A                                      HLPARA(04 006 04 007)
     A          H                           HLPPNLGRP(SYKS120530 SYKS12PNL)
     A                                      HLPARA(04 008 04 018)
     A          H                           HLPPNLGRP(SYKS120540 SYKS12PNL)
     A                                      HLPARA(04 020 04 043)
     A          H                           HLPPNLGRP(SYKS120550 SYKS12PNL)
     A                                      HLPARA(04 044 04 049)
     A          H                           HLPPNLGRP(SYKS120560 SYKS12PNL)
     A                                      HLPARA(04 051 04 080)
     A          H                           HLPPNLGRP(SYKS120570 SYKS12PNL)
     A                                      HLPARA(05 001 05 056)
     A          H                           HLPPNLGRP(SYKS120580 SYKS12PNL)
     A                                      HLPARA(05 058 05 065)
     A          H                           HLPPNLGRP(SYKS120590 SYKS12PNL)
     A                                      HLPARA(05 067 05 080)
     A          H                           HLPPNLGRP(SYKS120900 SYKS12PNL)
     A                                      HLPARA(22 062 22 080)
     A          H                           HLPPNLGRP(SYKS120900 SYKS12PNL)
     A                                      HLPARA(23 001 23 080)
     A                                  1  2'SYSPED/SYKS12'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2244 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV2243 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  6  2MSGCON(078 DIV2290 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  97                             20  2MSGCON(026 JOV0312 *LIBL/SYMSGF)
     A  97        KABNR          7S 0O 20 30COLOR(WHT)
     A  97                             20 38MSGCON(010 JOV0315 *LIBL/SYMSGF)
     A  97        KALNR     R        O 20 49REFFLD(KOSTAR/KALNR *LIBL/KOSTA)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  97                             20 58MSGCON(008 JOV0316 *LIBL/SYMSGF)
     A  97        KAFNR     R        O 20 67REFFLD(KOSTAR/KAFNR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A  97N88                          21  2MSGCON(026 JOV0313 *LIBL/SYMSGF)
     A  97N88     KAVAL          3A  O 21 30COLOR(WHT)
     A  97N88     XXKABL    R        O 21 35REFFLD(KOSTAR/KABL *LIBL/KOSTA)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A  97                             21 58MSGCON(012 JOV0317 *LIBL/SYMSGF)
     A  97        KAPMN     R        O 21 71REFFLD(KOSTAR/KAPMN *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A  97        KAPÅR     R        O 21 74REFFLD(KOSTAR/KAPÅR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A  97N88                          22  2MSGCON(026 JOV0314 *LIBL/SYMSGF)
     A  97 88                          22  2MSGCON(026 JOV0508 *LIBL/SYMSGF)
     A  97        XXVAL          3A  O 22 30COLOR(WHT)
     A  97        UREST     R        O 22 35REFFLD(KOSTAR/KABL *LIBL/KOSTA)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A N97                             22 62MSGCON(018 DIV2363 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  97 55                          22 62MSGCON(018 JOV0509 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  97N55                          22 62MSGCON(018 JOV0510 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N60                             23 42MSGCON(038 JOV0848 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  60                             23 42MSGCON(038 JOV0849 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE1
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF04(04 'SØK')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SYKS120010 SYKS12PNL)
     A                                      HLPARA(01 001 07 080)
     A          H                           HLPPNLGRP(SYKS120020 SYKS12PNL)
     A                                      HLPARA(08 001 08 080)
     A          H                           HLPPNLGRP(SYKS120030 SYKS12PNL)
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPPNLGRP(SYKS120040 SYKS12PNL)
     A                                      HLPARA(10 001 12 080)
     A          H                           HLPPNLGRP(SYKS120050 SYKS12PNL)
     A                                      HLPARA(13 001 13 080)
     A          H                           HLPPNLGRP(SYKS120060 SYKS12PNL)
     A                                      HLPARA(14 001 20 080)
     A          H                           HLPPNLGRP(SYKS120070 SYKS12PNL)
     A                                      HLPARA(21 001 23 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYKS12'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2244 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4  2MSGCON(025 JOV0318 *LIBL/SYMSGF)
     A            W1KBNR         7Y 0B  4 29EDTCDE(4)
     A                                      COLOR(YLW)
     A  72                                  DSPATR(RI)
     A                                  4 37MSGCON(040 JOV0530 *LIBL/SYMSGF)
     A                                  5  2MSGCON(025 JOV0531 *LIBL/SYMSGF)
     A            W1VIS1         1A  B  5 29VALUES(' ' 'N' 'J' 'Y')
     A                                      COLOR(YLW)
     A                                  5 31'/'
     A            W1VIS2         1A  B  5 33VALUES(' ' 'N' 'J' 'Y')
     A                                      COLOR(YLW)
     A                                  5 37MSGCON(040 JOV0532 *LIBL/SYMSGF)
     A                                  6  2MSGCON(025 JOV0851 *LIBL/SYMSGF)
     A            W1MVA          1A  B  6 29COLOR(YLW)
     A                                      VALUES(' ' 'N' 'J')
     A  96                                  DSPATR(PR)
     A                                  6 37MSGCON(015 JOV0852 *LIBL/SYMSGF)
     A                                  7 19MSGCON(040 DIV2245 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N60                              8  2MSGCON(025 DIV2289 *LIBL/SYMSGF)
     A  60                              8  2MSGCON(025 JOV0846 *LIBL/SYMSGF)
     A            W1LNR     R        B  8 29REFFLD(KOSBUR/BULNR *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A N60        W1BIL1    R        B  8 38REFFLD(KOSBUR/BUBILN *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A  60        W1TXT1        35A  B  8 38TEXT('BUDSJ-TEKST')
     A                                      COLOR(YLW)
     A N60        W1FED1         6Y 0B  8 49EDTCDE(4)
     A                                      COLOR(YLW)
     A  71                                  DSPATR(RI)
     A                                  9  2MSGCON(025 DIV2248 *LIBL/SYMSGF)
     A            W1TNR     R        B  9 29REFFLD(KOSBUR/BUTNR *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9 38MSGCON(020 DIV2326 *LIBL/SYMSGF)
     A                                 10  2MSGCON(025 JOV0319 *LIBL/SYMSGF)
     A            W1TUNR    R        B 10 29REFFLD(KOSBUR/BUTUNR *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(025 DIV2249 *LIBL/SYMSGF)
     A            W1BNR     R        B 11 29REFFLD(KOSBUR/BUBNR *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A N60                             12  2MSGCON(025 DIV2288 *LIBL/SYMSGF)
     A  60                             12  2MSGCON(025 JOV0847 *LIBL/SYMSGF)
     A N60        W1BIL2    R        B 12 29REFFLD(KOSBUR/BUBILN *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A  60        W1TXT2        35A  B 12 29TEXT('BUDSJ-TEKST')
     A                                      COLOR(YLW)
     A N60        W1FED2         6Y 0B 12 40EDTCDE(4)
     A                                      COLOR(YLW)
     A  70                                  DSPATR(RI)
     A                                 13  2MSGCON(025 DIV2353 *LIBL/SYMSGF)
     A            W1OAVD         4Y 0B 13 29EDTCDE(4)
     A                                      COLOR(YLW)
     A            W1OPD          7Y 0B 13 34EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 14  2MSGCON(025 DIV2413 *LIBL/SYMSGF)
     A            W1BUST    R        B 14 29REFFLD(KOSBUR/BUST *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                 14 32MSGCON(035 JOV0049 *LIBL/SYMSGF)
     A                                 15 32MSGCON(035 JOV0050 *LIBL/SYMSGF)
     A                                 16 32MSGCON(035 JOV0051 *LIBL/SYMSGF)
     A                                 17 32MSGCON(035 JOV0052 *LIBL/SYMSGF)
     A                                 18 32MSGCON(035 JOV0053 *LIBL/SYMSGF)
     A                                 19 32MSGCON(035 JOV0054 *LIBL/SYMSGF)
     A                                 21  2MSGCON(025 DIV2327 *LIBL/SYMSGF)
     A            W1FEYN         1A  B 21 29TEXT('Vis fergerekv. Y/N/blank')
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'Y' 'J' 'N')
     A                                 21 31MSGCON(030 DIV2328 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R PLDIFF
S01  A*                                     WINDOW(*DFT 12 80)
     A                                      CF12(12 'Returner uten oppdatering')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                  1  2'SYSPED/SYKS12'
     A                                      COLOR(BLU)
S01  A*                                 1 19'Ikke samsvar mellom beløpene'
N01  A                                  1 19MSGCON(018 SHU1322 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 48'Brutto'
     A                                      COLOR(WHT)
     A                                  2 63'Netto'
     A                                      COLOR(WHT)
S01  A*                                 3  1'Beløpet på budsjettposten du plukk-
S01  A*                                     er ....:'
N01  A                                  3  1MSGCON(042 SHU1323 *LIBL/SYMSGF)
     A            BRTBUB    R        O  3 44REFFLD(KOSBUR/BUBL *LIBL/KOSBU)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A            BUBL      R        O  3 58REFFLD(KOSBUR/BUBL *LIBL/KOSBU)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
S01  A*                                 4  1'er ulikt beløpet du oppgav i konte-
S01  A*                                     ringen.:'
N01  A                                  4  1MSGCON(042 SHU1324 *LIBL/SYMSGF)
     A            BRTKOB    R        O  4 44REFFLD(KOSBUR/BUBL *LIBL/KOSBU)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A            NETKOB    R        O  4 58REFFLD(KOSBUR/BUBL *LIBL/KOSBU)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
S01  A*                                 6  1'Hva ønsker du å gjøre..:'
N01  A                                  6  1MSGCON(023 SHU1325 *LIBL/SYMSGF)
     A            WPLNR          1A  B  6 31COLOR(YLW)
S01  A*                                 6 45'Differanse:'
N01  A                                  6  1MSGCON(011 SHU1326 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WPLDIF    R        B  6 58REFFLD(KOSBUR/BUBL *LIBL/KOSBU)
     A                                      EDTCDE(J)
     A                                      COLOR(YLW)
S01  A*                                 7 10'1=Konterings-beløpet skal benyttes-
S01  A*                                                               '
N01  A                                  7 10MSGCON(034 SHU1327 *LIBL/SYMSGF)
S01  A*                                 8 10'2=Konterings-beløpet benyttes, dif-
S01  A*                                     f. (kan endres) legges ut '
N01  A                                  8 10MSGCON(067 SHU1328 *LIBL/SYMSGF)
S01  A*                                 9 10'  som rest (ny åpen budsjettpost m-
S01  A*                                     ed samme referanser)      '
N01  A                                  9 10MSGCON(060 SHU1329 *LIBL/SYMSGF)
S01  A*                                10 10'3=Budsjettpostens beløp skal benyt-
S01  A*                                     tes                       '
N01  A                                  9 10MSGCON(060 SHU1330 *LIBL/SYMSGF)
     A                                 11  2MSGCON(012 BBA0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R ASSUME
     A                                      ASSUME
     A                                  1  4' '
     A          R BEKREFTF3
     A*%%TS  SD  20181002  072522  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                WINDOW(*DFT 5 55 *NOMSGLIN)
     A  *DS4                                WINDOW(*DFT 5 55 *NOMSGLIN)
     A                                      CF12(12 'FORRIGE')
     A                                      CF23(23 'BEKREFT')
     A                                  1  2'SYKS12'
     A                                      COLOR(BLU)
     A                                  1  9MSGCON(045 JOV1770 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(050 JOV1771 *LIBL/SYMSGF)
     A                                  3  2MSGCON(050 JOV1772 *LIBL/SYMSGF)
     A                                  4  2MSGCON(050 JOV1773 *LIBL/SYMSGF)
     A                                  5  2MSGCON(050 JOV1774 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
