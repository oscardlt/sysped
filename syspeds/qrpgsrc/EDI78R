     FEDISNUF   CF   F  256        WORKSTN USROPN
     F                                     INFDS(FEEDBK)
     FEDISE     IF   E           K DISK
     FEDIS      UF   E           K DISK
     FEDIM6     UF   E           K DISK
     FEDII      IF   E           K DISK
     FSADH      UF   E           K DISK    USROPN
     FSAEH      UF   E           K DISK    USROPN
     FTRUH      UF   E           K DISK    USROPN
     FTRIH      UF   E           K DISK    USROPN
     FTRUGH     UF   E           K DISK    USROPN
     FSVXH      UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
     FSVNH      UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
     FSVXGH     UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
      *____________________
      * DS for input string
      *""""""""""""""""""""
     D                 DS
     D  A                      1     70
     D                                     DIM(70)                              Input string
     D  ADS                    1     70
      *_____________________
      * DS for output string
      *"""""""""""""""""""""
     D                 DS
     D  B                      1     80
     D                                     DIM(80)                              Output string
     D  LDATA                  1     80
     D FEEDBK          DS
     D  MAJMIN               401    404
     D  MAJCOD               401    402
     IEDISNUF   AA  09
      *//////////////
      * MAIN LOGIC //
      *//////////////
      *_____
      * INIT
      *"""""
     C                   EXSR      SRA
      *__________
      * Open ICFF
      *""""""""""
     C                   OPEN      EDISNUF                              98
     C     MAJCOD        CABGT     '03'          END
     C     *IN98         CABEQ     '1'           END
      *____________________________________________
      * RECEIVE "WELCOME TO IBM IN EXPEDITE/DIRECT"
      *""""""""""""""""""""""""""""""""""""""""""""
     C  N98MAJMIN        DOUEQ     '0300'
     C     MAJMIN        OREQ      '0000'
     C                   READ      EDISNUF                              9898
     C     MAJCOD        CABGT     '03'          END
     C  N98              END
      *______________________________
      * LOGON TO INFORMATION EXCHANGE
      *""""""""""""""""""""""""""""""
     C                   MOVEL(P)  'IELOGON'     ADS
     C     ADS           CAT       'ACCOUN':1    ADS
     C     ADS           CAT       'T(':0        ADS
     C     ADS           CAT       INACC:0       ADS
     C     ADS           CAT       ') USER':0    ADS
     C     ADS           CAT       'ID(':0       ADS
     C     ADS           CAT       INUSR:0       ADS
     C     ADS           CAT       ') PASS':0    ADS
     C     ADS           CAT       'WORD(':0     ADS
     C     ADS           CAT       INPW:0        ADS
     C     ADS           CAT       ')':0         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     61            AXH
     C                   EXSR      SRÅ

     C                   MOVEL(P)  ' TIMEZON'    ADS
     C     ADS           CAT       'E(':0        ADS
     C     ADS           CAT       WTIMZ:0       ADS
     C     ADS           CAT       ') ACK(':0    ADS
     C     ADS           CAT       'D);':0       ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     24            AXH
     C                   EXSR      SRÅ

     C                   EXCEPT    IELOGN
     C     MAJCOD        CABGT     '03'          END
      * RECEIVE "DATA SET"
     C  N98MAJMIN        DOUEQ     '0300'
     C     MAJMIN        OREQ      '0000'
     C                   READ      EDISNUF                              9898
     C     MAJCOD        CABGT     '03'          END
     C                   END
      *_________________
      * DEFINE NICKNAMES
      *"""""""""""""""""
      * Avsender
     C     S0004         CHAIN     EDII                               20
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       LDATA
     C                   MOVEL(P)  'DEFINE N'    ADS
     C     ADS           CAT       'ICKNAM':0    ADS
     C     ADS           CAT       'E(':0        ADS
     C     ADS           CAT       S0004:0       ADS
     C     ADS           CAT       ') ACCO':0    ADS
     C     ADS           CAT       'UNT(':0      ADS
     C     ADS           CAT       INACC:0       ADS
     C     ADS           CAT       ') USER':0    ADS
     C     ADS           CAT       'ID(':0       ADS
     C     ADS           CAT       INUSR:0       ADS
     C     ADS           CAT       ');':0        ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     61            AXH
     C                   EXSR      SRÅ

     C                   EXCEPT    IELOGN
     C     MAJCOD        CABGT     '03'          END
      * RECEIVE "DATA SET"
     C  N98MAJMIN        DOUEQ     '0300'
     C     MAJMIN        OREQ      '0000'
     C                   READ      EDISNUF                              9898
     C     MAJCOD        CABGT     '03'          END
     C                   END
      * Mottaker
     C     S0010         CHAIN     EDII                               20
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       LDATA
     C                   MOVEL     S0010         W0010            10
     C                   MOVEL     S0010         W0010A            8
     C     W0010A        IFEQ      'NO000119'
     C                   MOVE      *BLANKS       W0010
     C                   MOVEL     'NO000119'    W0010
     C                   ENDIF
     C                   MOVEL(P)  'DEFINE N'    ADS
     C     ADS           CAT       'ICKNAM':0    ADS
     C     ADS           CAT       'E(':0        ADS
     C     ADS           CAT       W0010:0       ADS
     C     ADS           CAT       ') ACCO':0    ADS
     C     ADS           CAT       'UNT(':0      ADS
     C     ADS           CAT       INACC:0       ADS
     C     ADS           CAT       ') USER':0    ADS
     C     ADS           CAT       'ID(':0       ADS
     C     ADS           CAT       INUSR:0       ADS
     C     ADS           CAT       ');':0        ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     61            AXH
     C                   EXSR      SRÅ

     C                   EXCEPT    IELOGN
     C     MAJCOD        CABGT     '03'          END
      * RECEIVE "DATA SET"
     C  N98MAJMIN        DOUEQ     '0300'
     C     MAJMIN        OREQ      '0000'
     C                   READ      EDISNUF                              9898
     C     MAJCOD        CABGT     '03'          END
     C                   END
      *_____________
      * SEND EDIFACT
      *"""""""""""""
     C                   EXSR      SR
      *_________________________________
      * LOGOFF FROM INFORMATION EXCHANGE
      *"""""""""""""""""""""""""""""""""
     C                   EXCEPT    IELOGF
     C     MAJCOD        CABGT     '03'          END
      *___________________
      * RECEIVE "DATA SET"
      *"""""""""""""""""""
     C  N98MAJMIN        DOUEQ     '0308'
     C     MAJMIN        OREQ      '0008'
     C                   READ      EDISNUF                              9898
     C     MAJCOD        CABGT     '03'          END
     C                   END
      *________________________________________________________
      * Oppdatere sendningen med status=B (I kundens postkasse)
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     KEYA          CHAIN     EDIS                               51
     C                   MOVE      'B'           SST
     C     WMBRS         MULT      80            SMBRS
     C                   UPDATE    EDISR
     C     KEYA          CHAIN     EDIM6                              51
     C     *IN51         DOWEQ     '0'
     C                   MOVE      'B'           MST
     C                   UPDATE    EDIMR

     C     M0065         IFEQ      'CUSDEC'
     C     *IN52         ANDEQ     '0'
     C     *IN53         ANDEQ     '0'
     C     *IN55         ANDEQ     '0'
     C     *IN56         ANDEQ     '0'
     C     M0065         OREQ      'SANCRT'
     C     *IN57         ANDEQ     '0'
     C     *IN60         ANDEQ     *OFF
     C                   EXSR      SRB
     C                   ENDIF

     C     KEYA          READE     EDIM6                                  51
     C  N51              END
      *____________
      * END PROGRAM
      *""""""""""""
     C     END           TAG
     C                   MOVE      MAJCOD        WAJCOD
     C                   MOVE      '1'           *INLR
      *////////////////
      * SEND EDIFACT //
      *////////////////
     C     SR            BEGSR
      *_____
      * SEND
      *"""""
      * SEND EDIFACT DATA
     C     WMBR          READE     EDISE                                  51
     C     *IN51         DOWEQ     '0'
     C                   EXCEPT    IESNDD
     C     WMBR          READE     EDISE                                  51
     C  N51              END

     C     MAJCOD        CABGT     '03'          END
      * RECEIVE "MESSAGE REQUEST"
     C  N98MAJMIN        DOUEQ     '0300'
     C     MAJMIN        OREQ      '0000'
     C                   READ      EDISNUF                              9898
     C     MAJCOD        CABGT     '03'          END
     C  N98              END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SSN           WSN
     C                   MOVE      *BLANKS       WTIMZ             5
     C     *LIKE         DEFINE    MAJCOD        WAJCOD
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSN
     C     KEYB          KLIST
     C                   KFLD                    MAVD
     C                   KFLD                    MTDN
     C     KEYC          KLIST
     C                   KFLD                    MAVD
     C                   KFLD                    MTDN
      *_____________
      * Init av felt
      *"""""""""""""
      * Workfields for Array's
     C                   Z-ADD     0             AX                2 0          Index for A
     C                   Z-ADD     1             BX                2 0          Index for B
     C                   Z-ADD     0             AXV               2 0          Leftindx A
     C                   Z-ADD     0             AXH               2 0          Rightindx A
     C                   MOVE      'R'           WDIR              1            Search direction
     C                   MOVE      *BLANKS       WCHAR             1            Test character
     C                   MOVE      'Y'           WCHART            1            Test character
     C                   MOVE      *BLANKS       WCONT             5            Continues
      * Workfields for separation character  '
     C                   BITON     '123457'      WAPSTR            1
     C                   BITOFF    '06'          WAPSTR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WSN
     C                   PARM                    WAJCOD
     C                   PARM                    WMBRS            10 0
     C                   PARM                    WMBR             10
      *_______________________
      * Hente sendningsverdier
      *"""""""""""""""""""""""
     C     KEYA          CHAIN(N)  EDIS                               51
     C     S0004         CHAIN(N)  EDII                               51
      *______________________________
      * Hente systemverdi: qcutoffset
      *""""""""""""""""""""""""""""""
     C                   CALL      'EDI34C'
     C                   PARM                    WTIMZ
     C                   MOVEL     'E'           WTIMZ
      *________________________________________
      * Open files from SYSPED400, if installed
      *""""""""""""""""""""""""""""""""""""""""
     C                   OPEN      SADH                                 52
     C                   OPEN      SAEH                                 53
     C                   OPEN      TRUH                                 55
     C                   OPEN      TRIH                                 56
     C                   OPEN      TRUGH                                57
     C                   OPEN      SVXH                                 60
     C                   OPEN      SVNH                                 60
     C                   OPEN      SVXGH                                60
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Update SYSPED400                                     SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C                   SELECT
     C                   WHEN      M1001 = '929'
     C     KEYB          CHAIN     SADH                               54
     C     *IN54         IFEQ      '0'
     C                   MOVE      'B'           SIST
     C                   UPDATE    SADHR
     C                   ENDIF
     C                   WHEN      M1001 = '830'
     C     KEYB          CHAIN     SAEH                               54
     C     *IN54         IFEQ      '0'
     C                   MOVE      'B'           SEST
     C                   UPDATE    SAEHR
     C                   ENDIF
     C                   WHEN      M1001 = 'TET'
     C     KEYB          CHAIN     TRUH                               54
     C     *IN54         IFEQ      '0'
     C                   MOVE      'B'           THST
     C                   UPDATE    TRUHR
     C                   ENDIF
     C                   MOVEL     M0068         TGGNR
     C     TGGNR         CHAIN     TRUGH                              54
     C     *IN54         IFEQ      '0'
     C                   MOVE      'B'           TGST
     C                   UPDATE    TRUGHR
     C                   ENDIF
     C                   WHEN      M1001 = 'TEI'
     C     KEYB          CHAIN     TRIH                               54
     C     *IN54         IFEQ      '0'
     C                   MOVE      'B'           TIST
     C                   UPDATE    TRIHR
     C                   ENDIF
      *
     C                   WHEN      M1001 = 'STE'
     C     KEYC          CHAIN     SVXH                               54
     C     *IN54         IFEQ      '0'
     C                   MOVE      'B'           SV_THST
     C                   UPDATE    SVXHR
     C                   ENDIF
     C                   MOVEL     M0068         SV_TGGNR
     C     SV_TGGNR      CHAIN     TRUGH                              54
     C     *IN54         IFEQ      '0'
     C                   MOVE      'B'           SV_TGST
     C                   UPDATE    SVXGHR
     C                   ENDIF
      *
     C                   WHEN      M1001 = 'STI'
     C     KEYC          CHAIN     SVNH                               54
     C     *IN54         IFEQ      '0'
     C                   MOVE      'B'           SV_TIST
     C                   UPDATE    SVNHR
     C                   ENDIF
      *
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for output string, read input from left              SRÅ
      *////////////////////////////////////////////////////////////
     C     SRÅ           BEGSR
      *_______________
      * Find positions
      *""""""""""""""""
      * Find first non-0 position
     C     WDIR          IFEQ      'L'                                          <-------------I
     C     AXV           DO        AXH           AXV                             <-----------II
     C     A(AXV)        COMP      '0'                                  8383                 II
     C   83              END                                                     <-----------II
      * Find last non-blank position                                     I
     C                   ELSE                                                   <-------------I

     C     SRÅ01         TAG                                                                  I

     C     AXH           IFGT      AXV                                           <-----------II
     C     A(AXH)        IFEQ      *BLANKS                                        <---------III
     C                   SUB       1             AXH                                        III
     C                   GOTO      SRÅ01                                                    III
     C                   END                                                      <---------III
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C                   MOVE      'R'           WDIR
      *________________________________________________
      * Read input string and place it in output string
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AXV           DO        AXH           AXV                            <-------------I
     C                   MOVE      A(AXV)        WCHAR                                        I
      * If invalid character (' + : ?) then replace with 'Ø'             I
     C     WCHART        IFEQ      'Y'                                           <-----------II
     C     WDIR          IFEQ      'R'                                            <---------III
     C     WCHAR         COMP      WAPSTR                                 83                III
     C  N83WCHAR         COMP      '+'                                    83                III
     C  N83WCHAR         COMP      ':'                                    83                III
     C  N83WCHAR         COMP      '?'                                    83                III
     C   83              EXSR      SRÅA                                                     III
     C                   END                                                      <---------III
     C                   END                                                     <-----------II
      * Move to output string                                            I
     C                   MOVE      WCHAR         B(BX)                                        I
     C                   ADD       1             BX                                           I
      * If arrayindex for output array is over 80 Then add a record      I
     C     BX            IFGT      80                                            <-----------II
     C                   EXCEPT    IELOGN                                                    II
     C                   Z-ADD     1             BX                                          II
     C                   MOVE      *BLANKS       LDATA                                       II
     C                   END                                                     <-----------II

     C                   END                                                    <-------------I

     C                   MOVE      'Y'           WCHART

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for replace control characters                      SRÅA
      *////////////////////////////////////////////////////////////
     C     SRÅA          BEGSR
      * Move '?' before the character
     C                   MOVE      '?'           B(BX)
     C                   ADD       1             BX
     C     BX            IFGT      80                                           <-------------I
     C                   EXCEPT    IELOGN                                                     I
     C                   Z-ADD     1             BX                                           I
     C                   MOVE      *BLANKS       LDATA                                        I
     C                   END                                                    <-------------I

     C                   ENDSR
     OEDISNUF   E            IELOGN
     O                                           K8 'SNDDATA '
     O                       LDATA               80
     O          E            IELOGF
     O                                           K8 'SNDDATA '
     O                                              '/*LOGOFF'
     O          E            IESNDD
     O                                           K8 'SNDDATA '
     O                       SDATA               80
