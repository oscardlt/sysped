/* bruker DCLF - for å kompilere må fil QTEMP/WDIREUSR lages ...        */
/* F.eks:    DSPDIRE    USRID(JOVO NO542SYS) OUTPUT(*OUTFILE)           */
/*                        OUTFILE(QTEMP/WDIREUSR) OUTFILFMT(*TYPE2)     */
             PGM        parm(&AVD &OPD &LIN &KUN)
             DCL        VAR(&AVD)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&OPD)    TYPE(*CHAR) LEN(7)
             DCL        VAR(&LIN)    TYPE(*CHAR) LEN(5)
             DCL        VAR(&KUN)    TYPE(*CHAR) LEN(8)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&UNIK)   TYPE(*CHAR) LEN(7) /* Unikkode */
             DCL        VAR(&NBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SNDNA) TYPE(*CHAR) LEN(8)
             DCL        VAR(&SNDAD) TYPE(*CHAR) LEN(8)
             DCL        VAR(&SENDER) TYPE(*CHAR) LEN(50)
             DCL        VAR(&EPOST) TYPE(*CHAR) LEN(70)
/* OBS - DCLF må være siste declare-setning.   */
             DCLF       FILE(WDIREUSR) RCDFMT(*ALL)
             RTVDTAARA  DTAARA(FIRMDT (141 10)) RTNVAR(&LIB)
             RTVJOBA    NBR(&NBR) USER(&QUSER)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &NBR)
/* Finn epsotadresse til den som sender */
             CHGVAR     VAR(&SNDNA) VALUE(&QUSER)
             RTVNETA    SYSNAME(&SNDAD)
             CALL       PGM(EPOS04E) PARM(&QUSER &SNDNA &SNDAD &EPOST)
/* Finn navnet til den som sender ut fra userprofile */
             RTVUSRPRF  TEXT(&SENDER)
/* ... men dersom fornavn/etternavn er gitt i workdire - bruk den*/
             DLTF       FILE(QTEMP/WDIREUSR)
             MONMSG     MSGID(CPF0000)
             DSPDIRE    USRID(&SNDNA &SNDAD) OUTPUT(*OUTFILE) +
                          OUTFILE(QTEMP/WDIREUSR) OUTFILFMT(*TYPE2)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(NODIRE))
             RCVF       DEV(*FILE) RCDFMT(*FILE)
             IF         COND(&DFFSTN *NE ' ') THEN(DO)
             CHGVAR     VAR(&SENDER) VALUE(&DFFSTN *bcat &DFLSTN)
             ENDDO
 NODIRE:

             ADDPFM     FILE(ARKSRC) MBR(&UNIK)
             MONMSG     MSGID(CPF0000)
             CLRPFM     FILE(ARKSRC) MBR(&UNIK)
             OVRDBF     FILE(ARKSRC) TOFILE(&LIB/ARKSRC) MBR(&UNIK)

 /*Vis bilde med mail som vil bli sendt ... */
             CALL       PGM(SYFI10XE) PARM(&AVD &OPD &LIN &KUN &UNIK +
                          &SENDER &EPOST)
             RMVM       FILE(ARKSRC) MBR(&UNIK)
             MONMSG     MSGID(CPF0000)
             ENDPGM
