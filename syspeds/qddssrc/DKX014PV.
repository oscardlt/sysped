********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NOMARG --> ALLE POS. FLYTTES 3 POS. TIL HØYRE
********************************************************************
      *_________________________________
      * VARELISTE A    OBS! SAMME SOM B
      *"""""""""""""""""""""""""""""""""
     A          R DKX014V1
     A N96N50     PVFM1         75      1  2
     A  96N50     PVFM12        75      1  2
     A N96N50     PVFM2         75      2  2
     A  96N50     PVFM22        75      2  2
     A                                  3  8'_______________________________'
     A                                  3  8'_______________________________'
     A                                  3115'MRN'
     A                                      HIGHLIGHT
     A                                  4  8'|List of Items                |'
     A                                  4  8'|List of Items                |'
     A                                      UNDERLINE
     A                                  4 52'OoDep:'
     A            THCATS         8      4 61
     A            XHTRNR        18      4110BARCODE(CODE128 3 *HRI *HRZ +
     A                                      (*WIDTH 0.010) (*RATIO 2.5) X'02')
     A N96                              5  8'|Sheet       A    |     |     |'
     A N96                              5  8'|Sheet       A    |     |     |'
     A  96                              5  8'|Sheet       B    |     |     |'
     A  96                              5  8'|Sheet       B    |     |     |'
     A                                  5  8'                               '
     A                                      UNDERLINE
     A            XSIDEN         3  0   5 28EDTCDE(Z)
     A            XSIDET         3  0   5 34EDTCDE(Z)
     A                                  5 52'Date:'
     A            THTRDT         8  0   5 61EDTWRD('    .  .  ')
     A                                  6 52'Our ref:'
     A            THAVD          4  0   6 61
     A                                  6 65'/'
     A            THTDN          7  0   6 66
     A                                  6 73'/'
     A            THSG           3      6 74
     A                                  8  8'__________________________________-
     A                                      ___________________________________-
     A                                      ___________________________________-
     A                                      ___________________________________-
     A                                      ___________'
     A                                  9  8'|Item No            |'
     A                                  9 29'Marks / numbers    |'
     A                                  9 49'Number / kind               |'
     A                                  9 78'Container No      |'
     A                                  9 97'Description of goods              -
     A                                                                |'
     A                                 10  8'|(32)               |'
     A                                      UNDERLINE
     A                                 10 29'(31.1)             |'
     A                                      UNDERLINE
     A                                 10 49'(31.2)                      |'
     A                                      UNDERLINE
     A                                 10 78'(31.3)            |'
     A                                      UNDERLINE
     A                                 10 97'(31.4)                            -
     A                                                                |'
     A                                      UNDERLINE
     A                                 11  8'|Regime             |'
     A                                 11 29'Commodity Code     |'
     A                                 11 49'Sensitivity Code            |'
     A                                 11 78'Sens. Quantity    |'
     A                                 11 97'Summary declaration /'
     A                                 11119'Previous document                 -
     A                                          |'
     A                                 12  8'|(1/3)              |'
     A                                      UNDERLINE
     A                                 12 29'(33)               |'
     A                                      UNDERLINE
     A                                 12 49'(31.5)                      |'
     A                                      UNDERLINE
     A                                 12 78'(31.6)            |'
     A                                      UNDERLINE
     A                                 12 97'(40)                              -
     A                                                                |'
     A                                      UNDERLINE
     A                                 13  8'|Country of dispatch|'
     A                                 13 29'Country of dest.   |'
     A                                 13 49'    Gross mass              |'
     A                                 13 78'   Net mass       |'
     A                                 13 97'Additional information / Document -
     A                                      produced / Certificates   |'
     A                                 14  8'|export             |'
     A                                 14 29'                   |'
     A                                 14 49'          (kg)              |'
     A                                 14 78'        (kg)      |'
     A                                 14 97'and authorisation                 -
     A                                                                |'
     A                                 15  8'|(15)               |'
     A                                      UNDERLINE
     A                                 15 29'(17)               |'
     A                                      UNDERLINE
     A                                 15 49'(35)                        |'
     A                                      UNDERLINE
     A                                 15 78'(38)              |'
     A                                      UNDERLINE
     A                                 15 97'(44)                              -
     A                                                                |'
     A                                      UNDERLINE
     A                                 16  8'|Consignor / Export'
     A                                 16 82'|Consignee'
     A                                 16 97'                                  -
     A                                                                |'
     A                                 17  8'|(2)                              '
     A                                      UNDERLINE
     A                                 17 42'                                  '
     A                                      UNDERLINE
     A                                 17 76'      '
     A                                      UNDERLINE
     A                                 17 82'|(8)'
     A                                      UNDERLINE
     A                                 17 86'                                  -
     A                                                                         -
     A                                        |'
     A                                      UNDERLINE
     A                                 18  8'__________________________________-
     A                                      ___________________________________-
     A                                      ___________________________________-
     A                                      ___________________________________-
     A                                      ___________'
      *
     A          R DKX014V2                   SPACEB(1)
     A                                     8'|'
     A N98        XLN            4  0     11EDTCDE(Z)
     A                                    26'|'
     A            TVMN          42        27
     A                                    48'|'
     A            TVNT           5  0     49EDTCDE(Z)
     A            XTKTXTE       22        55
     A                                    77'|'
     A            TVCNR         17        78
     A                                    97'|'
     A            TVVT          48        98
     A                                   157'|'
      *
     A          R DKX014V3
     A                                     8'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                         -
     A                                                 '
     A                                      UNDERLINE
      *
     A          R DKX014V4                   SPACEB(1)
     A                                     8'|'
     A N98        TVDK           5        11
     A                                    26'|'
     A N98N27     TVVNT          6  0     29
     A N98 27                             29'--------'
     A                                    48'|'
     A            TVFV           2        53
     A                                    77'|'
     A            TVFVNT        11  3     80EDTWRD('        ,   ')
     A                                    97'|'
     A            TVTDT          6        98
     A            TVTDR         20       109
     A                                   157'|'
      *
     A          R DKX014V5                   SPACEB(1)
     A                                     8'|'
     A N98        TVALK          2        11
     A                                    26'|'
     A N98        TVBLK          2        29
     A                                    48'|'
     A N98        TVVKTB         9  1     51EDTCDE(4)
     A                                    77'|'
     A N98        TVVKTN         9  1     81EDTCDE(4)
     A                                    97'|'
     A            XVDTY         28        98
     A            TVDREF        20       136
     A                                   157'|'
      *
     A          R DKX014V6                   SPACEB(1)
     A                                     8'|'
     A N98        TVALK          2        11
     A                                    26'|'
     A N98        TVBLK          2        29
     A                                    48'|'
     A N98        TVVKTB         9  1     51EDTCDE(4)
     A                                    77'|'
     A N98        TVVKTN         9  1     81EDTCDE(4)
     A                                    97'|'
     A            TVDO          26        98
     A                                   157'|'
      *
     A          R DKX014V7                   SPACEB(1)
     A                                     8'|'
     A N98        TVALK          2        11
     A                                    26'|'
     A N98        TVBLK          2        29
     A                                    48'|'
     A N98        TVVKTB         9  1     51EDTCDE(4)
     A                                    77'|'
     A N98        TVVKTN         9  1     81EDTCDE(4)
     A                                    97'|'
     A            XVTLO         35        98
     A            XVEXKD         2       145
     A            TVEXLK         2       151
     A                                   157'|'
      *
     A          R DKX014V8                   SPACEB(1)
     A                                     8'|'
     A N98        TVALK          2        11
     A                                    26'|'
     A N98        TVBLK          2        29
     A                                    48'|'
     A N98        TVVKTB         9  1     51EDTCDE(4)
     A                                    77'|'
     A N98        TVVKTN         9  1     81EDTCDE(4)
     A                                    97'|'
     A            TVMTXT        48        98
     A                                   157'|'
      *
     A          R DKX014V9                   SPACEB(1)
     A                                     8'|'
     A            TVNAS         30        11
     A                                    82'|'
     A            TVNAK         30        84
     A                                   157'|'
      *
     A          R DKX014VA                   SPACEB(1)
     A                                     8'|'
     A            TVADS1        30        11
     A                                    82'|'
     A            TVADK1        30        84
     A                                   157'|'
      *
     A          R DKX014VB                   SPACEB(1)
     A                                     8'|'
     A            TVPNS          9        11
     A            TVPSS         24        24
     A            TVLKS          2        48
     A                                    82'|'
     A            TVPNK          9        84
     A            TVPSK         24        96
     A            TVLKK          2       134
     A                                   157'|'
      *
     A          R DKX014VC                   SPACEB(1)
     A                                     8'|'
     A            TVPSS         24        11
     A                                    82'|'
     A            TVPSK         24        84
     A                                   157'|'
      *
     A          R DKX014VD
     A N96N50     PVFM3         75     64  2
     A  96N50     PVFM32        75     64  2
