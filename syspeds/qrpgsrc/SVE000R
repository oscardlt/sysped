********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
10XXX * 02 CB  Ikke automatisk sigillering og ingen timeout, ikke merket
      *        Fjernet all gammel merking grunnet ny logikk PKI
11067 * 03 CB  PTF11 Endre signatur
11068 * 04 CB  PTF11 Endre status
11069 * 05 CB  PTF11 Annulera ærende
11XXX * 06 CB  PTF11 Ændra parametrar till arkivfunktionen
11XXX * 07 CB  PTF11 Testa status på varuposter vid Skicka
********************************************************************
     FFRISOK    IF   E           K DISK
     FFRISOL01  IF   E           K DISK    RENAME(FRISOKR:FRISOK9)
     FSVE000D   CF   E             WORKSTN SFILE(SVE000DA:ZRECNO)
     F                                     INFDS(FBAREA)
     FSVEH      UF   E           K DISK
     FSVEH3     IF   E           K DISK    RENAME(SVEHR:SVEHR3)
     FSVEH4     IF   E           K DISK    RENAME(SVEHR:SVEHR4)
     FSVEH5     IF   E           K DISK    RENAME(SVEHR:SVEHR5)
     FSVEH6     IF   E           K DISK    RENAME(SVEHR:SVEHR6)
     FSVEH7     IF   E           K DISK    RENAME(SVEHR:SVEHR7)
     FSVEH8     IF   E           K DISK    RENAME(SVEHR:SVEHR8)
     FSVEH10    IF   E           K DISK    RENAME(SVEHR:SVEHR10)
     FSVEH11    IF   E           K DISK    RENAME(SVEHR:SVEHR11)
     FSVEA      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FSVTHA     IF   E           K DISK
N07  FSVEV2     IF   E           K DISK
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN12         WIN12
     C                   EVAL      ZVEH_SYAV=UAVD
     C                   EVAL      ZVEH_SYSG=USG
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SADHFRI       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     FRIKEY        KLIST
     C                   KFLD                    SVEH_SYAV
     C                   KFLD                    SVEH_SYOP
     C                   KFLD                    WSKODE
     C     FR1KEY        KLIST
     C                   KFLD                    WSKODE
     C                   KFLD                    WFSSOK
     C     FR1KEYE       KLIST
     C                   KFLD                    WSKODE
     C     *LIKE         DEFINE    FSDTOP        WFSDTO
     C     *LIKE         DEFINE    FSKODE        WSKODE
     C                   MOVEL     '*CE'         WSKODE
     C     KEYA          KLIST
     C                   KFLD                    Z2SYAV
     C                   KFLD                    Z2SYOP
     C     KEYB          KLIST
     C                   KFLD                    XSYAVFF
     C                   KFLD                    XSYOPFF
     C     KEYH          KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSSYOP
     C     KEY41         KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSAVKN
     C                   KFLD                    ZSDTA
     C     KEY42         KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSAVKN
     C     KEY41A        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    ZSAVKN
     C                   KFLD                    ZSDTA
     C     KEY42A        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    ZSAVKN
     C     KEY51         KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSMOKN
     C                   KFLD                    ZSDTM
     C     KEY52         KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSMOKN
     C     KEY51A        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    ZSMOKN
     C                   KFLD                    ZSDTM
     C     KEY52A        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    ZSMOKN
     C     KEY61         KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSSYSG
     C                   KFLD                    ZSDTS
     C     KEY62         KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSSYSG
     C     KEY61A        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    ZSSYSG
     C                   KFLD                    ZSDTS
     C     KEY62A        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    ZSSYSG
     C     KEY7          KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSSYDT
     C     KEY8          KLIST
     C                   KFLD                    ZSSYAV
     C                   KFLD                    ZSSYST
     C     KUNDK         KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    ZSSYAV
N05  C     SVEHKEY       KLIST
N05  C                   KFLD                    SVEH_SYAV
N05  C                   KFLD                    SVEH_SYOP
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6

     C                   MOVE      UAVD          ZSSYAV
     C                   MOVE      USG           ZSSYSG
     C                   MOVE      99999999      ZSDTS
     C                   MOVE      'A'           KOAUNI
     C                   MOVE      UAVD          ZSSYAV
     C                   MOVE      USG           ZSSYSG
     C                   MOVE      99999999      ZSDTS
     C                   MOVEA     '000010000'   *IN(41)

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN92=*ON
     C                   EVAL      *IN93=*ON
     C                   WRITE     SVE000DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     ZSSYAV        IFEQ      0
     C     *IN46         ANDEQ     '0'
     C                   EXSR      LESRAL
     C                   ELSE
     C                   EXSR      LESREC
     C                   END

     C     SRB2          TAG
     C  N94              DO
     C                   DO        256
     C                   ADD       1             ZRECNO
      * kunderef REFERANSE ISTEDEN FOR MOTTAKERNAVN
     C     FRIKEY        CHAIN     FRISOK                             39
     C     *IN39         IFEQ      *OFF
     C                   MOVEL     FSSOK         SVEH_MONA
     C                   ENDIF
     C                   WRITE     SVE000DA
     C                   MOVE      ZRECNO        XRECN2            5 0
     C     ZSSYAV        IFEQ      0
     C     *IN46         ANDEQ     '0'
     C                   EXSR      LESRAL
     C                   ELSE
     C                   EXSR      LESREC
     C                   END
     C  N94              MOVE      SVEH_SYAV     XSYAVFF           4 0
     C  N94              MOVE      SVEH_SYOP     XSYOPFF           7 0
     C  N94              ENDDO
     C                   ENDDO
     C                   MOVE      ZRECNO        XRECNO            5 0

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   CLEAR                   SVEH_SYAV
     C                   CLEAR                   SVEH_SYOP
     C                   CLEAR                   SVEH_TUID
     C                   CLEAR                   SVEH_SYSG
     C                   CLEAR                   SVEH_SYDT
     C                   CLEAR                   SVEH_SYST
     C                   CLEAR                   SVEH_AVNA
     C                   CLEAR                   SVEH_MOLK
     C                   CLEAR                   SVEH_MONA
     C                   WRITE     SVE000DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVE000DØ
     C                   WRITE     SVE000DC
     C                   WRITE     SVE000DB
     C                   READ      SVE000DB

     C                   EVAL      *IN30=*OFF
     C                   EVAL      *IN31=*OFF

     C     *IN03         IFEQ      '0'
     C     *IN08         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     SRB4          TAG
     C     *IN06         IFEQ      '1'
     C     Z2SYAV        IFNE      0                                               4<-B
     C                   MOVE      Z2SYAV        ZSSYAV
     C                   END                                                       4<-B
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   ENDIF
      *_____________
      * F25=Bla frem
      *"""""""""""""
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C  N94KEYB          CHAIN(N)  SVEH                               33
     C                   SETOFF                                       25
     C                   GOTO      SRB2
     C                   END
      *__________
      * Ny søking
      *""""""""""
     C     Z2OP          IFNE      ' '                                           2<-B
     C     Z2SYOP        ORNE      0
     C     Z2TUID        ORNE      *BLANKS
     C     Z2SYSG        ORNE      *BLANKS
     C     Z2SYDT        ORNE      0
     C     Z2SYST        ORNE      *BLANKS
     C     Z2AVKN        ORNE      0
     C     Z2MOKN        ORNE      0
     C     Z2OP          IFEQ      ' '                                            3<-B
     C                   EXSR      NYSØK
     C                   GOTO      SRB1
     C                   ELSE                                                     3
     C     Z2OP          COMP      '1'                                    06
     C   06              DO                                                        4<-B
     C     Z2SYAV        IFNE      0                                                5<-B
     C                   MOVE      Z2SYAV        ZSSYAV
     C                   END                                                        5<-E
     C                   MOVE      ' '           Z2OP
     C                   MOVE      *ZEROS        Z2SYAV
     C                   GOTO      SRB4
     C                   END                                                       4<-E
     C     Z2SYOP        IFEQ      0                                               4<-B
     C                   SETON                                        9830
     C                   MOVE      'YBC0046'     MSGNR
     C                   ELSE                                                      4
     C                   EXSR      NYTDN
     C                   END                                                       4<-E
     C                   MOVE      *BLANKS       Z2TUID
     C                   MOVE      *BLANKS       Z2SYSG
     C                   MOVE      *ZEROS        Z2SYDT
     C                   MOVE      *BLANKS       Z2SYST
     C                   MOVE      *ZEROS        Z2AVKN
     C                   MOVE      *ZEROS        Z2MOKN
     C                   GOTO      SRB3
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     SVE000DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           IFNE      *BLANKS
     C     ZOP           ANDNE     '9'
     C     ZOP           ANDNE     '3'
     C     ZOP           ANDNE     '5'
     C     ZOP           ANDNE     '6'
     C     ZOP           ANDNE     'A'
N04  C     ZOP           ANDNE     'X'
N04  C     ZOP           ANDNE     'Y'
N04  C     ZOP           ANDNE     'Z'
     C     SVEH_SYST     IFEQ      'A'
     C     SVEH_SYST     OREQ      'C'
     C     SVEH_SYST     OREQ      'S'
     C     SVEH_SYST     OREQ      'E'
N03  C     SVEH_SYST     OREQ      'Y'
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   EVAL      ZMSGNO='SVE0004'
     C                   GOTO      SRB3
     C                   ENDIF
     C                   ENDIF
     C     ZOP           IFEQ      'S'
N05  C     ZOP           OREQ      '4'
     C     SVEH_SYST     ANDNE     *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   EVAL      ZMSGNO='SVE0004'
     C                   GOTO      SRB3
     C                   ENDIF

     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '3'           SRBC                           Kopiera
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C     ZOP           CASEQ     '5'           SRBE                           Vise
     C     ZOP           CASEQ     '9'           SRBH                           Historik
     C     ZOP           CASEQ     '6'           SRBP                           Skriv ut
     C     ZOP           CASEQ     'A'           SRAR                           Skriv ut
     C     ZOP           CASEQ     '11'          SRBS                           Skicka t Sigillering
     C     ZOP           CASEQ     'X'           SRBX                           Historik
N03  C     ZOP           CASEQ     'Y'           SRBY                           Signatur
N04  C     ZOP           CASEQ     'Z'           SRBZ                           Endre status
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB1
     C                   GOTO      SRB1

     C                   ENDIF

     C     *IN08         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   EXSR      SRB0
     C     *IN03         CABEQ     '0'           SRB1
     C                   ENDIF

     C     SRB9          ENDSR
     C***********************************************
     C     NYSØK         BEGSR
     C***********************************************
     C                   CLEAR                   SVE000DD
     C     Z2SYOP        IFNE      0                                            1<-B
     C     Z2SYAV        IFEQ      0
     C                   MOVE      UAVD          ZSSYAV
     C                   ELSE
     C                   MOVE      Z2SYAV        ZSSYAV
     C                   END
     C                   MOVE      Z2SYOP        ZSSYOP
     C                   ELSE                                                   1
     C     Z2TUID        IFNE      *BLANKS                                       2<-B
     C                   MOVE      Z2TUID        ZSTUID
     C                   ELSE                                                    2
     C     Z2SYSG        IFNE      *BLANKS                                        3<-B
     C                   MOVE      Z2SYSG        ZSSYSG
     C                   MOVE      Z2SYDT        ZSDTS
     C                   ELSE                                                     3
     C     Z2SYST        IFNE      *BLANKS                                         4<-B
     C                   MOVE      Z2SYST        ZSSYST
     C                   ELSE                                                      4
     C     Z2AVKN        IFNE      0                                                5<-B
     C                   MOVE      Z2AVKN        ZSAVKN
     C                   MOVE      Z2SYDT        ZSDTA
     C                   ELSE                                                       5
     C                   MOVE      Z2MOKN        ZSMOKN
     C                   MOVE      Z2SYDT        ZSDTM
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   MOVE      ' '           Z2OP
     C                   MOVE      *ZEROS        Z2SYAV
     C                   MOVE      *ZEROS        Z2SYOP
     C                   MOVE      *BLANKS       Z2TUID
     C                   MOVE      *BLANKS       Z2SYSG
     C                   MOVE      *ZEROS        Z2SYDT
     C                   MOVE      *BLANKS       Z2SYST
     C                   MOVE      *ZEROS        Z2AVKN
     C                   MOVE      *ZEROS        Z2MOKN
      *
     C                   MOVEA     '0000'        *IN(96)
     C                   EXSR      TESTB1
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYTDN         BEGSR
     C***********************************************
     C     Z2SYOP        IFEQ      0
     C                   MOVE      UAVD          Z2SYAV
     C                   END
     C     KEYA          CHAIN(N)  SVEH                               98
     C   98              DO
     C                   MOVE      'YBC0047'     MSGNR
     C                   GOTO      SLTDN
     C                   END
     C                   MOVE      '1'           *IN90
      *
     C     Z2OP          CASEQ     '2'           SRBB                           Endre
     C     Z2OP          CASEQ     '3'           SRBC                           Kopiera
     C     Z2OP          CASEQ     '4'           SRBD                           Slette
     C     ZOP           CASEQ     '5'           SRBE                           Vise
     C     Z2OP          CASEQ     '9'           SRBH                           Historik
     C     Z2OP          CASEQ     '6'           SRBP                           Skriv ut
     C     Z2OP          CASEQ     'P'           SRAR                           Skriv ut
     C     Z2OP          CASEQ     '11'          SRBS                           Skicka t Sigillering
     C     Z2OP          CASEQ     'X'           SRBX
N03  C     Z2OP          CASEQ     'Y'           SRBY
N04  C     Z2OP          CASEQ     'Z'           SRBZ
     C                   ENDCS
      *
     C     SLTDN         TAG
     C   98              MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN90
     C                   MOVE      ' '           Z2OP
     C  N98              MOVE      *ZEROS        Z2SYAV
     C  N98              MOVE      *ZEROS        Z2SYOP
      *
     C                   ENDSR
N03   *////////////////////////////////////////////////////////////
N03   *  Ændra signatur                                      SRBY /
N03   *////////////////////////////////////////////////////////////
N03  C     SRBY          BEGSR
N03   *_________________
N03   * Oppdatere SUBFIL
N03   *"""""""""""""""""
N03  C                   MOVE      *BLANKS       ZOP
N03  C                   UPDATE    SVE000DA
N03   *________________________
N03   * call på externt program
N03   *""""""""""""""""""""""""
N03  C                   CALL      'SVE015R'
N03  C                   PARM                    SVEH_SYAV
N03  C                   PARM                    SVEH_SYOP
N03  C                   ENDSR
N04   *////////////////////////////////////////////////////////////
N04   *  Ændra status                                        SRBZ /
N04   *////////////////////////////////////////////////////////////
N04  C     SRBZ          BEGSR
N04   *_________________
N04   * Oppdatere SUBFIL
N04   *"""""""""""""""""
N04  C                   MOVE      *BLANKS       ZOP
N04  C                   UPDATE    SVE000DA
      *________________________
      * call på externt program
      *""""""""""""""""""""""""
     C                   CALL      'SVE013R'
     C                   PARM                    SVEH_SYAV
     C                   PARM                    SVEH_SYOP
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Start søking                                        SRB0 /
      *////////////////////////////////////////////////////////////
     C     SRB0          BEGSR

     C     SRB01         TAG
     C                   EXFMT     SVE000DD
     C                   MOVEA     '0000'        *IN(96)
     C                   SETOFF                                       30

     C   03              DO
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C   04              DO
     C                   EXSR      SØKKUN
     C                   GOTO      SRB01
     C                   END

     C                   EXSR      TESTB1
     C   30              GOTO      SRB01

     C                   ENDSR
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KOANVN
     C     ZSSYAV        IFNE      0
     C     ZSSYOP        ORNE      0
     C     ZSSYAV        CHAIN     SVEA                               99
     C  N99KODTAK        CHAIN     KODTA                              99
     C   99              MOVE      'SVE0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      SLTB1
     C                   MOVE      ZSSYAV        UAVD
     C                   END
      *
     C                   MOVEA     '000000000'   *IN(41)
      *
     C     WFSSOK        comp      *BLANKS                            4040
     C   40              GOTO      STTB1
     C     ZSSYDT        COMP      *ZEROS                             4141
     C   41              GOTO      STTB1
     C     ZSSYOP        COMP      *ZEROS                             4242
     C   42              GOTO      STTB1
     C     ZSAVKN        COMP      *ZEROS                             4343
     C   43              GOTO      STTB1
     C     ZSMOKN        COMP      *ZEROS                             4444
     C   44              GOTO      STTB1
     C     ZSSYSG        COMP      *BLANKS                            4545
     C   45              GOTO      STTB1
     C     ZSTUID        COMP      *BLANKS                            4646
     C   46              GOTO      STTB1
     C     ZSSYST        COMP      *BLANKS                            4949
     C   49              GOTO      STTB1
      *
     C                   MOVE      'DIV0064'     MSGNR
     C                   SETON                                        3098
     C                   GOTO      SLTB1
      *
     C     STTB1         TAG
     C   43              MOVE      ZSAVKN        KUNDNR
     C   43KUNDK         CHAIN     CUNDL01                            97
     C   97              MOVE      'SPK0035'     MSGNR
     C   44              MOVE      ZSMOKN        KUNDNR
     C   44KUNDK         CHAIN     CUNDL01                            96
     C   96
     CANN97              MOVE      'SPK0035'     MSGNR
     C   97
     COR 96              SETON                                        30
      *
     C     SLTB1         ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Sett startposisjon                                SETKEY /
      *////////////////////////////////////////////////////////////
     C     SETKEY        BEGSR
      *
     C     ZSSYAV        IFEQ      0
     C     *IN46         ANDEQ     '0'
     C                   Z-ADD     1             YAVD              4 0
     C                   EXSR      MINTRL
     C                   ELSE
     C                   EXSR      MINTRE
     C                   END

     C                   ENDSR
      *
      ***********************************************
     C     MINTRE        BEGSR
      ***********************************************
      * SET KEY NÅR SØK KUN I EN !!!!  AVDELING
     C   40FR1KEY        SETLL     FRISOL01
     C   41KEY7          SETLL     SVEH7
     C   42KEYH          SETLL     SVEH
     C   43KEY41         SETLL     SVEH4
     C   44KEY51         SETLL     SVEH5
     C   45ZSDTS         IFEQ      99999999
     C     KEY61         SETGT     SVEH6
     C                   ELSE
     C     KEY61         SETLL     SVEH6
     C                   END
     C   46ZSTUID        SETLL     SVEH3
     C   49KEY8          SETLL     SVEH8
      *
     C                   ENDSR
      *
     C***********************************************
     C     MINTRL        BEGSR
     C***********************************************
      * SET KEY NÅR SØK I ALLE !!  AVDELINGER
     C   40FR1KEY        SETLL     FRISOL01
     C   41ZSSYDT        SETLL     SVEH10
     C   43KEY41A        SETLL     SVEH4
     C   44KEY51A        SETLL     SVEH5
     C   45ZSDTS         IFEQ      99999999
     C     KEY61A        SETGT     SVEH6
     C                   ELSE
     C     KEY61A        SETLL     SVEH6
     C                   END
     C   49ZSSYST        SETLL     SVEH11
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Les neste post                                    LESREC /
      *////////////////////////////////////////////////////////////
     C     LESREC        BEGSR
     C     STTLL         TAG
     C     *IN40         IFEQ      '1'
     C     LESNYFRI      TAG
     C                   SETOFF                                       33
     C     FR1KEYE       READE     FRISOL01                               94
     C  N94SADHFRI       CHAIN     SVEH                               33
     C   33              GOTO      LESNYFRI
     C                   END
     C   41ZSSYAV        READE     SVEH7                                  94
     C   42ZSSYAV        READE(N)  SVEH                                   94
     C   43KEY42         READE     SVEH4                                  94
     C   44KEY52         READE     SVEH5                                  94
     C   45ZSDTS         IFEQ      99999999
     C     KEY62         READPE    SVEH6                                  94
     C                   ELSE
     C     KEY62         READE     SVEH6                                  94
     C                   END
     C   46              READ      SVEH3                                  94
     C   49KEY8          READE     SVEH8                                  94

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les neste post (alle avdeling)                    LESRAL /
      *////////////////////////////////////////////////////////////
     C     LESRAL        BEGSR
      * LES REC NÅR SØK I ALLE !!  AVDELINGER
     C     STLRAL        TAG
     C     *IN40         IFEQ      '1'
     C     LESNYFR2      TAG
     C                   SETOFF                                       33
     C     FR1KEYE       READE     FRISOL01                               94
     C  N94SADHFRI       CHAIN     SVEH                               33
     C   33              GOTO      LESNYFR2
     C                   END
     C   41              READ      SVEH10                                 94
     C   43KEY42A        READE     SVEH4                                  94
     C   44KEY52A        READE     SVEH5                                  94
     C   45ZSDTS         IFEQ      99999999
     C     KEY62A        READPE    SVEH6                                  94
     C                   ELSE
     C     KEY62A        READE     SVEH6                                  94
     C                   END
     C   49ZSSYST        READE     SVEH11                                 94
     C   94YAVD          IFLT      9999
     C     *IN41         ANDEQ     '0'
     C     *IN49         ANDEQ     '0'
     C                   ADD       1             YAVD
     C     YAVD          SETLL     SVEA
     C                   READ      SVEA                                   33
     C  N33              DO
     C                   MOVE      SVEA_SYAV     YAVD
     C                   EXSR      MINTRL
     C                   SETOFF                                       94
     C                   GOTO      STLRAL
     C                   END
     C                   END

     C                   ENDSR
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     17
     C     POSNBR        ANDLE     24
     C                   MOVE      *BLANKS       UKNAVN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    ZSMOKN
     C                   PARM                    UFIRM
     C                   PARM                    UKNAVN
     C                   Z-ADD     37            POSNBR
     C                   END
      *
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     17
     C     POSNBR        ANDLE     24
     C                   MOVE      *BLANKS       UKNAVN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    ZSAVKN
     C                   PARM                    UFIRM
     C                   PARM                    UKNAVN
     C                   Z-ADD     37            POSNBR
     C                   END

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR

      *____________________________
      * call på vedlikeholdsprogram
      *""""""""""""""""""""""""""""
     C                   CALL      'SVE001R'
     C                   PARM      'N'           MODE              1            E=Endre N=Ny D=Vis
     C                   PARM                    ZVEH_SYAV
     C                   PARM      0             SVEH_SYOP
     C                   PARM                    ZVEH_SYSG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE000DA
      *____________________________
      * call på vedlikeholdsprogram
      *""""""""""""""""""""""""""""
     C                   CALL      'SVE001R'
     C                   PARM      'E'           MODE              1            E=Endre N=Ny D=Vis
     C                   PARM                    SVEH_SYAV
     C                   PARM                    SVEH_SYOP
     C                   PARM                    SVEH_SYSG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kopiera                                             SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE000DA
      *__________________________
      * call på kopieringsprogram
      *""""""""""""""""""""""""""
     C                   CALL      'SVE005R'
     C                   PARM                    SVEH_SYAV
     C                   PARM                    SVEH_SYOP
     C                   PARM                    ZVEH_SYSG
      *____________________________
      * call på vedlikeholdsprogram
      *""""""""""""""""""""""""""""
     C                   CALL      'SVE001R'
     C                   PARM      'E'           MODE
     C                   PARM                    SVEH_SYAV
     C                   PARM                    SVEH_SYOP
     C                   PARM                    ZVEH_SYSG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
N05  C     SVEHKEY       CHAIN     SVEH                               33
N05  C                   EVAL      SVEH_SYST='D'
N05  C                   UPDATE    SVEHR
N05   *_________________
N05   * Oppdatere SUBFIL
N05   *"""""""""""""""""
N05  C                   MOVE      *BLANKS       ZOP
N05  C                   UPDATE    SVE000DA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise                                                SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE000DA
      *____________________________
      * call på vedlikeholdsprogram
      *""""""""""""""""""""""""""""
     C                   CALL      'SVE001R'
     C                   PARM      'D'           MODE              1            E=Endre N=Ny D=Vis
     C                   PARM                    SVEH_SYAV
     C                   PARM                    SVEH_SYOP
     C                   PARM                    SVEH_SYSG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Historik                                            SRBH /
      *////////////////////////////////////////////////////////////
     C     SRBH          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE000DA
      *________________________
      * call på externt program
      *""""""""""""""""""""""""
     C*                  CALL      'SVE027R'
     C                   CALL      'SVI014R'
     C                   PARM                    SVEH_SYAV
     C                   PARM                    SVEH_SYOP
     C                   PARM      'SVE'         UMSN07            3
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv ut                                            SRBP /
      *////////////////////////////////////////////////////////////
     C     SRBP          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE000DA
      *________________________
      * call på externt program
      *""""""""""""""""""""""""
     C                   MOVE      'M'           CALLTYP
     C                   MOVE      SVEH_SYAV     SVEHASYAV         4
     C                   MOVE      SVEH_SYOP     SVEHASYOP         7
     C                   CALL      'SVE006R'
     C                   PARM                    SVEHASYAV
     C                   PARM                    SVEHASYOP
     C                   PARM                    CALLTYP           1
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Extern referanse                                    SRBX /
      *////////////////////////////////////////////////////////////
     C     SRBX          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE000DA
      *________________________
      * call på externt program
      *""""""""""""""""""""""""
     C                   MOVE      SVEH_SYAV     SY_HEAVD          4
     C                   MOVE      SVEH_SYOP     SY_HEOPD          7
     C                   CALL      'SYGE48R'
     C                   PARM                    SY_HEAVD
     C                   PARM                    SY_HEOPD
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Arkiv                                               SRAR /
      *////////////////////////////////////////////////////////////
     C     SRAR          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE000DA
      *________________________
      * call på externt program
      *""""""""""""""""""""""""
     C                   MOVE      *zeros        opd               7 0
N06  C                   MOVE      SVEH_SYOP     opd
     C                   MOVEL     'SE:'         AAUNDE
     C                   MOVE      SVEH_SYOP     AAUNDE           10
N06  C                   MOVE      *BLANKS       AAUNDE           10
     C                   MOVEL     SVEH_SYAV     avd               4 0
     C                   CALL      'ARC003'
     C                   PARM                    avd
     C                   PARM                    opd
     C                   PARM                    AaUNDE
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skicka till sigillering                             SRBS /
      *////////////////////////////////////////////////////////////
     C     SRBS          BEGSR
N07   *_________________________________________
N07   * Ær statusen på alla varuposter = blank ?
N07   *"""""""""""""""""""""""""""""""""""""""""
N07  C     SVEHKEY       SETLL     SVEV2
N07  C     SVEHKEY       READE     SVEV2                                  35
N07  C                   IF        *IN35=*OFF
N07  C                   IF        SVEV_ERR <> *BLANKS
N07  C                   EXFMT     SVE000DG
N07  C                   GOTO      SRBS9
N07  C                   ENDIF
N07  C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVE000DA
      *________________________
      * call på externt program
      *""""""""""""""""""""""""
     C                   CALL      'SVE021R'
     C                   PARM                    SVEH_SYAV
     C                   PARM                    SVEH_SYOP
N07  C     SRBS9         TAG
     C                   ENDSR
