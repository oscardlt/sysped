      *
      ************************************************************
      *** UTSKRIFT AV ORDRE (INTERN/EKSTERN(KJØREORDR)/BEGGE)  ***
      ************************************************************
      *
     FFIRM      IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FTURER     IF   E           K DISK
     FSYTR40FA  CF   E             WORKSTN
     D TALL            C                   CONST('0123456789')
     D                 DS
     D  UDATO                  1      6  0
     D  UÅR                    1      2  0
     D  UMN                    3      4  0
     D  UDG                    5      6  0
     C*
     C     KODSFK        KLIST
     C                   KFLD                    WSSFUN
     C                   KFLD                    WSSFSI
     C     KODTVK        KLIST
     C                   KFLD                    WSVUNI
     C                   KFLD                    WSVAVD
     C     TURKEY        KLIST
     C                   KFLD                    WSVAVD
     C                   KFLD                    WSTUR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UIN01             1
     C                   PARM                    AAVAVD            4
     C                   PARM                    WSSFSI
     C                   PARM                    AATUR             8
     C                   PARM                    WSPRT
     C                   PARM                    WSFORM
     C                   PARM                    WSHEAD
     C                   PARM                    AADATO            6
     C                   PARM                    WSTY
     C                   PARM                    WSJOBQ
     C     TALL          CHECK     AAVAVD                                 99
     C  N99              MOVE      AAVAVD        WSVAVD
     C   99              MOVE      *ZEROS        WSVAVD
     C*
     C                   READ      FIRM                                   33
     C                   MOVE      '0'           UIN01
     C                   MOVE      'SF'          WSSFUN            2
     C                   MOVE      'V'           WSVUNI            1
     C                   MOVE      'Y'           WSJOBQ
     C                   MOVE      UYEAR         UÅR
     C                   MOVE      UMONTH        UMN
     C                   MOVE      UDAY          UDG
     C                   MOVE      UDATO         WSDATO
     C                   CALL      'DATFM3'
     C                   PARM                    FIDTFM
     C                   PARM                    WSDATO
     C*
     C                   SETON                                        50
     C*
     C*
     C     START         TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       309899
     C                   SETOFF                                       9796
     C*
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C*
     C     KODTVK        CHAIN     KODTV                              99
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START
     C*
     C     WSSFSI        IFNE      'ALL'
     C     KODSFK        CHAIN     KODTSF                             98
     C   98              MOVE      'SPS0001'     MSGNR
     C   98              SETON                                        30
     C   98              GOTO      START
     C                   END
     C*
     C     WSTUR         IFNE      *ZEROS
     C     TURKEY        CHAIN     TURER                              97
     C   97              MOVE      'SPT0002'     MSGNR
     C   97              SETON                                        30
     C   97              GOTO      START
     C                   END
     C*
     C     WSTY          IFNE      'I'
     C     WSTY          IFNE      'E'
     C     WSTY          IFNE      'B'
     C                   MOVE      'DIV4097'     MSGNR
     C                   SETON                                        3096
     C                   GOTO      START
     C                   END
     C                   END
     C                   END
     C*
     C     WSSFSI        IFEQ      'ALL'
     C                   MOVE      *BLANKS       WSSFSI
     C                   END
     C*
     C* HENTER STANDARD PRINTERSTYRING
     C*
     C                   Z-ADD     51            PRTNR             3 0
     C                   CALL      'SYGE01'
     C                   PARM                    WSVAVD
     C                   PARM                    PRTNR
     C                   PARM                    WSPRT
     C                   PARM                    WSFORM
     C                   PARM                    WSHEAD            1
     C                   PARM                    WKOPFM            5
     C*
     C                   SETOFF                                       50
     C                   EXFMT     BILDE01
     C   03              MOVE      '1'           UIN01
     C*
     C     SLUTT         TAG
     C                   MOVE      WSTUR         AATUR
     C                   MOVE      WSVAVD        AAVAVD
     C                   MOVE      WSDATO        AADATO
     C                   SETON                                        LR
     C                   RETURN
     C*
