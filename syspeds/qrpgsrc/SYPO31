      *****************************************************************
      * PRISJISTERE POSTALE FRAKTAVTALER OVER I WORKFILE              *
      *****************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Unntatt fra/til kunde inntat i standard(Nortrail spesial)
10XXX * 02 SH  PTF10 Minimumsverdi fra - Tildato
********************************************************************
      *
     FKUPR      UF A E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FKUIPRL01  IF   E           K DISK
     FSYPO31FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     FAVFRH     IF   E           K DISK
     FAVFRD     IF   E           K DISK
     FAVFRHN    UF A E           K DISK
     F                                     RENAME(AVFRHR:AVFRHRN)
     FAVFRDN    UF A E           K DISK
     F                                     RENAME(AVFRDR:AVFRDRN)
     FFIRM      IF   E           K DISK
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSLF01                 1      2
     D  WSLF02                 3      4
     D  WSLF03                 5      6
     D  WSLF04                 7      8
     D  WSLF05                 9     10
     D  WSLF06                11     12
     D  WSLF07                13     14
     D  WSLF08                15     16
     D  WSLF09                17     18
     D  WSLF10                19     20
     D  WSLF11                21     22
     D  WSLF12                23     24
     D  LF                     1     24
     D                                     DIM(12)
     D  LFA                    1     24
     D                 DS
     D  WSLT01                 1      2
     D  WSLT02                 3      4
     D  WSLT03                 5      6
     D  WSLT04                 7      8
     D  WSLT05                 9     10
     D  WSLT06                11     12
     D  WSLT07                13     14
     D  WSLT08                15     16
     D  WSLT09                17     18
     D  WSLT10                19     20
     D  WSLT11                21     22
     D  WSLT12                23     24
     D  LT                     1     24
     D                                     DIM(12)
     D  LTA                    1     24
     D                 DS
     D  FRADAG                 1      2  0
     D  FRAMND                 3      4  0
     D  FRAAAR                 5      6  0
N02  D  FRADTN                 1      6  0
S02  D* FRADT                  1      6  0
n02  D                 DS
n02  D  FRAD                   1      2  0
n02  D  FRAM                   3      4  0
n02  D  FRAA                   5      6  0
n02  D  MFRADT                 1      6  0
N02  D                 DS
N02  D  FRAAT                  1      4  0
N02  D  FRAMT                  5      6  0
N02  D  FRADT                  7      8  0
N02  D  FRADTT                 1      8  0
     D                 DS
     D  AFAKAR                 1      4  0
     D  AFAKMN                 5      6  0
     D  AFAKDG                 7      8  0
     D  AFAKDT                 1      8  0
     D                 DS
     D  TILDAG                 1      2  0
     D  TILMND                 3      4  0
     D  TILAAR                 5      6  0
S02  D* TILDT                  1      6  0
N02  D  TILDTN                 1      6  0
N02  D                 DS
n02  D  TILD                   1      2  0
n02  D  TILM                   3      4  0
n02  D  TILA                   5      6  0
n02  D  MTILDT                 1      6  0
N02  D                 DS
N02  D  TILAT                  1      4  0
N02  D  TILMT                  5      6  0
N02  D  TILDT                  7      8  0
N02  D  TILDTT                 1      8  0
     D                 DS
     D  AFGJAR                 1      4  0
     D  AFGJMN                 5      6  0
     D  AFGJDG                 7      8  0
     D  AFGJDT                 1      8  0
     D                 DS
     D  ZNUMB                  1     11  2
     D  ZNUMBB                 9     11  2
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C  N30
     CANN04              Z-ADD     5             LINNBR
     C  N30
     CANN04              Z-ADD     25            POSNBR
     C                   EXSR      KDTFMT
     C                   EXSR      SRALL
     C                   EXFMT     BILDE01
     C   03              GOTO      SLUTT
      *
     C                   EXSR      KDTFMT
     C                   SETOFF                                       30
     C                   MOVEA     '00000000'    *IN(92)
     C                   MOVEA     '00000000'    *IN(84)
     C                   MOVEA     '00000000'    *IN(76)
     C                   MOVEA     '00000000'    *IN(68)
S01  C*                  MOVEA     '000'         *IN(65)
S02  C*                  MOVEA     '0000'        *IN(64)
N02  C                   MOVEA     '000000'      *IN(62)
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
      *
     C                   EXSR      TESTB1
     C  N30              EXSR      OPPDAT
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C******************************************
     C     TESTB1        BEGSR
     C******************************************
     C     PLUMIN        IFNE      '+'
     C     PLUMIN        ANDNE     '-'
     C     PLUMIN        ANDNE     'M'
     C                   SETON                                        6730
     C                   MOVE      'SHU0476'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLTB
     C                   END
     C     WSPROS        IFEQ      *ZEROS
     C     WSNULL        ANDNE     'J'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SHU0278'     MSGNR
     C                   GOTO      SLTB
     C                   END
      *
     C     WAFRAV        IFGT      3
     C                   SETON                                        6630
     C                   MOVE      'SHU0478'     MSGNR
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLTB
     C                   END
     C     WSKUND        IFNE      0
     C     CUND1K        CHAIN     CUNDL01                            98
     C     *IN98         IFEQ      '1'
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     25            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPK0001'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C                   END
N01   *
N01   * unntatt fra - til kund (hvis fra oppgitt må til også gis /lik el større)
N01  C     WSIKFR        IFNE      0
N01  C     WSIKTI        andlt     WSIKFR
N01  c                   z-add     WSIKFR        WSIKTI
N01  C                   move      *on           *in64
N01  C                   Z-ADD     8             LINNBR
N01  C                   Z-ADD     36            POSNBR
N01  C                   MOVE      '1'           *IN30
N01  C                   MOVE      'JOV1552'     MSGNR
N01  C                   GOTO      SLTB
N01  C                   END
      *
     C     1             DO        12            XN                             1<-B
     C     LF(XN)        IFNE      *BLANKS                                       2<-B
     C                   MOVE      LF(XN)        XLK               2
     C     KODLKK        CHAIN     KODTLK                             30
     C     *IN30         IFEQ      '1'                                            3<-B
     C     98            SUB       XN            XN
     C                   MOVE      '1'           *IN(XN)
     C                   Z-ADD     9             LINNBR
     C     98            SUB       XN            XN
     C                   MULT      3             XN
     C     XN            ADD       22            POSNBR
     C                   MOVE      'SPL0001'     MSGNR
     C                   GOTO      SLTB
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     1             DO        12            XN                             1<-B
     C     LT(XN)        IFNE      *BLANKS                                       2<-B
     C                   MOVE      LT(XN)        XLK               2
     C     KODLKK        CHAIN     KODTLK                             30
     C     *IN30         IFEQ      '1'                                            3<-B
     C     86            SUB       XN            XN
     C                   MOVE      '1'           *IN(XN)
     C                   Z-ADD     11            LINNBR
     C     98            SUB       XN            XN
     C                   MULT      3             XN
     C     XN            ADD       22            POSNBR
     C                   MOVE      'SPL0001'     MSGNR
     C                   GOTO      SLTB
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     WSFORF        IFNE      'J'
     C     WSHOVE        ANDNE     'J'
     C     WSVIDE        ANDNE     'J'
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN65
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   MOVE      'SHU0279'     MSGNR
     C                   GOTO      SLTB
     C                   END
      *
     C     WSAVDE        IFNE      *ZEROS
     C     KODTAK        CHAIN     KODTA                              73
     C     *IN73         IFEQ      '1'
     C                   Z-ADD     16            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPA0001'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C                   END
      *
     C     WSPROD        IFNE      '*'
     C                   MOVEL     WSPROD        KFKOD
     C     KUFASK        CHAIN     KUFAST                             72
     C     *IN72         IFEQ      '1'
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0360'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C                   END
      *
     C     WSFRAN        IFNE      '*AL'
     C     WSFRAN        ANDNE     '   '
     C     KODFRK        CHAIN     KOFRR                              71
     C     *IN71         IFEQ      '1'
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPF0006'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C                   END
      *
     C     WSGEBY        IFNE      '*AL'
     C     WSGEBY        ANDNE     '   '
     C     KODGEK        CHAIN     KOGER                              70
     C     *IN70         IFEQ      '1'
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     62            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPG0002'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C                   END
      *
N02  C     MFRADT        IFNE      0
N02  C                   MOVE      FRAA          UÅÅ               2 0
N02  C                   MOVE      FRAM          UMM               2 0
N02  C                   MOVE      FRAD          UDD               2 0
N02  C                   CALL      'TESTDATO'
N02  C                   PARM                    UDD
N02  C                   PARM                    UMM
N02  C                   PARM                    UÅÅ
N02  C                   PARM                    UFLAGG            1 0
N02  C     UFLAGG        IFEQ      1
N02  C                   Z-ADD     13            LINNBR
N02  C                   Z-ADD     46            POSNBR
N02  C                   MOVE      '1'           *IN69
N02  C                   MOVE      '1'           *IN30
N02  C                   MOVE      'SPD0001'     MSGNR
N02  C                   END
N02  C                   END
N02   *
N02  C     MTILDT        IFNE      0
N02  C                   MOVE      TILA          UÅÅ               2 0
N02  C                   MOVE      TILA          UMM               2 0
N02  C                   MOVE      TILD          UDD               2 0
N02  C                   CALL      'TESTDATO'
N02  C                   PARM                    UDD
N02  C                   PARM                    UMM
N02  C                   PARM                    UÅÅ
N02  C                   PARM                    UFLAGG            1 0
N02  C     UFLAGG        IFEQ      1
N02  C                   Z-ADD     13            LINNBR
N02  C                   Z-ADD     65            POSNBR
N02  C                   MOVE      '1'           *IN68
N02  C                   MOVE      '1'           *IN30
N02  C                   MOVE      'SPD0001'     MSGNR
N02  C                   END
N02  C                   END
N02   *
      *
      *
S02  C*    FRADT         IFNE      0
N02  C     FRADTN        IFNE      0
     C                   MOVE      FRAAAR        UÅÅ               2 0
     C                   MOVE      FRAMND        UMM               2 0
     C                   MOVE      FRADAG        UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   MOVE      '1'           *IN69
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPD0001'     MSGNR
     C                   END
     C                   END
      *
S02  C*    TILDT         IFNE      0
N02  C     TILDTN        IFNE      0
     C                   MOVE      TILAAR        UÅÅ               2 0
     C                   MOVE      TILMND        UMM               2 0
     C                   MOVE      TILDAG        UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     36            POSNBR
     C                   MOVE      '1'           *IN68
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPD0001'     MSGNR
     C                   END
     C                   END
      *
     C     PLUMIN        IFEQ      '-'
     C                   Z-SUB     WSPROS        WSPROS
     C                   END
      *
     C     WSPROS        DIV       100           OEK               7 4
     C     1             ADD       OEK           OEK
      *
      * fra-til vektgruppe - til ikke oppgit -> 999999999
     c     WSFRVG        ifne      *zeros
     c     WSTIVG        andeq     *zeros
     c                   z-add     999999999     WSTIVG
     C                   end
     c     WSMM          ifne      'N'
     C                   MOVE      'J'           WSMM
     C                   end
      *
     C* BEHANDLING AV DETALJREGISTER
     C     SLTB          ENDSR
      *
     C***********************************************
     C     OPPDAT        BEGSR
     C***********************************************
N02  C                   MOVE      *ZEROS        FRADTT
N02  C                   MOVE      *ZEROS        TILDTT
N02  C     MFRADT        IFNE      0
N02  C                   Z-ADD     2000          FRAAT
N02  C                   MOVE      FRAA          FRAAT
N02  C                   MOVE      FRAM          FRAMT
N02  C                   MOVE      FRAD          FRADT
N02  C                   END
N02  C     MTILDT        IFNE      0
N02  C                   Z-ADD     2000          TILAT
N02  C                   MOVE      TILA          TILAT
N02  C                   MOVE      TILM          TILMT
N02  C                   MOVE      TILD          TILDT
N02  C                   END
     C                   MOVE      WAFRAV        ZKODE             1 0
     C     ZKODE         COMP      1                                  29  29
     C                   MOVE      *ZEROS        ANTJUS
     C                   SETOFF                                       30
     C                   SORTA     LF
     C                   SORTA     LT
     C     WSKUND        SETLL     AVFRH
     C* EN KUNDE ER VALGT
     C*
     C     STKUND        TAG
     C     WSKUND        IFNE      *ZEROS
     C     WSKUND        READE     AVFRH                                  30
     C                   ELSE
     C                   READ      AVFRH                                  30
     C                   END
     C*
     C   30ANTJUS        IFEQ      *ZEROS
     C                   MOVE      'SHU0288'     MSGNR
     C                   GOTO      SLKUND
     C                   ELSE
     C                   SETOFF                                       30
     C                   GOTO      SLKUND
     C                   END
     C*
     C* TESTER OM KUNDE ER HOLDT UTENOM
     C*
N01   *fra - til kundenr (f.eks om agenter er samlet i egen serie..)
N01  C     WSKUND        IFEQ      *ZEROS
N01  C     WSIKFR        ifne      *zeros
N01  C     AFKUND        andge     WSIKFR
N01  C     AFKUND        andle     WSIKTI
N01  C                   GOTO      STKUND
N01  C                   endif
      *Unntaksfila
     C     KUNKEY        CHAIN     KUIPRL01                           33
     C     WSIKKE        IFNE      'J'
     C  N33              GOTO      STKUND
     C                   ELSE
     C   33              GOTO      STKUND
     C                   END
     C                   END
     C*
     C* TESTER LANDKODE FRA
     C*
     C     LFA           IFNE      *BLANKS
     C                   SETOFF                                       44
     C     AFLKFR        LOOKUP    LF                                     44
     C  N44              GOTO      STKUND
     C                   END
     C*
n02  C*
n02  C* TESTER FRADATO
n02  C*
n02  C     FRADTT        ifne      *zeros
n02c C     AFAKDT        andgt     FRADTT
N02  C                   GOTO      STKUND
N02  C                   END
n02  C*
n02  C* TESTER TILDATO
n02  C*
n02  C     TILDTT        ifne      *zeros
n02c C     AFGJDT        andgt     TILDTT
N02  C                   GOTO      STKUND
N02  C                   END
     C*
     C*
     C* TESTER tabell starter med
     C*
     C     WSTABF        IFNE      *BLANKS
     C                   movel     afpnf1        strtab            2
     C     WSTABF        IFNE      STRTAB
     C                   GOTO      STKUND
     C                   END
     C                   END
     C*
     C* TESTER LANDKODE FRA
     C*
     C     LFA           IFNE      *BLANKS
     C                   SETOFF                                       44
     C     AFLKFR        LOOKUP    LF                                     44
     C  N44              GOTO      STKUND
     C                   END
     C*
     C* TESTER LANDKODE TIL
     C*
     C     LTA           IFNE      *BLANKS
     C                   SETOFF                                       44
     C     AFLKTI        LOOKUP    LT                                     44
     C  N44              GOTO      STKUND
     C                   END
     C*
     C*
     C* TESTER tabell starter med
     C*
     C     WSTABT        IFNE      *BLANKS
     C                   movel     afpnt1        strtab            2
     C     WSTABT        IFNE      STRTAB
     C                   GOTO      STKUND
     C                   END
     C                   END
     C*
     C* TESTER AVDELING
     C*
     C     WSAVD2        IFNE      '*'
     C     AFAVDE        ANDNE     WSAVDE
     C                   GOTO      STKUND
     C                   END
     C*
     C* TESTER PRODUKTKODE
     C*
     C     WSPROD        IFNE      '*'
     C     AFPROD        ANDNE     WSPROD
     C                   GOTO      STKUND
     C                   END
     C*
     C* TESTER FRANKATUR
     C*
     C     WSFRAN        IFNE      '*AL'
     C     AFFRAN        ANDNE     WSFRAN
     C                   GOTO      STKUND
     C                   END
     C*
     C* TESTER GEBYR
     C*
     C     WSGEBY        IFNE      '*AL'
     C     AFGEBY        ANDNE     WSGEBY
     C                   GOTO      STKUND
     C                   END
     C*
     C* AVTALEN MÅ HA T-LINJER PÅ SEG
     C*
     C     AFBFR1        IFNE      'T'
     C     AFBFR2        ANDNE     'T'
     C     AFBFRA        ANDNE     'T'
     C                   GOTO      STKUND
     C                   END
     C*
     C* TAR VARE PÅ MINIMUM OG MAXIMUM
     C*
     C     *LIKE         DEFINE    AFMINI        AFMIN2
     C     *LIKE         DEFINE    AFMAXI        AFMAX2
     C     *LIKE         DEFINE    AFGRPR        AFGRP2
      *
      * skal min/max/grunnpris ikke endres??
     c     WSMM          ifeq      'N'
     C                   MOVE      AFMINI        AFMIN2
     C                   MOVE      AFMAXI        AFMAX2
     C                   MOVE      AFGRPR        AFGRP2
     c                   goto      SkipMM
     c                   end
     C*
     C  N29PLUMIN        IFNE      'M'
     C     AFMINI        MULT(H)   OEK           AFMIN2
     C     AFMAXI        MULT(H)   OEK           AFMAX2
     C     AFGRPR        MULT(H)   OEK           AFGRP2
     C                   ELSE
     C     AFMINI        DIV(H)    OEK           AFMIN2
     C     AFMAXI        DIV(H)    OEK           AFMAX2
     C     AFGRPR        DIV(H)    OEK           AFGRP2
     C                   END
     C   29              DO
     C     PLUMIN        IFNE      'M'
     C     AFMINI        MULT      OEK           ZNUMB
     C                   ELSE
     C     AFMINI        DIV       OEK           ZNUMB
     C                   END
     C                   EXSR      ROUND
     C                   Z-ADD(H)  ZNUMB         AFMIN2
     C     PLUMIN        IFNE      'M'
     C     AFMAXI        MULT      OEK           ZNUMB
     C                   ELSE
     C     AFMAXI        DIV       OEK           ZNUMB
     C                   END
     C                   EXSR      ROUND
     C                   Z-ADD(H)  ZNUMB         AFMAX2
     C     PLUMIN        IFNE      'M'
     C     AFGRPR        MULT      OEK           ZNUMB
     C                   ELSE
     C     AFGRPR        DIV       OEK           ZNUMB
     C                   END
     C                   EXSR      ROUND
     C                   Z-ADD(H)  ZNUMB         AFGRP2
     C                   END
      *
     C     SkipMM        tag
     C*
     C     WSFORF        IFEQ      'J'
     C     AFBFR1        IFEQ      'T'
     C                   MOVE      'F'           DELKOD
     C                   EXSR      CHGTAB
     C                   ADD       1             ANTJUS
     C                   END
     C                   END
     C*
     C     WSHOVE        IFEQ      'J'
     C     AFBFRA        IFEQ      'T'
     C                   MOVE      'H'           DELKOD
     C                   EXSR      CHGTAB
     C                   ADD       1             ANTJUS
     C                   END
     C                   END
     C*
     C     WSVIDE        IFEQ      'J'
     C     AFBFR2        IFEQ      'T'
     C                   MOVE      'V'           DELKOD
     C                   EXSR      CHGTAB
     C                   ADD       1             ANTJUS
     C                   END
     C                   END
     C*
     C                   GOTO      STKUND
     C*
     C     SLKUND        TAG
     C                   ADD       ANTJUS        TOTJUS
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
N01  C                   MOVE      *zeros        UUKUND            8 0
     C                   MOVE      *BLANKS       SOTXT1           30
     C                   CALL      'SOEKKUN'
S01  C*                  PARM                    WSKUND
N01  C                   PARM                    UUKUND
     C                   PARM                    FIFIRM
     C                   PARM                    SOTXT1           30
N01  C     LINNBR        ifeq      7
N01  C                   move      UUKUND        WSKUND
N01  c                   else
N01  C     POSNBR        ifle      33
N01  C                   move      UUKUND        WSIKFR
N01  c                   else
N01  C                   move      UUKUND        WSIKTI
N01  c                   endif
N01  c                   endif
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     25
     C     POSNBR        ANDLE     32
     C                   EXSR      SØKKUN
     C*                  Z-ADD     9             LINNBR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLSØK
     C                   END
N01   * unntatt fra - til
N01  C     LINNBR        IFEQ      8
N01  C     POSNBR        ANDGE     25
N01  C     POSNBR        ANDLE     50
N01  C                   EXSR      SØKKUN
N01  C                   Z-ADD     9             LINNBR
N01  C                   Z-ADD     25            POSNBR
N01  C                   GOTO      SLSØK
N01  C                   END
      *
     C     LINNBR        IFEQ      9                                            1<-B
     C     POSNBR        SUB       22            XN                3 0
     C                   DIV       3             XN
     C     XN            IFGE      1                                             2<-B
     C     XN            ANDLE     12
     C                   MOVE      LF(XN)        ULK               2
     C                   CALL      'SYFI10L'
     C                   PARM                    ULK
     C                   PARM                    USPED
     C     ULK           IFNE      *BLANKS                                        3<-B
     C                   MOVE      ULK           LF(XN)
     C                   END                                                      3<-E
     C     XN            IFLT      12                                             3<-B
     C                   MULT      3             XN
     C     XN            ADD       25            POSNBR
     C                   ELSE                                                     3
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   END                                                      3<-E
     C                   GOTO      SLSØK
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     LINNBR        IFEQ      11                                           1<-B
     C     POSNBR        SUB       22            XN                3 0
     C                   DIV       3             XN
     C     XN            IFGE      1                                             2<-B
     C     XN            ANDLE     12
     C                   MOVE      LT(XN)        ULK               2
     C                   CALL      'SYFI10L'
     C                   PARM                    ULK
     C                   PARM                    USPED
     C     ULK           IFNE      *BLANKS                                        3<-B
     C                   MOVE      ULK           LT(XN)
     C                   END                                                      3<-E
     C     XN            IFLT      12                                             3<-B
     C                   MULT      3             XN
     C     XN            ADD       25            POSNBR
     C                   ELSE                                                     3
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   END                                                      3<-E
     C                   GOTO      SLSØK
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     LINNBR        IFEQ      16
     C     POSNBR        ANDGE     25
     C     POSNBR        ANDLE     28
     C                   CALL      'SYFA14B'
     C                   PARM                    WSAVDE
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      17
     C     POSNBR        ANDEQ     25
     C                   MOVE      *BLANKS       KFKOD
     C                   MOVE      'N'           UUPDAT            1
     C                   CALL      'SYKUKD'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    KFKOD
     C                   MOVEL     KFKOD         WSPROD
     C                   Z-ADD     18            LINNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      18
     C     POSNBR        ANDGE     25
     C     POSNBR        ANDLE     27
     C                   CALL      'SYFI10G'
     C                   PARM                    WSFRAN
     C                   PARM                    USPED
     C                   Z-ADD     52            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      18
     C     POSNBR        ANDGE     62
     C     POSNBR        ANDLE     64
     C                   CALL      'SYFI10D'
     C                   PARM                    WSGEBY
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WSAVDE
     C                   MOVE      'A'           KOAUNI
     C     KODGEK        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    WSGEBY
     C                   MOVE      'GE'          KGEUNI
     C     KODFRK        KLIST
     C                   KFLD                    KFRUNI
     C                   KFLD                    WSFRAN
     C                   MOVE      'FR'          KFRUNI
     C     KODLKK        KLIST
     C                   KFLD                    KLKUNI
     C                   KFLD                    XLK               2
     C                   MOVE      'LK '         KLKUNI
     C     KUFASK        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   MOVEL     'PRODTYPE'    KFTYP
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUND
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    AFKUND
      *
N05  C                   MOVE      'J'           WSMM
     C                   MOVE      '+'           PLUMIN
     C                   MOVE      'J'           USPED             1
S02  C*                  MOVE      *ZEROS        FRADT
N02  C                   MOVE      *ZEROS        FRADTN
N02  C                   MOVE      *ZEROS        MFRADT
N02  C                   MOVE      *ZEROS        FRADTT
S02  C*                  MOVE      *ZEROS        TILDT
N02  C                   MOVE      *ZEROS        TILDTN
N02  C                   MOVE      *ZEROS        MTILDT
N02  C                   MOVE      *ZEROS        TILDTT
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      FRADAG        UDT1              2 0
     C                   MOVE      FRAMND        UDT2              2 0
     C                   MOVE      FRAAAR        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          FRADAG
     C                   MOVE      UDT2          FRAMND
     C                   MOVE      UDT3          FRAAAR
      *
     C                   MOVE      TILDAG        UDT1
     C                   MOVE      TILMND        UDT2
     C                   MOVE      TILAAR        UDT3
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          TILDAG
     C                   MOVE      UDT2          TILMND
     C                   MOVE      UDT3          TILAAR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGTAB        BEGSR
     C***********************************************
     C*
     C     CHGKEY        KLIST
     C                   KFLD                    AFAVNR
     C                   KFLD                    DELKOD            1
     C     CHGKE2        KLIST
     C                   KFLD                    AFKUND
     C                   KFLD                    AFPROD
     C                   KFLD                    AFAVDE
     C                   KFLD                    AFLKFR
     C                   KFLD                    AFPNF1
     C                   KFLD                    AFFRAN
     C                   KFLD                    AFGEBY
     C                   KFLD                    AFLKTI
     C                   KFLD                    AFPNT1
     C*
     C* OPPDATERER ELLER SKRIVER TIL HODEREGISTER
     C*
     C     CHGKE2        CHAIN     AVFRHN                             33
     C  N33              DELETE    AVFRHRN
     C*
     C* ORDNER MIN/MAX OG DATO
     C*
     C                   MOVE      AFMIN2        AFMINI
     C                   MOVE      AFMAX2        AFMAXI
     C                   MOVE      AFGRP2        AFGRPR
     C*
     C                   MOVE      FRADAG        AFAKDG
     C                   MOVE      FRAMND        AFAKMN
     c                   movel     20            AFAKAR
     C                   MOVE      FRAAAR        AFAKAR
     C*
     C                   MOVE      TILDAG        AFGJDG
     C                   MOVE      TILMND        AFGJMN
     c                   movel     20            AFGJAR
     C                   MOVE      TILAAR        AFGJAR
     C*
     C                   WRITE     AVFRHRN
     C*
     C* SKRIVER TIL FIL FOR PRISJUSTERTE KUNDER
     C*
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33KUNKEY        CHAIN     KUPR                               34
     C  N33
     CAN 34              WRITE     KUPRR
     C*
     C* BEHANDLING AV DETALJREGISTER
     C*
     C* FJERNER GAMLE FORHØYELSER
     C*
     C     CHGKEY        SETLL     AVFRDN
     C     STCHG0        TAG
     C     CHGKEY        READE     AVFRDN                                 33
     C  N33              DELETE    AVFRDRN
     C  N33              GOTO      STCHG0
     C*
     C* LEGGER INN NYE FORHØYELSER
     C*
     C     CHGKEY        SETLL     AVFRD
     C     STCHG         TAG
     C     CHGKEY        READE     AVFRD                                  33
     C  N33AFCOBE        IFNE      'P'
      * vektgrp-begrensning(WSTIVG da alltid oppgitt)  og denne post er UTENFOR.. behold gammel,
     c  N33WSTIVG        ifne      *zeros
     c     AFVEKT        iflt      WSFRVG
     c     AFVEKT        orgt      WSTIVG
     C                   WRITE     AVFRDRN
     C                   GOTO      STCHG
     C                   end
     C                   end
     c*
     C  N33
     CANN29PLUMIN        IFNE      'M'
     C     AFPRIA        MULT(H)   OEK           AFPRIA
     C                   ELSE
     C     AFPRIA        DIV(H)    OEK           AFPRIA
     C                   END
     C  N33
     CAN 29              DO
     C     PLUMIN        IFNE      'M'
     C     AFPRIA        MULT      OEK           ZNUMB
     C                   ELSE
     C     AFPRIA        DIV(H)    OEK           ZNUMB
     C                   END
     C                   EXSR      ROUND
     C                   Z-ADD(H)  ZNUMB         AFPRIA
     C                   END
     C                   END
     C  N33              WRITE     AVFRDRN
     C  N33              GOTO      STCHG
     C*
     C* KALLER RUTINE FOR NY BEREGNING AV MIN-MAX-VERDIER
     C*
     C                   CALL      'SYPO12CN'
     C                   PARM                    AFAVNR
     C                   PARM                    DELKOD
     C                   ENDSR
      *
     C***********************************************
     C     ROUND         BEGSR
     C***********************************************
     C* Param. = ZKODE (1,2,3,4)  = Roundingcode
     C*          ZNUMB workfield 11 long 2 digits
     C*         (ZNUMBB) is the 3 last pos. of ZNUMB (via Data Struct.)
     C*
     C* Code 2 = To nearest  5
     C     ZKODE         IFEQ      2                                            B1
     C     ZNUMBB        IFLT      2.50                                           B2
     C                   MOVE      '000'         ZNUMB
     C                   ELSE
     C     ZNUMBB        IFGE      2.50                                             B3
     C     ZNUMBB        ANDLE     7.49
     C                   MOVE      '500'         ZNUMB
     C                   ELSE
     C                   ADD       10            ZNUMB
     C                   MOVE      '000'         ZNUMB
     C                   END                                                        E3
     C                   END                                                      E2
     C* Code 3 = To nearest 10
     C                   ELSE
     C     ZKODE         IFEQ      3
     C     ZNUMBB        IFGE      5.00                                           B2
     C                   ADD       10            ZNUMB
     C                   END                                                      E2
     C                   MOVE      '000'         ZNUMB
     C                   END                                                    E1
     C                   END                                                    E1
     C*
     C*
     C* Code 1 = To nearest HELE KRONE
     C*
     C     ZKODE         IFEQ      1
     C                   ADD       .50           ZNUMB
     C                   MOVE      '00'          ZNUMB
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     SRALL         BEGSR
     C***********************************************
     C                   MOVE      '*'           WSAVD2
     C                   MOVE      '*'           WSPROD
     C                   MOVE      '*AL'         WSFRAN
     C                   MOVE      '*AL'         WSGEBY
     C                   ENDSR
