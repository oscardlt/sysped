      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/inqbiln MODULE(*LIBL/inqbiln *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 KAN IKKE BEGRENSE SØK TIL EN AVD. (I ALLE FALL IKKE SOM STANDARD)
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FUNIT      IF   E           K DISK    USROPN
     FUNITL01   IF   E           K DISK    USROPN
     F                                     RENAME(UNITR:UNITR01)
     FUNITL02   IF   E           K DISK    USROPN
     F                                     RENAME(UNITR:UNITR02)
     FTRAN      IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
OddEvD OddEven         s             10
      * Client input variables
     D WXBILN          s             20
     D WXLK            s             20
     D WXTILH          s             20
     D WXLKH           s             20
     D WXBILK          s             20
     D WSFORMS         s             20
     D WSREFR          s              1
     D REFRESH         s             50
     D SOKBNR          s             10
     D SOKTNR          s              8
     D SOKTNRN         s              8  0
     D SOKUT           s              4
     D user            s             10
     D MaxV            s              5
     D MaxVis          s              5  0
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      SOKBNR  = zhbgetvar('SOKBNR')
     C                   eval      SOKBNR  = uppify(SOKBNR)
     C                   eval      SOKTNR  = zhbgetvar('SOKTNR')
     C                   eval      SOKTNRN = c2n2(SOKTNR)
     C                   eval      SOKUT   = zhbgetvar('SOKUT')
     C                   eval      SOKUT   = uppify(SOKUT)
     C                   eval      WXBILN = zhbgetvar('WXBILN')
     C                   IF        WXBILN = *BLANKS
     C                   EVAL      WXBILN = 'WSBILN'
     C                   END
     C                   eval      WXLK   = zhbgetvar('WXLK')
     C                   IF        WXLK   = *BLANKS
     C                   EVAL      WXLK   = 'WSLK'
     C                   END
     C                   eval      WXTILH = zhbgetvar('WXTILH')
     C                   IF        WXTILH = *BLANKS
     C                   EVAL      WXTILH = 'WSHENG'
     C                   END
     C                   eval      WXLKH  = zhbgetvar('WXLKH')
     C                   IF        WXLKH  = *BLANKS
     C                   EVAL      WXLKH  = 'WSLKH'
     C                   END
     C                   eval      WXBILK = zhbgetvar('WXBILK')
     C                   IF        WXBILK = *BLANKS
     C                   EVAL      WXBILK = 'WSBILK'
     C                   END
     C                   eval      WSFORMS = zhbgetvar('WSFORMS')
     C                   IF        WSFORMS = *BLANKS
     C                   EVAL      WSFORMS = 'forms[0]'
     C                   END
     C                   eval      WSREFR  = zhbgetvar('WSREFR')
     C                   IF        ((((WSREFR = 'J') OR
     C                                (WSREFR = 'j')) OR
     C                                (WSREFR = 'Y')) OR
     C                                (WSREFR = 'y'))
     C                   eval      REFRESH = 'self.opener.document.'
     C                             + %trim(WSFORMS)
     C                             + '.submit();'
     C                   END
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     UNIT
     C                   CLOSE     UNITL01
     C                   CLOSE     UNITL02
     C                   CLOSE     TRAN
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'INQBILN'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('wxbiln':WXBILN)
     C                   callp     updHTMLvar('wxlk':WXLK)
     C                   callp     updHTMLvar('wxtilh':WXTILH)
     C                   callp     updHTMLvar('wxlkH':WXLKH)
     C                   callp     updHTMLvar('wxbilk':WXBILK)
     C                   callp     updHTMLvar('wsforms':WSFORMS)
     C                   callp     updHTMLvar('wsrefr':WSREFR)
     C                   callp     updHTMLvar('refresh':REFRESH)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     c                   Move      *zeros        AntVist           5 0
     C     SOKBNR        IFEQ      *BLANKS
     C     SOKTNRN       ANDEQ     *ZEROS
     C     SOKUT         ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   END
      *
     C                   SELECT
     C                   WHEN      SOKBNR <> *BLANKS
     C     SOKBNR        SETLL     UNITL01
     C                   READ      UNITL01                                33
     C                   WHEN      SOKTNRN <> 0
     C     SOKTNRN       SETLL     UNIT
     C     SOKTNRN       READE     UNIT                                   33
     C                   WHEN      SOKUT <> *BLANKS
     C     SOKUT         SETLL     UNITL02
     C                   READ      UNITL02                                33
     C                   ENDSL
      *
     C                   DOW       *IN33 = *OFF
     C                   exsr      exclude
     c                   IF        EXCL = 'N'
     c     TRANK         CHAIN     TRAN                               33
     c   33              MOVE      *BLANKS       VMNAVN
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('UNBILN':UNBILN)
     C                   callp     updHTMLvar('UNTILH':UNTILH)
     C                   callp     updHTMLvar('UNLAND':UNLAND)
     C                   callp     updHTMLvar('UNTRME':UNTRME)
     C                   callp     updHTMLvar('UNVEKT':
     C                             %trim(%editc(UNVEKT:'3')))
     C                   callp     updHTMLvar('UNTARA':
     C                             %trim(%editc(UNTARA:'3')))
     C                   callp     updHTMLvar('UNM3':
     C                             %trim(%editc(UNM3:'3')))
     C                   callp     updHTMLvar('UNLM':
     C                             %trim(%editc(UNLM:'3')))
     C                   callp     updHTMLvar('UNUNTY':UNUNTY)
     C                   callp     updHTMLvar('VMNAVN':VMNAVN)
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C                   END
      *
     C                   SELECT
     C                   WHEN      SOKBNR <> *BLANKS
     C                   READ      UNITL01                                33
     C                   WHEN      SOKTNRN <> 0
     C     SOKTNRN       READE     UNIT                                   33
     C                   WHEN      SOKUT <> *BLANKS
     C                   READ      UNITL02                                33
     C                   ENDSL
      *
     C                   ENDDO
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   Move      'J'           EXCL              1
      * diverse tester - om ikke bestått hopp ut (med J i EXCL)
S01  c*                  IF        UNAVD <> XAVD
S01  C*                  GOTO      UTEXCL
S01  C*                  END
      * Posten skal med på liste
     C                   Move      'N'           EXCL              1
      *
     C     UtExcl        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      UNIT
     C                   OPEN      UNITL01
     C                   OPEN      UNITL02
     C                   OPEN      TRAN
      * Key for CUNDL01
     C     TRANK         KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    UNTRAN
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVE      'OSAVD'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C                   Z-ADD     PAVRDN        XAVD              4 0
      *
     C                   endsr
