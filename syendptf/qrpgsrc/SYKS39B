      * KONTERING AV "SYSPED-FAKTURA"
      * OBS!!!!   P.T. (VÅR 2002) FORUTSETTES DET (HARDKODET)
      * OBS!!!!   AT FAKTURA ER I NORSKE KRONER !!!!
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 dersom faktdato(INDATO)=0 brukes DAGENS for MOMSSATS!!
10XXX * 02 JOV PTF10 dersom linje skal skippes, hopp over mer (DIGIT/sign-invalid i dato..:=)
10XXX * 03 JOV PTF10 endringer utført 2008.06.19 hos ramberg tatt inn i standard
10XXX * 04 JOV PTF10 7 lang OPD-feil
10XXX * 05 SH  PTF10 mva i kosta
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 06 JOV PTF12 Etter oppdrag 5.0->7.0 er felt INUOPD alltid 0 - test bare mot INUAVD
      *              Hele rutine bør over i EDISE1K - det forenkler alt...
10XXX * 07 SH  PTF10 skriver til logg
10XXX * 08 SH  PTF10 lagt til årr mnd i loggen
********************************************************************
      *
     H DATEDIT(*DMY)
N07  FKOSTL     O  A E             DISK
N07  FLEVEL01   IF   E           K DISK
     FINTFAM    UF   E           K DISK
     FKOSTA     O  A E           K DISK
     FKOSTA2    IF   E           K DISK
     F                                     RENAME(KOSTAR:KOSTAR2)
     FKOSTB     O  A E           K DISK
     FKOSTT     UF   E           K DISK
     FCUIFAMO   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFRISOL01  IF   E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR11)
     FHEAD39    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR39)
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     D                 DS
     D  CMVK1F                 1      3
     D  CMVK2F                 4      6
     D  CMVK3F                 7      9
     D  CMVK4F                10     12
     D  CMVK5F                13     15
     D  CMVK6F                16     18
     D  CMVK7F                19     21
     D  CMVK8F                22     24
     D  CMVK9F                25     27
N06  D  CMVK10F               28     30
N06  D  CMVK11F               31     33
N06  D  CMVK12F               34     36
N06  D  CMVK13F               37     39
N06  D  CMVK14F               40     42
N06  D  CMVK15F               43     45
S06  D* GF                     1     27
S06  D*                                    DIM(9)
N06  D  GF                     1     45
N06  D                                     DIM(15)
     D                 DS
     D  CMVK1T                 1      3
     D  CMVK2T                 4      6
     D  CMVK3T                 7      9
     D  CMVK4T                10     12
     D  CMVK5T                13     15
     D  CMVK6T                16     18
     D  CMVK7T                19     21
     D  CMVK8T                22     24
     D  CMVK9T                25     27
N06  D  CMVK10T               28     30
N06  D  CMVK11T               31     33
N06  D  CMVK12T               34     36
N06  D  CMVK13T               37     39
N06  D  CMVK14T               40     42
N06  D  CMVK15T               43     45
S06  D* GT                     1     27
S06  D*                                    DIM(9)
N06  D  GT                     1     45
N06  D                                     DIM(15)
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  XXANNE                 1     20
      * AvdOpd-sammenhengende
     D  INREA                  1      4
     D  INREO                  5     11
     D  INRE9                 11     11
     D  INREFX                12     15
      * Avd/Opd-med strek mellom (avd fremdeles 1-4=INREA)
     D  INREST                 5      5
     D  INREO2                 6     12
     D  INRE10                12     12
     D  INRE11                13     13
      * Turnummer
     D  INRET                  1      8
     D  INREFZ                 9     20
     D  INRET2                 6      9
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
      *
     C                   EXSR      INIT
      *
     C     ' '           SETLL     INTFAM
     C     ' '           READE     INTFAM                                 94
     C     *IN94         DOWEQ     '0'
      * BRUDD - LEGG UT KOSTA.
     C     INFANR        IFNE      GMFANR
     C     INSEID        ORNE      GMSEID
     C     GMFANR        IFNE      *ZEROS
     C                   EXSR      SRKAU
     C                   END
     C                   END
      *
     C                   EXSR      SRKB
      *
      * OPPDATER STATUS, X=BEHANDLET
     C                   MOVE      'X'           INST
     C                   UPDATE    INTFASR
      *
     C     ' '           READE     INTFAM                                 94
     C                   END
      *
      * E.O.F - LEGG UT SISTE KOSTA-POST
     C     GMFANR        IFNE      *ZEROS
     C                   EXSR      SRKAU
     C                   END
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *
      *////////////////////////////////////////////////////////////
     C     SRKB          BEGSR
      *////////////////////////////////////////////////////////////
      *
      * Hvis første linje på fakturaen, INIT av KOSTA-post
     C     KBBNR         IFEQ      *ZEROS
     C                   EXSR      SRKAI
N02  c     Skipp         cabeq     'J'           Utopd
     C                   END
      *
N03  c                   move      *blanks       KBBILT
     C                   MOVE      *ZEROS        XXMVA             9 2
     C     INMVA         IFEQ      'J'
N01  c                   move      INDATO        MVADT             8 0
N01  C     MVADT         IFEQ      *zeros
N01  c                   move      ULDT          MVADT
N01  c                   endif
     C                   Z-ADD     FITAX         FITAXN            5 4
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
S01  C*    INDATO        ANDLT     FITAXD
N01  C     MVADT         ANDLT     FITAXD
     C                   Z-ADD     FITAX2        FITAXN
     C                   END
     C     INBELN        MULT      FITAXN        XXMVA
     C                   END
      *
     C     INBELN        ADD       XXMVA         KBBLHB
     C                   SUB       KBBLHB        KABL
     C                   SUB       XXMVA         KABLM
     C     XXMVA         IFNE      *ZEROS
     C                   Z-ADD     1             KBKDM
     C                   MOVE      'P'           KBKDPF
N05  C                   ADD       1             ANTPLI            7 0
     C                   ELSE
     C                   Z-ADD     0             KBKDM
     C                   MOVE      'F'           KBKDPF
N05  C                   ADD       1             ANTFRI            7 0
     C                   END
      * FINN OPPDRAG DET SKAL HENFØRES TIL....
S03  C*                  MOVE      *OFF          *IN75
     C                   MOVE      INANNE        XXANNE           20
     C                   MOVE      INANN2        XXANN2           20
      * Avd/opd
     C                   MOVE      *ZEROS        KBAVD
     C                   MOVE      *ZEROS        KBOPD
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   MOVE      *ZEROS        KBKN
      * AVD/OPPDR.NR GITT...
     C     INOPD         IFNE      *ZEROS
     C                   MOVE      INAVD         HEAVD
     C                   MOVE      INOPD         HEOPD
     C                   MOVEL     INAVD         AVDOPD           11
     C                   MOVE      INOPD         AVDOPD
     C                   MOVEL     AVDOPD        KBBILT
     C     OPDKEY        CHAIN     HEADF                              33
     C  N33              GOTO      UTOPD
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   END
      *
      * Fant ikke via AVD/OPD. forsøk med annen ref.
     C     INANNE        IFNE      *BLANKS
     C                   MOVE      INANNE        TST2              5
     C     TST2          IFEQ      *BLANKS
     C                   MOVEL     INANNE        KBBILT
     C                   ELSE
      * VED LANG REF (SEND.NR) FLYTT INN HØYRE DEL.(MISTER "40170")
     C                   MOVE      INANNE        KBBILT
     C                   END
      * FINN OPPDRAG VIA SENDNR I FRIE SØKEVEIER.
     C                   MOVE      INANNE        TSTFBN           10
     C     TSTFBN        IFEQ      *BLANKS
      * Kort=10 første
     C                   MOVEL     INANNE        TSTFBN           10
     C                   eval      FSSOK    =    TSTFBN
     C                   else
      *Langt=17 siste
     C                   MOVE      INANNE        TSTFBN17         17
     C                   eval      FSSOK    =    TSTFBN17
     C                   END
N03   *
N03  C                   MOVEL     'IFB'         FSKODE
N03  c     DHLTag        tag
N03   *
     C     FSKEY         SETLL     FRISOL01
     C     FSKEY         READE     FRISOL01                               33
     C     *IN33         DOWEQ     '0'
     C     FSAVD         IFNE      INUAVD
N06   * CORNY - MEN ETTER UTVIDING T5->7 LANGT OPPDRNR SÅ KOMMER IKKE UTSTEDERS OPPDR.NR MED..
N06   * FOR RAMBERG SÅ ER INTERNFAKTURERING ALLTID MELLOM KONTOR /ULIKE AVDELINGER
N06   * TILSTREKKELIG Å TESTE MOT AVDELING.. (tror det vil gjelde alle ved intern-konterring)
S06  C*    FSOPD         ORNE      INUOPD
      * Funnet en match(som er forskjellig fra "seg selv")
     C                   MOVE      FSAVD         HEAVD
     C                   MOVE      FSOPD         HEOPD
     C     OPDKEY        CHAIN     HEADF                              33
     C  N33              GOTO      UTOPD
     C                   END
     C     FSKEY         READE     FRISOL01                               33
     C                   END
N03   * Kommet Hit = Fant ikke via IFB - forsøk DHL
N03   * (nytt DHL sendnr er kun 10 lang men i fila kommer 12 - blank de 2 siste..)
N03  c     FSKODE        IFEQ      'IFB'
N03  C                   MOVEL     'DHL'         FSKODE
N03  c                   move      *blanks       Blank25          25
N03  C                   move      Blank25       FSSOK
N03  C                   goto      DHLtag
N03  c                   endif
      *
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
      *
      * SENDINGSNR - INNLAND??
     C                   MOVEL     INANNE        XXSNDN           20
     C     XXSNDN        SETLL     HEAD39
     C                   READ      HEAD39                                 33
     C     *IN33         DOWEQ     '0'
     C     HXSNDN        CABNE     INANNE        STANN2
     C     HEAVD         IFNE      INUAVD
N06   * CORNY - MEN ETTER UTVIDING T5->7 LANGT OPPDRNR SÅ KOMMER IKKE UTSTEDERS OPPDR.NR MED..
N06   * FOR RAMBERG SÅ ER INTERNFAKTURERING ALLTID MELLOM KONTOR /ULIKE AVDELINGER
N06   * TILSTREKKELIG Å TESTE MOT AVDELING.. (tror det vil gjelde alle ved intern-konterring)
S06  C*    HEOPD         ORNE      INUOPD
      * Funnet en match(som er forskjellig fra "seg selv")
     C                   GOTO      UTOPD
     C                   END
     C                   READ      HEAD39                                 33
     C                   END
     C*
     C     STANN2        TAG
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
      * Oppgitt som 4 siffer + / + 5 siffer (+ EVT / SIGN)
     C     INREST        IFEQ      '/'
     C     INRE11        IFEQ      ' '
     C     INRE11        OREQ      '/'
     C                   TESTN                   INREA                33
     C     *IN33         IFEQ      '1'
     C                   TESTN                   INREO2               33
     C     *IN33         IFEQ      '1'
     C                   MOVE      INREA         HEAVD
     C                   MOVE      INREO2        HEOPD
     C     OPDKEY        CHAIN     HEADF                              33
     C  N33              GOTO      UTOPD
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   END
     C                   END
     C                   END
     C                   END
     C* Turnr??
     C     INREFZ        IFEQ      *BLANKS
     C     INRET         ANDNE     *BLANKS
     C                   TESTN                   INRET                33
     C     *IN33         IFEQ      '1'
     C     INRET         ANDNE     *ZEROS
     C                   MOVE      INRET         TURNR             8 0
     C     TURNR         CHAIN     HEAD11                             33
     C     *IN33         IFEQ      '0'
     C                   EXSR      TUR
     C                   GOTO      UTTUR
     C                   END
     C                   END
     C                   END
      *
      * ENNÅ IKKE TREFF, OGSÅ INANN2 OPGITT - PRØV PÅ NY MED DEN
     C     XXANN2        IFNE      *BLANKS
s03  C*                  MOVE      *ON           *IN75
     C                   MOVEL     XXANN2        KBBILT
     C                   MOVEL     XXANN2        XXANNE
     C                   MOVE      *BLANKS       XXANN2
     C                   GOTO      STANN2
     C                   END
     C                   END                                                    INANNE IFNE.
      *
     C     UTOPD         TAG
      * Legg ut posten i KOSTB (TUROPD = INDIKATOR FOR OM DET ER VIA
      * TUR ELLER OPPDRAG POSTEN SKAPES)
     C                   MOVE      'O'           TUROPD            1
     C                   EXSR      SRKBU
      *
      * Når tur-ref har subr TUR lagt ut 1 post (via SRKBU) pr opdrag
     C     UTTUR         TAG
      *
     C                   MOVE      INFANR        GMFANR            7 0
     C                   MOVE      INSEID        GMSEID           35
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     TUR           BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVE      *ZEROS        ANTOPD            5 0
     C                   MOVE      *ZEROS        TOTVKT           11 0
      * FINN TOTAL VEKT (IGNORER SLETTEDE)
     C     TURNR         SETLL     HEAD11
     C     TURNR         READE     HEAD11                                 34
     C     *IN34         DOWEQ     '0'
     C     HEST          IFNE      'S'
     C                   ADD       1             ANTOPD
     C     HEFBV         IFEQ      *ZEROS
     C                   Z-ADD     1             HEFBV
     C                   END
     C                   ADD       HEFBV         TOTVKT
     C                   END
     C     TURNR         READE     HEAD11                                 34
     C                   END
      *
     C                   MOVE      ANTOPD        TOTOPD            5 0
      *
      * FORDEL KOSTNAD (PÅ SISTEOPPDR. SETTES REST. FOR Å HINDRE DIFF)
     C                   Z-ADD     KBBLHB        TOTBEL           13 2
     C                   Z-ADD     KBBLHB        DIFF             13 2
     C                   MOVE      *ZEROS        ANTOPD            5 0
     C     TURNR         SETLL     HEAD11
     C     TURNR         READE     HEAD11                                 34
     C     *IN34         DOWEQ     '0'
     C     HEST          IFNE      'S'
     C                   ADD       1             ANTOPD
     C     HEFBV         IFEQ      *ZEROS
     C                   Z-ADD     1             HEFBV
     C                   END
     C     ANTOPD        IFEQ      TOTOPD
     C                   Z-ADD     DIFF          KBBLHB
     C                   ELSE
     C     TOTBEL        MULT      HEFBV         FAKTOR           19 2
     C     FAKTOR        DIV(H)    TOTVKT        KBBLHB
     C                   END
     C                   MOVE      *ZEROS        KBAVD
     C                   MOVE      *ZEROS        KBOPD
     C                   MOVE      'T'           TUROPD            1
     C                   EXSR      SRKBU
     C                   SUB       KBBLHB        DIFF
     C                   END
     C     TURNR         READE     HEAD11                                 34
     C                   END
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     SRKBU         BEGSR
      *////////////////////////////////////////////////////////////
      *
      * UKJENT SENDEPART - ELLER FAKTURA ALLEREDE REGISTRERT??
      * Skriv til liste FØRSTE GANG, SKIPP
     C     SKIPP         IFEQ      'J'
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C     UKJENT        IFEQ      'J'
     C     INSEID        ANDNE     GMSEID
     C                   EXCEPT    DETID
     C                   END
     C     FINNES        IFEQ      'J'
     C     KAFNR         ANDNE     GMFNR
     C                   EXCEPT    DETFIN
     C                   END
s03  C*                  MOVE      KAFNR         GMFNR             9
n03  C                   MOVE      KAFNR         GMFNR
     C                   MOVE      INSEID        GMSEID           35
     C                   GOTO      UTDET
     C                   END
      *
      * MANGLENDE MATCH -  Skriv til liste...
     C                   MOVE      *ZEROS        KBKAVD
     C                   MOVE      *BLANKS       KBKKEY
n03  C                   MOVE      *BLANKS       MATCHTXT         20
      * HEAVD/HEOPD lik 0 = ref ikke funnet - åpne ferdig-status m.m.
     C* 7/8 av KBTUNR: 7:GEBYR,1=GEB.0=Vkt 8:NØKKEL,1=etter opd 9=IKKE
     C     HEOPD         IFEQ      *ZEROS                                       B1
     C                   MOVE      ' '           KAST
n03  c                   eval      MATCHTXT= 'Ingen match !!!'
     C     TUROPD        IFEQ      'O'
     C                   MOVE      1             KBGEBY
     C                   MOVE      9             KBNØKK
     C                   ELSE
     C                   MOVE      0             KBGEBY
     C                   MOVE      9             KBNØKK
     C                   END
     C                   ELSE                                                   X1
     C     TUROPD        IFEQ      'O'
     C                   MOVE      1             KBGEBY
     C                   MOVE      1             KBNØKK
     C                   Z-ADD     HEAVD         KBKAVD
     C                   MOVEL     HEOPD         KBKKEY
     C                   ELSE
     C                   MOVE      0             KBGEBY
     C                   MOVE      2             KBNØKK
     C                   MOVE      *ZEROS        KBKAVD
     C                   MOVEL     HEPRO         KBKKEY
     C                   END
     C                   MOVE      HEAVD         KBAVD
     C                   MOVE      HEOPD         KBOPD
     C                   SELECT
     C     HEUR          WHENEQ    'H'
     C                   MOVE      HEKNSF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNKF        KBKN
     C                   END
     C     HEUR          WHENEQ    'A'
     C     HEUR          OREQ      'C'                                          IMPORT
     C     HEUR          OREQ      'F'
     C                   MOVE      HEKNKF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNSF        KBKN
     C                   END
     C     HEUR          WHENEQ    'B'                                          EKSPORT
     C     HEUR          OREQ      'D'
     C     HEUR          OREQ      'G'
     C                   MOVE      HEKNSF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNKF        KBKN
     C                   END
     C                   ENDSL
      * Ikke funnet kunde - åpne bilaget m.m.
     C     KBKN          IFEQ      *ZEROS                                        B2
s03  C*                  MOVE      *ZEROS        KBKN
s03  C*                  MOVE      *ZEROS        KBAVD
s03  C*                  MOVE      *ZEROS        KBOPD
n03  c                   eval      MATCHTXT= 'Knr mangler(oppd)!!!'
     C                   MOVE      ' '           KAST
     C                   MOVE      9             KBNØKK
     C                   END                                                     E2
      *
     C                   END                                                    E1
      * STANDARD KODE FOR AVSENDER +  KONVTABELL FOR OPPGITTE KODER..
     C                   MOVE      CMVK          KBVK
     C                   Z-ADD     1             NR                2 0
     C                   SETOFF                                       33
     C                   MOVE      'N'           GEBKON            1
     C     INVK          LOOKUP    GF(NR)                                 33
     C     *IN33         IFEQ      '1'
     C     GT(NR)        ANDNE     *BLANKS
     C                   MOVE      GT(NR)        KBVK
     C                   MOVE      'J'           GEBKON
     C                   END
      *
     C                   WRITE     KOSTBR
      *
      *
      * MANGLENDE MATCH -  Skriv til liste...
S03  C*    HEOPD         IFEQ      *ZEROS
N03  C     KBNØKK        IFEQ      9
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C     KABNR         IFNE      GMBNR
     C                   EXCEPT    DETHEA
     C                   END
     C                   EXCEPT    DET
     C                   MOVE      KABNR         GMBNR             7 0
     C                   END
      * MANGLENDE GEB-KONV  Skriv til liste...
     C     GEBKON        IFEQ      'N'
     C     CMPRKO        ANDEQ     '1'
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C     KABNR         IFNE      GMBNR
     C                   EXCEPT    DETHEA
     C                   END
     C                   EXCEPT    DETKON
     C                   MOVE      KABNR         GMBNR             7 0
     C                   END
      *
     C     UTDET         TAG
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     SRKAI         BEGSR
      *////////////////////////////////////////////////////////////
N03   * Ukjent avsender - skipp
N03  C     AVSKEY        CHAIN     CUIFAMO                            33
N03  C     *IN33         IFEQ      '1'
N03  C                   MOVE      'J'           UKJENT
N03  C                   MOVE      'J'           SKIPP
N03  C                   GOTO      UTINA
N03  C                   END
N03   * SAMME FAKT-NR MOTTATT FØR.. (FRA GITT LEVERANDØR??)  (men ignorere slettede)
N03  C                   MOVE      CMLEV         KALNR
N03  C                   MOVEL     INFANR        KAFNR
N03  C     KALNR         IFNE      *ZEROS
N03  C     KAFNR         ANDNE     *ZEROS
N03  C     KOSA2K        SETLL     KOSTA2                             33
N03  C     KOSA2K        READE     KOSTA2                                 33
N03  C     *IN33         DOWEQ     '0'
N03  C     kast          IFNE      'D'
N03  C                   MOVE      'J'           FINNES
N03  C                   MOVE      'J'           SKIPP
N03  C                   GOTO      UTINA
N03  C                   END
N03  C     KOSA2K        READE     KOSTA2                                 33
N03  C                   END
N03  C                   END
N03   *
N03   * bilaget SKAL behandles
      * INITIERING AV KOSTA-FELT
     C                   CLEAR                   KOSTAR
     C                   MOVE      'EDI'         KASG
     C                   MOVE      'NOK'         KAVAL
     C                   Z-ADD     100           KAVKU
     C                   Z-ADD     100           KAOMRF
     C                   MOVE      ZUSER         KAUSER
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      ULDT          KADTE
     C                   MOVE      XULTM         KATME
     C                   MOVE      ULDT          KADTR
     C                   MOVE      XULTM         KATDR
      *
     C                   MOVE      'N'           UKJENT            1
     C                   MOVE      'N'           FINNES            1
     C                   MOVE      'N'           SKIPP             1
      * HENT VISSE VERDIER SAMT REGLER FRA FIL.
s03  C*    AVSKEY        CHAIN     CUIFAMO                            33
s03  C*    *IN33         IFEQ      '0'
     C                   MOVE      CMLEV         KALNR
     C                   MOVE      CMBETB        KABB
s03  C*                  ELSE
s03  C*                  MOVE      'J'           UKJENT
s03  C*                  MOVE      'J'           SKIPP
s03  C*                  GOTO      UTINA
s03  C*                  END
      *
     C     CMVK          IFEQ      *BLANKS
     C                   MOVE      'FRA'         CMVK
     C                   END
s03  C* Nulle beløp for akkumulering
s03  C*                  MOVE      *ZEROS        KABL
s03  C*                  MOVE      *ZEROS        KABLM
     C                   MOVEL     INFANR        KAFNR
      *
s03  C* SAMME FAKT-NR MOTTATT FØR.. (FRA GITT LEVERANDØR??)
s03  C*    KALNR         IFNE      *ZEROS
s03  C*    KAFNR         ANDNE     *ZEROS
s03  C*    KOSA2K        CHAIN     KOSTA2                             33
s03  C*    *IN33         IFEQ      '0'
s03  C*                  MOVE      'J'           FINNES
s03  C*                  MOVE      'J'           SKIPP
s03  C*                  GOTO      UTINA
s03  C*                  END
s03  C*                  END
      *
     C* TILDEL PRE.REG.NR (LØPENR FRA FAST TELLER)...
     C     'Ø'           CHAIN     KOSTT                              33
     C                   MOVE      KTNR          KBBNR
     C                   ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
      * Prereg løpenr
     C                   MOVE      KBBNR         KABNR
     C     CMBISE        IFNE      *BLANKS
     C* Bilagsnr til økonomi
     C     CMBISE        CHAIN     KOSTT                              33
     C  N33              MOVE      KTNR          KABNR2
     C  N33              ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
     C                   END
      * Sett i utg.pkt status til ferdigmeldt, oppheves hvis...
     C     CMAUTF        IFNE      *BLANKS
     C     KABNR2        ANDNE     *ZEROS
     C                   MOVE      'G'           KAST
     C                   END
      * Bilagsdato / Lev.s fakt.dato/forfallsdato
     C                   Z-ADD     INDATO        KABDT
     C                   Z-ADD     INDATO        KAFDT
     C                   MOVE      INDAFF        KAFFDT
     C                   MOVEL     INKID         KALKID
      *
     C                   MOVE      20            KAPCC
     C                   MOVE      INAAR         KAPÅR
     C                   MOVE      INMND         KAPMN
      *  FLAGG FOR AT MOTTAKET SKAL OVER I KUNDERESKONTRO XXXX
     C                   MOVE      *BLANKS       KATXT
     C     CMKRES        IFNE      *ZEROS
     C                   MOVEL     '*KU*:'       XXTXT            13
     C                   MOVE      CMKRES        XXTXT
     C                   MOVEL     XXTXT         KATXT
     C                   END
      *
     C     UTINA         ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     SRKAU         BEGSR
      *////////////////////////////////////////////////////////////
      *
     C     SKIPP         IFNE      'J'
N05  C     ANTFRI        ifeq      0
N05  c                   move      'P'           KAMVA
N05  c                   end
N05  C     ANTPLI        ifeq      0
N05  c                   move      'F'           KAMVA
N05  c                   end
     C                   WRITE     KOSTAR
N07  c                   EXSR      LOGGSR
N05  c                   move      *zeros        antpli
N05  c                   move      *zeros        antfri
     C                   END
      * TRIGGE INIT AV NESTE KOSTA.
     C                   MOVE      *ZEROS        KBBNR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *ON           *IN99
      *
      *
      *
      *
     C     AVSKEY        KLIST
     C                   KFLD                    INSEID
     C                   KFLD                    INKUNR
      *
     C     FSKEY         KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   MOVEL     'IFB'         FSKODE
      *
     C     OPDKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
     C     KOSA2K        KLIST
     C                   KFLD                    KALNR
     C                   KFLD                    KAFNR
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      * Trigge print av heading (hvis manglende match)
     C                   SETON                                        OF
n03  c     *LIKE         DEFINE    KAFNR         GMFNR
      *
     C                   ENDSR
N01  C***********************************************
N01  C     LOGGSR        BEGSR
N01  C***********************************************
N01  c     levek         klist
N01  c                   kfld                    fifirm
N01  c                   kfld                    kalnr
N01  c     levek         chain     level01                            44
N01  c     *in44         ifeq      '0'
N01  C                   CALL      'SYGE15C'
N01  C                   PARM                    WDT               8
N01  C                   PARM                    WTM               6
N01  c                   move      wdt           LODATO
N01  c                   move      wtm           loklok
N01  c                   eval      louser=ZUSER
N01  c                   move      kabnr         lounik
N01  c                   move      kabnr2        lobila
n01  c                   move      'E'           lofoan
n01  c                   move      *zeros        lojour
n01  c                   move      kasg          losg
n01  c
N01  C                   eval      lotxt='Mottat int '
N01  c                             +'ernfaktura :'
N01  c                             +%trim(LNAVN)
n08  c                   move      kapÅr         loaar
n08  c                   move      kapmn         lomnd
n01  c                   write     kostlr
n01  c                   end
n01  c                   endsr
      *
      *
      *
      *
     OLISTE     E            PHEAD            01
     O                       FIFT                45
     OLISTE     E            PHEAD            02
     O                                           24 '** EDI-FAKTURA!! BELASTN'
     O                                           48 '. M/REF.  SOM IKKE MATCH'
     O                                           72 'ER SYSPED OPPDRAG **    '
     O                                          100 'DATO:'
     O                       UDATE         Y    110
     O                                          120 'SIDE:'
     O                       PAGE          Z    126
S03   *
S03  OLISTE     E            PHEAD            03
S03  O*                                          24 '------------------------'
S03  O*                      *PLACE              48
S03  O*                      *PLACE              72
S03  O*                      *PLACE              96
S03  O*                      *PLACE             120
S03  O*                                         132 '------------'
N03
N03  OLISTE     E            PHEAD            03
N03  O                                           24 '-----------       ****  '
N03  O                                           48 'Mottatte referanser for '
N03  O                                           72 'matching ****      -----'
N03  O                                           96 '------------------------'
N03  O                                          120 '------------------------'
N03  O                                          132 '------------'
     O          E            PHEAD            04
S03  O*                                          10 'PREREG.NR'
S03  O*                                          20 'BILAGSNR'
S03  O*                                          30 'FAKT.NR'
S03  O*                                          40 'KUNDENR'
S03  O*                                          52 'FAKT.DATO'
S03  O*                                          67 'AVD. OPPDNR.'
S03  O*                                          88 'ANNEN REFERANSE     '
S03  O*                                         106 'NETTO BELØP'
S03  O*                                         118 'MOMS'
S03  O*                                         126 'PERIODE'
N03  O                                           10 'PREREG.NR'
N03  O                                           22 'AVD. OPPDR.'
N03  O                                           43 'ANNEN REFERANSE     '
N03  O                                           67 'ANNEN REFERANSE 2   '
N03  O                                           88 'FEILTYPE            '
N03  O                                          104 'NETTO BELØP'
N03  O                                          116 'MOMS'
N03  O                                          124 'PERIODE'
     OLISTE     E            PHEAD          1 05
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            DETHEA         1
     O                       KABNR         Z     10
s03  O*                      KABNR2        Z     20
s03  O*                                          29 'Avs.ID:'
s03  O*                      INSEID              65
N03  O                                           22 'Bilagsnr:'
N03  O                       KABNR2        Z     30
N03  O                                           39 'Avs.ID:'
N03  O                       INSEID              75
N03  O                                           81 'Fakt.nr:'
N03  O                       INFANR        Z     89
N03  O                                           98 'Dato:'
N03  O                       INDATO             109 '    .  .  '
N03  O                                          115 'Kn:'
N03  O                       INKUNR        Z    123
S03   *
s03  O*ISTE     E            DET            1
S03  O*                      KABNR         Z     10
S03  O*                      KABNR2        Z     20
S03  O*                      INFANR        Z     30
S03  O*                      INKUNR        Z     40
S03  O*                      INDATO              52 '    .  .  '
S03  O*                      INAVD         Z     59
S03  O*                      INOPD         Z     67
S03  O*                      INANNE              88
S03  O*                      INBELN        J    106
S03  O*                      XXMVA         J    118
S03  O*                      INMND              122
s03  O*                      INAAR              125
s03  O*ISTE     E    75      DET            1
s03  O*                      INANN2              86
     OLISTE     E            DET            1
     O                       KABNR         Z     10
N04  O*                      INAVD         Z     16
N04  O                       INAVD         Z     15
     O                       INOPD         Z     22
     O                       INANNE              43
     O                       INANN2              67
     O                       MATCHTXT            88
     O                       INBELN        J    104
     O                       XXMVA         J    116
     O                       INMND              120
     O                       INAAR              123
     OLISTE     E            DETKON         1
     O                       KABNR         Z     10
     O                       KABNR2        Z     20
     O                       INFANR        Z     30
     O                       INKUNR        Z     40
     O                       INDATO              52 '    .  .  '
     O                       INAVD         Z     59
     O                       INOPD         Z     67
     O                       INANNE              88
     O                                          111 '*MANGL.GEB-KNV* Mott:'
     O                       INVK               115
     O                                          118 '-->'
     O                       KBVK               125
     OLISTE     E            DETID          1
     O                                           25 '*UKJENT AVSENDER-ID*:'
     O                       INSEID              65
     O                                           93 '(Registrer ID - Omkjør E'
     O                                          114 'DI via TVINMNU pkt 1)'
     OLISTE     E            DETFIN         1
     O                                           28 '*FAKTURA FINNES FRA FØR*'
     O                                           33 'Lev:'
     O                       KALNR         Z     41
     O                                           47 'F.nr:'
S03  O*                      KAFNR               59
N03  O                       KAFNR               61
     O                                           75 '(M/Prereg.nr:'
     O                       KABNR         Z     84
     O                                           90 'B.nr:'
     O                       KABNR2        Z     97
     O                                          105 'B.dt:'
     O                       KABDT              116 '    .  .  '
     O                                          118 ')'
