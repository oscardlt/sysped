      *
      **************************************************************
      *** PROGRAM SKAL OPPDATERE FORTOLLINGSGEBYR                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01-26                               ***
      *** LEDIGE INDIKATORER 27-32 37-40 42-83 87-99             ***
      **************************************************************
      *
     FFAK4      UF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTV     IF   E           K DISK
     FHEADF     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FPRIK      IF   E           K DISK
     FPRIK1     IF   E           K DISK
     F                                     RENAME(PRIKR:PRIKR1)
     FPRIS      IF   E           K DISK
     D                 DS
     D  PKKOLL                 1      1
     D  PRKOLL                 1      1
     D                 DS
     D  YTNR                   1      1
     D  YVK                    2      4
     D  YKEY                   1      4
     D                 DS
     D  PRTNR                  1      1
     D  PRGEBY                 2      4
     D  PRKEY                  1      4
     D                 DS
     D  YKNR                   1      8
     D  YAVTN                  9      9
     D  YGEBY                 10     12
     D  YKEY1                  1      9
     D  YKEY2                  1     12
     D                 DS
     D  PKKNR                  1      8
     D  PKAVTN                 9      9
     D  PKGEBY                10     12
     D  PKKEY1                 1      9
     D  PKKEY2                 1     12
     C*
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C*
     C                   RETURN
     C*
     C******************************************
     C     HOVED         BEGSR
     C******************************************
     C* Indicate that at least one record is found
     C     KODTVK        CHAIN     KODTV                              41
     C                   MOVE      'K'           YSK               1
     C     FAK4K         SETLL     FAK4                               41
     C*
     C     STLES         TAG
     C     FAK4K         READE     FAK4                                   41
     C*
     C* PRIS PR FORTOLLET LINJE
     C  N41FA01B         IFEQ      'L'
     C     FAOPKO        IFEQ      ' '
     C                   EXSR      BFAKT0
     C                   END
     C                   GOTO      SLLES
     C                   END
     C  N41              GOTO      STLES
      *
     C   41YSK           IFEQ      'K'
     C                   MOVE      'S'           YSK
     C     FAK4K         SETLL     FAK4                               41
     C                   GOTO      STLES
     C                   END
     C*
     C     SLLES         ENDSR
     C*
     C******************************************
     C     BFAKT0        BEGSR
     C******************************************
     C     HEADFK        CHAIN     HEADF                              33
     C                   MOVE      '1'           YTNR
     C                   MOVE      FICURR        VALKOD
     C                   MOVE      FASK          XSK               1
     C                   MOVE      YTNR          PKTNR
     C                   MOVE      FAVK          YGEBY
     C                   MOVE      *BLANKS       PRKOLL
     C*
     C* S E L G E R             F A K T U R E R I N G
     C*
     C     HEKDFS        COMP      'X'                                    33
     C  N33XSK           COMP      'K'                                    33
     C   33              GOTO      VMOTT
     C                   MOVE      *ZEROS        XPROS            10 3
     C                   MOVE      HEFBV         YINTR
     C                   MOVE      *BLANKS       YAVTT
     C                   MOVE      *ZEROS        YINT
     C*
     C*    H A R  S E L G E R  A V T A L E
     C*
     C     HEKDFS        IFEQ      ' '
     C                   MOVE      HEKNSF        YKNR
     C                   MOVE      HEANS         YAVTN
     C                   ELSE
     C     HEKDFS        IFEQ      'B'
     C                   MOVE      HEANK         YAVTN
     C     HEKDFK        IFEQ      'E'
     C                   MOVE      HEKNK         YKNR
     C                   ELSE
     C                   MOVE      HEKNKF        YKNR
     C                   END
     C                   ELSE
     C     HEKDFS        IFEQ      'E'
     C                   MOVE      HEKNS         YKNR
     C                   MOVE      HEANS         YAVTN
     C                   END
     C                   END
     C                   END
     C*
     C                   EXSR      AVTSR
     C*
     C* SKJEKKER BRUTTOTARIFF   FOR SELGER
     C*
     C   33              MOVE      'S'           YAVTN
     C   33              EXSR      BRUTAR
     C                   GOTO      UTLI
     C*
     C* K J Ø P E R    F A K T U R E R I N G
     C*
     C     VMOTT         TAG
     C     HEKDFK        COMP      'X'                                    33
     C   33              GOTO      UTLI
     C                   MOVE      *ZEROS        XPROS
     C                   MOVE      HEFBV         YINTR
     C                   MOVE      *BLANKS       YAVTT
     C                   MOVE      *ZEROS        YINT
     C*
     C*       S K J E K K E R  K J Ø P E R S  A  V T A L E
     C*
     C     HEKDFK        IFEQ      ' '
     C                   MOVE      HEKNKF        YKNR
     C                   MOVE      HEANK         YAVTN
     C                   ELSE
     C     HEKDFK        IFEQ      'B'
     C                   MOVE      HEANS         YAVTN
     C     HEKDFS        IFEQ      'E'
     C                   MOVE      HEKNS         YKNR
     C                   ELSE
     C                   MOVE      HEKNSF        YKNR
     C                   END
     C                   ELSE
     C     HEKDFK        IFEQ      'E'
     C                   MOVE      HEKNK         YKNR
     C                   MOVE      HEANK         YAVTN
     C                   END
     C                   END
     C                   END
     C*
     C                   EXSR      AVTSR
     C*
     C* SKJEKKER BRUTTOTARIFF KJØPER
     C*
     C   33              MOVE      'K'           YAVTN
     C   33              EXSR      BRUTAR
     C*
     C     UTLI          ENDSR
     C*
     C******************************************
     C     BRUTAR        BEGSR
     C******************************************
     C*
     C* H E N T E R   F R A   B R U T T O T A R I F F
     C*
     C                   SETOFF                                       34
     C                   MOVE      '    ALLE'    YKNR
     C                   MOVE      HEFR          YFRAN
     C                   MOVE      HEOT          YOPDT
     C                   MOVE      *BLANKS       YAVTT
     C                   MOVE      *ZEROS        YINT
     C     PRIK1K        SETLL     PRIK1
     C                   READ      PRIK1                                  34
     C  N34YFRAN         COMP      PKFR01                             3434
     C  N34YOPDT         COMP      PKOT01                             3434
     C  N34YKEY2         COMP      PKKEY2                             3434
     C  N34              MOVEL     PKTNR         YTNR
     C  N34              EXSR      FBELØ
     C*
     C                   ENDSR
     C*
     C******************************************
     C     AVTSR         BEGSR
     C******************************************
     C                   MOVE      HEFR          YFRAN
     C                   MOVE      HEOT          YOPDT
     C                   EXSR      BFAKT
     C  N33              EXSR      FBELØ2
     C*
     C                   ENDSR
     C*
     C******************************************
     C     BFAKT         BEGSR
     C******************************************
     C*
     C*  Tests that there is at least one valid price line in the price
     C*  agreement system (for a client (AVTSR)
     C*  33 OFF after this subroutine - at least one valid line found.
     C* ANNEN ENHET
     C*
     C                   Z-ADD     HENTF         YINT
     C                   SETOFF                                       33
     C* Find first priceline- assumes that ther is only one line
     C* on this key (No testing on outdated lines,includ/omit etc.)
     C     PRIKK         SETLL     PRIK
     C                   READ      PRIK                                   33
     C**
     C  N33YKEY2         COMP      PKKEY2                             3333
      * DATE LIMIT ?
     C  N33PKDT          IFNE      *ZEROS
     C     HEDTOP        COMP      PKDT                               33
     C                   END
      * SOURCE TYPE
     C  N33PKURIO        IFEQ      'I'
     C     HEUR          COMP      PKUR01                             3333
     C   33HEUR          COMP      PKUR02                             3333
     C   33HEUR          COMP      PKUR03                             3333
     C   33HEUR          COMP      PKUR04                             3333
     C   33HEUR          COMP      PKUR05                             3333
     C                   ELSE
     C     PKURIO        IFEQ      'O'
     C     HEUR          COMP      PKUR01                                 33
     C  N33HEUR          COMP      PKUR02                                 33
     C  N33HEUR          COMP      PKUR03                                 33
     C  N33HEUR          COMP      PKUR04                                 33
     C  N33HEUR          COMP      PKUR05                                 33
     C                   END
     C                   END
      * FRANKATUR
     C  N33PKFRIO        IFEQ      'I'
     C     HEFR          COMP      PKFR01                             3333
     C   33HEFR          COMP      PKFR02                             3333
     C                   ELSE
     C     PKFRIO        IFEQ      'O'
     C     HEFR          COMP      PKFR01                                 33
     C  N33HEFR          COMP      PKFR02                                 33
     C                   END
     C                   END
      * OPPDRAGSTYPE
     C  N33PKOTIO        IFEQ      'I'
     C     HEOT          COMP      PKOT01                             3333
     C   33HEOT          COMP      PKOT02                             3333
     C                   ELSE
     C     PKOTIO        IFEQ      'O'
     C     HEOT          COMP      PKOT01                                 33
     C  N33HEOT          COMP      PKOT02                                 33
     C                   END
     C                   END
      * LANDKODE
     C  N33PKLKIO        IFEQ      'I'
     C     HELKA         COMP      PKLK01                             3333
     C                   ELSE
     C     PKLKIO        IFEQ      'O'
     C     HELKA         COMP      PKLK01                                 33
     C                   END
     C                   END
     C  N33              MOVEL     PKTNR         YTNR
     C*
     C                   ENDSR
     C*
     C******************************************
     C     FBELØ         BEGSR
     C******************************************
     C*
     C* HENTER DATA FRA BRUTTO TARIFF
     C* PRIS2K ER LEST
     C* ANNEN ENHET
     C*
     C                   Z-ADD     HENTF         YINTR
     C                   Z-ADD     HENTF         HENTFX
     C                   SETOFF                                       34
     C                   MOVE      FAVK          YVK
     C*
     C* HENTER BRUTTO GEBYR
     C*
     C     PRIKEY        SETLL     PRIS
     C                   READ      PRIS                                   34
     C  N34YKEY          COMP      PRKEY                              3434
     C  N34PRTEGN        IFEQ      ' '
     C     PRJUST        ANDNE     *ZEROS
     C                   SUB       PRJUST        HENTFX            4 0
     C                   END
     C  N34HENTFX        COMP      0                                    3434
     C*
     C* HENTER BRUTTO FRAKT
     C*
     C  N34              EXSR      SRTAB
     C*
     C                   ENDSR
     C*
     C******************************************
     C     FBELØ2        BEGSR
     C******************************************
     C*
     C* KALLES HVIS KUNDEN HAR AVTALE
     C*
     C*          PKGEBY    COMP 'FRA'                    50
     C                   MOVE      *ZEROS        XPROS
     C     PKAVTT        COMP      'P'                                    34
     C  N34PKAVTT        COMP      'X'                                    34
     C   34              MOVE      PKBELØ        XPROS
     C  N34PKAVTT        COMP      'B'                                    34
     C   34              EXSR      FBELØ
     C     PKAVTT        COMP      'B'                                    34
     C  N34PKAVTT        COMP      'P'                                    34
     C  N34PKAVTT        COMP      'X'                                    34
     C   34              GOTO      UTSTP2
     C*
     C* N Ø K K E L   E R   N Å   T = E G E N   A V T A L E - T A B E L L
     C*
     C                   MOVE      PKGEBY        YGEBY
     C                   MOVE      PKAVTT        YAVTT
     C*
     C* ANNEN ENHET
     C*
     C                   Z-ADD     HENTF         YINT
     C                   Z-ADD     HENTF         HENTFX
     C*
     C* SKAL IKKE HA GEBYR VED X NÅR BRUTTOAVTALE BENYTTES
     C* ELLER VED Y NÅR KALLES FRA REG LINJE
     C*
     C     PKAVTT        COMP      'T'                                3434
     C   34PKAVTT        COMP      'Y'                                3434
     C   34              GOTO      UTSTP2
     C*
     C                   SETOFF                                       34
     C     PRIKK         SETLL     PRIK
     C                   READ      PRIK                                   34
     C*
     C  N34YKEY1         COMP      PKKEY1                             3434
     C  N34PKTEGN        IFEQ      ' '
     C     PKJUST        ANDNE     *ZEROS
     C                   SUB       PKJUST        HENTFX
     C                   END
     C  N34HENTFX        COMP      0                                    3434
     C   34              GOTO      UTSTP2
     C*
     C                   MOVE      PKBELØ        PRBELØ
     C                   MOVE      PKTEGN        PRTEGN
     C                   MOVE      PKJUST        PRJUST
     C                   EXSR      SRTAB
     C*
     C     UTSTP2        ENDSR
     C*
     C******************************************
     C     SRTAB         BEGSR
     C******************************************
     C*
     C* PRIS PR LINJE
     C*
     C                   MULT      HENTFX        PRBELØ
     C*
     C     PRTEGN        IFNE      ' '
     C                   EXSR      OMREGN
     C                   END
     C*
     C     XPROS         COMP      0                                  35
     C   35PRBELØ        DIV       100           XWORK            15 4
     C   35XWORK         MULT      XPROS         PRBELØ
     C   35              MOVE      '000'         PRBELØ
     C*
     C                   Z-ADD(H)  PRBELØ        FABELN
     C                   Z-ADD(H)  PRBELØ        FABELV
     C                   EXSR      VALUTA
     C*
     C                   ENDSR
     C*
     C******************************************
     C     VALUTA        BEGSR
     C******************************************
     C*
     C* OMREGNER VALUTABELØP
     C*
     C                   SETOFF                                       8586
     C     XSK           IFNE      'I'
     C     XSK           COMP      'S'                                868685
     C*
     C   85HEVALS        COMP      '   '                                  84
     C   85
     CANN84HEVALS        COMP      FICURR                                 84
     C   86HEVALK        COMP      '   '                                  84
     C   86
     CANN84HEVALK        COMP      FICURR                                 84
     C   84              GOTO      UTVAL
     C*
     C   85VALKOD        COMP      HEVALS                                 84
     C   86VALKOD        COMP      HEVALK                                 84
     C   84              GOTO      UTVAL
     C   85              MOVE      HEVALS        VALKOD
     C   86              MOVE      HEVALK        VALKOD
     C                   END
     C*
     C     VALU1K        CHAIN     VALUL01                            84
     C  N84VALKU2        IFNE      0
     C                   Z-ADD     VALKU2        VALKU1
     C                   END
     C  N84FABELN        MULT      OMRFAK        XWORK            15 4
     C  N84XWORK         DIV(H)    VALKU1        FABELV
     C   85              MOVE      HEVALS        VALKOD
     C   86              MOVE      HEVALK        VALKOD
     C     UTVAL         TAG
     C                   SETOFF                                       848586
     C     FABELN        IFGT      0
     C                   UPDATE    FAKTR
     C                   ELSE
     C                   DELETE    FAKTR
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     OMREGN        BEGSR
     C******************************************
     C     PRTEGN        COMP      '-'                                    36
     C   36              SUB       PRJUST        PRBELØ
     C  N36              ADD       PRJUST        PRBELØ
     C*
     C                   ENDSR
     C*
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     FAK4K         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    YSK
     C     KODTVK        KLIST
     C                   KFLD                    YVUNI
     C                   KFLD                    UAVD
     C                   MOVE      'V'           YVUNI             1
     C     VALU1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    VALKOD
     C     PRIKK         KLIST
     C                   KFLD                    YKNR
     C                   KFLD                    YAVTN
     C                   KFLD                    YGEBY
     C                   KFLD                    YAVTT
     C                   KFLD                    YINT
     C                   KFLD                    YLNR
     C     PRIK1K        KLIST
     C                   KFLD                    YKNR
     C                   KFLD                    YFRAN
     C                   KFLD                    YOPDT
     C                   KFLD                    YAVTN
     C                   KFLD                    YGEBY
     C                   KFLD                    YLNR
     C     PRIKEY        KLIST
     C                   KFLD                    YTNR
     C                   KFLD                    YVK
     C                   KFLD                    YINTR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
      *
     C                   MOVE      ' '           YSK               1
     C                   MOVE      *ZEROS        YKNR              8
     C                   MOVE      *BLANKS       YFRAN             3
     C                   MOVE      *BLANKS       YOPDT             2
     C                   MOVE      *BLANKS       YAVTN             1
     C                   MOVE      *BLANKS       YGEBY             3
     C                   MOVE      *BLANKS       YAVTT             1
     C                   MOVE      *ZEROS        YINT              5 0
     C                   MOVE      *BLANKS       YTNR              1
     C                   MOVE      *BLANKS       YVK               3
     C                   MOVE      *ZEROS        YINTR             5 0
     C                   MOVE      *ZEROS        YLNR              4 0
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
