********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 03 TH  PTF8 ny helpfunksjon
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
S03  A*                                     HLPDOC('SAD02801' 'SAD028' 'SY400-
S03  A*                                     LO/SADTVINN')
S03  A*                                     HELP
S03  A*                                     ALTHELP
N03  A                                      HELP(01)
N03  A                                      CF01(01)
      *
     A          R BILDE01
     A                                  2  2'SYSPED/SAD028'
     A                                      COLOR(BLU)
     A                                  2 25'Utskrift av spedisjonsjournal'
     A                                      COLOR(WHT)
     A                                  3 33'E K S P O R T'
     A                                      COLOR(WHT)
     A                                  9 25'Sorteringsmåte:'
     A            WSVALG         1D 0B  9 48DSPATR(UL)
     A                                      VALUES(0 1 2)
     A  99                                  ERRMSG('Du må velge en av de to sor-
     A                                      teringsmåte.' 99)
     A                                 10 25'1=Sorter på dato'
     A                                      COLOR(BLU)
     A                                 11 25'2=Sorter på godsnr.'
     A                                      COLOR(BLU)
     A                                 23 25'F3 = Avslutt'
     A                                      COLOR(BLU)
      *
     A          R BILDE02
     A                                      CF12(12 'Gå til sorteringsmåtevalgs-
     A                                       bilde')
     A                                  2  2'SYSPED/SAD028'
     A                                      COLOR(BLU)
     A                                  2 26'Utskrift av spedisjonsjournal'
     A                                      COLOR(WHT)
     A                                  3 34'E K S P O R T'
     A                                      COLOR(WHT)
     A                                  8 16'Journal fra dato:'
     A            WSDTF          6D 0B  8 37DSPATR(UL)
     A  99                                  ERRMSG('Fra dato kan ikke være stør-
     A                                      re enn til dato.' 99)
     A                                 10 16'Journal til dato:'
     A            WSDTT          6D 0B 10 37DSPATR(UL)
     A                                 12 16'Kontantkunder:'
     A            WSKKUN         1A  B 12 37DSPATR(UL)
     A                                      VALUES(' ' 'K')
     A                                 12 44'(Her tastes K hvis kun kontantkund-
     A                                      er)'
     A                                      COLOR(BLU)
     A                                 14 16'Kun utskrevene:'
     A            WSKUTS         1A  B 14 37DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A                                 14 44'(Her tastes X hvis kun utskrevne S-
     A                                      AD)'
     A                                      COLOR(BLU)
     A                                 16 16'Fra avdeling:'
     A            WSAVDF         4Y 0B 16 37DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  ERRMSG('Fra avdeling kan ikke være -
     A                                      større enn til avdeling' 98)
     A                                 18 16'Til avdeling:'
     A            WSAVDT         4Y 0B 18 37DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 22 26'F3 = Avslutt'
     A                                      COLOR(BLU)
     A                                 22 40'F12 = Forrige'
     A                                      COLOR(BLU)
      *
     A          R BILDE03
     A                                      CF12(12 'Gå til sorteringsmåtevalgs-
     A                                       bilde')
     A                                  2  2'SYSPED/SAD028'
     A                                      COLOR(BLU)
     A                                  2 26'Utskrift av spedisjonsjournal'
     A                                      COLOR(WHT)
     A                                  3 33'E K S P O R T'
     A                                      COLOR(WHT)
     A                                  8 16'Journal fra godsnr.'
     A            WSGNF         15A  B  8 37DSPATR(UL)
     A  99                                  ERRMSG('Fra godsnr kan ikke større -
     A                                      enn til godsnr.' 99)
     A                                 10 16'Journal til godsnr.'
     A            WSGNT         15A  B 10 37DSPATR(UL)
     A                                 12 16'Kontantkunder:'
     A            WSKKUN         1A  B 12 37DSPATR(UL)
     A                                      VALUES(' ' 'K')
     A                                 12 44'(Her tastes K hvis kun kontantkund-
     A                                      er)'
     A                                      COLOR(BLU)
     A                                 14 16'Kun utskrevene:'
     A            WSKUTS         1A  B 14 37DSPATR(UL)
     A                                      VALUES(' ' 'X')
     A                                 14 44'(Her tastes X hvis kun utskrevne S-
     A                                      AD)'
     A                                      COLOR(BLU)
     A                                 16 16'Fra avdeling:'
     A            WSAVDF         4Y 0B 16 37DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  ERRMSG('Fra avdeling kan ikke være -
     A                                      større enn til avdeling' 98)
     A                                 18 16'Til avdeling:'
     A            WSAVDT         4Y 0B 18 37DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 22 26'F3 = Avslutt'
     A                                      COLOR(BLU)
     A                                 22 40'F12 = Forrige'
     A                                      COLOR(BLU)
