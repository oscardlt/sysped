********************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P504XA* 02 CB  PTF5 VEDLIKEHOLD KONTAKTPERSONER
PXXXXA* 03 YBC PTF5 VEDLIKEHOLD AV MILJØAVGIFT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLKUN' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF04(04 'Søk kunder')
     A                                      CF06(06 'LAG NY')
     A                                      CF03(03 'Avslutt')
     A                                      CF10(10 'SEND')
     A            MSGNR          7A  P
     A                                  1  4'SYSPED/SYIM11'
     A                                      COLOR(BLU)
     A                                  1 19'Sende ufortollede      på mail'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 62TIME
     A                                      COLOR(BLU)
     A                                  8  4'Fra godsnummer.....:'
     A            WSGNFA         4A  B  8 26
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A            WSGNFB         5A  B  8 31
     A N30                                  DSPATR(PC)
     A            WSGNFC         3A  B  8 37
     A            WSGNFD         3A  B  8 41
     A                                 10  4'Til godsnummer.....:'
     A            WSGNTA         4A  B 10 26
     A            WSGNTB         5A  B 10 31
     A            WSGNTC         3A  B 10 37
     A            WSGNTD         3A  B 10 41
     A                                 12  4'Epost:'
     A            EPOST         70A  B 12 11CHECK(LC)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A                                 15  4MSGCON(021 SHU1316 *LIBL/SYMSGF)
     A            INKUTE         1A  B 15 26VALUES('U' 'I' ' ')
     A                                 15 28MSGCON(010 SHU1317 *LIBL/SYMSGF)
     A            ZOT01          2A  B 15 40
     A            ZOT02          2A  B 15 43
     A            ZOT03          2A  B 15 46
     A            ZOT04          2A  B 15 49
     A            ZOT05          2A  B 15 52
     A            ZOT06          2A  B 15 55
     A            ZOT07          2A  B 15 58
     A            ZOT08          2A  B 15 61
     A            ZOT09          2A  B 15 64
     A            ZOT10          2A  B 15 67
     A            ZOT11          2A  B 15 70
     A            ZOT12          2A  B 15 73
     A            ZOT13          2A  B 15 76
     A            ZOT14          2A  B 15 79
     A                                 17  4'Kun kundenummer.....:'
     A            WSKUND         8Y 0B 17 26EDTCDE(4)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                 19  4'Printer v/ Utskrift.:'
     A            WSPRIN        10A  B 19 26
     A            WSSDV          1A  B 21 26VALUES('J' 'N' ' ')
     A                                 21 31MSGCON(005 TBR0254 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40'F10=sende mail/starte utskrift'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                 21  4MSGCON(021 SHU1318 *LIBL/SYMSGF)
     A                                 21 39MSGCON(015 SHU1319 *LIBL/SYMSGF)
