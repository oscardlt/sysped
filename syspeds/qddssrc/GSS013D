      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8XXX * 01 YBC PTF8 FIRE NYE FRITEKST LINJER
P8XXX * 02 YBC PTF8 LANDKODE
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 13 60 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  5  2VALUES(' ' '2' '4' '5' 'B')
     A            AKVAL          3A  H
     A            AKLNO          3S 0H
     A            AKANR         15A  O  5  6
     A            AKCONO        20A  O  5 22
     A            AKSTK          5Y 0O  5 43EDTCDE(4)
     A            AKSTKM         5Y 0O  5 49EDTCDE(4)
     A            AKPR          11S 2H
     A            AKTXT         30A  H
     A            AKTXT2        30A  H
     A            AKTXT3        30A  H
     A            AKTXT4        30A  H
     A            AKTXT5        30A  H
     A            AKLK           2A  H
     A            AKONOR         7S 0H
     A            AKEDI          1   B  5  4
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  *DS3                                SFLSIZ(0099)
     A  *DS4                                SFLSIZ(0099)
     A  *DS3                                SFLPAG(0008)
     A  *DS4                                SFLPAG(0008)
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'GSS013R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(019 DIV2566 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 DIV2567 *LIBL/SYMSGF)
     A            UONO           7Y 0O  2 12EDTCDE(4)
     A            ZRECNO         4S 0O  2 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(050 DIV2568 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(051 DIV2569 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(13)
     A                                      CLRL(01)
     A                                      OVERLAY
     A  30        MSGTXT        33A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1  2MSGCON(031 DIV2570 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CA05(05 'Frisk')
     A N27                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'GSS013R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(019 DIV2566 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2'Ordrenr.'
     A            UONO           7Y 0O  2 12EDTCDE(4)
     A                                  4  2'Op Artikkelnr      P.O.nr         -
     A                                            F.stk M.stk'
     A                                      COLOR(WHT)
     A                                 13  3MSGCON(031 DIV2570 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BILDE01
     A  *DS3                                WINDOW(*DFT 13 50 *NOMSGLIN)
     A  *DS4                                WINDOW(*DFT 13 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'GSS013R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(019 DIV2566 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 DIV2567 *LIBL/SYMSGF)
     A            UONO           7Y 0O  2 12EDTCDE(4)
     A                                  2 22MSGCON(008 DIV2571 *LIBL/SYMSGF)
     A            AKLNO          3Y 0O  2 32EDTCDE(4)
     A                                  3  2MSGCON(007 DIV2572 *LIBL/SYMSGF)
     A            WSANR         15A  B  3 12DSPATR(UL)
     A  28                                  DSPATR(PR)
     A N30N28
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A                                  3 32'LK:'
     A            WSLK           2A  B  3 36DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A                                  4  2MSGCON(007 DIV2573 *LIBL/SYMSGF)
     A            WSCONO        20A  B  4 12DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                  5  2MSGCON(009 DIV2574 *LIBL/SYMSGF)
     A            WSSTK          5Y 0B  5 12DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(009 DIV2575 *LIBL/SYMSGF)
     A            WSSTKM         5Y 0B  6 12DSPATR(UL)
     A N30 28
     AO 87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(005 DIV2576 *LIBL/SYMSGF)
     A            WSVAL          3   B  7 12
     A            WSPR          11Y 2B  7 16DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(009 DIV2577 *LIBL/SYMSGF)
     A            WSTXT         30A  B  8 12DSPATR(UL)
     A            WSTXT2        30A  B  9 12DSPATR(UL)
     A            WSTXT3        30A  B 10 12DSPATR(UL)
     A            WSTXT4        30A  B 11 12DSPATR(UL)
     A            WSTXT5        30A  B 12 12DSPATR(UL)
     A N30                             13  2'F4=Søk F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        48A  O 13  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A  *DS3                                WINDOW(*DFT 13 50 *NOMSGLIN)
     A  *DS4                                WINDOW(*DFT 13 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                  1  2'GSS013R'
     A                                      COLOR(BLU)
     A                                  1 10'Arbeid med artikkel'
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 DIV2567 *LIBL/SYMSGF)
     A            UONO           7Y 0O  2 12EDTCDE(4)
     A                                  2 20MSGCON(008 DIV2569 *LIBL/SYMSGF)
     A            AKLNO          3Y 0O  2 29EDTCDE(4)
     A                                  2 33'R.ordre:'
     A            AKONOR         7Y 0O  2 42EDTCDE(4)
     A                                  3  2MSGCON(007 DIV2572 *LIBL/SYMSGF)
     A            AKANR         15A  O  3 12DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                  3 32'LK:'
     A            AKLK           2A  B  3 36DSPATR(UL)
     A                                      DSPATR(RI)
     A                                  4  2MSGCON(007 DIV2573 *LIBL/SYMSGF)
     A            AKCONO        20A  O  4 12DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(009 DIV2574 *LIBL/SYMSGF)
     A            AKSTK          5Y 0O  5 12DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  6  2'Mott.stk:'
     A            AKSTKM         5Y 0O  6 12DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(005 DIV2576 *LIBL/SYMSGF)
     A            AKPR          11Y 2O  7 16DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(009 DIV2577 *LIBL/SYMSGF)
     A            AKTXT         30A  O  8 12DSPATR(UL)
     A            AKTXT2        30A  O  9 12DSPATR(UL)
     A            AKTXT3        30A  O 10 12DSPATR(UL)
     A            AKTXT4        30A  O 11 12DSPATR(UL)
     A            AKTXT5        30A  O 12 12DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      COLOR(GRN)
     A                                 13  2MSGCON(010 DIV2578 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            AKVAL          3   O  7 12
      *
     A          R BILDE03
     A                                      WINDOW(*DFT 5 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Following P.O.no already'
     A                                      COLOR(WHT)
     A                                  2  2'registered'
     A                                      COLOR(WHT)
     A N50                              2 13'(partial)'
     A                                      COLOR(WHT)
     A  50                              2 13'(complete)'
     A                                      COLOR(WHT)
     A            WSCONO        20A  O  3  2DSPATR(UL)
     A                                      COLOR(RED)
     A            ORONO          7Y 0O  3 23DSPATR(UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A                                  4  2'Keep P.O.no?'
     A            WSOK           1A  B  4 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      VALUES('Y' 'N')
     A                                  4 21'(Y/N)'
