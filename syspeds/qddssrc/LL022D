********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXX * 01 YBC PTF5 BATCHNR, UTLØPSDATO, ...
P5127 * 02 YBC PTF5 FORHOLDSTALL OG ENHETSVEKT, ENHETSKUBIKK
P6XXX * 03 YBC PTF6 SALDO ER ØKT FRA 5.0 TIL 7.0
P7113 * 04 YBC PTF7 NYE FELTER FOR PALL-ID
P8XXX * 05 YBC PTF8 VIS GODSNR
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S01  A*                                     WINDOW(*DFT 10 54 *NOMSGLIN)
E03  A                                      WINDOW(*DFT 15 69 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
E03  A            WSWNT0         7  0B  5  5EDTCDE(4)
     A  31                                  COLOR(WHT)
E03  A            WSWNTL         7  0H
     A            WSWNT1         5  0H
     A            WSWNT2         5  0H
     A            WSWNT3         5  0H
     A            VAMN           7  0H
E01  A            VALLOK        25A  H
N01  A            VASENR        25A  H
N04  A            VAPLID        20A  H
N02  A            VAAN01         5  0H
N02  A            VAAN02         5  0H
N02  A            VAAN12         5  0H
N02  A            VAAN03         5  0H
N02  A            VAAN23         5  0H
N02  A            VAMIN          1  0H
E03  A            XXTXT         25A  O  5 13
N01  A  42                                  COLOR(YLW)
E03  A            VADATO         8  0O  5 39EDTWRD('    .  .  ')
E03  A            VAULDT         8  0O  5 50EDTWRD('    .  .  ')
E03  A            VASALD         7  0O  5 61EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
N01  A                                      CA07(07 'Re-allokkering')
N01  A                                      CA10(10 'Modus')
     A                                      CA12(12 'Forrige')
     A                                      CF23(23 'Bekreft')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'LL022R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(40 YBC1273 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(40 YBC1274 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(10 YBC1275 SYMSGF)
E03  A            UWNT0          7  0O  3 13DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
E03  A                                  3 21MSGCON(09 YBC1276 SYMSGF)
E03  A            XWNT0A         7  0O  3 31DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
E03  A                                  3 39MSGCON(06 YBC1277 SYMSGF)
E03  A            XWNT0D         7  0O  3 46DSPATR(UL)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
S01  A*                                 4  2'Op A.stk Lagerlokasjon            -
S01  A*                                      Innl.dato  Saldo'
E04  A N41N40                           4  2MSGCON(67 YBC1278 SYMSGF)
N01  A                                      COLOR(WHT)
E04  A  41N40                           4  2MSGCON(67 YBC1279 SYMSGF)
     A                                      COLOR(WHT)
N04  A  40                              4  2MSGCON(67 YBC1888 SYMSGF)
N04  A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
S01  A*N30                              1  2'F3/F12=Avbryt   F5=Forny   F7=Modu-
S01  A*                                     s   F23=Bekreft'
S04  A*N30N41                           1  2MSGCON(63 YBC1280 SYMSGF)
N04  A N30N41N40                        1  2MSGCON(63 YBC1280 SYMSGF)
N01  A                                      COLOR(BLU)
S04  A*N30 41N40                        1  2MSGCON(63 YBC1281 SYMSGF)
E04  A N30 40                           1  2MSGCON(63 YBC1281 SYMSGF)
     A                                      COLOR(BLU)
N04  A N30 41N40                        1  2MSGCON(63 YBC1887 SYMSGF)
N04  A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
