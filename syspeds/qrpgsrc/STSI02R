      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSI02R  MODULE(*LIBL/STSI02R *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FHeadf     if   e           k disk    usropn
     FSTSCLI    if   E           K DISK    usropn
     FSTSFBR    if   E           K DISK    usropn
     FSTSOPD    if   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D Ytype           s              5
     D pallv           s              1
     D TurNr           s              8
     D avd             s              4
     D opd             s              7
     D WSPRO           s              8  0
     D UserId          s             10
     D User            s             10
     D VisAlle         s              1
     D AlleTxt         s              7
     D NyAlle          s              1
     D Label           s              1
     D FEILKOD         s              1
     D MELDING         s             50
      * andre variabler
     D AntCli          s              5  0
     D Antmank         s              5  0
     D AntFbr          s              5  0
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  HXSNDN                 1     20
     D  Sndnr                  1     20
     D                 DS
     D  DSAvOp                 1     12
     D  Heavd                  1      4  0
     D  Strek                  5      5
     D  Heopd                  6     12  0
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      YTYPE  = zhbgetvar('YTYPE')
     C                   eval      sctype = zhbgetvar('YTYPE')
     C                   eval      PALLV  = zhbgetvar('PALLV')
     C                   eval      TURNR  = zhbgetvar('TURNR')
     C                   eval      WSPRO    = c2n2(TURNR)
     C                   eval      AVD    = zhbgetvar('AVD')
     C                   eval      scavd  = c2n2(AVD)
     C                   eval      OPD    = zhbgetvar('OPD')
     C                   eval      scopd  = c2n2(OPD)
     C                   eval      VisAlle = zhbgetvar('Alle')
     C                   eval      userid = zhbgetvar('user')
     c                   eval      Label  = zhbgetvar('LabeL')
      * Set libl/open files
     C                   exsr      Init
      * Prt Labels - Refresh (delete / retrieve cll-IDs)
     c     Label         ifeq      'J'
     c                   call      'STSI02RB'
     c                   parm                    BIFIRM            2
     c                   parm                    BIBRID           10
     c                   parm                    YTYPE
     c                   parm                    WSPRO
     c                   parm                    SCAVD
     c                   parm                    SCOPD
     c                   parm                    NULLFBR           3 0
     c                   parm                    UUMSG            50
     c                   eval      MELDING = UUMSG
     c     Melding       ifne      *blanks
     c                   move      'X'           FEILKOD
     c                   end
     c                   end
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Hent/vis kolli-id
     C                   exsr      HCLI
      *
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     headf
     C                   close     STSCLI
     C                   close     STSFBR
     C                   close     STSOPD
     C                   eval      *inlr = *on
     C                   RETURN
      *
     C***********************************************
     C     HCLI          BEGSR
     C***********************************************
      * Repeat UserId for the next invocation
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':userid)
     C                   callp     updhtmlvar('YTYPE':YTYPE)
     C                   callp     updhtmlvar('TURNR':turnr)
     C                   callp     updhtmlvar('ALLE':VisAlle)
     c     VisAlle       ifeq      'J'
     c                   eval      AlleTxt = 'KunÅpne'
     c                   eval      NyAlle  = ' '
     c                   else
     c                   eval      AlleTxt = 'VisAlle'
     c                   eval      NyAlle  = 'J'
     c                   end
     C                   callp     updhtmlvar('ALLETXT':AlleTxt)
     C                   callp     updhtmlvar('NYALLE':NyAlle)
     C     CliKey        chain     StsOpd
     C     HEAKey        chain     headf
     C                   callp     updhtmlvar('ANTFBT':' ')
     C                   callp     updhtmlvar('ANTFBR':' ')
     c                   move      *zeros        antfbr
     C     Heur          ifne      'H'
     c     Sndnr         oreq      *blanks
     C                   movel     DSAVOP        sndnr
      * vis ant fbr.
     C     cliKey        SETLL     STSFBR
     C     cliKey        READE     STSFBR                                 94
     C     *IN94         DOWEQ     '0'
     C                   add       1             antfbr
     C     cliKey        READE     STSFBR                                 94
     c                   end
     C                   callp     updhtmlvar('ANTFBT':'Ant Fb:')
     C                   callp     updhtmlvar('ANTFBR':
     C                             %trim(%editc(antfbr:'3')))
     c                   end
     C                   callp     updhtmlvar('SNDNR':sndnr)
     C                   callp     updhtmlvar('AVD':
     C                             %trim(%editc(SCAVD:'3')))
     C                   callp     updhtmlvar('OPD':
     C                             %trim(%editc(SCOPD:'3')))
     C                   callp     updhtmlvar('AVS':HENAS)
     C                   callp     updhtmlvar('MOT':HENAK)
     C                   callp     updhtmlvar('MOTPNR':HEADK3)
     C                   callp     updhtmlvar('PALLV':PALLV)
     C                   callp     updhtmlvar('ANT':
     C                             %trim(%editc(HENT:'3')))
      *
     C                   callp     wrtsection('top')
      *
     c                   move      *zeros        antcli
     c                   move      *zeros        antmank
     C     cliKey        SETLL     STSCLI
     C     cliKey        READE     STSCLI                                 94
     C     *IN94         DOWEQ     '0'
     C                   add       1             antcli
     C     VisAlle       Ifne      'J'
     C     SCSTAT        Andne     ' '
     C                   goto      NEXT
     C                   end
     C     SCSTAT        IFEQ      ' '
     C                   add       1             antmank
     C                   end
     C                   callp     updhtmlvar('ANTN':
     C                             %trim(%editc(Antcli:'3')))
     C                   callp     updhtmlvar('CLID':SCCLID)
     C     scstat        ifeq      *blanks
     C                   callp     updhtmlvar('STAT':spaces)
     C                   else
     C                   callp     updhtmlvar('STAT':SCSTAT)
     C                   end
      * Vis overskriftslinje hvis kolli-id finnes...
     C     RowVist       ifeq      ' '
     C                   callp     wrtsection('tablehead')
     C                   move      'X'           RowVist           1
     C                   end
     C                   callp     wrtsection('tablerow')
     C     Next          tag
     C     cliKey        READE     STSCLI                                 94
     C  N94              ENDDO
      * Hvis IKKE kollimerket gods - tilby Button for å Kvittere manuelt
     C     Antcli        IFEQ      *ZEROS
     C     sostat        ifeq      *blanks
     C                   callp     updhtmlvar('OSTAT':spaces)
     C                   callp     wrtsection('ManStat')
     C                   callp     wrtsection('ManButt')
     C                   else
     C     SOAMKK        ifne      *zeros
     C     HENT          sub       SOAMKK        AntMank
     C                   end
     C                   callp     updhtmlvar('OSTAT':SOSTAT)
     C                   callp     wrtsection('ManStat')
     C                   end
     c                   end
      *
     C                   callp     updhtmlvar('MANTXT':SOMERK)
     C                   callp     updhtmlvar('ANTM':
     C                             %trim(%editc(Antmank:'N')))
     C                   callp     updhtmlvar('MELDING':MELDING)
     C                   callp     updhtmlvar('FEILKOD':FEILKOD)
     C                   callp     wrtsection('tablebot')
      *
     C                   ENDSR
      *
      *
      *=====================================================================******
     C     init          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              30
      *
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      STSCLI
     C                   open      STSFBR
     C                   open      STSOPD
     C                   open      headf
      *
      *  keys mm
     C     CLIKEY        KLIST
     C                   KFLD                    sctype
     C                   KFLD                    wspro
     C                   KFLD                    scavd
     C                   KFLD                    scopd
     C     HEAKEY        KLIST
     C                   KFLD                    scavd
     C                   KFLD                    scopd
     C                   move      *zeros        heavd
     C                   move      '/'           strek
     C                   move      *zeros        heopd
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSI02'
      *------------------
     C                   endsr
