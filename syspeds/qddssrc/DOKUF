********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10005 * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRE FRA 2.0 TIL 4.0
10XXX * 02 YBC PTF10 TILLEGGS PRODUKTKODER (BRING)
********************************************************************
     A*___________________
     A* SYSPED / Fraktbrev
     A*"""""""""""""""""""
     A                                      REF(SR)
     A          R DOKUFR
     A            DFST      R               REFFLD(ST)
     A            DFRI      R               REFFLD(RI)
     A            DFAVD     R               REFFLD(AVD)
     A            DFOPD     R               REFFLD(OPD)
     A            DFFBNR    R               REFFLD(FRBN)
     A            DFSG      R               REFFLD(SG)
     A            DFCMN          1          COLHDG('J=Kommunikasjon')
     A            DFVEN     R               REFFLD(ST)
     A                                      COLHDG('Ventekode')
     A            DF1004    R               REFFLD(N1004)
     A                                      COLHDG('Sendningsnr')
     A            DFDTU     R               REFFLD(DT)
     A                                      COLHDG('R17.Utstedelsesdato')
S01  A*           DFTOLL    R               REFFLD(TS)
S01  A*                                     COLHDG('Tollsted, 00=Fr.brev')
N01  A            DFTOLL         4S 0       COLHDG('Tollsted, 00=Fr.brev')
      * Sender
     A            DFKNSS    R               REFFLD(KN)
     A                                      COLHDG('SYSPED Knr Sender')
     A            DFNAVS        30          COLHDG('R01.Navn Sender')
     A            DFAD1S        30          COLHDG('R02.Adresse Sender')
     A            DFAD2S        30          COLHDG('Adresse2 Sender')
     A            DFPNLS         4  0       COLHDG('Postnr Sender')
     A            DFAD3S        30          COLHDG('Sted Sender')
N03  A            DFLKS          2          COLHDG('Sender Landkode')
N03  A            DFPNUS         9          COLHDG('Sender Pnr utland')
     A            DFKNS     R               REFFLD(KN12A)
     A                                      COLHDG('R15.Kundenr Sender')
     A            DFSREF        17          COLHDG('R16.Senders ref')
     A            DFPNAT         4  0       COLHDG('Postnr avg.terminal')
      * Mottaker
     A            DFKNSM    R               REFFLD(KN)
     A                                      COLHDG('SYSPED Knr Mottaker')
     A            DFNAVM        30          COLHDG('R04.Navn Mottaker')
     A            DFAD1M        30          COLHDG('R05.Adresse Mottaker')
     A            DFAD2M        30          COLHDG('Adresse 2 Mottaker')
     A            DFAD3M        30          COLHDG('R06.Pnr/sted Mottak')
     A            DFLKM          2          COLHDG('Landkode Mottaker')
     A            DFPNUM         9          COLHDG('Pnr utland Mottaker')
     A            DFKNM     R               REFFLD(KN12A)
     A                                      COLHDG('R23.Kundenr Mottaker')
     A            DFMREF        17          COLHDG('R25.Mottakers ref')
      * Lev.adresse
     A            DFNAVL        30          COLHDG('R07.Navn Lev.adr')
     A            DFAD1L        30          COLHDG('R08.Leveringsadresse')
     A            DFAD2L        30          COLHDG('LeveringsAdresse 2')
     A            DFPOUL         4  0       COLHDG('R09.Postnr Utl.sted')
     A            DFADUL        30          COLHDG('R09.Utleveringssted')
     A            DFLKL          2          COLHDG('Lev.adr. Landkode')
     A            DFPNUL         9          COLHDG('Lev.adr. Pnr utland')
      * Evt. annen fakturabetaler
     A            DFKNSX    R               REFFLD(KN)
     A                                      COLHDG('SYSPED Knr Annen F')
     A            DFNAVX        30          COLHDG('R19.Navn Annen F')
     A            DFAD1X        30          COLHDG('R21.Adresse Annen F')
     A            DFAD2X        30          COLHDG('Adresse 2 Annen F')
     A            DFAD3X        30          COLHDG('R22.Pnr/sted Annen')
     A            DFLKX          2          COLHDG('Landkode Annen F')
     A            DFPNUX         9          COLHDG('Pnr utland Annen F')
     A            DFKNX     R               REFFLD(KN12A)
     A                                      COLHDG('R20.kundenr Annen F')
      *
     A            DFOVER        17          COLHDG('R11.Over')
     A            DFTRAN    R               REFFLD(KN)
     A                                      COLHDG('SYSPED Transp.no')
     A            DFNAT     R               REFFLD(NA20)
     A                                      COLHDG('R10.Navn Transportør')
     A            DFPROK         5          COLHDG('R14.Produkt kode')
     A            DFPRO1        30          COLHDG('R14.Prod. tekst 1')
     A            DFPRO2        30          COLHDG('R14.Prod. tekst 2')
     A            DFNTEU    R               REFFLD(NT2)
     A                                      COLHDG('R12.A.EURO-pl utveks')
     A            DFNTAU    R               REFFLD(NT2)
     A                                      COLHDG('R13.A.Andr-pl utveks')
     A            DFBELA         1          COLHDG('R28.Belast S/M/A')
     A            DFKDE     R               REFFLD(KDE)
     A            DFGM      R               REFFLD(GM12)
     A                                      COLHDG('R33.Godsmerke (1/2)')
     A            DFGM2     R               REFFLD(GM12)
     A                                      COLHDG('R33.Godsmerke (2/2)')
     A            DFNT      R               REFFLD(NTS)
     A                                      COLHDG('R34.Ant kolli (sum)')
     A            DFVS      R               REFFLD(VS)
     A                                      COLHDG('R35.Type/Slag (1/2)')
     A            DFVS2     R               REFFLD(VS)
     A                                      COLHDG('R35.Type/Slag (2/2)')
     A            DFVKT     R               REFFLD(VKT9)
     A                                      COLHDG('R36.Brt vekt (sum)')
     A            DFMÅL     R               REFFLD(MÅL)
     A                                      COLHDG('POD:DT(8)TID(4)SG(5)')
     A            DFM3           7P 3       COLHDG('R37.Kub. METER')
     A            DFLM      R               REFFLD(LM)
     A                                      COLHDG('Lastemeter')
     A            DFVKTF    R               REFFLD(VKT9)
     A                                      COLHDG('R39.F.ber.vkt(sum)')
      *
     A            DFKDME    R               REFFLD(KDME)
     A            DFNTLA         4S 0       COLHDG('Ant. merkelapper')
     A            DFFORE         9S 0       COLHDG('Foretaksnr')
     A            DFRG      R               REFFLD(RG)
     A            DFKTA     R               REFFLD(KTA)
     A            DFKTB     R               REFFLD(KTB)
     A            DFST2     R               REFFLD(ST)
     A                                      COLHDG('STATUS FOR SAD')
     A            DFFASE        25          COLHDG('Faktura-sender')
     A            DFFVBL         9  2       COLHDG('R24.Forsikringsverdi')
     A            DFFVCU         3          COLHDG('R24.F.verdi, valuta')
     A            DFFKAT         4          COLHDG('R24.F.verdi,kategori')
     A            DFPOLI        13          COLHDG('R26.Posisenr')
     A            DFSKFO         1          COLHDG('R26.SKAL forsikres')
     A            DFLEVB         3          COLHDG('R27.Lev.betingelse')
     A            DFLEVT        20          COLHDG('R27.Lev.bet.tekst')
     A            DFLEVI        30          COLHDG('R30.Lev.innstruks')
      * Div. felt fra utelands distrib.fraktbrev
     A
     A            DF6060         5  0       COLHDG('F.vekt i retur')
     A            DF500A         5  0       COLHDG('B.frakt i retur')
     A            DF500B         5  0       COLHDG('Rabatt i retur')
     A            DFBELE         9  2       COLHDG('Etterkravsbeløp')
      * ENDRET TEXT TIL KIDNR
     A*           DFFRKU        30          COLHDG('FRITT KUNDESPESIAL')
     A            DFFRKU        30          COLHDG('KID.NUMMER')
     A            DFBREF        17          COLHDG('R18.Booking ref')
N02  A            DFTPK1         5          COLHDG('TILLEGGS PROD.KODE 1')
N02  A            DFTPK2         5          COLHDG('TILLEGGS PROD.KODE 2')
N02  A            DFTPK3         5          COLHDG('TILLEGGS PROD.KODE 3')
N02  A            DFTPK4         5          COLHDG('TILLEGGS PROD.KODE 4')
     A          K DFRI
     A          K DFAVD
     A          K DFOPD
     A          K DFFBNR
