     FEDI66D    CF   E             WORKSTN
     F                                     SFILE(EDI66DA:ZRECNO)
     FEDIM6     IF   E           K DISK
     FEDIS      IF   E           K DISK
     FEUNH      IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    MSN           WSN
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSN
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     WSN           PARM                    WSN
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTA        PLIST
     C                   PARM                    MMN
     C                   PARM                    WUPD              1
     C     *IN03         PARM                    WIN03
      *________________________________
      * HENTE PARAMETERE FOR SENDNINGEN
      *""""""""""""""""""""""""""""""""
     C     WSN           CHAIN     EDIS                               51
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN93
     C                   WRITE     EDI66DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          CHAIN     EDIM6                              94
      *
     C     *IN94         DOWEQ     '0'
     C                   MOVE      '0'           *IN30
     C                   ADD       1             ZRECNO
      *
     C     MMN           CHAIN     EUNH                               51
     C     *IN51         IFEQ      *OFF
     C                   MOVEL     U0068         W0068
     C                   MOVEL     U1004         W1004
     C                   ELSE
     C                   MOVEL     M0068         W0068
     C                   MOVEL     M1004         W1004
     C                   ENDIF
      *
     C                   WRITE     EDI66DA
     C     KEYA          READE     EDIM6                                  94
     C  N94              END
     C     ZRECNO        CABEQ     0             SRB9
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI66DØ
     C                   WRITE     EDI66DC
     C                   EXFMT     EDI66DB
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     EDI66DA                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '5'           SRBE
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele meldingsloggen.                           SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   MOVE      'N'           WUPD
     C                   CALL      'EDI67R'      PLISTA
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI66DA
      *
     C                   ENDSR
