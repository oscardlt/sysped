     FEDIM2     UF   E           K DISK
     FEDIS      UF   E           K DISK
     FSAEF      UF   E           K DISK
     FSAEHD     UF   E           K DISK
     FSAEV      UF   E           K DISK
     FTVINF     UF   E           K DISK
      *
     D                 DS
     D  XDT2                   1      6  0
     D  XD                     1      2  0
     D  XM                     3      4  0
     D  XÅ                     5      6  0
     D                 DS
EV5  D  WDT                    1      8  0
EV5  D  WÅ                     1      4  0
EV5  D  WM                     5      6  0
EV5  D  WD                     7      8  0
      *
     C                   EXSR      INIT
      *
      *__________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SAEHD
      *""""""""""""""""""""""""""""""""""""""""""
     C     SEDT          SETLL     SAEHD
     C                   READ      SAEHD                                  51
     C  N51SEDT          COMP      WDT                                51
     C                   DOW       *IN51 = *OFF
     C                   IF        ((XAVD = 0) OR (SEAVD = XAVD))
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   IF        XDLT = 'J'
     C                   EXSR      SRC
     C                   END
     C                   DELETE    SAEHR
     C                   END
     C                   READ      SAEHD                                  51
     C  N51SEDT          COMP      WDT                                51
     C  N51              END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SAEV
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C     SAEHK         SETLL     SAEV
     C     SAEHK         READE     SAEV                                   52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    SAEVR
     C     SAEHK         READE     SAEV                                   52
     C                   END
     C                   ENDSR
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SAEF
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRB           BEGSR
     C     SAEHK         SETLL     SAEF
     C     SAEHK         READE     SAEF                                   52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    SADFR
     C     SAEHK         READE     SAEF                                   52
     C                   END
     C                   ENDSR
      *_____________________________________________________
      * SETLL, READ AND DELETE RECORDS FROM EDIM2/EDIS/TVINF
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRC           BEGSR
     C     SAEHK         SETLL     EDIM2
     C     SAEHK         READE     EDIM2                                  52
     C                   DOW       *IN52 = *OFF
     C                   IF        M1001 = '830'
     C     MMN           SETLL     TVINF
     C     MMN           READE     TVINF                                  53
     C                   DOW       *IN53 = *OFF
     C                   DELETE    TVINFF
     C     MMN           READE     TVINF                                  53
     C                   END
     C     MSN           CHAIN     EDIS                               53
     C  N53              DELETE    EDISR
     C                   DELETE    EDIMR
     C                   ELSE
     C                   UPDATE    EDIMR
     C                   END
     C     SAEHK         READE     EDIM2                                  52
     C                   END
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SAEHK         KLIST
     C                   KFLD                    SEAVD
     C                   KFLD                    SETDN
      *_______________
      * PARAMETER LIST
      *"""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    XAVD
     C     XDT2          PARM                    XDT
     C                   PARM                    XDLT              1
      *____________
      * WORK FIELDS
      *""""""""""""
     C     *LIKE         DEFINE    SEAVD         XAVD
     C     *LIKE         DEFINE    XDT2          XDT
      *____________
      * INIT FIELDS
      *""""""""""""
     C                   Z-ADD     XÅ            WÅ
     C                   Z-ADD     XM            WM
     C                   Z-ADD     XD            WD
NV5  C                   IF        XÅ > 50
NV5  C                   MOVEL     '19'          WÅ
NV5  C                   ELSE
NV5  C                   MOVEL     '20'          WÅ
NV5  C                   ENDIF
      *
     C                   ENDSR
      *
