     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 75 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            ULDT           8  0O  4  1EDTCDE(4)
     A            ULTID4         4  0O  4 10EDTWRD('0  :  ')
     A            ULTID          6  0H
     A            ULUSR8         8A  O  4 16
     A            ULUSR         10A  H
     A            ULKF           2A  O  4 25
     A            ULKFT1         1A  O  4 28
     A            ULFT1A        30A  O  4 30
     A            ULFT1B        15A  O  4 61
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYFA19D'
     A                                      COLOR(BLU)
     A                                  1 10'Spørring på logg av utleveringsfor-
     A                                      behold'
     A                                      COLOR(WHT)
     A                                  2  1'Avdeling:'
     A            UAVD           4  0O  2 11DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A                                  2 21'Oppdragsnr:'
     A            UOPD           7  0O  2 33DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A                                  3  1'Dato.... Tid.. Bruker.. KD S Tekst-
     A                                      ...................................-
     A                                      ......'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1  2'F3=Avslutt   F5=Frisk   F12=Forrig-
     A                                      e'
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYFA19D'
     A                                      COLOR(BLU)
     A                                  1 10'Spørring på logg av utleveringsfor-
     A                                      behold'
     A                                      COLOR(WHT)
     A                                  2  1'Avdeling:'
     A            UAVD           4  0O  2 11DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A                                  2 21'Oppdragsnr:'
     A            UOPD           7  0O  2 33DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(TRQ)
     A                                  3  1'Dato.... Tid.. Bruker.. KD S Tekst-
     A                                      ...................................-
     A                                      ......'
     A                                      COLOR(WHT)
     A                                 10  2'F3=Avslutt   F5=Frisk   F12=Forrig-
     A                                      e'
     A                                      COLOR(BLU)
