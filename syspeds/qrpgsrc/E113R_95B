     H DATEDIT(*YMD)
     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FECNI95B   IF   E           K DISK
     FECOM95B   IF   E           K DISK
     FEDTM95B   IF   E           K DISK
     FEEQD95B   IF   E           K DISK
     FELOC95B   IF   E           K DISK
     FETDT95B   IF   E           K DISK
     FEFTX95B   IF   E           K DISK
     FENAD95B   IF   E           K DISK
     FERFF95B   IF   E           K DISK
     FETOD95B   IF   E           K DISK
     FEGID95B   IF   E           K DISK
     FEMEA95B   IF   E           K DISK
     FZEADF     O  A E             DISK
     FZREETXT   O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C     *LIKE         DEFINE    HEHAWB        WEHAWB
     C     *LIKE         DEFINE    HETRI         WETRI
     C     *LIKE         DEFINE    HETRC         WETRC
     C     *LIKE         DEFINE    HETRCN        WETRCN
     C     *LIKE         DEFINE    HESDF         WESDF
     C     *LIKE         DEFINE    HENAA         WENAA
     C     *LIKE         DEFINE    HESDL         WESDL
      * INIT WORKFIELDS
     C                   MOVE      *BLANKS       WEHAWB
     C                   MOVE      *BLANKS       WETRI
     C                   MOVE      *BLANKS       WETRC
     C                   MOVE      *BLANKS       WETRCN
     C                   MOVE      *BLANKS       WESDF
     C                   MOVE      *BLANKS       WENAA
     C                   MOVE      *BLANKS       WESDL
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *_______________________
      * READ ALL MAIN SEGMENTS
      *"""""""""""""""""""""""
      * NAD09
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     09            ZGN0
     C     KEYMS0        SETLL     ENAD95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD95B                                51
     C     *IN51         DOWEQ     '0'
      *
     C     N3035         IFEQ      'MR'                                         RECEIVING AG
     C                   MOVEL     N3124A        WENAA
     C                   MOVE      '1'           *IN51
     C                   END
      *
     C  N51KEYMS0        READE     ENAD95B                                51
     C  N51              END
     C                   END
      * DTM05
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        CHAIN     EDTM95B                            51
     C     *IN51         IFEQ      '0'
     C                   MOVEL     D2380         W2380            35
     C                   ENDIF
      * LOC13
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     13            ZGN0
     C     KEYMS0        SETLL     ELOC95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELOC95B                                51
     C     *IN51         DOWEQ     '0'
      *
     C     L3227         IFEQ      '5'
     C                   MOVEL     E3055A        WESDF
     C                   ELSE
     C     L3227         IFEQ      'ZZZ'
     C                   MOVEL     E3055A        WETRI
     C                   END
     C                   END
      *
     C  N51KEYMS0        READE     ELOC95B                                51
     C  N51              ENDDO
     C                   END
      * EQD13
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     13            ZGN0
     C     KEYMS0        SETLL     EEQD95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EEQD95B                                51
     C     *IN51         DOWEQ     '0'
      *
     C     E8053         IFEQ      'CN'
     C                   MOVE      '1'           WETRC
     C                   MOVEL     E8260         WETRCN
     C                   MOVE      '1'           *IN51
     C                   ELSE
     C     E8053         IFEQ      'TE'
     C                   MOVEL     E8260         WEHAWB
     C                   MOVE      '1'           *IN51
     C                   END
     C                   END
      *
     C  N51KEYMS0        READE     EEQD95B                                51
     C  N51              ENDDO
     C                   END
      * TDT05
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ETDT95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT95B                                51
     C     *IN51         DOWEQ     '0'
      *
     C     T8067         IFEQ      '3'
     C                   MOVEL     T8212         WETRCN
     C                   MOVEL     T8212         WEHAWB
     C                   MOVEL     T8453         WETRI
     C                   MOVE      '1'           *IN51
     C                   END
      *
     C  N51KEYMS0        READE     ETDT95B                                51
     C  N51              ENDDO
     C                   END
      *___________________
      * READ CONSIGNEMENTS
      *"""""""""""""""""""
     C                   Z-ADD     0             WPOS              3 0
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     15            ZGN0
     C     KEYMS0        SETLL     ECNI95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNI95B                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ECNI95B                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   MOVE      'A'           HEUR
     C                   MOVEL     M1004         HERFA
     C                   MOVEL     C1004         HEGM1
     C                   ADD       1             WPOS
     C                   MOVEL     WPOS          HEPOS1
     C                   MOVEL     SLIB          HELIB
     C                   MOVEL     SFILE         HEFILE
     C                   MOVEL     SMBR          HEMBR
     C                   MOVE      'IFCSUM'      HETYPE
     C                   MOVE      '95B'         HELVL
     C                   Z-ADD     ZMN           HESNN
     C                   MOVEL     M1004         HEREFF
     C                   MOVEL     C1004         HEUNIK
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      FTX19
     C                   EXSR      LOC20
     C                   EXSR      TOD21
     C                   EXSR      NAD30
     C                   EXSR      RFF34
     C                   EXSR      GID37
     C                   EXSR      MEA39
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     HEADFR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LOC20                                               LOC20 /
      *////////////////////////////////////////////////////////////
     C     LOC20         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     20            ZGN1
     C     KEYMS1        SETLL     ELOC95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC95B                                52
     C     *IN52         DOWEQ     '0'
      *
     C     L3227         IFEQ      '1'
     C                   MOVEL     L3224         HESDT
     C                   MOVE      '1'           *IN52
     C                   END
      *
     C     KEYMS1        READE     ELOC95B                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TOD21                                               TOD21 /
      *////////////////////////////////////////////////////////////
     C     TOD21         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     21            ZGN1
     C     KEYMS1        SETLL     ETOD95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETOD95B                                52
     C     *IN52         IFEQ      '0'
      *
     C                   MOVEL     T4053         HEFR
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * NAD30                                               NAD30 /
      *////////////////////////////////////////////////////////////
     C     NAD30         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     30            ZGN1
     C     KEYMS1        SETLL     ENAD95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD95B                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CN'                                         LEV.ADR
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAK
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADK1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADK2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADK3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADK3
     C                   MOVE      WEADS2        HEADK3
     C                   ENDIF
      * HENT COM
     C                   Z-ADD     31            ZGN2
     C                   Z-ADD     NREP0         ZREPC0
     C                   Z-ADD     NREP1         ZREPC1
     C     KEYMS2        CHAIN     ECOM95B                            53
     C     *IN53         IFEQ      *OFF
     C                   MOVEL     C3155         HEKDL
     C     HEKDL         CAT       C3148:1       HEKDL
     C                   ENDIF
      *
     C                   ENDSL
      *
     C     KEYMS1        READE     ENAD95B                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RFF34                                               RFF34 /
      *////////////////////////////////////////////////////////////
     C     RFF34         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     34            ZGN1
     C     KEYMS1        SETLL     ERFF95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF95B                                52
     C     *IN52         DOWEQ     '0'
      *
     C     R1153         IFEQ      'AAJ'
     C                   MOVEL     R1154         HEGM2
     C                   MOVE      '1'           *IN52
     C                   ENDIF
      *
     C  N52KEYMS1        READE     ERFF95B                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX19                                               FTX19 /
      *////////////////////////////////////////////////////////////
     C     FTX19         BEGSR
     C                   CLEAR                   FREETXTR
     C                   MOVE      HEUNIK        FRUNIK
     C                   MOVE      HEREFF        FRREFF
     C                   Z-ADD     MDT           FRTDT
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     19            ZGN1
     C     KEYMS1        SETLL     EFTX95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX95B                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'DIN'
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440A        FRTTXT
     C                   WRITE     FREETXTR
     C     F4440B        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440B        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440C        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440C        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440D        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440D        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440E        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440E        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C                   ENDIF
      *
     C  N52KEYMS1        READE     EFTX95B                                52
     C  N52              ENDDO
     C                   ENDIF
      * LEGG IN AVGANGSDATO I FITEKST
     C     W2380         IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     'AVG.DATO'    FRTTXT
     C     FRTTXT        CAT       W2380:1       FRTTXT
     C                   WRITE     FREETXTR
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * GID37                                               GID37 /
      *////////////////////////////////////////////////////////////
     C     GID37         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     37            ZGN1
     C     KEYMS1        SETLL     EGID95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EGID95B                                52
     C     *IN52         IFEQ      '0'
      *
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C     HENT          IFEQ      0
     C                   ADD       ZNUM          HENT
     C                   MOVEL     G7065A        HEVS1
     C                   ELSE
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  G7224A        FRTTXT
     C     FRTTXT        CAT       G7064A:1      FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA39                                               MEA39 /
      *////////////////////////////////////////////////////////////
     C     MEA39         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     39            ZGN1
     C     KEYMS1        SETLL     EMEA95B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA95B                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C                   Z-ADD     ZNUM          HEVKT
     C                   ENDSL
      *
     C     KEYMS1        READE     EMEA95B                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
