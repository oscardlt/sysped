********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SLUTT DATO PÅ TOLLTARIFF
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL REGISTRERE TOLLDEKLARASJON (TRANSITT)
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01 02 16 18-20 23 24
      *** LEDIGE INDIKATORER 45-59 62-83 96
      ***        INDIKATORER 61 BRUKES FOR DUPKEY
      ***        INDIKATOR   35 PÅ = SJØ-OPPDRAG
      **************************************************************
      *
     FSADTRV    UF A E           K DISK
     FSADTRV02  IF   E           K DISK
     F                                     RENAME(SADTRVR:SADTRVR2)
     FSADTRV04  IF   E           K DISK
     F                                     RENAME(SADTRVR:SADTRVR4)
     FKODTSA    IF   E           K DISK
     FTARI      IF   E           K DISK
     FSAD041FA  CF   E             WORKSTN USROPN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     SFILE(SUBFIL2:ZRECN2)
     F                                     INFDS(FBAREA)
      *
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  W2EH                   1      4
     D  Y2EH                   1      2
     D                 DS
     D  SVFT                   1     17
     D  SVFT2                  1     11
     D                 DS
     D  SVTOPL                 1     45
     D  SVTOA                  1     22
     D  SVTOB                 23     45
     D                 DS
     D  W2VNT                  1      8  0
     D  W2VNT4                 1      4  0
     D                UDS
     D  UVNT                 101    108
     D  UFT2                 115    131
     D  UEH                  132    135
     D  UVT                  136    150
     D  UTOPL                151    195
     D  UIN37                199    199
      *
     C                   OPEN      SAD041FA
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
     C                   MOVE      W3LIGM        W3LI
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N31
     CANN32SATV2K        SETLL     SADTRV02
     C   31
     CANN32SATV4K        SETLL     SADTRV04
     C   32SATV1K        SETLL     SADTRV
     C     START1        TAG
     C  N31
     CANN32SADTHK        READE     SADTRV02                               94
     C   31
     CANN32SADTHK        READE     SADTRV04                               94
     C   32SADTHK        READE(N)  SADTRV                                 94
     C  N94
     CAN 32              MOVE      SVLI          W3LI
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C  N32              MOVE      SVLN          WXLN
     C   32              MOVE      SVLI          WXLN
     C                   WRITE     SUBFIL
     C  N31
     CANN32SADTHK        READE     SADTRV02                               94
     C   31
     CANN32SADTHK        READE     SADTRV04                               94
     C   32SADTHK        READE(N)  SADTRV                                 94
     C  N94
     CAN 32              MOVE      SVLI          W3LI
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      ***
      *
     C                   SETOFF                                       989930
      *
      *** F12 ---> AVBRYTTE REGISTRERING AV DETALJE LINJER
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
      *  07 = SØKE OPPDRAG
     C   07              DO
     C                   MOVE      'S'           UTYPE             1
     C                   CALL      'SYFM21'
     C                   PARM                    UTYPE
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    USG               3
     C                   GOTO      START3
     C                   END
      *
     C     START4        TAG
      *** F3  ---> BE OM UTSKRIFT ELLER SENDE MED TVINN
      *** F17 ---> SOM F3 + BEHOLDE EN DEL OPPLYSN. FOR NESTE TOLLDEK.
     C   03
     COR 17              DO
     C                   EXSR      PRINT
      *** F12 ---> AVBRYTTE F3/F17
     C   12              Z-ADD     4             LINNBR
     C   12              Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      SLUTT
     C                   END
      *
      *** F14 ---> SØK KUNDERS VAREREGISTER
     C   14              DO
     C                   EXSR      SØKVAR
     C  N03
     CANN17              GOTO      START
     C                   GOTO      SLUTT
     C                   END
      *
      *** F5  ---> VISE 1. SIDE PÅ SUBFIL.
     C   05              GOTO      START
      *** F6  ---> LAGE NYE DETALJE LINJER
     C   06              DO
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START5
     C                   END
      *** F8  ---> VELGE SORTERINGSMÅTE OG START POST FOR SUBFIL
     C   08              GOTO      START7
      *
      *** ROLL UP ---> BLA TIL NESTE SIDE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94
     CAN 32SATV1K        SETLL     SADTRV
     C  N94
     CAN 32SADTHK        READE(N)  SADTRV                                 94
     C                   GOTO      START2
     C                   END
      *
      *** SJEKKE OM BRUKER HAR VALGT OPSJONER. HIVS SÅ,T UTFØRE DISSE.
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
      *** OPSJON 9 ---> LAGE TILLEGGSLINJER
     C  N98WSNR          IFEQ      '9'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     SUBCTL
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     6             POSNBR
     C                   GOTO      START5
     C                   END
     C                   END
      *** 98 PÅ ---> BRUKER HAR IKKE VALGT OPSJON. VISE SAMME POSTER
      ***            I SUBFIL DVS. SAMME RECORDNR.
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
      *** 41 AV ---> BRUKER HAR SLETTET NOEN DETALJE LINJER.
      ***            VISE 1. SIDE PÅ SUBFIL
     C  N41              GOTO      START
     C                   GOTO      START3
      *
      *** LAGE NYE DETALJE LINJER
     C     START5        TAG
     C                   EXSR      MOVB2
      *
     C     START6        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   SETOFF                                       30
      *
     C*** F14 ---> SØKE KUNDERS VAREREGISTER
     C   14              DO
     C                   EXSR      SØKVAR
     C                   MOVE      W3LIGM        W3LI
      *** IKKE F3/F17 ---> BRUKER HAR IKKE BETT UTSKRIFT. VISE 1. SIDE
     C  N03
     CANN17              GOTO      START
     C                   GOTO      SLUTT
     C                   END
      *  07 = SØKE OPPDRAG
     C   07              DO
     C                   MOVE      'S'           UTYPE             1
     C                   CALL      'SYFM21'
     C                   PARM                    UTYPE
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    USG               3
     C                   GOTO      START6
     C                   END
      *
      *** F3  ---> BE OM UTSKRIFT ELLER SENDE MED TVINN
      *** F17 ---> SOM F3 + BEHOLDE EN DEL OPPLYSN. FOR NESTE TOLLDEK.
     C   03
     COR 17              GOTO      START4
      *
      *** F15 ---> SETTE IND. 37 PÅ/AV FOR Å AUTOKOPIERE TILLEGGSOPPL.
     C   15              DO
     C                   EXSR      SETIND
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   END
     C                   GOTO      START5
     C                   END
      *
      *** F4  ---> SØKE DATA FOR ET ELLER ANNET FELT.
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START6
     C                   END
      *
      *** F12 --> AVBRYTTE REGISTRERING AV NYE DETALJE LINJER.
      *** SJEKK OM MAN HAR TASTET <DUP>-TAST
     C  N12              EXSR      SRDUP
      *
     C  N12              DO
      *** TESTE INNTASTEDE DATA. HVIS FEIL, SETTE IND. PÅ OG VISE
      *** FEILMELDING.
     C                   EXSR      TESTB1
     C   30              GOTO      START6
     C                   ADD       1             XLI
     C                   MOVE      XLI           W2LI
      *** HVIS IKKE FEIL, OPPDATERE FIL, SUBFIL OG VISE DEN PÅ SKJERMEN
     C                   EXSR      OPDSAT
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
      *** VELGE SORTERINGSMÅTE OG START POST FOR SUBFIL
      *** 31 PÅ    ---> SORTERE PÅ TARIFFNR. + LANDKODE     (SADTRV2)
      *** 32 PÅ    ---> SORTERE PÅ REGISTRERINGS REKKEFØLGE (SADTRV)
      *** 31,32 AV ---> SORTERE PÅ UTSKRIFTS REKKEFØLGE     (SADTRV4)
     C     START7        TAG
     C                   MOVE      *ZEROS        W3VNT
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C     W3LI          COMP      0                                  3232
     C  N32              DO
     C     W3VNT         COMP      0                                      31
     C                   END
     C   32              SETOFF                                       31
     C                   MOVE      W3LI          W3LIGM            5 0
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   EXSR      FLYDU2
     C                   CLOSE     SAD041FA
      *                    SETON                     LR
     C                   RETURN
      *
     C***********************************************
     C     SETIND        BEGSR
     C***********************************************
      *
      *** F15 ---> AUTO KOPIERING AV TILLEGGS OPPLYSNIGER
      *            BENYTT INDIKATOR 37   37 PÅ -> AUTO KOPIERING
     C   15
     CAN 37              DO
     C                   SETOFF                                       37
     C                   GOTO      SLSETI
     C                   END
     C   15
     CANN37              DO
     C                   SETON                                        37
     C                   GOTO      SLSETI
     C                   END
      *
     C     SLSETI        ENDSR
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
      *
      *** 37 PÅ(AUTODUP)
     C   37W2TOPL        IFEQ      *BLANKS
     C                   MOVE      YTOPL         W2TOPL
     C                   END
      *
      *** XSTK = 'J' ---> KREVE Å GI MENGDE.
     C                   MOVE      ' '           XSTK              1
      *** TESTE OM INNTASTET TARIFFNR. ER RIKTIG.
     C     W2VNT         COMP      0                                  8989
      *
     C   89              DO
     C     TARIKM        SETLL     TARI
     C     TARIK         READPE    TARI                                   89
     C     *IN89         DOWEQ     '0'
     C     TADATO        IFLE      USEDT
N01  C     TADTS         ANDEQ     0
N01  C     TADATO        ORLE      USEDT
N01  C     TADTS         ANDGE     USEDT
     C                   LEAVE
     C                   END
     C     TARIK         READPE    TARI                                   89
     C  N89              ENDDO
     C  N89
     CAN 13W2VT          IFNE      *BLANKS
     C                   MOVE      W2VT          TAALFA
     C                   END
     C                   END
      *
     C  N89              MOVE      TASTK         XSTK
     C   89W2VNT         COMP      0                                  8989
     C   89              DO
     C                   SETON                                        30
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      'SPT0033'     MSGNR
     C                   END
      *
      *** TEST ENHETSKODE
     C     W2EH          IFNE      *BLANKS
     C     Y2EH          CHAIN     KODTSA                             84
     C  N30
     CAN 84              DO
     C                   SETON                                        30
     C                   MOVE      'SPE0002'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     31            POSNBR
     C                   END
     C                   END
      *
     C     W2VKTB        IFEQ      0
     C     W2VKTN        IFNE      0
     C     W2VKTN        MULT(H)   1.1           W2VKTB
     C                   END
     C                   ELSE
     C     W2VKTN        IFEQ      0
     C     W2VKTB        MULT(H)   0.9           W2VKTN
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
      *** SJEKKE OM BRUKER HAR VALGT NOEN OPSJONER. HVIS IKKE, SETTER
      *** IND. 98 PÅ OG GIR FEILMELDING TIL BRUKER.
      *** OPSJON 2 ---> ENDRE DETALJE LINJE.
      *** OPSJON 4 ---> SLETTE DETALJE LINJE.
      *** OPSJON 5 ---> VISE DETALJE LINJE
      *** OPSJON 9 ---> LAGE TILLEGGS DETALJE LINJE
      *** LINNBR, POSNBR ---> RETUR POSISJON AV CURSOR
     C                   SETON                                        41
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGSAT
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   EXSR      DLTSAT
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C                   EXSR      VISSAT
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '9'
     C                   EXSR      NYSAT
     C                   GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       3             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISSAT        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   SETOFF                                       39
      *
     C                   EXFMT     VAREL3
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
      *
     C***********************************************
     C     DLTSAT        BEGSR
     C***********************************************
     C                   Z-ADD     ZRECNO        XRECN2            4 0
     C                   MOVE      '1'           *IN43
     C                   WRITE     SUBCTL2
     C                   MOVEA     '00'          *IN(43)
      * HAR LEST FØRST SLETTET LINJE,  OPPDATERE PÅ SUBFIL2
     C                   Z-ADD     1             ZRECN2
     C                   WRITE     SUBFIL2
      *
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '4'                                           2<-B
     C                   ADD       1             ZRECN2
     C                   WRITE     SUBFIL2
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   Z-ADD     1             ZRECN2
     C     STDLT1        TAG
     C                   MOVEA     '11'          *IN(41)
     C                   WRITE     SUBCTL2
     C                   EXFMT     BUNN2
     C   25              GOTO      STDLT1
     C   12              Z-ADD     XRECN2        ZRECNO
     C  N12              DO                                                     1<-B
     C                   MOVE      *ZEROS        ZRECN2
     C     STDLT2        TAG
     C                   ADD       1             ZRECN2
     C     ZRECN2        CHAIN     SUBFIL2                            98
     C  N98              DO                                                      2<-B
     C     SATVK2        CHAIN     SADTRV                             41
     C     *IN41         IFEQ      '0'                                            3<-B
     C                   DELETE    SADTRVR
     C                   GOTO      STDLT2
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   EXSR      MOVB1
     C                   END                                                    1<-E
     C                   SETOFF                                       4198
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGSAT        BEGSR
     C***********************************************
      *** FLYTTE DATA FRA SUBFIL TIL ARBEIDSVARIABLER
     C                   EXSR      MOVSVW
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   SETOFF                                       30
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
      *  07 = SØKE OPPDRAG
     C   07              DO
     C                   MOVE      'S'           UTYPE             1
     C                   CALL      'SYFM21'
     C                   PARM                    UTYPE
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    USG               3
     C                   GOTO      STCHG
     C                   END
      *
      *** SJEKK OM MAN HAR TASTET <DUP>-TAST
     C  N12              EXSR      SRDUP
      *
      *** F12 ---> AVBRYTTE ENDRING
     C   12              UPDATE    SUBFIL
      *** TESTE INNTASTEDE TADA. HVIS IKKE NOE FEIL, OPPDRATERE PÅ FIL
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C  N12              EXSR      OPDSAT
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   MOVEA     '000'         *IN(37)
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYSAT         BEGSR
     C***********************************************
     C                   MOVE      SVVNT         YVNT
     C                   SETOFF                                       40
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPDSAT        BEGSR
     C***********************************************
      *** LESE LINJE FRA FIL OG OPPDATERE SUMMEN AV TOLLVERDIER.
      *** 33 PÅ ---> NY LINJE.        33 AV ---> ENDRE EN LINJE
     C     SATVK         CHAIN     SADTRV                             33
      *
      *** FLYTTE INNTASTET DATA TIL FIL-VARIABLER OG OPPDATERE DEM.
     C                   EXSR      MOVWSV
     C  N33              UPDATE    SADTRVR
      *
     C   33              DO
     C     SVVT          IFEQ      *BLANKS
     C                   MOVE      TAALFA        SVVT
     C                   END
     C  N38              MOVE      *ZEROS        SVLN
     C  N38              MOVE      *ZEROS        YLN
     C   38              MOVE      YLN           SVLN
     C                   WRITE     SADTRVR
     C                   END
      *
      *** OPPDATERE PÅ SUBFIL
     C  N32              MOVE      SVLN          WXLN
     C   32              MOVE      SVLI          WXLN
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVSVW        BEGSR
     C***********************************************
     C                   SETON                                        9940
     C                   Z-ADD     5             POSNBR
      *
     C     W2TOPL        IFNE      *BLANKS
     C                   MOVE      W2TOPL        YTOPL
     C                   END
      *
     C                   MOVE      SVLN          YLN               5 0
     C                   MOVE      SVLI          W2LI
     C                   MOVE      SVVNT         W2VNT
     C                   MOVE      SVVNT         W2VNTI
     C                   MOVE      SVVKTB        W2VKTB
     C                   MOVE      SVVKTN        W2VKTN
     C                   MOVE      SVNTM         W2NTM
     C                   MOVE      SVFT          W2FT
     C                   MOVE      SVNT          W2NT
     C                   MOVE      SVEH          W2EH
     C                   MOVE      SVVT          W2VT
     C                   MOVE      SVTOPL        W2TOPL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWSV        BEGSR
     C***********************************************
     C                   MOVE      UAVD          SVAVD
     C                   MOVE      UTDN          SVTDN
     C   22              MOVE      'B'           SV01
     C  N22              MOVE      ' '           SV01
     C                   MOVE      W2LI          SVLI
     C                   MOVE      W2VNT         SVVNT
     C                   MOVE      W2VKTB        SVVKTB
     C                   MOVE      W2VKTN        SVVKTN
     C                   MOVE      W2NTM         SVNTM
     C                   MOVE      W2FT          SVFT
     C                   MOVE      W2NT          SVNT
     C                   MOVE      W2EH          SVEH
     C                   MOVE      W2VT          SVVT
     C                   MOVE      W2TOPL        SVTOPL
      *
     C                   MOVE      W2VNT         YVNT              8 0
     C                   MOVE      W2FT          YFT              17
     C                   MOVE      W2EH          YEH               4
     C                   MOVE      W2VT          YVT              15
     C     W2TOPL        IFNE      *BLANKS
     C                   MOVE      W2TOPL        YTOPL            45
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       SV01
     C                   MOVE      *ZEROS        SVLI
     C                   MOVE      *ZEROS        SVVNT
     C                   MOVE      *ZEROS        SVVKTB
     C                   MOVE      *ZEROS        SVVKTN
     C                   MOVE      *ZEROS        SVNTM
     C                   MOVE      *BLANKS       SVFT
     C                   MOVE      *ZEROS        SVNT
     C                   MOVE      *BLANKS       SVEH
     C                   MOVE      *BLANKS       SVVT
     C                   MOVE      *BLANKS       SVTOPL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
      *
     C                   MOVE      *ZEROS        W2VNT
     C                   MOVE      *BLANKS       W2VNTI
     C                   MOVE      *ZEROS        W2VKTB
     C                   MOVE      *ZEROS        W2VKTN
     C                   MOVE      *ZEROS        W2NTM
     C                   MOVE      *ZEROS        W2LI
     C                   MOVE      *BLANKS       W2FT
     C                   MOVE      *ZEROS        W2NT
     C                   MOVE      *BLANKS       W2EH
     C                   MOVE      *BLANKS       W2VT
     C                   MOVE      *BLANKS       W2TOPL
      *
     C                   ENDSR
      *
     C***********************************************
     C     FINLNR        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        XLI               5 0
     C                   MOVE      *ZEROS        W2LI
     C     SATVK         SETLL     SADTRV                             33
     C                   SETON                                        99
      *
     C     STFS          TAG
     C     SADTHK        READE(N)  SADTRV                                 33
     C  N33              DO
     C                   MOVE      SVLI          XLI
     C                   SETOFF                                       99
     C                   GOTO      STFS
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     SRDUP         BEGSR
     C***********************************************
     C  N61              DO
     C                   TESTN                   W2VNTI               61
     C   61              MOVE      W2VNTI        W2VNT
     C  N61              MOVE      YVNT          W2VNT
     C                   SETOFF                                       61
     C                   END
     C*
     C   61              MOVE      YVNT          W2VNT
     C                   MOVEL     W2FT          XDUPKO            1
     C     XDUPKO        IFEQ      KDUKOD
     C                   MOVE      YFT           W2FT
     C                   END
     C                   MOVEL     W2EH          XDUPKO
     C     XDUPKO        IFEQ      KDUKOD
     C                   MOVE      YEH           W2EH
     C                   END
     C                   MOVEL     W2VT          XDUPKO
     C     XDUPKO        IFEQ      KDUKOD
     C                   MOVE      YVT           W2VT
     C                   END
     C                   MOVEL     W2TOPL        XDUPKO
     C     XDUPKO        IFEQ      KDUKOD
     C                   MOVE      YTOPL         W2TOPL
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C   17              Z-ADD     2             UFLAGG            1 0
     C   03              Z-ADD     3             UFLAGG            1 0
     C                   CALL      'SAD041T'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UFLAGG
     C                   PARM                    UWS
      *
     C     UFLAGG        COMP      1                                      12
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     31
     C     POSNBR        ANDLE     34
     C                   MOVE      *BLANKS       W2EH
     C                   CALL      'SAD052F'
     C                   PARM                    Y2EH
     C                   Z-ADD     36            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      20
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     12
     C                   CALL      'SAD062A'
     C                   PARM                    W2VNTI
     C                   PARM                    USEDT
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     SØKVAR        BEGSR
     C***********************************************
     C                   CALL      'SAD041C'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UWS
     C                   PARM                    UIN35
     C                   PARM                    UIN36
     C                   PARM                    UFLAGG
     C                   PARM                    XLI
      *
     C     UFLAGG        COMP      1                                      12
     C     UFLAGG        COMP      3                                      03
     C     UFLAGG        COMP      2                                      17
      *
     C                   ENDSR
      *
     C***********************************************
     C     FLYDUP        BEGSR
     C***********************************************
     C                   SETOFF                                       33
     C                   TESTN                   UVNT                 33
     C   33              MOVE      UVNT          YVNT
     C  N33              MOVE      *ZEROS        YVNT
     C                   MOVE      UFT2          YFT
     C                   MOVE      UEH           YEH
     C                   MOVE      UVT           YVT
     C                   MOVE      UTOPL         YTOPL
     C     UIN37         COMP      '1'                                    37
      *
     C                   ENDSR
      *
     C***********************************************
     C     FLYDU2        BEGSR
     C***********************************************
     C                   MOVE      YVNT          UVNT
     C                   MOVE      YFT           UFT2
     C                   MOVE      YEH           UEH
     C                   MOVE      YVT           UVT
     C                   MOVE      YTOPL         UTOPL
     C                   MOVE      *IN37         UIN37
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADTHK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SATVK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W2LI
     C     SATV1K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3LI
     C     SATVK1        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SATVK2        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    SVLI
     C     SATV2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3VNT
     C     SATV21        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    SVVNT
     C     SATV4K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3LN
     C     TARIK         KLIST
     C                   KFLD                    W2VNT
     C                   KFLD                    X2TAR
     C     TARIKM        KLIST
     C                   KFLD                    W2VNT
     C                   KFLD                    X2TAR
     C                   KFLD                    XADATO
     C                   MOVE      99999999      XADATO            8 0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UWS              10
     C     *IN35         PARM                    UIN35             1
      *** UIN36 = '1' -> I SAD400
     C     *IN36         PARM                    UIN36             1
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      *ZEROS        SVLI
     C                   MOVE      *BLANKS       X2TAR             3
     C                   MOVE      *ZEROS        W2VNT
     C                   MOVE      *ZEROS        SVVNT
     C                   EXSR      FLYDUP
      *
     C                   BITON     '345'         KDUKOD            1
     C                   BITOFF    '01267'       KDUKOD
     C                   MOVE      '0'           *IN37
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           USEDT             8 0
      *
     C                   EXSR      FINLNR
      *
     C                   ENDSR
      *
