      *********************************************************************
      * ARBEID MED ORDRE
      * PROGRAM BEHANDLER INPUT FRA ESOVERS21
      *
CRTPG+*  CRTPGM SYSPED/CCOS01 MODULE(*LIBL/CCOS01 *LIBL/CCOS01B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FHEADF     UF   E           K DISK    USROPN
     FCCBLV1    UF A E           K DISK    USROPN
     FCDCON4    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D SubmPressed     s              1
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Boats serial numbers received in the input string
     D ItemCount       s             10i 0
     D SernoC          s            105a
     D i               s             10i 0
     D TB              S              1    DIM(7)
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('SYSPED/CCOS01B')
     D FEIL01          C                   CONST('Container number is not -
     D                                     valid.')
     D FEIL02          C                   CONST('Type of container is not -
     D                                     valid.')
     D FEIL03          C                   CONST('Weight of container has to  -
     D                                     be given.')
     D FEIL05          C                   CONST('Colli can not be 0.')
     D FEIL01EN        C                   CONST('Container number is not -
     D                                     valid.')
     D FEIL02EN        C                   CONST('Type of container is not -
     D                                     valid.')
     D FEIL03EN        C                   CONST('Weight of container has to  -
     D                                     be given.')
     D FEIL05EN        C                   CONST('Colli can not be 0.')
      *
     D r               s             10i 0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FEILMELDING     S             80
     D XXNY            S              1
     D WSAVD           S              4
     D BSAVD           S              4  0
     D WSOPD           S              7
     D BSOPD           S              7  0
     D WSLI            S              3
     D BSLI            S              3  0
     D WSCN            S             17
     D WSSL            S             17
     D WSVS35          S             35
     D WSCT            S              2
     D WSNT            S              5
     D BSNT            S              5  0
     D WSVKT           S             10
     D BSVKT           S              9  2
     D WSM3            S              9
     D BSM3            S              9  3
     D                 DS
     D XXREF                   1     24
     D XXAVD                   1      4
     D XXOPD                   5     11
     D XXLI                   12     14
     D XXUSER                 15     24
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *------------------
      * Retrieve number of boats to be shown
     C                   EVAL      ItemCount = ZhbGetVarCnt('serno')
      * Show the requested boats
     C                   IF        ItemCount = 0
     C                   eval      SernoC = zhbgetvar('xxref')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSAVD   = zhbgetvar('WSAVD')
     C                   eval      BSAVD   = c2n2(WSAVD)
     C                   eval      WSOPD   = zhbgetvar('WSOPD')
     C                   eval      BSOPD   = c2n2(WSOPD)
     C                   eval      WSLI    = zhbgetvar('WSLI')
     C                   eval      BSLI    = c2n2(WSLI)
     C                   eval      XXNY    = zhbgetvar('XXNY')
     C                   ELSE
     C                   eval      i = 1
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   eval      WSUSER  = zhbgetvar('USER':i)
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName':i)
     C                   MOVEL     SERNOC        XXREF
     C                   eval      BSAVD   = c2n2(XXAVD)
     C                   eval      BSOPD   = c2n2(XXOPD)
     C                   eval      BSLI    = c2n2(XXLI)
     C                   MOVE      XXLI          BVLI
     C                   END
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C                   SELECT
     C                   WHEN      XXNY = 'J'
     C     HEADFK        CHAIN(N)  HEADF                              33
     C                   EVAL      BVVS35 = %TRIM(HEVS1) + ' ' + %TRIM(HEVS2)
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FMELDNR
     C                   WHEN      XXNY = 'E'
     C     CCBLV1K       CHAIN(N)  CCBLV1                             33
     C                   MOVE      BVMN          WSCN
     C                   MOVE      BVKDC         WSCT
     C                   MOVE      BVNT          BSNT
     C                   MOVE      BVVKT         BSVKT
     C                   MOVE      BVM3          BSM3
     C                   MOVE      BVSEAL        WSSL
     C                   MOVE      BVVS35        WSVS35
     C                   OTHER
     C                   EXSR      ARBLINJE
     C                   ENDSL
      *
     C                   CALLB     PGM2
     C                   PARM                    BSAVD
     C                   PARM                    BSOPD
     C                   PARM                    BSLI
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFARG
     C                   PARM                    WSCN
     C                   PARM                    WSSL
     C                   PARM                    WSVS35
     C                   PARM                    WSCT
     C                   PARM                    BSNT
     C                   PARM                    BSVKT
     C                   PARM                    BSM3
     C                   PARM                    FMELDNR
     C                   PARM                    FEILMELDING
      *
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   IF        UINN = 'N'
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   END
      *
     C                   close     HEADF
     C                   close     CCBLV1
     C                   close     CDCON4
     C                   close     BRIDF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSCN    = zhbgetvar('WSCN')
     C     LC:UC         XLATE     WSCN          WSCN
     C                   eval      WSSL    = zhbgetvar('WSSL')
     C     LC:UC         XLATE     WSSL          WSSL
     C                   eval      WSVS35  = zhbgetvar('WSVS35')
     C                   eval      WSCT    = zhbgetvar('WSCT')
     C                   eval      WSNT    = zhbgetvar('WSNT')
     C                   eval      BSNT    = c2n2(WSNT)
     C                   eval      WSVKT   = zhbgetvar('WSVKT')
     C                   eval      BSVKT   = c2n2(WSVKT)
     C                   eval      WSM3    = zhbgetvar('WSM3')
     C                   eval      BSM3    = c2n2(WSM3)
     C                   eval      XXNY    = zhbgetvar('XXNY')

     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      HEADF                                50
     C                   open      CCBLV1                               50
     C                   open      CDCON4                               50
     C                   open      BRPARF                               50
     C     HEADFK        klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C     CCBLV1K       klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C                   kfld                    BSLI
     c     CCBLV1KSIST   klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C                   KFLD                    BVLISIST
     c     CCBLV1K2      klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * HENT SPRÅKKODE
     C                   MOVEL     'SPRÅK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C     *LIKE         DEFINE    BVLI          BVLISIST
     C                   MOVE      *BLANKS       FMELDNR           2
     C                   MOVE      *BLANKS       FEILMELDING
     C                   Z-ADD     999           BVLISIST
      *
     C                   endsr
      *=====================================================================
      *  ARBEID MED LINJE
      *=====================================================================
     C     ARBLINJE      BEGSR
      * TEST INNGÅENDE DATA
     C                   EXSR      TESTB
      *
     C                   IF        FEILMELDING = *BLANKS
     C                   EXSR      OPDLINJE
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FMELDNR
      * TEST CONTAINERNR
     C                   IF        WSCN = *BLANKS AND
     C                             (WSCT <> *BLANKS OR WSVS35 = *BLANKS)
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEIL01EN      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL01        FEILMELDING
     C                   END
     C                   MOVEL     '01'          FMELDNR
     C                   MOVE      *ON           *IN33
     C                   GOTO      SLTESTB
     C                   END
      * TEST KODE
     C                   IF        WSCT <> *BLANKS
     C     WSCT          CHAIN     CDCON4                             33
     C                   IF        *IN33
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEIL02EN      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL02        FEILMELDING
     C                   END
     C                   MOVEL     '02'          FMELDNR
     C                   GOTO      SLTESTB
     C                   END
     C                   END
      * TEST KOLLI (men kun når contKode oppgitt/ulik *C = Stykkgods samlecontainer)
     C                   IF        WSCT <> *BLANKS
     C                   IF        ((BSNT = 0) AND (WSCT <> '*C'))
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEIL05EN      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL05        FEILMELDING
     C                   END
     C                   MOVEL     '05'          FMELDNR
     C                   MOVE      *ON           *IN33
     C                   END
     C                   END
      * TEST VEKT (men kun når contKode oppgitt/ulik *C = Stykkgods samlecontainer)
     C                   IF        WSCT <> *BLANKS
     C                   IF        ((BSVKT = 0) AND (WSCT <> '*C'))
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEIL03EN      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL03        FEILMELDING
     C                   END
     C                   MOVEL     '03'          FMELDNR
     C                   MOVE      *ON           *IN33
     C                   END
     C                   END
      *
     C     SLTESTB       ENDSR
      *
      *=====================================================================
      *  Oppdater linje
      *=====================================================================
     C     OPDLINJE      begsr
      *
     C                   IF        BSLI = 0
     C     CCBLV1KSIST   SETLL     CCBLV1
     C     CCBLV1K2      READPE(N) CCBLV1                                 33
     C                   IF        *IN33 = *OFF
     C                   ADD       1             BVLI
     C                   ELSE
     C                   Z-ADD     1             BVLI
     C                   ENDIF
     C                   MOVE      BSAVD         BVAVD
     C                   MOVE      BSOPD         BVOPD
     C                   EVAL      BVSORT = BVLI * 10
     C                   ELSE
     C     CCBLV1K       CHAIN     CCBLV1                             33
     C                   END
      *
     C                   MOVE      WSCN          BVMN
     C                   EVAL      BVNT   = BSNT
     C                   EVAL      BVVKT  = BSVKT
     C                   EVAL      BVM3   = BSM3
     C                   EVAL      BVKDC  = WSCT
     C                   EVAL      BVSEAL = WSSL
     C                   EVAL      BVVS35 = WSVS35
     C                   IF        BSLI   = 0
     C                   WRITE     CCBLV1R
     C                   ELSE
     C                   UPDATE    CCBLV1R
     C                   END
      *
     C                   MOVE      *ZEROS        BSLI
     C                   MOVE      *BLANKS       WSCN
     C                   MOVE      *BLANKS       WSSL
     C                   MOVE      *BLANKS       WSVS35
     C                   MOVE      *BLANKS       WSCT
     C                   MOVE      *ZEROS        BSNT
     C                   MOVE      *ZEROS        BSVKT
     C                   MOVE      *ZEROS        BSM3
      *
     C                   endsr
      *
