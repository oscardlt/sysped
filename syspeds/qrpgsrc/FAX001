********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 FEIL ved sjøsystem  SYFA20PT
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT KOPI AV BORDEREAU            ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04-26                      ***
      *** LEDIGE INDIKATORER 28 29 31 32 35 38 51-56 58-69    ***
      ***                    74-78 80-87 89-99                ***
      ***********************************************************
N01  FBILLOF    IF   E           k DISK
     FCUNDC     IF   E             DISK
     FFAXSEN    IF   E           K DISK
     FFAXLK     IF   E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD14    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADF14)
     FFAI4      IF   E           K DISK
     FFAK9      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FKUNDL01   UF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR2)
     FKODTW     IF   E           K DISK
     FFAX001FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D F5              S             84    DIM(12)
     D                 DS
     D  COV01                  1     40
     D  COV02                 41     80
     D  COV03                 81    120
     D  COV04                121    160
     D  COV05                161    200
     D  COV06                201    240
     D  COV07                241    280
     D  COV08                281    320
     D  COV1                   1    160
     D  COV2                 161    320
     D                 DS
     D  USØKKN                 1     30
     D  USØKKO                 1      2
     D  URRN                   3     12
     D  epofax                13     13
     D XXX            UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UPRO                  22     29  0
     D  UDT                   30     35  0
     D  UHOD                  47     47
     D  UBBS                  48     48
     D  UAWB                  81     81
     D  WSFN1                101    107  0
     D  WSFN2                108    114  0
     D  WSFN3                115    121  0
     D  WSFN4                122    128  0
     D  WSFN5                129    135  0
     D  WSFN6                136    142  0
     D  WSFN7                143    149  0
     D  WSFN8                150    156  0
     D  WSFN9                157    163  0
     D  WSFN10               164    170  0
     D  WSFN11               171    177  0
     D  WSFN12               178    184  0
     D  WF                   101    310  0
     D                                     DIM(30)
     D  UGN                  981    995
     D                 DS
     D  F1                     1     21
     D                                     DIM(21)
     D  FAXNRP                 1     21
     D                 DS
     D  F2                     1     15
     D                                     DIM(15)
     D  FAXNR                  1     15
     D  FAX05A                 1      4
     D                 DS
     D  F3                     1      6
     D                                     DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyz')
     D epotxt          C                   CONST('Sender over fakturak-
     D                                     opi via epost')
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   EXFMT     BILDE
     C                   EXSR      KDTFMT
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C     WSTUR         IFNE      *ZEROS
     C                   EXSR      TURSR
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C                   END
     C   04              DO
     C     LINNBR        IFeq      14
     C     LO:UP         XLATE     EPOST         usok
     C                   CALL      'VCUNDC2R'
     C                   PARM                    FIFIRM
     C                   PARM                    USOK             30
     C                   PARM                    URRN2            10 0
     c                   parm                    epofax            1
     C     URRN2         IFNE      *ZEROS
     C                   MOVE      URRN2         URRN
     C                   MOVE      'KP'          USØKKO
     c                   else
     c                   movel     usok          epost
     C                   END
     C                   ELSE
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    KUNDNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     c                   end
     C* RETUR MED KONTAKTPERS
     C     USØKKO        IFEQ      'KP'
     C                   MOVE      URRN          RRN              10 0
     C     RRN           CHAIN     CUNDC                              31
     c     epofax        ifne      'E'
     C  N31              MOVEL     CFAX          FAXNR
     c                   else
     c                   move      *blanks       faxnr
     c                   end
     c     epofax        ifne      'F'
     C  N31              MOVEL     CEMAIL        EPOST
     c                   else
     c                   move      *blank        epost
     c                   end
     C  N31              MOVE      CCOMPN        KUNDNR
     C                   END
     C                   END
     C                   EXSR      TEST
     C   30              GOTO      START
     C*
     C* OPPDATERER KUNDEREGISTER
     C*
     C     KUNDNR        IFNE      *ZEROS
     C     FAXNR         IFNE      *BLANKS
     C     TFAXNR        IFEQ      *BLANKS
     C     KUKEY         CHAIN     CUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD
     C     KUKEY         CHAIN     KUNDL01                            33
     C  N33              MOVE      FAXNR         TFAXNR
     C  N33              EXCEPT    UPD2
     C                   END
     C                   END
     C                   END
     C*
     C   10              EXSR      SNDFAX
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TEST          BEGSR
     C***********************************************
     C                   Z-ADD     1             XN                2 0
     C                   MOVEA     '00'          *IN(96)
     C     WF(1)         IFEQ      *ZEROS
     C                   SETON                                        30
     C                   MOVE      'SPF0026'     MSGNR
     C                   GOTO      SLTB
     C                   END
     C*
     C     TST12         TAG
     C     WF(XN)        IFNE      0
     C                   Z-ADD     WF(XN)        YFN
     C     FAINK         SETLL     FAK9
     C     STTST1        TAG
     C     FAINK         READE     FAK9                                   30
     C  N30FAKDA         CABEQ     'K'           STTST1
     C  N30HEADFK        CHAIN     HEADF                              30
     C   30              MOVE      'SPF0026'     MSGNR
     C   30              GOTO      SLTB
     C                   ADD       1             XN
     C     XN            CABGT     12            TST12
     C*
     C*
     C* KALLER SYFA20
     C*
     C  N30              DO
      *
     C     KUNDNR        IFEQ      *ZEROS
     C     KNAVN         ANDEQ     *BLANKS
     C                   MOVE      FAKUNR        KUNDNR
     C                   END
      *
     C     KUNDNR        IFNE      *ZEROS
     C     KUKEY         CHAIN(N)  CUNDL01                            31
      *
     C  N31WSLAND        IFEQ      *BLANKS
     C                   MOVE      SYLAND        WSLAND
     C                   END
     C                   END
      *
     C  N31FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   MOVE      TFAXNR        FAXNR
     C                   MOVEl     syepos        epost
     C                   END
      * IKKE FAX ELLER EPOST-ADRESSE LAGT INN
     C     FAXNR         IFEQ      *BLANKS
     C     EPOST         ANDEQ     *BLANKS
     C                   SETON                                        30
     C   30              MOVE      'SHU0165'     MSGNR
     C   30              GOTO      SLTB
     C                   END
      *
      * BÅDE FAX OG EPOST-ADRESSE LAGT INN
     C     FAXNR         IFNE      *BLANKS
     C     EPOST         ANDNE     *BLANKS
     C                   SETON                                        30
     C   30              MOVE      'SHU0538'     MSGNR
     C   30              GOTO      SLTB
     C                   END
      *
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        ANDEQ     *ZEROS
     C                   MOVE      'SHU0388'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C     WSTIME        IFEQ      *ZEROS
     C     WSDATE        ANDNE     *ZEROS
     C                   MOVE      'SHU0388'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C     WSTIME        IFLT      0
     C     WSTIME        ORGT      235959
     C                   MOVE      'SHU0389'     MSGNR
     C                   SETON                                        3097
     C                   GOTO      SLTB
     C                   END
     C*
     C*** TEST DATO
     C     WSDATE        IFNE      *ZEROS
     C                   CALL      'TESTDATO'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      96
     C   96
     CANN30              MOVE      'SPD0001'     MSGNR
     C   96              SETON                                        30
     C   30              GOTO      SLTB
     C                   END
     C*
     C                   END
     C     WSLAND        IFNE      *BLANKS
     C     WSLAND        CHAIN     FAXLK                              33
     C     FAX05A        IFEQ      FAX05B
     C                   MOVE      *BLANKS       FAXRET
     C                   END
     C                   END
     C                   END
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *DTAARA       DEFINE    *LDA          XXX
     C                   IN        XXX
     C     *ENTRY        PLIST
     C                   PARM                    PP1               2
     C                   PARM                    PP2               1
     C                   PARM                    PP3               1
     C                   PARM                    PP4               1
     C                   PARM                    PP5               1
     C                   PARM                    PP6               1
     C                   PARM                    PP7               1
     C     KODTWK        KLIST
     C                   KFLD                    WSWUNI
     C                   KFLD                    FAAVD
     C                   MOVE      'W'           WSWUNI            1
     C     FAINK         KLIST
     C                   KFLD                    YFN
     C     HEADFK        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C     KUKEY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
      *
     C                   READ      FIRM                                   44
     C                   READ      FAXSEN                                 44
     C     FAXLIB        COMP      *BLANKS                                27
     C*
     C                   MOVE      *ZEROS        WF
     C                   MOVE      'F'           UHOD
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     SNDFAX        BEGSR
     C***********************************************
      * behandling av retningsnummer
     C     FAXRET        IFNE      *BLANKS
     C                   EXSR      FAXFIX
     C                   ELSE
     C                   MOVEL     FAXNR         FAXNRP
     C                   END
      *
     C                   MOVE      'C'           UBBS
     C     KODTWK        CHAIN     KODTW                              99
     C  N99              MOVE      KOWAWB        UAWB
     C                   SETON                                        LR
     C                   OUT       XXX
     C*
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     WSTIME        IFNE      *ZEROS
     C     WSDATE        ANDNE     *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVEL     *BLANKS       PDATE
     C                   MOVEL     WSTIME        PTIME
     C                   MOVEL     WSDATE        PDATE
     C                   END
     C                   MOVE      'O'           UUKODE            1
     C                   MOVEL     'FAKTURA'     DOKNAV           12
     C                   MOVE      FaAVD         FAXAVD            4
     C                   MOVE      FaOPD         FAXOPD            7
     C                   MOVEL     'SYFA20P '    FILEN            10
     C     HEUR          IFEQ      'F'
     C     HEUR          OREQ      'G'
     c     uawb          ifne      'J'
n02  c     uawb          andne     'K'
n03  c     uawb          andne     'N'
     C                   MOVEL     'SYFA20PS'    FILEN            10
     C                   END
N01   *UNNTAK SYA20PT
N01  C     UAWB          IFNE      'S'                                          96 SJØ-FAKT.
N01  C     HEADFK        CHAIN     BILLOF                             33
N01  C  N33              MOVEL     'SYFA20PT'    FILEN            10
N01  C                   END
     C                   END
     c     fakda         ifeq      'S'
     c                   movel     'SYFA27PF'    filen
     c                   end
     C                   MOVE      'N'           COVP
     C     EPOST         IFne      *BLANKS
     c                   move      'J'           wscvrp
     c                   end
     C     WSCVRP        IFEQ      'J'                                          1<-B
     C     WSCVRP        OREQ      'Y'
     C                   MOVE      'J'           COVP
     C                   MOVE      *BLANKS       COV1
     C                   MOVE      *BLANKS       COV2
      *FAX COVERPAGE
      *
     C     EPOST         IFEQ      *BLANKS
     C     *IN27         IFEQ      '1'                                           2<-B
     C                   MOVEL     FIFT          COV05
     C                   CALL      'SYGE12CL'
     C                   PARM                    USE              10
     C                   PARM                    NVN              40
     C                   MOVEL     NVN           COV06
     C                   MOVEL     KNAVN         COV02
     C                   MOVEL     ADR1          COV03
     C                   CALL      'FAXCVP'
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C     FUNK          CABEQ     'A'           SRFEIL
     C                   ELSE                                                    2
     C                   MOVEL     FAXNRS        COV01
     C                   MOVEL     KNAVN         COV02
     C                   MOVEL     ADR1          COV03
     C                   MOVEL     ADR2          COV04
     C                   MOVEL     ADR3          COV05
     C                   CALL      'FAX001X'
     C                   PARM                    KUNDNR
     C                   PARM                    KNAVN
     C                   PARM                    FAXNRP
     C                   PARM                    WSLAND
     C                   PARM                    FAXRET
     C                   PARM                    COV1
     C                   PARM                    COV2
     C                   PARM                    COVP
     C                   PARM                    F5
     C                   END                                                     2<-E
     C*EPOST COVERPAGE
     C                   ELSE
     c                   movel     epotxt        cov01
     C                   CALL      'EPOCVP'
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    FUNK              1
     C     FUNK          CABEQ     'A'           SRFEIL
     C                   END
     C                   END                                                    1<-E
     C                   MOVE      'N'           EPOSTJ            1
     C     EPOST         IFNE      *BLANKS
     C                   MOVE      'J'           EPOSTJ
     C                   END
      *
      * kaller sendingsprogram
      *
     C                   CALL      'FAX002CL'
     C                   PARM                    FAXNRP
     C                   PARM                    FILEN
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    KNAVN
     C                   PARM                    FAXAVD
     C                   PARM                    FAXOPD
     C                   PARM                    UUKODE
     C                   PARM                    DOKNAV
     C                   PARM                    PP1               2
     C                   PARM                    PP2               1
     C                   PARM                    PP3               1
     C                   PARM                    PP4               1
     C                   PARM                    PP5               1
     C                   PARM                    PP6               1
     C                   PARM                    PP7               1
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    COVP              1
     C                   PARM                    F5
     C                   PARM                    EPOST
     C                   PARM                    EPOSTJ
     C                   MOVEA     *ZEROS        WF
     C                   MOVE      *BLANKS       WSLAND
     C                   MOVE      *BLANKS       FAXRET
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       FAXNR
     C                   MOVE      *ZEROS        KUNDNR
     c                   MOVE      *blanks       epost
     C                   z-add     7             LINNBR
     C                   z-add     16            POSNBR
     c                   seton                                        30
     c                   move      'SHU0756'     msgnr
     C     SRFEIL        ENDSR
     C*****************************************************
     C     TURSR         BEGSR
     C*****************************************************
     C     KEYFAI        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     WSTUR         SETLL     HEAD14
     C     STTUR         TAG
     C     WSTUR         READE     HEAD14                                 33
     C   33              GOTO      UTTUR
     C     HEUR          IFEQ      'B'
     C     HEUR          OREQ      'D'
     C     HEUR          OREQ      'G'
     C                   GOTO      STTUR
     C                   END
     C*
     C     KEYFAI        CHAIN     FAI4                               34
     C   33              GOTO      STTUR
     C  N33              MOVE      FIFN          WSFN1
     C                   EXSR      TEST
     C                   EXFMT     BILDE
     C   03              GOTO      UTTUR
     C   10              EXSR      SNDFAX
     C                   GOTO      STTUR
     C     UTTUR         ENDSR
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   Z-ADD     1             A1                2 0
     C                   Z-ADD     1             A2                2 0
     C                   Z-ADD     1             A3                2 0
     C                   DO        6
     C     F3(A3)        IFNE      *BLANKS
     C                   MOVEL     F3(A3)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A3
     C                   END
     C                   DO        15
     C     F2(A2)        IFNE      *BLANKS
     C                   MOVEL     F2(A2)        F1(A1)
     C                   ADD       1             A1
     C                   END
     C                   ADD       1             A2
     C                   END
     C                   ENDSR
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDATE        XDTFMT            4 0
     C                   MOVEL     WSDATE        UDT1              2 0
     C                   MOVEL     XDTFMT        UDT2              2 0
     C                   MOVE      XDTFMT        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
     OKUNDR     E            UPD
     O                       TFAXNR
     OKUNDR2    E            UPD2
     O                       TFAXNR
