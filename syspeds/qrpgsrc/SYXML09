      ***********************************************************
      *** PROGRAM SKAL SENDE XML AV MELDING FRA TET           ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 34-49 51-99                ***
      ***********************************************************
     FXMLTILP   O  A E           K DISK    USROPN
     FTRIH      IF   E           K DISK
     FTRUH      IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FEDIC      UF A E             DISK    USROPN
     FEDIS      O  A E             DISK    USROPN
     FEDIM      O  A E             DISK    USROPN
     FEDISE     O  A E             DISK    USROPN
     D TXT             S             80    DIM(47) CTDATA PERRCD(1)
     D                 DS
     D  XTXT801                1     80
     D  XT801                  1     80    DIM(80)
     D                 DS
     D  XTXT802                1     80
     D  XT802                  1     80    DIM(80)
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
     D  XMBRC                  2     10
     D                 DS
     D  WDT                    1      8
     D  WDTÅ                   1      4
     D  WDTM                   5      6
     D  WDTD                   7      8
     D                 DS
     D  WTM                    1      6
     D  WTMH                   1      2
     D  WTMM                   3      4
     D  WTMS                   5      6
     D                 DS
     D  XPARM1                 1   1024
     D  XP101                  1     10                                         IMP/EKSP
     D  XP102                 11     26                                         SYSPEDREF
     D  XP1021                11     14                                         AVDELING
     D  XP1022                15     21                                         OPPDRAGSNR
     D  XP1023                22     24                                         SIGNATUR
     D  XP103                 25     46                                         MELDINGSTYPE
     D  XP104                 47     54                                         AKSEPTDATO
     D  XP1041                47     50                                         AKSEPTDATO ÅR
     D  XP1042                51     52                                         AKSEPTDATO MND
     D  XP1043                53     54                                         AKSEPTDATO DAG
     D  XP105                 55     57                                         MELDINGSTYPE
     D                 DS
     D  XPARM2                 1   1024
     D  XP201                  1     20                                         ERP+??
     D  XP201T                 1     20    DIM(10)                              ERP+??
     D  XP202                 21    370                                         ERP+??:??????
     D  XP202T                21    370    DIM(10)                              ERP+??:??????
     D  XP203                371    520                                         ERC+???
     D  XP203T               371    520    DIM(50)                              ERC+???
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRINIT
     C                   EXSR      SRBYGGF
      *
     C                   EXSR      SRSTART
      *
     C                   EXSR      SRMSGDET
      *
     C                   EXSR      SRCUSRES
      *
     C                   EXSR      SRSLUTT
     C                   EXSR      SRCLOSE
     C                   EXSR      SRSEND
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRINIT        BEGSR
      ***********************************************
     C                   OPEN      EDIC
     C                   OPEN      EDIS
     C                   OPEN      EDIM
     C                   OPEN      EDISE
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       XMBR
     C                   MOVE      'F'           XMBRA
     C                   MOVE      CSN           XMBRB
     C                   MOVE      XMBR          SEMBR
     C                   MOVE      *BLANKS       SDATA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM
      *
     C                   EVAL      S0004 = 'SYSPEDNO'
     C                   EVAL      S0010 = 'AMPHORA'
     C                   EVAL      S0020 = FSSOK
     C                   EVAL      S0026 = 'XML-CUSTOMSRES'
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'C'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = '*LIBL'
     C                   EVAL      SFILE = 'EDISE'
     C                   EVAL      SMBR  = SEMBR
     C                   EVAL      SMBRS = 0
     C                   EVAL      S0036 = 1
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRBYGGF       BEGSR
      ***********************************************
     C                   CALL      'SYXML03B'
     C                   OPEN      XMLTILP
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSTART       BEGSR
      ***********************************************
     C                   EVAL      SDATA = TXT(001)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(002)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = %trim(TXT(003))
     C                             + %trim(XMBR)
     C                             + '"'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(004)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(005)
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = TXT(006)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRMSGDET      BEGSR
      ***********************************************
      * <MessageDetails>
     C                   EVAL      SDATA = TXT(008)
     C                   EXSR      TILFIL
      * <MessageSender>
     C                   EVAL      SDATA = %trim(TXT(010))
     C                             + ' partyId="'
     C                             + %trim(S0004)
     C                             + '">'
     C                             + %trim(TXT(011))
     C                   EXSR      TILFIL
      * <MessageRecipient>
     C                   EVAL      SDATA = %trim(TXT(012))
     C                             + ' partyId="'
     C                             + %trim(S0010)
     C                             + '">'
     C                             + %trim(TXT(013))
     C                   EXSR      TILFIL
      * <MessageDate>
     C                   EVAL      SDATA = %trim(TXT(014))
     C                             + ' timeZone="+01:00"'
     C                             + ' dstIndicator="YES">'
     C                             + WDTÅ + '-' + WDTM + '-' + WDTD + 'T'
     C                             + WTMH + ':' + WTMM + ':' + WTMS
     C                   EXSR      TILFIL
      * </MessageDate>
     C                   EVAL      SDATA = TXT(015)
     C                   EXSR      TILFIL
      * </MessageDetails>
     C                   EVAL      SDATA = TXT(009)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCUSRES      BEGSR
      ***********************************************
      * <ConsignmentStructure>
     C                   EVAL      SDATA = %trim(TXT(016))
     C                   EXSR      TILFIL
      * <SummarySet>
     C                   EVAL      SDATA = %trim(TXT(018))
     C                             + ' summaryId="'
     C                             + %trim(FSSOK)
     C                             + '">'
     C                   EXSR      TILFIL
      * TransportLeg
     C                   EXSR      SRTRALEG
      * Reference
     C                   EXSR      SRREF
      * Party
     C                   EXSR      SRPARTY
      * DateAndTimes
     C                   EXSR      SRDATTIM
      * TransitHeader
     C                   EXSR      SRTRAHEA
      *
      * </SummarySet>
     C                   EVAL      SDATA = TXT(019)
     C                   EXSR      TILFIL
      * </ConsignmentStructure>
     C                   EVAL      SDATA = TXT(017)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRTRALEG      BEGSR
      ***********************************************
      * <TransporLeg>
     C                   EVAL      SDATA = %trim(TXT(020))
     C                   EXSR      TILFIL
      * <Location>
     C                   EVAL      SDATA = %trim(TXT(022))
     C                             + ' subClass="CustomsDeparture" '
     C                             + 'locationId="'
     C                             + %trim(THCATS)
     C                             + '">'
     C                   EXSR      TILFIL
      * <CountryCode>
     C                   EVAL      SDATA = %trim(TXT(024))
     C                             + 'NO'
     C                             + %trim(TXT(025))
     C                   EXSR      TILFIL
      * </Location>
     C                   EVAL      SDATA = TXT(023)
     C                   EXSR      TILFIL
      * </TransportLeg>
     C                   EVAL      SDATA = TXT(021)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRREF         BEGSR
      ***********************************************
     C                   IF        XP105 = '028'
      * <Reference>
     C                   EVAL      SDATA = %trim(TXT(026))
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   EVAL      SDATA = %trim(TXT(028))
     C                             + %trim(XP103)
     C                             + %trim(TXT(029))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   EVAL      SDATA = %trim(TXT(030))
     C                             + 'AWR'
     C                             + %trim(TXT(031))
     C                   EXSR      TILFIL
      * </Reference>
     C                   EVAL      SDATA = %trim(TXT(027))
     C                   EXSR      TILFIL
     C                   END
      *
      * <Reference>
     C                   EVAL      SDATA = %trim(TXT(026))
     C                   EXSR      TILFIL
      * <ReferenceNo>
     C                   EVAL      SDATA = %trim(TXT(028))
     C                             + %trim(THTRNR)
     C                             + %trim(TXT(029))
     C                   EXSR      TILFIL
      * <ReferenceType>
     C                   EVAL      SDATA = %trim(TXT(030))
     C                             + 'MRN'
     C                             + %trim(TXT(031))
     C                   EXSR      TILFIL
      * </Reference>
     C                   EVAL      SDATA = %trim(TXT(027))
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRPARTY       BEGSR
      ***********************************************
      * <Party
     C                   EVAL      SDATA = %trim(TXT(032))
     C                             + ' subClass="CustomsBroker" partyId="'
     C                             + %trim(THTINA)
     C                             + '">'
     C                   EXSR      TILFIL
      * <Name>
     C                   EVAL      XTXT801 = THNAA
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = %trim(TXT(034))
     C                             + %trim(XTXT802)
     C                             + %trim(TXT(035))
     C                   EXSR      TILFIL
      * </Party>
     C                   EVAL      SDATA = %trim(TXT(033))
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRDATTIM      BEGSR
      ***********************************************
     C                   SELECT
     C                   WHEN      XP105 = '028'
     C                   EVAL      XDTTYPE = 'TransitAcceptance'
     C                   WHEN      XP105 = '029'
     C                   EVAL      XDTTYPE = 'ReleaseDate'
     C                   WHEN      XP105 = '045'
     C                   EVAL      XDTTYPE = 'ReleaseDate'
     C                   END
      * <DateAndTimes>
     C                   EVAL      SDATA = %trim(TXT(036))
     C                             + ' subClass="'
     C                             + %trim(XDTTYPE)
     C                             + '">'
     C                   EXSR      TILFIL
      * <Year>
     C                   EVAL      SDATA = %trim(TXT(038))
     C                             + %TRIM(XP1041)
     C                             + %trim(TXT(039))
     C                   EXSR      TILFIL
      * <Month>
     C                   EVAL      SDATA = %trim(TXT(040))
     C                             + %TRIM(XP1042)
     C                             + %trim(TXT(041))
     C                   EXSR      TILFIL
      * <Day>
     C                   EVAL      SDATA = %trim(TXT(042))
     C                             + %TRIM(XP1043)
     C                             + %trim(TXT(043))
     C                   EXSR      TILFIL
      * </DateAndTimes>
     C                   EVAL      SDATA = %TRIM(TXT(037))
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRTRAHEA      BEGSR
      ***********************************************
      * <TransitHeader>
     C                   EVAL      SDATA = TXT(044)
     C                   EXSR      TILFIL
      * <MessageType>
     C                   EVAL      SDATA = %TRIM(TXT(046))
     C                             + 'CC' + %TRIM(XP105) + 'A'
     C                             + %TRIM(TXT(047))
     C                   EXSR      TILFIL
      * </TransitHeader>
     C                   EVAL      SDATA = TXT(045)
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSLUTT       BEGSR
      ***********************************************
      *
     C                   EVAL      SDATA = TXT(007)
     C                   EXSR      TILFIL
      *
      * Oppdater EDIS
     C                   WRITE     EDISR
      * Oppdater EDIM
     C                   EXSR      OPDEDIM
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
     C                   CLOSE     EDIC
     C                   CLOSE     EDIS
     C                   CLOSE     EDIM
     C                   CLOSE     EDISE
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSEND        BEGSR
      ***********************************************
     C                   CLOSE     XMLTILP
     C                   EVAL      XFILNVN = 'XXXX'
     C                   CAT       XMBRC:0       XFILNVN          16
     C                   CALL      'SYXML09S'
     C                   PARM                    XFILNVN
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDEDIM       BEGSR
      ***********************************************
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EVAL      M0004 = S0004
     C                   EVAL      M0010 = S0010
     C                   MOVEL     CMN           M0062
     C                   EVAL      M0065 = 'XMLRES'
     C                   EVAL      M0068 = FSSOK
     C                   EVAL      M1001 = 'TET'
     C                   EVAL      M1004 = XP102
     C                   EVAL      M1225 = XP105
     C                   EVAL      MSN = SSN
     C                   EVAL      MMN = CMN
     C                   EVAL      MSR = 'S'
     C                   EVAL      MST = 'C'
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      XP1021        MAVD
     C                   MOVE      XP1022        MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     TILFIL        BEGSR
      ***********************************************
     C                   WRITE     EDISER
      *
     C                   MOVE      SDATA         XTPDTA
     C                   WRITE     XMLTILPR
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVST         BEGSR
      ***********************************************
     C     XN53          SUB       4             XN54              3 0
     C     XN53          SUB       5             XN55              3 0
     C     XN53          SUB       6             XN56              3 0
     C                   Z-ADD     0             XN52              3 0
     C                   MOVE      *BLANKS       XTXT802
     C     1             DO        80            XN51              3 0
     C                   IF        (((((XT801(XN51) = '&') OR
     C                                 (XT801(XN51) = '"')) OR
     C                                 (XT801(XN51) = XAPS)) OR
     C                                 (XT801(XN51) = '<')) OR
     C                                 (XT801(XN51) = '>'))
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   SELECT
     C                   WHEN      XT801(XN51) = '&'
     C                   IF        XN51 < XN55
     C                   MOVEA     '&amp;'       XT802(XN52)
     C                   ADD       4             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '"'
     C                   IF        XN51 < 75
     C                   MOVEA     '&quot;'      XT802(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = XAPS
     C                   IF        XN51 < XN56
     C                   MOVEA     '&apos;'      XT802(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '>'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&gt;'        XT802(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '<'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&lt;'        XT802(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   ENDSL
     C                   END
     C                   ELSE
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   MOVE      XT801(XN51)   XT802(XN52)
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     FRISOKK       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C     TRIHK         KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     TRUHK         KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
      *
     C     *ENTRY        PLIST
     C     XPARM1        PARM                    UPARM1         1024
     C     XPARM2        PARM                    UPARM2         1024
      *
     C                   MOVE      XP1021        FSAVD
     C                   MOVE      XP1022        FSOPD
     C                   IF        XP101 = 'Import'
     C                   MOVE      '*CR'         FSKODE
     C                   ELSE
     C                   MOVE      '*CN'         FSKODE
     C                   END
     C                   BITON     '123457'      XAPS              1
     C                   BITOFF    '06'          XAPS
     C                   MOVE      *BLANKS       XDTTYPE          30
      *
     C     FRISOKK       CHAIN     FRISOK                             33
     C                   IF        XP101 = 'Import'
     C     TRIHK         CHAIN     TRIH                               33
     C                   ELSE
     C     TRUHK         CHAIN     TRUH                               33
     C                   END
      *
     C                   ENDSR
      *
** TXT
<?xml version="1.0" encoding="UTF-8" ?>                                         001
<Transport version="1.1" domain="" collaboration="Instruction"                  002
messageFunction="TransitResponse" profile="" messageId="                        003
xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"                           004
xsi:noNamespaceSchemaLocation=                                                  005
"file:///K:/FEDI/Prosjekter/bringXML/Schema/Transport.xsd">                     006
</Transport>                                                                    007
<MessageDetails>                                                                008
</MessageDetails>                                                               009
<MessageSender                                                                  010
</MessageSender>                                                                011
<MessageRecipient                                                               012
</MessageRecipient>                                                             013
<MessageDate                                                                    014
</MessageDate>                                                                  015
<ConsignmentStructure>                                                          016
</ConsignmentStructure>                                                         017
<SummarySet                                                                     018
</SummarySet>                                                                   019
<TransportLeg>                                                                  020
</TransportLeg>                                                                 021
<Location                                                                       022
</Location>                                                                     023
<CountryCode>                                                                   024
</CountryCode>                                                                  025
<Reference>                                                                     026
</Reference>                                                                    027
<ReferenceNo>                                                                   028
</ReferenceNo>                                                                  029
<ReferenceType>                                                                 030
</ReferenceType>                                                                031
<Party                                                                          032
</Party>                                                                        033
<Name>                                                                          034
</Name>                                                                         035
<DateAndTimes                                                                   036
</DateAndTimes>                                                                 037
<Year                                                                           038
</Year>                                                                         039
<Month>                                                                         040
</Month>                                                                        041
<Day>                                                                           042
</Day>                                                                          043
<TransitHeader>                                                                 044
</TransitHeader>                                                                045
<MessageType>                                                                   046
</MessageType>                                                                  047
