     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      ALTPAGEUP
     A                                      ALTPAGEDWN
     A                                      CA03(03 'End')
     A                                      HELP
      *
     A          R E151DA
     A                                  1  2'EDI    '
     A                                      COLOR(BLU)
     A                                  1 32MSGCON(017 EDI0506 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 74'E151D A'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(014 EDI0507 *LIBL/SYMSGF)
     A            WSLK           2A  B  4 17
     A  99                                  DSPATR(HI)
     A  99                                  DSPATR(PC)
     A                                  6  2MSGCON(014 EDI0508 *LIBL/SYMSGF)
     A            WSPRT         10A  B  6 17
     A  98                                  DSPATR(HI)
     A  98                                  DSPATR(PC)
     A                                  8  2MSGCON(014 EDI0509 *LIBL/SYMSGF)
     A            WSLN           6Y 0B  8 17EDTCDE(4)
     A                                  8 32MSGCON(025 EDI0510 *LIBL/SYMSGF)
     A  99                             23  2MSGCON(027 EDI0511 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  98                             23  2MSGCON(027 EDI0512 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 24  2MSGCON(013 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E151DB                    SFL
     A                                      SFLNXTCHG
     A            UMN            9S 0H
     A            WSOPT          1A  B  5  3VALUES(' ' '5' '6')
     A  70                                  DSPATR(PR)
     A            U1004         35A  H
     A            WSDNO          5A  O  5  6
     A            D2380         35A  H
     A            WSDDT          6A  O  5 12
     A            WSONO          9A  O  5 19
     A            WSINO          9A  O  5 29
     A            WSAVS         20A  O  5 39
     A            WSMOT         20A  O  5 60
      *
     A          R E151DC                    SFLCTL(E151DB)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A                                  1  2'EDI    '
     A                                      COLOR(BLU)
     A                                  1 31MSGCON(019 EDI0506 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 74'E151D B'
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(014 EDI0507 *LIBL/SYMSGF)
     A            WSLK           2A  O  2 17
     A                                  2 22MSGCON(014 EDI0508 *LIBL/SYMSGF)
     A            WSPRT         10A  O  2 37
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(043 EDI0516 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(068 EDI0517 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E151DD
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 28MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E151DG
     A                                      CA05(05 'Refresh')
     A                                      OVERLAY
     A                                  1  2'EDI    '
     A                                      COLOR(BLU)
     A                                  1 31MSGCON(019 EDI0506 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 74'E151D G'
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(014 EDI0507 *LIBL/SYMSGF)
     A            WSLK           2A  O  2 17
     A                                  2 22MSGCON(014 EDI0508 *LIBL/SYMSGF)
     A            WSPRT         10A  O  2 37
     A            ZRECNO         4S 0O  2 76DSPATR(ND)
     A                                  3  2MSGCON(043 EDI0516 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(068 EDI0517 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  6'(No order to show   !)'
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 28MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
