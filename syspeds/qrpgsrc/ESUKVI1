      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/ESUKVI1 MODULE(*LIBL/ESUKVI1 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 bhr-spesialer fjernet fra standard
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FHead37B   IF   E           K DISK    usropn
     FHead37C   IF   E           K DISK    usropn
     F                                     RENAME(HEADFR:HEADFRC)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D user            s             10
     D DATO            s             10
      *
     D FraDT8          s              8  0
     D TilDT8          s              8  0
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D TraN            s              8
     D TraNN           s              8  0
     D SNDNR           s             20
     D AVS             s             12
     D MOT             s             17
     D MPNR            s              4
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  XXDate                 1      8  0
     D  XXDtÅ                  1      4  0
     D  XXDtM                  5      6  0
     D  XXDtD                  7      8  0
      * dummy slik at ARLINK er definer (kommer i PTF)
     D                 DS
     D  Wdata                  1    220
     D  ARLINK                57     96
     D                 DS
     D  XXDATO                 1      8  0
     D  XXDATOD                1      2  0
     D  XXDATOM                3      4  0
     D  XXDATOÅ                5      8  0
     D                 DS
     D  DATON                  1      6  0
     D  DATOND                 1      2  0
     D  DATONM                 3      4  0
     D  DATONÅ                 5      6  0
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
      * Vis liste over arkivposter leveringer
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     c     TRANN         IFNE      *ZEROS
     C     FraKey        SETLL     Head37B
     c     TRANN         Reade     Head37B                                35
     c                   else
     C     AgeKey        SETLL     Head37C
     c     AgeKeyE       Reade     Head37C                                35
     c                   move      Hedtop        TUDT
     c                   move      Hepro         TUPRO
     c                   end
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      * diverse filter legges her..
      * Tildato, ved søk via transp test mot turdato/ agent mot oppd.dt
     C     TilDt8        IFne      *zeros
     C     TUDT          Andgt     TilDt8
     c                   goto      Utles
     c                   end
      * Ok - skriv
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
      *
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
      *
     c     NextRec       Tag
     c     TRANN         IFNE      *ZEROS
     c     TRANN         Reade     Head37B                                35
     c                   else
     c     AgeKeyE       Reade     Head37C                                35
     c                   move      Hedtop        TUDT
     c                   move      Hepro         TUPRO
     c                   end
     c  N35              end
      *
     C     UTLES         TAG
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      DATO     = zhbgetvar('FRA')
     C                   eval      DATON    = c2n2(DATO)
      * Bruker DatoN - XXDate for datokonvertering
     c     DatoN         Ifeq      *zeros
     C                   move      *zeros        FraDt8
     c                   else
     C     2000          Add       DATONÅ        XXDtÅ
     C                   move      DATONM        XXDtM
     C                   move      DATOND        XXDtD
     C                   move      XXDate        FraDt8
     c                   end
      *
     C                   eval      DATO     = zhbgetvar('TIL')
     C                   eval      DATON    = c2n2(DATO)
      * Bruker DatoN - XXDate for datokonvertering
     c     DatoN         Ifeq      *zeros
     C                   move      *zeros        TilDt8
     c                   else
     C     2000          Add       DATONÅ        XXDtÅ
     C                   move      DATONM        XXDtM
     C                   move      DATOND        XXDtD
     C                   move      XXDate        TilDt8
     c                   end
      *
     C                   eval      TRAN     = zhbgetvar('TRAN')
     C                   eval      TRANN    = c2n2(TRAN)
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     cundl01
     C                   CLOSE     Head37B
     C                   CLOSE     Head37C
     C*                  return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESUKVI1':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
      * Søkeparam
     C                   callp     updHTMLvar('FRA':
     C                             %trim(%editw(FraDt8:'    .  .  ')))
     C                   callp     updHTMLvar('TIL':
     C                             %trim(%editw(TilDt8:'    .  .  ')))
     C     TRANN         Ifne      *Zeros
     C                   callp     updHtmlVar('TRA':
     C                             %trim(%editc(TRANN:'Z')))
     c                   else
     C                   callp     updHtmlVar('TRA':'** Som agent **')
     c                   end
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
      *
     c                   MOVEL     HENAS         AVS
     c                   MOVEL     HENAK         MOT
     c                   MOVEL     HEADK3        MPNR
     c                   MOVEL     HXSNDN        SNDNR
      *
     C                   callp     updHTMLvar('TRANR':
     C                             %trim(%editc(TUKNT2:'3')))
     C                   callp     updHTMLvar('SNDNR':SNDNR)
     C                   callp     updHTMLvar('TUR':
     C                             %trim(%editc(TUPRO:'3')))
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(TUDT:'    .  .  ')))
     C                   callp     updHTMLvar('AVS':AVS)
     C                   callp     updHTMLvar('MOT':MOT)
     C                   callp     updHTMLvar('MPNR':MPNR)
     C                   callp     updHTMLvar('ANT':
     C                             %trim(%editc(HENT:'3')))
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      Cundl01
     C                   OPEN      Head37B
     C                   OPEN      Head37C
      *
     C     FraKey        KLIST
     C                   KFLD                    TRANN
     C                   KFLD                    FraDt8
     C     AgeKey        KLIST
     C                   KFLD                    XXBlank          10
     C                   KFLD                    BiKUNR
     C                   KFLD                    FraDt8
     C     AgeKeyE       KLIST
     C                   KFLD                    XXBlank          10
     C                   KFLD                    BiKUNR
      *
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
      *
     C     KunKey        CHAIN     Cundl01                            33
     C                   endsr
      *
