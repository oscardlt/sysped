      ****************************************************************************************
      * Generere bekreftelse til web-booking                                                 *
      ****************************************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TEST OM MANUELL INNTASTET EPOSTADRESSE FINNES
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 DATO OG ANDRE REF
********************************************************************
     FZEADL02   IF   E           K DISK
     FZREETXT1  UF A E           K DISK
     FZRISOL02  UF A E           K DISK
     FKODTOTY   IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FBRIDF     IF   E           K DISK
     FBRPARF    IF   E           K DISK
     FEPOSRPL   IF   E           K DISK
     FARKSRC160 O    E             DISK    RENAME(ARKSRC160:ARCDAT)
     D XEMNE           S             50
     D EMNE            S             44
     D EMNE2           S             10
     D SENDER          S             17
     D CMD             S           5000
     D MELD            S           5000
     D MELD2           S            100
     D                 DS
     D  MAIL                   1    250    DIM(5)
     D  XINTAD1                1     50
     D  XINTAD2               51    100
     D  XINTAD3              101    150
     D  XINTAD4              151    200
     D  XINTAD5              201    250
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA2                1      2
     D  PAVRDA48               3     50
     D                 DS
     D  HEAVD                  1      4  0
     D  XXAVD                  1      4
     D                 DS
     D  HEOPD                  1      7  0
     D  XXOPD                  1      7
      *
     C                   EXSR      INIT
      *
     C     ZEADL02K      CHAIN     ZEADL02                            33
     C                   IF        *IN33
     C                   MOVE      'N'           UOK
     C                   GOTO      SLUTT
     C                   END
      * User som har booket oppdraget ligger lagret i HEFILE (og HELIB = blank)
     C     HEFILE        CHAIN     BRIDF                              33
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      'HEUMA'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      FIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      *ZEROS        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             33
     C                   END
     C                   END
      *
     C                   IF        *IN33
     C                   MOVE      'J'           XGML              1
     C                   ELSE
     C                   EVAL      UAVAD = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'SPRÅK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      FIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      *ZEROS        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             33
     C                   END
     C                   END
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
     C     CUNDL01K      CHAIN     CUNDL01                            33
      *
     C                   IF        UTYPE <> 'WEBPR'
      *
     C                   MOVE      UTYPE         PAID
     C                   MOVE      *ZEROS        XN                3 0
     C                   MOVE      *BLANKS       MAIL
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C     BrParKey      SETLL     BRPARF
     C     BrParKey      READE     BRPARF                                 40
     C                   IF        *IN40
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      FIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             40
     C                   IF        *IN40
     C                   MOVE      *ZEROS        PAKUNR
     C     BrParKey      CHAIN     BRPARF                             40
     C                   END
     C                   END
     C                   DOW       *IN40 = *OFF
     C                   IF        ((PAVRDN <> 0) AND (PAVRDN <> HEAVD))
     C                   GOTO      START1
     C                   END
     C                   IF        (((UTYPE = 'HEUMI') OR
     C                               (UTYPE = 'HEUMS')) OR
     C                               (UTYPE = 'HEUMT'))
     C                   ADD       1             XN
     C                   MOVEL     PAVRDA        MAIL(XN)
     C                   ELSE
     C                   IF        PAVRDA2 = HEOT
     C                   ADD       1             XN
     C                   MOVEL     PAVRDA48      MAIL(XN)
     C                   END
     C                   END
     C                   IF        XN = 5
     C                   LEAVE
     C                   END
     C     START1        TAG
     C     BrParKey      READE     BRPARF                                 40
     C                   ENDDO
      *
     C                   IF        UTYPE <> '*FTXT'
     C                   IF        MAIL(1) = *BLANKS
     C                   MOVE      'N'           UOK
     C                   GOTO      SLUTT
     C                   END
     C                   ENDIF
      *
     C                   END                                                    END UTYPE<>'WEBPR'
      *
     C                   SELECT
     C                   WHEN      ((UTYPE = 'HEUMI') OR (UTYPE = 'WEBPR'))
     C                   IF        XGML = 'J'
     C                   EXSR      SNDEM
     C                   ELSE
     C                   EXSR      LAGINH1
     C                   END
     C                   WHEN      UTYPE = 'HEUMO'
     C                   EXSR      LAGINH2
     C                   WHEN      UTYPE = 'HEUMT'
     C                   EXSR      LAGINH3
     C                   WHEN      UTYPE = 'HEUMS'
     C                   EXSR      LAGINH4
      * Mailadresse i ZREETXT?
     C                   WHEN      UTYPE = '*FTXT'
     C                   EXSR      LAGINH5
N01  C     UOK           CABNE     'J'           SLUTT
     C                   ENDSL
      *
     C                   MOVE      'J'           UOK
     C                   MOVE      XINTAD1       UINTAD1
     C                   MOVE      XINTAD2       UINTAD2
     C                   MOVE      XINTAD3       UINTAD3
     C                   MOVE      XINTAD4       UINTAD4
     C                   MOVE      XINTAD5       UINTAD5
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ******************************************************************************
     C     SNDEM         BEGSR
      ******************************************************************************
     C                   Z-ADD     1024          CMDL             15 5
     C     'QTMHHTP1  '  CHAIN     EPOSRPL                            33
     C   33'ESPED     '  CHAIN     EPOSRPL                            33
     C     *IN33         IFEQ      *ON
     C                   EVAL      SENDER = '*CURRENT'
     C                   ELSE
     C                   EVAL      SENDER = DIRNAM + ' ' + DIRADR
     C                   END
      * Emne
     C                   Eval      EMNE = 'Bookingbekreftelse '
     C                                    + %trim(HERFA)
     C     Heavd         ifne      *zeros
     C                   eval      EMNE2= ' Avd:'
     C                                    + %trim(%editc(HEAVD:'Z'))
     C                   cat       emne2:0       emne
     C                   END
      *
     C     Mail(1)       cabeq     *blanks       UtIMail
     C     BIFIRM        CHAIN     FIRM
     C                   EVAL      MELD = 'Booking er mottatt fra '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ':/NRef: ' + %trim(HERFA)
     C                             + ':/PAvsender:'
     C                             + ':/N     ' + HENAS
     C                             + ':/N     ' + HEADS1
     C                             + ':/N     ' + HEADS3
     C                             + ':/PMottaker:'
     C                             + ':/N     ' + HENAK
     C                             + ':/N     ' + HEADK1
     C                             + ':/N     ' + HEADK3
     C                             + ':/PFra: ' + HELKA + ' ' + HESDF
     C                             + ':/NTil: ' + HETRI + ' ' + HESDT
     C                             + ':/PAnt Kll: ' + %trim(%editc(HENT:'Z'))
     C                             + ':/NVekt: ' + %trim(%editc(HEVKT:'Z'))
     C                             + ':/NM3: ' + %trim(%editc(HEM3:'4'))
     C                             + ':/P' + %trim(FIFT)
     C                             + ' takker for din bestilling.'
     C     SENDER        IFEQ      '*CURRENT '
     C                   EVAL      MELD2=  ':/P:/PThis message is created '
     C                             + 'automaticly by the booking system.'
     C                             + ':/NPlease do not reply!'
     C                   cat       MELD2:0       MELD
     C                   END
      *
      * param for tointnet og longmsg må være i apostrofer ( hex 7D )
      * (strenger i single-parametere som inneholder blank el. spesialtegn)
     C                   eval      cmd = 'SNDDST TYPE(*LMSG) TOINTNET('
     C                             +'('+ X'7D' + %trim(Mail(1)) + X'7D' +') '
     C                             +'('+ X'7D' + %trim(Mail(2)) + X'7D' +') '
     C                             +'('+ X'7D' + %trim(Mail(3)) + X'7D' +') '
     C                             +'('+ X'7D' + %trim(Mail(4)) + X'7D' +') '
     C                             +'('+ X'7D' + %trim(Mail(5)) + X'7D' +')) +
     C                             DSTD('+ X'7D' + %trim(EMNE) + X'7D' + ') +
     C                             LONGMSG('+ X'7D' + %trim(MELD) + X'7D' +') +
     C                             USRID('+  %trim(SENDER) +') SNDFMT(*NOCHG)'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    CMD
     C                   PARM                    CMDL
      *
     C     UtImail       TAG
     C                   MOVE      'N'           UOK
     C                   ENDSR
      *
      ******************************************************************************
     C     LAGINH1       BEGSR
      ******************************************************************************
     C                   EVAL      UAVNA = FIFT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   Eval      UEMNE = 'Booking confirmation '
     C                                    + %trim(HERFA)
     C                   ELSE
     C                   Eval      UEMNE = 'Bookingbekreftelse '
     C                                    + %trim(HERFA)
     C                   END
     C                   IF        HEAVD <> 0
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      XEMNE = ' Dept:'
     C                                    + %trim(%editc(HEAVD:'Z'))
     C                   ELSE
     C                   EVAL      XEMNE = ' Avd:'
     C                                    + %trim(%editc(HEAVD:'Z'))
     C                   END
     C                   CAT       XEMNE:0       UEMNE
     C                   END
      *
     C                   MOVE      *ZEROS        SRCSEQ
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Booking is received from  '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                   ELSE
     C                   EVAL      SRCDTA = 'Booking er mottatt fra  '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EXSR      SRMSG01
     C                   EXSR      SRMSG02
     C                   EXSR      SRMSG03
     C                   EXSR      SRMSG04
     C                   EXSR      SRMSG05
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = FIFT
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        UTYPE <> 'WEBPR'
     C                   EXSR      SRMSG10
     C                   END
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     LAGINH2       BEGSR
      ******************************************************************************
     C                   EVAL      UAVNA = KNAVN
      *
     C                   Eval      UEMNE = 'Bookingref: '
     C                                    + %trim(HERFA)
     C                   IF        HEAVD <> 0
     C                   EVAL      XEMNE = ' Avd:'
     C                                    + %trim(%editc(HEAVD:'Z'))
     C                   CAT       XEMNE:0       UEMNE
     C                   END
      *
     C                   MOVE      *ZEROS        SRCSEQ
      *
     C                   EVAL      SRCDTA = 'Booking er kommet fra  '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EXSR      SRMSG01
     C                   EXSR      SRMSG04
     C                   EXSR      SRMSG05
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = KNAVN
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EXSR      SRMSG10
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     LAGINH3       BEGSR
      ******************************************************************************
     C                   EVAL      UAVNA = FIFT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   Eval      UEMNE = 'Your booking ('
     C                                    + %trim(HERFA)
     C                                    + ') is in work'
     C                   ELSE
     C                   Eval      UEMNE = 'Din booking ('
     C                                    + %trim(HERFA)
     C                                    + ') er under behandling'
     C                   END
      *
     C                   MOVE      *ZEROS        SRCSEQ
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Booking by '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ' with ref.=' + %trim(HERFA)
     C                             + ' is in work.'
     C                   ELSE
     C                   EVAL      SRCDTA = 'Booking er registrert av '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                             + ' med ref.=' + %trim(HERFA)
     C                             + ' er under behandling.'
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Our ref.: '
     C                             + XXAVD + '/' + XXOPD + '/' + HESG
     C                   ELSE
     C                   EVAL      SRCDTA = 'Vårref.: '
     C                             + XXAVD + '/' + XXOPD + '/' + HESG
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EXSR      SRMSG01
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = FIFT
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'You can see more informations '
     C                             + 'by click link.'
     C                   ELSE
     C                   EVAL      SRCDTA = 'Du kan få mer informasjon ved '
     C                             + 'å klikke link.'
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C     BIFIRM        CHAIN     FIRMARC                            33
     C                   EVAL      SRCDTA = 'http://'
     C                             + %trim(ARCPAE) + '/sycgip/syso02.pgm/'
     C                             + '?user=' + %trim(HEFILE)
      * InT5WXz4BJ er fra SYSO02
     C                             + '&UsrSpcName=InT5WXz4BJ'
     C                             + '&WSNA=WEBREG'
     C                             + '&HEAVD=' + %trim(XXAVD)
     C                             + '&HEOPD=' + %trim(XXOPD)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EXSR      SRMSG10
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     LAGINH4       BEGSR
      ******************************************************************************
     C                   EVAL      UAVNA = KNAVN
      *
     C                   Eval      UEMNE = 'Your booking ('
     C                                    + %trim(HERFA)
     C                                    + ') is received'
      *
     C                   MOVE      *ZEROS        SRCSEQ
      *
     C                   EVAL      SRCDTA = 'Thanks for your booking.'
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EXSR      SRMSG01
     C                   EXSR      SRMSG02
     C                   EXSR      SRMSG03
     C                   EXSR      SRMSG04
     C                   EXSR      SRMSG05
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = KNAVN
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EXSR      SRMSG10
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     LAGINH5       BEGSR
      ******************************************************************************
     C     XUNIK         SETLL     ZREETXT1
     C     XUNIK         READE     ZREETXT1                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        FRTKOD = 'M'
N01  c                   EVAL      UOK = 'J'
     c                   delete    Freetxtr
      *
     C                   ADD       1             XN
     C                   MOVEL     FRTTXT        MAIL(XN)
      *
     C                   EVAL      UAVNA = FIFT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   Eval      UEMNE = 'Booking confirmation '
     C                                    + %trim(HERFA)
     C                   ELSE
     C                   Eval      UEMNE = 'Bookingbekreftelse '
     C                                    + %trim(HERFA)
     C                   END
     C                   IF        HEAVD <> 0
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      XEMNE = ' Dept:'
     C                                    + %trim(%editc(HEAVD:'Z'))
     C                   ELSE
     C                   EVAL      XEMNE = ' Avd:'
     C                                    + %trim(%editc(HEAVD:'Z'))
     C                   END
     C                   CAT       XEMNE:0       UEMNE
     C                   END
      *
     C                   MOVE      *ZEROS        SRCSEQ
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Booking is received from  '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                   ELSE
     C                   EVAL      SRCDTA = 'Booking er mottatt fra  '
     C                             + %trim(BIBRID) + '/' + %trim(BIBESK)
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EXSR      SRMSG01
     C                   EXSR      SRMSG02
     C                   EXSR      SRMSG03
     C                   EXSR      SRMSG04
     C                   EXSR      SRMSG05
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = FIFT
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        UTYPE <> 'WEBPR'
     C                   EXSR      SRMSG10
     C                   END
     C                   END
     C     XUNIK         READE     ZREETXT1                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     SRMSG01       BEGSR
      ******************************************************************************
      *
     C                   EVAL      SRCDTA = 'Ref: ' + %trim(HERFA)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
N02   *
N02  C                   MOVEL     'ORF'         FSKODE
N02  C     ZRISOL02K     CHAIN(N)  ZRISOL02                           33
N02  C                   IF        *IN33 = *OFF
N02  C                   IF        XSPRÅK = 'EN'
N02  C                   EVAL      SRCDTA = 'Other ref: ' + %trim(FSSOK)
N02  C                   ELSE
N02  C                   EVAL      SRCDTA = 'Annen ref: ' + %trim(FSSOK)
N02  C                   END
N02  C                   ADD       1             SRCSEQ
N02  C                   WRITE     ARCDAT
N02  C                   END
N02  C                   MOVEL     '#VD'         FSKODE
N02   *
N02  C                   IF        HEDTOP <> 0 OR TRSDTD <> 0
N02  C                   EVAL      SRCDTA = *BLANKS
N02  C                   ADD       1             SRCSEQ
N02  C                   WRITE     ARCDAT
N02  C                   END
N02   *
N02  C                   IF        HEDTOP <> 0
N02  C                   IF        XSPRÅK = 'EN'
N02  C                   EVAL      SRCDTA = 'Pickingdate:'
N02  C                   ELSE
N02  C                   EVAL      SRCDTA = 'Hentedato:'
N02  C                   END
N02  C                   EVAL      SRCDTA = %trim(SRCDTA) + ' '
N02  C                             + %trim(%editw(HEDTOP:'    .  .  '))
N02  C                   ADD       1             SRCSEQ
N02  C                   WRITE     ARCDAT
N02  C                   END
N02   *
N02  C                   IF        TRSDTD <> 0
N02  C                   IF        XSPRÅK = 'EN'
N02  C                   EVAL      SRCDTA = 'Delivery date:'
N02  C                   ELSE
N02  C                   EVAL      SRCDTA = 'Leveringsdato:'
N02  C                   END
N02  C                   EVAL      SRCDTA = %trim(SRCDTA) + ' '
N02  C                             + %trim(%editw(TRSDTD:'    .  .  '))
N02  C                   ADD       1             SRCSEQ
N02  C                   WRITE     ARCDAT
N02  C                   END
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Shipper: '
     C                   ELSE
     C                   EVAL      SRCDTA = 'Avsender: '
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = '     ' + HENAS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = '     ' + HEADS1
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = '     ' + HEADS3
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Consignee: '
     C                   ELSE
     C                   EVAL      SRCDTA = 'Mottaker: '
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = '     ' + HENAK
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = '     ' + HEADK1
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = '     ' + HEADK3
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'From: ' + HELKA + ' ' + HESDF
     C                   ELSE
     C                   EVAL      SRCDTA = 'Fra: ' + HELKA + ' ' + HESDF
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'To: ' + HETRI + ' ' + HESDT
     C                   ELSE
     C                   EVAL      SRCDTA = 'Til: ' + HETRI + ' ' + HESDT
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Number of collies: '
     C                             + %trim(%editc(HENT:'Z'))
     C                   ELSE
     C                   EVAL      SRCDTA = 'Ant kll: '
     C                             + %trim(%editc(HENT:'Z'))
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Weight: '
     C                             + %trim(%editc(HEVKT:'Z'))
     C                   ELSE
     C                   EVAL      SRCDTA = 'Vekt: '
     C                             + %trim(%editc(HEVKT:'Z'))
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = 'M3: '
     C                             + %trim(%editc(HEM3:'4'))
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Goodsdescription:'
     C                   ELSE
     C                   EVAL      SRCDTA = 'Godsbeskrivelse:'
     C                   END
     C                   CAT       HEVS1:0       SRCDTA
     C                   CAT       ',':0         SRCDTA
     C                   CAT       HEVS2:1       SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Marks & Number:'
     C                   ELSE
     C                   EVAL      SRCDTA = 'Merking:'
     C                   END
     C                   CAT       HEGM1:0       SRCDTA
     C                   CAT       ',':0         SRCDTA
     C                   CAT       HEGM2:1       SRCDTA
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C     KODTOTYK      CHAIN     KODTOTY                            33
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Type of order: '
     C                             + %trim(KO1ETX)
     C                   ELSE
     C                   EVAL      SRCDTA = 'Oppdragstype: '
     C                             + %trim(KO1NTX)
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C     KODTFRK       CHAIN     KODTFR                             33
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Way of delevery: '
     C                             + %trim(KFRTTX)
     C                   ELSE
     C                   EVAL      SRCDTA = 'Leveringsbetingelse: '
     C                             + %trim(KFRNTX)
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     SRMSG02       BEGSR
      ******************************************************************************
      *
     C     ZREETXT1K     SETLL     ZREETXT1
     C     ZREETXT1K     READE     ZREETXT1                               33
     C                   MOVE      'N'           XLEDERTXT         1
     C                   DOW       *IN33 = *OFF
     C                   IF        FRTKOD = 'R'
      *
     C                   IF        XLEDERTXT = 'N'
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Message to consignee:'
     C                   ELSE
     C                   EVAL      SRCDTA = 'Melding til mottaker:'
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   MOVE      'J'           XLEDERTXT         1
     C                   END
      *
     C                   EVAL      SRCDTA = '            '
     C                             + %trim(FRTTXT)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   END
      *
     C     ZREETXT1K     READE     ZREETXT1                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     SRMSG03       BEGSR
      ******************************************************************************
      *
     C     ZREETXT1K     SETLL     ZREETXT1
     C     ZREETXT1K     READE     ZREETXT1                               33
     C                   MOVE      'N'           XLEDERTXT         1
     C                   DOW       *IN33 = *OFF
     C                   IF        FRTKOD = 'G'
      *
     C                   IF        XLEDERTXT = 'N'
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Message to carrier:'
     C                   ELSE
     C                   EVAL      SRCDTA = 'Melding til transportør:'
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   MOVE      'J'           XLEDERTXT         1
     C                   END
      *
     C                   EVAL      SRCDTA = '            '
     C                             + %trim(FRTTXT)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   END
      *
     C     ZREETXT1K     READE     ZREETXT1                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     SRMSG04       BEGSR
      ******************************************************************************
      *
     C     ZREETXT1K     SETLL     ZREETXT1
     C     ZREETXT1K     READE     ZREETXT1                               33
     C                   MOVE      'N'           XLEDERTXT         1
     C                   DOW       *IN33 = *OFF
     C                   IF        FRTKOD = ' '
      *
     C                   IF        XLEDERTXT = 'N'
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Message to forwarding agent:'
     C                   ELSE
     C                   EVAL      SRCDTA = 'Melding til speditør:'
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   MOVE      'J'           XLEDERTXT         1
     C                   END
      *
     C                   EVAL      SRCDTA = '            '
     C                             + %trim(FRTTXT)
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   END
      *
     C     ZREETXT1K     READE     ZREETXT1                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     SRMSG05       BEGSR
      ******************************************************************************
      *
     C     ZRISOL02K     SETLL     ZRISOL02
     C     ZRISOL02K     READE     ZRISOL02                               33
     C                   MOVE      'N'           XLEDERTXT         1
     C                   DOW       *IN33 = *OFF
      *
     C                   IF        XLEDERTXT = 'N'
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   IF        XSPRÅK = 'EN'
     C                   EVAL      SRCDTA = 'Uploaded documents:  '
     C                             + %trim(FSSOK)
     C                   ELSE
     C                   EVAL      SRCDTA = 'Opplastet dokumenter:'
     C                             + %trim(FSSOK)
     C                   END
     C                   ELSE
     C                   EVAL      SRCDTA = '                     '
     C                             + %trim(FSSOK)
     C                   END
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
     C                   MOVE      'J'           XLEDERTXT         1
      *
     C     ZRISOL02K     READE     ZRISOL02                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     SRMSG10       BEGSR
      ******************************************************************************
      *
     C                   EVAL      SRCDTA = *BLANKS
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = 'This message is created '
     C                             + ' automaticly by the booking system.'
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   EVAL      SRCDTA = 'Please do not reply!'
     C                   ADD       1             SRCSEQ
     C                   WRITE     ARCDAT
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     INIT          BEGSR
      ******************************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    UUNIK             9
     C                   PARM                    UTYPE             5
     C                   PARM                    UEMNE            70
     C                   PARM                    UAVNA            30
     C                   PARM                    UAVAD            50
     C                   PARM                    UINTAD1          50
     C                   PARM                    UINTAD2          50
     C                   PARM                    UINTAD3          50
     C                   PARM                    UINTAD4          50
     C                   PARM                    UINTAD5          50
     C                   PARM                    UOK               1
      *
     C                   MOVE      UUNIK         XUNIK             9 0
      * KeyS
     C     ZEADL02K      KLIST
     C                   KFLD                    XUNIK
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    BIKUNR
     C     BRPARKEY      KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
     C                   MOVE      *ZEROS        PAKUNR
     c     ZREETXT1K     klist
     c                   kfld                    HEUNIK
     c                   kfld                    HEREFF
     C     ZRISOL02K     klist
     C                   kfld                    HEUNIK
     C                   kfld                    HEREFF
     C                   kfld                    FSKODE
     C                   MOVEL     '#VD'         FSKODE
     c     KODTOTYK      KLIST
     c                   KFLD                    KO1UNI
     c                   KFLD                    HEOT
     c                   MOVE      'OTY'         KO1UNI
     c     KODTFRK       KLIST
     c                   KFLD                    KFRUNI
     c                   KFLD                    HEFR
     c                   MOVE      'FR'          KFRUNI
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
