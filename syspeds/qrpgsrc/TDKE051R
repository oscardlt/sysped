      *********************************************************************
CRTPG+*  CRTPGM SYSPED/TDKE051R MODULE(*LIBL/TDKE051R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKEH      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D DKEH_SYAVA      S              4
     D DKEH_SYAV       S              4  0
     D DKEH_SYOPA      S              7
     D DKEH_SYOP       S              7  0
     D STATUS          S              2
     D JSONstring      S          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(DKEH_SYAV:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(DKEH_SYOP:'Z'))+'¬, '
        +'¬status¬ : ¬'+%trim(STATUS)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"stsUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKEH

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      DKEH_SYAVA = zhbgetvar('AVD')
     C                   EVAL      DKEH_SYAV  = c2n2(DKEH_SYAVA)
     C                   EVAL      DKEH_SYOPA = zhbgetvar('OPD')
     C                   EVAL      DKEH_SYOP  = c2n2(DKEH_SYOPA)
     C                   EVAL      STATUS=   zhbgetvar('STATUS')
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DKEHKey       klist
     C                   kfld                    DKEH_SYAV
     C                   kfld                    DKEH_SYOP

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKEH

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hæmta ærende
     C     DKEHKey       CHAIN     DKEH                               55
      * Update
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XDKEH_SYST=STATUS
     C                   UPDATE    DKEHR
     C                   ENDSR
