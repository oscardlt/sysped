*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
N01  A* INNFØRT F=FAKTURA
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEF02PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV KUNDERS FL-
     A                                      YFRAKTRATER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            KFFKN          8S 0H
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            KFFPT          1A  O  6  4
     A            KFFLY          3S 0O  6  6
     A            WSNA          20A  O  6 10
     A            KFFSDF         5A  O  6 31
     A            KFFSDT         5A  O  6 37
     A            KFFKD          1A  O  6 43
     A            KFFTVK         5Y 0O  6 46EDTCDE(4)
     A            KFFBL         13Y 2O  6 52EDTCDE(4)
     A            KFFBL2         3Y 0O  6 68EDTCDE(4)
     A            KFFVAL         3   O  6 73
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYEF02'
     A                                      COLOR(BLU)
     A            MSG101        40A  O  1 19MSGID(DIV 1144 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            WSKN           8S 0O  2 19
     A            KNAVN         30A  O  2 29
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(078 DIV1145 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF08(08 'SEARCH')
     A                                      CF10(10 'DELETE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG201        18A  O  2  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  2 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  2 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  2 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV2206 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'PROMT')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSKN           8S 0H
     A                                  1  2MSGCON(070 DIV1146 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSPT           1A  B  2  2
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A            WSFLY          3Y 0B  2  5EDTCDE(4)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A            WSSDF          5A  B  2  9
     A  67
     AO 69                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A            WSSDT          5A  B  2 15
     A  67
     AO 68                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A            WSKD           1A  B  2 23
     A  67
     AO 66                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A                                      VALUES(' ' 'H' 'S' 'F')
     A            WSTVK          5Y 0B  2 26
     A  67                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSBL          13Y 2B  2 32DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSBL2          3Y 0B  2 50DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSVAL          3   B  2 55
     A  65                                  DSPATR(RI)
     A N40        MSG302        18A  O  3  2MSGID(DIV 0032 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG303        18A  O  3 22MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 18MSGCON(046 DIV1148 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(015 DIV1147 *LIBL/SYMSGF)
     A            W2PT           1   B  2 18
     A            W2FLY          3Y 0B  2 20EDTCDE(4)
S01  A*           W2KN           8Y 0B  2 22DSPATR(UL)
S01  A*                                     DSPATR(PR)
S01  A*                                     EDTCDE(4)
     A            W2SDF          5A  B  2 24DSPATR(UL)
     A            W2SDT          5A  B  2 31DSPATR(UL)
     A            W2KD           1A  B  2 39DSPATR(UL)
     A            W2TVK          5Y 0B  2 42DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(075 DIV1146 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KFFPT          1A  O  2  2COLOR(WHT)
     A            KFFLY          3S 0O  2  4COLOR(WHT)
     A            KFFSDF         5A  O  2  9COLOR(WHT)
     A            KFFSDT         5A  O  2 15COLOR(WHT)
     A            KFFKD          1A  O  2 24COLOR(WHT)
     A            KFFTVK         5Y 0O  2 28COLOR(WHT)
     A                                      EDTCDE(4)
     A            KFFBL         13Y 2O  2 34COLOR(WHT)
     A                                      EDTCDE(4)
     A            KFFBL2         3Y 0O  2 50COLOR(WHT)
     A                                      EDTCDE(4)
     A            KFFVAL         3A  O  2 55COLOR(WHT)
     A            KFFKN          8Y 0O  2 64DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF08(08 'SEARCH')
     A                                  1  2'SYSPED/SYEF02'
     A                                      COLOR(BLU)
     A            MSG601        40A  O  1 19MSGID(DIV 1144 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A            WSKN           8S 0O  2 19
     A            KNAVN         30A  O  2 29
     A            MSG602        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG603        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG604        78A  O  5  2MSGID(DIV 1145 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG605        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG606        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG607        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG608        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde')
     A                                      CF12(12 'Gå til forrige bilde')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYEF02'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 DIV1144 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                 11  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKN           8Y 0B 11 17DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                 14  2MSGCON(030 DIV2205 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKNRK         8Y 0B 16 17DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 18  2MSGCON(070 SHU1179 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 20  2MSGCON(004 SHU1180 *LIBL/SYMSGF)
     A            WSFRA          3   B 20  8
     A                                 20 14MSGCON(004 SHU1181 *LIBL/SYMSGF)
     A            WSTIL          3   B 20 20
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R DLTAVT
     A                                      CF05(05 'CONFORM DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(15)
     A                                      CLRL(04)
     A                                      ALARM
     A                                      OVERLAY
     A                                  1  8'|¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯-
     A                                      ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯|'
     A                                      COLOR(BLU)
     A                                  2  8'|'
     A                                      COLOR(BLU)
     A                                  2 11MSGCON(60 DIV2207 SYMSGF)
     A                                      DSPATR(PC)
     A                                      COLOR(RED)
     A                                  2 74'|'
     A                                      COLOR(BLU)
     A                                  3  8'|'
     A                                      COLOR(BLU)
     A                                  3 11MSGCON(60 DIV2208 SYMSGF)
     A                                      COLOR(RED)
     A                                  3 74'|'
     A                                      COLOR(BLU)
     A                                  4  8'|_________________________________-
     A                                      ________________________________|'
     A                                      COLOR(BLU)
