*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH PTF8 NY HELP - FUNKSJON
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTP)
     A                                      PRINT
S01  A*                                     HLPDOC('SYTR140000' 'SYTR14' 'SY400-
S01  A*                                     LO/TRANS')
N01  A                                      HLPPNLGRP(HLPSCR SYTR14PNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD AV OPPDRAG -
N01  A                                      (TRANSPORTMODULEN)')
     A                                      ALTHELP
     A                                      HELP
     A          R SUBFIL                    SFL
     A            W2NR           1A  I  6  3VALUES(' ' '2' '4')
     A            KGIKOD         3A  O  6  8COLOR(WHT)
     A            KGIINT         5S 0O  6 25
     A            KGIKOS         5S 0O  6 35
     A            KGIAVD         4S 0O  6 13
     A            KGIBKO         3A  O  6 19
     A            KGIBYT         1   O  6 43
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                      COLOR(BLU)
     A                                  1  2'SYSPED/SYTR14'
     A                                  1 19MSGCON(035 DIV3757 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 56DATE
     A                                      EDTCDE(Y)
     A                                  1 66TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV3756 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CF05(05 'Frisk')
     A                                      CF03(03 'Avslutt')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'Forrige')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF06(06 'ny')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(070 DIV3756 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KGIKOD         3A  B  2  7
     A N40                                  DSPATR(PC)
     A  40                                  DSPATR(PR)
     A            KGIAVD         4Y 0B  2 13EDTCDE(4)
     A  40                                  DSPATR(PR)
     A            KGIBKO         3A  B  2 19
     A  73                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A            W2IINT         5Y 0B  2 25EDTCDE(4)
     A  70                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A            W2IKOS         5Y 0B  2 35EDTCDE(4)
     A  71                                  DSPATR(RI)
     A            W2BYTT         1A  B  2 43
     A  73                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A  30        MSGTXT        78A  O  4  1MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            WSIKOD         3A  I  2 15
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
