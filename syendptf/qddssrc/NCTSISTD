      *__________________________________
      * SYSPED / Tolldekl. TRANSITT
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R NCTSISTDR
     A            TIST      R               REFFLD(ST)
      * THST = ' ' --> ÅPEN TRANSITTERING
      *        'E' --> HOLD PÅ Å BLI ENDRET
      *        'Q' --> I UTGÅENDE POSTKASSE
      *        '+' --> MERKET FOR Å SENDE UT
      *        'A' --> HOLD PÅ Å SENDE UT
      *        'B' --> MELDING ER SENDT TIL IBM
      *        'C' --> IE07 SENDT (ANKOMST MELDING)
      *        'U' --> IE43 MOTTATT (LOSSETILLATELSE)
      *        'V' --> IE43 MOTTATT (LOSSETILLATELSE) PAPIRLASTLISTE
      *        'F' --> IE44 SENDT (LOSSEMERKNAD)
      *        'P' --> IE25 MOTTATT (TRANSITTERING ER SLUTTET)
      *        'M' --> FEILMELDING FRA TOLLVESENET (IE08 ELLER CONTRL)
      *        'N' --> FEILMELDING FRA TOLLVESENET (IE58 ELLER CONTRL)
      *        'K' --> VENT PÅ AT LOSSETILLATELSE BLIR UTSKREVET
      *        'Å' --> MERKET FEILMELIDNG FØR DET SKRIVES UT
     A            TIAVD          4S 0       COLHDG('AVDELING')
     A            TITDN     R               REFFLD(TDN)
     A            TISG      R               REFFLD(SG)
     A            TITARF        16          COLHDG('TARIFFØRSNAVN')
     A            TIWS      R               REFFLD(WS)
     A            TIDTR     R               REFFLD(DT)
     A                                      COLHDG('REGISTRERINGSDATO')
     A            TIDTF     R               REFFLD(DT)
     A                                      COLHDG('FRIGIVELSESDATO')
     A            TITRNR        18A         COLHDG('MRN-NR')
     A            TIGN          35A         COLHDG('GODSNR')
     A            TIDTA     R               REFFLD(DT)
     A                                      COLHDG('ANKOMST DATO')
     A            TITMA     R               REFFLD(TM)
     A                                      COLHDG('ANKOMST TID')
     A            TISPR          1S 0       COLHDG('FORENKLET PROSEDYRE')
     A            TIIFLG         1S 0       COLHDG('HENDELSESFLAGG')
     A            TIATYP         4A         COLHDG('1. AUTORISATIONSTYPE')
     A            TIAREF        35A         COLHDG('1. AUTORISATIONSREF.')
      * CUSTOMS OFFICE OF DESTINATION
     A            TITSBA         8A         COLHDG('DESTINASJONSTOLLSTED')
      * TRADER AT DESTINATION
     A            TIKNTA    R               REFFLD(KN)
     A                                      COLHDG('Kundenr')
     A            TINATA    R               REFFLD(NA)
     A                                      COLHDG('Navn')
     A            TITINTA       17A         COLHDG('TIN-KODE')
     A            TISKTA         2A         COLHDG('SPRÅKKODE')
      * CONSIGNMENT
      *  TYPE OF LOCATION
     A            TILCTY         1A         COLHDG('TYPE AV LOKASJON')
     A            TIQID          1A         COLHDG('QUALIFIER OF ID.')
     A            TIANR         35A         COLHDG('AUTORISATION NUMBER')
     A            TIAID          4A         COLHDG('ADDITIONAL IDENTIFIE')
     A            TISD          17A         COLHDG('UN LOCODE')
      * CUSTOMS OFFICE
     A            TITS           8A         COLHDG('CUSTOMS OFFICE')
      * GNSS
     A            TILGLA        17A         COLHDG('LOC. LATITUDE')
     A            TILGLO        17A         COLHDG('LOC. LONGITUDE')
      * ECONOMIC OPERATOR
     A            TIKNE     R               REFFLD(KN)
     A                                      COLHDG('Kundenr ØKONOMI OPR')
     A            TITINE        17A         COLHDG('TIN-KODE ØKONOMI OPR')
     A            TIADE1        30A         COLHDG('ADR-1 ØKONOMI OPR')
     A            TIPNE         17A         COLHDG('POSTNR ØKONOMI OPR')
     A            TIPSE         24A         COLHDG('POSTSTED ØKONOMI OPR')
     A            TILKE          2A         COLHDG('LANDKODE ØKONOMI OPR')
     A            TIADE2        17A         COLHDG('HUS.NR ØKONOMI OPR')
     A            TIPNE2        17A         COLHDG('POSRNR HUS ØKONOMI')
     A            TILKE2         2A         COLHDG('LANDKODE HUS ØKONOMI')
     A            TICPE         30A         COLHDG('KONTAKTPERSON ØKONOM')
     A            TITLE         20A         COLHDG('TLF K.PERS. ØKONOMI')
     A            TIEME         50A         COLHDG('EMAIL K.PERS. ØKONOM')
      * TVINN - VARIABLER
     A            S0004     R               REFFLD(N0004)
     A            S0010     R               REFFLD(N0010)
     A            S0035     R               REFFLD(N0035)
     A            S0026     R               REFFLD(N0026)
      *
      *================================================================
      * FRA IE43 LOSSETILLATELSE  MESSAGE AND MASTER COMSIGNMENT
      *================================================================
     A            TIDK           5A         COLHDG('DEKLARASJONSKODE')
     A            TIDTG     R               REFFLD(DT)
     A                                      COLHDG('DECL ACCEPTANCE DATE')
     A            TISECU         1S 0       COLHDG('SECURITY')
     A            TIRDIN         1S 0       COLHDG('REDUCED DATASET IND.')
      * CUSTOMS OFFICE OF DESTNATION
     A            TITSB          8A         COLHDG('BESTEMMELSES T.STED')
      * HOLDER OF THE TRANSIT PROCEDURE
     A            TITINH        17A         COLHDG('ID NUMMER')
     A            TITIRH        17A         COLHDG('TIR HOLDER ID NUMBER')
     A            TINAH         30A         COLHDG('HOLDER NAVN')
     A            TIAD1H        30A         COLHDG('HOLDER ADDESS 1')
     A            TIPNH         17A         COLHDG('HOLDER POST CODE')
     A            TIPSH         24A         COLHDG('HOLDER CITYE')
     A            TILKH          2A         COLHDG('HOLDER COUNTRY')
      * TRADER AT DESTINATION
     A            TITINT        17A         COLHDG('TIN-KODE')
      * CTL_CONTROL
     A            TISTLO         1S 0       COLHDG('CONTINUE UNLOADING')
      *  BESTEMMELSES LAND  RUBR. 17A
     A            TIBLK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL B.LAND')
      *  CONTAINER  RUBR. 19
     A            TIKDC     R               REFFLD(KDC)
     A            TITMIN         1S 0       COLHDG('INLAND TR.MOD.')
     A            TIVKB         13S 3       COLHDG('BRUTTOVEKT')
      *  CONSIGNOR
     A            TITINS        17A         COLHDG('TIN-KODE SELGER')
     A            TINAS         30A         COLHDG('NAVN SELGER')
     A            TIAD1S        30A         COLHDG('ADRESSE-1 SELGER')
     A            TIPNS         17A         COLHDG('POSTNR SELGER')
     A            TIPSS         24A         COLHDG('POSTSTED SELGER')
     A            TILKS          2A         COLHDG('LANDKODE SELGER')
      *  CONSIGNEE
     A            TITINK        17A         COLHDG('TIN-KODE KJØPER')
     A            TINAK         30A         COLHDG('NAVN KJØPER')
     A            TIAD1K        30A         COLHDG('ADRESSE-1 KJØPER')
     A            TIPNK         17A         COLHDG('POSTNR KJØPER')
     A            TIPSK         24A         COLHDG('POSTSTED KJØPER')
     A            TILKK          2A         COLHDG('LANDKODE KJØPER')
      *  TRANSPORT EQUIPMENT
     A            TICNR         17A         COLHDG('CONTAINE')
     A            TIDANT         4S 0       COLHDG('ANTALL FORSEGLING')
      *   SEAL
     A            TIDFKD        20A         COLHDG('FORSEGLING-ID KODE')
      *   GOODS REFERENCE
     A            TIGINR         5S 0       COLHDG('GOODS ITEM NUMBER')
      *  TRANSPORTMÅTE  RUBR. 25
     A            TITRM     R               REFFLD(TRM)
      *  TRANSPORTMIDDEL VED AVGANG  RUBR. 18
     A            TITAID        35A         COLHDG('IDENTITET')
     A            TITALK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
      *  PREVIOUS DOCUMENT
     A            TIPDTY         4A         COLHDG('DOK. TYPEE')
     A            TIPDRF        70A         COLHDG('DOK. REF.NR')
     A            TIPDIN        35A         COLHDG('KOMPL. INFO.')
      *  SUPPORTING DOCUMENT
     A            TISDTY         4A         COLHDG('DOK. TYPEE')
     A            TISDRF        70A         COLHDG('DOK. REF.NR')
     A            TISDLN         5S 0       COLHDG('LINE ITEM NUMBER')
     A            TISDIN        35A         COLHDG('KOMPL. INFO.')
      *  TRANSPORT DOCUMENT
     A            TITDTY         4A         COLHDG('DOK. TYPEE')
     A            TITDRF        70A         COLHDG('DOK. REF.NR')
      *  ADDITIONAL DOCUMENT
     A            TIADTY         4A         COLHDG('DOK. TYPEE')
     A            TIADRF        70A         COLHDG('DOK. REF.NR')
      *  ADDITIONAL INFORMATION
     A            TIAICD         5A         COLHDG('INFO. CODE')
     A            TIAITX        70A         COLHDG('INFO. TEXT')
      *
      *================================================================
      *
     A          K TIAVD
     A          K TITDN
