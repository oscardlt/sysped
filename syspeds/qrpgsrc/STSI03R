      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSI03R   MODULE(*LIBL/STSI03R  *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 oppdater dokufm ved fullfør
10172 * 02 SH  PTF10 norsk text til Track and trace
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FSTSCLI    UF   E           K DISK    usropn
     FSTSfbr    UF   E           K DISK    usropn
     FSTSopd    UF   E           K DISK    usropn
     FSTStur    UF   E           K DISK    usropn
     FDISP      UF   E           K DISK    usropn
     FHEADF     UF   E           K DISK    usropn
     Ffirm      IF   E           K DISK    USROPN
     FDOKUF     IF   E           K DISK    USROPN
N01  FDOKUFM1   UF   E           K DISK    USROPN
     FTRACKF    O  A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Array for konv. av alfa til numerisk
     D FE              S              1    DIM(15)
      * Variables received from the input string
     D wspro           s              8  0
     DTurnr            s              8
     D User            s             10
     D FullF           s              1
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D Antp            s              3  0
     D                 DS
     D  SOMERK                 1     30
     D  TSTSOMERK              1     12
     D                 DS
n02  D  SOMERKL                1     30
n02  D  TSTSOMERKL             1     12
n01  D                 DS
n01  D  SCCLID                 1     20
n01  D  SCCLID18               3     37
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D  XULDDMM                7     10  0
     D  XULÅÅ2                13     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  DFBREF                 1     17
     D  DFBRTX                 1      5
     D  DFBRAV                 6      9
     D  DFBRST                10     10
     D  DFBROP                11     17
      *--------------------------------------------------------------------
      * Prolog common to CGI programs   (C-specs)
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      *
     C                   eval      USER   = zhbgetvar('user')
     C                   eval      TURNR  = zhbgetvar('turnr')
     C                   eval      wspro  = c2n2(TURNR)
     C                   eval      YTYPE  = zhbgetvar('YTYPE')
     C                   eval      Fullf  = zhbgetvar('fullf')
      *
     C                   open      BRIDF
     C     User          CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * INITIERING...
     C     *LIKE         DEFINE    SCTYPE        YTYPE
     C     TURKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C     DispKey       KLIST
     C                   KFLD                    SOAVD
     C                   KFLD                    SOOPD
     C     OPDKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C                   KFLD                    SOAVD
     C                   KFLD                    SOOPD
     C     DUPKEY        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   MOVE      'F'           DFRI
      *
     c                   open      stscli
     c                   open      stsfbr
     c                   open      stsopd
     c                   open      ststur
     c                   open      headf
     c                   open      disp
     c                   open      firm
     c                   open      trackf
     c                   open      DOKUF
n01  c                   open      DOKUFM1
      *
     c     *loval        setll     firm
     c                   read      firm
      *
     C     wspro         ifne      *zeros
      *-Sett fullført-merke - Y = fullføre
     c     Fullf         Ifeq      'Y'
     C                   exsr      FULL
     C                   else
      *-Delete fra workfiles - N = Nullstill
     c     Fullf         Ifeq      'N'
     C                   exsr      DCLI
     C                   end
     C                   end
     C                   end
      *-A = Avbryte / parkere...
      *
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     c     user          ifeq      'NN'
     C                   eval      Mbr = 'STS01B'
     c                   else
     C                   eval      Mbr = 'STS02'
     c                   end
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('user':user)
     c     user          ifeq      'NN'
     C                   callp     updhtmlvar('user':spaces)
     c                   end
     C                   callp     updHTMLvar('besk':BIBESK)
     C                   callp     updHTMLvar('kunr':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHTMLvar('FEILTXT':spaces)
      *
     C                   callp     wrtsection('all')
     C                   callp     wrtsection('*fini')
      *
     c                   close     bridf
     c                   close     stscli
     c                   close     stsfbr
     c                   close     stsopd
     c                   close     ststur
     c                   close     disp
     c                   close     headf
     c                   close     firm
     c                   close     trackf
     c                   close     DOKUF
n01  c                   close     DOKUFM1
     C                   eval      *inlr = *on
     C                   RETURN
     C***********************************************
     C     FULL          BEGSR
     C***********************************************
      * Set status Fullført manuelt...
     C     TurKey        chain     STStur                             30
     C     *in30         IFEQ      '0'
      * X=fullført lest  / M=avsluttet ed manko
     C     STANOK        IFGE      STANOP                                           5<-B
     C                   move      'X'           ststat
     C                   ELSE                                                       5<-E
     C                   move      'M'           ststat
     C                   END                                                        5<-E
     C                   UPDATE    STSTURR
     C                   end
      *
     C     TurKey        SETLL     STSopd
     C     TurKey        READE     STSopd                                 94
     C     *IN94         DOWEQ     '0'
     c     SOSTAT        IFNE      ' '
     C                   EXSR      TTSR
     c     TTACTI        ifeq      'TEI'
     C     DispKey       Chain     DISP                               33
     c     *in33         IFEQ      '0'
     c     DISTA1        IFEQ      'E'
     c     DISTA1        OREQ      'V'
     C                   move      ' '           DISTA1
     c                   end
     c                   update    DISPR
     C                   EXSR      Paller
     c                   end
     C                   END
     c                   END
     c                   exsr      IntDUP
     C     TurKey        READE     STSopd                                 94
     C  N94              ENDDO
      *
n01
n01  C     TurKey        SETLL     STSCLI
n01  C     TurKey        READE(N)  STSCLI                                 94
n01  C     *IN94         DOWEQ     '0'
n01  c     SCCLID18      chain     dokufm1                            33
n01  C     *IN33         ifeq      '0'
n01  c                   move      scstat        FMITER
n01  c                   move      SCDT          FMITDT
n01  c                   move      *zeros        FMITTM
n01  c                   movel     SCTID         FMITTM
n01  c                   move      SCUSER        FMITUS
n01  c                   update    dokufmr
n01  C                   endif
n01  C     TurKey        READE(N)  STSCLI                                 94
n01  C                   ENDdo
      *
     C                   ENDSR
      *
      *
     C***********************************************
     C     INTDUP        BEGSR
     C***********************************************
     C                   MOVE      SOAVD         DFAVD
     C                   MOVE      SOOPD         DFOPD
     C     DUPKEY        SETLL     DOKUF
     C     DUPKEY        READE     DOKUF                                  35
     C     *IN35         DOWEQ     '0'
     C* DERSOM FRAKTBRES EVR SENDT SOM INTERN DUP - OPPDATER UNDERLIGGENDE
     C     DFBRTX        IFEQ      'U.op:'
     C     DFBRAV        ANDGE     '0001'
     C     DFBRAV        ANDLE     '9999'
     C     DFBROP        ANDGE     '0000001'
     C     DFBROP        ANDLE     '9999999'
     C                   MOVE      DFBRAV        SOAVD
     C                   MOVE      DFBROP        SOOPD
     C                   EXSR      TTSR
     c     TTACTI        ifeq      'TEI'
     C     DispKey       Chain     DISP                               33
     c     *in33         IFEQ      '0'
     c     DISTA1        IFEQ      'E'
     c     DISTA1        OREQ      'V'
     C                   move      ' '           DISTA1
     c                   end
     c                   update    DISPR
     c                   end
     C                   END
     c                   END
     C     DUPKEY        READE     DOKUF                                  35
     C                   END
     C                   ENDSR
      *
     C***********************************************
     C     PALLER        BEGSR
     C***********************************************
      * Tr.modul innland - oppdater antall paller
     C                   move      *zeros        AntP
     C     OpdKey        SETLL     STSCLI
     C     OpdKey        READE(N)  STSCLI                                 94
     C     *IN94         DOWEQ     '0'
     c     SCSTAT        IFEQ      'P'
     C                   ADD       1             AntP
     C                   END
     C     OpdKey        READE(N)  STSCLI                                 94
     C  N94              ENDDO
      *
      * Tr.modul innland -  oppdater paller
     C     AntP          IFGT      *Zeros
     C     DispKey       Chain     HEADF                              33
     c     *in33         IFEQ      '0'
     c     HXSNDN        IFNE      *Blanks
     C                   MOVE      Antp          HXPALL
     C                   end
     C                   UPDATE    HEADFR
     C                   end
     C                   end
      *
     C                   ENDSR
      *
      *
      *
     C***********************************************
     C     DCLI          BEGSR
     C***********************************************
      *
     C     TurKey        SETLL     STSCLI
     C     TurKey        READE     STSCLI                                 94
     C     *IN94         DOWEQ     '0'
     c                   delete    stsclir
     C     TurKey        READE     STSCLI                                 94
     C  N94              ENDDO
      *
     C     TurKey        SETLL     STSfbr
     C     TurKey        READE     STSfbr                                 94
     C     *IN94         DOWEQ     '0'
     c                   delete    stsfbrr
     C     TurKey        READE     STSfbr                                 94
     C  N94              ENDDO
      *
     C     TurKey        SETLL     STSopd
     C     TurKey        READE     STSopd                                 94
     C     *IN94         DOWEQ     '0'
     c                   delete    stsopdr
     C     TurKey        READE     STSopd                                 94
     C  N94              ENDDO
      *
     C     TurKey        SETLL     STStur
     C     TurKey        READE     STStur                                 94
     C     *IN94         DOWEQ     '0'
     c                   delete    ststurr
     C     TurKey        READE     STStur                                 94
     C  N94              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
      * HENT DATO/KLOKKESLETT
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      SOAVD         TTAVD
     C                   MOVE      SOOPD         TTOPD
     C                   MOVE      'TEI'         TTACTI
     C                   MOVE      ULDT          TTDATE
     C                   MOVE      XULTM         TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      BIBRID        TTUSER
     C*                  EVAL      TTTEXT = 'Arrived on terminal ' + FIKRTN
     C                   EVAL      TTTEXT = 'Arrived terminal ' + FIKRTN
n02  C                   EVAL      TTTEXl = 'Ankommet terminal ' + FIKRTN
      * * manuelt merket(eller "Rest OK) - da er antall eller merknadsfelt utfylt..
     C     SOAMKK        IFNE      *ZEROS
     C     SOMERK        Orne      *BLANKS
     C                   EVAL      TTTEXT = 'Arr term. ' + FIKRTN
n02  C                   EVAL      TTTEXL = 'Ank term. ' + FIKRTN
     C     DispKey       Chain(N)  HEADF                              33
     C     SOAMKK        IFNE      HENT
      * manuelt merket med 0 levert = SHORT - TEI -> TES
     C     SOAMKK        IFEQ      *zeros
     C     SOMERK        IFEQ      'Rest OK!'
     C     TSTSOMERK     oreq      'Unloaded/che'
     c                   z-add     hent          SOAMKK
     C                   else
     C                   MOVE      'TES'         TTACTI
     C                   END
     C                   END
     c     TSTSOMERK     IFNE      'Unloaded/che'
     C                   EVAL      TTTEXT   = %trim(TTTEXT) + '  '  +
     C                             %trim(%editc(SOAMKK:'3')) + '/' +
     C                             %trim(%editc(HENT:'Z'))
n02  C                   EVAL      TTTEXL   = %trim(TTTEXL) + '  '  +
n02  C                             %trim(%editc(SOAMKK:'3')) + '/' +
n02  C                             %trim(%editc(HENT:'Z'))
     c                   end
      * Manuell / ant levert ulik ant på oppdraget og blank i tekst - ... unloaded.
     C     SOMERK        IFEQ      *BLANKS
     C                   EVAL      SOMERK = 'unloaded.'
n02  C                   EVAL      SOMERKL = 'Losset.'
     C                   END
     C                   END
     C     SOMERK        IFNE      *BLANKS
     C                   EVAL      TTTEXT = %trim(TTTEXT) + '  ' + SOMERK
n02  C                   EVAL      TTTEXL = %trim(TTTEXL) + '  ' + SOMERKL
     C                   END
     C                   END                                                    manuell (merk/ant)
     C                   MOVEL     FIKRTN        TTDEPO
     C                   MOVE      'S'           TTMANU
      * låner NAME for å loggføre hvilket løpenr i skyting inn
     c                   movel     wspro         TTNAME
      * generell test om gitt kode finnes i T&T - 0 i dato=IKKE
     c                   call      'SYGE46'
     c                   parm                    TTAVD
     c                   parm                    TTOPD
     c                   parm                    TTFBNR
     c                   parm                    TTACTI
     c                   parm      'F'           xxtype            1
     c                   parm                    xxdate            8 0
     c                   parm                    xxtime            6 0
     C                   parm                    xxant             5 0
     c     xxdate        ifeq      *zeros
     C                   WRITE     TRACKFR
     c                   end
     C                   ENDSR
