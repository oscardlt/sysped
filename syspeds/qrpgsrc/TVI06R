      *
      ***********************************************************
      *** PROGRAM SKAL BEREGN CIF-VERDI                       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 05 06 09 11 16 18-22 25 56
      *** LEDIGE INDIKATORER 27 34 37-60 63-67 74 75          ***
      *** ALLE VARIABLER SOM STARTER MED 'X' ER ARBERDSVAR.   ***
      ***   OG DEFINERES I PROGRAM                            ***
      ***********************************************************
      *
     FSADH      IF   E           K DISK
     FKODTLB    IF   E           K DISK
     FKODTVAL2  IF   E           K DISK
      *** SI27 I SADH ER TATT I BRUK (FØRST 15 POS.)
      *** PGA. ENDRING AV REGISTER BESKRIVELSE (SE SADH)
      *** GJØR VI FØLGENDE DOBBEL DEFINISJON.
     D                 DS
     D  SIBELC                 1     11  2
     D  SIRAB                 12     15  2
     D  SI27                   1     27
      *
     C                   EXSR      INIT
     C     SADHK         CHAIN     SADH                               33
     C                   IF        SIMF <> 'DFO'
     C                   EXSR      CIFSR
     C                   END
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     CIFSR         BEGSR
      ***********************************************
      *
      *** OMREGN FAKTURASUM TIL NORSK KRONE
      *** OG LEGG DETTE PÅ CIFSUM (UBEL4)
     C                   MOVE      SIVAL3        XVALKO
     C     KODVAK        SETGT     KODTVAL2
     C     SIVAL3        READPE    KODTVAL2                               33
     C  N33              MOVE      KVAOMR        XOMRF             3 0
     C  N33SIVKU         IFEQ      0                                            1<-B
     C                   MOVE      KVAKRS        SIVKU
     C                   END                                                    1<-E
     C     SIVAL2        IFEQ      SIVAL3
     C     SIBEL2        ANDNE     *ZEROS
     C                   MOVE      SIVKU         XKRS2
     C                   END
     C     XOMRF         IFNE      0
     C     SIBEL3        MULT      SIVKU         XWORK            16 5
      *   AVRUND CIFSUM
     C     XWORK         DIV(H)    XOMRF         XFAKNO           11 2
     C                   Z-ADD     XFAKNO        UBEL4            11 2
     C                   END
     C                   MOVE      *ZEROS        XKOSTN           15 4
      *
      *** EGET FELT FOR PROSENTRABATT I TILLEGG TIL ANDRE KOSTNAD
     C     SIRAB         IFNE      0
     C     UBEL4         DIV       100           XWORK
     C     XWORK         MULT      SIRAB         XBELC            11 2
     C     SIDP          IFEQ      49
     C                   MULT(H)   0.2           XBELC
     C                   END
     C                   SUB(H)    XBELC         UBEL4
     C                   ELSE
     C                   MOVE      *ZEROS        XBELC
     C                   END
      *
      *** OMREGN FRAKT TIL NORSK KRONE
      *** OG LEGG DETTE PÅ CIFSUM (UBEL4)
     C     SIBEL1        IFNE      0
     C                   MOVE      SIVAL1        XVALKO
     C     KODVAK        SETGT     KODTVAL2
     C     SIVAL1        READPE    KODTVAL2                               33
     C  N33              MOVE      KVAOMR        XOMR1             3 0
     C  N33              MOVE      KVAKRS        XKRS1             7 3
     C     SIBEL1        MULT(H)   XKRS1         XKOSTN
     C     XKOSTN        DIV(H)    XOMR1         XKOSTN
     C                   ADD(H)    XKOSTN        UBEL4
     C                   END
     C                   MOVE      *ZEROS        XKOSTN
      *
      *** OMREGN ANDRE KOSTNAD TIL NORSK KRONE
      *** OG LEGG DETTE PÅ CIFSUM (UBEL4)
     C                   MOVE      SIBEL2        XBELB             5 2
     C                   MOVE      SIVAL2        XVALB             3
     C     SIVAL2        IFEQ      'P  '
     C     SIBEL3        DIV       100           XWORK
     C     XWORK         MULT      SIBEL2        XBEL2            11 2
     C                   MOVE      SIVAL3        XVAL2             3
     C                   ELSE
     C     SIVAL2        IFEQ      'F  '
     C                   MOVE      SIVAL3        XVAL2
     C     SIBEL2        SUB       SIBEL3        XBEL2
     C                   ELSE
     C                   MOVE      SIBEL2        XBEL2
     C                   MOVE      SIVAL2        XVAL2
     C                   END
     C                   END
     C     XVAL2         IFEQ      *BLANKS
     C                   MOVE      UFICURR       XVAL2
     C                   END
     C                   MOVE      XVAL2         XVALKO
     C     KODVAK        SETGT     KODTVAL2
     C     XVAL2         READPE    KODTVAL2                               33
     C  N33              MOVE      KVAOMR        XOMR2             3 0
     C  N33              MOVE      KVAKRS        XKRS2             7 3
     C     XBEL2         IFNE      0
     C     XBEL2         MULT(H)   XKRS2         XKOSTN
     C     XKOSTN        DIV(H)    XOMR2         XKOSTN
     C     SIDP          IFEQ      49
     C                   MULT(H)   0.2           XKOSTN
     C                   END
     C     SIFTG2        IFEQ      '+'
     C     SIFTG2        OREQ      'P'
     C                   ADD(H)    XKOSTN        UBEL4
     C                   ELSE
     C                   SUB(H)    XKOSTN        UBEL4
     C                   END
     C                   END
      *
      *** SJEKK OM FORSIKRING SKAL BEREGNES
     C     KODLBK        CHAIN     KODTLB                             33
     C     KLBFOK        IFEQ      'A'
     C     UBEL4         MULT      KLBPRM        XWORK
     C     XWORK         DIV(H)    1000          XBELA            11 2
     C     SIDP          IFEQ      49
     C                   MULT(H)   0.2           XBELA
     C                   END
     C                   ELSE
     C                   MOVE      *ZEROS        XBELA
     C                   END
     C                   ADD       XBELA         UBEL4
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SADHK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     KODLBK        KLIST
     C                   KFLD                    KLBUNI
     C                   KFLD                    SILV
     C     KODVAK        KLIST
     C                   KFLD                    XVALKO            3
     C                   KFLD                    USIDT
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UFICURR           3
     C                   PARM                    USIDT             8 0
     C                   PARM                    UBEL4            11 2
      *
     C                   EVAL      KLBUNI = 'LB'
     C                   EVAL      UBEL4  = 0
      *
     C                   ENDSR
      *
