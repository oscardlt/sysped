     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 40 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SVX034D1                  SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            TDAK           4A  O  5  5
      *
     A          R SVX034D2                  SFLCTL(SVX034D1)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX034R'
     A                                      COLOR(BLU)
     A                                  1 10'Plocka tillgångskod'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  1 30SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Garantinr:'
     A            UGNR          24A  O  2 13DSPATR(UL)
     A                                  3  2'Vælj option:  1=Plocka'
     A                                      COLOR(BLU)
     A                                  4  2'Op Tillgångskod'
     A                                      COLOR(WHT)
      *
     A          R SVX034D3
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1  2'F3/F12=Avbryt  F5=Uppdatera'
     A                                      COLOR(BLU)
      *
     A          R SVX034D4
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX034R'
     A                                      COLOR(BLU)
     A                                  1 10'Plocka tillgångskod'
     A                                      DSPATR(HI)
     A                                  2  2'Garantinr:'
     A            UGNR          24A  O  2 13DSPATR(UL)
     A                                  3  2'Vælj option:  1=Plocka'
     A                                      COLOR(BLU)
     A                                  4  2'Op Tillgångskod'
     A                                      COLOR(WHT)
     A                                 10  2'F3/F12=Avbryt  F5=Uppdatera'
     A                                      COLOR(BLU)
