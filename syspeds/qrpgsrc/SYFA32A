     FKUFAST    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FVARESL    UF A E           K DISK
     FVARESL01  IF   E           K DISK
     F                                     RENAME(VARESLR:VARE01R)
     FSYFA32FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D*                                   B 382 3830WSCLOW
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  VAID                   1     17
     D  SLETT                  1      8
      *
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *LIKE         DEFINE    W2KN          WWKN
     C     *LIKE         DEFINE    W2ID          WWID
     C     *LIKE         DEFINE    W2TY          WWTY
     C*          *ENTRY    PLIST
     C*                    PARM           W2KN
     C*                    PARM           UPDATE  1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
     C     KEY           KLIST
     C                   KFLD                    VAKN
     C                   KFLD                    VAID
     C     KEYW          KLIST
     C                   KFLD                    WSKN
     C                   KFLD                    WSID
     C     TYPKEY        KLIST
     C                   KFLD                    XXTYPE           10
     C                   KFLD                    WSTY
     C                   MOVEL     'VATY'        XXTYPE
      *
     C                   READ      FIRM                                   55
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      W2KN          WWKN
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     W2KN          COMP      *ZEROS                                 56     *LES ALLE
     C     W2KN          SETLL     VARESL
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C  N56W2KN          READE(N)  VARESL                                 94
     C   56              READ(N)   VARESL                                 94
     C     *IN94         DOWEQ     '0'
      *
     C     NESTE         TAG
     C     VAMGW         SUB       VATARA        XXNETT
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
      *
     C  N56W2KN          READE(N)  VARESL                                 94
     C   56              READ(N)   VARESL                                 94
     C  N94              END
      *
     C     UT            TAG
      *
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
     C                   WRITE     BLANKREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_____________
      * F6=Lag ny
      *"""""""""""""
     C     *IN06         IFEQ      '1'
     C                   MOVE      'J'           NY                1
     C                   EXSR      CRTCHG
     C                   GOTO      START
     C                   END
      *_____________
      * Options på topplinja
      *"""""""""""""
     C     TOPOPT        IFNE      ' '
     C     TOPOPT        IFEQ      '1'
     C                   MOVE      'J'           NY                1
     C                   EXSR      CRTCHG
     C                   ELSE
     C                   MOVE      *BLANKS       SLETT
     C                   MOVE      TOPOPT        WSNR
     C                   EXSR      OPTION
     C                   END
      * Søk utvalg til gammel verdi.. Vis subfila fra front igjen..
     C                   MOVE      WWKN          W2KN
     C                   MOVE      ' '           TOPOPT
     C                   GOTO      START
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     W2KN          IFNE      WWKN
     C     W2ID          ORNE      WWID
     C     W2TY          ORNE      WWTY
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C                   EXSR      OPTION
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C     WSNR          IFEQ      '2'
     C*          UPDATE    ANDEQ'J'
     C     SLETT         ANDNE     '*SLETTET'
     C     KEY           CHAIN(N)  VARESL                             55
     C     *IN55         IFEQ      '0'
     C                   MOVE      'N'           NY                1
     C                   EXSR      CRTCHG
     C                   END
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
     C     WSNR          IFEQ      '3'
     C*          UPDATE    ANDEQ'J'
     C     SLETT         ANDNE     '*SLETTET'
     C     KEY           CHAIN(N)  VARESL                             55
     C     *IN55         IFEQ      '0'
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   MOVE      'K'           NY                1
     C                   EXSR      CRTCHG
     C                   END
      *
     C     WSNR          IFEQ      '4'
     C*          UPDATE    ANDEQ'J'
     C     SLETT         ANDNE     '*SLETTET'
     C     KEY           CHAIN     VARESL                             55
     C  N55              DELETE    VARESLR
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   MOVE      *BLANKS       VAID
     C                   MOVEL     '*SLETTET'    VAID
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     CRTCHG        BEGSR
      *////////////////////////////////////////////////////////////
     C* 51 ON = PROTECT KEY-FELT
     C                   SELECT
     C     NY            WHENEQ    'N'                                            ENDRE
     C                   SETON                                            51
     C                   EXSR      MOVVWS
     C     NY            WHENEQ    'K'                                            KOPI
     C                   SETOFF                                           51
     C                   EXSR      MOVVWS
     C                   MOVE      *BLANKS       WSID
     C     NY            WHENEQ    'J'                                            NY
     C                   SETOFF                                           51
     C                   EXSR      MOVBLA
     C                   ENDSL
      *
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     40            POSNBR
      *
     C     STCHG         TAG
     C                   EXFMT     CHGREC
     C     *IN12         IFNE      '1'
      *
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
      *
     C                   EXSR      TEST
     C   30              GOTO      STCHG
      *
     C     KEYW          CHAIN     VARESL                             55
     C                   EXSR      MOVWSV
     C   55              WRITE     VARESLR
     C  N55              UPDATE    VARESLR
     C                   END
      *
     C                   ENDSR
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFLE      19
      * KUNDENR...
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKN
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN
     C                   MOVE      USØKKN        XXNAVN
     C     USØKKN        IFNE      *BLANKS
     C                   SETOFF                                       7030
     C                   END
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   ELSE
      * LASTEBÆRERTYPE...
     C                   MOVE      'N'           UUPDAT            1
     C                   CALL      'SYKUKD'
     C                   PARM                    XXTYPE
     C                   PARM                    UUPDAT
     C                   PARM                    WSTY
     C     TYPKEY        CHAIN     KUFAST                             33
     C                   MOVE      *BLANKS       XXVT
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFTXT         XXVT
     C                   SETOFF                                       7230
     C                   END
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   END
     C*
     C     SLSØK         ENDSR
     C*
      *
      *
      *////////////////////////////////////////////////////////////
     C     TEST          BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVEA     '00000000'    *IN(70)
     C                   SETOFF                                       30
      * UTLEDEDE VERDIER..
     C     WSVOL         IFEQ      *ZEROS
     C     WSLEN         MULT(H)   WSBRD         WSVOL
     C                   MULT(H)   WSHOY         WSVOL
     C                   END
     C     WSMGW         SUB       WSTARA        XXNETT
      *
      *
      * GYLDIGHETSTESTER....
     C                   MOVE      *BLANKS       XXNAVN
     C     WSKN          IFNE      *ZEROS
     C     KUNKEY        CHAIN     CUNDL01                            70
     C  N70              MOVE      KNAVN         XXNAVN
     C                   END
     C     WSID          COMP      *BLANKS                                71
      * VED KOPIERING/NY, KAN IKKE FINNES FRA FØR....
     C  N71NY            IFEQ      'J'
     C     NY            OREQ      'K'
     C     WSID          CHAIN     VARESL01                           33
     C  N33              SETON                                        71
     C                   END
     C     TYPKEY        CHAIN     KUFAST                             72
     C                   MOVE      *BLANKS       XXVT
     C     *IN72         IFEQ      '0'
     C                   MOVEL     KFTXT         XXVT
     C                   END
     C     WSVOL         COMP      *ZEROS                                 73
     C     WSMGW         COMP      *ZEROS                                 74
     C     WSTARA        COMP      *ZEROS                                 75
     C     WSFBV         COMP      *ZEROS                                 76
      *
      * FEILMELDINGER
      * KUNDE
     C     *IN70         IFEQ      '1'
     C                   MOVE      'JOV0936'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   GOTO      UTTST
     C                   END
      * ID
     C     *IN71         IFEQ      '1'
     C                   MOVE      'JOV0935'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      UTTST
     C                   END
      * TYPE
     C     *IN72         IFEQ      '1'
     C                   MOVE      'JOV0937'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     43            POSNBR
     C                   GOTO      UTTST
     C                   END
      * VOLUM
     C     *IN73         IFEQ      '1'
     C                   MOVE      'JOV0938'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     48            POSNBR
     C                   GOTO      UTTST
     C                   END
      * MGW
     C     *IN74         IFEQ      '1'
     C                   MOVE      'JOV0939'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      UTTST
     C                   END
      * TARA
     C     *IN75         IFEQ      '1'
     C                   MOVE      'JOV0940'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   GOTO      UTTST
     C                   END
      * TYPE
     C     *IN76         IFEQ      '1'
     C                   MOVE      'JOV0941'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     46            POSNBR
     C                   GOTO      UTTST
     C                   END
      *
     C     UTTST         ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     MOVVWS        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVE      VAKN          WSKN
     C                   MOVE      VAID          WSID
     C                   MOVE      VATY          WSTY
     C                   MOVE      VALEN         WSLEN
     C                   MOVE      VABRD         WSBRD
     C                   MOVE      VAHOY         WSHOY
     C                   MOVE      VAVOL         WSVOL
     C                   MOVE      VALM          WSLM
     C                   MOVE      VAFBV         WSFBV
     C                   MOVE      VAMGW         WSMGW
     C                   MOVE      VATARA        WSTARA
     C                   MOVE      *BLANKS       XXNAVN
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              MOVE      KNAVN         XXNAVN
     C                   MOVE      *BLANKS       XXVT
     C     TYPKEY        CHAIN     KUFAST                             33
     C  N33              MOVEL     KFTXT         XXVT
     C     VAMGW         SUB       VATARA        XXNETT
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     MOVWSV        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVE      WSKN          VAKN
     C   55              MOVE      WSKN          W2KN
     C   55              SETON                                        95         FRA START
     C                   MOVE      WSID          VAID
     C                   MOVE      WSTY          VATY
     C                   MOVE      WSLEN         VALEN
     C                   MOVE      WSBRD         VABRD
     C                   MOVE      WSHOY         VAHOY
     C                   MOVE      WSVOL         VAVOL
     C                   MOVE      WSLM          VALM
     C                   MOVE      WSFBV         VAFBV
     C                   MOVE      WSMGW         VAMGW
     C                   MOVE      WSTARA        VATARA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     MOVBLA        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   CLEAR                   CHGREC
      * INPUT FRA TOPLINJA
     C                   MOVE      W2KN          WSKN
     C                   MOVE      W2ID          WSID
     C                   MOVE      W2TY          WSTY
     C                   ENDSR
