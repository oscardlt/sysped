      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA05PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV KUNDERS LE-
     A                                      VERINGSADRESSER')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde')
     A                                      CF12(12 'Gå til forrige bilde')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA05'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 DIV1586 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                 11  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0B 11 17DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 18  2MSGCON(012 JOV1136 *LIBL/SYMSGF)
     A            WSKUN2         8Y 0B 15 17DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 17  2MSGCON(070 JOV1135 *LIBL/SYMSGF)
     A                                 15  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSDUPK         1   B 18 17VALUES(' ' '0' '1' '2')
     A                                 18 20MSGCON(050 JOV1137 *LIBL/SYMSGF)
     A                                 14  2MSGCON(045 JOV1134 *LIBL/SYMSGF)
      *
     A          R SUBFIL                    SFL
     A            VADRNA        30A  H
     A            VADRN1        30A  H
     A            VADRN3        30A  H
     A            VADRN2        30A  H
     A            VALAND         2A  H
     A            VARG09         9A  H
     A            VAKON1         5Y 0H
     A            VAKON2         2Y 0H
     A            VATLF         15A  H
     A            VAFAX         15A  H
     A            VAMAIL        70A  H
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4' '9' 'I')
     A            VADRNR         5Y 0O  5  4TEXT('Vareadressenr.')
     A  70                                  COLOR(YLW)
     A                                      EDTCDE(M)
     A            VAADNX        25A  O  5 11
     A  70                                  COLOR(YLW)
     A            VAAD1X        25A  O  5 37
     A  70                                  COLOR(YLW)
     A            VAAD3X        18A  O  5 63
     A  70                                  COLOR(YLW)
     A            VAKURE        17A  O  6 11
     A  70                                  COLOR(YLW)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPPNLGRP(SYFA05030 SYFA05PNL)
     A                                      HLPARA(02 002 23 079)
     A                                  1  2'SYSPED/SYFA05'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 DIV1586 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 25TEXT('Kundenavn')
     A                                      COLOR(WHT)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(020 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 63SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(079 DIV1589 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 56MSGCON(020 JOV1140 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A  71                              2  2MSGCON(075 JOV1366 *LIBL/SYMSGF)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              5  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A N40                                  CF09(09 'NESTE LEDIG NR.')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(17)
     A                                      CLRL(08)
     A          H                           HLPPNLGRP(SYFA05040 SYFA05PNL)
     A                                      HLPARA(01 001 08 079)
     A                                  1  4MSGCON(075 DIV1588 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSADNR         5Y 0B  2  4TEXT('Vareadressenr.')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                      EDTCDE(M)
     A            WSADNA        30A  B  2 15DSPATR(UL)
     A  40N30
     AO 89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                      COLOR(GRN)
     A            WSAD1         30A  B  2 46DSPATR(UL)
     A                                      CHECK(LC)
     A                                      COLOR(GRN)
     A            WSAD2         30A  B  3 15CHECK(LC)
     A                                      COLOR(GRN)
     A            WSAD3         30A  B  3 46CHECK(LC)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A            WSLAND         2A  B  3 77DSPATR(UL)
     A                                      COLOR(GRN)
     A                                  4  2MSGCON(011 SHU0521 *LIBL/SYMSGF)
     A            WSKURE        17A  B  4 15
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                  4 39MSGCON(012 SHU0443 *LIBL/SYMSGF)
     A            WSRG09         9Y 0B  4 52TEXT('REGISTRERINGSNR.')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                  4 62MSGCON(008 DIV1587 *LIBL/SYMSGF)
     A                                      COLOR(GRN)
     A            WSKON1         5Y 0B  4 71TEXT('KONTONR.-1')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A            WSKON2         2Y 0B  4 77TEXT('KONTONR.-2')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                  5  2MSGCON(010 YBC0788 *LIBL/SYMSGF)
     A            WSTLF         15A  B  5 13DSPATR(UL)
     A                                      CHECK(LC)
     A                                  5 42MSGCON(010 YBC0789 *LIBL/SYMSGF)
     A            WSFAX         15A  B  5 53DSPATR(UL)
     A                                      CHECK(LC)
     A                                  6  2MSGCON(007 YBC0790 *LIBL/SYMSGF)
     A            WSMAIL        70A  B  6 10DSPATR(UL)
     A                                      CHECK(LC)
     A                                  7  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N40                              7 21MSGCON(018 YBC0677 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A                                  7 40MSGCON(040 JOV1152 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N30                              8 40MSGCON(040 JOV1495 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(010 SHU1321 *LIBL/SYMSGF)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2MSGCON(025 YBC0791 *LIBL/SYMSGF)
     A            W2ADNR         5Y 0I  1 28DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2MSGCON(025 YBC0792 *LIBL/SYMSGF)
     A            W2ADNA        30A  I  2 28DSPATR(UL)
     A                                      CHECK(LC)
     A                                  3  2MSGCON(025 JOV1192 *LIBL/SYMSGF)
     A            W2PNR          4A  I  3 28DSPATR(UL)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF10(10 'Merk Inaktiv')
     A                                      SLNO(18)
     A                                      CLRL(07)
     A                                  1  4MSGCON(075 DIV1588 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            VADRNR         5Y 0O  2  4TEXT('Vareadressenr.')
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(M)
     A            VADRNA        30A  O  2 15COLOR(WHT)
     A            VADRN1        30A  O  2 46COLOR(WHT)
     A            VADRN2        30A  O  3 15COLOR(WHT)
     A            VADRN3        30A  O  3 46COLOR(WHT)
     A            VALAND         2A  O  3 77COLOR(WHT)
     A                                  4 13MSGCON(012 SHU0443 *LIBL/SYMSGF)
     A            VARG09         9A  O  4 26TEXT('REGISTRERINGSNR.')
     A                                      COLOR(WHT)
     A                                  4 36MSGCON(008 DIV1587 *LIBL/SYMSGF)
     A            VAKON1         5Y 0O  4 45TEXT('KONTONR.-1')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            VAKON2         2Y 0O  4 51TEXT('KONTONR.-2')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(010 YBC0788 *LIBL/SYMSGF)
     A            VATLF         15A  O  5 13COLOR(WHT)
     A                                  5 42MSGCON(010 YBC0789 *LIBL/SYMSGF)
     A            VAFAX         15A  O  5 53COLOR(WHT)
     A                                  6  2MSGCON(007 YBC0790 *LIBL/SYMSGF)
     A            VAMAIL        70A  O  6 10COLOR(WHT)
     A                                  7  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  70                              7 22MSGCON(040 JOV1368 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A N70                              7 22MSGCON(040 JOV1369 *LIBL/SYMSGF)
     A                                      COLOR(RED)
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SYFA05'
     A                                      COLOR(BLU)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  1 19MSGCON(050 DIV1586 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(BLU)
     A            KNAVN         30A  O  2 25TEXT('Kundenavn')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(079 DIV1589 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL4
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      SLNO(18)
     A                                      CLRL(07)
     A            MSGNR          7A  P
     A                                  1  4MSGCON(075 DIV1588 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            VADRNR         5Y 0O  2  4TEXT('Vareadressenr.')
     A                                      COLOR(WHT)
     A                                      EDTCDE(M)
     A            VADRNA        30A  O  2 15COLOR(WHT)
     A            VADRN1        30A  O  2 46COLOR(WHT)
     A            VADRN2        30A  O  3 15COLOR(WHT)
     A            VADRN3        30A  O  3 46COLOR(WHT)
     A            VALAND         2A  O  3 77COLOR(WHT)
     A                                  4  4MSGCON(025 JOV1138 *LIBL/SYMSGF)
     A            W4KUNR         8Y 0B  4 31EDTCDE(4)
     A                                      DSPATR(PC)
     A                                  5  4MSGCON(070 JOV1135 *LIBL/SYMSGF)
     A                                  6  4MSGCON(012 JOV1136 *LIBL/SYMSGF)
     A            W4DUPK         1A  B  6 18
     A                                  6 22MSGCON(050 JOV1137 *LIBL/SYMSGF)
     A N30                              7  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A N30                              7 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
