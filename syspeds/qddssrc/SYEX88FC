*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX88CPNL)
     A                                      HLPTITLE('PLUKKE OPPDRAG TIL TUR')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  8  3
     A  98                                  DSPATR(RI)
     A                                      VALUES(' ' '1' '4' '5' 'X')
     A            HEAVD          4Y 0O  8  5EDTCDE(4)
     A            HEOPD          7Y 0O  8 10EDTCDE(4)
     A            HENAS         30A  O  8 18
     A            SFNAVN        30A  O  8 49
     A            HENT           7Y 0O  9  6EDTCDE(4)
     A            HEVS1         25A  O  9 14
     A            HEVKT          9Y 0H
     A            W2VKT          5Y 0O  9 40EDTCDE(4)
     A            HEM3           7Y 3H
     A            W2M3           4Y 2O  9 46EDTCDE(4)
     A            HELM           4Y 2O  9 53EDTCDE(4)
     A            HESDF          5A  O  9 59
     A            HESDT          5A  O  9 65
     A            HEOT           2A  O  9 71
     A            HEFR           3A  O  9 75
     A            HEKDPL         1   O  9 79COLOR(WHT)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYEX88C'
     A                                      COLOR(BLU)
     A  31N34                           1 22MSGCON(030 DIV0699 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N31N34                           1 22MSGCON(030 DIV0725 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  34                              1 22MSGCON(030 JOV0715 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            TUAVD          4Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            TUPRO          8  0O  2 32EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 35SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 53MSGCON(006 DIV0683 *LIBL/SYMSGF)
     A            TUSDF         20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(008 DIV0684 *LIBL/SYMSGF)
     A            TUKNA          8Y 0O  3 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAA         30   O  3 21COLOR(WHT)
     A                                  3 53MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            TUSDT         20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            TUKNT          8Y 0O  4 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  4 21COLOR(WHT)
     A                                  4 53MSGCON(006 DIV0687 *LIBL/SYMSGF)
     A            TUTARF        15A  O  4 60COLOR(WHT)
     A N34                              5  2MSGCON(049 DIV0700 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(049 JOV0716 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 53MSGCON(020 DIV0703 *LIBL/SYMSGF)
     A                                      COLOR(YLW)
     A            WSAVD          4Y 0O  5 75EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(078 DIV0689 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  6MSGCON(074 DIV0690 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF08(08 'SØK FRA NY AVD.')
     A                                      CF12(12 'FORRIGE')
     A                                      CF16(16 'Plukke alle')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            MSGNR          7A  P
S01  A*                                 1  2MSGCON(015 JOV0713 *LIBL/SYMSGF)
S01  A*           TUTS           5Y 0O  1 18COLOR(WHT)
N01  A                                  1  2MSGCON(013 YBC1996 *LIBL/SYMSGF)
N01  A            TUTS           7Y 0O  1 16COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 1 26MSGCON(006 DIV0694 *LIBL/SYMSGF)
S01  A*           TUTVKT         5Y 0O  1 33COLOR(WHT)
N01  A                                  1 24MSGCON(006 DIV0694 *LIBL/SYMSGF)
N01  A            TUTVKT         9Y 0O  1 31COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 1 41MSGCON(006 DIV0695 *LIBL/SYMSGF)
S01  A*           TULM3          4Y 2O  1 48COLOR(WHT)
N01  A                                  1 41MSGCON(004 YBC1995 *LIBL/SYMSGF)
N01  A            TUTM3          7Y 3O  1 46COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 55MSGCON(006 DIV0696 *LIBL/SYMSGF)
     A            TUTLM          4Y 2O  1 62COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 69MSGCON(006 DIV0697 *LIBL/SYMSGF)
     A            TUAO           5Y 0O  1 76COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 2  2MSGCON(015 JOV0712 *LIBL/SYMSGF)
S01  A*           WSNT           5Y 0O  2 18COLOR(WHT)
N01  A                                  2  2MSGCON(013 YBC1997 *LIBL/SYMSGF)
N01  A            WSNT           7Y 0O  2 16COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           WSVKT          5Y 0O  2 33COLOR(WHT)
N01  A            WSVKT          9Y 0O  2 31COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           WSM3           5Y 2O  2 47COLOR(WHT)
N01  A            WSM3           7Y 3O  2 46COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSLM           5Y 2O  2 61COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSNTO          5Y 0O  2 76COLOR(WHT)
     A                                      EDTCDE(4)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 22MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              3 35MSGCON(006 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              3 44MSGCON(015 TRO0166 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'Avslutt')
     A                                      CF08(08 'SØK FRA NY AVD.')
     A                                      CF12(12 'FORRIGE')
     A                                      CF16(16 'Plukke alle')
     A                                  1  2'SYSPED/SYEX88C'
     A                                      COLOR(BLU)
     A  31N34                           1 22MSGCON(030 DIV0699 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N31N34                           1 22MSGCON(030 DIV0725 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            TUAVD          4Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            TUPRO          8Y 0O  2 32EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 52MSGCON(007 DIV0683 *LIBL/SYMSGF)
     A            TUSDF         20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(008 DIV0684 *LIBL/SYMSGF)
     A            TUKNA          8Y 0O  3 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAA         30A  O  3 21COLOR(WHT)
     A                                  3 52MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            TUSDT         20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            TUKNT          8Y 0O  4 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  4 21COLOR(WHT)
     A                                  4 52MSGCON(006 DIV0687 *LIBL/SYMSGF)
     A            TUTARF        15A  O  4 60COLOR(WHT)
     A                                  5 53MSGCON(020 DIV0703 *LIBL/SYMSGF)
     A                                      COLOR(YLW)
     A            WSAVD          4Y 0O  5 75EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(078 DIV0689 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  6MSGCON(074 DIV0690 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S01  A*                                22  2MSGCON(015 JOV0713 *LIBL/SYMSGF)
S01  A*           TUTS           5Y 0O 22 18COLOR(WHT)
N01  A                                 22  2MSGCON(013 YBC1996 *LIBL/SYMSGF)
N01  A            TUTS           7Y 0O 22 16COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                22 26MSGCON(006 DIV0694 *LIBL/SYMSGF)
S01  A*           TUTVKT         5Y 0O 22 33COLOR(WHT)
N01  A                                 22 24MSGCON(006 DIV0694 *LIBL/SYMSGF)
N01  A            TUTVKT         9Y 0O 22 31COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                22 41MSGCON(006 DIV0695 *LIBL/SYMSGF)
S01  A*           TUTM3          5Y 2O 22 48COLOR(WHT)
N01  A                                 22 41MSGCON(004 YBC1995 *LIBL/SYMSGF)
N01  A            TUTM3          7Y 3O 22 46COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 55MSGCON(006 DIV0696 *LIBL/SYMSGF)
     A            TUTLM          4Y 2O 22 62COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 69MSGCON(006 DIV0697 *LIBL/SYMSGF)
     A            TUOA           5Y 0O 22 76COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                23  2MSGCON(015 JOV0712 *LIBL/SYMSGF)
S01  A*           WSNT           5Y 0O 23 18COLOR(WHT)
N01  A                                 23  2MSGCON(013 YBC1997 *LIBL/SYMSGF)
N01  A            WSNT           7Y 0O 23 16COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           WSVKT          5Y 0O 23 33COLOR(WHT)
N01  A            WSVKT          9Y 0O 23 31COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           WSM3           5Y 2O 23 47COLOR(WHT)
N01  A            WSM3           7Y 3O 23 46COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSLM           5Y 2O 23 61COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSNTO          5Y 0O 23 76COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 24 22MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 35MSGCON(006 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 44MSGCON(015 TRO0166 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  34                              1 21MSGCON(030 JOV0715 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N34                              5  2MSGCON(049 DIV0700 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  34                              5  2MSGCON(049 JOV0716 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SØKOPD
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A            MSGNR          7A  P
     A                                  1  2MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  1 15DSPATR(UL)
     A                                      DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  1 20MSGCON(006 JOV0717 *LIBL/SYMSGF)
     A                                  1 27MSGCON(006 TRO0179 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  1 34DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  1 43MSGCON(005 JOV0718 *LIBL/SYMSGF)
     A            WSFRA          5   B  1 49COLOR(YLW)
     A                                  1 55MSGCON(005 JOV0719 *LIBL/SYMSGF)
     A            WSTIL          5   B  1 61COLOR(YLW)
     A                                  1 67MSGCON(006 JOV0720 *LIBL/SYMSGF)
     A            WSOT           2   B  1 74COLOR(YLW)
     A                                  2  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 28MSGCON(005 JOV0722 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  2 34DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 43MSGCON(005 JOV0723 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B  2 49DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 58MSGCON(010 JOV0721 *LIBL/SYMSGF)
     A            WSKDPL         1   B  2 69COLOR(YLW)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
