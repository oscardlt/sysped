     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                      CF01(01 'Bytt avdelinger')
     A                                      CF02(02 'Gå til valgbilde')
     A                                      CF03(03 'Avslutt')
     A                                      CF06(06 'Gå til faktura')
     A                                      CF12(12 'Gå til rollbilde')
     A                                      CF24(24 'FLERE TASTER')
     A                                  1  2MSGCON(30 DIV0179 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 59MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(7 DIV0180 SYMSGF)
     A            DIFBNR         3Y 0O  3 10TEXT('Fraktbrevnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 17MSGCON(11 DIV0148 SYMSGF)
     A            DIOPD          7Y 0O  3 29TEXT('Oppdragsnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 38MSGCON(5 DIV0181 SYMSGF)
     A            DIAWB         11S 0O  3 44TEXT('HAWB-NR')
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(7 DIV0151 SYMSGF)
     A            DIST           1A  O  4 10TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2MSGCON(4 DIV0079 SYMSGF)
     A            DIFRA          5A  O  5 10TEXT('FRA-DESTINASJON')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 17MSGCON(4 DIV0080 SYMSGF)
     A            DITIL          5A  O  5 22TEXT('TIL-DESTINASJON')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 28MSGCON(7 DIV0182 SYMSGF)
     A            DIRUNR         6A  O  5 36TEXT('RUNR')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIDAG1         2Y 0O  5 43TEXT('DAG1')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  5 46'/'
     A            DIRUN2         6A  O  5 48TEXT('RUN2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIDAG2         2Y 0O  5 55TEXT('DAG2')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  5 58'/'
     A            DIRUN3         6A  O  5 60TEXT('RUN3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIDAG3         2Y 0O  5 67TEXT('DAG3')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  5 70MSGCON(5 DIV0183 SYMSGF)
     A            DICCPP         2A  O  5 76TEXT('CCPP')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(7 DIV0184 SYMSGF)
     A            DIPREF         3Y 0O  6 10TEXT('PREF')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DIFNAV        30A  O  6 15TEXT('FNAV')
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(6 DIV0132 SYMSGF)
     A            DISKNR         8Y 0O  7 10TEXT('SKNR')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DISNAV        30A  O  7 19TEXT('SNAV')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DISAD1        30A  O  7 50TEXT('SAD1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DISAD3        30A  O  8 50TEXT('SAD3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DISAD2        30A  O  8 19TEXT('SAD2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 10MSGCON(6 DIV0185 SYMSGF)
     A            DISTLF        15A  O  9 19TEXT('STLF')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10  2MSGCON(6 DIV0135 SYMSGF)
     A            DIKNAV        30A  O 10 19TEXT('KNAV')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIKAD1        30A  O 10 50TEXT('KAD1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIKAD2        30A  O 11 19TEXT('KAD2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIKAD3        30A  O 11 50TEXT('KAD3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 10MSGCON(6 DIV0185 SYMSGF)
     A            DIKTLF        15A  O 12 19TEXT('KTLF')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14  2MSGCON(78 DIV0186 SYMSGF)
     A            DIANT          7Y 0O 15  2TEXT('ANT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DIVEKT         5Y 0O 15 10TEXT('VEKT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DISLAG        20A  O 15 16TEXT('SLAG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIVSL2        20A  O 15 38TEXT('VSL2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIFVKT         5Y 0O 15 61TEXT('FVKT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DITARI         1A  O 15 69TEXT('TARI')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DIPRIS         5Y 2O 15 73TEXT('PRIS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 17  2MSGCON(7 DIV0187 SYMSGF)
     A            DIMERK        50A  O 17 10TEXT('MERK')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2MSGCON(7 DIV0188 SYMSGF)
     A            DIFORS         9Y 0O 18 10TEXT('FORS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 18 22MSGCON(5 DIV0189 SYMSGF)
     A            DIFORA        25A  O 18 28TEXT('FORA')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2MSGCON(7 DIV0171 SYMSGF)
     A            DIFRAK         7Y 0O 19 10TEXT('FRAK')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 19 30MSGCON(50 DIV0190 SYMSGF)
     A                                      COLOR(WHT)
     A                                 20  2MSGCON(7 DIV0191 SYMSGF)
     A            DIFS           7Y 0O 20 10TEXT('FS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 20 30MSGCON(50 DIV0192 SYMSGF)
     A                                      COLOR(WHT)
     A                                 21  2MSGCON(7 DIV0193 SYMSGF)
     A            DIFPRE         7Y 0O 21 10TEXT('FPRE')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 22  2MSGCON(7 DIV0194 SYMSGF)
     A            DIVGEB         7Y 0O 22 10TEXT('VGEB')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 22 19MSGCON(4 DIV0195 SYMSGF)
     A            DIMVA          9Y 2O 22 24TEXT('MVA')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 37MSGCON(12 DIV0152 SYMSGF)
     A            DIMLAP         1A  O 22 50TEXT('MLAP')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            MSG128        18A  O 23  2MSGID(&MSG01 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG129        18A  O 23 22MSGID(&MSG02 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG130        18A  O 23 42MSGID(&MSG03 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG131        18A  O 23 62MSGID(&MSG04 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSG01          7A  P
     A            MSG02          7A  P
     A            MSG03          7A  P
     A            MSG04          7A  P
     A            MSGNR          7A  P
