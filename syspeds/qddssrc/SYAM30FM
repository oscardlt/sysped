     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A          R SAM30B10
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYAM30'
     A                                      COLOR(BLU)
     A                                  1 26MSGCON(020 TRO0254 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A            WKNA           8Y 0B  9 29EDTCDE(4)
     A                                  9 17MSGCON(010 TRO0066 *LIBL/SYMSGF)
     A                                 11 17MSGCON(010 TGI0013 *LIBL/SYMSGF)
     A            WPERM          2Y 0B 11 29EDTCDE(4)
     A            WPERÅ          2Y 0B 11 32EDTCDE(4)
     A                                 11 36MSGCON(010 TGI0014 *LIBL/SYMSGF)
     A                                 13 17'Fra sted:'
     A            WFRA          15   B 13 29
     A                                 15 17'Til sted:'
     A            WTIL          15   B 15 29
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        75A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SAM30B30
     A                                      CF03(03 'Avslutt')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1  3'SYSPED/SYAM30'
     A                                      COLOR(BLU)
     A                                  1 26MSGCON(020 TRO0254 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A            WKNA           8S 0O  3 10DSPATR(HI)
     A                                  3  3MSGCON(006 DIV2424 *LIBL/SYMSGF)
     A            W2NAVN        30A  O  3 19DSPATR(HI)
     A                                  3 54MSGCON(009 TGI0013 *LIBL/SYMSGF)
     A            WPERM          2S 0O  3 64DSPATR(HI)
     A            WPERÅ          2S 0O  3 67DSPATR(HI)
     A                                  4  3MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  5TEXT('1')
     A                                      MSGCON(030 TRO0255 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7 11MSGCON(008 TGI0005 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 27MSGCON(008 TGI0006 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 43MSGCON(008 TGI0019 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 54MSGCON(008 TGI0007 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 64MSGCON(008 TGI0002 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7  6MSGCON(004 TRO0256 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 10  9MSGCON(045 TRO0257 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SAM30B40
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CF10(10 'Avregning/utskrift')
     A                                      CA12(12 'CANCEL')
     A                                      SLNO(23)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 23MSGCON(025 TRO0260 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SAM30H50                  SFL
     A                                      SFLNXTCHG
     A            WSNR           1A  B  8  3
     A  30                                  DSPATR(PC)
     A                                      VALUES(' ' '2' '4')
     A            AHST           1A  O  8  7
     A            AHEREF        15A  O  8 11
     A            AHAREF        15A  O  8 27
     A            AHBILN        10A  O  8 43
EV5  A            AHADAT         8Y 0H
NV5  A            WHADAT         6Y 0O  8 54EDTCDE(Y)
EV5  A            AHBDAT         8Y 0H
NV5  A            WHBDAT         6Y 0O  8 64EDTCDE(Y)
     A            AIUNIK         9Y 0H
     A            WSKODE         8A  O  8 73DSPATR(HI)
     A            AHTUNR         8Y 0H
     A          R SAM30K60                  SFLCTL(SAM30H50)
     A                                      SFLSIZ(1000)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF10(10 'Avregning/utskrift')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  31                                  SFLMSG('Tastet valg er ikke gyldig')
     A            CURS1          4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  3'SYSPED/SYAM30'
     A                                      COLOR(BLU)
     A                                  1 26MSGCON(020 TRO0254 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A                                  4  3MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            WKNA           8S 0O  3 10DSPATR(HI)
     A                                  3  3MSGCON(006 DIV2424 *LIBL/SYMSGF)
     A            W2NAVN        30A  O  3 19DSPATR(HI)
     A                                  3 54MSGCON(009 TGI0013 *LIBL/SYMSGF)
     A            WPERM          2S 0O  3 64DSPATR(HI)
     A            WPERÅ          2S 0O  3 67DSPATR(HI)
     A                                  5  5TEXT('1')
     A                                      MSGCON(030 TRO0255 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  2'Opt'
     A                                      DSPATR(HI)
     A                                  7  6MSGCON(004 TRO0256 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 11MSGCON(008 TGI0005 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 27MSGCON(008 TGI0006 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 43MSGCON(008 TGI0019 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 54MSGCON(008 TGI0007 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  7 64MSGCON(008 TGI0002 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
