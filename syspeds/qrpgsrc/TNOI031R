      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI031R MODULE(*LIBL/TNOI031R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSADF      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D FAK             S             17
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬fak¬ : ¬'+%trim(FAK)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ¬oneInvoice¬ : [';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Hæmta detta ena ærendet.

     C     Error         IFEQ      *blanks
     C     DKIVKey       CHAIN     SADF                               55
     C     *IN55         IFEQ      '0'
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬sfavd¬ : ¬'+%trim(%editc(sfavd:'Z'))+'¬, '
          +'¬sfopdn¬ : ¬'+%trim(%editc(sfopdn:'Z'))+'¬, '
          +'¬sftxt¬ : ¬'+%trim(sftxt)+'¬, '
          +'¬sfdt¬ : ¬'+%trim(%editc(sfdt:'Z'))+'¬, '
          +'¬sfbl28¬ : ¬'+%trim(%editc(sfbl28:'4'))+'¬, '
          +'¬sfvk28¬ : ¬'+%trim(sfvk28)+'¬, '
          +'¬sfkr28¬ : ¬'+%trim(%editc(sfkr28:'4'))+'¬, '
          +'¬sfom28¬ : ¬'+%trim(%editc(sfom28:'Z'))+'¬, '
          +'¬sfxxx¬ : ¬'+%trim(sfxxx)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDIF
     C                   ENDIF
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     C                   EVAL      JSONstring =']}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Quit
     C                   EXSR      EXIT
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SADF

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   EVAL      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   EVAL      OPDN    = c2n2(OPD)
     C                   EVAL      FAK     = zhbgetvar('FAK')
     C                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   CALL      BILIBL
     C                   end

     C     DKIVKey       klist
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    FAK

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      SADF

     C                   ENDSR
