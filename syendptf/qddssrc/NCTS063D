     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TR063PNL)
     A                                      HLPTITLE('BEKREFTE SENDING AV TET I-
     A                                      MPORT')
     A                                      HELP
     A                                      ALTHELP
     A          R NC063D01
     A                                      CA12(12 'Avbryt')
     A                                      CA03(03 'Avbryt')
     A                                  1 26'Bekrefte på å sende NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  1 73'TR063R'
     A                                      COLOR(BLU)
     A                                  3  1'Trykk Enter for å bekrefte:'
     A                                      COLOR(BLU)
     A                                  5  3'Avdeling . . . . . . . . :'
     A            UAVD           4Y 0O  5 32EDTCDE(4)
     A                                  6  5'Tolldeklarasjonsnummer :'
     A            UTDN           7Y 0O  6 32EDTCDE(4)
     A                                  7  3'MRN-nummer . . . . . . . :'
     A            TITRNR        18A  O  7 32TEXT('MRN-nr')
     A                                  8  3'Næringsliv . . . . . . . :'
     A            TINA          30A  O  8 32TEXT('Næringsliv')
     A                                 10  3'Meldings funksjon  . . . :'
     A            W1N07          3A  O 10 32
     A                                      DSPATR(UL)
     A                                 10 48'007=Ankomstmelding'
     A                                 11 48'044=Lossemerknader'
     A                                 13  3'Sendingsdato . . . . . . :'
     A            WSDT           6  0B 13 32DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A                                      EDTCDE(Y)
     A                                 13 48'(DDMMÅÅ)'
     A                                 14  3'Sendingstid  . . . . . . :'
     A            WSTD           4  0B 14 32DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A                                      EDTWRD('0 .  ')
     A                                 14 48'(TTMM)'
     A                                 16  3'Ventegrupe . . . . . . . :'
     A            WVEN      R        B 16 32REFFLD(MVEN *LIBL/EDIM)
     A                                      TEXT('Ventekode')
     A                                      DSPATR(UL)
     A                                 16 48'Blank=Sende direkte '
     A                                 17  3'Testekode  . . . . . . . :'
     A            W0035     R        O 17 32REFFLD(M0035 *LIBL/EDIM)
     A                                 17 48'Blank=Produksjon'
     A                                 23  1'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                 23 22'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
