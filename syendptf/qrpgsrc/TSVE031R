      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE031R MODULE(*LIBL/TSVE031R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVEF3     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDN            S              7  0
     D FAK             S             35
     D TYP             S              4
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬fak¬ : ¬'+%trim(TYP)+'¬, '
        +'¬fak¬ : ¬'+%trim(FAK)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ¬oneInvoice¬ : [';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Hæmta detta ena ærendet.

     C     Error         IFEQ      *blanks
     C     SVEFKEY       CHAIN     SVEF3                              55
     C     *IN55         IFEQ      '0'
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬svef_syav¬ : ¬'+%trim(%editc(SVEF_syav:'Z'))+'¬, '
          +'¬svef_syop¬ : ¬'+%trim(%editc(SVEF_syop:'Z'))+'¬, '
          +'¬svef_faty¬ : ¬'+%trim(SVEF_faty)+'¬, '
          +'¬svef_fatx¬ : ¬'+%trim(SVEF_fatx)+'¬, '
          +'¬svef_vakd¬ : ¬'+%trim(SVEF_vakd)+'¬, '
          +'¬svef_vaku¬ : ¬'+%trim(%editc(SVEF_vaku:'4'))+'¬, '
          +'¬svef_fabl¬ : ¬'+%trim(%editc(SVEF_fabl:'4'))+'¬, '
          +'¬svef_tuid¬ : ¬'+%trim(SVEF_tuid)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDIF
     C                   ENDIF
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     C                   EVAL      JSONstring =']}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Quit
     C                   EXSR      EXIT
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVEF3

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   EVAL      AVDN    = c2n2(AVD)
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   EVAL      OPDN    = c2n2(OPD)
     C                   EVAL      FAK     = zhbgetvar('FAK')
     C                   EVAL      TYP     = zhbgetvar('TYP')
     C                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   CALL      BILIBL
     C                   end

     C     SVEFKEY       klist
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    TYP
     C                   KFLD                    FAK

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      SVEF3

     C                   ENDSR
