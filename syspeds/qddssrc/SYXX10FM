     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '5' '8')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A*           HEFNK          6Y 0O  6  4EDTCDE(4)
     A            HEOPD          7Y 0O  6 10EDTCDE(4)
     A            HEST           1A  O  6 18
     A            HESG           3A  O  6 20
     A            WHEGN         11A  O  6 24
     A            WSPOSS         8A  O  6 36
     A            WSPOSK         8A  O  6 46
     A            HEOT           2A  O  6 55
     A            HEFR           3A  O  6 58
     A            HENT           7Y 0O  6 62EDTCDE(4)
     A            HEVKT          9Y 0O  6 70EDTCDE(4)
     A            HEDTR          8Y 0H
     A            WSDTR          6Y 0O  7  6DSPATR(UL)
     A                                      EDTCDE(4)
     A            HESDF          5A  O  7 13DSPATR(UL)
     A            HESDT          5A  O  7 19DSPATR(UL)
     A            HEPRO          8Y 0O  7 25DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HERFA         15A  O  7 34DSPATR(UL)
     A            WHEGNN        11A  O  7 50DSPATR(UL)
     A            HEFNS          6Y 0O  7 62DSPATR(UL)
     A                                      EDTCDE(4)
     A            HEUR           1A  O  7 71DSPATR(UL)
     A            HEAVD          4Y 0O  7 74EDTCDE(4)
     A                                      DSPATR(UL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYXX21'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  2 61TIME
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 20MSGCON(040 DIV0044 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 20MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A            WSPRO          8Y 0O  2 44DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(015 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 24MSGCON(017 DIV0048 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 50MSGCON(030 DIV3576 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV0045 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(078 DIV0046 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2 22MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      OVERLAY
     A                                  1  3'SYSPED/SYXX21'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  2 62TIME
     A                                  1 21MSGCON(040 DIV0044 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 32MSGCON(017 DIV0048 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(078 DIV0045 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(078 DIV0046 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
