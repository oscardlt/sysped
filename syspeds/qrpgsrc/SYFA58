********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 BEGRENSE ANTALL POSTER VED VISNING AV ALLE BRUKERE
10XXX * 02 SH  PTF11 Viser om epost
********************************************************************
N02  FCUNDC03   IF   E           K DISK
     FSYFA58FM  CF   E             WORKSTN
     F                                     SFILE(RECB:ZRECNO)
     F                                     INFDS(STATDS)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FHEADF     IF   E           K DISK
     FKODAL01   IF   E           K DISK
     FFERDF     IF   E           K DISK
     F                                     RENAME(FERDR:FERDI)
     FFERDL01   UF   E           K DISK
     D                 DS
     D  FEWS                   1     10
     D  FEWS8                  1      8
     D STATDS          DS
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *LIKE         DEFINE    WSWSID        WWWSID
     C     *LIKE         DEFINE    WSAVD         WWAVD
     C     *LIKE         DEFINE    WSOPD         WWOPD
     C     *ENTRY        PLIST
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    ALLWS             1
     C                   PARM                    UPDATE            1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
N02  C     EPOKEYSI      KLIST
N02  C                   KFLD                    FIFIRM
N02  C                   KFLD                    mail30           30
N02  C                   KFLD                    FEKN
N02  C                   eval      mail30='*SINGELFAKTURA'
N02  C     EPOKEYSA      KLIST
N02  C                   KFLD                    FIFIRM
N02  C                   KFLD                    mail30S          30
N02  C                   KFLD                    FEKN
N02  C                   eval      mail30S='*SAMLEFAKTURA'
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FEKN
     C     HEAKEY        KLIST
     C                   KFLD                    FEAVD
     C                   KFLD                    FEOPD
     C     BETKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSBETB
     C                   READ      FIRM                                   55
     C* 'J' I ALLWS  : SKAL SE ALLE USER-ID
     C* 'N' I ALLWS  : LÅST TIL EGEN USER-ID
     C* ' ' I ALLWS  : DEFAULT VIS EGEN / KAN OVERSTYRE TIL ALLE...
     C     ALLWS         IFEQ      'J'
     C                   MOVE      *BLANKS       WSWSID
     C                   ELSE
     C                   MOVE      ZUSER         WSWSID
     C                   END
     C* Endring av USER tillatt ??
     C     ALLWS         COMP      'N'                                    51     *PROTECT
     C* Update tillatt ??
     C     UPDATE        COMP      'J'                                5050       *NONDISP
      *_____________
      * Init av felt
      *"""""""""""""
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      WSWSID        WWWSID
     C                   MOVE      WSAVD         WWAVD
     C                   MOVE      WSOPD         WWOPD
     C                   MOVE      '1'           *IN93
     C                   WRITE     RECC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     WSWSID        COMP      *BLANKS                                56     *LES VIA WS
     C     WSWSID        SETLL     FERDF
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C  N56WSWSID        READE     FERDF                                  94
     C   56              READ      FERDF                                  94
     C     *IN94         DOWEQ     '0'
      *
     C     NESTE         TAG
     C     WSAVD         IFNE      *ZEROS
     C     WSAVD         ANDNE     FEAVD
     C  N56WSWSID        READE     FERDF                                  94
     C   56              READ      FERDF                                  94
     C  N94              GOTO      NESTE
     C   94              GOTO      UT
     C                   END
     C     WSOPD         IFNE      *ZEROS
     C     WSOPD         ANDNE     FEOPD
     C  N56WSWSID        READE     FERDF                                  94
     C   56              READ      FERDF                                  94
     C  N94              GOTO      NESTE
     C   94              GOTO      UT
     C                   END
     C     FEKN          IFEQ      *ZEROS
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      'NULLMERK'    KNAVN
     C                   ELSE
     C     KUNKEY        CHAIN     CUNDL01                            33
     C     *IN33         IFEQ      '0'
     C     DKUND         ANDEQ     'D'
     C     HEAKEY        CHAIN     HEADF                              33
     C  N33FESKX         IFEQ      'K'
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVEL     HENAK         KNAVN2           26
     C                   MOVEL     '*D*'         KNAVN
     C                   MOVE      KNAVN2        KNAVN
     C                   END
     C  N33FESKX         IFEQ      'S'
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVEL     HENAS         KNAVN2
     C                   MOVEL     '*D*'         KNAVN
     C                   MOVE      KNAVN2        KNAVN
     C                   END
     C                   END
     C                   END
     C                   ADD       1             ZRECNO
     C                   WRITE     RECB
N01  C   56              IF        ZRECNO > 1000
N01  C                   GOTO      UT
N01  C                   END
      *
     C  N56WSWSID        READE     FERDF                                  94
     C   56              READ      FERDF                                  94
     C  N94              END
     C     UT            TAG
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV0437'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     RECØ
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   51              Z-ADD     17            POSNBR
     C                   EXFMT     RECA
     C                   MOVE      '0'           *IN30
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      START
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
     C***********************************************
     C     SRB4          TAG
     C***********************************************
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     RECØ
     C                   WRITE     RECD
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     RECC
      *
     C     *IN12         CABEQ     '1'           SRB9
     C     *IN03         IFEQ      '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     WSWSID        IFNE      WWWSID
     C     WSAVD         ORNE      WWAVD
     C     WSOPD         ORNE      WWOPD
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     RECB                                   52
     C     *IN52         IFEQ      '0'
     C                   EXSR      ENDRE
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           SRB4
     C                   END
      *
     C     SRB9          ENDSR
     C***************************************************
     C     ENDRE         BEGSR
     C***************************************************
     C     KEY           KLIST
     C                   KFLD                    FEAVD
     C                   KFLD                    FEOPD
     C                   KFLD                    FEKN
     C*
     C     WSNR          IFEQ      '4'
     C     UPDATE        ANDEQ     'J'
     C     FEWS8         IFNE      '*SLETTET'
     C     KEY           CHAIN     FERDL01                            55
     C                   DELETE    FERDR
     C                   MOVE      *BLANKS       FEWS
     C                   MOVEL     '*SLETTET'    FEWS
     C                   MOVE      ' '           WSNR
     C                   UPDATE    RECB
     C                   END
     C                   END
     C     WSNR          IFEQ      '2'
     C     UPDATE        ANDEQ     'J'
     C     FEWS8         IFNE      '*SLETTET'
     C                   MOVE      FESFAK        WSSFAK
     C                   MOVE      OVBETB        WSBETB
N02  C                   MOVE      EPOJN         WSEPJN
N02  C                   MOVE      FVENT         WSVENT
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     75            POSNBR
     C     feil          tag
     C                   EXFMT     RECCH
     C                   setoff                                       3070
     C     *IN12         IFNE      '1'
     C     KEY           CHAIN     FERDL01                            55
      * "N" ER IKKE LOVLIG SOM "SAMLEFAKT-GRP": TOLKES SOM N=NEI->" "
     C     WSSFAK        IFEQ      'N'
     C                   MOVE      ' '           WSSFAK
     C                   END
     C                   MOVE      WSSFAK        FESFAK
     C     WSBETB        IFNE      *BLANKS
     C     BETKEY        CHAIN     KODAL01                            70
     C     *IN70         IFEQ      '1'
     c                   seton                                        30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     78            POSNBR
     C                   MOVE      'SHU1137'     ZMSGNO
     c                   goto      feil
     C                   END
     C                   END
N02  C     WSEPJN        IFEQ      'J'
N02  C     FESFAK        IFEQ      ' '
N02  c     epokeySI      CHAIN     cundc03                            33
N02  C                   ELSE
N02  c     epokeySA      CHAIN     cundc03                            33
N02  C                   END
N02  C     *in33         ifeq      '1'
N02  C                   Z-ADD     23            LINNBR
N02  C                   Z-ADD     84            POSNBR
N02  C                   MOVE      'SHU1626'     ZMSGNO
N02  c                   seton                                        30
N02  c                   goto      feil
N02  C                   END
N02  C                   END
     C                   MOVE      WSBETB        OVBETB
     C                   MOVE      WSEPJN        EPOJN
     C                   MOVE      WSVENT        FVENT
     C                   UPDATE    FERDR
     C                   MOVE      ' '           WSNR
     C                   UPDATE    RECB
     C                   END
     C                   END
     C                   END
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    RECB
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    FEAVD
     C                   PARM                    FEOPD
     C                   PARM                    UFLAGG
     C                   END
     C                   ENDSR
