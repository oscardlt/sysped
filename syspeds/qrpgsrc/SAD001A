********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 søk tariffnr endring
10XXX * 01 SH  PTF10 gjort endringer litt direkte
10XXX * 02 SH  PTF10 kopiere avtaler
10244 * 03 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A
      *              MYE ENDRING. INGEN MERKING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 04 YBC PTF10 FLERE LINJER VISES
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KUNDERS VAREREGISTER      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-85 90 95 96 ***
      ***********************************************************
      *
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSADVARE   UF A E           K DISK
     FSADVARE1  IF   E           K DISK    RENAME(SADVARER:SADVARR1)
     FSADVARE2  IF   E           K DISK    RENAME(SADVARER:SADVARR2)
     FSAD001FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D                 DS
     D  VARENR                 1     28
     D  XDLT                   1      4
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      * CURSOR LOKASJON AV AKTIV VINDU
     D  WSCLOW               382    383B 0
      *
     C                   EXSR      INIT
      *
      *** LES KUNDENR.
     C     ST            TAG
     C                   MOVE      *BLANKS       W2VANR
     c                   seton                                        27
     c                   setoff                                       2829
     C                   EXFMT     BILDE01
     C                   SETOFF                                       309996
      *
     C   03              GOTO      SLUTT
      *
      * søk kunde
      *
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR            3 0
     C                   MVR                     POSNBR            3 0
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   EXSR      SØKUNR
     C     KUND1K        CHAIN     CUNDL01                            99
     C                   GOTO      ST
     C                   END
      *
      * slutt F4
      *
     C     KUND1K        CHAIN     CUNDL01                            99
     C   99              DO
     C                   MOVE      'SPK0001'     MSGNR
     C                   SETON                                        30
     C                   GOTO      ST
     C                   END
      *
      * kopiering
      *
     C     *IN02         IFEQ      '1'
     C     KUND1KC       CHAIN     CUNDL01                            96
     C   96              DO
     C                   MOVE      'SPK0001'     MSGNR
     C                   SETON                                        30
     C                   GOTO      ST
     C                   END
     c                   exsr      kopisr
     c                   end
      *
      *
     C                   MOVE      *BLANKS       W2VANR
      *
      *
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2VANR        YVANR            28
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   27SADVA0S       SETLL     SADVARE
     C   28SADVA1S       SETLL     SADVARE1
     C   29SADVA2S       SETLL     SADVARE2
     C     START1        TAG
     C   27WSKUNR        READE(N)  SADVARE                                94
     C   28WSKUNR        READE     SADVARE1                               94
     C   29WSKUNR        READE     SADVARE2                               94
     C   94              MOVE      *BLANKS       VARENR
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
S04  C*                  DO        15
N04  C                   DO        18
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C   27WSKUNR        READE(N)  SADVARE                                94
     C   28WSKUNR        READE     SADVARE1                               94
     C   29WSKUNR        READE     SADVARE2                               94
     C  N94              MOVE      VARENR        YVANR            28
     C  N94              MOVE      VAREBE        YVABE            50
     C  N94              MOVE      W2VNTI        YVNTI             8
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   SETOFF                                       98
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     NYVARE
     C                   GOTO      ST41
     C                   ELSE
     C                   END
     C                   WRITE     SUBCTL
      *
      *** AVSLUTT
     C     ST41          TAG
     C                   EXFMT     BUNN
     C                   SETOFF                                       9830
     C                   MOVE      *BLANKS       VARENRGM         28
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY VARE
     C   06              GOTO      START5
      *** SØK FRA VARE
     C   08              GOTO      START6
      *** TIL BILDE SOM HENTER KUNDENR
     C   12              GOTO      ST
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C     *IN27         IFEQ      '1'
     C  N94SADROL0       SETLL     SADVARE
     C  N94WSKUNR        READE     SADVARE                                94
     C                   END
     C     *IN28         IFEQ      '1'
     C  N94SADROL1       SETLL     SADVARE1
     C  N94WSKUNR        READE     SADVARE1                               94
     C                   END
     C     *IN29         IFEQ      '1'
     C  N94SADROL2       SETLL     SADVARE2
     C  N94WSKUNR        READE     SADVARE2                               94
     C                   END
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
      *
      *** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     SADLK2        CHAIN     SADVARE                            33
     C  N33              DELETE    SADVARER
     C                   EXSR      MOVB1
     C                   EVAL      VARENR = '*DLT'
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
      *
      *** LAGE/ENDRE VARE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       818283
     C                   SETOFF                                       84
     C                   SETOFF                                       898887
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
      *
      *** SØK FRA UTSKRIFTTYPE
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   setoff                                       272829
     C     W2VABE        COMP      *BLANKS                            2828
     C     W2TARI        COMP      *BLANKS                            2929
     C  N28
     CANN29              seton                                        27
     C                   EVAL      W2VABE50 = W2VABE
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
      *
      * TEST OM SØKETEKST ER TASTET
     C  N40              DO
     C     WSVANR        COMP      *BLANKS                                89
     C   89              MOVE      'SPF0002'     MSGNR
     C   89              SETON                                            30
     C   89              GOTO      SLT1
     C                   END
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     SADLK         CHAIN     SADVARE                            33
     C  N33
     CANN40              DO
     C                   MOVE      'SHU0801'     MSGNR
     C                   SETON                                        3089
     C                   GOTO      SLT1
     C                   END
     C                   IF        VARENRGM = *BLANKS
     C                   EXSR      MOVWSL
     C  N33              EXCEPT    OPDREC
     C   33              EXCEPT    ADDREC
     C                   ELSE
     C     SADLKKOPI     CHAIN(N)  SADVARE                            33
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVWSL
     C                   WRITE     SADVARER
     C                   END
      *
      * KALLER EKSTERNT PROGRAM
      *
     C                   CALL      'SAD001'
     C                   PARM                    LEVENR
     C                   PARM                    VARENR
     C                   PARM                    VAREBE
      *
      * CHAIN FOR SUBFIL
      *
     C     SADLK         CHAIN(N)  SADVARE                            33
      *
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
      *
     C     SLT1          ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     XDLT          COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVSLW
     C                   MOVE      ' '           WSNR
     C                   SETON                                        40
     C                   SETOFF                                       999789
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '3'
     C     XDLT          COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVSLW
     C                   MOVE      VARENR        VARENRGM
     C                   SETOFF                                       40
     C                   SETOFF                                       999789
     C                   MOVE      ' '           WSNR
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     XDLT          COMP      '*DLT'                                 98
     C                   GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVWSL        BEGSR
      ***********************************************
     C                   MOVE      WSKUNR        LEVENR
     C                   MOVE      WSVANR        VARENR
     C                   MOVE      WSVABE        VAREBE
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVSLW        BEGSR
      ***********************************************
     C                   MOVE      VARENR        WSVANR
     C                   MOVE      VAREBE        WSVABE
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB1         BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       VARENR
     C                   MOVE      *BLANKS       VAREBE
     C                   ENDSR
      *
      ***********************************************
     C     MOVB2         BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       WSVANR
     C                   MOVE      *BLANKS       WSVABE
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SADLK         KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSVANR
     C     SADLKKOPI     KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    VARENRGM
      *
      * SETLL KEYER
      *
     C     SADVA0S       KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2VANR
     C     SADVA1S       KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2VABE50         50
     C     SADVA2S       KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2TARI
      *
      * SETLL KEYER
      *
     C     SADLK2        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    VARENR
     C     SADROL0       KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    YVANR
     C     SADROL1       KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    YVABE
     C     SADROL2       KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    YVNTI
     C                   KFLD                    YVANR
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     KUND1KC       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNRC
     C                   MOVE      *BLANKS       W2VANR
     C                   MOVE      *BLANKS       WSTAR             3
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   SETON                                        27
     C                   ENDSR
      *
      ***********************************************
     C     SØKUNR        BEGSR
      ***********************************************
     C     linnbr        IFEQ      11
     C                   MOVE      *BLANKS       USØKKN
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     c                   end
      *
     C     linnbr        IFEQ      13
     C                   MOVE      *BLANKS       USØKKN
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNRC
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     c                   end
     C                   ENDSR
      ***********************************************
     C     kopisr        begsr
      ***********************************************
     C                   setoff                                       33
     C     WSKUNRC       SETLL     SADVARE
     C     *in33         doweq     '0'
     C     wskunrc       reade(n)  sadvare                                33
     C     *in33         ifeq      '0'
     C                   move      wskunr        levenr
     C                   write     sadvarer
     C                   end
     C                   end
     C                   endsr
      *
      *
     OSADVARER  E            OPDREC
     O                       LEVENR
     O                       VARENR
     O                       VAREBE
     OSADVARER  EADD         ADDREC
     O                       LEVENR
     O                       VARENR
     O                       VAREBE
