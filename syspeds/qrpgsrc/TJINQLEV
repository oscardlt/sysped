      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQLEV  MODULE(*LIBL/TJINQLEV  *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 innføre AKT-kod    .....
********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FLEVEL01   IF   E           K DISK    USROPN
     FLEVEL02   IF   E           K DISK    USROPN
     F                                     RENAME(LEVER:LEVER02)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D Kode            S              8
     D KodeN           S              8  0
     D KodeLest        S              8  0
     D KodeMax         S              8  0
     D Tekst           S             10
     D TekstLest       S             10
     D TekstMax        S             10
     D PNR             S             10
     D GetVal          S              1                                         J=Get/validate
     D FullInfo        S              1                                         J=Ret. full info
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D MaxLes          S              9  0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬kode¬ : ¬'+%trim(kode)+'¬, '
        +'¬tekst¬ : ¬'+%trim(tekst)+'¬, '
        +'¬getVal¬ : ¬'+%trim(getVal)+'¬, '
        +'¬fullInfo¬ : ¬'+%trim(fullInfo)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"leverandorer" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hente koder
      * (ved begge utfylt er kode prio 1/tekstsøk blankes)
     c     Error         ifeq      *blanks
     c**                 eval      KODEMAX=%trim(KODE)+'999999999'
     c                   eval      TEKSTMAX=%trim(TEKST)+'999999999'
     c                   IF        KODE<>*blanks or TEKST=*blanks
     c                   eval      TEKST = *blanks
     C     LevKey01      SETLL     LEVEL01                                      søk via kode
     C     BIFIRM        READE     LEVEL01                                55
     c                   else
     C     LevKey02      SETLL     LEVEL02                                      søk via tekst
     C     BIFIRM        READE     LEVEL02                                55
     c                   endif
     C     *IN55         DOWEQ     *OFF
     c                   eval      KodeLest  = LEVNR
     c                   eval      TekstLest = SONAVN
     c**                 IF        KODE <> *blanks and
     c**                           KodeLest  > KODEMAX
     c**                 goto      Utles
     c**                 ENDIF
     c                   IF        TEKST<> *blanks and
     c                             TekstLest > TEKSTMAX
     c                   goto      Utles
     c                   ENDIF
     C                   Add       1             AntLest
     c     Antlest       cabgt     maxles        Utles
      * posten skal med ..
     c                   move      *blanks       PNR
     c                   if        POSTNR <> *zeros
     c                   movel     POSTNR        PNR
     c                   else
     c                   movel     POSTNU        PNR
     c                   endif
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
N01       +'¬aktkod¬ : ¬'+%trim(AKTKOD)+'¬, '
          +'¬levnr¬ : ¬'+%trim(%editc(LEVNR:'Z'))+'¬,'
          +'¬lnavn¬ : ¬'+%trim(LNAVN)+'¬,'
          +'¬adr1¬ : ¬'+%trim(ADR1)+'¬,'
          +'¬adr2¬ : ¬'+%trim(ADR2)+'¬,'
          +'¬postnr¬ : ¬'+%trim(PNR)+'¬,'
          +'¬sted¬ : ¬'+%trim(ADR3)+'¬,'
          +'¬land¬ : ¬'+%trim(LAND )+'¬';
       if FullInfo='J';
        JSONstring=%trim(JSONstring)+', '
          +'¬kundnr¬ : ¬'+%trim(%editc(KUNDNR:'Z'))+'¬';
       endif;
       JSONstring=%trim(JSONstring)+'}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ved get/Validate  er det kun en
     c     GetVal        ifeq      'J'
     c                   leave
     c                   endif

     C     NEXT          TAG

     C                   IF        KODE<>*blanks or TEKST=*blanks
     C     BIFIRM        READE     LEVEL01                                55
     C                   ELSE
     C     BIFIRM        READE     LEVEL02                                55
     C                   ENDIF
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     LEVEL01
     C  N50              CLOSE     LEVEL02

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      KODE    = zhbgetvar('KODE')
     C                   EVAL      KODEN   = c2n2(KODE)
     C                   EVAL      TEKST   = zhbgetvar('TEKST')
     C     LO:UPC        XLATE     TEKST         TEKST
     C                   EVAL      GetVal  = zhbgetvar('GetVal')
     C                   EVAL      FullInfo= zhbgetvar('FullInfo')
     C
     c                   endsr
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     LevKey01      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    KODEN
     C     LevKey02      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    TEKST

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      LEVEL01
     C  N50              OPEN      LEVEL02
      *
     c                   IF        MAXLES = 0
     c                   z-add     100           MAXLES
     c                   ENDIF
      *
     C                   IF        *IN50=*OFF
     C                   IF        GetVal='J'
     C     LevKey01      CHAIN     LEVEL01                            30
     C   30              eval      Error = 'Angitt leverandør finnes ikke'
     C                   ENDIF
     C                   ENDIF
     c                   endsr
