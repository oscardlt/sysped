     FTRACKF    UP   E             DISK
     D                 DS
N03  D  TTTXTA                 1     70
     D  TXDATE                 1      5
     D  ZEDTMO                 7     12
     D  ZEDTM2                 7     14
     D  ZEDTD                  7      8
     D  ZEDTM                  8     10
     D  ZEDTÅ                 11     12
     D  TXTIME                15     19
     D  TXTIM2                17     19
     D  ZEKLMO                21     24
     D  TXBY                  27     29
     D  ZESGM                 31     40
     D  TXSTKO                54     57
     D  XXSTKO                59     61
     D                 DS
     D  TTDATE                 1      8  0
     D  TTDTEH                 1      2  0
     D  TTDTEÅ                 3      4  0
     D  TTDTEM                 5      6  0
     D  TTDTED                 7      8  0
      *
     C     TTDATL        IFEQ      *ZEROS
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
      *
      * POD/PO0 HAR EVENT-DATO/TID I DS.
      * PO0 BRUKT HOS ENKELTE(F.MAAS) VED MOTTAK AV INFO OM SLIKE TING
      *     SOM UTKJØRT M.M. (HENDELSER FØR POD...)
     C     TTACTI        IFEQ      'POD'
     C     TTACTI        OREQ      'PO0'
      * OBS!!! Hos ulike kunder med EDI-mottak av POD har det vært
      *        litt ulik praksis med lagring i TTTEXT (date time)
      * F.eks Frans Maas bør ha spesialvri OM en skal får rett
      *       TTDATE/TTTIME
      * Dette dekker SYSPED STANDARD (logging i SYFA94R)
     C                   MOVE      TTTEXT        TTTXTA
     C*
     C* Sjekk at standard DS er brukt...
     C     TXDATE        IFEQ      'Date:'
     C* Date Event
     C     TXTIME        IFEQ      'Time:'
     C                   TESTN                   ZEDTMO               34
     C     *IN34         IFEQ      '1'
     C* FMT ÅÅDDMM FOR De som har kjørt ptf på syfa94r etter 2003.06.18
     C*          TAR SJANSEN PÅ AT NÅR ÅR I TEXTDS IKKE STEMMER
     C*          MED LOGGFØRINGSÅR SÅ LIGGER DATOEN ÅÅDDMM
     C*          I ALLE FALL NÅR DE 2 SISTE I DATOEN ER MELLOM 5 - 31!
     C                   MOVE      ZEDTÅ         ZEDTÅN            2 0
     C     ZEDTÅN        IFNE      TTDTEÅ
     C     ZEDTÅN        ANDGE     5
     C     ZEDTÅN        ANDLE     31
     C                   MOVE      ZEDTD         TTDTEÅ
     C                   MOVE      ZEDTM         TTDTEM
     C                   MOVE      ZEDTÅ         TTDTED
     C                   ELSE
     C                   MOVE      ZEDTÅ         TTDTEÅ
     C                   MOVE      ZEDTM         TTDTEM
     C                   MOVE      ZEDTD         TTDTED
     C                   END
     C     TTDTEÅ        IFLT      49
     C                   MOVE      20            TTDTEH
     C                   ELSE
     C                   MOVE      19            TTDTEH
     C                   END
     C                   END                                                     *IN34
     C                   ELSE                                                    TXTIME
      * 8.SIFRET DATO PÅ EN DEL EDI (F.EKS F.MAAS IFTSTA )
     C     TXTIM2        IFEQ      'Ti:'
     C                   TESTN                   ZEDTM2               34
     C     *IN34         IFEQ      '1'
     C                   MOVE      ZEDTM2        TTDATE
     C                   END
     C                   END
     C                   END
     C* Time Event
     C                   TESTN                   ZEKLMO               34
     C     *IN34         IFEQ      '1'
     C                   MOVEL     ZEKLMO        TMPTIM            6
     C                   MOVE      '00'          TMPTIM
     C                   MOVE      TMPTIM        TTTIME
     C                   END
      *
     C                   END                                                     TXDATE
      *
     C     TXBY          IFEQ      'By:'
     C                   MOVEL     ZESGM         TTNAME
     C                   END
      * (kun f.maas har Edi statuskode lagret...?)
     C     TXSTKO        IFEQ      'Sts:'
     C                   MOVEL     XXSTKO        TTEDEV
     C                   END
      *
     C                   END
     C                   END
      *
     C                   UPDATE    TRACKFR
