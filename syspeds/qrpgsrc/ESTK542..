      *  Obs ! Kompiler med tillat error 20  GENLVL(20)    (decedit angitt 2 ganger-siste ignoreres)
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK542 MODULE(*LIBL/ESTK542 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF11 Ved UKJENT USER kan en ikke kalle jsonfix (manglende libl..)
10XXX * 02 JOV PTF11 Når kun øyeblikksmelding sendes skal ikke 'endring' bekreftes.
10XXX * 03 JOV PTF11 hente/Lev-klokkeslett i egne TAGer
10XXX * 04 JOV PTF11 hindre send av endret hentekode dersom det er levering ...
********************************************************************
     H decedit('0.')
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FTURER14   UF   E           K DISK    USROPN
     FTKSGM     UF A E           K DISK    USROPN
     FTurer20   IF   E           K DISK    USROPN
     F                                     RENAME(TURERR:TURERR20)
     FHeadF     IF   E           K DISK    USROPN
     FHead11    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR11)
     FFREETXT2  UF   E           K DISK    USROPN
     FFREETUR2  UF   E           K DISK    USROPN
     FDOKUF     IF   E           K DISK    USROPN
     FDOKUFV    IF   E           K DISK    USROPN
     FDOKUFM    IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FTRACKL02  IF   E           K DISK    USROPN
     FKUFAST    IF   E           K DISK    USROPN
     FKODTRI    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
     D JSONstring      s          32000
      * Prototype
     D JSONescape      PR                  ExtPgm('JSONESC')
     D   JSONstr                  32000A
     D IfsMultIndicators...
     D                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Client input variables
     D userId          s             10
     D UsrSpcName      s             10                                         =
     D Tur             s              8
     D TurNr           s              8  0
     D HEAVDA          s              4
     D HEOPDA          s              7
     D Snd             s             20
     D Snd17           s             17
     D Snd20           s             20
     D AvdAA           s             20
     D OpdAA           s             20
     D SJA             s              8
     D SJN             s              8  0
N03  D HenKl           s              5
N03  D LevKl           s              5
     D VERSJ           s             10
     D TurInfo         s             20
     D Nr              s              2  0
     D FØRSTEOPD       s              1
     D Refresh         s              1
     D Tegn            s              1
     D HexTegn         s              2
     D Endr            s           1000
     D ItemCount       s             10i 0
     D i               s             10i 0
     D i2              s             10i 0
      * Definere  hex Æ Ø Å æ ø å
     D HexÆ            c                   x'6606'
     D HexØ            c                   x'6638'
     D HexÅ            c                   x'66B2'
     D HexÆl           c                   x'6670'
     D HexØl           c                   x'669D'
     D HexÅl           c                   x'66B2'
      * Definere  hex appostrof
     D Appo            c                   x'7D'
      * Definere  hex 0D / 25 (CRLF)
     D Hex0D           c                   x'0D'
     D Hex25           c                   x'25'
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  HXSNDN                 1     20
     D  HXSND17B               4     20
     D  HXSND11_20            11     20
     D                 DS
     D  DOKFM1K                3     37
     D   DSSnd                 1     20
     D                 DS
     D   DSSnd2                1     20
     D   DSSnd1_3              1      3
     D   DSSnd4_20             4     20
     D   DSSnd11_20           11     20
     D   DSSnd17_17           17     17
     D   DSSnd18_18           18     18
     D                 DS
     D  DSAVOP                 1     12
     D  HEAVD                  1      4  0
     D  Strek                  5      5
     D  HEOPD                  6     12  0
     D                 DS
     D  TUDTA                  1      8
     D  TudtÅ                  1      4
     D  TudtM                  5      6
     D  TudtD                  7      8
     D                 DS
     D  TRSDFD                 1      8  0
     D  FMM                    5      6
     D  FDD                    7      8
     D                 DS
     D  TRSDFK                 1      4  0
     D  FTI                    1      2
     D  FMI                    3      4
     D                 DS
     D  TRSDTD                 1      8  0
     D  TMM                    5      6
     D  TDD                    7      8
     D                 DS
     D  TRSDTK                 1      4  0
     D  TTI                    1      2
     D  TMI                    3      4
     D                 DS
     D  HEVS1                  1     25
     D  HEVS1A                 1      4
     D  HEVS1B                 5     25
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      *
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  Info                   1    700
     D  INF                    1    700
     D                                     DIM(10)
     D                 DS
     D  TGInfo                 1    700
     D    TGIN01               1     70
     D    TGIN02              71    140
     D    TGIN03             141    210
     D    TGIN04             211    280
     D    TGIN05             281    350
     D    TGIN06             351    420
     D    TGIN07             421    490
     D    TGIN08             491    560
     D    TGIN09             561    630
     D    TGIN10             631    700
     D  TGINF                  1    700
     D                                     DIM(10)
     D                 DS
     D  Tinf                   1    700
     D  TIN                    1    700
     D                                     DIM(10)
     D                 DS
     D  HAR                    1    900
     D                                     DIM(100)
     D                 DS
     D  TTUSER                 1     10
     D  TTUS12                 1      2
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Parse input / cvt to numeric
     C                   exsr      Parse
N01  C                   exsr      LoadHtml
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
S01  C*                  exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
      * Userid = PD+8 siffer (8 siffer er sjåførid)
     C                   eval      userId = zhbgetvar('userId')
     C                   move      userId        SJA
     C                   eval      SJN     = c2n2(SJA)
      * versjonsnr av klientprogramvare
     C                   eval      VERSJ = zhbgetvar('V')
     C                   eval      TUR  = zhbgetvar('TUR')
     C                   eval      TURNR   = c2n2(TUR)
     C                   eval      ReFresh  = zhbgetvar('REFR')
     C                   eval      Snd  = zhbgetvar('SND')
     C     REFRESH       IFEQ      ' '
     C     Turnr         oreq      *zeros
     C                   MOVE      'N'           REFRESH
     C                   END
     C                   eval      UsrSpcName= zhbgetvar('sessionId')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     TURER20
     C                   CLOSE     TURER14
     C                   CLOSE     HEADF
     C                   CLOSE     HEAD11
     C                   CLOSE     FRISOK
     C                   CLOSE     DOKUF
     C                   CLOSE     DOKUFV
     C                   CLOSE     DOKUFM
     C                   CLOSE     FREETXT2
     C                   CLOSE     FREETUR2
     C                   close     TKSGM
     C                   close     Trackl02
     C                   close     KUFAST
     C                   close     KODTRI
     C                   close     BRPARF
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
     C                   callp     wrtsection('top')
      /free
        JSONstring='{'
        +'¬userId¬ : ¬'+%trim(userId)+'¬, '
        +'¬error¬ : ¬¬, ';
         exsr SkrivJSON;
      /end-free
      *
      * Turnr oppgitt > gå innpå den
     C                   exsr      ENTUR
      *
      *  avslutt Oppdr-liste, turobjektet OG hele strengenn
      /free
        JSONstring=']}}';
        updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
        wrtsection('JSONlin');
      /end-free
     C
     C                   endsr
      *=====================================================================******
      *  EnTur - Legg ut sendingsdta for EN tur
      *=====================================================================******
     C     EnTur         begsr
      * EN tur - vis godsliste - sett kode 1=hentet i TUSTS4
     C     TURNR         CHAIN     TURER14                            33
     C     *IN33         IFEQ      '0'
     C     REFRESH       IFeq      'N'
     C                   move      '1'           TUTST4
     C                   END
     C                   update    turerr
     C                   END
      * Finn antall turer i turvelgere for å varsle ved endring..
     C                   EXSR      ANTTSR
     C     TURNR         CHAIN(N)  TURER14                            33
      * Initier oversikt over oppdrag som alt er sendt til pda på denne turen
     c                   exsr      IniTKSGM
      *
      * Skriv turHeader
     C                   Move      *zeros        Antpos            5 0
     C                   exsr      Turhead
      *
      * Les tur (i stigende pos.nr)
     C     TUPRO         setll     HEAD11
     C     TUPRO         Reade     HEAD11                                 33
     C     *IN33         DOWEQ     '0'
      * Hent info som må"settes sammen" for å kunne  teste mot gml v/refresh
     C                   EXSR      GetInfo
     C                   movea     '0000000000'  *IN(60)                        60-69
     C                   movea     '0000000000'  *IN(70)                        70-79
     C                   movea     '0000000000'  *IN(80)                        80-89
      *
      * finn status posten skal ha dersom den sendes
     c                   exsr      StatTKs
      * Ved refresh sjekk om posten finnes OG i så fall om noe er endret (sammenlign mot TKSGM)
     C     Refresh       IFEQ      'J'
     C                   Exsr      FinsOPD
      * 60 ON = NOE er endret
     C     Finnes        IFEQ      'J'
     C     *in60         andeq     *off
     C                   GOTO      NXTOPD
     C                   end
     C                   end
      * dersom oppdrag alt er levert aksepteres ikke TIL PDA
     C                   move      'POD'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     C     *IN33         doweq     '0'
     c     TTEDRE        ifne      'KOP'
     c                   leave
     c                   endif
     c     TrackKey      reade     TrackL02                               33
     c                   enddo
     C     *IN33         cabeq     '0'           NXTOPD
      * "levert" til terminal logges som TE2 (sluttlevering)
      * logikk tar høyde for at det KAN finnes TE2 tidligere i kjeden..- Kun "Signed by" gir skipp.
     C                   move      'TE2'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *IN33         doweq     '0'
     c     TTEDRE        ifne      'KOP'
     c     TTUS12        andeq     'PD'                                         kun TE2 fra PDA=POD
     c                   leave
     c                   endif
     c     TrackKey      reade     TrackL02                               33
     c                   enddo
     C     *IN33         cabeq     '0'           NXTOPD
      * Ved FF logges "levert" til terminal som TEI
     c     TRSLAG        ifeq      'FF'
     C                   move      'TEI'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     C     *IN33         doweq     '0'
     c     TTEDRE        ifne      'KOP'
     c                   leave
     c                   endif
     c     TrackKey      reade     TrackL02                               33
     c                   enddo
     C     *IN33         cabeq     '0'           NXTOPD
     c                   endif
      *
      * Klargjør post som
     c                   if        Refresh = 'N'
     c                   exsr      SkrivEnlinje
     c                   else
     c                   exsr      RefrEnlinje
     c                   endif
      * Skriv tilloggfil - sist sendt(er i TKsign FØR posten sendes(pga blanking av HE..felt)
     c                   exsr      LogTKS
      *
     C     NXTOPD        tag
     C     TUPRO         Reade     HEAD11                                 33
     C                   END
      ***
      * Ved refresh - send anmodning om fjerning av de som nå ikke er på turen
     C     Refresh       IFEQ      'J'
     C                   Exsr      DelOPD
     C                   END
      *
     C                   endsr
      *
      *

      * ***********************************
     C     ANTTSR        begsr
      * ***********************************
      * EGEN linje for evt overvåking av antall turer i "Hent liste"
     C                   MOVE      'TKATU'       PAID
     C     FiParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      *OFF
     C                   MOVEl     PAVRDA        TKATUjn           1
     C     TKATUjn       CABNE     'J'           UtAntt
     c                   end
      *
     C                   move      *zeros        AntTur            5 0
     C     TURKEY20      setll     TURER20
     C     TURKEY20      Reade     TURER20                                33
     C     *IN33         DOWEQ     '0'
     C                   ADD       1             AntTur
     C     TURKEY20      Reade     TURER20                                33
     C                   end
     c     UtAntt        tag
     C                   endsr
     C
      *=====================================================================******
      *  TurHead
      *=====================================================================******
     C     TurHead       begsr
      * Turheader-record med status
      * sendes alltid ved original/kun ved endring når refresh
      * Utfra turstatus velger TKSign hva den skal gjøre når alle oppdrag er Ferdig
     C                   move      TUPRO         TUA               8
     C     TUST          IFEQ      'P'                                          Protect= I arbeid
     C                   move      ' '           TUST                             = åpen
     c                   end
     C     TUST          IFEQ      ' '
     C                   Move      '*BT*'        TUSTA             4            Behold tur
     c                   else
     C                   Move      '*DT*'        TUSTA             4            Delet tur
     c                   end
      * foreløpig brukes turrecorden i TKSGM kun for å lagre status (I TGHEKO)
      * men med tiden kan en tenke seg at turinfo legges i INFOFELT...
     C                   clear                   TKSGMR
     C                   move      TUPRO         TGPRO
     C     GmTurKey      CHAIN     TKSGM                              35
     C                   add       1             AntPos
     C                   MOVE      TUPRO         TUPROA            8
     C                   exsr      GetTurInfo
      /free
        JSONstring= '¬tur¬ :  {'
        +'¬turNr¬ : ¬'     +TUPROA                               +'¬, '
        +'¬dato¬ : ¬'      +%trim(%editW(TUDT:'    .  .  '))     +'¬, '
        +'¬status¬ : ¬'    +%trim(TUSTA)                         +'¬, '
        +'¬antOrd¬ : '     +%trim(%editc(TUAO:'3'))              +', '
        +'¬vekt¬ : '       +%trim(%editc(TUTVKT:'3'))             +', '
        +'¬volum¬ : '      +%trim(%editc(TUTM3:'J'))              +', '
        +'¬fra¬ : ¬'       +%trim(TUSDF)                         +'¬, '
        +'¬til¬ : ¬'       +%trim(TUSDT)                         +'¬, '
        +'¬turMelding¬ : ¬'+%trim(TMeld)                         +'¬, ';
        exsr GetTurInfo2;
        exsr SkrivJSON;
        // start oppdragsListe (skader ikke om den er tom
         JSONstring= '¬oppdrag¬ : [';
         exsr SkrivJSON;
      /end-free
     C
     C                   endsr
      *
      *=====================================================================******
      *  TURINFO fra notisblokk
      *=====================================================================******
     C     GetTurInfo    begsr
      *
     C                   move      *zeros        nr
     C                   move      *blanks       Tinf
     C                   MOVE      'G'           XXSKKO
     C     FREETURK      SETLL     FREETUR2
     C     FREETURK      READE     FREETUR2                               33
     C     *in33         doweq     '0'
     C                   add       1             nr
     C                   Eval      Tin(nr)= FRTTXT
     C     Nr            cabge     10            utTinf
     C     FREETURK      READE     FREETUR2                               33
     c                   end
      *
     C     utTinf        TAG
      * Hvis refresh - sett indikator for endret fritekst
     C     Refresh       IFEQ      'J'
     C     TGIN01        COMP      TIN(01)                                71
     C     TGIN02        COMP      TIN(02)                                72
     C     TGIN03        COMP      TIN(03)                                73
     C     TGIN04        COMP      TIN(04)                                74
     C     TGIN05        COMP      TIN(05)                                75
     C     TGIN06        COMP      TIN(06)                                76
     C     TGIN07        COMP      TIN(07)                                77
     C     TGIN08        COMP      TIN(08)                                78
     C     TGIN09        COMP      TIN(09)                                79
     C     TGIN10        COMP      TIN(10)                                80
     c* Er felt endret?  -> 60 ON
     C                   move      '0'           *IN60
     c     *in71         ifeq      '0'
     c     *in72         oreq      '0'
     c     *in73         oreq      '0'
     c     *in74         oreq      '0'
     c     *in75         oreq      '0'
     c     *in76         oreq      '0'
     c     *in77         oreq      '0'
     c     *in78         oreq      '0'
     c     *in79         oreq      '0'
     c     *in80         oreq      '0'
     C     TUST          orne      TGHEKO
     C                   move      '1'           *IN60
      * sist endret
     C                   MOVE      ULDT          TGDTH
     C                   MOVE      XULTM         TGTIH
     c                   end
     c                   end
      *
      * OPPDATER TKSGM
     C                   MOVE      TUST          TGTUST
     C                   move      TIN(01)       TGIN01
     C                   move      TIN(02)       TGIN02
     C                   move      TIN(03)       TGIN03
     C                   move      TIN(04)       TGIN04
     C                   move      TIN(05)       TGIN05
     C                   move      TIN(06)       TGIN06
     C                   move      TIN(07)       TGIN07
     C                   move      TIN(08)       TGIN08
     C                   move      TIN(09)       TGIN09
     C                   move      TIN(10)       TGIN10
      * sist hentet/endret
     C                   MOVE      ULDT          TGDTH
     C                   MOVE      XULTM         TGTIH
     c                   move      TUST          TGHEKO
      * sist endret
     C                   MOVE      TUST          TGTUST
     C                   MOVE      ULDT          TGDTH
     C                   MOVE      XULTM         TGTIH
      * sist Refreshet
     C                   MOVE      ULDT          TGDTR
     C                   MOVE      XULTM         TGTIR
     C   35              write     TKSGMR
     C  N35              update    TKSGMR
      *
     C                   ENDSR
      *=====================================================================******
      *  TURINFO ut til TKsmart
      *=====================================================================******
     C     GetTurInfo2   begsr
      *
      *  Refresh/Ikke endret - skip fritekst
     c                   if        Refresh = 'J' and *in60='0'
     c                   goto      UtTinf2
     c                   endif
      * Start info/fritekst
     C                   eval      JSONstring=%trim(JSONstring)
     C                             + '¬info¬ : ['
      *
     c                   move      'Y'           FØrsteFtx         1
     c                   move      Nr            Nr2               2 0
     C     1             DO        Nr2           Nr
     c                   if        FØrsteFtx = 'N'
     C                   eval      JSONstring=%trim(JSONstring)+', '
     c                   endif
     C                   eval      JSONstring=%trim(JSONstring)
     C                             + '¬'  +%trim(Tin(Nr)) +'¬'
     c                   move      'N'           FØrsteFtx
     c                   enddo

      * slutt fritekst
     C                   eval      JSONstring=%trim(JSONstring)+'], '
     c     UtTinf2       tag
      *
      *
      * Linjer fra notisblokk merket M=melding (øyeblikks..)
     C                   MOVE      *BLANKS       TMELD           210
     C                   MOVE      'M'           XXSKKO
     C     FREETURK      SETLL     FREETUR2
     C     FREETURK      READE     FREETUR2                               33
     C     *in33         doweq     *OFF
     C                   eval      Tmeld = %trim(Tmeld)+'  '+%trim(FRTTXT)
      * kode M-> N funker som status - ER sendt.
     c                   move      'N'           FRTKOD
     C                   update    FREETURR
     C     FREETURK      READE     FREETUR2                               33
     C                   END
      *
     C                   ENDSR
      **************************************************************
     C     SkrivEnlinje  begsr
      **************************************************************
      *
     c                   move      HEPRO         HEPROA            8
     C                   add       1             AntPos
      *
      *
      * Legg ut feltene i et oppdrag
      /free
       if FØrsteOpd = 'N';
         JSONstring=', ';
         exsr SkrivJSON;
       endif;
       FØrsteOpd='N';

        JSONstring='{'
        +'¬turNr¬ : ¬'     +HEPROA                               +'¬, '
        +'¬pos¬ : ¬'       +%trim(HEPOS1)                        +'¬, '
        +'¬avd¬ : ¬'       +%trim(%editc(HEAVD:'3'))             +'¬, '
        +'¬opd¬ : ¬'       +%trim(%editc(HEOPD:'3'))             +'¬, '
        +'¬dato¬ : ¬'      +%trim(%editW(TUDT:'    .  .  '))     +'¬, '
        +'¬sendingsNr¬ : ¬'+%trim(HXSND17)                       +'¬, '
        +'¬senNavn¬ : ¬'   +%trim(HENAS)                         +'¬, '
        +'¬senAdr1¬ : ¬'   +%trim(HEADS1)                        +'¬, '
        +'¬senAdr2¬ : ¬'   +%trim(HEADS2)                        +'¬, '
        +'¬senAdr3¬ : ¬'   +%trim(HEADS3)                        +'¬, '
        +'¬henTerm¬ : ¬'   +%trim(HenTerm)                       +'¬, '
        +'¬motNavn¬ : ¬'   +%trim(HENAK)                         +'¬, '
        +'¬motAdr1¬ : ¬'   +%trim(HEADK1)                        +'¬, '
        +'¬motAdr2¬ : ¬'   +%trim(HEADK2)                        +'¬, '
        +'¬motAdr3¬ : ¬'   +%trim(HEADK3)                        +'¬, '
        +'¬levTerm¬ : ¬'   +%trim(LevTerm)                       +'¬, '
N03     +'¬henKl¬ : ¬'     +%trim(HenKl)                         +'¬, '
N03     +'¬levKl¬ : ¬'     +%trim(LevKl)                         +'¬, '
        +'¬antKll¬ : '     +%trim(%editc(HENT:'3'))              +', '
        +'¬vekt¬ : '       +%trim(%editc(HEVKT:'3'))             +', '
        +'¬volum¬ : '      +%trim(%editc(HEM3:'J'))              +', '
        +'¬lm¬ : '         +%trim(%editc(HELM:'J'))              +', '
        +'¬linInfo¬ : ¬'   +%trim(LININFO)                       +'¬, '
        +'¬hentKode¬ : ¬'  +%trim(HentKode)                      +'¬, '
        +'¬info¬ : [';
         exsr SkrivJSON;
        // Fritekst-Info bør gjøres om til leseloop rett på fila..
        if inf(01) <> *blanks;
          JSONstring='¬'     +%trim(INF(01))                     +'¬ ';
        endif;
        nr=2;
        dow INF(nr)<> *blanks;
          JSONstring=%trim(JSONstring)+', ¬' +%trim(INF(nr))     +'¬ ';
          nr=nr+1;
          if nr>10;
            leave;
          endif;
        enddo;
        JSONstring=%trim(JSONstring)                             +'], ';
         exsr SkrivJSON;
        JSONstring='¬sMeld¬ : ¬'     +%trim(Smeld)               +'¬, '
        +'¬statNext¬ : '  +%trim(%editc(StatNext:'3'))           +'} ';
         exsr SkrivJSON;
      /end-free
     C                   endsr
      *
      **************************************************************
     C     RefrEnlinje   begsr
      **************************************************************
      *
     c                   eval      Endr = *blanks
     c                   move      HEPRO         HEPROA            8
     C                   add       1             AntPos
      *
      *
      * Legg ut feltene i et oppdrag
      /free
       if FØrsteOpd = 'N';
         JSONstring=', ';
         exsr SkrivJSON;
       endif;
       FØrsteOpd='N';

        JSONstring='{'
        +'¬turNr¬ : ¬'     +HEPROA                               +'¬, '
        +'¬pos¬ : ¬'       +%trim(HEPOS1)                        +'¬, '
        +'¬avd¬ : ¬'       +%trim(%editc(HEAVD:'3'))             +'¬, '
        +'¬opd¬ : ¬'       +%trim(%editc(HEOPD:'3'))             +'¬, ';
          exsr SkrivJSON;

        if HXSND17  <> TGSN17;
          JSONstring= '¬sendingsNr¬ : ¬'+%trim(HXSND17)          +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Snr:'+%trim(TGSN17) +'->'+%trim(HXSND17)+'\n';
        endif;
        // Avsender
        if HENAS <> TGAVNA;
          JSONstring= '¬senNavn¬ : ¬'   +%trim(HENAS)            +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Avs:'+%trim(TGAVNA) +'->'+%trim(HENAS)  +'\n';
        endif;
        if HEADS1 <> TGAVA1;
          JSONstring= '¬senAdr1¬ : ¬'   +%trim(HEADS1)           +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Aa1:'+%trim(TGAVA1) +'->'+%trim(HEADS1) +'\n';
        endif;
        if HEADS2 <> TGAVA2;
          JSONstring= '¬senAdr2¬ : ¬'   +%trim(HEADS2)           +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Aa2:'+%trim(TGAVA2) +'->'+%trim(HEADS2) +'\n';
        endif;
        if HEADS3 <> TGAVA3;
          JSONstring= '¬senAdr3¬ : ¬'   +%trim(HEADS3)           +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Aa3:'+%trim(TGAVA2)  +'->'+%trim(HEADS3) +'\n';
        endif;
        if HenTerm <> TGHETE;
          JSONstring= '¬henTerm¬ : ¬'  +%trim(HenTerm)          +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'H-T:'+%trim(TGHETE)  +'->'+%trim(HenTerm) +'\n';
        endif;
        // Mottaker
        if HENAK <> TGMONA;
          JSONstring= '¬motNavn¬ : ¬'   +%trim(HENAK)            +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Mot:'+%trim(TGMONA) +'->'+%trim(HENAK)  +'\n';
        endif;
        if HEADK1 <> TGMOA1;
          JSONstring= '¬motAdr1¬ : ¬'   +%trim(HEADK1)           +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Ma1:'+%trim(TGMOA1) +'->'+%trim(HEADK1) +'\n';
        endif;
        if HEADK2 <> TGMOA2;
          JSONstring= '¬motAdr2¬ : ¬'   +%trim(HEADK2)           +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Ma2:'+%trim(TGMOA2) +'->'+%trim(HEADK2) +'\n';
        endif;
        if HEADK3 <> TGMOA3;
          JSONstring= '¬motAdr3¬ : ¬'   +%trim(HEADK3)           +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Ma3:'+%trim(TGMOA3) +'->'+%trim(HEADK3) +'\n';
        endif;
        if LevTerm  <> TGLETE;
          JSONstring= '¬levTerm¬ : ¬'  +%trim(LevTerm)          +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'L-T:'+%trim(TGLETE)  +'->'+%trim(LevTerm) +'\n';
        endif;
        // Ant /vekt/vol mm
        if HENT   <> TGNT;
          JSONstring= '¬antKll¬ : '     +%trim(%editc(HENT:'3')) +', ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Ant:'+%trim(%editc(TGNT:'3'))
                                      +'->'+%trim(%editc(HENT:'3'))  +'\n';
        endif;
        if HEVKT  <> TGVKT;
          JSONstring= '¬vekt¬ : '       +%trim(%editc(HEVKT:'3'))+ ', ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Vkt:'+%trim(%editc(TGVKT:'3'))
                                      +'->'+%trim(%editc(HEVKT:'3'))  +'\n';
        endif;
        if HEM3 <> TGM3;
          JSONstring= '¬volum¬ : '      +%trim(%editc(HEM3:'J')) +', ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'M3.:'+%trim(%editc(TGM3:'J'))
                                      +'->'+%trim(%editc(HEM3:'J'))  +'\n';
        endif;
        if HELM <> TGLM;
          JSONstring= '¬lm¬ : '         +%trim(%editc(HELM:'J')) +', ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'LM.:'+%trim(%editc(TGLM:'J'))
                                      +'->'+%trim(%editc(HELM:'J'))  +'\n';
        endif;
        if HentKode <> TGHEKO;
          JSONstring= '¬hentKode¬ : ¬'  +%trim(HentKode)         +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'HKO:'+%trim(TGHEKO)  +'->'+%trim(HentKode) +'\n';
        endif;
        //fritekst (minst en av 10 første lijner endret ,,,)
        if Info <> TGInfo;
          // EndrFri= hver delstreng trimmet i Sr  FinsOpd
          Endr=%trim(Endr)+ %trim(EndrFri);
        JSONstring= '¬info¬ : [';
         exsr SkrivJSON;
        // Fritekst-Info bør gjøres om til leseloop rett på fila..
        if inf(01) <> *blanks;
          JSONstring='¬'     +%trim(INF(01))                     +'¬ ';
        endif;
        nr=2;
        dow INF(nr)<> *blanks;
          JSONstring=%trim(JSONstring)+', ¬' +%trim(INF(nr))     +'¬ ';
          nr=nr+1;
          if nr>10;
            leave;
          endif;
        enddo;
        JSONstring=%trim(JSONstring)                             +'], ';
         exsr SkrivJSON;
        endif;
        //
        if LININFO <> TGVLIN;
          JSONstring= '¬linInfo¬ : ¬'   +%trim(LININFO)          +'¬, ';
          exsr SkrivJSON;
          Endr=%trim(Endr)+'Lin:'+%trim(TGVLIN)  +'->'+%trim(LINInfo) +'\n';
        endif;
        if sMeld <> *blanks;
          JSONstring= '¬sMeld¬ : ¬'     +%trim(Smeld)            +'¬, ';
          exsr SkrivJSON;
        endif;
        if Endr  <> *blanks;
          JSONstring= '¬oppdatering¬ : {'
                             +'¬timestamp¬ : ¬' +  TS            +'¬, '
                             +'¬endring¬ : ¬'  +%trim(Endr)      +'¬}, ';
          exsr SkrivJSON;
        endif;
        // Neste status er alltid med, benyttes som siste felt/ avslutter ( } ) ett oppdrag
          JSONstring= '¬statNext¬ : ' +%trim(%editc(StatNext:'3'))  +'} ';
          exsr SkrivJSON;
      /end-free
     C                   endsr
      *
      **************************************************************
     C     SkrivJSON     begsr
      **************************************************************
      /free
        callp JSONescape(JSONstring);
        updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
        wrtsection('JSONlin');
      /end-free
     C                   endsr
      *
      *=====================================================================******
      *  StatTKS
      *=====================================================================******
     C     StatTKS       begsr
      * (ONS- I TKsign utføres StatTK ETTER alt annet, I TKsmart FØR
      *  Statusen inngår i sortering OG forteller - Hva er det neste som skal skje
      *  statuser: (0=Turheader 90=Ferdig)
      *            10= Ny sending, Henting (skal bekreftes)
      *            20= Ny sending, Levering (skal bekreftes)
      *            30= Oppdatering, henting (skal bekreftes)
      *            40= Oppdatering, levering (skal bekreftes)
      *            45= Henting Terminal
      *            50= Henting   Kunde
      *            60= Levering  (hos sluttkunde ELLER term/agent)
      *
      *  Henting hos kunde...
     C     Hentkode      IFEQ      '3'
     C                   move      50            StatNext          2 0
      * Henteoppdrag, men hentingen er alt utført/har vært innom term - Status/neste = Levering
     C                   move      'COL'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     C   33              move      'TEU'         TTACTI
     c   33TrackKey      Chain     TrackL02                           33
     C   33              move      'TEI'         TTACTI
     c   33TrackKey      Chain     TrackL02                           33
     C  N33              move      60            StatNext
     c                   else
      * henkode 0=starter på terminal - fra 2.15 - egen status (før -> leveres)
     C                   move      60            StatNext
     c     TKhenTerm     ifeq      'J'
     C                   move      45            StatNext
      * Hent/last på terminal, men lasting ER alt utført - Status/neste = Levering
     C                   move      'TEU'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     C  N33              move      60            StatNext                       H
     C     StatNext      Ifeq      45
     C                   moveL     HESDFF        HenTerm
     c     HenTerm       ifeq      *blanks
     C                   moveL     HESDF         HenTerm
     c                   end
     c                   end
     c                   end
     c                   end
      *
N02  C                   move      StatNext      OriStatNext       2 0
      *
      * ved Refresh NY /Endret så sendes egne koder (SKAL BEKREFTES/i top av lista)
     C     Refresh       Ifeq      'J'
      * NY
     C     *IN60         IFEQ      '0'                                          Intet endret= NY
     C     StatNext      IFEQ      50
     C     StatNext      oreq      45
     C                   move      10            StatNext                       Ny henting
     c                   else
     C                   move      20            StatNext                       Ny levering
     c                   end
     c                   else
      * Endret                                                                  60 ON=Noe endret
     C     StatNext      IFEQ      50
     C     StatNext      oreq      45
     C                   move      30            StatNext                       Endret henting
     c                   else
     C                   move      40            StatNext                       Endret levering
     c                   end
     c                   end
     c                   end
     c*
     C     UtStSTKS      tag
      * 2-sifret ststus/neste-kode sendes i stedet for 3/0 Hent /lev..
     C                   endsr
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Remove neg sign.
     C                   IF        ØN153 < 0
     C                   Z-SUB     ØN153         ØN153
     C                   ENDIF
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'
     C                   MOVE      ØA153H        ØA12
     C                   ELSE
     C                   MOVEL     ØA153X        ØA12
     C                   MOVE      ',   '        ØA12
     C                   MOVE      ØA153D        ØA12
     C                   END
      * search from left and find first sign over 0
     C     1             DO        11            ØX                2 0
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'
     C     ØA153X        IFEQ      '00000000'
     C                   SUB       1             ØX
     C                   ENDIF
     C     Ø12(12)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(12)
     C     Ø12(11)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(11)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      *BLANKS       ØAN              12
     C                   MOVEA     Ø12(ØX)       ØAN

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Hent inntil 10 linjer med info                           /
      *////////////////////////////////////////////////////////////
     C     GetInfo       BEGSR
      * Trmodul innland, langt/kort sendnr....
     c                   move      *blanks       HXSND17          17
     c     HXSNDN        ifne      *blanks
     c     HXSND11_20    ifne      *blanks
     c                   movel     HXSND17B      HXSND17                        Langt innland
     c                   else
     c                   movel     HXSNDN        HXSND17                        Kort innland
     c                   end
     c                   end
      * Om IKKE fra trmodul innland hent sndnr fra dokuf  / evt IFB
     C     HXSND17       IFEQ      *BLANKS
     C     DOKUFKEY      CHAIN     DOKUF                              33
     c     *in33         ifeq      '0'
     C  N33              MOVEL     DF1004        HXSND17
     c                   else
     C     IFBKey        CHAIN     FRISOK                             33
     C  N33              movel     FSSOK         HXSND17
     c                   endif
     C                   END
      *
      *  HEADFINFO BASERT PÅ FELTTESTER/SAMMENSATTE FELT..
      * Lastes opp (Dft = er alt lastet/egen terminal = 0 / Henting =3(norm hos kunde)
     C                   move      '0'           HentKode          1
      *   blank via1=Direkte - Da er det hening(normalt hos kunde).
      * MEN IKKE HVIS INNLAND FRA EGEN TERMNINAL (34 OFF)
     C                   move      '1'           *IN34
     C                   Movel     HESDF         FraPnrA           4
     C                   TESTN                   FraPnrA              33
     C     *IN33         IFEQ      '1'
     C                   Movel     FraPnrA       FraPnr            4 0
     c     fraPNR        chain     KODTRI                             34
     C                   END
     C     HESDFF        Ifeq      *blanks
     C     *in34         andeq     '1'
     C                   move      '3'           HentKode
     C                   end
      * dup - viderefrakt er alltid henting på terminal
     c     Hentkode      ifeq      '3'
     c     TRSLAG        andeq     'VF'
     C                   move      '0'           HentKode          1
     c                   end
      * Hente-terminal (kombinert med kode 3 foran..)
     C                   move      *blanks       HenTerm          20
      * Lev.terminal
     C                   move      *blanks       LevTerm          20
      * dup - forfraktk er alltid levering til terminal
     c     TRSLAG        ifeq      'FF'
     C                   eval      LevTerm  = HESDT
     c                   end
      *
      *
      *  FRITEKST-INFO - Feltet INFO = 700 langt (via Ds 10 X 70)
     C                   move      *blanks       Info
     C* Starten vil ta fra 1-3 linjer(variabelt)
     C     HEVS1a        Ifeq      'KLL '
     C     HEVS1a        Oreq      'CLL '
     C     HEVS1B        ifne      *blanks
     C                   Eval      Info='Varesl: ' + %trim(HEVS1B)+%trim(HEVS2)
     c                   end
     C                   ELSE
     C                   Eval      Info='Varesl: ' + %trim(HEVS1)+ %trim(HEVS2)
     C                   END
      *
      * Finn første siste brukte linje..
     C     1             do        10            nr                2 0
     C     INF(nr)       Ifeq      *blanks
     c                   sub       1             nr
     C                   leave
     c                   end
     C                   enddo
      * GODSMERKING
     C     HEGM1         Ifne      *blanks
     C     HEGM1         andne     'ADR'
     C     HEGM1         andne     'ADRESSE'
     C                   add       1             nr
     C                   Eval      Inf(nr)='Merket: '+%trim(HEGM1)+
     C                             ' '+%trim(HEGM2)
     C                   END
      * dato klokke i egen notislinje - ryddigere ved varsel om endring - lettere å teste
      * Oppdr.givers ref
     C                   add       1             nr
     C                   Eval      Inf(nr)='Senders ref: '+HERFA
      * ØNSKET HENTE/LEV-TID.   dersom oppdraget er UTENLANNDS-modul (fast lengde (ikke edit)
     C     HXSNDN        IFEQ      *blanks
     C                   add       1             nr
     C                   Eval      Inf(nr)= 'H/L-Tid: ' +
     C                             FDD+'.'+FMM+' '+FTI+':'+FMI+'/'+
     C                             TDD+'.'+TMM+' '+TTI+':'+TMI
     C                   END
N03   * KLOKKESLETT VISES I HEADER DERSOM UTFYLT...
N03  C                   IF        TRSDFK = *ZEROS
N03  C                   EVAL      HenKl = *blanks
N03  c                   ELSE
N03  C                   EVAL      HenKl = FTI+':'+FMI
N03  c                   ENDIF
N03  C                   IF        TRSDTK = *ZEROS
N03  C                   EVAL      LevKl = *blanks
N03  c                   ELSE
N03  C                   EVAL      LevKl = TTI+':'+TMI
N03  c                   ENDIF
      *
      * Linjer fra notisblokk merket G - permanent notis
      * (70 lang  . (lest)  på slutten droppes)
     C                   MOVE      'G'           XXSKKO
     C     FREETK        SETLL     FREETXT2
     C     FREETK        READE(N)  FREETXT2                               33
     C  N33              add       1             nr
     C  N33              Eval      Inf(nr)= '**OBS** Merknad: '
     C     *in33         doweq     *OFF
     C                   add       1             nr
     C                   Eval      Inf(nr)= FRTTXT
     C     Nr            cabge     10            utInfo
     C     FREETK        READE(N)  FREETXT2                               33
     C                   END
     C     utInfo        tag

      * Tollpass??
     C     DOKUFKEY      CHAIN     DOKUF                              33
     C     *in33         ifeq      *off
     C     DFTOLL        andne     *zeros
     c     Nr            Iflt      10
     c                   add       1             Nr
     c                   else
     c                   z-add     10            Nr
     c                   endif
     C                   Eval      Inf(nr)= 'TOLLPASS! UFORTOLLET GODS!'
     c                   endif
      * Linjer fra notisblokk merket M=melding (øyeblikks..)
     C                   MOVE      *BLANKS       SMELD           210
     C                   MOVE      'M'           XXSKKO
     C     FREETK        SETLL     FREETXT2
     C     FREETK        READE     FREETXT2                               33
     C     *in33         doweq     *OFF
     C                   eval      Smeld = %trim(smeld) +%trim(FRTTXT)
      * kode M-> N funker som status - ER sendt.
     c                   move      'N'           FRTKOD
     C                   update    FREETXTR
     C     FREETK        READE     FREETXT2                               33
     C                   END
      *
      * Varelinjer fra fraktbrevet....
     C                   MOVE      *zeros        Linje             5 0
     C                   MOVE      *BLANKS       LININFO         750
     C     DokufKey      SETLL     DOKUFV                             33
     C     DokufKey      READE     DOKUFV                                 33
     C     *IN33         DOWEQ     '0'
     C                   Z-ADD     1             FMMKNR
     C                   Z-ADD     1             FMLONR
     C                   move      *blanks       FMMRK1
     C     DOKUFMKEY     CHAIN     DOKUFM                             33
     C                   ADD       1             LINJE
     C                   if        Linje > 1
     C                   eval      LinInfo = %trim(lininfo)+'|'
     C                   endif
     C                   eval      LinInfo = %trim(lininfo)
     C                             +%trim(%editc(FVANT:'Z'))+' '
     C                             +%trim(FVPAKN)+' '+%trim(FVVT)
     C     FVVKT         ifgt      0
     C                   eval      LinInfo = %trim(lininfo)+' '
     C                             +%trim(%editc(FVVKT:'Z'))+' KG'
     C                   endif
     C     FVVOL         ifgt      0
     C                   eval      LinInfo = %trim(lininfo)+' '
     C                             +%trim(%editc(FVVOL:'4'))+' M3'
     C                   endif
     C     FVLM          ifgt      0
     C                   eval      LinInfo = %trim(lininfo)+' '
     C                             +%trim(%editc(FVLM:'4'))+' LM'
     C                   endif
     C     FMMRK1        ifne      *blanks
     C                   eval      LinInfo = %trim(lininfo)+' Mrk:'
     C                             +%trim(FMMRK1)
     C                   endif
     C     DokufKey      READE     DOKUFV                                 33
     C                   ENDDO
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USERID        CHAIN     BRIDF                              50
      * Ukjent UserID
     c     *in50         ifeq      '1'
      /free

        wrtsection('top');
        JSONstring='{'
        +'"userId" : "'+%trim(userId)+'", '
        +'"error" : "UnKnown UserID" ';
        JSONstring='}';
        updHTMLvar2('JSONstring':%addr(JSONstring):%len(JSONstring));
        wrtsection('JSONlin');

        wrtsection('*fini');
      /end-free
     C                   close     BRIDF
     C                   eval      *inlr = *on
     C                   return
     c                   endif
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      * Kontoll av UsrSpcName (= sessionId)
      * pr juli 2014 - ingen test av korrekt UsrSpcName (lage variant av CHECK-USER ??)
     C*                  CALLB     PGMXXX
     C*                  PARM                    BIBRID
     C*                  PARM                    UsrSpcName
     C*                  PARM                    UINN              1
     C*                  IF        UINN = 'N'
     C* .....
     C*                  eval      *inlr = *on
     C*                  return
     C*                  END
      *
     C                   OPEN      TURER20
     C                   OPEN      TURER14
     C                   OPEN      HEADF
     C                   OPEN      HEAD11
     C                   OPEN      FRISOK
     C                   OPEN      DOKUF
     C                   OPEN      DOKUFV
     C                   OPEN      DOKUFM
     C                   OPEN      FREETXT2
     C                   OPEN      FREETUR2
     C                   OPEN      TKSGM
     C                   OPEN      TRACKL02
     C                   OPEN      KUFAST
     C                   OPEN      KODTRI
     C                   OPEN      BRPARF
      * Key for TURER
     C     TurKey20      KLIST
     C                   KFLD                    SJN
     C                   KFLD                    xxTST4
     C                   MOVE      ' '           xxTST4            1
      * Key for Dokuf
     C     DokufKey      KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    DFFBNR
     C                   Move      'F'           DFRI
     C                   z-add     1             DFFBNR
      * key for godsmerke(nr 1) på varelinjenivå
     C     DOKUFMKEY     KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    FVAVD
     C                   KFLD                    FVOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
     C                   KFLD                    FMMKNR
     C                   KFLD                    FMLONR
     C     FS1Key        KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C     IFBKey        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    IFBkod            3
     c                   eval      IFBkod = 'IFB'
     C     FREETK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXSKKO            1
     C     FREETURK      KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    TUPRO
     C                   KFLD                    XXSKKO            1
     C     HEAKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TKSGMKey      KLIST
     C                   KFLD                    HEPRO
     C                   KFLD                    Xblank            1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TKSGMK2       KLIST
     C                   KFLD                    TUPRO
     C                   KFLD                    Xblank
     C     FINSKEY       KLIST
     C                   KFLD                    HEPRO
     C                   KFLD                    XDEL
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      'X'           XDEL              1
     C     GmTurKey      KLIST
     C                   KFLD                    TGPRO
     C                   KFLD                    TGDEL
     C                   KFLD                    TGAVD
     C                   KFLD                    TGOPD
      * alle poster som alt er sendt til PDA merkes i utg.pkt som om de skal deletes
      * ved denne oppgradering - inntil dette er "motbevist" i rutine FinsOpd
      * Om de ETTER rutine fremdeles har X finnes de ikke lenger og skal deletes
     C     FINSIKKE      KLIST
     C                   KFLD                    TUPRO
     C                   KFLD                    XDEL
      * TRACKl02 -key
     C     TrackKey      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
      *
     C     FiParKey      klist
     C                   kfld                    FIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   movel     '*KUNDFT*'    FIBRID           10
     C                   move      BIFIRM        FIBRID
      * HENTING TERMINAL skal utføres  ??? (J=*DFT ) (ellers Levering er første handling)
     C                   MOVE      'J'           TKhenTerm         1
     C                   MOVE      'TKHET'       PAID
     C     FiParKey      CHAIN     BRPARF                             33
     C     *IN33         IFEQ      *OFF
     C                   MOVEl     PAVRDA        TKhenTerm
     C     TKhenTerm     IFNE      'J'
     C     TKhenTerm     andne     'N'
     C                   MOVEl     'J'           TKhenTerm
     c                   end
     c                   end
      *
     C                   move      *zeros        Heavd
     C                   move      '_'           Strek
     C                   move      *zeros        Heopd
      * Initier CrLF
     c                   movel     Hex0D         CrLf              2
     c                   move      Hex25         CrLf
      *
     C                   move      *zeros        TRSDFD
     C                   move      *zeros        TRSDTD
      *
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVEL     ULDT          TS               14
     C                   MOVE      XULTM         TS
      * skal kolli-ID sendes - (bør hentes som firmaparameter..)
     C                   move      'N'           KolliJN           1
      * HENT NetCCSID (1208 = UTF8)
     c                   move      *blanks       NetCCSID         10
     C                   eval      NetCCSID   =getenv('CGI_ASCII_CCSID':
     C                             qusec)
      *
     C     UtInit        endsr
      *
      *=====================================================================******
      *  TEST OM OPPDRAG ALT FINNES PÅ PDA (Ved refresh)
      *=====================================================================******
     C     FinsOpd       begsr
     C                   move      *blanks       EndrFri        1000
     C                   move      'N'           FINNES            1
      * FINNES... ta bort flagg for deletes..  MEN ER ENDRET?   UPDATE
     C     FINSKEY       CHAIN     TKSGM                              33
     C     *IN33         IFEQ      *OFF
     C                   move      'J'           FINNES
     C                   move      ' '           TGDEL
      * dato/kl sist refreshet
     C                   MOVE      ULDT          TGDTR
     C                   MOVE      XULTM         TGTIR
     c                   update    TKSGMR
N04   * dirty triks for å hindre test på hentekode
N04  c                   if        HenTerm <> TGHETE
N04  c                             and StatNext<>10 and StatNext<>30
N04  c                             and StatNext<>40 and StatNext<>45
N04  c                   eval      HenTerm = TGHETE
N04  c                   endif
      * 60 ON =NOE er endret - sett indikator på det som er uendret (skal blankes)
     C     TGNT          COMP      HENT                                   61
     C     TGVKT         COMP      HEVKT                                  62
     C     TGM3          COMP      HEM3                                   63
     C     TGLM          COMP      HELM                                   64
     C     TGPLL         COMP      HXPALL                                 65
     C     TGAVNA        COMP      HENAS                                  66
     C     TGAVA1        COMP      HEADS1                                 82
     C     TGAVA2        COMP      HEADS2                                 83
     C     TGAVA3        COMP      HEADS3                                 84
     C     TGMONA        COMP      HENAK                                  67
     C     TGMOA1        COMP      HEADK1                                 85
     C     TGMOA2        COMP      HEADK2                                 86
     C     TGMOA3        COMP      HEADK3                                 87
     C     TGHEKO        COMP      Hentkode                               68
     C     TGHETE        COMP      Henterm                                69
     C     TGLETE        COMP      LevTerm                                70
     C     TGIN01        COMP      INF(01)                                71
     C     TGIN02        COMP      INF(02)                                72
     C     TGIN03        COMP      INF(03)                                73
     C     TGIN04        COMP      INF(04)                                74
     C     TGIN05        COMP      INF(05)                                75
     C     TGIN06        COMP      INF(06)                                76
     C     TGIN07        COMP      INF(07)                                77
     C     TGIN08        COMP      INF(08)                                78
     C     TGIN09        COMP      INF(09)                                79
     C     TGIN10        COMP      INF(10)                                80
     C     TGSN17        COMP      HXSND17                                81
     C     TGVLIN        COMP      LININFO                                88
      * Indikator = 0 = endret    -> *IN60 ON = noe er endret...
     c     *in61         Ifeq      '0'
     c     *in62         oreq      '0'
     c     *in63         oreq      '0'
     c     *in64         oreq      '0'
     c     *in65         oreq      '0'
     c     *in66         oreq      '0'
     c     *in67         oreq      '0'
     c     *in68         oreq      '0'
     c     *in69         oreq      '0'
     c     *in70         oreq      '0'
     c     *in71         oreq      '0'
     c     *in72         oreq      '0'
     c     *in73         oreq      '0'
     c     *in74         oreq      '0'
     c     *in75         oreq      '0'
     c     *in76         oreq      '0'
     c     *in77         oreq      '0'
     c     *in78         oreq      '0'
     c     *in79         oreq      '0'
     c     *in80         oreq      '0'
     c     *in81         oreq      '0'
     c     *in82         oreq      '0'                                          HEADS1
     c     *in83         oreq      '0'                                          HEADS2
     c     *in84         oreq      '0'                                          HEADS3
     c     *in85         oreq      '0'                                          HEADK1
     c     *in86         oreq      '0'                                          HEADK2
     c     *in87         oreq      '0'                                          HEADK3
     c     *in88         oreq      '0'                                          lininfo
     C                   move      '1'           *IN60
     C                   end
      * V/endret fritekst-lin vis i infoboks  kun de som er endret (men alle oppdateres)
      /free
        nr=1;
        dow nr <= 10;
        if TGINF(nr) <> INF(nr);
          EndrFri=%trim(EndrFri)+'Fr'+ %trim(%editc(nr:'3')) +':'
                 +%trim(TGINF(nr)) +'->'+%trim(INF(nr)) +'\n';
        endif;
        nr=nr+1;
        enddo;
      /end-free

      * Smeld=Melding til sjåfør på Sendingsnivå ??
     C                   move      'N'           KunSmeld          1
     c     *in60         Ifeq      '0'
     C     Smeld         Andne     *blanks
     C                   move      '1'           *IN60
     C                   move      'J'           KunSmeld
N02   * Refresh/Endring,men KUN "Øyebliksmelding" - sett endrings-status til 'normalstatus'
N02  c                   eval      StatNext = OriStatNext
     c
     C                   end
     C                   end
      *
     C     UtFins        endsr
      *
      *=====================================================================******
      *  Update loggfil med gamle verdier - for hva som sendes til pda
      *  ved endring av ordre - blank felt som ikke skal sendes
      *  (numeriske blankes basert på indikator direkte i SEMI)
      *=====================================================================******
     C     LogTKS        begsr
      * På poster som fines vil TGDEL være blank
     C     TKSGMKey      CHAIN     TKSGM                              35
     c                   clear                   TKSGMR
     C                   move      HEPRO         TGPRO
     C                   move      ' '           TGDEL
     C                   move      HEAVD         TGAVD
     C                   move      HEOPD         TGOPD
     C                   move      HXSND17       TGSN17
     C                   move      HENAS         TGAVNA
     C                   move      HEADS1        TGAVA1
     C                   move      HEADS2        TGAVA2
     C                   move      HEADS3        TGAVA3
     C                   move      HENAK         TGMONA
     C                   move      HEADK1        TGMOA1
     C                   move      HEADK2        TGMOA2
     C                   move      HEADK3        TGMOA3
     C                   move      HXPALL        TGPLL
     C                   move      HENT          TGNT
     C                   move      HEVKT         TGVKT
     C                   move      HELM          TGLM
     C                   move      HEM3          TGM3
     C                   move      Hentkode      TGHEKO
     C                   move      HenTerm       TGHETE
     C                   move      LevTerm       TGLETE
     C                   move      INF(01)       TGIN01
     C                   move      INF(02)       TGIN02
     C                   move      INF(03)       TGIN03
     C                   move      INF(04)       TGIN04
     C                   move      INF(05)       TGIN05
     C                   move      INF(06)       TGIN06
     C                   move      INF(07)       TGIN07
     C                   move      INF(08)       TGIN08
     C                   move      INF(09)       TGIN09
     C                   move      INF(10)       TGIN10
     C                   move      LININFO       TGVLIN
     C     Refresh       ifne      'J'
     C                   move      'O'           TGKOH                          Original godsl.
     C                   else
     C  N60              move      'A'           TGKOH                          Addet til godsl
      * ikke sett flagg om endret dersom det kun er Øyeblikksmelding som er sendt
     C     KunSmeld      ifne      'J'
     C   60              move      'U'           TGKOH                          Updated verdi
     C                   end
     C                   end
      * sist sendt (original/endret)
     C                   MOVE      ULDT          TGDTH
     C                   MOVE      XULTM         TGTIH
      * sist Refreshet
     C                   MOVE      ULDT          TGDTR
     C                   MOVE      XULTM         TGTIR
     C   35              write     TKSGMR
     C  N35              update    TKSGMR
      *  60 ON = Update av et eksisterende oppdrag - send kun endrede verdier
     C                   endsr
      **************************************************************
     C     IniTKSGM      begsr
      **************************************************************
      * ved første innhenting, delete avt gamle poster (etter Parker...)
      * ved Refresh sett Delet-merke X på alle oppdrag
      *     (....   SR FinsOpd opphever dette (setter blank) desom oppdrag ennå fins på tur
      *             til sist frese SR "DELOPD" gjennom alle som fremdeles har "X'  ...)
     C     TKSGMK2       setll     TKSGM
     C     TKSGMK2       reade     TKSGM                                  33
     C     *in33         doweq     '0'
     C     ReFresh       Ifeq      'N'
     C                   delete    TKSGMR
     C                   else
     C     TGOPD         IFNE      *ZEROS
     C                   move      'X'           TGDEL
     C                   end
     C                   Update    TKSGMR
     C                   end
     C     TKSGMK2       reade     TKSGM                                  33
     c                   end
     C                   endsr
      *
      *=====================================================================******
      *  Oppdrag som Finnes på PDA men ikke på Turen nå skal slettes
      *=====================================================================******
     C     DelOPD        begsr
      *
     C     FINSIKKE      setll     TKSGM                                        TURNR+X=Skal deletes
     C     FINSIKKE      reade     TKSGM                                  33
     C     *in33         doweq     '0'
     C                   delete    TKSGMR
     C                   move      TGAVD         Heavd
     C                   move      TGOPD         Heopd
     C     HeaKey        chain     Headf                              33
     C     *in33         ifeq      *off
     C                   eval      StatNext =  99                               = Skal slettes
     c                   move      TUPRO         HEPRO                          Benyttes i SR
     c                   exsr      RefrEnlinje
     C                   end
     C     FINSIKKE      reade     TKSGM                                  33
     c                   end
     C                   endsr
      *
