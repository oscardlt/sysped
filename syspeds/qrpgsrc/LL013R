     FKODTFM    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FLLHEAD    UF   E           K DISK
     FLLAGJL01  IF   E           K DISK
     FLLAGJD02  UF   E           K DISK
     FLLVARE01  IF   E           K DISK
     FLLSERL02  IF   E           K DISK
     FLLHETXT   IF   E           K DISK
     FLLW013    UF A E           K DISK
     FLLW013L1  IF   E           K DISK
     F                                     RENAME(LLW013R:LLW013R1)
     FLLW013L2  UF   E             DISK
     F                                     RENAME(LLW013R:LLW013R2)
     FLLW013L3  IF   E           K DISK
     F                                     RENAME(LLW013R:LLW013R3)
     FLLTELL    UF A E             DISK
     FLL013P    O    E             PRINTER OFLIND(*IN01)
     D PL              S              7  0 DIM(99)                              PLUKKLISTENR
     D NA              S             30    DIM(99)                              MOTTAKERSNAVN
     D KP              S              1    DIM(99)                              KOPIUTSKRIFT
     D                 DS
     D XTXT01          C                   CONST('*UKJENT LAGERLOKASJO-
     D                                     N')
     D                 DS
     D  KFMT1                  1     75
     D  KFMT11                 1     39
     D  KFMT12                40     46
      *
     C                   EXSR      INIT
     C     ULTYPE        IFEQ      'P'
     C                   EXSR      PLUKKL
     C                   ELSE
     C                   EXSR      SAMLEL
     C                   END
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UALNG             7
     C                   PARM                    UALCPY            7
     C                   PARM                    USORT             1
     C                   PARM                    ULTYPE            1
     C     LASER         PARM      LASER         LASER             1
     C     STRING        PARM      STRING        STRING            5
     C     COPY          PARM      COPY          COPY              2 0
      * USORT = 'L'  -->  SORTERT PÅ LAGERLOKASJONER
      * USORT = 'F'  -->  SORTERT PÅ FYSISK INNLEGGINGS REKKEFØLGER FOR PLUKK-LISTE
      *              -->  SORTERT PÅ ARTIKKELNR FOR SAMLEPLUKK-LISTE
      * USORT = 'S'  -->  SORTERT PÅ FYSISK INNLEGGINGS REKKEFØLGER FOR PLUKK-LISTE
      *                   PGA. SAMLEPLUKK-LISTE
      * ULTYPE = 'P'  -->  PLUKK-LISTE
      * ULTYPE = 'S'  -->  SAMLEPLUKK-LISTE
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     CUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    LHPNR
     C     LLAGDK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LLAGLK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VDMN
     C     LLVARE01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLANR
     C     LLSERL02K     KLIST
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XIDAG
     C                   MOVEL     WTM           XTID
     C                   MOVE      *ZEROS        PL
     C     STRING        IFNE      *BLANKS
     C     STRING        CHAIN     KODTFM                             33
     C                   END
     C     LASER         IFEQ      'A'
     C                   SETON                                        88
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     PLUKKL        BEGSR
      *////////////////////////////////////////////////////////////
     C     UALNG         IFNE      *ZEROS
     C                   MOVE      UALNG         XALNG             7 0
     C                   MOVE      '1'           *IN45
     C                   END
     C     UALCPY        IFNE      *ZEROS
     C                   MOVE      '1'           *IN46
     C                   END
      *
     C     USORT         IFEQ      'L'
     C     *LOVAL        SETLL     LLW013                                 33
     C                   END
     C     STPL          TAG
     C     USORT         IFEQ      'L'
     C                   READ      LLW013                                 33
     C                   ELSE
     C                   READ      LLW013L2                               33
     C                   END
      *
     C     *IN33         CABEQ     '1'           SLPL
     C     LHSTA         CABEQ     'P'           STPL
      *
     C     UALCPY        IFEQ      *ZEROS
      * POST NUMMER 2 INNEHOLDER SIST BRUKT ARBEIDSLISTENR.
     C     2             CHAIN     LLTELL                             33
     C                   IF        *IN33
     C     1             CHAIN(N)  LLTELL                             33
     C                   IF        *IN33
     C                   Z-ADD     1001          LLREC1
     C                   WRITE     LLTELLR
     C                   END
     C                   Z-ADD     1001          LLREC1
     C                   WRITE     LLTELLR
     C                   ELSE
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   END
     C                   Z-ADD     LLREC1        XALN
     C                   MOVE      XALN          UALNG
     C                   ELSE
     C                   MOVE      UALCPY        XALN
     C                   END
     C                   MOVE      '1'           *IN01
     C                   IF        KFMT1 <> *BLANKS
     C                   MOVE      XALN          KFMT12
     C                   END
      *
     C     *IN33         DOWEQ     '0'
     C     LHMN          IFNE      XHMN
     C     LHMN          CHAIN     LLHEAD                             34
     C                   MOVE      *BLANKS       XHBA3
     C     LHBPU         IFEQ      *BLANKS
     C                   MOVEL     LHBPN         XHBA3
     C                   ELSE
     C                   MOVEL     LHBPU         XHBA3
     C                   END
     C                   CAT       LHBA3:1       XHBA3
     C     CUND1K        CHAIN     CUNDL01                            34
     C     UALCPY        IFEQ      *ZEROS
     C                   MOVE      LHALN         LHALG
     C                   MOVE      XALN          LHALN
     C                   END
     C                   UPDATE    LLHEADR
     C                   MOVE      LHMN          XHMN              7 0
     C                   END
      *
     C                   MOVEA     '00'          *IN(41)
     C     USORT         COMP      'S'                                    43
     C     LLAGDK        SETLL     LLAGJD02
     C     LLAGDK        READE     LLAGJD02                               34
      *
     C                   MOVE      'P'           LHSTA
     C                   MOVE      LLLOK         VALLOK
     C                   MOVE      LLSER         VASENR
     C                   MOVE      *IN46         LHST1
     C     USORT         IFEQ      'L'
     C                   UPDATE    LLW013R
     C                   ELSE
     C                   UPDATE    LLW013R2
     C                   END
     C                   MOVE      'N'           XOPD              1
      *
     C     LLWNT0        IFEQ      0
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C   34              CLEAR                   LLAGJDR
     C                   WRITE     LL013P2
     C                   ELSE
     C     *IN34         DOWEQ     '0'
      *
     C     VDLDAT        IFNE      99999999
     C     UALCPY        ANDEQ     *ZEROS
     C                   GOTO      SKIPP
     C                   END
     C                   MOVE      XALN          VDTEXT
     C                   ADD       VDLANT        TLWNT0
     C                   ADD       VDSAE0        TLWNTL
     C                   ADD       VDSAE1        TLWNT1
     C                   ADD       VDSAE2        TLWNT2
     C                   ADD       VDSAE3        TLWNT3
      *
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C     LLAGLK        CHAIN     LLAGJL01                           35
     C   35              MOVE      *BLANKS       VALLOK
     C   35              MOVEL     XTXT01        VALLOK
     C   41
     CANN42LLSER         IFNE      *BLANKS
     C                   WRITE     LL013P4
     C                   MOVE      '1'           *IN42
     C                   END
     C                   WRITE     LL013P2
     C                   MOVE      '1'           *IN41
      *
     C     XOPD          IFEQ      'J'
     C                   WRITE     LLW013R
     C                   END
     C                   MOVE      'J'           XOPD
     C     SKIPP         TAG
     C                   UPDATE    LLAGJDR
      *
     C     LLAGDK        READE     LLAGJD02                               34
     C  N34              ENDDO
      * SERIENR
     C     LLVARE01K     CHAIN     LLVARE01                           34
     C  N34              IF        ((LVSN = 'U') OR (LVSN = 'I'))
     C     LLSERL02K     SETLL     LLSERL02
     C     LLSERL02K     READE     LLSERL02                               34
     C                   MOVE      *ZEROS        XANT              3 0
     C                   MOVE      *OFF          *IN44
     C                   DOW       *IN34 = *OFF
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL013P21
     C                   MOVE      *ON           *IN44
     C                   ADD       1             XANT
     C                   IF        XANT = 10
     C                   LEAVE
     C                   END
     C     LLSERL02K     READE     LLSERL02                               34
     C                   ENDDO
     C                   IF        ((XANT < 10) AND (XANT < LLWNT0))
     C                   MOVE      *BLANKS       SNSN
     C                   DOW       ((XANT < 10) AND (XANT < LLWNT0))
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL013P21
     C                   MOVE      *ON           *IN44
     C                   ADD       1             XANT
     C                   ENDDO
     C                   END
     C                   END
      *
     C                   END
      *
     C   41
     CANN42LLSER         IFNE      *BLANKS
     C                   WRITE     LL013P4
     C                   END
      *
     C     STPL2         TAG
     C     USORT         IFEQ      'L'
     C                   READ      LLW013                                 33
     C                   ELSE
     C                   READ      LLW013L2                               33
     C                   END
     C  N33LHSTA         CABEQ     'P'           STPL2
      *
     C  N33              ENDDO
      *
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL013P3
      *
     C     LHMN          SETLL     LLHETXT
     C     LHMN          READE     LLHETXT                                33
     C                   DOW       *IN33 = *OFF
     C                   IF        LTTXT <> *BLANKS
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL013P8
     C                   END
     C     LHMN          READE     LLHETXT                                33
     C                   ENDDO
      *
     C     SLPL          ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     SAMLEL        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       KFMT1
     C     USORT         IFEQ      'L'                                          1<-B
     C     *LOVAL        SETLL     LLW013L3                               33
     C                   READ      LLW013L3                               33
     C                   ELSE                                                   1
     C     *LOVAL        SETLL     LLW013L1                               33
     C                   READ      LLW013L1                               33
     C                   END                                                    1<-E
     C     *IN33         CABEQ     '1'           SLSL
     C                   MOVE      *BLANKS       XHBA3
     C     LHBPU         IFEQ      *BLANKS
     C                   MOVEL     LHBPN         XHBA3
     C                   ELSE
     C                   MOVEL     LHBPU         XHBA3
     C                   END
     C                   CAT       LHBA3:1       XHBA3
      *
     C     CUND1K        CHAIN     CUNDL01                            34
      *
     C                   MOVE      '1'           *IN47
     C                   WRITE     LL013P1
     C                   MOVE      '0'           *IN01
      *
     C     *IN33         DOWEQ     '0'                                          1<-B
      *
     C     1             DO        99            XNP               3 0           2<-B
     C     LHALN         IFEQ      PL(XNP)                                        3<-B
     C                   LEAVE
     C                   ELSE                                                     3
     C     PL(XNP)       IFEQ      0                                               4<-B
     C                   MOVE      LHALN         PL(XNP)
     C                   MOVE      LHBNA         NA(XNP)
     C                   MOVE      LHST1         KP(XNP)
     C                   LEAVE
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   ENDDO                                                   2<-E
      *
     C     LLANR         IFNE      XLANR                                         2<-B
     C     VALLOK        ORNE      XALLOK
     C     VASENR        ORNE      XASENR
     C     XLANR         IFNE      *BLANKS                                        3<-B
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL013P5
     C     XASENR        IFNE      *BLANKS                                         4<-B
     C                   WRITE     LL013P6
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   ADD       XDLANT        TLWNT0
     C                   ADD       XDSAE0        TLWNTL
     C                   ADD       XDSAE1        TLWNT1
     C                   ADD       XDSAE2        TLWNT2
     C                   ADD       XDSAE3        TLWNT3
     C                   MOVE      LLANR         XLANR
     C                   MOVE      LLVB          XLVB
     C                   MOVE      *ZEROS        XDLANT
     C                   MOVE      *ZEROS        XDSAE0
     C                   MOVE      *ZEROS        XDSAE1
     C                   MOVE      *ZEROS        XDSAE2
     C                   MOVE      *ZEROS        XDSAE3
     C                   MOVE      VALLOK        XALLOK
     C                   MOVE      VASENR        XASENR
     C                   END                                                     2<-E
      *
     C                   ADD       VDLANT        XDLANT
     C                   ADD       VDSAE0        XDSAE0
     C                   ADD       VDSAE1        XDSAE1
     C                   ADD       VDSAE2        XDSAE2
     C                   ADD       VDSAE3        XDSAE3
      *
     C     USORT         IFEQ      'L'                                           2<-B
     C                   READ      LLW013L3                               33
     C                   ELSE                                                    2
     C                   READ      LLW013L1                               33
     C                   END                                                     2<-E
     C  N33              ENDDO                                                  1<-E
      *
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL013P5
     C     XASENR        IFNE      *BLANKS                                      1<-B
     C                   WRITE     LL013P6
     C                   END                                                    1<-E
     C                   ADD       XDLANT        TLWNT0
     C                   ADD       XDSAE0        TLWNTL
     C                   ADD       XDSAE1        TLWNT1
     C                   ADD       XDSAE2        TLWNT2
     C                   ADD       XDSAE3        TLWNT3
      *
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL013P3
     C                   MOVE      '1'           *IN41
      *
     C     1             DO        99            XNP               3 0          1<-B
     C     PL(XNP)       IFEQ      0                                             2<-B
     C                   LEAVE
     C                   ELSE                                                    2
     C                   MOVE      PL(XNP)       LHALN
     C                   MOVE      NA(XNP)       LHBNA
     C     KP(XNP)       COMP      '1'                                    42
     C   01              WRITE     LL013P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL013P7
     C                   MOVE      '0'           *IN41
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C     SLSL          ENDSR
      *
