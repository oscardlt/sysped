      *********************************************************************
      * Spørring på innenlands sendinger
      * PROGRAM BEHANDLER INPUT FRA SYMN1
      *
CRTPG+*  CRTPGM SYSPED/SYOPTF MODULE(*LIBL/SYOPTF *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 også filter på kun kundenr
10XXX * 02 JOV PTF12 TERMK på vei ut / ny  kode er CSSON med prefix F
      *              versjon for begge droppet (se SYOPTD. ) nå KUN NY
********************************************************************
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D WSUSER          S             10
     D Dager           s              3
     D DagerN          s              5  0
     D MaxSN           s              5
     D MaxRC           s              5
      *
     D Idag            s              8  0
     D Dato8N          s              8  0
     D Dato8A          s              8
     D AddSub          s              3
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA1_2              1      2
     D  PAVRDA1_5              1      5
     D  PAVRDA7_46             7     46
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * Blank passord=anonym, OK, Ellers må være "Inne"
     C     BIPO          IFNE      *BLANKS
     C*                  IF        ((BIBRID = *BLANKS) OR
     C                   IF        ((*IN50 = *ON) OR
     C                              (BIINN = *BLANKS))
     C                   exsr      SetSkl01B
     C                   GOTO      SLUTT
     C                   END
     C                   END
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     c                   z-add     0             AntKod            3 0
N02  c     FeltListe     tag
     C     BrParKey      SETLL     BRPARF
     C     BrParKey      READE     BRPARF                                 40
     c     *in40         doweq     '0'
S02  C*    PAVRDA1_2     ifeq      'T_'
N02  C                   if        %subst(PAVRDA:1:1)='F'
     C                   callp     updhtmlvar('KFKOD':PAVRDA1_5)
     C                   callp     updhtmlvar('KFTXT':PAVRDA7_46)
     C                   callp     wrtsection('FKrow')
     c                   add       1             Antkod
     C                   endif
     C     BrParKey      READE     BRPARF                                 40
     C                   ENDDO
     C     Antkod        ifeq      *zeros
N02  C                   if        %subst(PABRID:1:8)='*KUNDFT*'
n02  C                   MOVE      BIBRID        PABRID
N02  c                   goto      FeltListe
N02  c                   endif
     C                   callp     updhtmlvar('KFKOD':PAVRDA1_5)
     C                   callp     updhtmlvar('KFTXT':
N02  c                                       '**Ingen F -koder finnes**')
S02  c*                                      '**Ingen T_ -koder finnes**')
     C                   callp     wrtsection('FKrow')
     C                   endif
     C                   callp     wrtsection('bot')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
xxxxxC                   eval      DAGER    = zhbgetvar('DAGER')
     C                   eval      DAGERN   = c2n2(DAGER)
     C                   eval      MaxSn   = zhbgetvar('MaxSn')
     C                   eval      MaxRc   = zhbgetvar('MaxRc')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYMNB'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     updHTMLvar('FEILTXT':'.')
     C                   callp     wrtsection('all')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     c     XINTERN       ifeq      'J'
     c                   eval      MaxSn ='99999'
     c                   eval      MaxRc ='99999'
     c                   end
     C                   callp     updHtmlVar('MaxSn':MaxSn)
     C                   callp     updHtmlVar('MaxRc':MaxRc)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      * Hent dato fra og med. (legg ut også som hidden imput)
     C                   exsr      GetDato6A
     C                   callp     updHTMLvar('Dato':Dato8A)
     C                   callp     updHTMLvar('AGENT':'')
N01  C                   callp     updHTMLvar('KUNKU':'')
     C                   callp     updHTMLvar('TUR':'')
     C                   callp     updHTMLvar('AVD':'')
     C                   callp     updHTMLvar('TRANS':'')
     C                   callp     updHTMLvar('MOTTPOST':'')
     C                   callp     updHTMLvar('FEILTEXT':Spaces)
      *
     C                   endsr
      *=====================================================================******
      * hent dato for x dager tilbake i tid
      *=====================================================================******
     C     GetDato6a     begsr
      * KUNNE fjernet kall på SYGE20(brukt TIME) men lar det ligge i fall en vil ha i går som DFT
     C     DAGERN        IFEQ      *ZEROS
     C                   Z-ADD     1             DAGERN
     C                   END
     c                   move      *zeros        Idag
     c                   move      *zeros        Dato8N
     c                   move      'SUB'         AddSub
     C                   CALL      'SYGE20'
     C                   PARM                    AddSub
     C                   PARM                    Idag
     C                   PARM                    Dato8N
     C                   PARM                    DagerN
     c                   move      idag          Dato8A
     c** ??              move      Dato8N        Dato8A
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYOPTF'
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
     C                   OPEN      Cundl01
     C                   open      BRPARF
      * Key for BRPARF - terminal/felt-koder  (FRA FIRMA nivå)
     C     BrParKey      KLIST
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
s02  C*                  MOVE      BIBRID        PABRID
N02  C                   eval      PABRID  ='*KUNDFT*'+BIFIRM
     C                   MOVE      *zeros        PAKUNR
S02  C*                  MOVEL     'TERMK'       PAID
N02  C                   MOVEL     'CSSON'       PAID
      * Key for CUNDL01
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     KunKey        CHAIN     Cundl01                            33
      * Spesialvri for å kunne deme uten å avsløre kundenavn
     c                   MOVEL     WsUSER        tstjov            4
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   CLOSE     cundl01
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
