     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/VARESL VARESLR)
     A                                      PRINT
     A                                      CF03(03 'Exit')
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CF12(12)
     A                                      CF06(06 'LAG NY')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA32A'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av lastebærere/faste e-
     A                                      nheter pr kunde'
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Kundenr Enhets ID         Type -
     A                                         Max G.W. Lastekap.    Kubikk F.b-
     A                                      er.vkt   '
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            W2KN      R        B  6  4REFFLD(VARESLR/VAKN *LIBL/VARESL)
     A                                      EDTCDE(4)
     A            W2ID      R        B  6 13REFFLD(VARESLR/VAID *LIBL/VARESL)
     A            W2TY      R        B  6 31REFFLD(VARESLR/VATY *LIBL/VARESL)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            WSNR           1A  B  7  2VALUES(' ' '2' '3' '4' '5')
     A            VAKN      R        O  7  4EDTCDE(4)
     A            VAID      R        O  7 13
     A            VATY      R        O  7 31
     A            VAVOL     R        O  7 59EDTWRD('    ,   ')
     A            VAMGW     R        O  7 38EDTCDE(4)
     A            XXNETT    R        O  7 48REFFLD(VATARA)
     A                                      EDTCDE(4)
     A            VAFBV     R        O  7 68EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF06(06 'LAG NY')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA32A'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av lastebærere/faste e-
     A                                      nheter pr kunde'
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  5  2'Op Kundenr Enhets ID         Type -
     A                                         Max G.W. Lastekap.    Kubikk F.b-
     A                                      er.vkt   '
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2
     A            W2KN      R        B  6  4REFFLD(VARESLR/VAKN *LIBL/VARESL)
     A                                      EDTCDE(4)
     A            W2ID      R        B  6 13REFFLD(VARESLR/VAID *LIBL/VARESL)
     A            W2TY      R        B  6 31REFFLD(VARESLR/VATY *LIBL/VARESL)
     A                                  4  2'2=Endre   3=Kopier   4=Slett'
     A                                      COLOR(BLU)
     A          R BOTREC
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
     A          R CHGREC
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Annuller')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      TEXT('INFO')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'Kundenr for eier (hvis ønskelig)..-
     A                                      ..:'
     A            WSKN      R        B  1 40REFFLD(VARESLR/VAKN *LIBL/VARESL)
     A                                      EDTCDE(4)
     A  51                                  DSPATR(PR)
     A  70                                  DSPATR(RI)
     A            XXNAVN        30   O  1 49
     A                                  2  2'Enhets ID:'
     A            WSID      R        B  2 13REFFLD(VARESLR/VAID *LIBL/VARESL)
     A  51                                  DSPATR(PR)
     A  71                                  DSPATR(RI)
     A                                  2 32'Enh.type.:'
     A            WSTY      R        B  2 43REFFLD(VARESLR/VATY *LIBL/VARESL)
     A  72                                  DSPATR(RI)
     A            XXVT          30   O  2 49
     A                                  3  2'LxBxH....:'
     A            WSLEN     R        B  3 13REFFLD(VARESLR/VALEN *LIBL/VARESL)
     A                                      EDTCDE(4)
     A                                  3 20'x'
     A            WSBRD     R        B  3 22REFFLD(VARESLR/VABRD *LIBL/VARESL)
     A                                      EDTCDE(4)
     A                                  3 29'x'
     A            WSHOY     R        B  3 31REFFLD(VARESLR/VAHOY *LIBL/VARESL)
     A                                      EDTCDE(4)
     A                                  3 40'Kubikk:'
     A            WSVOL     R        B  3 48REFFLD(VARESLR/VAVOL *LIBL/VARESL)
     A                                      EDTWRD('    ,   ')
     A  73                                  DSPATR(RI)
     A                                  3 59'Lastemtr:'
     A            WSLM      R        B  3 69REFFLD(VARESLR/VALM *LIBL/VARESL)
     A                                      EDTWRD('  ,  ')
     A                                  4  2'Max G.W..:'
     A            WSMGW     R        B  4 13REFFLD(VARESLR/VAMGW *LIBL/VARESL)
     A                                      EDTCDE(4)
     A  74                                  DSPATR(RI)
     A                                  4 21'Tara:'
     A            WSTARA    R        B  4 27REFFLD(VARESLR/VATARA *LIBL/VARESL)
     A                                      EDTCDE(4)
     A  75                                  DSPATR(RI)
     A                                  4 35'Lastekap.:'
     A            XXNETT    R        O  4 46REFFLD(VARESLR/VATARA *LIBL/VARESL)
     A                                      EDTCDE(4)
     A                                  5  2'F4=Søk   F12=Forrige'
     A                                      COLOR(BLU)
     A                                  5 32'Fraktber.vkt:'
     A            WSFBV     R        B  5 46REFFLD(VARESLR/VAFBV *LIBL/VARESL)
     A                                      EDTCDE(4)
     A  76                                  DSPATR(RI)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
