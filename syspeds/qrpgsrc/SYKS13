********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 RETTELSE ETTER UTVIDELSE AV OPPDRAGSNR
********************************************************************
      *****************************************************************
      * SKRIVE UT FORSLAG PÅ AVSETNINGER VED PERIODESLUTT           ***
      *****************************************************************
     FWRKAVS    UF A E           K DISK
     FWRKAVS1   IF   E           K DISK
     F                                     RENAME(WAVSR:WAVS1R)
     FKOSBU     UF   E           K DISK
     FKOSBU11   IF   E           K DISK
     F                                     RENAME(KOSBUR:KOSB11R)
     FFAK3      IF   E           K DISK
     FDISP      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FTURER     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTG2    IF   E           K DISK
     FTRAVH1    IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FTRAN      IF   E           K DISK
     FSYKS13PF  O    E             PRINTER OFLIND(*IN88)
     D                 DS
     D  FADS                   1     30
     D  KGENOT                 1     20
     D  HEDTD                 22     23  0
     D  HEDTM                 25     26  0
     D  HEDTÅ                 28     29  0
     D                 DS
     D  DSPERI                 1      6  0
     D  BUPCC                  1      2  0
     D  BUPÅR                  3      4  0
     D  BUPMN                  5      6  0
     D                 DS
     D  NYLVL                  1     20
     D  AVPERC                 1      2  0
     D  AVPERÅ                 3      4  0
     D  AVPERM                 5      6  0
     D  AVÅÅMM                 1      6  0
     D  AVKONT                 7     11  0
     D  AVKSTE                12     15  0
     D  AVKBÆR                16     19  0
     D  AVKOIN                20     20
     D                 DS
     D  GMLVL                  1     20
     D  GMPERC                 1      2  0
     D  GMPERÅ                 3      4  0
     D  GMPERM                 5      6  0
     D  GMKONT                 7     11  0
     D  GMKSTE                12     15  0
     D  GMKBÆR                16     19  0
     D  GMKOIN                20     20
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTOC                 1      2
     D  HEDTOÅ                 3      4
     D  HEDTOM                 5      6
     C*
     C                   EXSR      INIT
     C* Legg i workfile åpne FAKTURALINJER
     C                   MOVEA     '1000'        *IN(51)
     C                   MOVE      ' '           YOPSTA                         *Ordinær
     C                   EXSR      ADDFAK
     C                   MOVE      'C'           YOPSTA                         *Samlefakt.
     C                   EXSR      ADDFAK
     C* Legg i workfile FØRTE transportørutlegg som IKKE ER TATT IGJEN
     C* .. kode BUBLST blir satt til O på alle rekvisisjoner som har
     C* .. inngått i avregning (er trekt)
     C* KAN KUN  SNAKKE OM Å INNTEKSTSAVSETTE FOR
     C* "ENNÅ IKKE UTFØRT TREKK" NÅR HOVEDTURENS TRANSPORTØR AVREGNES
     C                   MOVEA     '0100'        *IN(51)
     C* .. når utførende part av rekv. er blitt godskrevet på avregning
     C                   MOVE      'A'           YBUST             1            *Godsk.annen
     C                   MOVE      'T'           YBLST             1            *Ikke tatt igjen
     C                   EXSR      ADDKOS
     C* .. når utførende part av rekv. har sendt faktura.
     C                   MOVE      'F'           YBUST             1            *Kostn.ført
     C                   MOVE      'T'           YBLST             1            *Ikke tatt igjen
     C                   EXSR      ADDKOS
     C* Legg i workfile åpne ordinære "EGENKOSTNADER"
     C                   MOVEA     '0010'        *IN(51)
     C                   MOVE      ' '           YBUST                          *Åpen
     C                   MOVE      'E'           YBLST                          *Egenkostnad
     C                   EXSR      ADDKOS
     C* Legg i workfile åpne transportørutlegg hvor en  HAR TATT IGJEN
     C* KAN KUN  SNAKKE OM Å KOSTNADSAVSETTE FOR
     C* "ÅPNE UTLEGG SOM ER TATT IGJEN (MEN ENNÅ IKKE GODSKREVET/FØRT)"
     C*  NÅR UTFØRENDE TRANSPORTØR IKKE ER "EGEN BIL"
     C                   MOVEA     '0001'        *IN(51)
     C                   MOVE      ' '           YBUST             1            *Åpen
     C                   MOVE      'O'           YBLST             1            *tatt igjen
     C                   EXSR      ADDKOS
     C*
     C* HENT INN FRA SORTERT WORKFILE - SKRIV HVERT OPPDRAG
     C*
     C   60*LOVAL        SETLL     WRKAVS
     C   61*LOVAL        SETLL     WRKAVS1
     C   60              READ      WRKAVS                                 35
     C   61              READ      WRKAVS1                                35
     C     *IN35         DOWNE     '1'
     C                   EXSR      ENPOST
     C   60              READ      WRKAVS                                 35
     C   61              READ      WRKAVS1                                35
     C  N35              END
     C*
     C* DERSOM GRATOT ULIK 0 - SKRIV SISTE NIVÅBRUDD + GRANDTOTAL
     C     GRATOT        IFNE      *ZEROS
     C     UDET          IFEQ      'J'
     C     UDET          OREQ      'Y'
     C   88              WRITE     HEAD0D
     C                   WRITE     DETT
     C                   WRITE     GRAT
     C                   ELSE
     C   88              WRITE     HEAD0T
     C*  "Åpne fakturalinjer...."
     C     GMKOIN        COMP      'I'                                    55
     C*  "Førte transportørutlegg som IKKE er tatt igjen.."
     C     GMKOIN        COMP      'J'                                    56
     C*  "Forventede kostnader med "Egen" i belastn.kode"
     C     GMKOIN        COMP      'K'                                    57
     C*  "Forventede transportørutlegg som ER tatt igjen.."
     C     GMKOIN        COMP      'L'                                    58
     C                   WRITE     SUMT
     C                   WRITE     SUMGT
     C                   END
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*
     C***********************************************
     C     ADDFAK        BEGSR
     C***********************************************
     C     FAK3K         SETLL     FAK3
     C*
     C* Les alle med riktig status
     C     FAK3K         READE     FAK3                                   32
     C* Internbelastninger skal ikke avsettes for ...
     C     *IN32         DOWEQ     '0'
     C     FASK          IFNE      'I'
     C     FASK          ANDNE     'F'
     C                   CLEAR                   WAVSR
     C                   MOVE      FAAVD         AVKSTE
     C* Utelukkes pga avdeling.....?
     C     UAVD          IFEQ      *ZEROS                                         * ALLE AVD
     C     UAVD          OREQ      AVKSTE                                         ..EL.RIKT.
     C                   MOVE      FAAVD         AVOAVD
     C                   MOVE      FAOPD         AVOPD
     C                   MOVE      *ZEROS        AVTAVD
     C                   MOVE      *ZEROS        AVTUR
     C                   MOVE      *BLANKS       AVSIGN
     C     HEADFK        CHAIN     HEADF                              33
     C* Utelukkes pga oppdrag finnes ikke/har status slettet...?
     C     *IN33         IFEQ      '0'
     C     HEST          ANDNE     'S'
     C                   MOVE      HESG          AVSIGN
     C                   MOVE      HEPRO         AVTUR
     C* Finn antatt periode via oppdrag/tur (DISP. FINNES=TRANSP.MODUL)
     C     HEADFK        CHAIN     DISP                               33
     C     *IN33         IFEQ      '0'
     C* DISP finnes=transp.modul.oppdr. finn periode primært i turen
     C     DITUR         IFNE      *ZEROS
     C     TURERK        CHAIN     TURER                              33
     C  N33              MOVE      TURACC        AVPERC
     C  N33              MOVE      TURAAR        AVPERÅ
     C  N33              MOVE      TURMND        AVPERM
     C  N33              MOVE      TUAVD         AVTAVD
     C  N33              MOVE      TUPRO         AVTUR
     C   33              MOVEL     DIDATF        AVÅÅMM
     C                   ELSE
     C* transp.modul. men ennå ikke på tur - bruk fradato i ordre
     C                   MOVEL     DIDATF        AVÅÅMM
     C                   END
     C                   ELSE
     C* DISP finnes ikke = spedi.oppdr. finn periode i oppdragsdato
     C                   MOVE      HEDTOC        AVPERC
     C                   MOVE      HEDTOÅ        AVPERÅ
     C                   MOVE      HEDTOM        AVPERM
     C                   END
     C* Utelukkes pga periode .....?
     C     AVÅÅMM        IFGE      UPERF6
     C     AVÅÅMM        ANDLE     UPERT6
     C* DENNE SKAL VÆRE MED -
     C* Hent sted bærer fra avd.register
     C     KODTAK        CHAIN     KODTA                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      KOAKON        AVKSTE
     C                   MOVE      KOABÆR        AVKBÆR
     C                   END
     C* Hent Konto m.m. fra gebyrkoderegister
     C                   MOVE      FAVK          XXVK
     C     KODGEK        CHAIN     KODTGE                             33
     C*Dersom fra transp.modul og varekode av type transportkostnad:
     C* --- bruk bilkoden for å hente konto
     C  N33KGEAVR        IFEQ      'T'
     C     TUBILK        ANDNE     '   '
     C     HEUR          ANDEQ     'H'
     C                   MOVE      TUBILK        XXVK
     C  N33KODGEK        CHAIN     KODTGE                             33
     C                   END
     C  N33KODG2K        CHAIN     KODTG2                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      KGEKON        AVKONT
     C     FAKDM         IFEQ      'N'
     C     KG2KON        ANDNE     *ZEROS
     C                   MOVE      KG2KON        AVKONT
     C                   END
     C                   END
     C* Legg ut beløp i valuta (motsatt fortegn)
     C                   MOVE      FAVAL         AVVAL
     C                   Z-SUB     FABELV        AVBELV
     C* Legg ut beløp i lokal valuta
     C                   Z-SUB     FABELN        AVBELL
     C                   MOVE      'I'           AVKOIN
     C                   MOVE      FADS          AVTEXT
     C* Skriv posten til workfile
     C     avbell        ifne      *zeros
     C                   WRITE     WAVSR
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C     FAK3K         READE     FAK3                                   32
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     ADDKOS        BEGSR
     C***********************************************
     C*
     C     KOSBUK        SETLL     KOSBU11
     C*
     C*
     C     KOSBUE        READE     KOSBU11                                32
     C  N32DSPERI        COMP      UPERT6                             32
     C     *IN32         DOWEQ     '0'
     C                   CLEAR                   WAVSR
     C                   MOVE      BUOAVD        AVKSTE
     C     AVKSTE        IFEQ      *ZEROS
     C                   MOVE      BUTAVD        AVKSTE
     C                   END
     C* SKAL TRANSPORTØRKODE PÅ EN AV SIDENE MEDFØRE UTELUKKELSE...
     C                   MOVE      'J'           MEDTAS            1
     C* KAN KUN  SNAKKE OM Å KOSTNADSAVSETTE FOR
     C* "ÅPNE UTLEGG SOM ER TATT IGJEN (MEN ENNÅ IKKE GODSKREVET/FØRT)"
     C*  NÅR UTFØRENDE TRANSPORTØR IKKE ER "EGEN BIL"
     C     *IN54         IFEQ      '1'
     C                   MOVE      BUTNR         YYTNR
     C     TRANKE        CHAIN     TRAN                               33
     C     *IN33         IFEQ      '0'
     C     VMINCR        ANDEQ     2
     C                   MOVE      'N'           MEDTAS
     C                   END
     C                   END
     C* KAN KUN  SNAKKE OM Å INNTEKSTSAVSETTE FOR
     C* "ENNÅ IKKE UTFØRT TREKK" NÅR HOVEDTURENS TRANSPORTØR AVREGNES
     C     *IN52         IFEQ      '1'
     C                   MOVE      BUTAVD        DITUAV
     C                   MOVE      BUTUNR        DITUR
     C     TURERK        CHAIN     TURER                              33
     C  N33              MOVE      TUKNT2        YYTNR
     C  N33TRANKE        CHAIN     TRAN                               33
     C     *IN33         IFEQ      '0'
     C     VMINCR        ANDNE     0
     C                   MOVE      'N'           MEDTAS
     C                   END
     C                   END
     C     MEDTAS        IFEQ      'J'                                            *EKSTERN R
     C     UAVD          IFEQ      *ZEROS                                         * ALLE AVD
     C     UAVD          OREQ      AVKSTE                                         ..EL.RIKT.
     C                   MOVE      BUOAVD        AVOAVD
     C                   MOVE      BUOPD         AVOPD
     C                   MOVE      BUSG          AVSIGN
     C                   MOVE      BUTAVD        AVTAVD
     C                   MOVE      BUTUNR        AVTUR
     C* DENNE SKAL VÆRE MED -
     C* Hent sted bærer fra avd.register
     C     KODTAK        CHAIN     KODTA                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      KOAKON        AVKSTE
     C                   MOVE      KOABÆR        AVKBÆR
     C                   END
     C* Hent Konto m.m. fra gebyrkoderegister
     C                   MOVE      BUVK          XXVK
     C     KODGEK        CHAIN     KODTGE                             33
     C*Dersom fra transp.modul og varekode av type transportkostnad:
     C* --- bruk bilkoden for å hente konto
     C  N33KGEAVR        IFEQ      'T'
     C     BUBILK        ANDNE     '   '
     C                   MOVE      BUBILK        XXVK
     C                   END
     C  N33KODG2K        CHAIN     KODTG2                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      KG2UTG        AVKONT
     C                   END
     C* Turkostnad (for en som sender regning) hverken avtalt/estimert
     C* Simuler avregning-legg inn med avt/est.kode="P"(provisj.basert)
     C     BUBL          IFEQ      *ZEROS
     C     BUFER         ANDEQ     'T'
     C     BUTAVD        ANDNE     *ZEROS
     C     BUTUNR        ANDNE     *ZEROS
     C                   MOVE      BUTAVD        XXAVD             4 0
     C                   MOVE      BUTUNR        XXTUNR            8 0
     C                   MOVE      'N'           XXVIS             1
     C                   CALL      'SYTR12A'
     C                   PARM                    XXAVD
     C                   PARM                    XXTUNR
     C                   PARM                    XXVIS
     C                   PARM                    N4                4 0
S01  C*                  PARM                    N5                5 0
N01  C                   PARM                    N7                7 0
     C                   PARM                    N8                8 0
     C* CHAIN FOR Å HENTE TALL FRA AVREGNING
     C     TRAH1K        CHAIN     TRAVH1                             33
     C     BUBNR         CHAIN     KOSBU                              33
     C  N33              MOVE      'P'           BUTYPE
     C  N33              MOVE      FICURR        BUVAL
     C  N33              Z-ADD     THTOT         BUBL
     C  N33              UPDATE    KOSBUR
     C                   END
     C     BUVAL         IFEQ      *BLANKS
     C                   MOVE      FICURR        BUVAL
     C                   END
     C* Legg ut beløp i valuta
     C                   MOVE      BUVAL         AVVAL
     C     *IN52         IFEQ      *ON
     C* (52 ON=ført men ennå ikke trekt.... inntektsavsetn.)
     C                   Z-SUB     BUBL          AVBELV
     C                   ELSE
     C                   Z-ADD     BUBL          AVBELV
     C                   END
     C* Omregn valuta
     C     BUVAL         IFNE      FICURR
     C     VALU1K        CHAIN     VALUL01                            33
     C  N33VALKU1        DIV       OMRFAK        XFAK              9 6
     C  N33              MULT(H)   XFAK          BUBL
     C                   END
     C* Legg ut beløp i lokal valuta
     C     *IN52         IFEQ      *ON
     C* (52 ON=ført men ennå ikke trekt.... inntektsavsetn.)
     C                   Z-SUB     BUBL          AVBELL
     C                   ELSE
     C                   Z-ADD     BUBL          AVBELL
     C                   END
     C* Legg ut periode og kode "avsetningstype"
     C                   MOVE      BUPCC         AVPERC
     C                   MOVE      BUPÅR         AVPERÅ
     C                   MOVE      BUPMN         AVPERM
     C   52              MOVE      'J'           AVKOIN
     C   53              MOVE      'K'           AVKOIN
     C   54              MOVE      'L'           AVKOIN
     C                   MOVEL     BUTXT         AVTEXT
     C                   MOVEL     'A/E/P:'      XAVES             8
     C                   MOVE      BUTYPE        XAVES
     C                   MOVE      XAVES         AVTEXT
     C* Skriv posten til workfile
     C     avbell        ifne      *zeros
     C                   WRITE     WAVSR
     C                   END
     C                   END
     C                   END
     C     KOSBUE        READE     KOSBU11                                32
     C  N32DSPERI        COMP      UPERT6                             32
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     ENPOST        BEGSR
     C***********************************************
     C* INITIER NIVÅBRUDD VED 1. LESTE POST.
     C     GMLVL         IFEQ      *BLANKS
     C                   MOVE      NYLVL         GMLVL
     C                   END
     C* Sidebrudd ?
     C     *IN88         IFEQ      *ON
     C     UDET          IFEQ      'J'
     C     UDET          OREQ      'Y'
     C                   WRITE     HEAD0D
     C                   MOVE      *OFF          *IN88
     C                   END
     C                   END
     C*
     C* SKRIV / NULLSTILL BRUDDSUM VED ENDRING AV PER/KTO/STE/BÆR/TYPE
     C* (både ved detaljert / summert utskrift)
     C     GMLVL         IFNE      *BLANKS
     C     GMLVL         ANDNE     NYLVL
     C* DETALJERT (bruddsum for..)
     C     UDET          IFEQ      'J'
     C     UDET          OREQ      'Y'
     C* Sidebrudd ?
     C     *IN88         IFEQ      *ON
     C                   WRITE     HEAD0D
     C                   MOVE      *OFF          *IN88
     C                   END
     C* Skriv bruddsum
     C                   WRITE     DETT
     C                   ELSE
     C* SUMMERT.....
     C* Sidebrudd ?
     C     *IN88         IFEQ      *ON
     C                   WRITE     HEAD0T
     C                   MOVE      *OFF          *IN88
     C                   END
     C* Sett indikator for kvilken type avsetning som er summert...
     C*  "Åpne fakturalinjer...."
     C     GMKOIN        COMP      'I'                                    55
     C*  "Førte transportørutlegg som IKKE er tatt igjen.."
     C     GMKOIN        COMP      'J'                                    56
     C*  "Forventede kostnader med "Egen" i belastn.kode"
     C     GMKOIN        COMP      'K'                                    57
     C*  "Forventede transportørutlegg som ER tatt igjen.."
     C     GMKOIN        COMP      'L'                                    58
     C* Skriv bruddsum
     C                   WRITE     SUMT
     C                   END
     C* Initier for neste bruddsum
     C                   MOVE      NYLVL         GMLVL
     C                   MOVE      *ZEROS        DETTOT           13 2
     C                   END
     C* SKRIV ALLE DETALJER ????
     C     UDET          IFEQ      'J'
     C     UDET          OREQ      'Y'
     C                   WRITE     DET
     C                   END
     C*
     C* AKKUMULER INN I BRUDDSUM OG GRAND TOTAL (både ved detalj/sum)
     C                   ADD       AVBELL        DETTOT
     C                   ADD       AVBELL        GRATOT           13 2
     C*
     C                   ENDSR
     C*
     C*****************************************************************
     C     INIT          BEGSR
     C*****************************************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UUAVD             4
     C                   PARM                    UUPERF            4
     C                   PARM                    UUPERT            4
     C                   PARM                    USORT             1
     C                   PARM                    UDET              1
     C* FLYTT PARAM INN I NUMERISKE FELT
     C                   MOVE      UUAVD         UAVD              4 0
     C                   MOVE      UUPERF        UPERF             4 0
     C                   MOVEL     UUPERF        UPERFÅ            2 0
     C                   MOVE      UUPERF        UPERFM            2 0
     C     UPERFÅ        IFGT      50
     C                   MOVE      '19'          UPERFC            2 0
     C                   ELSE
     C                   MOVE      '20'          UPERFC
     C                   END
     C                   Z-ADD     UPERF         UPERF6            6 0
     C                   MOVEL     UPERFC        UPERF6
     C                   MOVE      UUPERT        UPERT             4 0
     C     UPERT         IFGT      5012
     C     UPERT         ADD       190000        UPERT6            6 0
     C                   ELSE
     C     UPERT         ADD       200000        UPERT6
     C                   END
     C* SKRIV 1.PAGE VED DETALJERT (FOR Å KUNNE FORKLARE KODER)
     C     UDET          IFEQ      'J'
     C     UDET          OREQ      'Y'
     C                   WRITE     HEAD1P
     C                   END
     C* SETT INDIKATOR FOR SORTERINGSFIL
     C     USORT         COMP      '1'                                    60
     C     USORT         COMP      '2'                                    61
     C     USORT         IFLT      '1'
     C     USORT         ORGT      '2'
     C                   SETON                                            60
     C                   END
     C                   MOVE      *ZEROS        DSPERI
     C     FAK3K         KLIST
     C                   KFLD                    YOPSTA            1
     C     HEADFK        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C     TURERK        KLIST
     C                   KFLD                    DITUAV
     C                   KFLD                    DITUR
     C     TRAH1K        KLIST
     C                   KFLD                    BUTAVD
     C                   KFLD                    BUTUNR
     C     KODTAK        KLIST
     C                   KFLD                    KAUNI
     C                   KFLD                    AVKSTE
     C                   MOVE      'A'           KAUNI             1
     C     KODGEK        KLIST
     C                   KFLD                    GEUNI
     C                   KFLD                    XXVK              3
     C                   MOVE      'GE'          GEUNI             2
     C     KODG2K        KLIST
     C                   KFLD                    G2UNI
     C                   KFLD                    XXVK
     C                   MOVE      'G2'          G2UNI             2
     C     VALU1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    BUVAL
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YYTNR             8 0
     C     KOSBUK        KLIST
     C                   KFLD                    YBUST
     C                   KFLD                    YBLST
     C                   KFLD                    UPERFC
     C                   KFLD                    UPERFÅ
     C                   KFLD                    UPERFM
     C*
     C     KOSBUE        KLIST
     C                   KFLD                    YBUST
     C                   KFLD                    YBLST
     C* Det er kun kostnader med belastningskode "E" (Egen) som er
     C* aktuelle å gjøre avsetning for i regnskapet ved per.avslutning
     C* (De med kode "T" eller "K" er riktignok også forventede inng.
     C*  kostnader - men vil bli viderebelastet krone-for-krone enten
     C*  mot Transportør eller kunde, og vil IKKE påvirke resultatet.)
     C*
     C                   READ      FIRM                                   33
     C* Framtving heading på 1. side (Overflw-indikator)
     C                   MOVE      *ON           *IN88
     C*
     C                   ENDSR
