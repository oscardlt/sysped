********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 endret litt på hawbnummer
10XXX * 02 SH  PTF11 endret litt på hawbnummer
********************************************************************
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT MANIFEST                     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** INDIKATOR 27 PÅ = UTEN OPPDATERING                  ***
      *** INDIKATOR 28 PÅ = PRØVEUTSKRIFT                     ***
      ***********************************************************
     FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FarkivP    O  A E           K DISK
     FKODTLK    IF   E           K DISK
     FHEADF     UF   E           K DISK
     F*EAD11  IF  E           K        DISK
     FHEAD19    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD7R)
     FSAMLB     UF   E           K DISK
     FSUMMAN    UF A E           K DISK
     FFAIN      O  A E           K DISK
     FFAKT      UF   E           K DISK
     FFAK4      IF   E           K DISK
     F                                     RENAME(FAKTR:FAKTR2)
     FfIRM      iF   E           K DISK
     Ftellfa    uF   E           K DISK
     FDOKEF     IF   E           K DISK
     FHODE      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTH     IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FSYEF30P   O    E             PRINTER OFLIND(*IN01)
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D                 DS
     D  IDAG                   1      8  0
     D  IDAGÅ                  1      4  0
     D  IDAGM                  5      6  0
     D  IDAGD                  7      8  0
     D                 DS
     D  XFFDTD                 1      2  0
     D  XFFDTM                 3      4  0
     D  XFFDTA                 5      6  0
     D  XFFDT                  1      6  0
     D                 DS
     D  XFFD2A                 1      4  0
     D  XFFD2M                 5      6  0
     D  XFFD2D                 7      8  0
     D  XFFDT2                 1      8  0
     D                 DS
     D  ADR3                   1     24
     D  ADR3A                  1     24
     D                 DS
     D  SMSD                   1      3
     D  SMSDA                  1      3
     D                 DS
     D  HENT                   1      7  0
     D  XNT                    2      7  0
     D                 DS
     D  HEVKT                  1      9  0
     D  XVKT                   2      9  0
     D                 DS
     D  XSD                    1      3
     D  XLNR                   4      6  0
     D  XBLANK                 7     15
     D  YRFA                   1     15
s02  D*                DS
s02  D* HEHAWB                 1      9
s02  D* XHAWB                  4      9
     D                 DS
     D  HEGN                   1     15
     D  XFSN                   5      7
     D  XGN8                   8     15
     D                 DS
     D  WKEY19                 1      6
     D  WUSD                   1      3
     D  WULNR                  4      6  0
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UGN                   36     46
     D  USD                   36     38
     D  ULNR                  39     41  0
     D  UFAK                  42     42
     D  UHOD                  47     47
     D  UBBS                  48     48
     D  UPRT4                681    690
     D  UPT4                 691    700
     D  UPRT73               961    970
     D  UPT73                971    980
      *
     C                   MOVE      UFAK          XFAK              1
     C                   EXSR      INIT
      *
     C  N27
     CANN28              DO
     C                   EXSR      BETSR
     C                   END
      *
     C                   MOVE      'J'           XOPPD             1
     C     KOPFM         SETLL     KODTFM
     C     START0        TAG
     C     KOPFM         IFNE      *BLANKS                                      1<-B
     C     KOPFM         READE     KODTFM                                 30
     C     *IN30         CABEQ     '1'           SLUTT
     C                   END                                                    1<-E
     C     WKEY19        SETLL     HEAD19
     C     STARTE        TAG
     C     WKEY19        READE     HEAD19                                 30
     C  N30HEST          IFEQ      'S'
     C                   GOTO      STARTE
     C                   END
     C                   EXSR      KDTFMT
     C  N27              EXSR      TSTFA
     C     KOPFM         IFNE      *BLANKS                                      1<-B
     C                   WRITE     LHEADH1
     C     KFMT2         IFNE      *BLANKS                                        3<-B
     C                   WRITE     LHEADH2
     C                   END                                                      3<-E
     C                   END                                                    1<-E
     C                   WRITE     LHEAD
     C     WKEY19        SETLL     HEAD19                             30
     C     START         TAG
     C     WKEY19        READE     HEAD19                                 30
     C  N30HEST          IFEQ      'S'
     C                   GOTO      START
     C                   END
      *
     C  N27              EXSR      TSTFA
     C  N30              EXSR      DETA
     C  N30              GOTO      START
     C     '   '         SETLL     SUMMAN
     C                   SETOFF                                       30
     C                   WRITE     LTOT0
     C                   MOVE      *ZEROS        XTANT
     C                   MOVE      *ZEROS        XTVKT
     C     STSUMT        TAG
     C                   READ      SUMMAN                                 30
     C  N30              DO
     C   01              DO                                                     1<-B
     C                   EXSR      KDTFMT
     C                   SETOFF                                       01
     C     KOPFM         IFNE      *BLANKS                                       2<-B
     C                   WRITE     LHEADH1
     C     KFMT2         IFNE      *BLANKS                                        3<-B
     C                   WRITE     LHEADH2
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   WRITE     LHEAD
     C                   END                                                    1<-E
     C                   WRITE     LTOT
     C                   MOVE      *ZEROS        SUVALB
     C                   GOTO      STSUMT
     C                   END
     C                   WRITE     LTOT9
      *
     C                   MOVE      'N'           XOPPD
     C     KOPFM         CABNE     *BLANKS       START0
     C     SLUTT         TAG
      * Arkiv ikke ved prøveutskrift
     C  N28              EXSR      ARKSR
      *
      *** HENTE PRINTERNR OG PAPIRTYPE FOR FAKTURA
     C     XFAK          IFEQ      'J'
     C                   Z-ADD     73            YPLNR             3 0
     C     KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT73
     C  N99              MOVE      KOPTY         UPT73
     C                   Z-ADD     4             YPLNR             3 0
     C     KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT4
     C  N99              MOVE      KOPTY         UPT4
     C                   END
     C                   MOVE      XGN           UGN
     C                   MOVE      XPRANT        UPRANT
     C                   SETON                                        LR
     C                   RETURN
      ***********************************************
     C     FASR          BEGSR
      ***********************************************
      *
      *
     C     UFIRM         CHAIN     tellfa                             33
     C   33              GOTO      UTSRF
     C                   ADD       1             FIFNR
     C                   UPDATE    tellfar
     C                   SUB       1             FIFNR
     C     UTSRF         ENDSR
      ***********************************************
     C     BETSR         BEGSR
      ***********************************************
      *
      *** FINNER BETBET OG FORFALLSDATO
     C                   MOVE      *ZEROS        XFFDT
     C                   MOVE      *BLANKS       BETTXT           30
     C                   CALL      'SYFA20A'
     C     XFFDT         PARM      XFFDT         UFFDT             6 0
     C                   PARM                    BETTXT
     C                   PARM                    UFIRM
     C                   PARM                    BETBET
     C                   MOVE      XFFDTD        XFFD2D
     C                   MOVE      XFFDTM        XFFD2M
     C                   MOVE      XFFDTA        XFFD2A
     C     XFFDTA        IFGT      50
     C                   MOVEL     '19'          XFFD2A
     C                   ELSE
     C                   MOVEL     '20'          XFFD2A
     C                   END
      *
     C                   ENDSR
      *
      ******************************************
     C     DETA          BEGSR
      ******************************************
     C     DOKEFK        CHAIN     DOKEF                              31
     C     DFROA3        IFNE      *BLANKS
     C                   MOVE      DFROA3        XTIL2             3
     C                   ELSE
     C     DFROA2        IFNE      *BLANKS
     C                   MOVE      DFROA2        XTIL2
     C                   ELSE
     C                   MOVE      DFROA1        XTIL2
     C                   END
     C                   END
      *
     C                   ADD       HENT          XTANT             7 0
     C                   ADD       HEVKT         XTVKT
     C                   EXSR      TDUASR
     C                   WRITE     LDET
     C  N27              DO
     C     HEVALK        CHAIN     SUMMAN                             89
     C   89              DO
     C                   MOVE      HEVALK        SUVAL
     C                   Z-ADD     XKDUA         SUVALB
     C                   WRITE     SUMMAR
     C                   END
     C  N89              DO
     C                   ADD       XKDUA         SUVALB
     C                   UPDATE    SUMMAR
     C                   END
     C                   END
     C   01              DO                                                     1<-B
     C                   EXSR      KDTFMT
     C                   SETOFF                                       01
     C     KOPFM         IFNE      *BLANKS                                       2<-B
     C                   WRITE     LHEADH1
     C     KFMT2         IFNE      *BLANKS                                        3<-B
     C                   WRITE     LHEADH2
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   WRITE     LHEAD
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
      ******************************************
     C     TSTFA         BEGSR
      ******************************************
     C     HEVALK        CHAIN     SUMMAN                             89
     C  N89              UPDATE    SUMMAR
     C   89              DO
     C  N28              EXSR      FASR
     C                   MOVE      HEVALK        SUVAL
     C                   Z-ADD     FIFNR         SUFANR
     C                   Z-ADD     0             SUVALB
     C                   WRITE     SUMMAR
     C                   END
     C                   ENDSR
      ******************************************
     C     TDUASR        BEGSR
      ******************************************
     C                   MOVE      *ZEROS        XKDUA            13 2
     C                   MOVE      *ZEROS        MVAGRL           13 2
     C                   MOVE      ' '           AGLINJ            1
     C     FAK2K         SETLL     FAK4
     C                   SETOFF                                       31
     C     ST2           TAG
     C     FAK2K         READE     FAK4                                   31
     C  N31FAKDA         IFEQ      'D'
     C                   GOTO      ST2
     C                   END
      *
      * DOES THE ADDITION AND UPDATES INVOICELINES
      *
     C  N31              DO
     C                   MOVE      'J'           AGLINJ
     C     HEVALK        IFEQ      FICURR
     C                   ADD       FABELN        XKDUA
     C     FAKDM         IFEQ      'J'
     C                   ADD       FABELN        MVAGRL
     C                   END
     C                   ELSE
     C                   ADD       FABELV        XKDUA
     C     FAKDM         IFEQ      'J'
     C                   ADD       FABELV        MVAGRL
     C                   END
     C                   END
     C  N28
     CANN27              DO
     C     FAKTK         CHAIN     FAKT                               31
     C     XOPPD         IFEQ      'J'
     C                   MOVE      'F'           FAOPKO
     C                   MOVE      'S'           FAKDA
     C                   MOVE      HEVALK        FACURI
     C                   MOVE      KUNDNR        FAKUNR
     C                   MOVE      FIFNR         FAFAKT
     C                   MOVE      IDAG          FADATO
     C                   MOVE      XFFDT2        FADAFF
     C                   MOVE      HEDTM         FAMND
     C                   MOVE      HEDTÅ         FAÅR
     C                   MOVEL     HEDTÅ         FACC
     C                   END
     C                   EXCEPT    OPDFAK
     C                   END
     C                   GOTO      ST2
     C                   END
      *
     C                   ADD       XKDUA         XTDUA            13 2
      *
     C  N27
     CANN28              DO
     C     HEADFK        CHAIN     HEADF                              31
     C     AGLINJ        IFEQ      'J'
     C                   MOVE      'F'           HEST
     C                   END
     C  N31              DO
     C                   MOVE      HEGN          XGN              11
     C                   UPDATE    HEADFR
     C     XOPPD         IFEQ      'J'
     C                   MOVE      FIFNR         FIFN
     C                   MOVE      HEAVD         FIAVD
     C                   MOVE      HEOPD         FIOPD
     C                   WRITE     FAINR
     C                   END
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C                   MOVE      'P'           YPUNI             1
     C                   Z-ADD     18            YPLNR             3 0
     C     KODTPK        CHAIN     KODTP                              99
      *
     C     KODLKK        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    SYLAND            2
     C                   MOVE      'LK '         YLKUNI            3
      *
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     SAMLBK        KLIST
     C                   KFLD                    USD
     C                   KFLD                    ULNR
     C     FAK2K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YSK
     C     FAKTK         KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C                   KFLD                    FALI
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    UAVD
     C     DOKEFK        KLIST
     C                   KFLD                    YRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    SMKNS
     C     KODTHK        KLIST
     C                   KFLD                    YHUNI
     C                   KFLD                    UAVD
     C     KODTK         KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    BETBET
     C     KODTPK        KLIST
     C                   KFLD                    YPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    YPLNR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UIN07             1
     C                   PARM                    UIN08             1
     C                   PARM                    UPRANT            2
     C     UIN07         COMP      '1'                                    27
     C     UIN08         COMP      '1'                                    28
      *
     C                   MOVE      'K'           YSK               1
     C                   MOVE      'H'           YHUNI             1
     C                   MOVE      'P'           YPUNI             1
     C                   MOVE      'F'           YRI               1
     C                   MOVE      USD           XSD
     C                   MOVE      ULNR          XLNR
     C                   MOVE      *BLANKS       XBLANK
     C                   MOVE      *ZEROS        XPRANT            2 0
     C                   MOVE      *ZEROS        IDAG
     C                   MOVE      UDAY          IDAGD
     C                   MOVE      UMONTH        IDAGM
     C                   MOVE      UYEAR         IDAGÅ
     C     UYEAR         IFGT      50
     C                   MOVEL     '19'          IDAGÅ
     C                   ELSE
     C                   MOVEL     '20'          IDAGÅ
     C                   END
     C                   MOVE      *ZEROS        XFFDT
     C                   MOVE      *ZEROS        XFFDT2
     C                   MOVE      *ZEROS        XVKT
     C                   MOVE      *ZEROS        XNT
     C                   MOVE      *BLANKS       ADR3
     C                   MOVE      *BLANKS       SMSD
      *
     C     KODTHK        CHAIN     KODTH                              33
     C     KOHMAN        COMP      'J'                                    29
      *
     C     SAMLBK        CHAIN     SAMLB                              33
     C  N28
     CANN33              MOVE      'U'           SMST
     C  N28
     CANN33              UPDATE    SAMLAR
     C     SMROB2        COMP      *BLANKS                            7171
     C     SMROB3        COMP      *BLANKS                            7272
     C     KUND1K        CHAIN     CUNDL01                            33
     C                   MOVE      *BLANKS       KJLAND
     C     KODLKK        CHAIN     KODTLK                             34
     C  N34              MOVE      KLKNVN        KJLAND           30
      *
     C   29
     CANN33SPRAAK        IFEQ      'E'
     C                   MOVE      'E'           YNET              1
     C                   ELSE
     C     SPRAAK        IFEQ      'T'
     C                   MOVEL     'T'           YNET
     C                   ELSE
     C                   MOVEL     ' '           YNET
     C                   END
     C                   END
     C   29
     CANN33HODEK         CHAIN     HODE                               33
      *
     C     POSTNR        COMP      *ZEROS                                 88
      *
     C                   MOVEL     USD           WUSD
     C                   Z-ADD     ULNR          WULNR
      *
     C     UFIRM         CHAIN     FIRM                               33
     C                   ENDSR
      *
      ***********************************************
     C     KDTFMT        BEGSR
      ***********************************************
     C                   MOVE      UDAY          UDT1              2 0
     C                   MOVE      UMONTH        UDT2              2 0
     C                   MOVE      UYEAR         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
      *
     C                   ENDSR
      *
      ***********************************************
     C     ARKSR         BEGSR
      ***********************************************
      *
      * SKRIV TIL ARKIV
      *
      * SKAL DOKUMENT ARKIVERES
      *
     C                   MOVE      'mf'          ARTYPE
     C     FIFIRM        CHAIN     FIRMARC                            34
     C  N34ARTYPE        CHAIN     ARKTXT                             34
     C     *IN(34)       IFEQ      '0'
     C     arkjn         andeq     'J'
      *
     C                   z-add     kundnr        arkund
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      FIFIRM        ARFIRM
     C                   MOVE      *zeros        ARAVD
     C                   MOVE      *zero         AROPD
     C                   MOVE      ULDT          ARDATE
     C                   MOVE      XULTM         ARTIME
     C                   MOVE      ZUSER         ARUSER
     C                   MOVEL     WKEY19        ARUNDE
     C                   WRITE     ARKIVR
     C                   END
     C                   ENDSR
      *
      *
     OFAKTR     E            OPDFAK
     O                       FAKUNR
     O                       FAFAKT
     O                       FAOPKO
     O                       FAKDA
     O                       FACC
     O                       FAÅR
     O                       FAMND
     O                       FADATO
     O                       FADAFF
