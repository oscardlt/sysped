     A*****************************************************************
     A* UTSKRIFT AV GODSJOURNAL                                       *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=80, OVERFLOW=70       *
     A* 01 PTF8 RETTELSE LAYOUT
     A*****************************************************************
     A          R HEAD1                     SKIPB(2)
     A*                                    1'¤(s17H'
     A* HEADING
     A                                     2MSGCON(15 TRO0434 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                    18MSGCON(14 TRO0435 SYMSGF)
     A            GFLBS1        30A       33
     A                                      HIGHLIGHT
     A                                    64MSGCON(9 SHU0999 SYMSGF)
     A            GFLBKO         5A       73
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
     A            GFLBS2        30A       33
     A                                      HIGHLIGHT
     A                                    64MSGCON(05 TRO0449 SYMSGF)
     A            UDAY           2S 0     70
     A                                    72'.'
     A            UMONTH         2S 0     73
     A                                    75'.'
     A            UYEAR          2S 0     77
     A            GFLBS3        30A       33
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                    64MSGCON(03 TRO0450 SYMSGF)
     A            TID            6S 0     70
     A                                       EDTCDE(Y)
     A            GFLBS4        30A       33
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                     2'Til fax-nummer:'
     A                                      SPACEB(1)
     A            GFFAX         15A       20
     A                                      HIGHLIGHT
     A*DETALJ HEADINGER
     A          R HEAD2                     SPACEB(2)
     A N50                                 1'GN:'
     A N50        WSGNF1         9A        4
     A N50                                  HIGHLIGHT
     A                                    15MSGCON(20 TRO0442 SYMSGF)
     A                                    29MSGCON(15 TRO0443 SYMSGF)
     A                                    46MSGCON(19 TRO0444 SYMSGF)
     A                                    67MSGCON(10 TRO0445 SYMSGF)
     A                                      SPACEA(1)
     A*DETALJER
     A          R DETALJ                    SPACEB(1)
     A  50        GOGNGM        11A        1
     A N50        GOGNDG         3A        4
     A N50                                 7 '.'
     A N50        GOGNLØ         3A        8
     A            GOBILN        13A       15
     A            GOAVG         15A       29
     A            GOTRNR        20A       46
     A            XOTRDT         6  0     67
     A            XXMKOD         5        75
