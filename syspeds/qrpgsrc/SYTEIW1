      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYTEIW1 MODULE(*LIBL/SYTEIW1
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FHENOP02   IF   E           K DISK    USROPN
     FHENOS     IF   E           K DISK    USROPN
     FHENOK     IF   E           K DISK    USROPN
     FDISPL19   IF   E           K DISK    USROPN
     F                                     RENAME(DISPR:DISPR19)
     FFRISOK    IF   E           K DISK    USROPN
     FFRISOL01  IF   E           K DISK    USROPN
     F                                     RENAME(FRISOKR:FRISR01)
     FHEADF     IF   E           K DISK    USROPN
     FDISP      IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FDOKUFM    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D Term            s              4
     D VisLChecked     s             10
     D VisLager        s              1
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D TITLE           C                   CONST('Terminal INN')
      *ARRAY FOR MELLOMLAGRING AV FRIE SØKEVEIER
     D SOK             S             38    DIM(500)
      *
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
     D                 DS
     D  HOSNDN                 1     20
     D  HOSNDN3                1      3
     D                 DS
     D  FSSOK                  1     35
     D  FsDsKNS                1      8
     D  FsDsDT                10     17
     D  FsDsBil               19     28
     D                 DS
     D  FMMRK1                 1     35
     D  FMSTINN               33     33
     D                 DS
     D  WdataDs                1    220
     D  DSLNR                  1      7  0
     D  DSFSKOD                8     10
     D  DSFSSOK               11     45
     D  DSNAVNS               46     75
     D  DSBILNR               76     85
     D  DSSKll                86     90  0
     D  DSSVkt                91     99  0
     D  DSSM3                100    108  3
     D  DSAntStot            111    115  0
     D  DSAntSOpn            116    120  0
     D  DSAntKtot            121    125  0
     D  DSAntKOpn            126    130  0
     D  DSKNS                131    138  0
     D  DSDT                 139    146  0
     D  DSTYPE               147    156
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
     C                   MOVE      BIBESK        WINT              3
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      * 4-Sections /$tablerow
      *
     C                   EXSR      HENTOPD
      *
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
      *
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   eval      term     = zhbgetvar('term')
      * Checkboks for Vis lager
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellers kommer verdi fra siste valg via "KunPaBil" som checked / blank(N)
     C     VisLchecked   Ifeq      'checked'
     C                   eval      VisLager = 'Y'
     C                   else
     C                   eval      VisLager = zhbgetvar('VisLager')
     C                   end
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('TITLE':TITLE)
      * KUNDE
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('WSNA':KNAVN)
     C                   callp     updhtmlvar('USER':WSUSER)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHTMLvar('VISLager':VISLager)
     C                   callp     updHTMLvar('TERM':TERM)
     C     VisLager      Ifeq      'Y'
     C                   eval      VisLChecked = 'checked'
     C                   end
     C                   callp     updHTMLvar('VisLChecked':VisLChecked)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updHTMLvar('HOLNR':
     C                             %trim(%editc(DSLNR:'Z')))
     C                   callp     updHTMLvar('EKOD':DSFSKOD)
     C                   callp     updHTMLvar('EKEY':DSFSSOK)
     C                   callp     updHTMLvar('HOKNS':
     C                             %trim(%editc(DSKNS:'Z')))
     C                   callp     updHTMLvar('HONAS':DSNAVNS)
     C                   callp     updHTMLvar('HODT':
     C                             %trim(%editW(DSDT:'    .  .  ')))
     C                   callp     updHTMLvar('TYPE':DSTYPE)
     C                   callp     updHTMLvar('HOBILN':DSBILNR)
     C                   callp     updHTMLvar('Skll      ':
     C                             %trim(%editc(DSSkll       :'Z')))
     C                   callp     updHTMLvar('Svkt      ':
     C                             %trim(%editc(DSSvkt       :'Z')))
     C                   callp     updHTMLvar('SM3       ':
     C                             %trim(%editc(DSSM3        :'3')))
     C                   callp     updHTMLvar('AntsTot   ':
     C                             %trim(%editc(DSAntsTot   :'Z')))
     C                   callp     updHTMLvar('AntsOpn   ':
     C                             %trim(%editc(DSAntsOpn    :'Z')))
     C                   callp     updHTMLvar('AntKTot   ':
     C                             %trim(%editc(DSAntKTot   :'Z')))
     C                   callp     updHTMLvar('AntKOpn   ':
     C                             %trim(%editc(DSAntkOpn    :'Z')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
     C*  Select a Recd
      *=====================================================================******
     C     RecordSlt     begsr
     C                   move      'N'           selected          1
     C                   move      'Y'           selected
      *
     C     RecordSltX    tag
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/SYTEIW1.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================******
      *  FYLL FORKFILE
      *=====================================================================******
     C     HENTOPD       begsr
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     c     ' '           setLL     HENOP02
     c     ' '           ReadE     HENOP02                                35
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTOPD
      *
     c                   move      HOPNRT        TSTTerm           4
     c     TSTTerm       cabne     Term          NextRec
     c     Vislager      ifne      'Y'
     c     HOSNDN3       cabeq     '999'         NextRec
     c                   end
     C                   clear                   WdataDS
     C                   MOVE      *ZEROS        DSSKll
     C                   MOVE      *ZEROS        DSSVkt
     C                   MOVE      *ZEROS        DSSM3
     C                   MOVE      *ZEROS        DSAntStot
     C                   MOVE      *ZEROS        DSAntSOpn
     C                   MOVE      *ZEROS        DSAntKtot
     C                   MOVE      *ZEROS        DSAntKOpn
     C                   MOVE      *ZEROS        DSKNS
     C                   MOVE      *ZEROS        DSDT
     c                   Move      HOLNR         DSLNR
     c                   Move      *BLANKS       DSFSKOD
     c                   Move      *BLANKS       DSFSSOK
     c                   Move      HONAS         DSNAVNS
     c                   Move      HOBILN        DSBILNR
     c                   Move      HOKNS         DSKNS
     c                   Move      HODT          DSDT
     c                   Movel     'HenteOppd '  DSTYPE
     C                   exsr      AntHsend
      *
     C                   exsr      RecordSlt
     C     selected      ifeq      'Y'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTOPD
     C                   endif
      *
     c     NextRec       Tag
     c     ' '           ReadE     HENOP02                                35
     c                   end
      *
      * Hent så alle åpne Esendinger..
     C                   exsr      HENTOPD2
      *
     C     SLHENTOPD     TAG
      *
     C                   endsr
      *=====================================================================******
      *  Antall Hsendinger Totalt / Åpne
      *=====================================================================******
     C     AntHsend      begsr
      *
     c     HOLNR         setll     HENOS
     c     HOLNR         reade     HENOS                                  35
     c     *in35         doweq     '0'
     c                   add       HSANT         DSSkll
     c                   add       HSVKT         DSSVKT
     c                   add       HSM3          DSSM3
     c                   add       1             DSAntSTot
     c     HSST          ifeq      ' '
     c                   add       1             DSAntSOpn
     c                   end
     C                   exsr      AntHkll
     c     HOLNR         reade     HENOS                                  35
     C                   end
      * KOLLI - Alle/Åpne akkumulere EN gang på tvers av sendingene innen henteoppdr.
      *
     C                   endsr
      *=====================================================================******
      *  Antall Hsendings-kolli  Totalt / Åpne
      *=====================================================================******
     C     AntHkll       begsr
      *
     C     HENOKK        setll     HENOK
     C     HENOKK        reade     HENOK                                  35
     c     *in35         doweq     '0'
     c                   add       1             DSAntKTot
     c     HKSTIN        ifeq      ' '
     C     HSST          ANDEQ     ' '
     c                   add       1             DSAntKOpn
     c                   end
     C     HENOKK        reade     HENOK                                  35
     C                   end
     C                   endsr
      *=====================================================================******
      *  Tell opp E-sendinger, men kun de ÅPNE med
      *=====================================================================******
     C     HENTOPD2      begsr
     c                   MOVE      *ZEROS        Snr               5 0
     c                   Movel     'zzzzzzzzz'   GMSOK
      * 1. Header (fri søkevei) for alle åpne E-sendinger ut i array
     C     'E'           SETLL     Displ19
     C     'E'           READE     Displ19                                94
     C     *IN94         DOWEQ     '0'
     c     Frikey1       Chain     Frisok                             33
     c   33Frikey2       Chain     Frisok                             33
     c     *in33         cabeq     '1'           NextE
     C     KEYHEA        CHAIN     HEADF                              33
     c     *in33         cabeq     '1'           NextE
     c     HEPRO         cabne     0             NextE
     c     FSSOK         IFNE      GMSOK
     c                   movel     FSKODE        KODSOK           38
     c                   move      FSSOK         KODSOK
     c                   move      FSSOK         GMSOK            35
     c     KODSOK        Lookup    SOK                                    33
     c     *in33         cabeq     '1'           NextE
     c                   ADD       1             Snr
     c                   MOVE      KODSOK        SOK(Snr)
     c     Snr           cabeq     500           Maxpost
     c                   end
     c     NextE         tag
     C     'E'           READE     Displ19                                94
     C                   END
      *
     C     Maxpost       tag
     C     Snr           ifgt      0
     C     1             do        snr           snr2              5 0
     c                   movel     SOK(Snr2)     FSKODE
     c                   move      SOK(Snr2)     FSSOK
     C                   eval      KUNDNR  = c2n2(FsDsKNS)
     C     KUNKEY        CHAIN     CUNDL01                            33
     C                   clear                   WdataDS
     C                   MOVE      *ZEROS        DSLNR
     C                   MOVE      *ZEROS        DSSKll
     C                   MOVE      *ZEROS        DSSVkt
     C                   MOVE      *ZEROS        DSSM3
     C                   MOVE      *ZEROS        DSAntStot
     C                   MOVE      *ZEROS        DSAntSOpn
     C                   MOVE      *ZEROS        DSAntKtot
     C                   MOVE      *ZEROS        DSAntKOpn
     C                   MOVE      *ZEROS        DSKNS
     C                   MOVE      *ZEROS        DSDT
     c                   Move      *ZEROS        DSLNR
     c                   Move      FSKODE        DSFSKOD
     c                   Move      FSSOK         DSFSSOK
     c                   Move      KNAVN         DSNAVNS
     c                   Move      FsDsBil       DSBILNR
     c                   Move      FsDsKNS       DSKNS
     c                   Move      FsDsDT        DSDT
     c                   Movel     'E-sending '  DSTYPE
     C                   exsr      AntEsend
      *
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTOPD2
     C                   exsr      RecordSlt
     C     selected      ifeq      'Y'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTOPD2
     C                   endif
      *
     c                   enddo
     c                   end
      *
     C     SLHENTOPD2    TAG
      *
     C                   endsr
      *
      ******************************
     C     AntEsend      Begsr
      ******************************
     C     FriL01Key     SETLL     Frisol01
     C     FriL01Key     READE     Frisol01                               33
     C     *IN33         DOWEQ     '0'
     c                   add       HENT          DSSkll
     c                   add       HEVKT         DSSVKT
     c                   add       HEM3          DSSM3
     c                   add       1             DSAntSTot
     C     KEYHEA        CHAIN     DISP                               33
     c     *IN33         ifeq      '0'
     c     DISTA1        andeq     'E'
     c                   add       1             DSAntSOpn
     c                   end
     c                   exsr      AntEkll
     C     FriL01Key     READE     Frisol01                               33
     C                   END
      *
     c                   endsr
      *
      ******************************
     C     AntEKll       Begsr
      ******************************
      *  Antall Esendings-kolli  Totalt / Åpne
     c     DokufmKey     setll     Dokufm
     c     DokufmKey     reade     Dokufm                                 35
     c     *in35         doweq     '0'
     c                   add       1             DSAntKTot
     c     FMSTINN       ifeq      ' '
     C     *IN33         ANDEQ     '0'
     C     DISTA1        andeq     'E'
     c                   add       1             DSAntKOpn
     c                   end
     c     DokufmKey     reade     Dokufm                                 35
     C                   end
      *
     c                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
OddEv *
     C                   OPEN      HENOP02
     C                   OPEN      HENOS
     C                   OPEN      HENOK
     C                   OPEN      DISPL19
     C                   OPEN      FRISOK
     C                   OPEN      FRISOL01
     C                   OPEN      HEADF
     C                   OPEN      DISP
     C                   OPEN      CUNDL01
     C                   OPEN      DOKUFM
      *
     C     Frikey1       KLIST
     C                   KFLD                    DIAVD
     C                   KFLD                    DIOPD
     C                   KFLD                    XXEDS
     C                   MOVE      'EDS'         XXEDS             3
     C     Frikey2       KLIST
     C                   KFLD                    DIAVD
     C                   KFLD                    DIOPD
     C                   KFLD                    XXE_S
     C                   MOVE      'E_S'         XXE_S             3
     C     FriL01Key     KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C     KEYHEA        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     KUNKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    KUNDNR
     C     HENOKK        KLIST
     C                   KFLD                    HSLNR
     C                   KFLD                    HSLNR2
      *
     C     DokufmKey     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   move      'F'           FMRI
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   CLOSE     BRIDF
     C                   CLOSE     HENOP02
     C                   CLOSE     HENOS
     C                   CLOSE     HENOK
     C                   CLOSE     DISPL19
     C                   CLOSE     FRISOK
     C                   CLOSE     FRISOL01
     C                   CLOSE     HEADF
     C                   CLOSE     DISP
     C                   CLOSE     CUNDL01
     C                   CLOSE     DOKUFM
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
