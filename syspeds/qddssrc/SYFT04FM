********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10005 * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRE FRA 2.0 TIL 4.0
11XXX * 02 CB  PTF11 RYDDET I KILDEKODEN, IKKE MERKET
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  3VALUES(' ' '2' '4')
     A            KTSPNR         4S 0O  6 10TEXT('DISTRIKT')
     A            KTSNAV        15A  O  6 24TEXT('TOLLSTEDSNAVN')
     A            KTXPNR         4S 0O  6 43TEXT('POSTNR.')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFT04'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(035 DIV1370 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 YBC1967 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN                      CSRLOC(LINNBR POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  8MSGCON(072 YBC1968 *LIBL/SYMSGF)
     A            WTSPNR         4Y 0B  2 10TEXT('DISTRIKT')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     AAN40                                  COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTCDE(4)
     A            WTSNAV        15A  B  2 24TEXT('TOLLSTEDSNAVN')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WTXPNR         4Y 0B  2 43TEXT('POSTNR.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(024 DIV1373 *LIBL/SYMSGF)
     A            W2NAV         15A  I  2 27DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  8MSGCON(072 YBC1968 *LIBL/SYMSGF)
     A            KTSPNR         4  0O  2 10TEXT('DISTRIKT')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            KTSNAV        15   O  2 24TEXT('TOLLSTEDSNAVN')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            KTXPNR         4  0O  2 43TEXT('POSTNR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFT04'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(035 DIV1370 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 YBC1967 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
