*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTAR)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      HLPPNLGRP(HLPSCR SYEF11PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV ANBEFALT R-
     A                                      OUTING')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            KARSDF         3A  O  6  5
     A            KARSD     R    3   O  6  9
     A            KARRA1    R    3   O  6 13
     A            KARRB1    R    7   O  6 17
     A            KARRA2    R    3   O  6 25
     A            KARRB2    R    7   O  6 29
     A            KARRA3    R    3   O  6 37
     A            KARRB3    R    7   O  6 42
     A            KARTXT    R   31   O  6 50
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYEF11'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  5  2MSGCON(070 DIV1416 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV1417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A            MSG014        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG013        18A  O 23 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG012        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG011        18A  O 23 59MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG545        18A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            MSGNR          7A  P
     A                                  1  1MSGCON(070 DIV1416 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSSDF          3A  B  2  4DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A            WSSD           3A  B  2  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            WSRA1          3A  B  2 12DSPATR(UL)
     A  40N30
     AO 69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A            WSRB1          7A  B  2 16DSPATR(UL)
     A  68                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            WSRA2          3A  B  2 24DSPATR(UL)
     A  67                                  DSPATR(PC)
     A  67                                  DSPATR(RI)
     A            WSRB2          7A  B  2 28DSPATR(UL)
     A  66                                  DSPATR(PC)
     A  66                                  DSPATR(RI)
     A            WSRA3          3A  B  2 36DSPATR(UL)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            WSRB3          7A  B  2 40DSPATR(UL)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A            WSTXT         31A  B  2 48DSPATR(UL)
     A                                  3  2'Est.Time:'
     A            WSETD1         4Y 0B  3 12EDTCDE(4)
     A            WSETA1         4Y 0B  3 17EDTCDE(4)
     A                                  3 22'/'
     A            WSETD2         4Y 0B  3 24EDTCDE(4)
     A            WSETA2         4Y 0B  3 29EDTCDE(4)
     A                                  3 34'/'
     A            WSETD3         4Y 0B  3 36EDTCDE(4)
     A            WSETA3         4Y 0B  3 41EDTCDE(4)
     A            MSG007        18A  O  4  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG008        18A  O  4 21MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG148        12A  O  2  2MSGID(DIV 0026 SYMSGF)
     A            W2SDF          3A  I  2 17DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2SD           3A  I  2 21DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG006        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            MSG047        70A  O  1  2MSGID(DIV 1416 SYMSGF)
     A                                      DSPATR(HI)
     A            KARSDF         3   O  2  4
     A            KARSD     R    3   O  2  8DSPATR(PC)
     A                                      DSPATR(HI)
     A            KARRA1    R    3   O  2 12DSPATR(HI)
     A            KARRB1    R    7   O  2 16DSPATR(HI)
     A            KARRA2    R    3   O  2 24DSPATR(HI)
     A            KARRB2    R    7   O  2 28DSPATR(HI)
     A            KARRA3    R    3   O  2 36DSPATR(HI)
     A            KARRB3    R    7   O  2 40DSPATR(HI)
     A            KARTXT    R   31   O  2 48DSPATR(HI)
     A            KAETD1    R        O  3 12EDTCDE(4)
     A            KAETA1    R        O  3 17EDTCDE(4)
     A                                  3 22'/'
     A            KAETD2    R        O  3 24EDTCDE(4)
     A            KAETA2    R        O  3 29EDTCDE(4)
     A                                  3 34'/'
     A            KAETD3    R        O  3 36EDTCDE(4)
     A            KAETA3    R        O  3 41EDTCDE(4)
     A            MSG005        18A  O  4  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYEF11'
     A                                      COLOR(BLU)
     A            MSG101        40A  O  1 19MSGID(DIV 0016 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A            MSG096        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG095        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG067        70A  O  5  2MSGID(DIV 1416 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG004        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG003        18A  O 23 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG002        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG001        18A  O 23 59MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
