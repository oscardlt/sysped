/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 10XXX * 01 YBC PTF10 BEHANDLE XML-FIL                                   */
/* 10XXX * 02 YBC PTF11 REELL KUNDESPESIAL TATT INN I STANDARD (GOD MAL)   */
/* 11XXX * 03 CB  PTF11 XML-FILER FRA TULLVERKET                         */
/* ********************************************************************    */
/* *********************************************************************   */
/*  TOLKE "MELDINGSTYPE" I FLATFIL - LOGGE                                 */
/*SYSTEMA*/
/*  STATEMENTS MERKET SYSTEMA MÅ NORMALT TILPASSES DEN ENKELTE KUNDE       */
/* *********************************************************************   */
             PGM        PARM(&FILE &LIBRARY &MEMBER)
             DCL        VAR(&LIBRARY) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TYP) TYPE(*CHAR) LEN(3)
/*SYSTEMA*/  DCL        VAR(&FROM) TYPE(*CHAR) LEN(35) +
                          VALUE('NODNB101')
/*SYSTEMA*/  DCL  VAR(&FROCR) TYPE(*CHAR) LEN(35) +
                          VALUE('BBS/OCR ')
/*SYSTEMA*/  DCL        VAR(&TO) TYPE(*CHAR) LEN(35) +
                          VALUE('NO005405')
             DCL        VAR(&STRPOS) TYPE(*DEC) LEN(3) VALUE(2) /* +
                          Startposisjon for å lese datarea: EDIDTA */
             DCL        VAR(&USRID) TYPE(*CHAR) LEN(10) /* USERID +
                          FOR MSG*/
             DCL        VAR(&MSGTXT) TYPE(*CHAR) LEN(256)

     /*________________________________*/
     /* SJEKK OM INN- ELLER UTBETALINGER */
     /*""""""""""""""""""""""""""""""""*/
             CALL       PGM(EDIOCCR1K) PARM(&TYP &FROM &FROCR &TO +
                          &LIBRARY &FILE &MEMBER)
     /*_______________*/
     /* INNBETALINGER */
     /*"""""""""""""""*/
             IF         COND(&TYP *EQ 'OCR') THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('OCR - innbetalinger er +
                          ankommet, hentes inn til Løsning-1 via +
                          EDIMNU4 pkt 1')
             ENDDO
     /*_______________*/
     /* REMMITERING */
     /*"""""""""""""""*/
             IF         COND(&TYP *EQ 'REM') THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('Returdata fra +
                          remitering/utbetalinger er ankommet, +
                          hentes inn til Løsning-1 via +
                          EDIMNU4 pkt 1')
             ENDDO
     /*_______________*/
     /* NAVITRO     */
     /*"""""""""""""""*/
             IF         COND(&TYP *EQ 'NAV') THEN(DO)
             CALL       PGM(SYNA01CL) PARM(&MEMBER)
             ENDDO
     /*_______________*/
     /* BANKAVSTEMNING */
     /*"""""""""""""""*/
             IF         COND(&TYP *EQ 'AVR') THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('Bankavstemning er +
                          ankommet, hentes inn til PC-fil via +
                          EDIMNU4 pkt 1')
             ENDDO
     /*_______________________________________*/
     /* INTERN SYSPED-FAKTURA ("INVOICE")..*/
     /*"""""""""""""""""""""""""""""""""""""""*/
             IF         COND(&TYP *EQ 'IFA') THEN(DO)
             CALL       PGM(SYKS39CL2) PARM(&MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
     /*____________*/
     /* TDS TARIC  */
     /*""""""""""""*/
             IF         COND(&TYP *EQ 'TDS') THEN(DO)
             CALL       PGM(SVT001C) PARM(&MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
     /*_______________*/
     /* CTS MANIFEST  */
     /*"""""""""""""""*/
 /*N02*/     IF         COND(&TYP *EQ 'CTS') THEN(DO)
 /*N02*/     CALL       PGM(E113CTS) PARM(&MEMBER)
 /*N02*/     GOTO       CMDLBL(END)
 /*N02*/     ENDDO
     /*__________*/
     /* *IMPORT  */
     /*""""""""""*/
             IF         COND(&TYP *EQ 'IMP') THEN(DO)
             CALL       PGM(IMPGEN) PARM(&MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
     /*__________*/
     /* *EKSPORT */
     /*""""""""""*/
             IF         COND(&TYP *EQ 'EXP') THEN(DO)
             CALL       PGM(SADECSVCL) PARM(&MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
     /*_______________________________________*/
     /* KUNDESPESIFIKE FLATFIL MELDINGSTYPER..*/
     /*"""""""""""""""""""""""""""""""""""""""*/
      /* FJERN STJERNINGEN/ENDRE DE STATEMENTS EN VIL AKTIVERE       */
      /* LAG  EN NY SEKVENS FOR HVER TYPE SOM SKAL BEARBEIDES....    */
      /*     IF         COND(&TYP *EQ 'XXX') THEN(DO)                */
      /*     CHGVAR     VAR(&MSGTXT) VALUE('Data fra XXXXXXX er +    */
      /*                  ankommet......')                           */
                /* KALLE SPESIFIKT PROGRAM FOR BEARBEIDINGEN....*/
      /*     CALL XXXXXXXX                                           */
                /* MELDING TIL ANDRE PERSONER/KØER ENN EDIDTA?*/
      /*    SNDMSG     MSG(' ' *BCAT &MSGTXT *BCAT ' ') TOUSR(&USRID)*/
      /*    MONMSG     MSGID(CPF0000)                                */
                /* HOPPE OVER MELDING TIL DE I EDIDTA?????    */
      /*     GOTO       CMDLBL(END)                                  */
      /*     ENDDO                                                   */
     /*________________*/
     /* CASS AVREGNING */
     /*""""""""""""""""*/
             IF         COND(&TYP *EQ 'CAS') THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('DATA FRA CASS E+
                          R ANKOMMET')
             CALL       PGM(SYCASS1C) PARM(&MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
     /*_______________________________*/
     /* KVITTERING XML-FAKT FRA POSTEN*/
     /*"""""""""""""""""""""""""""""""*/
             IF         COND(&TYP *EQ 'XMP') THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('Kviitering av +
                          XML-faktura fra Posten.')
             CALL       PGM(SYXML11C) PARM(&LIBRARY &FILE &MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
/*N01  START */
     /*_______________________________*/
     /* KVITTERING XML-FILER          */
     /*"""""""""""""""""""""""""""""""*/
             IF         COND(&TYP *EQ 'XML') THEN(DO)
             CALL       PGM(XMLRCVC) PARM(&LIBRARY &FILE &MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
/*N01  SLUTT */
/*N03  START */
     /*_______________________________*/
     /* TULLVERKET XML-1              */
     /*"""""""""""""""""""""""""""""""*/
             IF         COND(&TYP *EQ 'XM1') THEN(DO)
             CALL       PGM(SVT100R) PARM(&LIBRARY &FILE &MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
/*N03  SLUTT */
     /*_______________________________*/
     /* TRANSPORTXML     INHOUSE      */
     /*"""""""""""""""""""""""""""""""*/
             IF         COND(&TYP *EQ 'INH') THEN(DO)
             CHGVAR     VAR(&MSGTXT) VALUE('TransportXML  InHouse er +
                          kommet.')
             CALL       PGM(SYXML13C) PARM(&LIBRARY &FILE &MEMBER)
             GOTO       CMDLBL(END)
             ENDDO
     /*_______________________________________*/
     /* VARSEL TIL DE BRUKERE SOM ER DEFINERT */
     /*"""""""""""""""""""""""""""""""""""""""*/
GETUSR:      RTVDTAARA  DTAARA(EDIDTA (&STRPOS 10)) RTNVAR(&USRID)
             IF         COND(&USRID *NE ' ') THEN(DO)
             SNDMSG     MSG(' ' *BCAT &MSGTXT *BCAT ' ') TOUSR(&USRID)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(&STRPOS) VALUE(&STRPOS + 10)
             GOTO GETUSR
             ENDDO

 END:        ENDPGM
