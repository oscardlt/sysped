     FSVG512D   CF   E             WORKSTN
     F                                     SFILE(SVG512DA:ZRECNO)
     FSVG511F   UF A E           K DISK
     FSVG511F1  IF   E           K DISK
     F                                     RENAME(SVG511FR:SVG511FR1)
     FSVG511F3  IF   E           K DISK
     F                                     RENAME(SVG511FR:SVG511FR3)
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    *IN30         WIN30
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     TUKEY1        KLIST
     C                   KFLD                    YVIH_GODN
     C                   KFLD                    YVIH_TUID
     C     TUKEY2        KLIST
     C                   KFLD                    ZVIH_GODN
     C                   KFLD                    ZVIH_TUID
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN93=*ON
     C                   WRITE     SVG512DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     SVG511F1
     C                   IF        XVIH_GODN <> *BLANKS
     C     XVIH_GODN     SETLL     SVG511F1
     C                   ENDIF
     C                   IF        XVIH_TUID <> *BLANKS
     C     XVIH_TUID     SETLL     SVG511F3
     C                   ENDIF
     C                   IF        XVIH_TUID <> *BLANKS
     C                   READ      SVG511F3                               94
     C                   ELSE
     C                   READ      SVG511F1                               94
     C                   ENDIF

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SVG512DA
     C                   IF        XVIH_TUID <> *BLANKS
     C                   READ      SVG511F3                               94
     C                   ELSE
     C                   READ      SVG511F1                               94
     C                   ENDIF
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     SVG512DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVG512DE
     C                   WRITE     SVG512DØ
     C                   WRITE     SVG512DC
     C                   EXFMT     SVG512DB

     C                   IF        XVIH_GODN <> *BLANKS
     C                   GOTO      SRB1
     C                   ENDIF

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   ENDIF
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     SVG512DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR

     C                   CLEAR                   SVG512DD
     C                   MOVEA     '00000000'    *IN(30)
     C                   EVAL      *IN40=*OFF

     C     SRBA1         TAG

      *____________
      * Blanke felt
      *""""""""""""
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVG512DØ
     C                   EXFMT     SVG512DD
     C                   MOVEA     '00000000'    *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN05         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'
      *_______
      * F4=Søk
      *"""""""
     C     *IN04         IFEQ      '1'
     C                   ENDIF
      *____________
      * Teste input-1
      *""""""""""""
      * Kod må vare utfyllt
     C     ZVIH_GODN     IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'SVT0025'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Godslokalkod
     C                   IF        ZVIH_GOLK = *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'SVT0024'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Uttagsdatum
     C                   IF        ZVIH_SYDT = *ZEROS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0026'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Førfarande
     C                   IF        ZVIH_EUP1 = *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN34=*ON
     C                   MOVE      'SVT0028'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Tullid
     C                   IF        ZVIH_TUID = *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN35=*ON
     C                   MOVE      'SVT0029'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   EVAL      YVIH_GODN=ZVIH_GODN
     C                   EVAL      YVIH_GOLK=ZVIH_GOLK
     C                   EVAL      YVIH_SYDT=ZVIH_SYDT
     C                   EVAL      YVIH_EUP1=ZVIH_EUP1
     C                   EVAL      YVIH_TUID=ZVIH_TUID
     C                   EVAL      YVIH_KOTA=ZVIH_KOTA
     C                   EVAL      YVIH_BRUT=ZVIH_BRUT
     C                   WRITE     SVG511FR
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   EVAL      ZRECNO=WRECNO
     C                   WRITE     SVG512DA

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
     C                   EVAL      *IN40=*ON
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVG512DA
      *____________
      * Flytte felt
      *""""""""""""
     C     TUKEY1        CHAIN(N)  SVG511F                            53
     C                   MOVEA     '00000000'    *IN(30)
     C                   EVAL      ZVIH_GODN=YVIH_GODN
     C                   EVAL      ZVIH_GOLK=YVIH_GOLK
     C                   EVAL      ZVIH_SYDT=YVIH_SYDT
     C                   EVAL      ZVIH_EUP1=YVIH_EUP1
     C                   EVAL      ZVIH_TUID=YVIH_TUID
     C                   EVAL      ZVIH_KOTA=YVIH_KOTA
     C                   EVAL      ZVIH_BRUT=YVIH_BRUT

     C     SRBB1         TAG

     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVG512DØ
     C                   EXFMT     SVG512DD
     C                   MOVEA     '00000000'    *IN(30)
     C     *IN12         IFEQ      '0'
      *_______
      * F4=Søk
      *"""""""
     C     *IN04         IFEQ      '1'
     C                   ENDIF
      *____________
      * Teste input-1
      *""""""""""""
      * Godslokalkod
     C                   IF        ZVIH_GOLK = *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'SVT0024'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Uttagsdatum
     C                   IF        ZVIH_SYDT=*ZEROS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0026'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     TUKEY2        CHAIN     SVG511F                            53
     C                   EVAL      YVIH_GOLK=ZVIH_GOLK
     C                   EVAL      YVIH_SYDT=ZVIH_SYDT
     C                   EVAL      YVIH_EUP1=ZVIH_EUP1
     C                   EVAL      YVIH_KOTA=ZVIH_KOTA
     C                   EVAL      YVIH_BRUT=ZVIH_BRUT
     C                   UPDATE    SVG511FR

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     TUKEY1        CHAIN     SVG511F                            53
     C                   DELETE    SVG511FR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   SVG512DA
     C                   UPDATE    SVG512DA
     C                   EVAL      *IN95=*ON

     C                   ENDSR
