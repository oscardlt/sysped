********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VAREBESKRIVELSE ER ENDRET
10311 * 02 YBC PTF10 DOKUMENTKODE ER ENDRET FRA 3A TIL 4A
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SVX065PNL)
     A                                      HLPTITLE('REGISTRERING AV LOSSEMERK-
     A                                      NADER TET IMPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5' '9')
     A            TVLI           5Y 0O  4  5EDTCDE(4)
     A  41                                  COLOR(YLW)
     A            W2VNT          6S 0O  4 11
     A            W2VT          15A  O  4 20
     A  41                                  COLOR(YLW)
     A            W2VKTN         9Y 3O  4 36EDTCDE(4)
S02  A*           W2DTY          3A  O  4 47
N02  A            W2DTY          4A  O  4 47
E02  A            W2DREF        19A  O  4 52
E02  A            W2EH           2A  O  4 72
E02  A            W2NTK          5Y 0O  4 75EDTCDE(4)
     A            TVVNT          6S 0H
     A            TVDK           5A  H
S01  A*           TVVT         140A  H
S01  A*           TVVT48        48A  H
N01  A            TVVT          70A  H
     A            TVVTSK         2A  H
     A            TVVKTB         9Y 3H
     A            TVVKTN         9Y 3H
     A            TVALK          2A  H
     A            TVBLK          2A  H
S02  A*           TVDTY          3A  H
N02  A            TVDTY          4A  H
     A            TVDREF        35A  H
     A            TVDSK          2A  H
     A            TVDO          26A  H
     A            TVDOSK         2A  H
     A            TVTLO          3A  H
     A            TVEXKD         1Y 0H
     A            TVEXLK         2A  H
     A            TVAVD2         4Y 0H
     A            TVTDN2         7Y 0H
     A            TVCNR         17A  H
     A            TVMN          42A  H
     A            TVMNSK         2A  H
     A            TVEH           2A  H
     A            TVNT           5Y 0H
     A            TVNTEH         5Y 0H
     A            TVFV           2A  H
     A            TVFVNT        11Y 3H
     A            TV01           1A  H
     A            NVVNT          6  0H
S01  A*           NVVT          48A  H
N01  A            NVVT          70A  H
     A            NVVTSK         2A  H
     A            NVVKTB         9  3H
     A            NVVKTN         9  3H
S02  A*           NVDTY          3A  H
N02  A            NVDTY          4A  H
     A            NVDREF        35A  H
     A            NVDSK          2A  H
     A            NVDO          26A  H
     A            NVDOSK         2A  H
     A            NVCNR         17A  H
     A            NVMN          42A  H
     A            NVMNSK         2A  H
     A            NVEH           2A  H
     A            NVNT           5S 0H
     A            NVNTEH         5S 0H
     A            NVFV           2A  H
     A            NVFVNT        11S 3H
     A            NVCT           2A  H
     A            NVCTB         70A  H
     A            NVCTSK         2A  H
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SVX065R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS Import Varulinjer'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Vælj option, 2=Ændra 4=Radera 5=Vi-
     A                                      sa 9=Ångra'
     A                                      COLOR(BLU)
     A  27                              2  2'Vælj option, 5=Visa'
     A                                      COLOR(BLU)
     A N32                              3  2'Op Linje Varulinj Varubeskrivning -
E02  A                                      Nettovikt  Dokumenttyp/ref ........-
E02  A                                       Kolli'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Postn Varenr.. Varebeskrivelse -
E02  A                                      Nettovekt  Dokumenttype/ref........-
E02  A                                       Kolli'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              5  2'F3=Avsluta   F5=Uppdatera   F6=Ska-
     A                                      pa ny   F8=Hoppa till   F12=Avbryt'
     A                                      COLOR(BLU)
     A  27                              5  2'F3=Avsluta   F5=Uppdatera   F8=Hop-
     A                                      pa till   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CF12(12 'Forrige')
     A          H                           HLPPNLGRP(SVX065001 SVX065PNL)
     A                                      HLPARA(02 001 02 046)
     A          H                           HLPPNLGRP(SVX065001 SVX065PNL)
     A                                      HLPARA(03 001 03 003)
     A          H                           HLPPNLGRP(SVX065001 SVX065PNL)
     A                                      HLPARA(04 001 04 003)
     A          H                           HLPPNLGRP(SVX065002 SVX065PNL)
     A                                      HLPARA(05 001 11 080)
     A          H                           HLPPNLGRP(SVX065003 SVX065PNL)
     A                                      HLPARA(12 001 12 011)
     A          H                           HLPPNLGRP(SVX065004 SVX065PNL)
     A                                      HLPARA(12 014 12 026)
     A          H                           HLPPNLGRP(SVX065005 SVX065PNL)
     A                                      HLPARA(13 001 13 080)
     A          H                           HLPPNLGRP(SVX065006 SVX065PNL)
     A                                      HLPARA(14 001 14 044)
     A          H                           HLPPNLGRP(SVX065007 SVX065PNL)
     A                                      HLPARA(15 001 15 017)
     A          H                           HLPPNLGRP(SVX065008 SVX065PNL)
     A                                      HLPARA(16 001 16 057)
     A          H                           HLPPNLGRP(SVX065004 SVX065PNL)
     A                                      HLPARA(16 062 16 071)
     A          H                           HLPPNLGRP(SVX065009 SVX065PNL)
     A                                      HLPARA(17 001 17 018)
     A          H                           HLPPNLGRP(SVX065010 SVX065PNL)
     A                                      HLPARA(17 022 17 038)
     A          H                           HLPPNLGRP(SVX065011 SVX065PNL)
     A                                      HLPARA(18 001 18 012)
     A          H                           HLPPNLGRP(SVX065012 SVX065PNL)
     A                                      HLPARA(18 022 18 049)
     A          H                           HLPPNLGRP(SVX065004 SVX065PNL)
     A                                      HLPARA(18 052 18 061)
     A          H                           HLPPNLGRP(SVX065013 SVX065PNL)
     A                                      HLPARA(19 001 19 035)
     A          H                           HLPPNLGRP(SVX065004 SVX065PNL)
     A                                      HLPARA(19 042 19 051)
     A          H                           HLPPNLGRP(SVX065014 SVX065PNL)
     A                                      HLPARA(20 001 20 026)
     A          H                           HLPPNLGRP(SVX065004 SVX065PNL)
     A                                      HLPARA(20 053 20 054)
     A          H                           HLPPNLGRP(SVX065016 SVX065PNL)
     A                                      HLPARA(20 061 20 077)
     A          H                           HLPPNLGRP(SVX065017 SVX065PNL)
     A                                      HLPARA(21 002 21 011)
     A          H                           HLPPNLGRP(SVX065018 SVX065PNL)
     A                                      HLPARA(21 015 21 023)
     A          H                           HLPPNLGRP(SVX065019 SVX065PNL)
     A                                      HLPARA(21 027 21 035)
     A          H                           HLPPNLGRP(SVX065020 SVX065PNL)
     A                                      HLPARA(21 042 21 066)
     A          H                           HLPPNLGRP(SVX065021 SVX065PNL)
     A                                      HLPARA(23 001 23 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSLI           5S 0H
     A                                  1  2'SYSPED/SVX065R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS import  Varulinje'
     A                                      COLOR(WHT)
     A                                  1 45'Ærende '
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'Varunr.'
     A            TVVNT          6S 0O  3 10TEXT('Varenr Tolltariff')
     A                                      COLOR(WHT)
     A                                  3 22'Dekl.typ '
     A            TVDK           5A  O  3 32TEXT('DEKL.TYPE')
     A                                      COLOR(WHT)
     A                                  3 42'Br.vikt'
     A            TVVKTB         9Y 3O  3 50TEXT('Vekt Brutto')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 62'Nt.vikt'
     A            TVVKTN         9Y 3O  3 70TEXT('Vekt Netto')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2'V.beskr'
S01  A*           TVVT48        48A  O  4 10TEXT('VAREBESKRIVELSE')
S01  A*                                     COLOR(WHT)
S01  A*                                 4 62'SpråkKD'
S01  A*           TVVTSK         2A  O  4 70TEXT('SPÅRKKODE')
S01  A*                                     COLOR(WHT)
N01  A            TVVT          70A  O  4 10TEXT('VAREBESKRIVELSE')
N01  A N57                                  COLOR(WHT)
N01  A  57                                  COLOR(BLU)
     A                                  5  2'44. Dok'
S02  A*           TVDTY          3A  O  5 10TEXT('DOKUMENTTYPE')
N02  A            TVDTY          4A  O  5 10TEXT('DOKUMENTTYPE')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  5 22'Dok.Ref'
     A            TVDREF        35A  O  5 30TEXT('DOKUMENTREF.')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  5 66'Dok.SK '
     A            TVDSK          2A  O  5 75TEXT('SPRÅKKODE')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  6  2'44. Upp'
     A            TVDO          26A  O  6 10TEXT('Utfylt opplysning')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  6 42'SpråkKD'
     A            TVDOSK         2A  O  6 50TEXT('Spårkkode')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  7  2'Speciella mærknader(Rubr. 44):'
     A                                      COLOR(WHT)
     A                                  7 42'T.O. ID'
     A            TVTLO          3A  O  7 51TEXT('Tilleggsoppl.ID')
     A                                      COLOR(WHT)
     A                                  7 58'Exp. från EU/Land'
     A            TVEXKD         1Y 0O  7 76TEXT('Eksp. fra EU')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TVEXLK         2A  O  7 78TEXT('Eksp. fra land')
     A                                      COLOR(WHT)
     A                                  8  2'31. Upp'
     A            TVMN          42A  O  8 10TEXT('MERKE & NR')
     A N53                                  COLOR(WHT)
     A  53                                  COLOR(BLU)
     A            TVMNSK         2A  O  8 53TEXT('SPRÅKKODE')
     A N53                                  COLOR(WHT)
     A  53                                  COLOR(BLU)
     A                                  8 57'Cnr'
     A            TVCNR         17A  O  8 61TEXT('Containernr')
     A N54                                  COLOR(WHT)
     A  54                                  COLOR(BLU)
     A                                  9  2'Enhet'
     A            TVEH           2A  O  9 10TEXT('Enhet')
     A N56                                  COLOR(WHT)
     A  56                                  COLOR(BLU)
     A                                  9 15'Kll'
     A            TVNT           5Y 0O  9 19TEXT('Antall')
     A N56                                  COLOR(WHT)
     A  56                                  COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  9 28'STK'
     A            TVNTEH         5Y 0O  9 32TEXT('Antall')
     A N56                                  COLOR(WHT)
     A  56                                  COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  9 42'F.vara '
     A            TVFV           2A  O  9 50TEXT('Koder for følsomme varer')
     A                                      COLOR(WHT)
     A            TVFVNT        11Y 3O  9 55TEXT('Antall føæsomme varer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11  2'Kontroll resultat:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2'Kod    '
     A            WSCT           2A  B 12 10DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 14'Språkkod: '
     A            WSCTSK         2A  B 12 25DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'Beskriv'
     A            WSCTB         70A  B 13 10DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15  2'Varunr.'
     A            WSVNTI         6A  B 15 10TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVNT          6S 0H
     A                                 16  2'V.beskr'
S01  A*           WSVT          48A  B 16 10TEXT('VAREBESKRIVELSE')
N01  A            WSVT          70A  B 16 10TEXT('VAREBESKRIVELSE')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
N01  A N47                                  COLOR(YLW)
N01  A  47                                  COLOR(RED)
S01  A*                                     COLOR(YLW)
S01  A*                                16 62'SpråkKD'
S01  A*           WSVTSK         2A  B 16 70TEXT('SPÅRKKODE')
S01  A*                                     DSPATR(UL)
S01  A* 84                                  DSPATR(RI)
S01  A*                                     COLOR(YLW)
     A                                 17  2'Br.vikt'
     A            WSVKTB         9Y 3B 17 10TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 17 22'Nt.vikt'
     A            WSVKTN         9Y 3B 17 30TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
N01  A                                 17 62'SpråkKD'
N01  A            WSVTSK         2A  B 17 70TEXT('SPÅRKKODE')
N01  A                                      DSPATR(UL)
N01  A  84                                  DSPATR(RI)
N01  A                                      COLOR(YLW)
     A                                 18  2'44. Dok'
S02  A*           WSDTY          3A  B 18 10TEXT('DOKUMENTTYPE')
N02  A            WSDTY          4A  B 18 10TEXT('DOKUMENTTYPE')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 18 22'Dok.Ref'
     A            WSDREF        35A  B 18 30TEXT('DOKUMENTREF.')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 18 66'Dok.SK '
     A            WSDSK          2A  B 18 75TEXT('SPRÅKKODE')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 19  2'44. Upp'
     A            WSDO          26A  B 19 10TEXT('Utfylt opplysning')
     A                                      DSPATR(UL)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 19 42'SpråkKD'
     A            WSDOSK         2A  B 19 50TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 20  2'31. Upp'
     A            WSMN          35A  B 20 10TEXT('MERKE & NR')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A N43                                  COLOR(YLW)
     A  43                                  COLOR(RED)
     A            WSMNSK         2A  B 20 53TEXT('SPRÅKKODE')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A N43                                  COLOR(YLW)
     A  43                                  COLOR(RED)
     A                                 20 57'Cnr'
     A            WSCNR         17A  B 20 61TEXT('Containernr')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A N44                                  COLOR(YLW)
     A  44                                  COLOR(RED)
     A                                 21  2'Enhet'
     A            WSEH           2A  B 21 10TEXT('Enhet')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A N46                                  COLOR(YLW)
     A  46                                  COLOR(RED)
     A                                 21 15'Kll'
     A            WSNT           5Y 0B 21 19TEXT('Antall')
     A                                      DSPATR(UL)
     A N46                                  COLOR(YLW)
     A  46                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                 21 27'STK'
     A            WSNTEH         5Y 0B 21 31TEXT('Antall')
     A                                      DSPATR(UL)
     A N46                                  COLOR(YLW)
     A  46                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                 21 42'F.vara '
     A            WSFV           2A  B 21 50TEXT('Koder for følsomme varer')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFVNT        11Y 3B 21 55TEXT('Antall føæsomme varer')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 23  2'F4=Søk   F12=Avbryt   F15=Tillæggs-
     A                                      upplysningar'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                  1  2'SYSPED/SVX065R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS import'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'Varenr.'
     A            TVVNT          6S 0O  3 10TEXT('Varenr Tolltariff')
     A                                      COLOR(WHT)
     A                                  3 22'Dekl.type'
     A            TVDK           5A  O  3 32TEXT('DEKL.TYPE')
     A                                      COLOR(WHT)
     A                                  3 42'Br.vekt'
     A            TVVKTB         9Y 3O  3 50TEXT('Vekt Brutto')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 62'Nt.vekt'
     A            TVVKTN         9Y 3O  3 70TEXT('Vekt Netto')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2'V.beskr'
S01  A*           TVVT48        48A  O  4 10TEXT('VAREBESKRIVELSE')
S01  A*                                     COLOR(WHT)
S01  A*                                 4 62'SpråkKD'
S01  A*           TVVTSK         2A  O  4 70TEXT('SPÅRKKODE')
S01  A*                                     COLOR(WHT)
N01  A            TVVT          70A  O  4 10TEXT('VAREBESKRIVELSE')
N01  A N57                                  COLOR(WHT)
N01  A  57                                  COLOR(BLU)
     A                                  5  2'44. Dok'
S02  A*           TVDTY          3A  O  5 10TEXT('DOKUMENTTYPE')
N02  A            TVDTY          4A  O  5 10TEXT('DOKUMENTTYPE')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  5 22'Dok.Ref'
     A            TVDREF        35A  O  5 30TEXT('DOKUMENTREF.')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  5 66'Dok.SK '
     A            TVDSK          2A  O  5 75TEXT('SPRÅKKODE')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  6  2'44. Opl'
     A            TVDO          26A  O  6 10TEXT('Utfylt opplysning')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  6 42'SpråkKD'
     A            TVDOSK         2A  O  6 50TEXT('Spårkkode')
     A N52                                  COLOR(WHT)
     A  52                                  COLOR(BLU)
     A                                  7  2'Speielle merknader (Rubr. 44):'
     A                                      COLOR(WHT)
     A                                  7 42'T.O. ID'
     A            TVTLO          3A  O  7 51TEXT('Tilleggsoppl.ID')
     A                                      COLOR(WHT)
     A                                  7 58'Eksp. fra EU/Land'
     A            TVEXKD         1Y 0O  7 76TEXT('Eksp. fra EU')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TVEXLK         2A  O  7 78TEXT('Eksp. fra land')
     A                                      COLOR(WHT)
     A                                  8  2'31. Opl'
     A            TVMN          42A  O  8 10TEXT('MERKE & NR')
     A N53                                  COLOR(WHT)
     A  53                                  COLOR(BLU)
     A            TVMNSK         2A  O  8 53TEXT('SPRÅKKODE')
     A N53                                  COLOR(WHT)
     A  53                                  COLOR(BLU)
     A                                  8 57'Cnr'
     A            TVCNR         17A  O  8 61TEXT('Containernr')
     A N54                                  COLOR(WHT)
     A  54                                  COLOR(BLU)
     A                                  9  2'Enhet'
     A            TVEH           2A  O  9 10TEXT('Enhet')
     A N56                                  COLOR(WHT)
     A  56                                  COLOR(BLU)
     A                                  9 15'Kll'
     A            TVNT           5Y 0O  9 19TEXT('Antall')
     A N56                                  COLOR(WHT)
     A  56                                  COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  9 27'STK'
     A            TVNTEH         5Y 0O  9 31TEXT('Antall')
     A N56                                  COLOR(WHT)
     A  56                                  COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  9 42'F.vare '
     A            TVFV           2A  O  9 50TEXT('Koder for følsomme varer')
     A                                      COLOR(WHT)
     A            TVFVNT        11Y 3O  9 55TEXT('Antall føæsomme varer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11  2'Kontroll resultat:'
     A                                      COLOR(WHT)
     A                                 12  2'Kode   '
     A            NVCT           2A  O 12 10DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 14'Språkkode:'
     A            NVCTSK         2A  O 12 25DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13  2'Beskriv'
     A            NVCTB         70A  O 13 10DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  2'Varenr.'
     A            NVVNT          6S 0O 15 10TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  2'V.beskr'
S01  A*           NVVT          48A  O 16 10TEXT('VAREBESKRIVELSE')
S01  A*                                     DSPATR(UL)
S01  A*                                     COLOR(YLW)
S01  A*                                16 62'SpråkKD'
S01  A*           NVVTSK         2A  O 16 70TEXT('SPÅRKKODE')
S01  A*                                     DSPATR(UL)
S01  A*                                     COLOR(YLW)
N01  A            NVVT          70A  O 16 10TEXT('VAREBESKRIVELSE')
N01  A                                      DSPATR(UL)
N01  A N47                                  COLOR(YLW)
N01  A  47                                  COLOR(RED)
     A                                 17  2'Br.vekt'
     A            NVVKTB         9Y 3O 17 10TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 17 22'Nt.vekt'
     A            NVVKTN         9Y 3O 17 30TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
N01  A                                 17 62'SpråkKD'
N01  A            NVVTSK         2A  O 17 70TEXT('SPÅRKKODE')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
     A                                 18  2'44. Dok'
S02  A*           NVDTY          3A  O 18 10TEXT('DOKUMENTTYPE')
N02  A            NVDTY          4A  O 18 10TEXT('DOKUMENTTYPE')
     A                                      DSPATR(UL)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 18 22'Dok.Ref'
     A            NVDREF        35A  O 18 30TEXT('DOKUMENTREF.')
     A                                      DSPATR(UL)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 18 66'Dok.SK '
     A            NVDSK          2A  O 18 75TEXT('SPRÅKKODE')
     A                                      DSPATR(UL)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 19  2'44. Opl'
     A            NVDO          26A  O 19 10TEXT('Utfylt opplysning')
     A                                      DSPATR(UL)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 19 42'SpråkKD'
     A            NVDOSK         2A  O 19 50TEXT('Spårkkode')
     A                                      DSPATR(UL)
     A N42                                  COLOR(YLW)
     A  42                                  COLOR(RED)
     A                                 20  2'31. Opl'
     A            NVMN          42A  O 20 10TEXT('MERKE & NR')
     A                                      DSPATR(UL)
     A N43                                  COLOR(YLW)
     A  43                                  COLOR(RED)
     A            NVMNSK         2A  O 20 53TEXT('SPRÅKKODE')
     A                                      DSPATR(UL)
     A N43                                  COLOR(YLW)
     A  43                                  COLOR(RED)
     A                                 20 57'Cnr'
     A            NVCNR         17A  O 20 61TEXT('Containernr')
     A                                      DSPATR(UL)
     A N44                                  COLOR(YLW)
     A  44                                  COLOR(RED)
     A                                 21  2'Enhet'
     A            NVEH           2A  O 21 10TEXT('Enhet')
     A                                      DSPATR(UL)
     A N46                                  COLOR(YLW)
     A  46                                  COLOR(RED)
     A                                 21 15'Kll'
     A            NVNT           5Y 0O 21 19TEXT('Antall')
     A                                      DSPATR(UL)
     A N46                                  COLOR(YLW)
     A  46                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                 21 27'STK'
     A            NVNTEH         5Y 0O 21 31TEXT('Antall')
     A                                      DSPATR(UL)
     A N46                                  COLOR(YLW)
     A  46                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                 21 42'F.vare '
     A            NVFV           2A  O 21 50TEXT('Koder for følsomme varer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            NVFVNT        11Y 3O 21 55TEXT('Antall føæsomme varer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 23  2'F12=Forrige   F15=Tillegg opplysni-
     A                                      nger'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til bilde02')
     A                                  1  2'SYSPED/SVX065R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS import'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                       9=Angre'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis'
     A                                      COLOR(BLU)
     A N32                              3  2'Op Linje Varenr.. Varebeskrivelse -
     A                                      Nettovekt  Dokumenttype/ref........-
E02  A                                       Kolli'
E02  A                                      COLOR(WHT)
     A  32                              3  2'Op Postn Varenr.. Varebeskrivelse -
E02  A                                      Nettovekt  Dokumenttype/ref........-
E02  A                                       Kolli'
     A                                      COLOR(WHT)
     A N27                             22  2'F3=Avslutt   F5=Forny   F6=Lag ny -
     A                                        F8=Hopp til   F12=Forrige'
     A                                      COLOR(BLU)
     A  27                             22  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGFHOL)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  3  2'Hopp til varenr. -->'
     A            W3VNT          6Y 0B  3 24DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'Hopp til postnr. -->'
     A            W3LI           5S 0I  4 24DSPATR(UL)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
