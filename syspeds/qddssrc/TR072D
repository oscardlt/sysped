********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10250 * 01 YBC PTF10 DOKUMENTKODE ER ENDRET FRA 3A TIL 4A
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 16 74 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '9')
     A  27                                  DSPATR(PR)
S01  A*           NVDTY          3A  O  4  5
N01  A            NVDTY          4A  O  4  5
E01  A            NVDREF        35A  O  4 10
     A            NVDSK          2A  H
E01  A            NVDO          26A  O  4 46
     A            NVDOSK         2A  H
S01  A*           TVDTY          3A  O  5  5
N01  A            TVDTY          4A  O  5  5
E01  A            TVDREF        35A  O  5 10
     A            TVDSK          2A  H
E01  A            TVDO          26A  O  5 46
     A            TVDOSK         2A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'Lag ny')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 48SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'TR072R'
     A                                      COLOR(BLU)
     A                                  1 10'Arbeid med nytt dokument'
     A                                      COLOR(WHT)
     A N27                              2  2'2=Endre 4=Slett 9=Angre'
     A                                      COLOR(BLU)
E01  A                                  3  2'Op Dty  Dokumentreferanse         -
E01  A                                                Utfylt opplysning       '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A N27                             15  2'F3/12=Avbryt F4=Søk F5=Forny F6=La-
     A                                      g ny'
     A                                      COLOR(BLU)
     A  27                             15  2'F3/12=Avbryt F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O 16  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(14)
     A                                      CLRL(03)
     A                                      OVERLAY
E01  A                                  1  2'Dty  Dokumentreferanse            -
E01  A                                            SK Utfylt opplysning         -
E01  A                                        SK'
     A                                      COLOR(WHT)
S01  A*           WSDTY          3A  B  2  2DSPATR(UL)
N01  A            WSDTY          4A  B  2  2DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90
     AON30                                  DSPATR(PC)
E01  A            WSDREF        35A  B  2  7DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
E01  A            WSDSK          2A  B  2 43DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A  88                                  DSPATR(PC)
E01  A            WSDO          26A  B  2 46DSPATR(UL)
     A  27                                  DSPATR(PR)
E01  A            WSDOSK         2A  B  2 73DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A  87                                  DSPATR(PC)
     A N30                              3  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A N27                                  CA06(06 'Lag ny')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'TR072R'
     A                                      COLOR(BLU)
     A                                  1 10'Arbeid med nytt dokument'
     A                                      COLOR(WHT)
E01  A                                  3  2'Op Dty  Dokumentreferanse         -
E01  A                                                Utfylt opplysning       '
     A                                      COLOR(WHT)
     A N30N27                          15  2'F3/12=Avbryt F4=Søk F5=Forny'
     A                                      COLOR(BLU)
     A N30 27                          15  2'F3/12=Avbryt F5=Forny'
     A                                      COLOR(BLU)
