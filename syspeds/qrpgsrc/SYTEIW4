      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYTEIW4 MODULE(*LIBL/SYTEIW4
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FHENOP     IF   E           K DISK    USROPN
     FHENOS     IF   E           K DISK    USROPN
     FHENOK     IF   E           K DISK    USROPN
     FFRISOL01  IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FDISP      IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FDOKUFM    IF   E           K DISK    USROPN
     FWRKCGI    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D Term            s              4
     D XXLNR           s              7
     D Ekod            s              3
     D XHSTDK          s             20
     D VisAChecked     s             10
     D VisAlleS        s              1
     D VisACheckedK    s             10
     D VisAlleK        s              1
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D TITLE           C                   CONST('Terminal INN')
      *
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
     D                 DS
     D  HERFK                  1     35
     D  HXSNDN3                1      3
     D  HXSNDN                 1     20
     D  HXSNDN17               4     20
     D                 DS
     D  Ekey                   1     35
     D  FsDsKNS                1      8
     D  FsDsDT                10     17
     D  FsDsBil               19     28
     D                 DS
     D  FMMRK1                 1     35
     D  FMID                   1     18
     D  FMSTINN               33     33
     D                 DS
     D  WKey2                  1     30
     D  WDsUser                1     10
     D  WDsCLID               11     30
     D                 DS
     D  Wdata                  1    220
     D  WDsDT                  1      8
     D  WDsDTÅÅ                1      4
     D  WDsDTMM                5      6
     D  WDsDTDD                7      8
     D  WDsTID                 9     14
     D  WDsPALL               15     15
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
     C                   MOVE      BIBESK        WINT              3
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      * 4-Sections /$tablerow
      *
     c     HOLNR         ifne      *zeros
     c                   exsr      Hsend
     c                   else
     c                   exsr      Esend
     c                   end
      *
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
      *
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   eval      term     = zhbgetvar('term')
     C                   eval      XXLNR    = zhbgetvar('HOLNR')
     C                   eval      HOLNR   = c2n2(XXLNR)
     C                   eval      eKod     = zhbgetvar('ekod')
     C                   eval      eKey     = zhbgetvar('ekey')
      * Checkboks for Vis lager
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellers kommer verdi fra siste valg via "KunPaBil" som checked / blank(N)
     C     VisAChecked   Ifeq      'checked'
     C                   eval      VisAlleS = 'Y'
     C                   else
     C                   eval      VisAlleS = zhbgetvar('VisAlleS')
     C                   end
      *
     C     VisACheckedK  Ifeq      'checked'
     C                   eval      VisAlleK = 'Y'
     C                   else
     C                   eval      VisAlleK = zhbgetvar('VisAlleK')
     C                   end
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('TITLE':TITLE)
      * KUNDE
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('WSNA':KNAVN)
     C                   callp     updhtmlvar('USER':WSUSER)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updHTMLvar('TERM':TERM)
     C     VisAlleS      Ifeq      'Y'
     C                   eval      VisAChecked = 'checked'
     C                   end
     C                   callp     updHTMLvar('VisAChecked':VisAChecked)
     C     VisAlleK      Ifeq      'Y'
     C                   eval      VisACheckedK = 'checked'
     C                   end
     C                   callp     updHTMLvar('VisACheckedK':VisACheckedK)
     C                   callp     updHTMLvar('HOLNR':
     C                             %trim(%editc(HOLNR:'Z')))
     C                   callp     updHTMLvar('EKOD':EKOD)
     C                   callp     updHTMLvar('EKEY':EKEY)
      *
     c     HOLNR         ifne      *zeros
     C                   callp     updHTMLvar('TYPE':'Hente-Oppdrag')
     C                   callp     updHTMLvar('PRTMLTXT':'')
     c     HOLNR         CHAIN     HENOP
     c                   else
     C                   callp     updHTMLvar('TYPE':'E-sending')
     C                   callp     updHTMLvar('PRTMLTXT':'Print ML')
     C                   eval      HOKNS   = c2n2(FsDsKNS)
     C     KUNKEY        CHAIN     CUNDL01                            33
     C                   eval      HONAS   = KNAVN
     C                   eval      HODT    = c2n2(FsDsDt)
     C                   eval      HOBILN  = FsDsBil
     c                   end
     C                   callp     updHTMLvar('HOKNS':
     C                             %trim(%editc(HOKNS:'Z')))
     C                   callp     updHTMLvar('HONAS':HONAS)
     C                   callp     updHTMLvar('HODT':
     C                             %trim(%editW(HODT:'    .  .  ')))
     C                   callp     updHTMLvar('HOBILN':HOBILN)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updHTMLvar('TERM':TERM)
     C                   callp     updHTMLvar('HOLNR':
     C                             %trim(%editc(HOLNR:'Z')))
     C                   callp     updHTMLvar('EKOD':EKOD)
     C                   callp     updHTMLvar('EKEY':EKEY)
      *
     C                   callp     updHTMLvar('HSLNR2':
     C                             %trim(%editc(HSLNR2:'Z')))
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(FSAVD:'Z')))
     C                   callp     updHTMLvar('OPD':
     C                             %trim(%editc(FSOPD:'Z')))
      *
     C                   callp     updHTMLvar('HSSNDN':HSSNDN)
     C                   callp     updHTMLvar('HSNAK':HSNAK)
     C                   callp     updHTMLvar('HSSTDK':HSSTDK)
     C                   callp     updHTMLvar('HSST':HSST)
      *
     C                   exsr      AntHkll
      *
     C                   callp     updHTMLvar('Skll      ':
     C                             %trim(%editc(HSANT      :'Z')))
     C                   callp     updHTMLvar('Svkt      ':
     C                             %trim(%editc(HSVKT      :'Z')))
     C                   callp     updHTMLvar('SM3       ':
     C                             %trim(%editc(HSM3       :'3')))
     C                   callp     updHTMLvar('AntKTot   ':
     C                             %trim(%editc(AntKTot   :'Z')))
     C                   callp     updHTMLvar('AntKOpn   ':
     C                             %trim(%editc(AntkOpn    :'Z')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRowHK   begsr
      *
     C                   callp     updHTMLvar('TERM':TERM)
     C                   callp     updHTMLvar('HOLNR':
     C                             %trim(%editc(HOLNR:'Z')))
     C                   callp     updHTMLvar('EKOD':EKOD)
     C                   callp     updHTMLvar('EKEY':EKEY)
      *
     C                   callp     updHTMLvar('HSLNR2':
     C                             %trim(%editc(HSLNR2:'Z')))
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(FSAVD:'Z')))
     C                   callp     updHTMLvar('OPD':
     C                             %trim(%editc(FSOPD:'Z')))
      *
     C                   callp     updHTMLvar('HSSNDN':'')
     C                   callp     updHTMLvar('HSNAK':HKID)
     C                   eval      XHSTDK = %TRIM(%EDITC(KLLSEKV:'Z')) + ' av '
     C                             + %TRIM(%EDITC(HSANT:'Z'))
     C                   callp     updHTMLvar('HSSTDK':XHSTDK)
     C                   callp     updHTMLvar('HSST':HKSTIN)
      *
     C*                  callp     updHTMLvar('Skll      ':'')
     C                   callp     updHTMLvar('Svkt      ':'')
     C                   callp     updHTMLvar('SM3       ':'')
     C                   callp     updHTMLvar('AntKTot   ':'')
     C                   callp     updHTMLvar('AntKOpn   ':'')
      *
     C                   exsr      TstWrk
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRowE    begsr
      *
     C                   callp     updHTMLvar('TERM':TERM)
     C                   callp     updHTMLvar('HOLNR':
     C                             %trim(%editc(HOLNR:'Z')))
     C                   callp     updHTMLvar('EKOD':EKOD)
     C                   callp     updHTMLvar('EKEY':EKEY)
      *
     C                   callp     updHTMLvar('HSLNR2':
     C                             %trim(%editc(HSLNR2:'Z')))
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(FSAVD:'Z')))
     C                   callp     updHTMLvar('OPD':
     C                             %trim(%editc(FSOPD:'Z')))
      *
     C                   callp     updHTMLvar('HSSNDN':HXSNDN)
     C                   callp     updHTMLvar('HSNAK':HENAK)
     c                   move      headk3        TMPadk           25
     C                   callp     updHTMLvar('HSSTDK':TMPADK)
     C                   callp     updHTMLvar('HSST':DISTA1)
      *
     C                   exsr      AntEkll
      *
     C                   callp     updHTMLvar('Skll      ':
     C                             %trim(%editc(HENT       :'Z')))
     C                   callp     updHTMLvar('Svkt      ':
     C                             %trim(%editc(HEVKT      :'Z')))
     C                   callp     updHTMLvar('SM3       ':
     C                             %trim(%editc(HEM3       :'3')))
     C                   callp     updHTMLvar('AntKTot   ':
     C                             %trim(%editc(AntKTot   :'Z')))
     C                   callp     updHTMLvar('AntKOpn   ':
     C                             %trim(%editc(AntkOpn    :'Z')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRowEK   begsr
      *
     C                   callp     updHTMLvar('TERM':TERM)
     C                   callp     updHTMLvar('HOLNR':
     C                             %trim(%editc(HOLNR:'Z')))
     C                   callp     updHTMLvar('EKOD':EKOD)
     C                   callp     updHTMLvar('EKEY':EKEY)
      *
     C                   callp     updHTMLvar('HSLNR2':
     C                             %trim(%editc(HSLNR2:'Z')))
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(FSAVD:'Z')))
     C                   callp     updHTMLvar('OPD':
     C                             %trim(%editc(FSOPD:'Z')))
      *
     C                   callp     updHTMLvar('HSSNDN':'')
     C                   eval      HKID  = '00'+FMID
     C                   callp     updHTMLvar('HSNAK':HKID)
     C                   eval      XHSTDK = %TRIM(%EDITC(KLLSEKV:'Z')) + ' av '
     C                             + %TRIM(%EDITC(HENT:'Z'))
     C                   callp     updHTMLvar('HSSTDK':'')
     C                   eval      HKSTIN= FMITER
     C                   callp     updHTMLvar('HSST':HKSTIN)
      *
     C*                  callp     updHTMLvar('Skll      ':'')
     C                   callp     updHTMLvar('Svkt      ':'')
     C                   callp     updHTMLvar('SM3       ':'')
     C                   callp     updHTMLvar('AntKTot   ':'')
     C                   callp     updHTMLvar('AntKOpn   ':'')
      *
     C                   exsr      TstWrk
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
     C*  Select a Recd
      *=====================================================================******
     C     RecordSlt     begsr
     C                   move      'N'           selected          1
     C                   move      'Y'           selected
      *
     C     RecordSltX    tag
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/SYTEIW4.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================******
      *  FYLL
      *=====================================================================******
     C     HSEND         begsr
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     c     HOLNR         setLL     HENOS
     c     HOLNR         ReadE     HENOS                                  35
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHSEND
      *
     c     VisAlleS      ifne      'Y'
     c     HSST          cabne     ' '           NextRec
     c                   end
      *
     C                   exsr      RecordSlt
     C     selected      ifeq      'Y'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   EXSR      HKLL
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHSEND
     C                   endif
      *
     c     NextRec       Tag
     c     HOLNR         ReadE     HENOS                                  35
     c                   end
      *
     C     SLHSEND       TAG
      *
     C                   endsr
      *
      *******************************************************
     c     HKLL          BEGSR
      *******************************************************
      * Vis liste over alle åpne kolli på henteoppdrag
      *
     c                   move      *zeros        KLLSEKV           5 0
     c     HKKey         setLL     HENOK
     c     HKKey         ReadE     HENOK                                  35
      *
     c     *IN35         doweq     '0'
     c                   add       1             KLLSEKV
     c     VisAlleK      ifne      'Y'
     c     HKSTIN        cabne     ' '           NextHKL
     c                   end
     C                   exsr      SetTabrowHK
     C                   callp     wrtsection('row')
     c     NextHKL       Tag
     c     HKKey         ReadE     HENOK                                  35
     c                   end
      *
     c                   ENDSR
      *
      *=====================================================================******
      *  FYLL
      *=====================================================================******
     C     ESEND         begsr
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C                   move      EKOD          FSKODE
     C                   move      EKEY          FSSOK
     c     FriL01Key     SETLL     Frisol01
     c     FriL01Key     READE     Frisol01                               35
      *
     c     *IN35         doweq     '0'
     C     KEYHEA        CHAIN     DISP                               33
     c     VisAlleS      ifne      'Y'
     c     *IN33         andeq     '0'
     c     DISTA1        cabne     'E'           NextRecE
     c                   end
     C     KEYHEA        CHAIN     HEADF                              33
      *
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLESEND
      *
     c     VisAlleS      ifne      'Y'
     c     HSST          cabne     ' '           NextRecE
     c                   end
      *
     C                   exsr      RecordSlt
     C     selected      ifeq      'Y'
     C                   exsr      SetTabRowE
     C                   callp     wrtsection('row')
     C                   EXSR      EKLL
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLESEND
     C                   endif
      *
     c     NextRecE      Tag
     c     FriL01Key     READE     Frisol01                               35
     c                   end
      *
     C     SLESEND       TAG
      *
     C                   endsr
      *******************************************************
     c     EKLL          BEGSR
      *******************************************************
      * Vis liste over alle åpne kolli på henteoppdrag
      *
     c                   move      *zeros        KLLSEKV           5 0
     c     DokufmKey     setll     Dokufm
     c     DokufmKey     reade     Dokufm                                 35
      *
     c     *IN35         doweq     '0'
     c     FMITER        ifeq      ' '
     C                   eval      FMITER= FMSTINN
     c                   end
     c                   add       1             KLLSEKV
     c     VisAlleK      ifne      'Y'
     c     FMITER        cabne     ' '           NextEKL
     c                   end
     C                   exsr      SettabrowEK
     C                   callp     wrtsection('row')
     c     NextEKL       Tag
     c     DokufmKey     reade     Dokufm                                 35
     c                   end
      *
     c                   ENDSR
      *
      *=====================================================================******
      *  Antall Hsendings-kolli  Totalt / Åpne
      *=====================================================================******
     C     AntHkll       begsr
      *
     c                   move      *zeros        AntKTot           5 0
     c                   move      *zeros        AntKOpn           5 0
     c     HKKey         setll     HENOK
     c     HKKey         reade     HENOK                                  35
     c     *in35         doweq     '0'
     c                   add       1             AntKTot
     c     HKSTIN        ifeq      ' '
     c                   add       1             AntKOpn
     c                   end
     c     HKKey         reade     HENOK                                  35
     C                   end
      *
     C                   endsr
      *
      ******************************
     C     AntEKll       Begsr
      ******************************
      *  Antall Esendings-kolli  Totalt / Åpne
     c                   move      *zeros        AntKTot           5 0
     c                   move      *zeros        AntKOpn           5 0
     c     DokufmKey     setll     Dokufm
     c     DokufmKey     reade     Dokufm                                 35
     c     *in35         doweq     '0'
     c                   add       1             AntKTot
     c     FMSTINN       ifeq      ' '
     c     FMSTINN       andeq     ' '
     c                   add       1             AntKOpn
     c                   end
     c     DokufmKey     reade     Dokufm                                 35
     C                   end
      *
     c                   endsr
      *
      *********************************************************
     C     TstWrk        begsr
      *********************************************************
      * Sjekk om de ligger i PDA-slyteworkfile, men kun hvis staus blank
     c     HKSTIN        ifne      ' '
     c                   move      *ON           *in33
     c                   goto      UtTstW
     c                   end
      *
     C                   move      *zeros        WINTEG
     C     WINTEG        SETLL     WRKCGI
     C     WINTEG        Reade     WRKCGI                                 33
     C     *in33         doweq     '0'
     c     WDsCLID       cabeq     HKID          UtTstW
     C     WINTEG        Reade     WRKCGI                                 33
     C                   end
      *
     C     UtTstw        tag
     c     *in33         ifeq      '0'
     C                   callp     updHTMLvar('CSUSER':WDsUser)
     C                   move      WDsTID        CSTID             6 0
     C                   callp     updHTMLvar('Skll':
     C                             %trim(%editw(CSTID:'  :  :  ')))
     C                   else
     C                   callp     updHTMLvar('CSUSER':'')
     C                   callp     updHTMLvar('CSTID ':'')
     C                   callp     updHTMLvar('Skll':'')
     C                   end
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
OddEv *
     C                   OPEN      HENOP
     C                   OPEN      HENOS
     C                   OPEN      HENOK
     C                   OPEN      FRISOL01
     C                   OPEN      HEADF
     C                   OPEN      DISP
     C                   OPEN      CUNDL01
     C                   OPEN      DOKUFM
     C                   OPEN      WRKCGI
      *
     C     HKKey         KLIST
     C                   KFLD                    HSLNR
     C                   KFLD                    HSLNR2
     C     FriL01Key     KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C     KEYHEA        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     KUNKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    HOKNS
     C     DokufmKey     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
      *
     C                   move      'F'           FMRI
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   CLOSE     BRIDF
     C                   CLOSE     HENOP
     C                   CLOSE     HENOS
     C                   CLOSE     HENOK
     C                   CLOSE     FRISOL01
     C                   CLOSE     HEADF
     C                   CLOSE     DISP
     C                   CLOSE     CUNDL01
     C                   CLOSE     DOKUFM
     C                   CLOSE     WRKCGI
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
