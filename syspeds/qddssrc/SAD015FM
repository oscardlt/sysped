     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' 'A' 'I')
     A            AGTANR         8Y 0O  5  5
     A            TAALFA        15A  O  5 14
     A            AGAKD          2A  O  5 30
     A            AGSKV          3A  O  5 36
     A            AGDTF          8Y 0H
     A            WXDTF          8Y 0O  5 42EDTCDE(4)
     A            AGDTT          8Y 0H
     A            WXDTT          8Y 0O  5 51EDTCDE(4)
     A            AGKD           1A  O  5 60
     A            AGPP           1A  O  5 63
     A            AGSATS         5Y 3O  5 67EDTCDE(4)
     A            AGAKTK         1A  H
     A            AGST           6A  O  5 74
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD015'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold avgifter (eksport)'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Velg opsjon:   2=Endre   A=Aktiver-
     A                                         I=Gjør inaktiv'
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2'Op Tariffnr Beskrivelse     AvgKD -
     A                                      Sekv. F.o.m.DT Til Dato EU %/P   Sa-
     A                                      ts status'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA08(08 'SOEK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   Tariffnr Beskrivelse     AvgKD -
     A                                      Sekv. F.o.m.DT Til Dato EU %/P   Sa-
     A                                      ts status'
     A                                      COLOR(WHT)
     A            WSTANR         8Y 0B  2  5DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A N40                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            TAALFA        15A  O  2 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSAKD          2A  O  2 30DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSSKV          3A  B  2 36DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A N40                                  COLOR(YLW)
     A            WSDTF          8Y 0B  2 42EDTCDE(4)
     A  88                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A            WSDTT          8Y 0B  2 51EDTCDE(4)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKD           1A  B  2 60DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'B')
     A            WSPP           1A  B  2 63DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('P' '%')
     A            WSSATS         5Y 3B  2 67DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA08(08 'SOEK')
     A                                  1  2'SYSPED/SAD015'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold avgifter (eksport)'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  4  2'Op Tariffnr Beskrivelse     AvgKD -
     A                                      Sekv. F.o.m.DT Til Dato EU %/P   Sa-
     A                                      ts status'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SOKAVG
     A                                      CA12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk fra tariffnr.  --->'
     A            W2TANR         8Y 0B  2 26TEXT('TARIFFNR.')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
