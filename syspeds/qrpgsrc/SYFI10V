     FDOK36     UF A E           K DISK
     FKODTFT    IF   E           K DISK
     FSYFI10FV  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
      *
      *
     D FBAREA          DS
      * OBS! SKAL HA TIL AKTIVT VINDU      B 370 3710WSCLOC
     D  WSCLOC               382    383B 0
     D                 DS
     D  TX                     1    700
     D                                     DIM(10)
     D  TXT01                  1     70
     D  TXT02                 71    140
     D  TXT03                141    210
     D  TXT04                211    280
     D  TXT05                281    350
     D  TXT06                351    420
     D  TXT07                421    490
     D  TXT08                491    560
     D  TXT09                561    630
     D  TXT10                631    700
     D                 DS
     D  KO                     1     20
     D                                     DIM(10)
     D  KOD01                  1      2
     D  KOD02                  3      4
     D  KOD03                  5      6
     D  KOD04                  7      8
     D  KOD05                  9     10
     D  KOD06                 11     12
     D  KOD07                 13     14
     D  KOD08                 15     16
     D  KOD09                 17     18
     D  KOD10                 19     20
      *
     C     R36KEY        KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WOPD
     C                   KFLD                    WFNR
      *
     C     *ENTRY        PLIST
     C                   PARM                    PAVD
     C                   PARM                    POPD
     C                   PARM                    PFNR
     C                   PARM                    PFLAG             1
      *
     C     *LIKE         DEFINE    D36AVD        PAVD
     C     *LIKE         DEFINE    D36OPD        POPD
     C     *LIKE         DEFINE    D36FNR        PFNR
     C     *LIKE         DEFINE    D36AVD        WAVD
     C     *LIKE         DEFINE    D36OPD        WOPD
     C     *LIKE         DEFINE    D36FNR        WFNR
     C                   Z-ADD     1             X                 3 0
     C                   Z-ADD     0             WX                3 0
     C     PFLAG         IFNE      *BLANKS
     C                   MOVE      *ON           *IN88
     C                   ENDIF
      *
     C                   Z-ADD     PAVD          WAVD
     C                   Z-ADD     POPD          WOPD
     C                   Z-ADD     PFNR          WFNR
      * Sjekker først om det finnes noen verdier å vise
     C     R36KEY        SETLL     DOK36
     C     R36KEY        READE(N)  DOK36                                  90
     C     *IN90         DOWEQ     *OFF
     C                   MOVEA     D36TXT        TX(X)
     C                   ADD       1             X
     C     X             IFGE      11
     C                   LEAVE
     C                   ENDIF
     C     R36KEY        READE(N)  DOK36                                  90
     C                   ENDDO
      * Viser bilde
     C     TAG01         TAG
     C                   EXFMT     BILDE1
     C     *IN12         CABEQ     *ON           SLUTT
     C     *IN88         CABEQ     *ON           SLUTT
      * Søk på fritekstkoder
     C     *IN04         IFEQ      *ON
     C                   EXSR      SOEK
     C                   GOTO      TAG01
     C                   ENDIF
      * Sjekk om det skal hentes noe fritekst
     C                   EXSR      GETTXT
     C     *IN30         CABEQ     *ON           TAG01
      * Enter medfører oppdatering, først sletting og deretter WRITE
     C     R36KEY        SETLL     DOK36
     C     R36KEY        READE     DOK36                                  90
     C     *IN90         DOWEQ     *OFF
     C                   DELETE    DOK36R
     C     R36KEY        READE     DOK36                                  90
     C                   ENDDO
      * Skriver så alle poster til fil.
      * Skriver også blanke linjer, men ikke når det er bare blanke
     C                   Z-ADD     1             X
     C                   Z-ADD     0             WX
     C                   DO        10
     C     TX(X)         IFNE      *BLANKS
     C                   Z-ADD     X             WX
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
     C                   Z-ADD     1             X
     C     WX            IFGT      0
     C                   DO        10
     C                   MOVEA     TX(X)         D36TXT
     C                   Z-ADD     WAVD          D36AVD
     C                   Z-ADD     WOPD          D36OPD
     C                   Z-ADD     WFNR          D36FNR
     C                   WRITE     DOK36R
     C     X             IFGE      WX
     C                   LEAVE
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
     C                   ENDIF
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *****************************************************************
     C     SOEK          BEGSR
      *****************************************************************
     C     WSCLOC        DIV       256           LINNBR            3 0
     C                   MOVEL     *BLANKS       PKODE             2
     C                   CALL      'SYFI10I'
     C                   PARM                    PKODE
     C     LINNBR        IFGT      3
     C     LINNBR        ANDLT     18
     C     LINNBR        SUB       3             X
     C                   MOVEL     PKODE         KO(X)
     C                   ENDIF
      *
     C                   ENDSR
      *****************************************************************
     C     GETTXT        BEGSR
      *****************************************************************
     C                   Z-ADD     0             X                 3 0
     C                   Z-ADD     0             Y                 3 0
     C                   MOVE      *OFF          *IN30
     C                   DO        10
     C                   ADD       1             X
     C     KO(X)         IFNE      *BLANKS
     C                   MOVE      *ON           *IN30
     C     KO(X)         SETLL     KODTFT                                 90
     C     *IN90         IFEQ      *ON                                          <RECORD FINNES
     C                   Z-ADD     X             Y
     C     KO(X)         READE     KODTFT                                 90
     C     *IN90         DOWEQ     *OFF
     C     Y             IFGT      10
     C                   LEAVE
     C                   ENDIF
     C                   MOVEL     KFTFTX        TX(Y)
     C                   ADD       1             Y
     C     KO(X)         READE     KODTFT                                 90
     C                   ENDDO                                                  <*IN90
     C                   ENDIF                                                  <*IN90
     C                   MOVEA     *BLANKS       KO(X)
     C                   ENDIF                                                  <KO,X
     C                   ENDDO                                                  <DO 10
      *
     C                   ENDSR
