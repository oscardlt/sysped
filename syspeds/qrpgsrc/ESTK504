      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK504 MODULE(*LIBL/ESTK504 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FTURER14   UF   E           K DISK    USROPN
     FUNITL04   IF   E           K DISK    USROPN
     FDRIV      UF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D SJA             s              8
     D SJN             s              8  0
     D VERSJ           s             10
     D NYUser          s             10
     D NySja           s              8
     D NySjN           s              8  0
     D Tur             s              8
     D TurNr           s              8  0
     D LinTxt          s            500
     D Hvil            s              4
     D HvilN           s              4  0
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
      * Definere  hex 0D / 25 (CRLF)
     D                 DS
     D CRLF                    1      2
     D Hex0D                   1      1    inz(x'0D')
     D Hex25                   2      2    inz(x'25')
     D                 DS
     D  TimeTmp                1     26
     D  TimeTmpÅ               1      4
     D  TimeTmpM               6      7
     D  TimeTmpD               9     10
     D  TimeTmpTMS            12     18
     D  TimeTmpTT             12     13
     D  TimeTmpTM             15     16
     D  TimeTmpTS             18     19
     D  TS                     1     19
     D                 DS
     D  IdagTmpÅ               1      4
     D  IdagTmpM               5      6
     D  IdagTmpD               7      8
     D  IdagÅMD                1      8
     D  IdagTT                 9     10
     D  IdagTM                11     12
     D  IdagTS                13     14
     D  IdagTTMM               9     12
     D  IdagTMS                9     14
     D  IdagDtTMS              1     14
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
     C                   movel     timedata1     TimeTmp
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user = zhbgetvar('user')
     C                   move      user          SJA
     C                   eval      SJN     = c2n2(SJA)
     C                   eval      VERSJ = zhbgetvar('V')
     C                   eval      TUR  = zhbgetvar('TUR')
     C                   eval      TURNR   = c2n2(TUR)
     C                   eval      NySJA  = zhbgetvar('NYSJA')
     C                   eval      NYSJN   = c2n2(NYSJA)
     C                   move      NYSJN         NYSJA
      * 2 første bokstaver MÅ være samme på NY-user (kan ikke bytte på tvers av firma)
     C                   move      User          Nyuser
     C                   move      NySjn         Nyuser
     C                   eval      Hvil   = zhbgetvar('HV')
     C                   eval      HvilN   = c2n2(Hvil)
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     TURER14
     C                   CLOSE     UNITL04
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESTK504':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
     C                   callp     wrtsection('top')
      * Ugyldig userID
     C     *IN50         IFEQ      '1'
     C                   eval      LinTxt = '#ERROR#Ukjent userID (egen)'
     C                   goto      error
     C                   end
      * Mottar Hviletid - da er det KUN det som skjer
     c     HvilN         ifne      *zeros
     c                   exsr      HvilSR
     C                   goto      UtLes
     c                   end
      *  userID - sjåfør som skal overta
     C     NYUSER        CHAIN     BRIDF                              50
     C     *IN50         IFEQ      '1'
     C                   eval      LinTxt = '#ERROR#Ukjent NY sjåfør '
     C                   goto      error
     C                   end
      *  Turen / har gammel sjåfør på når
     c     TurNr         Chain(N)  TURER14                            33
     C     *IN33         IFEQ      '1'
     C                   eval      LinTxt = '#ERROR#Ugyldig turnr '
     C                   goto      error
     C                   end
     C     TUST          IFEQ      'P'
     C                   eval      LinTxt = '#ERROR#Noen arbeider med turen. '+
     c                             'Kan ikke bytte nå (kontakt kontoret/vent)'
     C                   goto      error
     C                   end
      * Sjårfør på turen ER alt endret på kontoret
     C     SJN           IFne      TUSJA1
     C                   eval      LinTxt = '#ERROR#Du er IKKE registrert ' +
     c                             'som sjåfør på tuen - SLETT den fra PDA.'
     C                   goto      error
     C                   end
      *  sjåfør som Mottar MÅ være knyttet til bil som iggjen tilgører samme trans.
     c                   Eval      UNRETU = 'PDA:'+SJA
     c     UNRETU        setll     UNITL04
     c     UNRETU        reade     UNITL04                                33
     c  N33UNTRAN        comp      TUKNT2                             33
     C     *IN33         IFEQ      '1'
     C                   eval      LinTxt = '#ERROR#NY sjåfør er ikke ' +
     c                             'tilknyttet samme sjåfør som gammel.'
     C                   goto      error
     C                   end
     c
      *
     c     error         tag
     C     LinTxt        IFne      *blanks
     C                   callp     updHTMLvar('LINTXT':LinTxt)
     C                   callp     wrtsection('lin')
     C                   goto      utles
     c                   end
      * finn avd..
     c     TurNr         Chain     TURER14                            33
     C     *IN33         cabeq     '1'           Utles
     c                   move      UNBILN        TUBILN
     c                   move      NYSjN         TUSJA1
     c                   move      ' '           TUTST4
     c                   update    TURERR
      * OBS! her burde endringen logges (i fritekst??)
      *  OG BILNR endres på alle oppdrag..
      *
      * altOK - meld at sjåfør er byttet (=slett fra PDA)
     C                   eval      LinTxt = '#OK#;'+Tur
     C                   callp     updHTMLvar('LINTXT':LinTxt)
     C                   callp     wrtsection('lin')
      * Send section bot
     C     UTLES         TAG
     C                   callp     wrtsection('bot')
     C
     C                   endsr
     C
      *=====================================================================******
      *  Lagre Hviletid i sjåførregisterets fritekstfelt
      *=====================================================================******
     C     HvilSr        begsr
      *  f.eks:   HVILETID TIL: 1445 20070411
     C                   open      DRIV
     C     SJN           chain     DRIV                               33
     C     *in33         ifeq      *off
     c                   move      IdagÅMD       HVILDN            8 0
     c                   move      HvilN         HVIL
      * dersom nåværende  klokkeslett er høyere en oppgitt, så er det for i morgen.
     c     IdagTTMM      ifgt      HVIL
     c                   Move      'ADD'         Ufunksjon
     c                   z-add     1             UDager
     C                   call      'SYGE20'
     C                   PARM                    Ufunksjon         3
     c                   parm                    HVILDN
     c                   parm                    HVILDN
     c                   parm                    UDager            5 0
     c                   end
     c                   move      HVILDN        HVILDA            8
     C                   eval      DRFRIT = 'HVILETID TIL: '+HVILDA+' '+HVIL
     C                   update    DRIVR
     C                   end
     C                   Close     DRIV
     C                   endsr
     C
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr

      * DATO TRENGS I FLERE..
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
      *
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      TURER14
     C                   OPEN      UNITL04
      *
     C     UtInit        endsr
