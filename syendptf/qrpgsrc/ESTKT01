      * OBS! verdiene MinVer / MaxVer (HARDKODET i INIT / sist i sourcen)
      *      viser Laveste og høyeste klient(PDA)versjon som støttes
      *      HVER gang ny versj  distribueres må det endres her.
      *********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTKT01 MODULE(*LIBL/ESTKT01 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************

********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Loggføring i TKIMEI (bruk TK hardwareID om den finnes..)
      *        02.14 - kun på klientsiden - TK comm- handler
10XXX * 02 JOV PTF10 Ikke open files ved uguldig ID.
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 03 JOV PTF12 Ikke kall på LIBL fev deil User
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FTKIMEI    UF A E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D WSuser          s             10
     D passord         s             10
     D Mnr             s              9  0
     D UPDATE          s              1
     D IMEI            s             15
N01  D HWID            s             15
     D VERSJ           s             10
     D MinVer          s             10
     D MaxVer          s             10
     D LinTxt          s            500
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
      * Definere  hex 0D / 25 (CRLF)
     D                 DS
     D CRLF                    1      2
     D Hex0D                   1      1    inz(x'0D')
     D Hex25                   2      2    inz(x'25')
     D                 DS
     D  TimeTmp                1     26
     D  TimeTmpÅ               1      4
     D  TimeTmpM               6      7
     D  TimeTmpD               9     10
     D  TimeTmpTMS            12     18
     D  TimeTmpTT             12     13
     D  TimeTmpTM             15     16
     D  TimeTmpTS             18     19
     D  TS                     1     19
     D                 DS
     D  IdagTmpÅ               1      4
     D  IdagTmpM               5      6
     D  IdagTmpD               7      8
     D  IdagÅMD                1      8
     D  IdagTT                 9     10
     D  IdagTM                11     12
     D  IdagTS                13     14
     D  IdagTMS                9     14
     D  IdagDtTMS              1     14
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
     C                   movel     timedata1     TimeTmp
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      Wsuser = zhbgetvar('user')
     C                   eval      WSUSER = uppify(WSUSER)
     C                   EVAL      PASSORD = zhbgetvar('PASSORD')
     C                   eval      PASSORD  = uppify(PASSORD)
     C                   eval      VERSJ = zhbgetvar('V')
     C                   eval      UPDATE = zhbgetvar('UPD')
     C                   eval      IMEI   = zhbgetvar('IMEI')
N01  C                   eval      HWID   = zhbgetvar('HWID')
N01  c     HWID          ifne      *blanks
N01  C                   eval      IMEI   = HWID
N01  C                   end
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
E02  C  N50              CLOSE     BRPARF
E02  C  N50              CLOSE     TKIMEI
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESTKSTD':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
     C                   callp     wrtsection('top')
      * Ugyldig userID
     C     UPDATE        IFEQ      'Y'
     C                   eval      PASSORD = BIPO
     C                   end
     C     *IN50         IFEQ      '1'
     C     PASSORD       ORNE      BIPO
     C                   eval      LinTxt = '#ERROR# Ukjent userID'
     c                             + ' eller feil passord.'
     C                   callp     updHTMLvar('LINTXT':LinTxt)
     C                   callp     wrtsection('lin')
     C                   goto      utles
     c                   end
      * loggfør bruk..
     C                   movel     timedata1     TimeTmp
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
     C                   movel     'TKterm'      PROD
     C     IMEIkey       chain     TKIMEI                             33
     C                   movel     VERSJ         VERS
     C                   movel     WSUSER        USR
     C                   MOVE      IdagÅMD       DATOS
     C                   MOVE      IdagTMS       TIDS
     C                   add       1             ANT
     C  N33              update    TKIMEIR
     C   33              write     TKIMEIR
      * VED UPDATE - kun DLLer som trengs...
     C     UPDATE        IFEQ      'Y'
     C                   eval      LinTxt = *blanks
     C                   callp     updHTMLvar('LINTXT':LinTxt)
     C                   callp     wrtsection('lin')
     C                   else
     C                   exsr      PDAINIT
     C                   end
     C                   goto      utles
      * Send section bot
     C     UTLES         TAG
     C                   callp     wrtsection('bot')
     C
     C                   endsr
     C
      *=====================================================================******
      *  PDAINIT
      *=====================================================================******
     C     PDAINIT       begsr
      *  (skilt ut i egen SR i endring 13 uten videre merking)
      *  BER OM SERVERENS KLOKKESLETT mm  FOR Å SYNKE PDAEN
     C                   eval      LinTxt = IdagDtTMS + ';' +
     c                             %trim(MinVer) + ';' +
     c                             %trim(MaxVer)
      * Skriv "hovedlinja".
     C                   callp     updHTMLvar('LINTXT':LinTxt)
     C                   callp     wrtsection('lin')
     c     VERSJ         IFge      '01.10'
     C                   eval      LinTxt = 'J,J,J,J,N'
     C                   MOVE      'TKTMN'       PAID
     C     USParKey      CHAIN     BRPARF                             33
     C   33FiParKey      CHAIN     BRPARF                             33
     c  N33              movel     PAVRDA        LINTXT
     C                   callp     updHTMLvar('LINTXT':LinTxt)
     C                   callp     wrtsection('lin')
     c                   end
      *
     C                   endsr
     C
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr

      * DATO TRENGS I FLERE..
     C                   MOVE      TimeTmpÅ      IdagTmpÅ
     C                   MOVE      TimeTmpM      IdagTmpM
     C                   MOVE      TimeTmpD      IdagTmpD
     C                   MOVE      TimeTmpTT     IdagTT
     C                   MOVE      TimeTmpTM     IdagTM
     C                   MOVE      TimeTmpTS     IdagTS
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
E03  C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
e02  C  N50              OPEN      BRPARF
e02  C  N50              OPEN      TKIMEI
      *
     C     IMEIkey       KLIST
     C                   KFLD                    IMEI
     C                   KFLD                    PROD
     C     FiParKey      klist
     C                   kfld                    FIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   movel     '*KUNDFT*'    FIBRID           10
     C                   move      BIFIRM        FIBRID
     C     USParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      *
     c                   eval      MinVer = '01.01'
     c                   eval      MaxVer = '01.20'
      *
     C     UtInit        endsr
