********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10311 * 01 YBC PTF10 DOKUMENTKODE ER ENDRET FRA 3A TIL 4A
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED DOKUMENTER                 ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-83 95-97
      ***********************************************************
      *
     FSVXVDK    UF A E           K DISK
     FSVXKODL01 IF   E           K DISK
     FSVTKD     IF   E           K DISK
     FSVX020D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSSUBN               378    379B 0
     D  WSCLOC               382    383B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UDTY          IFNE      *BLANKS
     C                   MOVE      UDTY          TVDTY
     C                   MOVE      UDREF         TVDREF
     C                   MOVE      UDSK          TVDSK
     C                   MOVE      UDO           TVDO
     C                   MOVE      UDOSK         TVDOSK
     C                   MOVE      UTLO          TVTLO
     C                   MOVE      UEXKD         TVEXKD
     C                   MOVE      UEXLK         TVEXLK
     C                   MOVE      UMTXT         TVMTXT
     C                   MOVE      UMSK          TVMSK
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SVVDKK        SETLL     SVXVDK
     C     SVVDKK        READE(N)  SVXVDK                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SVVDKK        READE(N)  SVXVDK                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C  N30              Z-ADD     4             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C   06              DO
     C                   EXSR      CRTDK
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     SVVDKK        SETLL     SVXVDK
     C     SVVDKK        READE     SVXVDK                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SVXVDKR
     C     SVVDKK        READE     SVXVDK                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UDTY
     C                   MOVE      *BLANKS       UDREF
     C                   MOVE      *BLANKS       UDSK
     C                   MOVE      *BLANKS       UDO
     C                   MOVE      *BLANKS       UDOSK
     C                   MOVE      *BLANKS       UTLO
     C                   MOVE      *ZEROS        UEXKD
     C                   MOVE      *BLANKS       UEXLK
     C                   MOVE      *BLANKS       UMTXT
     C                   MOVE      *BLANKS       UMSK
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     TVDREF        IFNE      '*DELETE'
     C                   MOVE      TVDTY         UDTY
     C                   MOVE      TVDREF        UDREF
     C                   MOVE      TVDSK         UDSK
     C                   MOVE      TVDO          UDO
     C                   MOVE      TVDOSK        UDOSK
     C                   MOVE      TVTLO         UTLO
     C                   MOVE      TVEXKD        UEXKD
     C                   MOVE      TVEXLK        UEXLK
     C                   MOVE      TVMTXT        UMTXT
     C                   MOVE      TVMSK         UMSK
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     TVDREF        IFNE      '*DELETE'
     C     UDTY          IFEQ      *BLANKS
     C                   MOVE      TVDTY         UDTY
     C                   MOVE      TVDREF        UDREF
     C                   MOVE      TVDSK         UDSK
     C                   MOVE      TVDO          UDO
     C                   MOVE      TVDOSK        UDOSK
     C                   MOVE      TVTLO         UTLO
     C                   MOVE      TVEXKD        UEXKD
     C                   MOVE      TVEXLK        UEXLK
     C                   MOVE      TVMTXT        UMTXT
     C                   MOVE      TVMSK         UMSK
     C                   ELSE
     C                   WRITE     SVXVDKR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N27
     CANN98WSNR          IFEQ      '2'
     C     TVDREF        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGDK
     C                   END
     C  N27
     CANN98WSNR          IFEQ      '4'
     C     TVDREF        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTDK
     C                   END
     C  N98WSNR          IFEQ      '5'
     C     TVDREF        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DSPDK
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C                   MOVE      '013'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSDTY         YKODE
     C     SVK01K        CHAIN     SVXKODL01                          90
     C     *IN90         IFEQ      '1'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      'SVX0813'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
      *
     C     WSDREF        COMP      *BLANKS                                89
     C     *IN89         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   MOVE      'SVX0814'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
      *
     C     WSDSK         IFNE      *BLANKS
     C                   MOVE      '012'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSDSK         YKODE
     C     SVK01K        CHAIN     SVXKODL01                          88
     C     *IN88         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     30            POSNBR
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C     WSDOSK        IFNE      *BLANKS
     C                   MOVE      '012'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSDOSK        YKODE
     C     SVK01K        CHAIN     SVXKODL01                          87
     C     *IN87         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     60            POSNBR
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C     WSTLO         IFNE      *BLANKS
     C                   MOVE      '039'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSTLO         YKODE
     C     SVK01K        CHAIN     SVXKODL01                          86
     C     *IN86         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     63            POSNBR
     C                   MOVE      'SVX0815'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
     C  N86WSTLO         IFNE      'DG0'
     C     WSTLO         ANDNE     'DG1'
     C                   MOVE      *ZEROS        WSEXKD
     C                   MOVE      *BLANKS       WSEXLK
     C                   END
      *
     C     WSTLO         IFNE      *BLANKS
     C     WSEXLK        IFNE      *BLANKS
     C     WSEXKD        OREQ      0
     C                   EVAL      SVKD_KD = WSEXLK
     C     SVTKDK        CHAIN     SVTKD                              85
     C     *IN85         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     69            POSNBR
     C                   MOVE      'SPL0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
     C                   END
      *
     C     WSMSK         IFNE      *BLANKS
     C                   MOVE      '012'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSMSK         YKODE
     C     SVK01K        CHAIN     SVXKODL01                          84
     C     *IN84         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     60            POSNBR
     C                   MOVE      'SVX0810'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWDK        BEGSR
     C***********************************************
     C                   MOVE      WSDTY         TVDTY
     C                   MOVE      WSDREF        TVDREF
     C                   MOVE      WSDSK         TVDSK
     C                   MOVE      WSDO          TVDO
     C                   MOVE      WSDOSK        TVDOSK
     C                   MOVE      WSTLO         TVTLO
     C                   MOVE      WSEXKD        TVEXKD
     C                   MOVE      WSEXLK        TVEXLK
     C                   MOVE      WSMTXT        TVMTXT
     C                   MOVE      WSMSK         TVMSK
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTDK         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSDTY
     C                   MOVE      *BLANKS       WSDREF
     C                   MOVE      *BLANKS       WSDSK
     C                   MOVE      *BLANKS       WSDO
     C                   MOVE      *BLANKS       WSDOSK
     C                   MOVE      *BLANKS       WSTLO
     C                   MOVE      *ZEROS        WSEXKD
     C                   MOVE      *BLANKS       WSEXLK
     C                   MOVE      *BLANKS       WSMTXT
     C                   MOVE      *BLANKS       WSMSK
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCRT         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCRT
     C                   END
     C   12              GOTO      SLCRT
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCRT
      *
     C                   EXSR      MOVWDK
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGDK         BEGSR
     C***********************************************
     C                   MOVE      TVDTY         WSDTY
     C                   MOVE      TVDREF        WSDREF
     C                   MOVE      TVDSK         WSDSK
     C                   MOVE      TVDO          WSDO
     C                   MOVE      TVDOSK        WSDOSK
     C                   MOVE      TVTLO         WSTLO
     C                   MOVE      TVEXKD        WSEXKD
     C                   MOVE      TVEXLK        WSEXLK
     C                   MOVE      TVMTXT        WSMTXT
     C                   MOVE      TVMSK         WSMSK
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCHG
     C                   EXSR      MOVWDK
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTDK         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       TVDTY
     C                   MOVE      *BLANKS       TVDREF
     C                   MOVEL     '*DELETE'     TVDREF
     C                   MOVE      *BLANKS       TVDSK
     C                   MOVE      *BLANKS       TVDO
     C                   MOVE      *BLANKS       TVDOSK
     C                   MOVE      *BLANKS       TVTLO
     C                   MOVE      *ZEROS        TVEXKD
     C                   MOVE      *BLANKS       TVEXLK
     C                   MOVE      *BLANKS       TVMTXT
     C                   MOVE      *BLANKS       TVMSK
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPDK         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   EXFMT     VAREL3
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     7
     C                   MOVE      '013'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSDTY         UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         WSDTY
     C                   Z-ADD     9             POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     30
     C     POSNBR        ANDLE     31
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSDSK         UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         WSDSK
     C                   Z-ADD     33            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     60
     C     POSNBR        ANDLE     61
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSDOSK        UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         WSDOSK
     C                   Z-ADD     63            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     63
     C     POSNBR        ANDLE     65
     C                   MOVE      '039'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSTLO         UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         WSTLO
     C                   Z-ADD     67            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     69
     C     POSNBR        ANDLE     70
     C                   CALL      'SVT003R'
     C                   PARM                    SVKD_TYP
     C                   PARM                    SVKD_IE
     C                   PARM                    WDT
     C                   PARM                    UKD              10
     C                   IF        UKD <> *BLANKS
     C                   EVAL      WSEXLK = UKD
     C                   END
     C                   Z-ADD     11            POSNBR
     C                   Z-ADD     11            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     60
     C     POSNBR        ANDLE     61
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSMSK         UKODE
     C                   CALL      'SVX002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   MOVEL     UKODE         WSMSK
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SVVDKK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    ULI
     C     SVK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
     C     SVTKDK        KLIST
     C                   KFLD                    SVKD_IE
     C                   KFLD                    SVKD_TYP
     C                   KFLD                    SVKD_KD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
S01  C*                  PARM                    UDTY              3
N01  C                   PARM                    UDTY              4
     C                   PARM                    UDREF            20
     C                   PARM                    UDSK              2
     C                   PARM                    UDO              26
     C                   PARM                    UDOSK             2
     C                   PARM                    UTLO              5
     C                   PARM                    UEXKD             1 0
     C                   PARM                    UEXLK             2
     C                   PARM                    UMTXT            48
     C                   PARM                    UMSK              2
     C     *IN27         PARM      *IN27         UIN27             1
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      UAVD          TVAVD
     C                   MOVE      UTDN          TVTDN
     C                   MOVE      ULI           TVLI
     C                   MOVE      'GCY'         SVKD_TYP
     C                   MOVE      'A'           SVKD_IE
     C                   MOVE      'N'           USPED             1
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   ENDSR
      *
