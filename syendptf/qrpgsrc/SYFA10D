      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE Farlig gods på en varelinje  ***
      **************************************************************
*****F**************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnrF*Sek Sig Vers Beskrivelse...........................
"""""F*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     F* 01 JOV PTF11 sjekk mot samlasting andre fareklasser
10XXX * 02 JOV PTF11 utvidede tester Fralig gods   + G --> GR  i enhet
10XXX * 03 JOV PTF11 Et oppdrag kan være unntatt for ADRsjekk
10XXX * 04 JOV PTF11 benytt INDEX i ADRUNNRF derso fler like UNNR/EMBgrp
      * 05 JOV PTF11 Sikre mot for høy(teknisk) ADR-poeng..
      * %%%%%%%%%%
12038 * 06 JOV PTF12 rekomp - DOKUFVF endr i skjermbilde
*****F**************************************************************
      * BRINGTMS-prosjekt.
     FADRUNNRF  IF   E           K DISK
     FDOKUFV    IF   E           K DISK
     FDOKUFVF   UF A E           K DISK
N03  FHEADF     IF   E           K DISK
     FSYFA10DD  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
N05  D BigNum          S             19S 5
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   move      *zeros        SumPoeng
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LinKey        SETLL     DOKUFVF
     C     LinKey        READE(N)  DOKUFVF                                94
     C     *in94         DOWEQ     '0'
     C     UnNrKeyF      chain     ADRUNNRF                           33
     C   33              eval      UNBESK = *blanks
     C  N33              eval      UNBESK = ADTEXT
S05  c*                  add       FFPOEN        SUMPOENG
N05  C                   EVAL      BigNum = SUMPOENG + FFPOEN
N05  C                   IF        BigNum < 99999
N05  C                   EVAL      SUMPOENG=BigNum
N05  C                   else
N05  C                   EVAL      SUMPOENG=99999
N05  C                   endif
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     LinKey        READE(N)  DOKUFVF                                94
     C  N94              ENDDO
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   clear                   VARELI
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      START5
     C                   END
     C*
     C     ZRECNO        IFNE      0
S03  C*                  EXSR      HENT
N03  C                   EXSR      HENTSR
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   GOTO      START3
     C                   END
     c                   goto      start                                        alltid refreh
     c                   endif
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       888786
     c     *in04         ifeq      '1'
     c                   CALL      'SYFA10D2'
     c                   parm                    WSUNNR
     c                   parm                    WSEMBG
N04  c                   parm                    WSINDX
     c                   if        WSUNNR <> *blanks
     C                   Z-ADD     21            POSNBR
     c                   endif
     c                   goto      START5
     c                   endif
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C* Ok - refresh
     C                   GOTO      START                                        alltid refresh
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C* UNNR
     C     UnNrKeyW      chain     ADRUNNRF                           90
     c     *in90         ifeq      '1'
     c     WSUNNR        chain     ADRUNNRF                           90
     c     *in90         ifeq      '1'
     C                   eval      UNBESK = *blanks
     C                   MOVE      'JOV1653'     MSGNR
     C                   seton                                        30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      SLTB
     C                   endif
      * kommer hit kun når gitt UNNR+EMBG IKKE finnes men det finnes minst en på gitt UNNR
      * en og bare en=velg denne....
     c     WSUNNR        reade     ADRUNNRF                               33
     c     *in33         ifeq      *on
     C                   eval      WSEMBG = ADEMBG
N04  C                   eval      WSINDX = ADINDX
     c                   else
      * det finnes flere - må taste/velge
     C                   MOVE      'JOV1654'     MSGNR
     C                   seton                                        3090
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLTB
     c                   endif
     C                   endif
      * UNNR/Emb.grp  OK - HENT INFO
     C                   eval      UNBESK = ADTEXT
     C                   eval      WSKLAS = ADKLAS
     C                   eval      WSSEDD = ADSEDD
     C                   eval      WSTRES = ADTRES
     C                   eval      WSFAKT = ADFAKT

     C*
      * Test kll
     C     WSANTK        ifeq      *zeros
     C                   MOVE      'JOV1655'     MSGNR
     C                   seton                                        3089
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     21            POSNBR
     C                   GOTO      SLTB
     c                   endif
      * Test kll
     C     WSANTE        ifeq      *zeros
     C                   MOVE      'JOV1656'     MSGNR
     C                   seton                                        3088
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   GOTO      SLTB
     c                   endif
      * Test kll
     C     WSENH         ifeq      'L  '
     C                   eval      WSENH = 'LTR'
     C                   endif
      * Test kll
     C     WSENH         ifeq      'K  '
     C                   eval      WSENH = 'KG '
     C                   endif
N02   * FIX G->GR
N02  C     WSENH         ifeq      'G  '
N02  C                   eval      WSENH = 'GR '
N02  C                   endif
     C     WSENH         ifne      'LTR'
     C     WSENH         andne     'KG '
N10  C     WSENH         andne     'GR '
     C                   MOVE      'JOV1657'     MSGNR
     C                   seton                                        3087
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     35            POSNBR
     C                   GOTO      SLTB
     c                   endif
N01   * Sjekk samlasting mot andre klasser
N03  C     HESTN8        IFNE      'X'
N01  c                   call      'SYGE84L'
N01  c                   parm                    Uavd              4 0
N01  c                   parm                    UOpd              7 0
N01  c                   parm      ADKLAS        UNyKlasse         4
N02  c                   parm      ADSEDD        UNySeddel        10
N01  c                   parm                    ufarSam           1
N01  C     UFARSAM       IFEQ      '1'
N01  C                   MOVE      'JOV1673'     MSGNR
N01  C                   SETON                                        30
N01  C                   Z-ADD     22            LINNBR
N01  C                   Z-ADD     8             POSNBR
N01  C                   GOTO      SLTB
N01  C                   endif
N02  C     UFARSAM       IFEQ      '2'
N02  C                   MOVE      'JOV1676'     MSGNR
N02  C                   SETON                                        30
N02  C                   Z-ADD     22            LINNBR
N02  C                   Z-ADD     8             POSNBR
N02  C                   GOTO      SLTB
N02  C                   endif
N02  C     UFARSAM       IFEQ      '3'
N02  C                   MOVE      'JOV1677'     MSGNR
N02  C                   SETON                                        30
N02  C                   Z-ADD     22            LINNBR
N02  C                   Z-ADD     8             POSNBR
N02  C                   GOTO      SLTB
N02  C                   endif
N03  C                   endif
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     c     *IN40         ifeq      *off
     C                   MOVE      *ZEROS        FFLIN2
     C     LinKey2Max    SETGT     DOKUFVF
     C     LinKey        READPE(N) DOKUFVF                                33
     C     FFLIN2        ADD       1             WSLIN2
     c                   endif
     C     LinKey2       CHAIN     DOKUFVF                            33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    DOKUFVFR
     C   33              WRITE     DOKUFVFR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
S03  C*    HENT          BEGSR
N03  C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXSR      MOVSIW
     C     LinKey2       CHAIN(N)  DOKUFVF                            33
     C                   EXFMT     VAREL3
     C     *IN11         IFEQ      '1'
     C     LinKey2       CHAIN     DOKUFVF                            33
     C  N33              DELETE    DOKUFVFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     c     *in04         ifeq      '1'
     c                   CALL      'SYFA10D2'
     c                   parm                    WSUNNR
     c                   parm                    WSEMBG
N04  c                   parm                    WSINDX
     c                   if        WSUNNR <> *blanks
     C                   Z-ADD     21            POSNBR
     c                   endif
     c                   goto      STCHG
     c                   endif
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
      * BLANK (I SUBFIL) VED DELETE
     C                   MOVE      *BLANKS       FFUNNR
     C                   MOVE      *BLANKS       FFKLAS
     C                   MOVEL     '*DELETE*  '  FFSEDD
     C                   MOVE      *BLANKS       FFEMBG
     C                   MOVE      *BLANKS       FFTRES
     C                   MOVE      *ZEROS        FFFAKT
     C                   MOVE      *ZEROS        FFANTK
     C                   MOVE      *ZEROS        FFANTE
     C                   MOVE      *BLANKS       FFENH
     C                   MOVE      *ZEROS        FFPOEN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
      * BLANK NÅR EN GÅR UT AV VARELINJEVEDLIKEHOLD
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSLIN2
     C                   MOVE      *BLANKS       WSUNNR
     C                   MOVE      *BLANKS       WSKLAS
     C                   MOVE      *BLANKS       WSSEDD
     C                   MOVE      *BLANKS       WSEMBG
N04  C                   MOVE      *BLANKS       WSINDX
     C                   MOVE      *BLANKS       WSTRES
     C                   MOVE      *ZEROS        WSFAKT
     C                   MOVE      *ZEROS        WSANTK
     C                   MOVE      *ZEROS        WSANTE
     C                   MOVE      *BLANKS       WSENH
     C                   MOVE      *ZEROS        WSPOEN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
      * HENTE DATA FOR ENDRING AV LINJE..
     C                   MOVE      FFLIN2        WSLIN2
     C                   MOVE      FFUNNR        WSUNNR
     C                   MOVE      FFUNNR        WSUNNR
     C                   MOVE      FFANTK        WSANTK
     C                   MOVE      FFANTE        WSANTE
     C                   MOVE      FFENH         WSENH
     C                   MOVE      FFKLAS        WSKLAS
     C                   MOVE      FFSEDD        WSSEDD
     C                   MOVE      FFEMBG        WSEMBG
N04  C                   MOVE      FFINDX        WSINDX
     C                   MOVE      FFTRES        WSTRES
     C                   MOVE      FFFAKT        WSFAKT
     C                   MOVE      FFPOEN        WSPOEN
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     21            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
      * FLYTTE DATA ETTER ENDRING AV LINJE..
     C   33              DO
     C                   MOVE      UAVD          FFAVD
     C                   MOVE      UOPD          FFOPD
     C                   MOVE      UFBNR         FFFBNR
     C                   MOVE      ULINR         FFLINR
     C                   MOVE      WSLIN2        FFLIN2
     C                   END
     C*
     C                   MOVE      WSUNNR        FFUNNR
     C                   MOVE      WSUNNR        FFUNNR
     C                   MOVE      WSANTK        FFANTK
     C                   MOVE      WSANTE        FFANTE
     C                   MOVE      WSENH         FFENH
     C                   MOVE      WSKLAS        FFKLAS
     C                   MOVE      WSSEDD        FFSEDD
     C                   MOVE      WSEMBG        FFEMBG
N04  C                   MOVE      WSINDX        FFINDX
     C                   MOVE      WSTRES        FFTRES
     C                   MOVE      WSFAKT        FFFAKT
S05  c*                  eval      FFPOEN = FFANTE * FFFAKT
N05  C                   EVAL      BigNum = FFANTE * FFFAKT
N05  C                   IF        BigNum < 99999
N05  C                   EVAL      FFPOEN=BigNum
N05  C                   else
N05  C                   EVAL      FFPOEN=99999
N05  C                   endif
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFBNR             3 0
     C                   PARM                    ULINR             2 0
     C                   PARM                    UFLAGG            1
     C*
N03  C     HEAKEY        KLIST
N03  C                   KFLD                    UAVD
N03  C                   KFLD                    UOPD
N03  C     HEAKEY        CHAIN     HEADF
     C     UnNrKeyF      KLIST
     C                   KFLD                    FFUNNR
     C                   KFLD                    FFEMBG
N04  C                   KFLD                    FFINDX
     C     UnNrKeyW      KLIST
     C                   KFLD                    WSUNNR
     C                   KFLD                    WSEMBG
N04  C                   KFLD                    WSINDX
     C     LinKey        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C     LinKey2Max    KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C                   KFLD                    maxSub
     C                   z-add     99            maxSub            2 0
     C     LinKey2       KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C                   KFLD                    WSLIN2
     C*
     C     DokufvKey     KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C                   move      'F'           FVRI
     c     DokufvKey     chain     DOKUFV                             33
     c     *in33         ifeq      '0'
     c                   eval      FVBESKR = %trim(%editc(FVANT:'4'))+' '
     c                             + %trim(FVPAKN)+' '+%trim(FVVT)+'    '
     c                             + %trim(%editc(FVVKT:'4'))+' Kg'
     c                   endif
     C*
     C                   WRITE     WINDOWSZ
     C*
     C                   ENDSR
