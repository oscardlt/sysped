     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTLK)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      CF03(03 'UT')
     A                                      CF04(04 'SØK')
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  7  3VALUES(' ' '1' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            FZDELS         1   O  7  7
     A            FZTYPE         2   O  7 10
     A            KLKNVN        30   O  7 13
     A            FZKODE         5   O  7 44
     A            ST2NVN        30   O  7 50
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYEF04'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0927 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  2 19MSGCON(006 DIV0018 *LIBL/SYMSGF)
     A            WSKUND         8S 0O  2 27
     A            KNAVN         30A  O  2 37
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(060 SHU0935 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(077 SHU0931 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSNR2          1   B  6  3
     A            WSDELS         1   B  6  7
     A  71                                  DSPATR(RI)
     A            WSTYPE         2   B  6 10
     A  73                                  DSPATR(RI)
     A            WSKODE         5   B  6 44
     A  72                                  DSPATR(RI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2'INVALID OPTION'
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2004  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                 23 42MSGCON(018 SHU0934 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYEF04'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(077 SHU0931 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(030 SHU0935 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            WSNR2          1A  B  6  3
     A            WSDELS         1A  B  6  7
     A  71                                  DSPATR(RI)
     A            WSTYPE         2A  B  6 10
     A  73                                  DSPATR(RI)
     A            WSKODE         5A  B  6 44
     A  72                                  DSPATR(RI)
     A                                  1 19MSGCON(040 SHU0927 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 19MSGCON(006 DIV0018 *LIBL/SYMSGF)
     A            WSKUND         8S 0O  2 27
     A  74                                  DSPATR(RI)
     A            KNAVN         30A  O  2 37
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2004  '
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                 23 40MSGCON(018 SHU0934 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE01
     A                                      CF12(12 'Gå til forrige bilde')
     A                                      CF23(23 'OVER TIL VEDLIKEHOLD')
     A                                  1  2'SYSPED/SYEF04  '
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(032 SHU0927 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            WSTYPE         2A  O  2  1
     A            WSTYPT        15A  O  2  5
     A            WSKODE         5A  O  2 22
     A            WSKODT        15A  O  2 29
     A                                  2 52MSGCON(010 SHU0432 *LIBL/SYMSGF)
     A            WSDELS         1A  O  2 63COLOR(RED)
     A            ALLE           4A  O  2 65COLOR(RED)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A            WST1          79A  B  3  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST2          79A  B  4  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST3          79A  B  5  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST4          79A  B  6  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST5          79A  B  7  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST6          79A  B  8  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST7          79A  B  9  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST8          79A  B 10  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST9          79A  B 11  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST10         79A  B 12  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST11         79A  B 13  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST12         79A  B 14  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST13         79A  B 15  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST14         79A  B 16  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST15         79A  B 17  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST16         79A  B 18  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST17         79A  B 19  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST18         79A  B 20  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST19         79A  B 21  1DSPATR(UL)
     A                                      CHECK(LC)
     A            WST20         79A  B 22  1DSPATR(UL)
     A                                      CHECK(LC)
     A                                 23  1MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSKUND         8Y 0B  2  2EDTCDE(4)
     A  74                                  DSPATR(RI)
     A            WSDELS         1A  B  2 14
     A  71                                  DSPATR(RI)
     A            WSTYPE         2A  B  2 25
     A  73                                  DSPATR(RI)
     A            WSKODE         5A  B  2 31
     A  72                                  DSPATR(RI)
     A                                  1  2MSGCON(070 SHU0932 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A          R VAREL4
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSKUND         8Y 0B  2  2EDTCDE(4)
     A  74                                  DSPATR(RI)
     A            KNAVN         30A  O  2 11
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                  1  2MSGCON(075 SHU0933 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL5
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            FZKUND         8Y 0B  2  2EDTCDE(4)
     A            FZDELS         1A  B  2 14
     A            FZTYPE         2A  B  2 25
     A            FZKODE         5A  B  2 31
     A                                  1  2MSGCON(070 SHU0936 *LIBL/SYMSGF)
     A                                      COLOR(RED)
